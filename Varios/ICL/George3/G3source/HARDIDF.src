  16 22FL    #SEG  HARDIDF8                     [G CONSTANTINIDES
  13 22PD ...# COPYRIGHT INTERNATIONAL COMPUTERS 1982
   8 22_=    #OPT  K0HARDIDF=0   
  16 23DW    #LIS  K0HARDIDF>K0ALLGEO>K0GREATGEO>K0COMMAND>K0IDF 
  10 23YG                   8HHARDIDF8   
   7 24D6    # ENTRY POINTS  
  12 24XQ          SEGENTRY K1HARDIDF,Z1HARDIDF  
  11 25CB          MENDAREA 50,K100HARDIDF   
   7 25X2    [ERROR MESSAGES 
   8 26BL    XMOPON         +JRES
  16 26W=    XILLUN         +ILLUN              [ILLEGAL UNIT NO 
  16 27*W    XILLINE        +ILLINE             [ILLEGAL LINE NO 
  20 27TG    XELEVEN        +ELEVEN             [ELEVEN 7020 DEVICES ON THIS LINE
  14 28*6    XIDFUN         +IDFUN              [NO UNITS
   9 28SQ    XADDPOLL       +ADDPOLL 
   9 29#B    XILLDEV        +ILLDEV  
  18 29S2    XCONPROAT   +CONPROAT              [CONSOLE PROP ATTRIBUTED 
  20 2=?L    XCONCLUS    +CONCLUS               [THIS DEVICE IS CLUSTER CONSOLE  
  20 2=R=    XIDENT         +IDEXISTS           [7020 IDENTIFIER ALREADY EXISTS  
   7 2=YD ...#UNS  ANSTOOMANY
  19 2?5L ...XTOOMANY          +JMAXPAR           [TOOMANY PARAMETERS IE >24 
   8 2#PQ    TABLE BRN      NULL 
   8 2*9B          BRN      XMOP 
   8 2*P2          BRN      XBULK
   8 2B8L          BRN      SENT 
  10 2BN=    SCMID HUNTMISB 1,IDF,ITERMID
   8 2C7W          BNG   1  (5)  
   7 2CMG          EXIT  5  1
  19 2J46    XMSEARCH           [X2=ADD OF RECORD TO START SEARCH:X3=CELL PTR
   8 2JHQ          SBX   6  FX1  
   8 2K3B          MSEARCH  2,3,4
   8 2KH2          ADX   6  FX1  
   7 2L2L          EXIT  6  0
   5 2M_6    RECFIND 
  10 2NDQ          HUNT2    2,FILE,FRRB  
  10 2NYB          TXU   5  BRECNO+A1(2) 
   9 2PD2          BCS      RECFIND  
   8 2PXL          ADN   2  A1   
   7 2QC=          EXIT  6  0
   8 2Y=Q    YREC  BPZ   3  SAME 
   8 2YQB    YREC1 SBX   5  FX1  
   7 2_=2          LDX   0  3
   7 2_PL          SRL   0  9
   8 329=          ANDN  0  #7777
   8 32NW          LDX   2  FX2  
  10 338G          STO   0  ACOMMUNE3(2) 
   7 33N6          MXB      2
   8 347Q          ADX   5  FX1  
   8 34MB    SAME  ANDN  3  #777 
   7 3572          EXIT  5  0
  17 35LL    MFREEC                             [ROUTINE TO FREE CELL
   8 366=          SBX   5  FX1  
   8 36KW          MFREECELL 2,3 
   8 375G          ADX   5  FX1  
   7 37K6          EXIT  5  0
  21 384Q    THROW SBX   6  FX1                 [THIS ROUTINE DISCARDS ASSOC & ATTRIB
   7 38JB          SMO      2
   9 3942          LDX   5  BATT(3)  
  17 39HL          BZE   5  TH4                 [J.IF NO ATTRIBUTIONS
  21 3=3=          LDX   3  5                   [OTHERWISE GO TO ATTRIBUTION CELL-   
  21 3=GW          CALL  5  YREC                [& SEE IF CONSOLE PROPERTY ATTRIBUT. 
   7 3?2G          SMO      2
   9 3?G6          LDX   0  BTYPE(3) 
  21 3?_Q          BNZ   0  PROAT               [ERROR:THIS DEVICE HAS CONS.PROP.ATT.
  21 3#FB          LDX   3  4                   [BACK TO ORIGINATING CELL-X4 PRE-SET 
   8 3#_2          CALL  5  YREC1
  18 3*DL          ORX   6  GSIGN               [X6 NEG USED AS SWITCH   
  16 3*Y=          LDN   3  BATT(3)             [TO FREE ATT.CELL
  15 3BCW    TH1   CALL  5  MFREEC              [FREE CELL   
  17 3BXG          BPZ   6  TH3                 [X6 POS IF ALL DONE  
   9 3CC6          ANDX  6  BITS22LS 
  17 3CWQ          SBN   3  BATT-BASS           [TO FREE ASSOC. CELL 
   7 3DBB    TH2   SMO      2
   8 3DW2          LDX   5  0(3) 
  17 3F*L          BNZ   5  TH1                 [J.TO FREE ASSOC.CELL
   8 3FT=    TH3   ADX   6  FX1  
   7 3G#W          EXIT  6  0
  20 3GSG    TH4   LDN   3  BASS(3)             [NO ATT'S,BUT WHAT ABOUT ASSOCS  
   8 3H#6          BRN      TH2  
   8 3HRQ    TERAD       #7420   
   8 3J?B                #7430   
   8 3JR2                #7400   
   8 3K=L                #7410   
   8 3KQ=                #7440   
   8 3L9W                #7450   
   8 3LPG                #7460   
   8 3M96                #7470   
   8 3MNQ                #7540   
   8 3N8B                #7550   
   8 3NN2                #7560   
   8 3P7L                #7570   
   4 3P8M ...[   
   8 3P?Q ...ZBRKINI     COMBRKIN
   4 3P#R ...[   
   4 3P*S ...[   
   6 3RKQ    Z1HARDIDF   
  10 3RS2 ...      IDFOPEN     ZBRKINI   
  10 3SK2          MHUNTW   1,IDF,CONDATA
   7 3T4L    #SKI JSKI17<1$1 
  18 3TJ=          LDX   4  A1(1)               [UNIT NO FOR HW COMMAND  
  21 3W3W          LDN   7  0                   [POINTER TO CURRENT WD IN CONDATA BLK
   9 3WHG          LDX   3  BUNIT(2) 
   6 3X36    #SKI JSKI17 
   8 3XGQ          LDXC  4  A1(1)
  16 3Y2B          BZE   3  YIDFUN              [ERROR-NO UNITS  
   8 3YG2          CALL  5  SCMID
   8 3Y_L          BRN      TMDOK
  10 3_F=          LDX   6  ALOGLEN(1)   
   8 3_YW          BZE   6  TMDOK
  16 42DG          TRACE    6,YZLOGLN               [YZQZTRACE   
   9 42Y6          LDX   3  BRECNO(2)
  11 43CQ          SETUPCORE 6,2,IDF,ITERMID 
   8 43XB          CALL  5  SCMID
   9 44C2          BRN      (GEOERR) 
   8 44WL          ADN   2  A1   
   8 45B=          ADN   1  A1   
   7 45TW          SMO      6
   7 46*G          MOVE  1  0
   8 46T6          LDX   2  FX2  
   7 47#Q          LDX   5  3
   9 47SB          CALL  6  RECFIND  
   5 48#2    TMDOK   
  20 48RL          LDN   3  BUNIT               [CELL PTR TO BASE OF UNIT CHAIN  
  21 49?=          CALL  6  XMSEARCH            [SEARCH FOR UNIT CELL:UNIT NO IN X4  
   8 49QW          SMO      FX2  
   9 4==G          LDX   6  ACOMMUNE1
  17 4=Q6          BZE   6  YILLUN              [ERROR-ILLEGAL UNIT  
   7 4?9Q      [UNIT EXISTS  
   9 4?PB          LDX   6  BRECNO(2)
   8 4#92          SMO      FX2  
  19 4#NL          STO   6  AWORK1              [STORE REC NO FOR UNIT CELL  
   8 4*8=          SMO      FX2  
  19 4*MW          STO   3  AWORK2              [STORE CELL PTR FOR UNIT CELL
   5 4QB6    NEXTLINE
  10 4QTQ          MHUNTW   1,IDF,CONDATA
  15 4R*B          ADN   7  1                   [UPDATE X7   
   7 4RT2          SMO      7
  19 4S#L          LDX   4  A1(1)               [PICK UP TYPE/LINE NO WORD   
  16 4SS=          BNG   4  YIPEE               [J.IF FINISHED   
   9 4T?W          LDN   3  BLINE(3) 
   9 4TRG          CALL  6  XMSEARCH 
   8 4W?6          SMO      FX2  
   9 4WQQ          LDX   6  ACOMMUNE1
  18 4X=B          BZE   6  YILLINE             [ERROR-LINE NO ILLEGAL   
  16 4XQ2      [LINE EXISTS:CHAR 0 OF X4 CONTAINS NEW DEVICE TYPE
   7 4Y9L          SMO      2
  20 4YP=          LDX   6  BTYPE(3)            [PICK UP OLD DEVICE TYPE/GEOG NO 
   7 4_8W          SMO      2
  16 4_NG          LDX   0  BNUMB(3)            [-VE IF MOPPED ON
  17 5286          BPZ   0  XNMOPONA            [J IF MOPPED OFF -OK 
  18 52MQ    # IF UNIT MORRED ON THEN HW ONLY ALLOWED IF LINE WAS NULL   
  11 537B    # OR ATTACHED TO A CONCEPTUAL   
   7 53M2          SMO      2
  10 546L          LDX   0  CONTCEP(3)   
  17 54L=          BNZ   0  XMOPONA             [J IF CONCEPTUAL-OK  
   7 555W          LDN   1  0
  16 55KG          LDCH  0  6(1)                [CHECK OLD TYPE  
  16 5656          BNZ   0  XERA                [ERROR-NOT NXLL  
   5 56JQ    XMOPONA 
  16 574B          LDEX  5  6                   [GEOG NO IN X5   
  15 57J2          FMOPG    1,5,MOP             [FIND AMOR   
  15 583L          BNG   1  XGUN                [MUST EXIST  
  16 58H=          STO   1  5                   [SAVE PTR TO AMOR
  15 592W          HUNT2OPT 1,AMXOR,ALINES      [MUST EXIST  
   8 59GG          BNG   1  XGUN 
   7 5=26          SMO      2
  14 5=FQ          LDX   0  BNO(3)              [LINE NO 
   7 5=_B          SLL   0  2
   7 5?F2          ADS   0  1
   8 5?YL          LDCT  0  #530 
  11 5#D=          ANDX  0  A1+FOURTHWD(1)   
  19 5#XW          BNZ   0  XNMOPONA            [J IF IMOPQ AWAKE OR ATTACHED
   8 5*CG          LDCT  0  #424 
  11 5*X6          ORS   0  A1+FOURTHWD(1)   
  14 5BBQ          STO   5  1                   [AMOP PTR
   7 5BWB          LDN   0  1
  15 5CB2          SMO      IMOPADDR(1)         [REQUEST CORE
   8 5CTL          ADS   0  IWAKE
  14 5D*=          STO   2  5                   [SAVE X2 
  13 5DSW          LONGCLEAR IMOPADDR(1),#125,XNMOPON
   5 5F#G    XNMOPON 
   7 5FS6          LDX   2  5
   5 5G?Q    XNMOPONA
  21 5GRB          LDN   1  0                   [TO USE X1 AS A CHARACTER MODIFIER   
  19 5H?2          LDCH  5  4(1)                [CHAR 3 OF X5=NEW DEVICE TYPE
  21 5HQL          SMO      5                   [BRANCHING IS DONE LIKE THIS SO NEW  
  20 5J==          BRN      TABLE               [DEVICE TYPES MAY BE EASILY ADDED
   4 5JPW    [   
   4 5K9G    [   
   4 5KP6    [   
  18 5L8Q    NULL                 [THE NEW DEVICE IS THE ^NULL^ DEVICE   
   8 5LNB          LDN   0  #7777
   7 5M82          SMO      2
  21 5MML          ANDS  0  BTYPE(3)            [CLEAR OUT OLD DEVICE TYPE TO ^NULL^ 
   9 5N7=          LDX   4  BRECNO(2)
   7 5NLW          SLL   4  9
  18 5P6G          ORX   4  3                   [PTR TO LINE CELL IN X4  
   8 5PL6          SMO      FX2  
  21 5Q5Q          STO   4  AWORK4              [KEEP FOR POSSIBLE ERROR OR 7020 PATH
  21 5QKB          LDCH  0  6(1)                [X6 & X1 SET UP ABOVE-GET DEVICE TYPE
   7 5R52          SBN   0  2
  15 5RJL          BZE   0  Y7020               [J.IF 7020   
  20 5S4=          CALL  6  THROW               [OTHERWISE GET RID OF ASS & ATT  
  16 5SHW          BRN      UNIREC              [& THEN ALL DONE 
   8 5T3G    Y7020 CALL  6  THROW
   8 5TH6          SBN   3  BASS 
   8 5W2Q          LDCT  5  #777 
   7 5WGB          SMO      2
  21 5X22          ANDS  5  BNUMB(3)            [ZEROIZE NO.OF 7020-KEEP REST OF WORD
  19 5XFL          LDN   3  BLINE(3)            [PTR TO 7020 CHAIN POINTER   
   7 5_D6    NELL  SMO      2
  17 5_XQ          LDX   3  0(3)                [PICK UP 7020 POINTER
  19 62CB          BZE   3  SCANT               [J.TO SCAN IDF/TERMID BLOCK  
  19 62X2          LDX   4  3                   [KEEP FOR LATER USE IN^THROW^
  18 63BL          CALL  5  YREC                [OTHERWISE FIND 7020 CELL
   8 63W=          LDCT  0  #40  
   7 64*W          SMO      2
  20 64TG          ANDX  0  BTYPE+1(3)          [GET ^THIS IS CLUSTER CONSOLE^BIT
  21 65*6          BNZ   0  NCLUS               [AND ERROR IF THIS IS A CLUSTER CONS.
  19 65SQ    #     NOW SCAN IDF/ITERMID BLOCK & MARK ENTRIES TO BE REMOVED   
   7 66#B          SMO      2
  15 66S2          LDXC  0  BTYPE+2(3)          [TO TEST B0  
  21 67?L          BCC      X1                  [J.IF B0 NOT SET-SO NOT LAST 7020 CEL
   7 67R=          SMO      2
  16 68=W          LDX   6  BNO(3)              [FOR IDENTIFIER  
   9 68QG          ANDX  6  HALFTOP  
  20 69=6          DCH   0  6                   [STORE LINE NO IN BOTTOM 6 BITS  
  18 69PQ          CALL  5  MISCH               [FIND IDF/TERMID BLOCK   
  18 6=9B          BRN      TEROR               [BLOCK SHOULD BE PRESENT 
   9 6=P2    X3    LDX   0  A1+1(1)  
  19 6?8L          BZE   0  TEROR               [GEORGE ERROR IF END OF BLOCK
   7 6?N=          ERX   0  6
  16 6#7W          BZE   0  X2                  [J.IF MATCH FOUND
  21 6#MG          BDX   1  X3                  [INCREMENT X1 & LOOK AT NEXT WD.PAIR 
   8 6*76    X2    LDN   0  #4000
  21 6*LQ          ORS   0  A1+1(1)             [MARK ^TO BE REMOVED^ IN TERMID BLK. 
   8 6B6B    X1    CALL  6  THROW
   8 6BL2          SMO      FX2  
  19 6C5L          LDX   3  AWORK4              [PICK UP LINE CELL POINTER   
  16 6CK=          CALL  5  YREC1               [FIND LINE CELL  
   9 6D4W          LDN   3  BLINE(3) 
  16 6DJG          CALL  5  MFREEC              [FREE 7020 CELL  
  19 6F46          BRN      NELL                [J.FOR NEXT 7020 CELL IF ANY 
  17 6FHQ    MISCH HUNTW 1,IDF,ITERMID       [HUNT FOR IDF/ITERMID   
   8 6G3B          BNG   1  M1   
   7 6GH2          EXIT  5  1
   7 6H2L    M1    EXIT  5  0
  21 6HG=    #     SCAN IDF/ITERMID BLOCK & REMOVE ENTRIES WITH B12=1 IN FIRST WORD  
   8 6H_W    SCANT CALL  5  MISCH
  19 6JFG          BRN      TEROR               [GEOERR IF BLOCK NOT FOUND   
  19 6J_6          LDX   5  A1(1)               [PICK UP COUNT OF WORDS USED 
  20 6KDQ          SBN   5  3                   [COUNT NOW CAN BE USED TO CREATE-
  21 6KYB          SMO      5                   [POINTERS IN X2 & X3 TO LAST WORD-   
  16 6LD2          LDN   2  A1(1)               [PAIR IN BLOCK   
   7 6LXL          LDX   3  2
  18 6MC=          ADN   5  1                   [ADJUST COUNT & CREATE-  
  18 6MWW          SRL   5  1                   [A COUNT OF WORD PAIRS   
   8 6NBG    SC2   LDX   0  0(3) 
   8 6NW6          ANDN  0  #4000
  16 6P*Q          BNZ   0  SC1                 [J.IF B12 IS SET 
  21 6PTB    SC4   SBN   3  2                   [ADJUST X3 TO INVESTIGATE NEXT WORD- 
  21 6Q*2          BCT   5  SC2                 [PAIR SO LONG AS COUNT IS NOT ZERO   
  20 6QSL          LDX   5  A1(1)               [SEE IF LENGTH OF BLOCK MUST BE- 
  18 6R#=          SBN   5  2                   [CHANGED OR BLOCK FREED  
  21 6RRW          BNZ   5  SC3                 [J.TO DO POSS.ALTLEN IF BLOCK IN USE 
  17 6S?G          FREECORE 1                   [OTHERWISE FREE IT   
   9 6SR6          BRN      UNIREC   
  10 6T=Q    SC3   LDX   5  ALOGLEN(1)   
   8 6TQB          SBX   5  A1(1)
   8 6W=2          SBN   5  16   
  20 6WPL          BNZ   5  UNIREC              [J.IF LESS THAN 16 WORDS TO SPARE
   8 6X9=          LDX   5  A1(1)
   7 6XNW          LDX   6  1
  18 6Y8G          ALTLEN   6,5                 [MAKE IDF/TERMID SMALLER 
   9 6YN6          BRN      UNIREC   
  21 6_7Q    SC1   MOVE  2  2                   [MOVE UP ENTRY TO POSITION X3 POINTS 
  21 6_MB          STOZ     0(2)                [TO:NO HARM DONE IF X2=X3:THEN ZERO- 
  20 7272          LDN   0  2                   [IZE LAST RELEVANT WORD IN BLOCK 
  20 72LL          SBS   0  A1(1)               [ADJUST COUNT OF NO OF WORDS USED
  20 736=          SBS   0  2                   [ADJUST X2 TO POINT TO LAST ENTRY
  16 73KW          BRN      SC4                 [J.TO RESUME SCAN
   4 745G    [   
   4 74K6    [   
  15 7D#6    SENT                [THE NEW DEVICE IS SENTINEL 
  16 7DRQ    XMOP                [THE NEW DEVICE IS MOP TERMINAL 
   8 7F?B          LDCT  0  #770 
  18 7FR2          ANDX  6  0                   [MASK OUT OLD DEVICE TYPE
  18 7G=L          BNZ   6  YILLDEV             [ERROR-DEVICE NOT NULL   
  18 7GQ=          ANDX  4  0                   [MASK OUT NEW DEVICE TYPE
   7 7H9W          SMO      2
  18 7HPG          ORS   4  BTYPE(3)            [STORE NEW DEVICE TYPE   
  20 7J96    UNIREC                             [RESTORE POINTERS TO UNIT CELL SO
  20 7JNQ                                       [THAT NEXT LINE MAY BE CONSIDERED
   8 7K8B          LDX   2  FX2  
  16 7KN2          LDX   5  AWORK1(2)           [REC NO FOR UNIT 
  17 7L7L          LDX   3  AWORK2(2)           [CELL PTR FOR UNIT   
   9 7LM=          CALL  6  RECFIND  
   9 7M6W          BRN      NEXTLINE 
   4 7MLG    [   
   4 7N66    [   
   4 7NKQ    [   
  13 7QJ=    XBULK               [NEW DEVICE IS 7020 
  16 7R3W          LDX   0  BRECNO(2)           [LINE RECORD NO  
   7 7RHG          SLL   0  9
  21 7S36          ORX   0  3                   [MAKE IDF-TYPE POINTER TO LINE CELL  
   8 7SGQ          SMO      FX2  
  15 7T2B          STO   0  AWORK3              [AND KEEP IT 
  18 7TG2          LDEX  0  6                   [PICK OUT GEOG.UNIT NO   
   7 7T_L          SLL   0  6
  17 7WF=          DCH   4  0                   [AND PUT IN LINE NO  
   8 7WYW          SMO      FX2  
  18 7XDG          STO   0  AWORK4              [KEEP GEOG.NO & LINE NO  
   8 7XY6          LDCT  0  #770 
  16 7YCQ          ANDX  6  0                   [OLD DEVICE TYPE 
   8 7YXB          LDCT  5  #020 
  10 7_C2          MHUNTW   1,IDF,CONDATA
  21 7_WL          BZE   6  MORE                [J.IF NULL-SO NO 7020 ALREADY DEFINED
   7 82B=          SBX   6  5
  17 82TW          BNZ   6  YILLDEV             [J.IF NON-7020 DEVICE
  16 83*G          ANDN  4  #7777               [MASK OUT LINE NO
   7 83T6          SMO      2
  16 84#Q          LDX   5  BNUMB(3)            [KEEP NO OF 7020 
   8 84SB          SMO      FX2  
  18 85#2          STO   4  ACOMMUNE6           [KEEP X4 IN CASE OF ERROR
   7 85RL          SMO      7
  20 86?=          LDX   4  A1+1(1)             [PICK UP IDENTIFIER/POLLING ADD  
   9 86QW          LDN   3  BLINE(3) 
   9 87=G          CALL  6  XMSEARCH 
   8 87Q6          SMO      FX2  
   9 889Q          LDX   6  ACOMMUNE1
  21 88PB          BNZ   6  YADDPOLL            [ERROR-POLLING ADD ALREADY DEFINED   
   8 8992          ANDN  5  #7777
   8 89NL          SBN   5  11   
  20 8=8=          BZE   5  YELEVEN             [ERROR-11 7020 DEVICES ALREADY   
  20 8=MW          LDN   0  1                   [CHECK THAT TX+16 IF X>12,TX-16  
  18 8?7G          ERX   4  0                   [IS NOT ALREDY DEFINED   
   8 8?M6          LDX   2  FX2  
   9 8#6Q          LDX   5  AWORK3(2)
  16 8#LB          LDEX  3  5                   [DISTANCE FROM A1
   7 8*62          SRL   5  9
  17 8*KL          CALL  6  RECFIND             [FIND LINE CELL AGAIN
  19 8B5=          LDN   3  BLINE(3)            [SEARCH BY IDENT/POLLING ADDR
   9 8BJW          CALL  6  XMSEARCH 
   8 8C4G          SMO      FX2  
   9 8CJ6          LDX   6  ACOMMUNE1
   7 8D3Q          LDN   0  1
   7 8DHB          ERX   4  0
  17 8F32          BNZ   6  YADDPOLL            [J IF ALREADY DEFINED
   8 8FGL          BRN      NA   
  16 8G2=    MORE                               [NO 7020 ALREADY 
   7 8GFW          SMO      2
  19 8G_G          ORS   5  BTYPE(3)            [STORE IN 7020 DEVICE TYPE   
   9 8HF6          LDN   3  BLINE(3) 
   8 8HYQ           SMO      7   
   9 8JDB           LDX   4  A1+1(1) 
  20 8JY2    #     NOW SCAN IDF/ITERMID BLOCK TO MAKE SURE IDENTIFIER IS UNIQUE  
  18 8KCL    NA    CALL  5  MISCH               [FIND IDF/TERMID BLOCK   
  16 8KX=          BRN      MOR                 [J.IF NONE EXISTS
   9 8LBW    XB4   LDX   0  A1+1(1)  
  17 8LWG          BZE   0  MOR                 [J.IF END OF BLOCK   
   7 8MB6          ERX   0  4
   9 8MTQ          ANDX  0  HALFTOP  
  20 8N*B          BZE   0  YIDENT              [ERROR IDENTIFIER ALREADY EXISTS 
  19 8NT2          BDX   1  XB4                 [UPDATE X1 & J.FOR NEXT ENTRY
  18 9XX2    MOR   LDN   5  1                   [INTERFACE COUNT IN X5   
  15 9YBL    ZOOP  ADN   7  1                   [UPDATE X7   
  10 9YW=          MHUNTW   1,IDF,CONDATA
   7 9_*W          SMO      7
  18 9_TG          LDX   6  A1+1(1)             [PICK UP INTERFACE DATA  
  21 =2*6          BZE   6  NOMOR               [J.IF NO PERIPHERAL ON THIS INTERFACE
   6 =2SQ    #SKI JSKI17 
   4 =3#B    (   
   8 =3S2          LDX  0  A1(1) 
   8 =4?L          BPZ    0  NSPL
   8 =4R=          LDCT  0  #130 
   7 =5=W          ORX   5  0
   7 =5QG          LDN   0  5
   7 =6=6          SBX   0  6
   8 =6PQ          BNZ   0  NSPL 
   8 =79B          LDCT  0  #10  
   7 =7P2          ERX   5  0
   4 =88L    NSPL
   4 =8N=    )   
  18 =97W          MGETCELL 2,3                 [GET CELL FOR INTERFACE  
   7 =9MG          SMO      2
  20 ==76          STO   4  BNO(3)              [STO IDENTIFIER/POLLING ADDRESS  
   8 ==LQ          LDCT  0  #100 
   6 =?6B    #SKI JSKI17 
   4 =?L2    (   
   7 =#5L          ORX   0  5
   9 =#K=          ANDX  0  HALFTOP  
   7 =*4W          LDEX  5  5
   4 =*JG    )   
   7 =B46          SMO      2
  19 =BHQ          STO   0  BTYPE+1(3)          [MARK THIS CELL AS 7020 CELL 
   8 =C3B          SMO      FX2  
   9 =CH2          LDX   0  AWORK4   
   7 =D2L          SMO      2
  10 =DG=          STO   0  BTYPE+2(3)   
   7 =D_W          LDX   0  6
   7 =FFG          SRC   6  6
   7 =F_6          ORX   6  5
   7 =GDQ          SMO      2
  20 =GYB          STO   6  BTYPE(3)            [STORE DEVICE TYPE/INTERFACE NO  
   7 =HD2          SBN   0  3
  16 =HXL          BNZ   0  NOMOR               [J.IF NOT CONSOLE
  21 =JC=          MHUNTW   1,IDF,CONDATA       [OTHERWISE FIND OTHER HALF OF CONSOLE
   7 =JWW          LDX   0  5
   7 =KBG    NXT   ADN   0  1
   7 =KW6          ADN   1  1
   7 =L*Q          SMO      7
   9 =LTB          LDX   6  A1+1(1)  
   7 =M*2          SBN   6  3
  16 =MSL          BNZ   6  NXT                 [J.IF NOT CONSOLE
   8 =N#=           LDX   6  0   
   7 =NRW          ADX   6  5
   7 =P?G          ANDN  6  1
  21 =PR6          BZE   6  NXT                 [J. IF THIS CONSOLE ON WRONG INTERFAC
   7 =Q=Q          SMO      2
  18 =QQB          ORS   0  BTYPE+1(3)          [STORE AWAY CONSOLE DATA 
   7 =R=2          SMO      7
  21 =RPL          STOZ     A1+1(1)             [CLEAR CONDATA WORD-NOT NEEDED AGAIN 
  15 =TN6    NOMOR ADN   5  1                   [UPDATE X5   
   7 =W7Q          LDN   0  7
  17 =WMB          SBX   0  5                   [J.FOR NEXT INTERFACE
   8 =X72          BNZ   0  ZOOP 
   8 =XLL          LDCT  0  #400 
   7 =Y6=          SMO      2
  10 =YKW          ORS  0   BTYPE+2(3)   
   7 =_5G          ADN   7  1
   8 =_K6          LDX   2  FX2  
   9 ?24Q          LDX   3  AWORK3(2)
   8 ?2JB          ANDN  3  #777 
   9 ?342          LDX   5  AWORK3(2)
   7 ?3HL          SRL   5  9
   9 ?43=          CALL  6  RECFIND  
   7 ?4GW          LDN   6  1
   7 ?52G          ADX   3  2
  19 ?5G6          ADS   6  BNUMB(3)            [UPDATE NO OF 7020 DEVICES   
   9 ?5_Q          LDX   6  BNO(3)   
  21 ?6FB          DSA   6  4                   [MAKE FIRST WD OF PAIR FOR TERMID BLK
   9 ?6_2          LDX   6  BTYPE(3) 
   8 ?7DL          ANDN  6  #777 
  21 ?7Y=          FMOPG    3,6,MPX             [FIND POINTER TO AMOP(MPX & UPX ONLY)
  17 ?8CW          BNG   3  TEROR               [GEOERR IF NONE FOUND
  18 ?8XG          CALL  5  MISCH               [FIND IDF/TERMID BLOCK   
  20 ?9C6          BRN      XB10                [J.TO CREATE ONE IF NONE FOUND   
  21 ?9WQ          LDX   0  ALOGLEN(1)          [SEE IF THERE IS ENOUGH ROOM IN BLOCK
   8 ?=BB          SBX   0  A1(1)
   7 ?=W2          SBN   0  2
  16 ??*L          BPZ   0  XB11                [J IF THERE IS   
  21 ??T=          LDX   5  ALOGLEN(1)          [OTHERWISE INCREASE LENGHT BY 4 WORDS
   7 ?##W          ADN   5  4
   7 ?#SG          LDX   6  1
  17 ?*#6          ALTLEN   6,5                 [ENLARGE IDF/ITERMID 
   8 ?*RQ          CALL  5  MISCH
  15 ?B?B          NULL                         [MUST EXIST  
   7 ?BR2    XB11  LDN   0  2
  19 ?C=L          ADS   0  A1(1)               [UPDATE COUNT OF WORDS USED  
   8 ?CQ=          LDX   5  A1(1)
  21 ?D9W          SBN   5  3                   [SO X5 CAN BE USED TO CALCULATE PTR- 
  20 ?DPG          SMO      5                   [TO NEW POSITION OF NEW WORD PAIR
  21 ?F96          LDN   1  A1(1)               [X1 NOW POINTS TO POSITION FOR WD.PR.
  19 ?FNQ          STO   4  0(1)                [STORE FIRST WD:IDENT/LINE NO
  18 ?G8B          STO   3  1(1)                [STORE SECOND WD:AMOP PTR
  17 ?GN2          STOZ     2(1)                [ZEROIZE LAST WORD   
  21 ?H7L          BRN      UNIREC              [J.FOR NEXT IDF/CONDATA BLOCK ENTRY  
  18 ?HM=    XB10  SETNCORE 4,1,IDF,ITERMID     [CREATE IDF/ITERMID BLOCK
   7 ?J6W          LDN   0  2
  19 ?JLG          STO   0  A1(1)               [SETUP A COUNT OF WORDS USED 
   8 ?K66          BRN      XB11 
  19 ?KKQ    YIPEE                              [WRITE UPDATED RECORDS TO IDF
   8 ?L5B          CALL  5  SCMID
   8 ?LK2          BRN      NOOLD
   7 ?M4L          FREECORE 1
   8 ?MJ=    NOOLD CALL  5  MISCH
   8 ?N3W          BRN      TMDFN
   9 ?NHG          LDX   2  BMISC+1  
   8 ?P36          CHAIN    1,2  
  15 ?PGQ          TRACE    ALOGLEN(1),ZZLOGLN2  [YZQZTRACE  
   5 ?Q2B    TMDFN   
   7 ?QG2          IDFWRITE  
   6 ?Q_L          CLOSE 
   6 ?RF=          ENDCOM
  16 #8M6    YILLUN                             [ILLEGAL UNIT NO 
   8 #96Q          SMO      FX1  
   9 #9LB          LDX   3  XILLUN   
   8 #=62    ONUMA ANDN  4  #7777
   8 #=KL    ONUM  OUTNUM   4,0  
   6 #?5=    YERRX CLOSE 
   7 #?JW          COMERRX  3
  16 ##4G    [                                  [ILLEGAL LINE NO 
   5 ##J6    YILLINE 
   8 #*3Q          SMO      FX1  
   9 #*HB          LDX   3  XILLINE  
   8 #B32          BRN      ONUMA
   4 #BGL    [   
  18 #C2=    YELEVEN                            [11 7020 DEVICES ALREADY 
   8 #CFW          SMO      FX2  
   9 #C_G          LDX   4  ACOMMUNE6
   8 #DF6          SMO      FX1  
   9 #DYQ          LDX   3  XELEVEN  
   8 #FDB          BRN      ONUM 
   4 #FY2    [   
   5 #GCL    YIDFUN  
   8 #GX=          SMO      FX1  
   9 #HBW          LDX   3  XIDFUN   
   8 #HWG          BRN      YERRX
   4 #JB6    [   
  21 #JTQ    YILLDEV                            [DEVICE ALREADY DEFINED ON THIS LINE 
   8 #K*B          SMO      FX1  
   9 #KT2          LDX   3  XILLDEV  
   8 #L#L          BRN      ONUMA
   7 #LBJ ...#UNS  ANSTOOMANY
   4 #LDG ...(   
   5 #LGD ...YTOOMANY
   8 #LJB ...      SMO      FX1  
   9 #LL# ...      LDX   3  XTOOMANY 
   8 #LN= ...      BRN      YERRX
   4 #LQ8 ...)   
   4 #LS=    [   
   5 #M?W    YADDPOLL
   8 #MRG          ANDN  4  #7777
  18 #N?6          LDN   7  0                   [MARKER 1ST TIME ROUND   
   8 #NQQ          LDX   1  FX1  
   5 #P=B    NXTER16 
   8 #PQ2          LDN   3  12   
  16 #Q9L    NXTER SMO      3                   [FIND TERMINAL NO
  10 #QP=          TXU   4  TERAD-1(1)   
   8 #R8W          BCC      TERF 
   8 #RNG          BCT   3  NXTER
  18 #S86          BNZ   7  TEROR               [NO SUCH POLLING ADDRESS 
  16 #SMQ          ADN   7  1                   [2ND TIME ROUND  
  14 #T7B          SBN   4  1                   [T17-T28 
   9 #TM2          BRN      NXTER16  
   5 #W6L    TEROR   
  18 #WL=          GEOERR   1,TERMINAL          [POLLING ADD SHOULD EXIST
  14 #X5W    TERF  BZE   7  T112                [T1-T12  
  14 #XKG          ADN   3  16                  [T17-T28 
   7 #Y56    T112  LDX   4  3
  10 #YJQ          LDX   3  XADDPOLL(1)  
   8 #_4B          BRN      ONUM 
   4 #_J2    [   
   8 *23L    PROAT SMO      FX1  
  20 *2H=          LDX   7  XCONPROAT           [CLUSTER PROP.ATTRIBUTED MESSAGE 
   8 *32W    PRO   SMO      FX2  
  17 *3GG          LDX   3  AWORK4              [LINE CELL POINTER   
   8 *426          CALL  5  YREC1
   7 *4FQ          SMO      2
  14 *4_B          LDX   4  BNO(3)              [LINE NO 
  18 *5F2          LDX   3  7                   [MONFILE POINTER INTO X3 
  17 *5YL          BRN      ONUMA               [J.TO OUTPUT MESSAGE 
   4 *6D=    [   
  15 *6XW    XERA  LDN   3  1                   [GET GEOGNO  
   7 *7CG          LDN   4  0
   9 *7X6          SPARANXT #36,4,3  
   7 *84# ...#UNS  ANSTOOMANY
  12 *89G ...      TESTREP2   TOOMANY,YTOOMANY   
   9 *8BQ          LDX   3  XMOPON(1)
  18 *8WB          BRN      YERRX               [NOT IN REQUIRED STATE   
  10 *9B2    XGUN  GEOERR   1,UNIT MIS   
  21 *?#G    NCLUS CALL  5  MISCH               [NOW UNSET ANY B12 IN ITERMID BLOCK  
   8 *?S6          BRN      TEROR
  21 *#?Q          NGN   5  #4001               [MAKE MASK #777737777 TO UNSET B12   
   9 *#RB    NCL2  LDX   0  A1+1(1)  
  17 **?2          BZE   0  NCL1                [J.IF END OF BLOCK   
  18 **QL          ANDS  5  A1+1(1)             [MAKE SURE B12 IS UNSET  
   8 *B==          BDX   1  NCL2 
   8 *BPW    NCL1  SMO      FX1  
  20 *C9G          LDX   7  XCONCLUS            [THIS IS CLUSTER CONSOLE MESSAGE 
   8 *CP6          BRN      PRO  
   5 *K52    YIDENT  
   8 *KJL          SMO      FX2  
   9 *L4=          STO   4  AWORK3   
   7 *LHW          LDN   5  2
   9 *M3G          OUTPARAM 5,AWORK3 
   9 *MH6          LDX   3  XIDENT(1)
   8 *N2Q          BRN      YERRX
   4 *NGB    #END
   2 *P22
   6 ____ ...667334330001
