  14 23DW    #SEG  CLOSEIPB8                    [R.WEYAND
  11 23YG    #LIS  K0CLOSEIPB>K0POST>K0ALLGEO
  10 24D6                   8HCLOSEIPB   
   4 24XQ    #   
  13 25CB ...      SEGENTRY K1CLOSEIPB,XK1CLOSEIPB   
   4 26BL    #   
  21 26DJ ...#  ENTRY POINTS FOR SHFS - BEWARE THEY ARE NOT COMPILED IN STD. G3 !!   
   4 26GG ...#   
   8 26JD ...      FSHCODE  AORB 
   4 26LB ...(   
  13 26N# ...      SEGENTRY K2CLOSEIPB,XK2CLOSEIPB   
  13 26Q= ...      SEGENTRY K3CLOSEIPB,XK3CLOSEIPB   
   4 26S8 ...)   
   4 26W=    #   
  21 27*W    # THIS SEGMENT SENDS END OF IPB OUTPUT AND CLOSE DOWN MESSAGES TO ALL   
  16 27TG    # IPB'S ON THE INSTALLATION AND ISSUES A DIS TO EACH
   4 28*6    #   
   4 28SQ    #   
  15 29#B    PMRW           +0                  [REPLY WORD  
  16 29S2    PMCHA          +0                  [CHAINING WORD   
  19 2=?L    PMADDR1        #400:0,#01030000            [END OF IPB OUTPUT   
   9 2=R= ...#DEF  PMLEN1=0?-PMADDR1 
  17 2?=W    PMADDR2        #400:0,#01110000            [CLOSE DOWN  
   9 2?QG ...#DEF  PMLEN2=0?-PMADDR2 
   5 2#=6    PMADDR  
   5 2#PQ    #REP  66
   8 2*9B                   +0   
   9 2*P2 ...#DEF  PMLEN=0?-PMADDR   
   9 2B8L    SSMASK         #770000  
   9 2BN=    ROILL          #30000   
  20 2C7W    READCT         +0                  [COUNT OF NO. OF READS ATTEMPTED 
  10 2C*4 ...      SEGENTRY  K50CLOSEIPB 
   8 2CG= ...SLAB1          8H   
   8 2CGH ...                +0  
   8 2CGS ...                8H  
   8 2CH5 ...                +0  
   8 2CHB ...                 8H 
   8 2CHM ...                +0  
   8 2CHY ...                8H  
   8 2CJ9 ...                +0  
   8 2CJG ...                8H  
   8 2CJR ...                +0  
   8 2CK4 ...                8H  
   8 2CK* ...                +0  
   8 2CKL ...S                8H 
   8 2CKX ...                +0  
   8 2CL8 ...                8H  
   8 2CLF ...                +0  
  10 2CLQ ...      SEGENTRY  K99CLOSEIPB 
   8 2CM3 ...                -3  
   4 2CMG    #   
   4 2D76    #   
  15 2DLQ    # THIS SUBROUTINE CHECKS THE REPLY TO A PERI.   
  13 2F6B    # IT EXITS 0 IF THE PERI WAS REJECTED   
  19 2FL2    # IT EXITS 1 IF THE IPB WAS INOPERABLE OR REPLY INDICATED ERROR 
  20 2G5L    # IT EXITS 2 IF THE REPLY INDICATED TIMED OUT AND/OR NOT IMPLEMENTED
  14 2GK=    # IT EXITS 3 IF THE PERI TERMINATED NORMALLY
  18 2H4W    # X6 CONTAINS THE LINK AND PMCA+1 CONTAINS THE REPLY WORD   
   8 2HJG    # IT OVERWRITES X0. 
   4 2J46    #   
   4 2JHQ    #   
   5 2K3B    REPCHEK 
   8 2K5_ ...#SKI K6IPB>100-100  
   4 2K8J ...(   
   9 2K?7 ...      LDX   0  IREP1(2) 
  21 2K*Q ...      STO   0  GEN0                [DENOTES PERI TYPE TO TRACE ROUTINE  
   4 2KD* ...)   
   7 2KH2          PERI  0  2
   9 2L2L ...      LDX   0  PMRW(1)  
  18 2LG=          BPZ   0  TER                 [J IF TRANSFER TERMINATED
   7 2L_W          SLC   0  5
  17 2MFG          BPZ   0  OP                  [J IF IPB OPERABLE   
  19 2M_6    RINOP EXIT  6  1                   [OTHERWISE EXIT INOPERABLE   
   7 2NDQ    OP    SRC   0  1
  16 2NYB          BNG   0  REJ                 [J IF REJECTED   
  20 2PD2 ...WAIT  LDX   0  PMRW(1)             [SEE IF TRANSFER HAS TERMINATED  
  17 2PXL          BNG   0  WAIT                [WAIT FOR TERMINATION
   7 2QC=    TER   SLC   0  5
  16 2QWW          BPZ   0  RINOP               [J IF INOPERABLE 
   7 2RBG          SRC   0  2
  15 2RW6          BNG   0  RINOP               [J IF ERROR  
   7 2S*Q          SLC   0  1
  17 2STB ...      BNG   0  REJ                 [J IF NOT IMPLEMENTED
   7 2T*2          SRC   0  3
  16 2TSL          BNG   0  NOTIMP              [J IF TIMED OUT  
   8 2TT3 ...#SKI K6IPB>100-100  
   4 2TTD ...(   
   9 2TTT ...      LDX   0  ASWITCH1 
   7 2TW= ...      SLL   0  7
  21 2TWM ...      BPZ   0  TRACEX              [SWITCH NOT SET(BY MOP TRACE COMMAND)
  20 2TX4 ...[TRACES (ONLINE) SUCCESSFUL CC TRANSFERS IF ^MOP TRACE^ SWITCHED ON 
   8 2TXF ...      STO   3  GEN3 
   8 2TXW ...      STO   4  GEN4 
   8 2TY? ...      STO   5  GEN5 
   7 2TYN ...      LDN   5  0
   8 2T_5 ...      LDX   4  GEN0 
   8 2T_G ...      SMO      FXPM1
   8 2T_X ...      LDEX  0  PMRW 
   7 2W2# ...      NGX   0  0
  15 2W2P ...      BZE   4  TRACEL1             [READ PERI   
   9 2W36 ...      ADX   0  ICOUNT(2)
   8 2W3H ...      SBN   0  PMLEN
   5 2W3Y ...TRACEL1 
   8 2W4* ...      ADN   0  PMLEN
   7 2W4Q ...      LDX   3  2
   9 2W57 ...      ADN   3  ISCTSA   
  15 2W5J ...      BZE   4  TRACEL2             [READ PERI   
  10 2W5_ ...      SBN   3  ISCTSA-ICTSA 
   5 2W6B ...TRACEL2 
   8 2W6R ...      LDX   3  0(3) 
   5 2W78 ...TRACEL3 
   7 2W7K ...      SMO      5
   8 2W82 ...      '177  0  0(3) 
   7 2W8C ...      8HCLOSEIPB
   7 2W8S ...      ADN   5  1
   9 2W99 ...      BCT   0  TRACEL3  
   8 2W9L ...      LDX   3  GEN3 
   8 2W=3 ...      LDX   4  GEN4 
   8 2W=D ...      LDX   5  GEN5 
   5 2W=T ...TRACEX  
   4 2W?= ...)   
  15 2W#=          EXIT  6  3                   [EXIT IF OK  
   5 2WRW    NOTIMP  
  21 2X?G          EXIT  6  2                   [EXIT FOR TIMED OUT/NOT IMPLEMENTED  
  17 2XR6    REJ   EXIT  6  0                   [EXIT FOR REJECTED   
   4 2Y=Q    #   
   4 2YQB    #   
  18 2_=2    # THIS SUBROUTINE SETS UP THE CONTROL AREA FOR A READ PERI. 
   8 2_PL    # IT OVERWRITES X0  
   7 329=    # THE LINK IS X6
   4 32NW    #   
   4 338G    #   
   5 33D# ...SETRD   
  19 33N6 ...      JSBUSY      2,SETRD             [WAIT TILL READ FINISHES  
  16 33XY ...      LDN   0  2                     [MODE FOR READ 
  20 347Q          DSCA     2,DLA,0,MODE        [MODE FOR READ IN DL - SPOUT AREA
  16 34MB          LDN   0  PMLEN               [LENGTH OF BUFFER
  16 3572          DSCA     2,DLA,0,COUNT       [SET COUNT IN DL 
  16 35LL ...      LDN   0  PMADDR(1)           [BUFFER ADDRESS  
  19 366=          DSCA     2,STO,0,CTSA        [SET TRANSFER START ADDRESS  
   8 36KW ...      LDCT  0  #400 
   9 375G ...      ORN   0  PMCHA(1) 
  20 37K6          DSCA     2,STO,0,CHADD       [SET CHAINING ADDRESS IN SPOUT   
   9 384Q ...      LDN   0  PMRW(1)  
  16 38JB          DSCA     2,STO,0,REP1        [SET R.W IN SPOUT
   7 3942          EXIT  6  0
   4 39HL    #   
   8 39JS ...      FSHCODE  AORB 
   4 39L2 ...(   
  21 39M8 ...XK2CLOSEIPB                        [ ENTRY TO SEND FIRST GEORGE BROKEN  
  20 39NB ...      LDX   1  FXPM1               [ MESSAGE TO ALL IPC'S FOR SHFS  
  18 39PJ ...      NGN   7  1                   [ SET ENTRY POINT MARKER 
   9 39QQ ...      BRN      PCCLOSE  
   4 39RY ...#   
  20 39T6 ...XK3CLOSEIPB                        [ ENTRY POINT FOR SECOND GEORGE  
  16 39W# ...      LDX   1  FXPM1               [ BROKEN MESSAGE 
  17 39XG ...      LDN   7  0                   [ SET ENTRY MARKER   
   9 39YN ...      BRN      PCCLOSE  
   4 39_W ...)   
   4 3=3=    #   
   6 3=GW    XK1CLOSEIPB 
  19 3=QN ...      LDX   1  FXPM1               [BASE ADDRESS OF THIS SEGMENT
  16 3?2G          LDN   4  24                  [SET TYPE TO IPB 
  17 3?G6    NUNIT FINDPEREC 2,APFOTY,4,RFR2    [DOES THE UNIT EXIST?
  21 3?M# ...      FSHSKIP                      [ FOR SHARED FILESTORE OMIT IPC CLOSE
  21 3?SG ...(                                  [ -ALREADY BEEN DONE ON EARLIER ENTRY
   8 3?_Q    #SKIP       IPCV1   
  17 3#FB          BNG   2  PCCLOSE             [J TO CHECK FOR IPC  
   8 3#_2    #SKIP       IPCV1-1 
  15 3*DL ...      BNG   2  (JPMSEG)            [J IF NO MORE
   4 3*H9 ...)   
  21 3*KS ...      FSHCODE  AORB                [ FOR SHFS IPC CLOSE HAS ALREADY BEEN
  20 3*NC ...(                                  [ SO JUST RETURN TO NEXT OVERLAY 
   9 3*R2 ...      BNG   2  (JPMSEG) 
   4 3*TK ...)   
  15 3*Y=          JWRONG   2,RFR2              [J IF WRONGED
  15 3BCW          JEXOTIC  2,RFR2              [J IF EXOTIC 
  21 3BK4 ...      JONL     2,PFOUND            [JIF ONLINE TO JUST SEND'CLOSEDOWN'  
  21 3BQ= ...      JFREE    2,PFOUND            [JIF UNALLOCATED:JUST SEND'CLOSEDOWN'
  17 3BXG          LDN   0  9                   [MODE INTO DL ENTRY  
  10 3CC6          DCA      2,STO,0,MODE 
  16 3CWQ          LDN   0  PMLEN1              [COUNT INTO DL   
  10 3DBB          DCA      2,DSA,0,COUNT
  17 3DW2 ...      LDN   0  PMADDR1(1)          [BUFFER ADDRESS IN DL
  10 3F*L          DCA      2,STO,0,CTSA 
  19 3FT= ...      LDCT  0  #400                [SET CHAINING ADDRESS IN DL  
   9 3G#W ...      ORN   0  PMCHA(1) 
  17 3GSG          DCA      2,STO,0,CHADD       [NUMBER - NO CHAINING
  19 3H#6 ...P1    LDN   0  PMRW(1)             [SET RW ADDR IN CURRENT AREA 
  10 3HRQ          DCA      2,STO,0,REP1 
  19 3J?B          CALL  6  REPCHEK             [WRITE END OF IPB OUTPUT AND 
  15 3JR2                                       [CHECK REPLY 
  19 3K=L          BRN      TINP1               [ISSUE READ IF WRITE REJECTED
  18 3KQ=          BRN      PMDIS               [ONLY DIS IF INOPERABLE  
  20 3L9W          BRN      P1                  [TRY AGAIN IF NOT IMPLEMENTED OR 
  15 3LPG                                       [TIMED OUT   
  16 3M96 ...      LDX   0  PMRW(1)             [SEE IF WRITE WAS
  15 3MNQ          ANDN  0  #777                [ENDED AND   
  14 3N8B          BZE   0  OK1                 [J IF NOT
  19 3NN2    TINP1 CALL  6  SETRD               [ISSUE READ IF WRITE REJECTED
  14 3P7L                                       [OR ENDED
  15 3PM=          CALL  6  REPCHEK             [CHECK REPLY 
  19 3Q6W          BRN      PMDIS               [JUST DIS IF READ REJECTED   
  16 3QLG          BRN      PMDIS               [OR INOPERABLE   
  20 3R66          NULL                         [TRY AND WRITE AGAIN IF NOT IMP. 
   5 3R*Y ...P1AGAIN 
  15 3RKQ          LDN   0  #11                 [OR TIMED OUT
  18 3S5B          DCA      2,DSA,0,MODE        [OR IF READ TERMINATED OK
  16 3SK2          LDN   0  PMLEN1              [SET UP CONTROL  
  15 3T4L          DCA      2,DSA,0,COUNT       [AREA AGAIN  
  18 3TJ= ...      LDN   0  PMADDR1(1)          [FOR END OF IPB OUTPUT   
  10 3W3W          DCA      2,STO,0,CTSA 
   8 3WHG          BRN      P1   
   4 3X36    #   
   4 3XGQ    #   
  18 3Y2B    # COME HERE IF END OF IPB OUTPUT WAS WRITTEN SUCCESSFULLY   
   4 3YG2    #   
   4 3Y_L    #   
  18 3_F=    OK1   CALL  6  SETRD               [NOW TRY TO READ REPLY   
  15 3_YW    P2    CALL  6  REPCHEK             [CHECK REPLY 
  20 42DG          BRN      PMDIS               [J TO DIS UNIT IF READ REJECTED  
  17 42Y6          BRN      PMDIS               [OR IF IPB INOPERABLE
  19 43CQ          BRN      P2R                 [J IF TIME OUT OR NOT IMP.   
   9 43XB ...      STOZ     READCT(1)
  15 44C2                                       [OR TIME OUT 
   9 44WL ...      LDX   0  PMRW(1)  
  18 45B=          ANDN  0  #777                [ISOLATE REMAINDER COUNT 
  20 45TW          LDN   6  PMLEN               [AND GET COUNT OF NO. OF WORDS   
  16 46*G          SBX   6  0                   [INPUT INTO X6   
  20 46T6          SBN   6  1                   [SUBTRACT 1 FOR BLOCK STATUS WORD
  20 47#Q          BZE   6  PMDIS               [DIS IF ONLY 1 WORD READ - THIS  
  17 47SB                                       [SHOULD NOT HAPPEN   
  21 4885 ...      LDN   3  1                   [INITIALISE MODIFIER FOR INPUT BUFF  
  20 48HS ...NXT   SMO      1                   [GET NO. OF WDS. IN NEXT MESSAGE 
   9 48XH ...      LDCH  5  PMADDR(3)
  20 49*8 ...      BZE   5  RESETRRWA           [J IF ZERO - NO MORE SUPERVISORY 
  18 49QW                                       [SEGMENTS SO READ AGAIN  
  21 4==G          SBX   6  5                   [UPDATE NO. OF WDS. LEFT TO LOOK AT  
   7 4=JX ...      SMO      1
  14 4=X# ...      LDX   0  PMADDR(3)           [ISOLATE 
  15 4?9Q          SRC   0  12                  [SUPERVISORY 
  13 4?PB          ANDN  0  #77                 [CODE
   8 4#92          SBN   0  35   
  20 4#NL ...      BZE   0  PFOUND              [J IF END OF REMOTE OUTPUT(35)   
   7 4*8=          SBN   0  2
  17 4*MW          BZE   0  TILL                [J IF ILLEGAL (37)   
   8 4B7G          SBN   0  16   
  21 4BC# ...      BZE   0  P1CC                [FAIL(53):CRASH CLOSE THE IDENTIFIER 
   7 4BM6 ...      SBN   0  5
  21 4BWY ...      BZE   0  P1CC                [UNAVAILABLE(58):CRASH CLOSE THE IDEN
  20 4C6Q ...NXT1  BZE   6  RESETRRWA           [READ AGAIN IF NO INPUT LEFT AND 
  21 4CLB                                       [HAVE NOT FOUND REQD. SUPERVISORY SG 
  19 4D62 ...      ADX   3  5                   [UPDATE INPUT BUFFER MODIFIER
  19 4DKL          BRN      NXT                 [LOOK AT NEXT INPUT SEGMENT  
   7 4F26 ...TILL  SMO      1
  10 4FBL ...      LDX   0  PMADDR+1(3)  
   9 4FR6 ...      ANDX  0  SSMASK(1)
  21 4G7L ...      BXE   0  ROILL(1),PFOUND     [J IF ILLEGAL FOR END OF IPB OUTPUT  
   8 4GJ6          BRN      NXT1 
  20 4GK3 ...P1CC  LDN   0  #0127               [CONVERT THE END OF IPB OUTPUT   
  20 4GKY ...      SLL   0  12                  [SEGMENT TO A CRASH CLOSE SEGMENT
  10 4GLT ...      STO   0  PMADDR1+1(1) 
  21 4GMQ ...      SMO      1                   [TRANSFER THE IDENTIFIER NUMBER TO IT
   9 4GNM ...      LDX   0  PMADDR(3)
  10 4GPJ ...      DSA   0  PMADDR1+1(1) 
  20 4GQF ...      LDX   5  6                   [PRESERVE READ REMAINDER COUNT   
  17 4GRB ...P4    LDN   0  PMRW(1)             [SEND 'CRASH CLOSE'  
  10 4GS? ...      DCA      2,STO,0,REP1 
   9 4GT8 ...      CALL  6  REPCHEK  
  17 4GW5 ...      BRN      P4R                 [REJECTED : FORGET IT
  16 4GX2 ...      BRN      PMDIS               [INOPERABLE : DIS
  19 4GXX ...      BRN      P4                  [NOT IMP OR TIME OUT : REPEAT
  19 4GYS ...P4R   LDX   6  5                   [RESTORE READ REMAINDER COUNT
  18 4G_P ...      BRN      NXT1                [CHECK REMAINING INPUT   
   4 4H2L ...#   
   9 4H3Q ...P2R   LDX   0  READCT(1)
   9 4HHB          SBN   0  IMREPT   
  21 4J32          BZE   0  PMDIS               [GIVE UP IF TIMED OUT IMREPT TIMES   
  19 4JGL          ADN   0  IMREPT+1            [OTHERWISE ADD 1 TO COUNT OF 
  15 4K2= ...      STO   0  READCT(1)           [NO. OF READS
   6 4K=4 ...RESETRRWA   
  19 4KFW ...      LDN   0  PMRW(1)             [SET RW ADDR IN SPOUTED AREA 
  10 4K_G          DSCA     2,STO,0,REP1 
  16 4LF6          BRN      P2                  [AND TRY AGAIN   
   4 4LYQ    #   
   4 4MDB    #   
  14 4MY2    # COME HERE TO ISSUE 'CLOSE DOWN' COMMAND   
   4 4NCL    #   
   4 4NX=    #   
   5 4PBW    PFOUND  
  17 4PWG          LDN   0  #11                 [SET UP CONTROL AREA 
  16 4QB6          DCA      2,DSA,0,MODE        [FOR CLOSE DOWN  
   9 4QTQ          LDN   0  PMLEN2   
  10 4R*B          DCA      2,DSA,0,COUNT
  10 4RT2 ...      LDN   0  PMADDR2(1)   
  10 4S#L          DCA      2,STO,0,CTSA 
  21 4SDH ...      LDCT  0  #400                [SET CHAINADD IN DL IN CASE 1ST PERI 
   9 4SJD ...      ORN   0  PMCHA(1) 
  10 4SN* ...      DCA      2,STO,0,CHADD
  19 4SS= ...P3    LDN   0  PMRW(1)             [SET RW ADDR IN CURRENT AREA 
  10 4T?W          DCA      2,STO,0,REP1 
  19 4TRG          CALL  6  REPCHEK             [WRITE AND CHECK REPLY WORD  
  20 4W?6          BRN      TINP2               [TRY TO READ IF WRITE REJECTED   
  17 4WQQ          BRN      PMDIS               [DIS IF INOPERABLE   
  20 4X=B          BRN      P3                  [REPEAT IF NOT IMP. OR TIME OUT  
  16 4XQ2 ...      LDX   0  PMRW(1)             [LOOK TO SEE IF  
  16 4Y9L          ANDN  0  #777                [WRITE ENDED AND 
  14 4YP=          BZE   0  PMDIS               [J IF NOT
  20 4_8W    TINP2 CALL  6  SETRD               [TRY TO READ IF WRITE REJECTED   
  14 4_NG                                       [OR ENDED
  16 5286          CALL  6  REPCHEK             [CHECK REPLY WORD
  17 52MQ          BRN      PMDIS               [DIS IF READ REJECTED
  17 537B          BRN      PMDIS               [OR IF IPB INOPERABLE
   6 53M2          NULL  
  20 546L          BRN      PFOUND              [TRY TO WRITE AGAIN IF TIMED OUT 
  18 54L=                                       [OR NOT IMPLEMENTED OR OK
   8 555W    PMDIS DISENGPER 2   
  19 55KG ...      STOZ     READCT(1)           [ZEROISE TIMED OUT COUNT FOR 
  15 5656                                       [NEXT UNIT   
  19 56JQ          BRN      RFR2                [J TO SEE IF THERE IS ANOTHER
   5 583L    PCCLOSE 
  16 58H=          LDN   4  28                  [SET TYPE FOR IPC
  17 592W          FINDPEREC   2,APFOTY,4,PRA   [LOOK FOR IPC ENTRY  
   9 59GG ...      BNG   2  (JPMSEG) 
  17 5=_B    #     SEND 'GEORGE BROKEN' MESSAGE TO REMOTE PROCESSOR  
  15 5?F2          JDISENG  2,PCC1              [INOPERABLE  
   8 5?J6 ...      FSHCODE   AORB
   4 5?M= ...(   
   9 5?QB ...      BZE   7  PCCLOSE1 
   4 5?TG ...)   
  10 5?YL          DSCA     2,LDX,3,CHADD
   9 5#8D ...      SBN   3  CHAINADD 
  18 5#D=          JIPCM    DS,PCC1,3           [OR DIALOGUE SUSPENDED   
   5 5#N4 ...PCCLOSE1
   7 5#XW          RESETIPC 2
  16 5*X6          BPZ   0  PCC1                [RESET REJECTED  
   8 5BBQ          LDX   5  JTIME
   9 5BWB          ADN   5  5000/K9  
   9 5CB2    PCC8  JRESET   2,PCC7   
  21 5CTL          DFJUMPN  2,RGM,PCC1          [REMOTE PROCESSOR NOT UNDER GEORGE   
  11 5D*=    #     SET UP WRITE CONTROL AREA 
  15 5DSW    PCC3  LDN   0  #11                 [SET UP C.A. 
  10 5F#G          DCA      2,DSA,0,MODE 
   7 5FS6          LDN   0  3
  10 5G?Q          DCA      2,DSA,0,COUNT
  10 5GRB ...      LDN   0  PMCADDR(1)   
  10 5H?2          DCA      2,STO,0,CTSA 
   9 5HQL ...      LDN   0  PMRW(1)  
  10 5J==          DCA      2,STO,0,REP1 
   8 5JPW ...      LDCT  0  #400 
   9 5K9G ...      ORN   0  PMCHA(1) 
  10 5KP6          DCA      2,STO,0,CHADD
   4 5KPP ...#   
   8 5KQ# ...      FSHCODE  AORB 
   4 5KQX ...(   
  21 5KRG ...      BZE   7  PMSTD               [ J. IF ENTRY FOR STD. G3 BROKEN MESS
   7 5KRH ...      LDN   6  8
   4 5KRJ ...SDA 
  20 5KRK ...      LDX   0  SLAB1(1)            [TEST IF REQUD TO BREAK MASTER.  
  17 5KRL ...      BXE   0  ACES,SD1            [NO BREAK REQUIRD.   
  20 5KRM ...      BZE   0  SD2                 [BREAK MASTER REGUARDLESS OF TYPE
   9 5KRN ...      SMO      GONRDUMP 
  17 5KRP ...      LDX   0  0                   [LD. SLAVES GEOERR.  
   9 5KRQ ...      SMO      GONRDUMP 
   7 5KRR ...      LDX   3  1
   9 5KRS ...      TXU   0  SLAB1(1) 
  10 5KRT ...      TXU   3  SLAB1+1(1)   
   8 5KRW ...      BCC      SD2  
   7 5KRX ...      ADN   1  3
   8 5KRY ...      BCT   6  SDA  
   8 5KR_ ...      BRN      SD1  
   4 5KS2 ...SD2 
  10 5KS3 ...      LDX   0  SLAB1+2(1)   
  19 5KS4 ...      BRN      STONO               [PASS -2 TO MASTER MACHINE.  
   4 5KS5 ...SD1 
  21 5KS6 ...      LDX   3  FX2                 [ ELSE WE NEED TO ALTER THE MESSAGE  
  18 5KSN ...      PMCORES  3,TERFX2            [ CHECK THAT FX2 IS LEGAL
  20 5KT? ...      LDX   0  JOBNO(3)            [ GET JOB NO FOR CURRENT ACTIVITY
  20 5KTW ...      BZE   0  USEACTN             [ IF NO USER CONTEXT USE ACT NO  
  21 5KWF ...      LDCT  6  #400                [ ELSE SET BIT 0 TOSHOW JOB NO USED  
   7 5KX4 ...      ORX   0  6
  18 5KXM ...      BRN      STONO               [ J. TO PUT IN MESSAGE   
   5 5KY= ...TERFX2  
  21 5KYT ...      NGN   0  1                   [ IF FX2 ILLEGAL - SET JOB/ACT NO=-1 
  18 5K_D ...      BRN      STONO               [ J. TO PUT IN MESSAGE   
   5 5L23 ...USEACTN 
  21 5L2L ...      LDX   0  ACTNUM(3)           [ USE ACTIVITY NO. IF NO USER CONTEXT
  17 5L39 ...      BNZ   0  STONO               [ JIF NOT ACT NO. 0  
  21 5L3S ...      LDCT  6  #400                [ ELSE SET BIT 0 OF ACT NO. TO LOOK  
  21 5L4C ...      ORX   0  6                   [LIKE JOBNO(0 LOOKS LIKE 2ND MESSAGE)
   5 5L52 ...STONO   
  20 5L5K ...      STO   0  PMCADDR+1(1)        [ PUT JOB/ACT NO IN SECOND WORD  
  18 5L68 ...      ADX   0  PMCADDR(1)          [ RECALCULATE CHECKSUM   
  18 5L6R ...      NGS   0  PMCADDR+2(1)        [ AND STORE IN LAST WORD 
   5 5L7B ...PMSTD   
   4 5L7_ ...)   
  11 5L8Q    #     ISSUE PERI & CHECK REPLY  
   7 5LNB          PERI  0  2
   8 5M82          LDX   5  JTIME
   9 5MML          ADN   5  5000/K9  
   9 5N7= ...PCC2  LDX   0  PMRW(1)  
  15 5NLW          BPZ   0  PCC5                [TERMINATED  
   8 5P6G          TXL   5  JTIME
  13 5PL6 ...      BCC      PCC2                [WAIT
   8 5PQ3 ...      FSHCODE   AORB
   4 5PTY ...#SKI
   4 5P_T ...(   
  15 5Q5Q          BRN      PCC1                [TIMED-OUT   
   4 5Q8* ...)   
   8 5Q=Y ...      FSHCODE  AORB 
   4 5Q*H ...(   
   8 5QD6 ...      BRN      PCC9 
   4 5QGP ...)   
   7 5QKB ...PCC5  SLC   0  5
  15 5S4=          BPZ   0  PCC1                [INOPERABLE  
   7 5SHW          SRC   0  2
  14 5T3G          BNG   0  PCC1                [RESET   
   8 5T65 ...      FSHCODE  AORB 
   4 5T8N ...(   
  18 5T?? ...      SLC   0  1                   [JIF ALREADY IMPLEMENTED 
   8 5T*W ...      BPZ   0  PCC1 
   4 5TDF ...)   
  12 5TH6    #     CONTENTION LOST,ACCEPT INPUT  
   8 5W2Q          LDN   0  #12  
  10 5WGB          DSCA     2,DSA,0,MODE 
   8 5X22          LDN   0  11   
  10 5XFL          DSCA     2,DSA,0,COUNT
   9 5X_= ...      LDN   0  PMADDR(1)
  10 5YDW          DSCA     2,STO,0,CTSA 
   9 5YYG ...      LDN   0  PMRW(1)  
  10 5_D6          DSCA     2,STO,0,REP1 
   8 5_XQ ...      LDCT  0  #400 
   9 62CB ...      ORN   0  PMCHA(1) 
  10 62X2          DSCA     2,STO,0,CHADD
  12 63BL    #     ISSUE READ PERI & CHECK REPLY 
   8 63W=          LDX   5  JTIME
   9 64*W          ADN   5  5000/K9  
   7 64TG          PERI  0  2
   9 65*6 ...PCC4  LDX   0  PMRW(1)  
  15 65SQ          BPZ   0  PCC6                [TERMINATED  
   8 66#B          TXL   5  JTIME
  13 66S2 ...      BCC      PCC4                [WAIT
   8 66XX ...      FSHCODE   AORB
   4 673S ...#SKI
   4 677P ...(   
  15 67?L          BRN      PCC1                [TIMED-OUT   
   4 67B9 ...)   
   8 67DS ...      FSHCODE   AORB
   4 67HC ...(   
   8 67L2 ...      BRN      PCC9 
   4 67NK ...)   
   9 67R= ...PCC6  LDX   0  PMADDR(1)
   8 69=6          ANDN  0  #77  
   8 69PQ          SBN   0  #71  
  17 6=9B          BZE   0  PCC1                [INITIALISE MESSAGE  
   8 6=?5 ...      FSHCODE  AORB 
   4 6=#S ...(   
   7 6=BH ...      SBN   0  6
  17 6=D= ...      BNZ   0  PCC3                [JIF NOT ERROR CODE  
  17 6=F_ ...      LDN   0  #1000               [ ELSE CHECK WHETHER 
  18 6=HN ...      ANDX  0  PMADDR(1)           [ 'GEORGE BROKEN' MESSAGE
  19 6=KC ...      BNZ   0  PCC1                [ J. TO GIVE UP IF IT WAS !  
   4 6=M6 ...)   
   8 6=P2          BRN      PCC3 
   4 6?8L    PCC1
   4 6?N=    )   
   8 6?RB ...      FSHCODE  AORB 
   4 6?WG ...(   
  20 6?_L ...      BNZ   7  PRA                 [DONT DO 'DIS' IF NOT LAST TIME  
   4 6#4Q ...)   
   8 6#58 ...      FSHCODE   AORB
   4 6#5L ...(   
   9 6#64 ...      SMO      FXPMDR   
   9 6#6G ...      LDX   0  ERRORTYPE
   8 6#6Y ...      BNG   0  PRA  
   4 6#7B ...)   
  16 6#7W          DISENGPER   2                [DISENGAGE UNIT  
   8 6#=5 ...      FSHCODE   AORB
   4 6### ...(   
   8 6#BH ...      LDX   0  GSIGN
   9 6#DQ ...      SMO      FXPMDR   
   9 6#G_ ...      ORS   0  ERRORTYPE
   4 6#K8 ...)   
   8 6#MG          BRN      PRA  
   8 6B6B    PCC7  TXL   5  JTIME
   8 6BL2 ...      BCC      PCC8 
   8 6C5L          BRN      PCC1 
   8 6C7T ...      FSHCODE   AORB
   4 6C=4 ...(   
   4 6C#? ...PCC9
   8 6CBG ...      RESETIPC   2  
   8 6CDP ...      BRN      PCC1 
   4 6CGY ...)   
   9 6CK= ...PMCADDR        #03001077
   8 6D4W                   +0   
   9 6DJG ...               #74776701
   4 6G3B    #END
   8 ____ ...11713310001300000000
