  13 22FL ...      SEG   DUMP,860,MIKE PUTNAM,INCDUMP
   4 22_=    #   
  10 23DW          SEGENTRY K1DUMP,X1DUMP
  10 23L4 ...      SEGENTRY K2DUMP,X2DUMP
  10 23R= ...      SEGENTRY K3DUMP,X3DUMP
   4 23YG    #   
  10 24D6    #  EXAMINES A DUMP COMMAND. 
  18 24XQ    #    1) IF NO PARAMETER, SETS B0 OF INCWORD.(IN FIXED CORE).
  19 25CB    #    2) IF 1ST PARAMETER IS ^TOTAL^, SETS B0 & B1 OF INCWORD.   
  21 25X2    #    3) IF 1ST PARAMETER IS ^RETURN^ OR ^REUSE^ ,SETS UP ISSUJOB TO RETU
  21 26BL    #           OR REUSE OBSOLETE DUMP TAPES. 2ND PARAMETER OF DUMP COMMAND 
  21 26W=    #           DETERMINES 2ND PARAMETER OF RUNJOB ISSUED   , AS FOLLOWS :- 
  20 27*W    #          A) 2ND PARAMETER IS ^ALL^ , NO 2ND PARAMETER PROVIDED.   
  20 27TG    #          B) 2ND PARAM. IS A NUMBER, THIS IS PROVIDED AS 2ND PARAM.
  21 28*6    #          C) 2ND & SUBSEQUENT PARAMS. ARE MT ENTRANT DESCRIPIONS WITH  
  17 28SQ    #                  TSN SPECIFIED, THESE ARE PROVIDED.   
  21 29#B    #  COMMAND ERRORS ARE GIVEN IF THE COMMAND DOES NOT CONFORM TO THE ABOVE
   4 29S2    #   
   8 2=?L    SALL           4HALL
  18 2=R=    RET            40HRJ  RET,:DUMPER,PROCESSOR,PARAM(RETURN )  
  18 2?=W    REUSE          40HRJ  RET,:DUMPER,PROCESSOR,PARAM(REUSE  )  
   4 2?QG    #   
  18 2#=6    ZERM           +ERMCH              [PARAMETER NOT RECOGNISED
  17 2#PQ    ZPAR           +JPARMIS            [PARAMETER MISSING   
  20 2*9B    ZNEG           +JNEGNO             [NEGATIVE NO NOT ALLOWED IN %C   
  16 2*P2    ZNULL          +JPARNULL           [PARAMETER NULL  
  17 2*SX ...MZIPC          +INCIPC             [ INVALID PARAMETERS 
  18 2*YS ...#     A TABLE OF VALID PARAMETERS AND LABELS TO ANALYSE THEM
   4 2B4P ...#   
   4 2B8L    #   
   5 2BN=    START   
   9 2C7W                   8HTOTAL  
   9 2CMG ...               +ZTOTAL  
   9 2D76                   8HRETURN 
   8 2DLQ ...               +ZRET
   9 2F6B                   8HREUSE  
   8 2F=S ...               +ZREU
   9 2FC= ...MSPARE         8HSPARE  
   9 2FHN ...               +ZSPARE  
   9 2FN6 ...MPOOL          8HPOOL   
   9 2FSJ ...               +ZPOOL   
   9 2F_2 ...               8HTAPES  
   9 2G5D ...               +ZTAPES  
   8 2G9W ...               8HALL
   8 2GB# ...               +ZALL
   8 2GGQ ...               +0   
   8 2GM8 ...MSPACE         8H   
  16 2GRL ...MDUMPER        12HDUMPER           [ USER NAME AND  
  18 2GY4 ...MTNAME         12HGEO3DUMPTAPE     [ LOCAL NAME OF DUMPTAPE 
   8 2H4W                   +0   
   4 2H?M ...#   
   4 2HGD ...#   
  10 2HP9 ...#     SUBROUTINE NEXTPARAM  
   4 2HY2 ...#   
  16 2J6R ...#     THIS SUBROUTINE ANALYSES THE NEXT PARAMETER   
  13 2J*J ...#     IT EXITS +0 IF THERE ARE NO MORE  
  12 2JJ* ...#              +1 FOR A TSN  (IN X5)
  16 2JR6 ...#              +2 FOR A POSITIVE INTEGER  (IN X4)   
  15 2J_X ...#              +3 FOR ONE OF TOTAL SPARE OR POOL
  17 2K8N ...#                     (WITH RELEVANT BIT SET IN FDUCOM) 
  17 2KCF ...#              +4 FOR ONE OF RETURN,REUSE,TAPES OR ALL  
  14 2KL= ...#                     (WITH MARKER IN X5)   
  19 2KT3 ...#     FOR ANYTHING ELSE A BRANCH IS MADE TO THE RELEVANT COMERR 
   4 2L3S ...#   
  18 2L=K ...#     NEXTSN IS AN ADDITIONAL ENTRY POINT WHICH WILL ONLY   
  15 2LFB ...#     CHECK FOR A TSN AND ERROR ANYTHING ELSE   
   4 2LN7 ...#   
  15 2LWY ...#     CALLED ON X6.   USES ALL OTHERS EXCEPT X7 
   6 2M5P ...NEXTPARAM   
   7 2M#G ...      LDN   5  0
   8 2MH? ...      BRN      SPAS 
   5 2MQ4 ...NEXTSN  
   7 2MYT ...      LDN   5  1
   4 2N7L ...SPAS
   8 2NBC ...      SBX   6  FX1  
   7 2NK8 ...      SPARAPAS  
  10 2NR_ ...      MHUNT    3,CPB,CUNI   
   9 2P2Q ...      LDX   0  ANUM(3)  
  17 2P9H ...      BNG   0  XIT0                [NO MORE PARAMETERS  
  16 2PD# ...      BZE   0  XNULL                [NULL PARAMETER 
  19 2PM5 ...      BNZ   5  ZNOT2               [J IF ONLY LOOKING FOR TSN   
   8 2PTW ...      ANDN  0  #7777
   7 2Q4M ...      SBN   0  9
  16 2Q?D ...      BPZ   0  ZNOTSTRING          [J IF >8 CHARS   
   8 2QG9 ...      ADN   0  12   
   7 2QP2 ...      SRL   0  2
  19 2QXR ...      LDX   4  0                   [SAVE NO. OF WORDS IN PARAM  
   7 2R6J ...      SBN   1  3
   4 2RBG    TEST
  20 2RW6 ...      LDX   5  4                   [X3= NO. OF WORDS IN PARAMETER   
  19 2S*Q          ADN   1  3                   [X1-> NTH PARAMETER IN LIST  
  21 2STB          LDX   0  START(1)            [X0= 1ST WORD OF PARAMETER IN LIST   
  18 2T*2 ...      BZE   0  ZNOTSTRING          [J IF PARAM. NOT IN LIST 
   4 2TSL    NEXT
   7 2W#= ...      SMO      5
  18 2WRW ...      LDX   0  -1+APARA(3)         [X0= NEXT WORD OF PARAM. 
   7 2X?G ...      SMO      5
  18 2XR6          BXU   0  -1+START(1),TEST    [J IF NOT THIS PARAMETER 
  18 2Y64 ...      BCT   5  NEXT                [J IF MORE WORDS TO TEST 
  20 2YF2 ...      LDX   5  START+2(1)          [X5-> LABEL IN LIST TO BRANCH TO 
  10 2YRY ...      LDX   4  JPARNUM(3)   
  16 2_6W ...      FREECORE 3                   [FREE CPB/CUNI   
  10 2_FS ...      MHUNTW  1,FI,FDUCOM   
   8 2_N9 ...      ADX   6  FX1  
   8 2_WL ...      ADX   5  FX1  
  21 3253 ...      EXIT  5  0                   [BRANCH TO APPROPRIATE CODE TO DO RES
  21 32?F ...#     ***************************************************************** 
   4 32*N ...#   
  21 32CX ...#     THIS PARAMETER IS NOT ONE OF THE CHARACTER STRINGS , TOTAL ETC.   
  11 32G6 ...#     IT MAY BE A TSN OR NUMBER 
   4 32J* ...#   
  21 32LJ ...#     ***************************************************************   
   4 32NW    #   
   6 32PJ ...ZNOTSTRING  
   7 32PR ...      LDN   5  0
   5 32Q2 ...ZNOT2   
  12 32Q= ...      MHUNT    1,CPB,CUNI          [
  18 32QY ...      LDCH  0  APARA(1)            [ IF FIRST PARAMETER IS A
  17 32RL ...      SBN   0  #30                 [ ;(; THEN IT IS NOT 
  15 32S# ...      BZE   0  NOTNUM              [ A NUMBER   
   8 32SM ...      BNZ   5  ZIPC 
   8 32T2 ...      CHNUMCON  1   
  10 32TN ...      MHUNT    1,CPB,CUNI   
  20 32WB ...      TESTREP2 CHNUMERR,XER        [ IF PARAMETER IS NOT A NUMBER   
  12 3338 ...      MHUNT   2,CPAR,GNUMCON       [
  19 333W ...      LDX   4  A1+1(2)             [ PICK UP INTEGER IN BINARY  
  16 334J ...      FREECORE  2                  [ FREE THE BLOCK 
  19 335= ...      BNG   4  NEG                 [ JUMP IF NUMBER IS NEGATIVE 
   8 335Y ...      ADX   6  FX1  
   7 336L ...      EXIT  6  2
   4 337# ...#   
  21 3382 ...#     ***************************************************************   
  21 338N ...#                                                                   *   
  21 339B ...#     IS THE PARAMETER A VALID TSN ?                                *   
  21 33=4 ...#                                                                   *   
  21 33=Q ...#     ***************************************************************   
   5 33?D ...NOTNUM  
   7 33?R ...      LDX   3  1
  19 33#6 ...      NAMETOP  3,FILE,FNAME        [ NAME THE CPB/CUNI A FNAME  
  20 33#S ...      FNORM                        [ AND SET UP A FABSNB CONTAINING 
  16 33*G ...      MHUNT    3,FILE,FNAME        [ THE PARAMETER  
  18 33B8 ...      NAMETOP  3,CPB,CUNI          [ NAME IT A CUNI AGAIN   
  18 33BW ...      TESTREP  NAMEFORM,XER        [ ERROR - NOT RECOGNISED 
  12 33CJ ...      MHUNT    2,FILE,FABSNB       [
  20 33D= ...      LDX   4  ATYPE(2)            [ BIT IN FABSNB TO INDICATE THAT 
  17 33DY ...      ANDN  4  #2000               [ PARAMETER IS A TSN 
  18 33FL ...      SMO      A1(2)               [ MODIFIER DOWN FABSNB   
  17 33G# ...      LDX   5  A1+1(2)             [ PICK UP TSN IN X5  
  16 33H2 ...      FREECORE  2                  [ FREE THE FABSNB
  20 33HN ...      BZE   4  XER                 [ JUMP IF NOT A TSN - ERROR !!   
   8 33HT ...      ADX   6  FX1  
   7 33J2 ...      EXIT  6  1
   4 33J7 ...XIT0
   8 33J# ...      ADX   6  FX1  
   7 33JF ...      EXIT  6  0
   5 33JL ...ZTOTAL  
  14 33JR ...      BS       1,BFDTOTAL           [ TOTAL 
   7 33JY ...      EXIT  6  3
   5 33K5 ...ZSPARE  
  14 33K= ...      BS       1,BFDSPARE          [  SPARE 
   7 33KC ...      EXIT  6  3
   5 33KJ ...ZPOOL   
  14 33KP ...      BS       1,BFDPOOL           [   POOL 
   7 33KW ...      EXIT  6  3
   4 33L3 ...ZALL
   7 33L8 ...      LDN   5  0
  14 33L* ...      BS       1,BFDALL            [    ALL 
   7 33LG ...      EXIT  6  4
   5 33LM ...ZTAPES  
  15 33LS ...      NGN   5  1                   [     TAPES  
   8 33L_ ...      BRN      TRR  
   4 33M6 ...ZRET
  15 33M? ...      LDN   5  RET                 [      RETURN
   8 33MD ...      BRN      TRR  
   4 33MK ...ZREU
  15 33MQ ...      LDN   5  REUSE               [       REUSE
   4 33MX ...TRR 
  19 33N4 ...      SBN   4  2                    [TAPES,RETURN AND REUSE MUST
  18 33N9 ...      BPZ   4  ZIPC                 [ BE THE 1ST PARAMETER  
   7 33NB ...      EXIT  6  4
   4 33NH ...#   
  18 33NN ...#     ****************************************************  
   4 33NT ...#   
   4 33P2 ...#   
   9 33P7 ...#     SUBROUTINE TSNADD 
   4 33P# ...#   
  17 33PF ...#     THIS SUBROUTINE ADDS A TSN TO A FI-FDUCOM BLOCK   
   4 33PL ...#   
   4 33PR ...#   
  11 33PY ...#     CALLED ON X6.   TSN IN X5 
   4 33Q5 ...#   
   5 33Q= ...TSNADD  
   8 33QC ...      SBX   6  FX1  
  10 33QJ ...      MHUNTW   3,FI,FDUCOM  
  17 33QP ...      LDX   0  FDUTSNCT(3)         [IF TSN IS ALREADY   
  18 33QW ...      BZE   0  NOTINIT             [ IN FDUCOM - IGNORE IT  
   5 33R3 ...NEXTIN  
   7 33R8 ...      SMO      0
  12 33R* ...      BXE   5  FDUTSN-1(3),TSNXIT   
   9 33RG ...      BCT   0  NEXTIN   
   5 33RM ...NOTINIT 
  18 33RS ...      LDX   4  ALOGLEN(3)          [PICK UP LENGTH OF FDUCOM
  17 33R_ ...      ADN   4  1                   [AND INCREASE BY 1   
  12 33S6 ...      ALTLEN   3,4,FI,FDUCOM       [
  10 33S? ...      MHUNTW   3,FI,FDUCOM  
  20 33SD ...      BS       3,BFDTSN            [SHOW WE HAVE FOUND A VALID TSN  
   9 33SK ...      SMO   FDUTSNCT(3) 
  17 33SQ ...      STO   5  FDUTSN(3)           [STORE TSN IN FDUCOM 
   7 33SX ...      LDN   0  1
  19 33T4 ...      ADS   0  FDUTSNCT(3)         [ADD 1 TO THE COUNT OF TSN@S 
   5 33T9 ...TSNXIT  
   8 33TB ...      ADX   6  FX1  
   7 33TH ...      EXIT  6  0
   4 33TN ...#   
  18 33TT ...#     ***************************************************** 
   4 33W2 ...#   
   5 33W7 ...X1DUMP  
  14 33W# ...      SETNCORE FDUTSN-FDUPARAMS,1,FI,FDUCOM 
  10 33WF ...      STOZ     FDUPARAMS(1) 
  17 33WL ...      LDN   0  FDUPARAMS(1)        [SET UP FDUCOM BLOCK 
  16 33WR ...      LDN   1  FDUPARAMS+1(1)     [AND INITIALISE   
  12 33WY ...      MOVE  0  FDUTSN-FDUPARAMS-1   
   6 33X5 ...PARAMETER   
   9 33X= ...      CALL  6  NEXTPARAM
  16 33XC ...      BRN      XDUMPINIT           [NO MORE PARAMS  
  13 33XJ ...      BRN      ZTSN1               [TSN 
  15 33XP ...      BRN      ZIPC                [+VE INTEGER 
  17 33XW ...      BRN      PARAMETER           [SPARE POOL OR TOTAL 
  19 33Y3 ...      BRN      RETREU              [TAPES,RETURN,REUSE OR ALL   
   5 33Y8 ...ZTSN1   
   7 33Y* ...      FREECORE 3
   9 33YG ...      CALL  6  TSNADD   
  19 33YQ ...      BRN      PARAMETER           [ BACK TO READ NEXT PARAMETER
   4 33_D ...#   
  20 34JN ...#     THIS ENTRY POINT IS CALLED FROM INCTAPES TO RE-VALIDATE TSNS  
  12 34KB ...X3DUMP                             [
  20 34L4 ...      CALL  4  XCHECKTSNS          [ IT SIMPLY CALLS THE SUBROUTINE 
  20 34LQ ...      LDX   0  AWORK1(2)           [ AWORK1 IS SET NON ZERO IF THERE
  18 34MD ...      STO   0  ACOMMUNE1(2)        [ ARE ANY INVALID TSNS   
  12 34N6 ...      UP                           [
   4 34NS ...#   
  21 34PG ...#     ******************************************************************
  21 34Q8 ...#                                                                      *
  21 34QW ...#     THIS SUBROUTINE CHECKS THAT ALL TSNS IN THE FDUCOM ARE AVAILABLE *
  21 34RJ ...#     FOR USE BY DUMPER - THEY MUST BE IN :DUMPER,HAVE LOCAL NAME      *
  21 34S= ...#     GEO3DUMPTAPE,MUST BE EITHER NOT IN USE IN INCINDEX OR IN LAST    *
  21 34SY ...#     INCREMENT,THEY MUST NOT BE WRONGED                               *
  21 34TL ...#                                                                      *
  21 34W# ...#     ******************************************************************
   4 34X2 ...#   
   6 34XN ...XCHECKTSNS  
  18 34YB ...      STOZ     AWORK1(2)           [ CLEAR THE ERROR MARKER 
  12 34_4 ...      MHUNTW   3,FI,FDUCOM         [
  18 34_Q ...      LDX   6  FDUTSNCT(3)         [ PICK OUT NUMBER OF TSNS
  20 352D ...      BNZ   6  OPENSERIAL          [ IF NO TSNS,NO CHECKING TO DO   
  12 3536 ...      EXIT  4  0                   [
  12 353S ...OPENSERIAL                         [
  12 354G ...      SBX   4  FX1                 [
  18 3558 ...      OPENSYS  (GEOERR),SERIAL,READ [ OPEN :SYSTEM.SERIAL   
  18 355W ...      DOWN     INCTAPES,2          [ DOWN TO OPEN :DUMPER   
  12 356J ...TSTEP3                             [
  12 357= ...      MHUNTW   2,FI,FDUCOM         [
  12 357Y ...      SMO      6                   [
  17 358L ...      LDX   5  FDUTSN-1(2)         [ PICK UP NEXT TSN   
  18 359# ...      SSEARCH  MTUNAV,5,1          [ JUMP IF NOT IN SERIAL  
  21 35=2 ...      TESTNAMX  3,2(3),MDUMPER(1),MTUNAV  [ JUMP IF IT IS NOT IN DUMPER 
  12 35=N ...      LDCT  0  #30                 [
  12 35?B ...      ANDX  0  5(3)                [
  16 35#4 ...      BNZ   0  MTUNAV              [ JUMP IF WRONGED
   4 35#Q ...#   
  21 35*D ...#     ******************************************************************
  21 35B6 ...#                                                                      *
  21 35BS ...#     WE HAVE A NON-WRONGED TAPE BELONGING TO DUMPER                   *
  21 35CG ...#     CHECK WHETHER IT IS A GEO3DUMPTAPE                               *
  21 35D8 ...#                                                                      *
  21 35DW ...#     ******************************************************************
  16 35FJ ...      REWIND                       [ REWIND :DUMPER 
  12 35G= ...TSTEP1                             [
  12 35GY ...      STEP                         [
  21 35HL ...      BZE   3  MTUNAV              [ JUMP IF END OF FILE -CAN'T FIND TSN
  18 35J# ...      BWNZ     ERESN(3),TSTEP1     [ JUMP IF NOT NAME RECORD
  18 35K2 ...      BXU   5  ESERN(3),TSTEP1     [ JUMP IF NOT THIS TSN   
  18 35KN ...      JBS      MTUNAV,3,BNERASE    [ JUMP IF BEING RETURNED 
  21 35LB ...      TESTNAMX  3,ELOC1N(3),MTNAME(1),MTUNAV [JUMP IF NOT ^GEO3DUMPTAPE^
  18 35M4 ...      BWNZ     EREELN(3),MTUNAV   [ IGNORE NON-ZERO REEL NO 
   4 35MQ ...#   
  17 35ND ...#     THIS IS A :DUMPER.GEO3DUMPTAPE AND IS NOT WRONGED 
  18 35P6 ...#     HAVE TO FIND IF IT IS IN USE OR IS IN LAST INCREMENT  
   4 35PS ...#   
  19 35QG ...      JBC      TAPEOK,3,BNUSEDUMP  [ JUMP IF TAPE IS NOT IN USE 
  18 35R8 ...      WIND  2                      [ WIND TO END OF INCINDEX
  19 35RW ...      STEPAGAIN  2                 [ GET POINTER TO LAST RECORD 
  19 35SJ ...      JBS   MTUNAV,3,BINEWDTR      [ JUMP IF NEW TAPES NEEDED   
  20 35T= ...#                                  [ WE CANT USE THE TSN SPECIFIED  
  12 35TY ...      LDN   0  0                   [
  21 35WL ...      LDX   1  AMTSR(3)            [ NUMBER OF TAPES IN THIS INCREMENT  
  12 35X# ...TSTEP2                             [
  18 35Y2 ...      SMO      0                   [ MODIFIER DOWN RECORD   
  20 35YN ...      BXE   5  AMAGR(3),TAPEOK     [ JUMP IF THE TAPE WAS USED IN   
  16 35_B ...#                                  [ LAST INCREMENT 
  18 3624 ...      ADN   0  INCMAGLEN           [ INCREASE POINTER AND   
  20 362Q ...      BCT   1  TSTEP2              [ GO BACK FOR NEXT (IF ANY LEFT) 
  21 363D ...#     WE DROP THROUGH THE LOOP IF TAPE IS USED,BUT NOT IN LAST INCREMENT
   4 3646 ...#   
  21 364S ...#    ****************************************************************   
  21 365G ...#    THE TSN SPECIFIED CAN NOT BE USED BY DUMPER = GIVE ERROR MESSAGE   
  12 3668 ...MTUNAV                             [
  12 366W ...      MHUNTW   1,FI,FDUCOM         [
   8 367J ...      LDX   2  FX2  
  18 368= ...      ADX   1  6                    [ MODIFIER DOWN BLOCK   
   7 368Y ...      SBN   1  1
  18 369L ...      JBSS     MTU2,1,FDUTSNUSED     [ SHOW TAPE NOT USEABLE
  17 36=# ...      OUTPACK  5,1,TSN              [ TSN AS A PARAMETER
  21 36?2 ...      MONOUT   INCTAPEUNAV    [ MT %A NOT AVAILABLE FOR USE BY DUMPER   
   4 36?N ...MTU2
  18 36#B ...      STO   2  AWORK1(2)           [ SET ' ERROR ' MARKER   
  12 36*4 ...TAPEOK                             [
  21 36*Q ...      BCT   6  TSTEP3              [ GO BACK FOR NEXT TAPE(IF ANY LEFT) 
  16 36BD ...      CLOSETOP                     [ CLOSE DUMPER   
  18 36C6 ...      CLOSETOP                     [ CLOSE :SYSTEM.SERIAL   
  16 36CS ...      ADX   4  FX1                 [ RELATIVISE LINK
  15 36DG ...      EXIT  4  0                   [ AND EXIT   
   4 36F8 ...#   
  21 36FW ...#    *****************************************************************  
   4 36GJ ...#   
   5 36GK ...RETREU  
  14 36GL ...      BZE   5  ZIPC                [J IF ALL
  15 36GM ...      BNG   5  TAPES               [J IF TAPES  
  14 36GN ...      SETNCORE CPDATA-A1+10,2,ADATA,CREADL  
   8 36GQ ...      LDN   0  40   
  20 36GR ...      STO   0  A1(2)               [SET NO. OF CHARS. IN RJ COMMAND 
  20 36GS ...      STOZ     A1+1(2)             [CLEAR 2ND WORD OF RECORD HEADER 
   9 36GT ...      ADN   2  CPDATA   
  20 36GW ...      ADX   1  5                   [DATUMISE RUNJOB COMMAND POINTER 
  20 36GX ...      MOVE  1  10                  [MOVE RJ COMMAND READY FOR ISSUE 
  17 36GY ...      LDN   7  0                   [CLEAR MARKER WORD   
  16 36H2 ...      CALL  6 NEXTPARAM           [GET NEXT PARAM   
  14 36H4 ...      BRN      ZIPC                [NO MORE 
  13 36H6 ...      BRN      ZAPP                [TSN 
  15 36H8 ...      BRN      RETINT              [+VE INTEGER 
  17 36H= ...      BRN      ZIPC                [TOTAL,POOL OR SPARE 
  13 36H# ...      BZE   5  PROCESS             [ALL 
   8 36HB ...      BRN      ZIPC 
   5 36HD ...RETINT  
  21 36HG ...      ORX   7  GSIGN               [SET ^2ND PARAMETER IS NUMBER^ MARKER
   4 36J8 ...ZAPP
  20 36J= ...      LDEX  4  ANUM(3)             [X4= NO. OF CHARS. IN PARAMETER  
  20 36J? ...      ADN   4  1                   [PLUS 1 FOR COMMA TO BE INSERTED 
  11 36J# ...      MHUNTW   3,ADATA,CREADL   
  20 36J* ...      LDX   5  A1(3)               [X5= NO. OF CHARS. ALREADY IN RJ 
  19 36JB ...      SBN   5  1                   [WANT TO OVERWRITE FINAL ^)^ 
  21 36JC ...      ADS   4  A1(3)               [UPDATE COUNT OF CHARS. IN RJ COMMAND
  18 36JD ...      LDX   4  A1(3)               [X4=NEW COUNT OF CHARS.  
   7 36JF ...      ADN   4  3
  21 36JG ...      SRL   4  2                   [X4= NO. OF WORDS IN RJ COMMAND NOW  
   9 36JH ...      ADN   4  CPDATA-A1
  19 36JJ ...      ALTLEN   3,4,ADATA,CREADL    [LENGTHEN RJ COMMAND BLOCK   
   9 36JK ...      MHUNT 1,CPB,CUNI  
  11 36JL ...      MHUNTW   3,ADATA,CREADL   
   9 36JM ...      ADN   3  CPDATA   
  19 36JN ...      SRC   5  2                   [CHARACTERIZE COUNT OF CHARS.
  21 36JP ...      ADX   3  5                   [X3-> NEXT FREE CHAR.  IN RJ COMMAND 
   8 36JQ ...      LDN   0  #34  
  15 36JR ...      DCH   0  0(3)                [INSERT ^,^  
  18 36JS ...      BCHX  3  /                   [UPDATE CHAR./MODIFIER   
  19 36JT ...      LDEX  0  ANUM(1)             [X0= NO. OF CHARS. IN PARAM. 
   9 36JW ...      LDN   2  APARA(1) 
  21 36JX ...      SMO      0                   [MOVE PARAM. OF DUMP COMMAND TO BE   
  18 36JY ...      MVCH  2  0                   [   PARAM. OF RJ COMMAND 
   8 36J_ ...      LDN   0  #31  
  16 36K2 ...      DCH   0  0(3)                [ADD ^)^ AT END  
   5 36K3 ...PFREE   
   9 36K4 ...      MFREE    CPB,CUNI 
  18 36K5 ...      VFREE CPB,CUNI               [ONE FOR LUCK FOR FNORM  
  21 36K6 ...                                   [ (PRESUMBALY FNORM SOMETIMES LEAVES 
  18 36K7 ...                                   [ AN EXTRA CUNI AROUND)  
  20 36K8 ...      BNG   7  PROCESS             [J IF PARAM. WAS JUST A NUMBER   
  17 36K9 ...      CALL  6  NEXTSN              [PASS NEXT PARAMETER 
  15 36K= ...      BRN      PEND                [  NO MORE   
  14 36K? ...      BRN      ZAPP                [  TSN   
   4 36K# ...PEND
  16 36K* ...      FREECORE 3                   [FREE CPB/CUNI   
   5 36KD ...PROCESS 
  21 36KF ...      ISSUJOB  0                   [SET APPROPRIATE SYSTEM-ISSUED JOB GO
   5 36KG ...NDCOM   
  17 36KH ...      ENDCOM                       [END OF DUMP COMMAND 
   5 36KJ ...XFINT   
  21 36KK ...      ERROR AMISS,ERNOP            [OUTPUT MESSAGE ^SERIAL NUMBER MISSIN
   8 36KL ...      BRN      PFREE
  18 36KM ...SFINDCREADL                        [ SUBROUTINE FOR ALTLEN  
  12 36KN ...      MHUNTW   2,ADATA,CREADL      [
  12 36KP ...      EXIT  1  0                   [
   5 375* ...TAPES   
  17 37J_ ...      CALL  6  NEXTPARAM           [PASS NEXT PARAMETER 
  15 384K ...      BRN      TAPEOUT             [   NO MORE  
  14 38J9 ...      BRN      ZIPC                [   TSN  
  16 393T ...      BRN      TAPEINT             [   +VE INTEGER  
  19 39HF ...      BRN      ZIPC                     [   SPARE,POOL OR TOTAL 
  14 3=35 ...      BZE   5  TAPEOUT             [J IF ALL
   8 3=GP ...      BRN      ZIPC 
   5 3?2* ...TAPEINT 
   7 3?F_ ...      FREECORE 3
  10 3?_K ...      MHUNTW   3,FI,FDUCOM  
   9 3#F9 ...      BS       3,BFDINT 
   9 3#YT ...      STO   4  FDUNUM(3)
   5 3*DF ...TAPEOUT 
   4 4KJN ...#   
  21 4KK7 ...#  *******************************************************************  
  21 4KKL ...#  *                                                                 *  
  21 4KL5 ...#  *  THE FOLLOWING CODE IMPLEMENTS THE ^DUMP TAPES^ COMMAND         *  
  21 4KLJ ...#  *                                                                 *  
  21 4KM3 ...#  *******************************************************************  
   4 4KMG ...#   
  10 4KM_ ...      HUNTMISB  2,FI,FDUCOM 
  21 4KND ...      BNG   2  NOFDUCOM            [ JUMP IF NO ^STORED^ DUMP COMMAND   
  12 4KNX ...      LDX   3  ALOGLEN(2)          [
  19 4KPB ...      SETUPCORE  3,3,FI,FDUCOM     [ TAKE A COPY OF THE FDUCOM  
  12 4KPT ...      HUNTMISB  2,FI,FDUCOM        [
  20 4KQ# ...      BNG   2  PHDUCOMGON          [ JUMP IF IT HAS BEEN CANCELLED  
  10 4KQR ...      LDX   1  ALOGLEN(2)   
  19 4KR= ...      LDN   2  A1(2)               [ MOVE CONTENTS OF ORIGINAL  
  16 4KRP ...      LDN   3  A1(3)               [ BLOCK INTO COPY
  12 4KS8 ...      MOVE  2  0(1)                [
  12 4KSM ...      MHUNTW  2,FI,FDUCOM          [
  18 4KT6 ...      LDX   3  FDUTSNCT(2)         [ COUNT OF TSNS IN FDUCOM
  17 4KTK ...      BNZ   3  TSNPRES             [ JUMP IF ANY GIVEN  
  18 4KW4 ...      OUTNULL                      [ OTHERWISE OUTPUT NULL  
  12 4KWH ...      BRN      MTEND               [
  12 4KX2 ...TSNPRES                            [
  12 4KXF ...      ADX   3  3                   [
  21 4KXY ...      SETNCORE  AMAGSTATER(3),3,FILE,APROC [ SET UP ^EMPTY^ FILE/APROC  
  17 4KYC ...      STOZ     A1(3)                 [ AND ZEROISE IT   
  13 4KYW ...      LDN   0  A1(3)                 [  
  12 4K_* ...      LDN   1  A1+1(3)             [
  12 4K_S ...      MOVE  0  AMAGR               [
  12 4L2? ...      MHUNTW   2,FI,FDUCOM         [
  20 4L2Q ...      LDX   4  FDUTSNCT(2)         [ MOVE COUNT OF TAPES FROM FDUCOM
  15 4L39 ...      STO   4  AMTS(3)             [ TO APROC   
  12 4L3N ...NEXTTSN                            [
  19 4L47 ...      LDX   0  FDUTSN(2)           [ MOVE NEXT TSN INTO APROC   
  10 4L4L ...      STOZ     AMAGSTATE(3) 
  12 4L55 ...      STO   0  AMAG(3)             [
  16 4L5J ...      ADN   2  1                   [ UPDATE POINTERS
  19 4L63 ...      ADN   3  2                   [ AND GO BACK FOR NEXT TSN   
  12 4L6G ...      BCT   4  NEXTTSN             [
  17 4L6_ ...      OUTTSNS                      [ OUTPUT LIST OF TSNS
  12 4L7D ...MTEND                              [
  18 4L7X ...      LDN   4  MSPACE(1)           [ ADDRESS OF ^        ^  
  17 4L8B ...      MHUNTW   2,FI,FDUCOM         [ TO BE REPLACED BY  
  12 4L8T ...      JBC      NOTPOOL,2,BFDPOOL   [
  14 4L9# ...      LDN   4  MPOOL(1)            [ ^POOL^ 
   5 4L9R ...NOTPOOL 
  12 4L== ...      JBC      NOTSPARE,2,BFDSPARE  
  17 4L=P ...      LDN   4  MSPARE(1)           [ OR ^SPARE^ IF GIVEN
  12 4L?8 ...NOTSPARE                          [ 
  12 4L?M ...      LDX   2  FX2                [ 
  20 4L#6 ...      LDN   5  ACOMMUNE2(2)       [ MOVE TEXT INTO ACOMMUNE WORDS   
  12 4L#K ...      MOVE  4  2                  [ 
  18 4L*4 ...      OUTPARN  5,ACOMMUNE2        [ AND OUTPUT THE PARAMETER
  19 4L*H ...      MONOUT   INCSTORED          [ ^STORED COMMAND DUMP %B %C  
  12 4LB2 ...PHDUCOMGON                        [ 
  16 4LBF ...      MFREEW   FI,FDUCOM          [ FREE THE BLOCK  
   4 4LBY ...#   
  21 4LCC ...#  *********************************************************************
  21 4LCW ...#  *                                                                   *
  21 4LD* ...#  * THIS CODE OUTPUTS A LIST OF CURRENT DUMP TAPES                    *
  21 4LDS ...#  *                                                                   *
  21 4LF? ...#  *********************************************************************
   4 4LFQ ...#   
  12 4LG9 ...NOFDUCOM                          [ 
  21 4LGN ...      OPENINC   ,READ             [ OPEN :SYSTEM.INCINDEX IN READ MODE  
  18 4LH7 ...      WIND                        [ WIND TO END OF THE FILE 
  19 4LHL ...      STEPAGAIN                   [ GET POINTER TO LAST RECORD  
  20 4LJ5 ...      JBS      NOCURRENT,3,BINEWDTR [ JUMP IF NEW TAPES ARE NEEDED  
   9 4LJJ ...      LDX   0  AMTSR(3) 
   5 4LK3 ...ZCURRENT
   9 4LKG ...     LDX   1  AMAGR(3)  
   9 4LK_ ...      BNG   1  NOCURRENT
   9 4LLD ...      ADN   3  INCMAGLEN
   9 4LLX ...      BCT   0  ZCURRENT 
  20 4LMB ...      READAGAIN                   [ OTHERWISE GET COPY OF RECORD FOR
  19 4LMT ...      MHUNTW   1,FILE,FRB         [ LAST INCREMENT AND NAME IT  
  18 4LN# ...      NAME  1,FILE,APROC          [ A FILE/APROC FOR USE BY 
  19 4LNR ...      OUTTSNS                     [ MACRO TO GIVE A LIST OF TSNS
  18 4LP= ...      MONOUT   INCCURTAPES        [ ^CURRENT DUMP TAPE%A %B^
  16 4LPP ...      MFREEW   FILE,APROC         [ FREE BLOCK AND  
  12 4LQ8 ...NOCURRENT                         [ 
  18 4LQM ...      CLOSETOP                    [ CLOSE :SYSTEM.INCINDEX  
   4 4LR6 ...#   
  21 4LRK ...# **********************************************************************
  21 4LS4 ...# *                                                                    *
  21 4LSH ...# *                CODE TO OUTPUT ^SPARE DUMP TAPES^                   *
  21 4LT2 ...# *                                                                    *
  21 4LTF ...# **********************************************************************
   4 4LTY ...#   
  21 4LWC ...      DOWN     INCTAPES,4         [ DOWN TO SET UP LIST OF SPARE TAPES  
  17 4LWW ...      MHUNTW   3,FI,FDUMPTAPES    [ IN A FI/FDUMPTAPES  
  12 4LX* ...      LDX   2  ALOGLEN(3)         [ 
  21 4LXS ...      BNZ  2   SPARETAPES         [ JUMP IF THERE ARE ANY SPARE TAPES   
   8 4LY? ...      LDX   2  FX2  
  18 4LYQ ...      MONOUT   INCNOSPARE          [ ^NO SPARE DUMP TAPES ^ 
   9 4L_9 ...      BRN      NOSPARE  
  18 4L_N ...SFINDPROC                          [ SUBROUTINE FOR ALTLENG 
  10 4M27 ...      MHUNTW   2,FILE,APROC 
   7 4M2L ...      EXIT  1  0
   4 4M35 ...#   
  20 4M3J ...#     ************************************************************  
   4 4M43 ...#   
   6 4M4G ...SPARETAPES  
   4 4M4_ ...#   
  21 4M5D ...#  *********************************************************************
  21 4M5X ...#  *                                                                   *
  21 4M6B ...#  *      NOW CONVERT THE LIST OF SPARE TAPES INTO A FILE/APROC        *
  21 4M6T ...#  *                   FOR EASE OF OUTPUTTING                          *
  21 4M7# ...#  *                                                                   *
  21 4M7R ...#  *********************************************************************
   4 4M8= ...#   
  19 4M8P ...      SETNCORE  AMAGSTATER,2,FILE,APROC  [ APROC TO CONTAIN TSNS
  17 4M98 ...      LDN   6  0                   [ INITIALISE COUNTERS
  12 4M9M ...      LDN   7  0                   [
  18 4M=6 ...      STOZ     AMTS(2)             [ ZEROISE COUNT OF TAPES 
  12 4M=K ...      MHUNTW   1,FI,FDUCOM         [
  20 4M?4 ...      LDN   4  10                  [ DEFAULT NUMBER OF TAPES = 10   
  18 4M?H ...      JBC      NONUMBER,1,BFDINT   [ JUMP IF NO NUMBER GIVEN
  19 4M#2 ...      LDX   4  FDUNUM(1)           [ PICK UP NUMBER PARAMETER   
   9 4M#F ...      BZE   4  NOSPARE  
   5 4M#Y ...NONUMBER
  12 4M*C ...      JBC      NOTALL,1,BFDALL     [
  21 4M*W ...      LDN   4  4095                [ IF ^ALL^ SPECIFIED GIVE 4095 TAPES 
  12 4MB* ...NOTALL                             [
  18 4MBS ...      OPENSYS  (GEOERR),SERIAL,READ [ OPEN :SYSTEM.SERIAL   
  12 4MC? ...NEXTTAPE                           [
  12 4MCQ ...      MHUNTW   2,FILE,APROC        [
  19 4MD9 ...      LDX   3  ALOGLEN(2)          [ INCREASE LENGTH OFAPROC BY 
  20 4MDN ...      ADN   3  INCMAGLEN           [ LENGTH OF ENTRY FOR ONE TAPE   
  12 4MF7 ...      ALTLENG  2,3,SFINDPROC       [
  12 4MFL ...      MHUNTW   1,FI,FDUMPTAPES     [
  12 4MG5 ...      MHUNTW   2,FILE,APROC        [
  18 4MGJ ...      SMO      6                   [ PICK UP NEXT SPARE TSN 
  12 4MH3 ...      LDX   5  FDTTAPE(1)          [
  18 4MHG ...      SSEARCH  MTGONE,5            [ LOOK FOR IT IN SERIAL  
  12 4MH_ ...      MHUNTW   2,FILE,APROC        [
  12 4MJD ...      LDN   0  1                   [
  18 4MJX ...      ADS   0  AMTS(2)             [ INCREASE COUNT IN APROC
  12 4MKB ...      LDCT  0  #30                 [
  18 4MKT ...      ANDX  0  5(3)                [ PICK UP ^WRONGED^ BITS 
  16 4ML# ...      BNZ   0  TAPEWRONGED         [ JUMP IF WRONGED
  20 4MLR ...      SBN   4  1                   [ DECREASE NUMBER OF TAPES WANTED
  12 4MM= ...TAPEWRONGED                        [
  21 4MMP ...      SLL   0  1                   [ SHIFT ^WRONG^ BITS TO CORRESPONDING
  21 4MN8 ...      ADX   2  7                   [ POSITION IN MAGSTATE WORD IN INCIND
  12 4MNM ...      STO   0  AMAGSTATE(2)        [
  15 4MP6 ...      STO   5  AMAG(2)             [ STORE TSN  
  19 4MPK ...      ADN   7  INCMAGLEN           [ UPDATE POINTER FOR APROC   
  12 4MQ4 ...MTGONE                             [
  15 4MQH ...      ADN   6  1                   [ AND FDUCOM 
  21 4MR2 ...      BZE   4  MTFIN               [ JUMP IF WE HAVE ALL THE TAPES NEEDE
  11 4MRF ...      MHUNTW   1,FI,FDUMPTAPES  
  12 4MRY ...      BXL   6  ALOGLEN(1),NEXTTAPE [
  12 4MSC ...MTFIN                              [
  12 4MSW ...      CLOSETOP                     [
  12 4MT* ...      OUTTSNS                      [
  12 4MTS ...      MFREEW   FILE,APROC          [
  12 4MW? ...OUTSPARE                           [
  12 4MWQ ...      MONOUT   INCSPARE            [
   5 4MX9 ...NOSPARE 
  12 4MXN ...      MFREEW   FI,FDUMPTAPES       [
  12 4MY7 ...      MFREEW  FI,FDUCOM            [
   6 4MYL ...      ENDCOM
   4 4MYR ...#   
  18 4MYY ...#     ***************************************************** 
  12 4M_5 ...#     ACTUAL DUMPER INITIATING CODE 
  18 4M_# ...#     ***************************************************** 
   6 4M_H ...XDUMPINIT   
  10 4M_Q ...      MHUNTW   1,FI,FDUCOM  
  13 4N23 ...      JMBAS    ZIPC,1,BFDPOOL,BFDSPARE  
  11 4N2_ ...      JBC      NOTOPN,1,BFDTSN  
   9 4N3D ...      OPENINC   ,READ   
  10 4N3X ...      CALL  4  XCHECKTSNS   
   7 4N4B ...      CLOSETOP  
   9 4N4T ...      LDX   0  AWORK1(2)
   9 4N5# ...      BNZ   0  ZMTUNAV  
   5 4N5R ...NOTOPN  
  10 4N6= ...      HUNTMISB  2,FI,FDUCOM 
   9 4N6P ...      BNG   2  NOTHERE  
   8 4N78 ...      FREECORE  2   
   5 4N7M ...NOTHERE 
  10 4N86 ...      MHUNTW   1,FI,FDUCOM  
   9 4N8K ...      CHAIN    1,BMISC+1
   9 4N94 ...      BS        ,BDCISS 
  10 4N9H ...      LONGON      ADUMPWAIT 
   6 4N=2 ...      ENDCOM
   5 4N=F ...ZMTUNAV 
   9 4N=Y ...      MFREEW   FI,FDUCOM
   6 4N?C ...      ENDCOM
   8 4N?W ...ZIPC  LDX   1  FX1  
   9 4N#* ...      LDX   0  MZIPC(1) 
   8 4N#S ...      BRN      XCOM 
   4 4NCL    XER 
   8 4NX=          SMO      FX1  
  18 4PBW          LDX   0  ZERM                [PARAMETER NOT RECOGNIZED
   8 4PWG          BRN      XCOM 
  21 4QB6    NEG   LDX   0  ZNEG(1)             [NEGATIVE NUMBER NOT ALLOWED IN %C   
   8 4QTQ          BRN      XCOM 
  16 4R*B    XNULL LDX   0  ZNULL(1)            [PARAMETER NULL  
   8 4RT2          BRN      XCOM 
   5 4S#L    TWOERR  
  21 4SS=          LDX   0  ZPAR(1)             [NO VALID 2ND OR SUBSEQUENT PARAM. (I
  21 4T?W                                       [   RETURN OR REUSE IS 1ST PARAM.)   
   4 4TRG    XCOM
  18 4W?6          COMERRX  0                   [GIVE APPROPRIATE COMERR 
   4 4WQQ    #   
   5 4WRM ...X2DUMP  
   4 4WSJ ...#   
  21 4WTF ...#     ##################################################################
   4 4WWB ...#   
  16 4WX? ...#     THIS CODE IMPLEMENTS THE CANCEL DUMP COMMAND  
   4 4WY8 ...#   
   9 4W_5 ...      BC      ,BDCISS   
  19 4X22 ...      BS        ,BDUMPAB             [ SET ABANDON DUMP MARKER  
  17 4X2X ...      LONGON  FGOTWAIT  [ WAKE DUMPER IF WAITING FOR DTS
  10 4X3S ...      HUNTMISB   1,FI,FDUCOM
  18 4X4P ...      BNG   1  ZNOFREE1                 [ FREE FDUCOM IF ANY
   8 4X5L ...      FREECORE   1  
   4 4X64 ...ZNOF
  18 4X6H ...      MONOUT   INCCAN     [ SSTORED DUMP COMMAND CANCELLED  
   6 4X7D ...      ENDCOM
   5 4X7F ...ZNOFREE1
  11 4X7G ...      JBC      ZNOFREE,,BNOWD   
   9 4X7H ...      BC       ,BNOWD   
   8 4X7J ...      BRN      ZNOF 
   5 4X7M ...ZNOFREE 
  19 4X7W ...      JBS      NDCOM,,BCANAB       [ J IF NO CANCELLABLE DUMP   
  19 4X85 ...      COMERR   INCCANTCAN          [ ERROR NO CANCELLABLE DUMP  
   4 4X8* ...#   
  21 4X9= ...#     ##################################################################
  10 4X=B          MENDAREA 20,K99DUMP   
   4 4XQ2    #   
   4 4Y9L    #END
   8 ____ ...23511646000700000000
