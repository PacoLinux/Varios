   8 22FL    #LIS  K0PLUGIN>K0LLS
  17 22_= ...#SEG  PLUGIN                       [OWEN N. JOHN (:CENT)
   4 2357 ...[   
  15 2394 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   4 23#_ ...[   
   7 23DW          8HPLUGIN  
   4 23YG    #   
  18 24D6    #     THIS SEGMENT'S ONLY FUNCTION IS TO PLUG IN A PROGRAM  
   4 24XQ    #   
  10 25CB    #     THE ENTRY POINTS ARE :
   4 25X2    #   
   4 26BL    #   
  19 26W=          SEGENTRY K1PLUGIN,XX1P       [FROM RUNPROG - PROGRAM S/I  
  16 27*W          SEGENTRY K2PLUGIN,XX2P       [PCAPLUG ENTRY   
  20 27TG          SEGENTRY K3PLUGIN,XX3P       [FROM CORESHED - PROGRAM WAS S/O 
  18 28*6    P12   COMPROLINK                   ['PROGBRKIN' TYPE LINK   
   8 28SQ    ZAYT           +8   
  21 29#B    SMASK          #40777000           [MASK FOR CLEARING SOME BITS IN ASTOP
  17 29S2    # S/R TO SET TIME SLOT: LINK X7,EXPECTS X3 -> JOBBLOCK  
   4 2=?L    #   
   5 2=R=    TIMESLOT
   9 2?=W          LDX   5  JCSIZE(3)
   8 2?QG          ADN   5  1023 
  15 2#=6          SRL   5  10                  [SIZE IN K   
  19 2#PQ          BSOFF    CPIBIT,USESIZE      [J IF USING SIZE FOR TIMESLOT
   8 2*9B          LDN   5  100  
  17 2*P2          MPY   5  JCPI(3)             [OTHERWISE USE CPI   
   8 2B8L          BNZ   5  NONZ 
   7 2BN=          LDN   5  1
   4 2C7W    NONZ
   5 2CMG    USESIZE 
   9 2D76          MPY   5  ASLOTTIME
  16 2DLQ          STO   6  ATIMES(3)           [SET IN JOB BLOCK
   9 2F6B          STOZ     HTIMES(3)
   9 2FL2          STOZ     HMSLOT(3)
   7 2G5L          EXIT  7  0
   4 2GK=    [   
   4 2H4W    [   
  13 2HJG    [ SUBROUTINE TO RING MEL ON PROGRAM RING
   4 2J46    [   
   4 2JHQ    [   
   5 2JM_ ...PGEO2   
  16 2JPB ...      LDX   0  GSIGN               [SET B0 OF ATU1  
  19 2JQR ...      SMO      6                   [TO INDICATE PROGRAM HAS BEEN
  19 2JS8 ...      ORS   0  ATU1                [PLUGGED IN.USED BY CHARGESC 
   7 2JTK ...      MELRING 6 
  11 2KH2          TRACE    JOBNO(2),PLUGGDIN
   7 2L2L          EXIT  4  0
   7 2LG=    XFCA  FCAPCA   2
   7 2L_W          EXIT  6  0
  11 2MFG    [  S/R TO FIND J VIA JOB RING   
   4 2M_6    [   
   8 2NDQ    TJOB  FJOPCA   3,FX2
   7 2NYB          EXIT  6  0
   4 2PD2    [   
   4 2PXL    [   
  18 2QC=    [ SUBROUTINE TO CLEAR TOO BIG MARKERS I.E. RETURN SUCH JOBS 
  12 2QWW    [   TO CORE SCHEDULER CONSIDERATION 
   4 2RBG    [   
   4 2RW6    #   
   5 2S*Q    TCLEAR  
   9 2SGD ...      LDX   0  BIGPROGCP
  18 2SJ2 ...      BZE   0  (6)                 [EXIT IF NO BIG PROG'S   
   9 2SKJ ...      STOZ     BIGPROGCP
   8 2TSL          LDN   3  BJOBQ
   9 2W#=    NN    LDX   3  FPTR(3)  
   9 2WRW          BXE   3  CXJO,(6) 
  19 2X?G          BC       3,JBBIG             [CLEAR ANY TOO BIG MARKERS   
   8 2XR6          BRN      NN   
   4 2Y=Q    #   
  19 2YQB    #     PUT ALL MELS OF JOB ON TIMESHARER'S LIST USING MELRING.   
  21 2_=2    #     ON ENTRY, X3 CONTAINS THE ADDRESS OF THE RELEVANT JOB BLOCK. LINK 
  21 2_PL    #     ON EXIT, X1=FX1,X2=FX2,X3 IS UNCHANGED,X0 AND X4,X5,X6 ARE DESTROY
   9 329=    #     NO COORDINATION.  
   4 32NW    #   
   5 338G    SPLUGIN1
   7 33N6          LDX   5  1
  10 347Q          PCATEST  1,NORM,TP,PUC
   8 34MB    TP    FINDPUC  1,6  
  19 3572          CALL  4  RCTP                [SORT OUT WORD 31 PROBLEMS   
   8 35LL          BRN      PUC1 
  16 366=    PUC   LDX   6  1                   [X6 ALWAYS -> PUC
  16 36KW          FINDTP   1,5                 [X5 ALWAYS -> TP 
   8 375G          CALL  4  RCTP1
  19 37K6    PUC1  CALL  4  PGEO2               [RING PUC ON TIMESHARER RING 
   8 384Q          LDBIT    14,0 
   7 38JB          SMO      6
  17 3942          STO   0  ASTOP               [SET PUCS STOP WORD  
  17 39HL    NORM  LDX   6  5                   [X6 -> NORMAL/TP PCA 
   8 3=3=          CALL  4  PGEO2
  19 3?G6          JBCC     (7),3,JBREON        [J UNLESS REALTIME REQUESTED 
   7 3?_Q          LDX   5  1
  18 3#FB          BS       3,JBREEF            [MARK REALTIME EFFECTED  
   7 3#_2          SMO      6
   9 3*DL          LDX   2  ADATUM   
   6 3*HQ ...#UNS CA1D   
  17 3*LW ...      SBX   2  CA1D                [X2 -> AOBJPROG BLOCK
   6 3*Q2 ...#UNS CA1D   
   4 3*T6 ...#SKI
  17 3*Y=          SBN   2  A1D                 [X2 -> AOBJPROG BLOCK
  10 3BCW          LDX   0  ALOGLEN(2)   
  20 3BXG          ADS   0  COBJUSRT            [UPDATE CORE FOR REALTIME PROGS  
   8 3CC6          LDX   0  GSIGN
   9 3CWQ          ORS   0  JRETI(2) 
   8 3DBB          LDX   1  FX1  
   7 3F*L          EXIT  7  0
   4 3FT=    #   
  10 3G#W    #     S/R FOR RCTP PROBLEMS 
  20 3GGP ...#           TP ENTERS AT RCTP, PUC AT RCTP1,X5-> TP,X6 ->PUC,LINK X4
   4 3GNJ ...[   
  19 3GWC ...[         THE FOLLOWING CORRECTS B3068 - WHICH RESULTED INTHE   
  16 3H4= ...[           PUC BEING INCORRECTLY RINGED RE JOBBLOCK
  16 3H=5 ...[                 DONE  19TH  JUNE  1975       :CENT
   4 3HCY ...[   
  16 3HKR ...RCTP1 RINGTP   6                   [ENRING TP.PCA   
   7 3HRQ    RCTP  LDX   1  5
   9 3J?B          LINKSET  1,GVFN   
  15 3JR2          LINKSET  6,GVFN              [SET LINKS   
  16 3K=L          MACCS    ,1,2                [X2 -> TP DATUM  
   7 3KQ=          SMO      6
   8 3L9W          LDX   0  ANM  
  15 3LPG          ANDN  0  3                   [PUC MEMB NO 
   7 3M96          SLL   0  6
   8 3MNQ          ORS   0  31(2)
  16 3N8B          MACCS    ,6,2                [X2 -> PUC DATUM 
  14 3NN2 ...      BXE   5  FX2,(4)     [J IF TP CUURENT 
   8 3PSD ...      LDX   0  GMSK1
  15 3P_L ...      ANDS   0  9(2)         [CLEAR MODE BITS   
   7 3Q6W          SMO      6
  16 3QLG          LDCH  0  ALMT                [X0 HAS PUC MODES
   7 3R66          SRC   0  8
  17 3RKQ          ORS   0  9(2)                [SET MODE BITS IN PUC
   7 3T4L          EXIT  4  0
   4 3TJ=    #   
   4 3W3W    #   
  21 3WHG    #     SUBROUTINE TO FIND BLOCK OF TYPE IN X4 SEARCHING FROM X5,RESULT   
  10 3X36    #           IN X2,LINK X1   
   4 3XGQ    #   
   7 3Y2B    RFIND LDX   2  5
   9 3YG2    RFIN1 LDX   2  FPTR(2)  
   9 3Y_L          LDX   0  ATYPE(2) 
   8 3_F=          SRL   0  12   
   9 3_YW          BXU   0  4,RFIN1  
   7 42DG          EXIT  1  0
   4 42Y6    #   
  17 43CQ    #     SUBROUTINE TO LOCK BLOCK POINTED TO BY X2,LINK X6 
   4 43XB    #   
   8 44C2    XLOCK SBX   6  FX1  
   9 44WL          LOCKC    2,RFIND  
   5 45B=    XLOCK1  
  17 45TW          CALL  1  RFIND               [GET POINTERS AGAIN  
   7 46*G          LDX   1  5
   9 46T6          LDCH  0  ATYPE(1) 
   9 47#Q          SBN   0  APET/64  
  16 47SB          BZE   0  XIT                 [J IF X1 -> APET 
  15 48#2          LDX   1  APETADDR(1)         [X1 ->APET   
   8 48RL    XIT   ADX   6  FX1  
   7 49?=          EXIT  6  0
  14 49QW    #     SUBROUTINE TO LOCK BLOCKS FOR PLUGIN  
   4 4==G    #   
   4 4=Q6    #   
   5 4?9Q    SPLUGIN2
   8 4?PB          SBX   7  FX1  
   7 4#92          LDX   5  1
  11 4#NL          LDN   4  APERI+APERIRES   
  15 4*8=          CALL  1  RFIND               [X2 -> PRB   
  20 4*MW          CALL  6  XLOCK               [LOCK PRB,X2 ->PRB,X5=X1 ->APET  
   8 4B7G          LDN   4  A1(2)
  10 4BM6          PCATEST  1,NORMAL,TP1 
  19 4C6Q          FINDTP   1,1                 [X1 -1TP/PCA FOR X5 -> PUC   
   8 4CLB          BRN      PUC3 
  17 4D62    TP1   FINDPUC  1,5                 [X5 -> PUC,X1 ->TP   
   7 4DKL    PUC3  SMO      5
  19 4F5=          ADS   4  APRB                [MAKE PUCS PRB PTR ABSOLUTE  
   5 4FJW    NORMAL  
  17 4G4G          LDX   5  1                   [X5 -> NORMAL/TP PCA 
   9 4GJ6          STO   4  APRB(1)  
  16 4H3Q          LDX   3  4                   [X3 -> DOWN PRB  
   8 4HHB          LDN   4  AOLPT
   8 4J32    XLOOP LDXC  0  0(3) 
  16 4JGL          BCS      PEND                [J IF END OF PRB 
   8 4K2=          LDXC  0  1(3) 
  15 4KFW          BCC      XLOOP1              [J IF ONLINE 
  16 4K_G          CALL  1  RFIND               [X2 -> NEXT OLPA 
   7 4L6N ...#UNS  CAFSINT   
  21 4L?W ...      JCAFSBSY 2,XNOLOCK           [DON'T LOCK OLPA IF CAFS ORDERS O/S  
   8 4LF6          CALL  6  XLOCK
   7 4LL# ...#UNS  CAFSINT   
   4 4LMK ...(   
   9 4LNW ...      BRN      XLOCKED  
   4 4LQ7 ...#   
   5 4LRG ...XNOLOCK 
  21 4LS4 ...      LDX   1  5                   [WHEN WE DON'T LOCK THE OLPA, WE HAVE
  21 4LSL ...      LDCH  0  ATYPE(1)            [TO RECONSTRUCT THE POINTER TO THE   
  21 4LT8 ...      SBN   0  APET/64             [APET WHICH IS CORRUPTED BY CALLING  
  21 4LTQ ...      BZE   0  XLOCKED             ['RFIND' & NORMALLY RESET BY 'XLOCK' 
  10 4LW# ...      LDX   1  APETADDR(1)  
   4 4LWW ...#   
   5 4LXD ...XLOCKED 
   4 4LY2 ...)   
  10 4LYQ          LDN   0  CHAINADD(2)  
   8 4MDB          ORX   0  GSIGN
  18 4MY2          STO   0  1(3)                [CHAINING ADDRESS TO PRB 
  10 4NCL          STO   1  APETADDR(2)  
  16 4NX=          LDX   5  2                   [X5 -> LAST OLPA 
   5 4PBW    XLOOP1  
  16 4PWG          BDX   3  XLOOP               [BACK FOR NEXT   
   4 4QB6    PEND
   8 4QTQ          ADX   7  FX1  
   8 4R*B          LDX   1  FX1  
   8 4RT2          LDX   2  FX2  
  16 4S#L          CALL  6  TJOB                [X3 -> JOBBLOCK  
  15 4SS=          BC       3,JBPLUGGING        [ALL SAFE NOW
  11 4T?W          MBC      3,JBBEING,JBSUSP 
  17 4TRG          MBS      3,JBSI,JBPLUG,JBRUN [SET JOBBLOCK MARKERS
  16 4W?6          JBC      (7),3,JBRCTP        [EXIT IF NOT RCTP
  20 4WQQ          JBC      (7),3,JBREEF        [EXIT IF NOT REALTIME EFFECTED   
   8 4X=B          FPCAJO   1,3  
  16 4XQ2          BXU   2  1,PUCCURRENT        [J IF PUC CURRENT
   8 4Y9L          FINDPUC  1,6  
  16 4YP=          LDX   5  1                   [X5-> TP,X6->PUC 
   8 4_8W          CALL  4  RCTP 
   8 4_NG          BRN      PUCC1
   6 5286    PUCCURRENT  
  14 52MQ          LDX   5  1                   [X5 -> TP
  15 537B          LDX   6  2                   [X6 -> PUC   
   8 53M2          CALL  4  RCTP1
   5 546L    PUCC1   
   8 54L=          LDN   0  #1000
   7 555W          SMO      6
  18 558N ...      STO   0  ASTOP               [SET B14 OF PUC STOP WORD
   4 55?G ...[   
  18 55B# ...[        THIS CURES B2925 - GEOERR TWOTRANS WHEREBY PROGRAMS
  20 55F6 ...[      WITH A HIGHER PRIORITY THAN GEORGE HAVE THERE ASTOP CLEARED  
  17 55HY ...[       BECAUSE  ON LEAVING THIS ROUTINE  X1 .NE. FX1   
  15 55LQ ...[            DONE   3RD  JUNE  75          :CENT
   4 55PJ ...[   
   8 55SB ...      LDX   1  FX1  
   8 55X8 ...      LDX   2  FX2  
   4 5622 ...[   
   7 5656          EXIT  7  0
   4 56JQ    #   
   4 574B    XX3P
   7 57J2          LDN   0  0
   8 583L          BRN      XJOYN
   4 58H=    XX1P
   7 592W          LDN   0  1
  21 59GG ...XJOYN STO   0  AWORK2(2)             [CLEARS B0170 - ACOMMUNE1 OVERWRITT
  13 5=26    # ENTRY IN PCA : TO PLUGIN AFTER SWAPIN 
  15 5=FQ    #     FIRST WE OUTPUT MESSAGES IF APPROPRIATE   
   2 5=_B
   9 5?F2          CALL  6  TCLEAR   
   8 5?YL          CALL  6  TJOB 
   7 5#D=          COBJUSE 5 
  21 5#SH ...      JBC NOTRE,,ASWREALBAD         [J IF RE FRAG MESS.NOT BEEN OUTPUT  
  20 5#YL ...      BXL   5  COBJQUOTA,NOCAN     [IF WE ARE MEETING OQ,THEN J TO  
  21 5*4P ...      BC ,ASWREALBAD               [ QUOTAMET TO OUTPUT CLEAR MESSAGE   
   9 5*8S ...      BRN      QUOTAMET 
   5 5*#X ...NOTRE   
   4 5*K5 ...[   
  21 5*P8 ...[     NOW ADD TO TOTAL IN USE FOR O/P THE AMOUNT OF FREE/FREEABLE CORE  
   4 5*T? ...[   
   8 5*_B ...      LDX   0  ACHAP
   9 5B5F ...      SBX   0  CHAPQUOTA
   9 5B9J ...      BNG   0  NOCHAP   
  20 5B*M ...      ADX   5  0                   [ADD IN CHAPTERS OVER CHAPQUOTA  
   5 5BFQ ...NOCHAP  
   7 5BKT ...      COBJUNUSE 
  19 5BPY ...      ADX   5  0                   [ +FREE PROGRAM BLOCKS/PAGES 
  15 5BW3 ...      ADX   5  CFREE               [ + FREE CORE
  19 5C26 ...      SBX   5  FREZTOT             [ - FROZEN BLOCKS APART FROM 
  14 5C69 ...      ADX   5  FREZFREE            [ FREE   
  14 5C=# ...      ADX   5  FREZCHAP            [ & CHAP 
  18 5CBC ...      BXL   5  COBJQUOTA,NOCAN     [J IF WE CANNOT MEET OQ  
  21 5CGG ...      JBCC NODEL,,ASWOQMISS        [J IF MESS.NOT OUTPUT TO CLEAR DELAY 
   5 5CLK ...QUOTAMET
  20 5CQN ...      MONOUT CANQUOT               [OUTPUT THAT QUOTA CAN NOW BE MET
   8 5CWR ...      CALL  6  TJOB 
  21 5D2W ...      BRN      NOCAN               [CAN'T STOZ DELAY IN CASE RE FRAG.   
   5 5D6_ ...NODEL   
  20 5D?4 ...      STOZ     CLLSUNOQ            [MAKE SURE NO DELAY IN PROGRESS  
   4 5DC7 ...[   
  21 5DH= ...[     NOW SEE IF CLEARANCE MESSAGES ARE NECESSARY FOR TOO BIG MESSAGES  
  14 5DM* ...[     WHICH HAVE BEEN/ARE WAITING FOR OUTPUT
   4 5DRD ...[   
   5 5DXH ...NOCAN   
  16 5F3L ...      BC 3,JBSIFAIL                [CLEAR BIT IF SET
  20 5F7P ...      JBCC     NOOK,3,JBBIGMESS    [J IF NO MESS OUTSTANDING 4 CLEAR
  15 5F#G                                       [OTHERWISE   
  11 5FS6          OUTPACK  JOBNUM(3),1,NUMA 
   9 5G?Q          MONOUT   APROGOK  
   8 5GRB          CALL  6  TJOB 
   9 5H?2    NOOK  BC       3,JBBEING
   4 5HQL    #   
  16 5J==    # NOW CHECK IF THIS IS SWAPIN AFTER SPECIAL SWAPOUT 
  18 5JPW    # IF SO WE MUST FIND WHICH ACTIVITY WAS WAITING FOR CORE AND
   9 5K9G    # FPUT IT TO TRY AGAIN  
  16 5KP6    # AS WE ARE IN PCA,WE MOVE CJACCLINK OVER OURSELVES 
   4 5L8Q    #   
  14 5LNB          BS       3,JBSI              [MARK S/I
  21 5M82          JMBAC    NSPEC,3,JBPISO,JBSTSO  [J IF NOT SPEC S/O & CLEAR IF WAS 
  16 5MML          JBCC     STOPPED,3,JBPISO    [J IF WAS STOPPED
  17 5N7=          LDX   1  2                   [LEAVING PI BIT CLEAR
   8 5NLW          SMO      FX2  
   9 5P6G          STO   1  AWORK1   
  15 5PL6          CALL  7  SPLUGIN2            [LOCK BLOCKS 
   8 5Q5Q          SMO      FX2  
  15 5QKB          LDX   1  AWORK1              [X1 -> PCA   
  12 5R52          PCATEST  1,NORMAL1,TPS,PUCS   
   8 5RJL    TPS   FINDPUC  1,1  
   7 5S4=    PUCS  LDX   6  1
  15 5SHW          CALL  4  PGEO2               [ENRING PUC  
   8 5T3G          FINDTP   1,1  
   5 5TH6    NORMAL1 
   7 5W2Q          LDX   6  1
  16 5WGB          CALL  4  PGEO2               [ENRING NORMAL/TP
   7 5X22          LDX   2  6
   8 5XFL          BRN      MEET 
   5 5X_=    STOPPED 
   7 5YDW          LDX   6  2
  16 5YYG          MBI      3,JBSUSP,JBSTSO,JBSTOP  [MARK STOPPED
  21 5_D6    MEET                               [NOW FIND WHICH ACTIVITY WAS WAITING 
  14 5_XQ ...      PCATEST  2,NORMAL2,NORMAL2  [FOR CORD 
   8 63BL          FINDTP   2,2  
   5 63W=    NORMAL2 
   9 64*W          LDN   4  CJACCLINK
   9 64TG          LDN   5  ACC3(2)  
   8 65*6          MOVE  4  ALINK
   8 65SQ          ADN   4  ALINK
  10 66#B          LDN   5  ACOMMUNE1(2) 
   7 66S2          MOVE  4  9
   7 67?L          ADN   4  9
   9 67R=          LDN   5  CLONG1(2)
  19 68=W          MOVE  4  1                   [RESTORE PCA INFO FROM FIXED 
  21 68QG          STOZ     CJACCLINK+ALINK1-ACC3   [MARK CLEAR FOR ANOTHER SPEC SWAP
  12 68T5 ...      PCATEST  2,NORMAL3,TP3,PUC3A  
   8 68XN ...TP3   FINDPUC  2,2  
   8 692? ...PUC3A CALL  4  TESTW
   8 694W ...      FINDTP   2,2  
   5 697F ...NORMAL3 
  15 69=6          CALL  4  TESTW               [OR NORMAL/TP
  10 69PQ    NACT  LDX   2  ACTRING(2)   
   9 6=9B          SBN   2  ACTRING  
   9 6=P2          LDCH  0  ATYPE(2) 
   9 6?8L          SBN   0  AOLPT/64 
   8 6?N=          BNZ   0  XCPA 
  14 6#7W          CALL  4  TESTW               [OR OLPA 
   8 6#MG          BRN      NACT 
  15 6*76    XCPA  FCAPCA   2,6                 [X2 -> CPA   
   8 6*LQ          CALL  4  TESTW
  18 6B6B          GEOERR   1,WAITMISS          [ERROR IF NOTHING MISSING
  17 6BL2    [     S/R TO CHECK IF X2-7 ACT WAITING FOR CORE,LINK X4 
   9 6C5L    TESTW LDX   0  CLONG1(2)
   9 6CK=          BPZ   0  XOBJCNT  
  18 6D4W          LDN   0  1                   [INCREMENT WAITING FOR   
  16 6DJG          ADS   0  CFAIL               [ CORE FAIL COUNT
   8 6F46          BRN      XAGIN
   5 6FHQ    XOBJCNT 
   7 6KYB          EXIT  4  0
   5 6LD2    XAGIN   
  18 6LXL          FPUT                         [LET IT HAVE ANOTHER GO  
  15 6MC=          COOR1X                       [LET IT RUN  
   5 6MWW    NSPEC   
   4 6NBG    #   
  20 6NW6    # NORMAL CASE: PLUGIN AFTER SWAPIN : 1ST CHECK FOR PCACA AND BREAKIN
  15 6P*Q    # IF PCACA,KICK CPA  IF B-IN,PROGBRKIN VIA CPA, 
  11 6PTB    # OTHERWISE PLUGIN AND EVENTFIN.
   2 6Q*2
  21 6QSL          JBCC     NPCACA,3,JBPCACA    [J IF CPA NOT WAITING IN PCACA 4 CLEA
  14 6R#=          BS       3,JBSTOP            [IF IT IS
  19 6RRW          MBC      3,JBSUSP,JBPLUGGING            [SUSP -> STOPPED  
   8 6S?G          CALL  6  XFCA 
   6 6SR6          FPUT  
  15 6T=Q          COOR1X                       [LET CPA RUN 
   5 6TQB    NPCACA  
  10 6W=2          JBC      R10,3,JBBIN  
   4 6WBD ...[   
  20 6WGW ...[         THIS CURES B3092 - BREAK IN PLUGIN BECAUSE WE DO A FCAPCA 
  17 6WM# ...[          WITHOUT X2 POINTING AT PCA (ONLY IF BREAK-IN)
  16 6WRQ ...[               DONE   23RD  JUNE  75       :CENT   
   4 6WY8 ...[   
   8 6X4L ...      LDX   2  FX2  
   5 6X9=    RBREAKIN
  20 6XBD ...      MBC      3,JBSUSP,JBPLUGGING            [SUSPENDED -> DORMANT 
   4 6XHL ...[   
  19 6XNS ...[      CURES  B3288 - CONCERNING FACT THAT IF PLUGIN DETECTED   
  20 6XW2 ...[        B-IN  IT DID NOT FIRST CHECK THAT CPA WAS ALREADY ACTIVE   
  18 6Y38 ...[          THIS WOULD BE IMPLIED IF PROG WAS MARKED DORMANT 
   4 6Y8B ...[   
  20 6Y*J ...      JBSS     RBIN,3,JBDORM       [IF DORMANT=>B-IN ALREADY NOTICED
  19 6YGQ ...                                   [ELSE SET JBDORM AND CONTINUE
   8 6YN6          CALL  6  XFCA 
  16 6_7Q          LINKSET  2,P12(1)            [PROGBRKIN LINK  
  19 6_MB          FPUT                         [PUT COMMAND ACTIVITY ON LIST
   4 6_X8 ...RBIN
  17 7272          COOR1X                       [LET CPA DO PROGBRKIN
   9 72LL    # PCAPLUG ENTRY IN PCA  
  16 736=    # WE  PLUG IN,SETTING TIMESLOT IF NEC,AND UPPLUS  1 
   4 73KW    #   
   4 745G    #   
   4 74K6    XX2P
  16 754Q          BS       2,ASTOPEXEC         [ENSURE STOPPED  
   7 75JB          LDX   1  2
  17 7642          CALL  7  SPLUGIN2            [LOCK PRB AND OPLAS  
  19 76HL          JBS      REALTIME,3,JBREEF   [NO NEED TO RING IF REALTIMR 
  16 773=          LDX   1  FX2                 [YES I MEAN FX2  
  19 77GW          CALL  7  SPLUGIN1            [PLUG IN(MELRING) THE PROGRAM
   5 782G    REALTIME
   8 78G6          LDX   2  FX2  
   9 78_Q          LDX   0  SMASK(1) 
   9 79FB          ANDS  0  ASTOP(2) 
   9 79_2          LDX   0  ATIMES(3)
   8 7=DL          BNZ   0  XK55 
   2 7=Y=
  15 7?CW          CALL  7  TIMESLOT            [SET TIMESLOT
   4 7?XG    XK55
  20 7#C6          ENDPAXES 3                   [CLEAR 'BEING ACCESSED' MARKER   
  16 7#WQ          BS       3,JBRUN             [MRAK RUNNABLE   
   8 7D#6         FCAPCA   3,2   
   8 7DRQ          UPLINK   3,1  
   4 7F?B    #   
  12 7FR2    #  NOW REST OF PLUGIN AFTER SWAPIN  
   4 7G=L    #   
   4 7GQ=    #   
   4 7H9W    R10 
   8 7HPG          LDCT  0  #400 
  17 7J96          STO   0  ASTOP(2)            [READY FOR EVENTFIN  
  16 7JNQ          LDX   1  2                   [PCA ADDR TO X1  
  19 7K8B          CALL  7  SPLUGIN2            [LOCK DOWN PRB AND ANY OLPAS 
  19 7KN2          LDX   0  ATIMES(3)           [ HAS TIMESLOT EVER BEEN SET?
  21 7L7L          BZE   0  R10CA               [ IF NOT THEN SET (FOR 1ST ENTER ONLY
   8 7LM=          SMO      FX2  
   9 7M6W ...      LDX   0  AWORK2   
  18 7MLG          BNZ   0  R10C                [J IF TIME SLOT UNEXPIRED
  15 7N66    R10CA CALL  7  TIMESLOT            [SET TIMESLOT
  15 7NKQ          BS       3,JBINSLOT          [MARK INSLOT 
   4 7P5B    R10C
  16 7PK2          JBC      T21,3,JBRCTP        [J UNLESS RCTP   
   5 7PMS ...RCTP15  
  19 7PQL ...[    THIS CURES B2896 WHEREBY PLUGIN WAS SETTING B16 OF WD150   
  19 7PTD ...[      AND CLEARING B3 OF WD 149 OF THE PUC OBJECT PROGRAN !!   
  15 7PY= ...[            DONE   30  .  5 . 75       :CENT   
   4 7Q34 ...[   
  16 7Q5W ...      LDX   2  FX2               [REFIND CURRENT PCA
  17 7Q8N ...      PCATEST  2,(GEOERR),TPEX      [J  IF TP IS CURRENT
  18 7Q?G ...      FINDTP   2,2              [PUC IS CURRENT SO FIND TP  
   4 7QB# ...TPEX
   4 7QF6 ...[   
  17 7QJ=          LDX   0  ASU1(2)             [TICKLE TP MEMBER 0  
   8 7R3W          BZE   0  SETP 
   8 7RHG          LDCT  0  #40  
   9 7S36          ANDX  0  ASU1(2)  
   8 7SGQ          BZE   0  SETP 
   9 7ST7 ...      LDX   0  HSMPBIT  
   9 7T7J ...      ANDS  0  ASU1(2)  
   8 7TG2          BRN      T21  
   4 7T_L    SETP
  10 7W5H ...      SETBIT   16,ARM1(2)   
   4 7W9D ...[   
  20 7W** ...[    THE FOLLOWING CURES B3241 - MUST ENSURE SMP CLEAR ONCE P SET   
   4 7WF= ...[   
   9 7WK7 ...      LDX   0  HSMPBIT  
  14 7WP4 ...      ANDS  0  ASU1(2)       [CLEAR SMP BIT 
   4 7WS_ ...[   
   4 7WYW    T21 
  21 82TW    NOTRT JBS      REALT,3,JBREEF      [J IF REALTIME EFFECTED : NO RINGING 
   5 83*G    REALR   
  15 83T6          LDX   1  FX2                 [NECESSARY   
  21 84#Q          CALL  7  SPLUGIN1            [PLUG IN PROGRAM IF NOT YET REALTIME 
   2 84SB
  18 84YS ...REALT JBC      NOTRCTP,3,JBRCTP             [J UNLESS RCTP  
  14 855= ...      FPCAJO   2,3            [X2 -> TP PCA 
  18 859N ...      STOZ     ASTOP(2)         [DONT WANT TO USE EVENTFIN  
  18 85B6 ...      COOR1X                    [AS COULD MESS UP STOP WORD 
   5 85GJ ...NOTRCTP 
  15 85M2 ...      EVENTFIN                     [LET PROG RUN
   4 85RL    #   
   2 86?=
   4 86QW    #   
  10 87=G          MENDAREA 50,K100PLUGIN
   4 87Q6    #END
   6 ____ ...115227140002
