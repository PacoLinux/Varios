  15 22FL    #SEG  IDFCLEAN8                    [A C PUTMAN  
  13 22PD ...# COPYRIGHT INTERNATIONAL COMPUTERS 1982
   8 22_=    #OPT  K0IDFCLEAN=0  
  16 23DW    #LIS  K0IDFCLEAN>K0ALLGEO>K0GREATGEO>K0COMMAND>K0IDF
   7 23YG          8HIDFCLEAN
  12 24D6          SEGENTRY K10IDFCLEAN,XIDF10   
   4 24XQ    #   
  13 25CB    #     THIS  SEGMENT CLEANS UP THE IDF   
   4 25X2    #   
   9 26BL    MASK           #77770000
   9 26W=                   #73777777
   9 27*W                   #17777   
   5 27TG    RECZERO 
  16 28*6          LDN   6  0                   [HUNT FIRST REC. 
   8 28SQ    RECFD LDX   2  FX2  
  10 29#B    REH   HUNT2    2,FILE,FRRB  
  10 29S2          TXU   6  BRECNO+A1(2) 
   8 2=?L          BCS      REH  
   8 2=R=          ADN   2  A1   
   7 2?=W          EXIT  7  0
   5 2?QG    NEXCELL 
  12 2#=6          SMO      FX2                 [
   9 2#PQ          STO   3  AWORK1   
   7 2*9B    NX1   LDX   6  3
   7 2*P2          SRL   6  9
   8 2B8L          ANDN  6  #7777
   8 2BN=          CALL  7  RECFD
   8 2C7W          ANDN  3  #777 
   7 2CMG          EXIT  5  0
   4 2D63 ...#   
   5 2D76    SFREE   
   8 2DLQ          LDX   1  FX2  
  10 2F6B    SF1   HUNT2OPT 1,FILE,FRRB  
  17 2FL2          BNG   1  NOFREE              [NO MORE FREE SPACES.
  10 2G5L          TXU   4  BRECNO+A1(1) 
  17 2GK=          BCS      SF1                 [J.IF WRONG RECORD.  
   8 2H4W          LDX   3  A1(1)
  20 2HJG          BZE   3  SF2                 [J. IF NO SPACE IN THIS RECORD.  
   8 2J46          ADN   1  A1   
   5 2JHQ    NOFREE  
  15 2K3B          EXIT  6  0                   [NORMAL EXIT 
   7 2KH2    SF2   ADN   4  1
   8 2L2L          BRN      SFREE
   4 2LG=    TEST
   8 2L_W          SMO      FX2  
   9 2MFG          STO   7  ACOMMUNE7
   7 2M_6          LDX   4  3
   7 2NDQ          SRL   4  9
  18 2NYB          ANDN  4  #7777               [FREE SPACE REC.NO IN X4 
   7 2PD2          LDX   6  2
   7 2PXL          SRL   6  9
  21 2QC=          ANDN  6  #7777               [REC.NO OF CELL TO BE MOVED IF NEC.  
   8 2QWW          BZE   3  TS   
   7 2RBG          TXL   4  6
   8 2RW6          BCS      TS1  
   8 2S*Q    TS    SMO      FX2  
  15 2STB          STO   6  AWORK4              [KEEP REC.NO.
   8 2T*2          SMO      FX2  
  18 2TSL          STO   2  ACOMMUNE6           [FOR LATER RESCUE PERHAPS
   8 2W#=          LDN   5  #777 
  15 2WRW          ANDX  5  2                   [CELL PTR.   
   8 2X?G          CALL  7  RECFD
  21 2XR6          ADX   5  2                   [X5 POINTS TO CELL FOR NEXT TEST ENT.
   7 2Y=Q          LDX   2  5
  18 2YQB          LDX   2  0(2)                [CONTENTS OF FORWARD PTR.
   8 2_=2          BRN      TS6  
   8 2_PL    TS1   SMO      FX2  
  17 329=          LDX   0  AWORK4              [RESCUE RECORD NO.   
   7 32NW          TXU   0  4
   8 338G          BCC      TS2  
   8 33N6          LDCT  0  #400 
  18 347Q          ORX   3  0                   [SET ^ANOTHER RECORD^ BIT
   4 34MB    TS2 
   7 3572          SMO      5
  17 35LL          STO   3  0                   [STORE NEW RECORD PTR
   8 366=          SMO      FX2  
  19 36KW          STO   3  ACOMMUNE6           [FOR LATER RESCUE PERHAPS.   
   8 375G          SMO      FX2  
  18 37K6          STO   4  AWORK4              [FOR NEXT ENTRY INTO TEST
   8 384Q          ANDN  3  #777 
  20 38JB          ADX   3  1                   [ABSOLUTE PTR TO NEW CELL IN X3  
   8 3942          LDX   0  0(3) 
  18 39HL          STO   0  0(1)                [FREE SPACE CHAIN UPDATED
  19 3=3=          LDXC  5  2                   [IDF POINTER TO CURRENT CELL 
  21 3=GW          CALL  7  RECFD               [GET PTR TO RECORD CONTAINING CELL   
  21 3?2G          LDX   0  0(2)                [X0=IDF PTR TO 1ST FREE CELL IN REC  
  21 3?G6          STO   5  0(2)                [CHAIN CURRENT CELL INTO FREE LIST   
   8 3?_Q          ANDN  5  #777 
  19 3#FB          ADX   2  5                   [X2=CORE PTR TO CURRENT CELL 
  21 3#_2          MOVE  2  8                   [MOVE CONTENTS OF CURRENT TO NEW CELL
  17 3*DL          STO   0  0(2)                [FINISH FREEING CELL 
  21 3*Y=          LDX   2  FX2                 [CELL HAS NOW BEEN MOVED : UPDATE ALL
  20 3BCW          LDX   5  AWORK3(2)           [PTRS IN CELL : AWORK3=MARKER WD 
  20 3BXG          LDN   7  8                   [B1-B8= 1 BIT FOR EACH WD IN CELL
  21 3CC6          SLL   4  9                   [IF SET WD IS PTR & NEEDS UPDATING   
  15 3CWQ    TS3   ADXC  5  5                   [J IF NOT SET
   8 3DBB          BCC      TS5  
  15 3DW2          LDXC  0  0(3)                [GET POINTER 
  20 3F*L          BZE   0  TS5                 [J IF NOT NECESSARY TO ALTER IT  
  21 3FT=          DEX   0  4                   [SET UP EQUIV PTR WITH CURRENT REC   
  20 3G#W          BXE   4  0,TS4               [TEST WHETHER PTRS ARE THE SAME  
  21 3GSG          ORX   0  GSIGN               [IF NOT SET B0,AS PTS TO ANOTHER BLK 
  17 3H#6    TS4   STO   0  0(3)                [STORE CORRECTED PTR 
  16 3HRQ    TS5   ADN   3  1                   [J FOR NEXT WORD 
  20 3J?B          BCT   7  TS3                 [DROP OUT WHEN ALL 8 BITS TESTED 
  21 3JR2          LDEX  7  AWORK3(2)           [THIRD CHARACTER OF MARKER WD SHOWS  
  18 3K=L          SBN   3  8                   [PRINCIPAL PTR IN CELL   
  21 3KQ=          SRL   4  9                   [TURN <X4> BACK INTO RECORD NUMBER   
  20 3L9W          SMO      7                   [GET CONTENTS OF PRINCIPAL PTR   
   8 3LPG          LDX   2  0(3) 
  18 3M96          LDX   5  3                   [REMEMBER CELL ADDRESS   
  21 3MNQ          LDX   3  0(1)                [IDF PTR TO 1ST AVAILABLE FREE CELL  
  20 3N8B          SMO      FX2                 [STORE CONTENTS OF PRINCIPAL PTR 
  19 3NN2          STO   2  ACOMMUNE9           [FOR POSSIBLE LATER ^RESCUE^ 
  21 3P7L          BNZ   3  TS6                 [J UNLESS NO MORE FREE CELLS IN THIS 
  21 3PM=          BUX   4  XWS                 [RECORD,OTHERWISE J TO STEP TO NEXT  
  20 3Q6W    PT0B  CALL  4  RESCUE              [RESCUE PTR TO LAST MAIN PROPERTY
  19 3QLG          LDX   2  3                   [CELL,GET PTR TO NEXT CELL   
  20 3R66          LDN   4  0                   [X4 HAS BEEN OVERWRITTEN,SO USE  
  20 3RKQ          CALL  6  SFREE               [SFREE TO RELOCATE 1ST FREE CELL 
  19 3S5B          BNZ   2  PART0               [J UNLESS NO MORE PROPERTIES 
  19 3SK2    NOPRP SMO      FX2                 [NOW DEAL WITH CONCEPTUALS   
   9 3T4L          STOZ     AWORK4   
   8 3TJ=          BRN      PART1
  16 3TTL ...XWS   CALL  6  SFREE      [RELOCATE NEXT FREE CELL  
   8 3W72 ...TS6   SMO      FX2  
  16 3WDB ...      LDX   7  ACOMMUNE7        [GET LINK AND EXIT  
  15 3WPQ ...      EXIT  7  0                [END OF SR TEST 
  20 3X36    RESCUE                             [CREATE X2 & X5 READY FOR TEST   
   8 3XGQ          LDX   2  FX2  
  19 3Y2B          LDX   5  AWORK1(2)           [RESCUE IDF FORMATED POINTER 
   7 3YG2    RESQ  LDX   6  5
   7 3Y_L          SRL   6  9
   8 3_F=          ANDN  6  #7777
   9 3_YW          STO   6  AWORK4(2)
   8 42DG          CALL  7  REH  
   8 42Y6          ANDN  5  #777 
   7 43CQ          ADX   5  2
   7 43XB          LDX   2  5
   8 44C2          LDX   3  0(2) 
  19 44WL          EXIT  4  0                   [X3 &X5 SET UP USING AWORK1. 
  21 4LF6    ZBATT                              [ROUTINE TO DEAL WITH BATT ROUTINE.  
  21 4LYQ          SBX   7  FX1                 [ON ENTRY ACOMMUNE7 =PTR.TO PREVIOUS 
   8 4MDB          SMO      FX2  
  16 4MY2          STO   7  ACOMMUNE8           [PRESERVE LINK   
   7 4NCL          SMO      2
   9 4NX=          LDX   3  BATT(3)  
  17 4PBW          BZE   3  XPACK               [J. IF NO ATTRIBUTES 
  16 4PWG          CALL  5  NX1                 [HUNT ASS. BLOCK.
   7 4QB6          ADX   3  2
   7 4QTQ          LDN   4  5
   7 4R*B          LDX   1  3
  15 4RT2          LDX    6  BTYPE+1(3)         [LD. PROPERTY
  19 4S#L          BZE   6  XPACK               [MAYBE ONLY A CONSOLE PROP   
   7 4SS=    ZBA   SLL   6  1
  16 4T?W          BNG   6  XMOD                [J. PERM.PROP.   
  10 4TRG          STOZ     BTYPE+1(3)   
   7 4W?6          LDN   0  1
  17 4WQQ          SBS   0  BNO(1)              [DECREASE CT. IN BNO.
   7 4X=B    XMOD  ADN   3  1
   7 4XQ2          SBN   4  1
  16 4Y9L          BZE   4  XCTZ                [J. END OF CELL  
  20 4YP=          LDX   6  BTYPE+1(3)          [LD. CONTENTS NEXT WD. IN CELL   
   8 4_8W          BNZ   6  ZBA  
   8 4_NG          BRN      XMOD 
   9 5286    XCTZ  LDX   6  BNO(1)   
  17 52MQ          BZE   6  XFCE                [J. IF COUNT ZERO.   
  16 537B          LDX   3  1                   [LD. OTHER PTR.  
   9 53M2          LDX   4  BTYPE(1) 
  18 546L          BZE   4  XMOVA               [J. IF NO CONSOLE PROP   
   7 54L=          SBN   6  1
  20 555W          BZE   6  XPACK               [J. IF ONLY CONSOLE PROP. LEFT   
   7 55KG    XMOVA LDX   3  1
  10 5656    XMOV  LDX   4  BTYPE+1(1)   
   8 56JQ          BZE   4  XUP  
   9 574B          STOZ  BTYPE+1(1)  
  10 57J2          STO   4  BTYPE+1(3)   
   7 583L          ADN   3  1
   7 58H=          SBN   6  1
   8 592W          BZE   6  XPACK
   7 59GG    XUP   ADN   1  1
   8 5=26          BRN      XMOV 
   4 5=FQ    XFCE
   8 5=_B          SMO      FX2  
  18 5?F2          LDX   3  ACOMMUNE7           [LD.PTR. TO CELL BEFORE  
   8 5?YL          CALL  5  NX1  
  18 5#D=          ADN   3  BATT                [MAKE PTR. TO END OF CELL
   8 5#XW ...      MFREECELL  2,3
   8 5*CG          SBN   3  BATT 
   4 5*X6    XOUT
   8 5BBQ          SMO      FX2  
  15 5BWB          LDX   7  ACOMMUNE8           [LD. LINK.   
   8 5CB2          ADX   7  FX1  
  13 5CTL          EXIT  7  0                   [EXIT
   5 5D*=    XPACK   
   8 5DSW          SMO      FX2  
   9 5F#G          LDX   3  ACOMMUNE7
   8 5FS6          CALL  5  NX1  
   8 5G?Q          BRN      XOUT 
   5 5GRB    XIDF10  
  21 5H?2          STOZ     AWORK4(2)           [START GARBAGE COLLECTING FROM REC 0 
  13 5H#* ...#               FIRST DO PROPERTY CELLS 
  17 5H*N ...START LDN   4  0        [FIND 1ST AVAILABLE FREE CELL   
   8 5HC3 ...      CALL  6  SFREE
  13 5HDB ...      CALL  7  RECZERO    [AND BASE CELL
  16 5HFP ...      LDN   5  BEXOT(2)     [FIND CONSOLE PROP CHAIN
   9 5HH4 ...      LDX   2  BEXOT(2) 
  14 5HJC ...      BZE   2  NOPRP        [JUMP IF NONE   
  17 5HKQ ...PART0 LDCT  0  #257         [PTRS IN CONSOLE CELL ARE   
  14 5HM5 ...      SMO      FX2          [WDS 0,2,4,5,6,7
   9 5HND ...      STO   0  AWORK3   
  18 5HPR ...      CALL  7  TEST         [MOVE MAIN CELL DOWN IDF IF POSS
   8 5HR6 ...      LDX   2  FX2  
  18 5HSF ...      LDX   0  ACOMMUNE6(2) [SAVE PTR TO NEXT CONSOLE CELL  
  15 5HTS ...      STO   0  AWORK1(2)    [FOR LATER RESCUE   
  18 5HX7 ...      LDCT  7  #377         [IN SUBSID CELLS, ALL WDS ARE   
  18 5HYG ...      STO   7  AWORK3(2)    [POINTERS - WD0 IS PRINCIPAL ONE
   7 5H_T ...      ADN   5  4
   7 5J38 ...      SMO      5
  17 5J4H ...      LDX   2  0            [POINTER TO 1ST SUBSID CELL 
  17 5J5W ...PT0A  BZE   2  PT0B         [JUMP IF FINISHED THIS PROP 
  17 5J79 ...      CALL  7  TEST         [OTHERWISE MOVE SUBSID CELL 
   8 5J8J ...      BRN      PT0A 
  21 5J==    PART1 CALL  7  RECZERO             [SFREE FINDS 1ST AVAILABLE FREE CELL 
  19 5JPW          LDN   5  BCONT(2)            [NOW PROCESS CONCEPTUAL CHAIN
  17 5K9G          LDX   2  BCONT(2)            [J IF NO CONCEPTUALS 
   8 5KP6          BZE   2  PART2
  20 5L8Q    PT6   LDCT  0  #201                [PTRS IN MAIN CONCEPTUAL CELL ARE
  19 5LNB          SMO      FX2                 [IN WDS 0 (CONCEPTUAL CHAIN) 
  20 5M82          STO   0  AWORK3              [AND 7 (PTR TO USER NAME CELL)   
  20 5MML          CALL  7  TEST                [MOVE CELL DOWN IDF IF POSSIBLE  
   8 5N7=          LDX   2  FX2  
  20 5NLW          LDX   0  ACOMMUNE6(2)        [SAVE PTR TO NEXT CONCEPTUAL FOR 
  15 5NWN ...      STOC  0  AWORK1(2)           [LATER RESCUE
  19 5P6G ...                                   [CLEAR BIT 0 AS AW1 USED FOR 
  19 5PB# ...                                     [REC NO COMPARISON LATER   
  21 5PL6          LDCT  7  1                   [NOW DEAL WITH USER NAME CELL - PTR  
  19 5Q5Q          ORN   7  7                   [PTR IN WD 7 ADDRESSES 1ST   
  21 5QKB          STO   7  AWORK3(2)           [SUBSIDIARY CELL - ONLY PTR IN CELL  
   7 5R52          LDX   2  5
   7 5WGB          ADN   5  7
   7 5X22          NGNC  0  1
  16 5X6D ...      ANDS  0  4(2)                [CLEAR ONLINE BIT
   8 5X=W ...      LDX   0  6(2) 
   8 5X?_ ...      SMO      FX2  
  18 5X*4 ...                              [ZERO COUNT OF IDES DETACHED  
   9 5XB7 ...      STOZ     ACOMMUNE5
  21 5XC# ...      BPZ   0  PTR5                [J TO CLEAR ALL AH'S IF NOT IPB CL   
  10 5XHQ ...      SEGENTRY K91IDFCLEAN  
  18 5XN8 ...      LDN   0  AHRETAIN            [MENDED BY RETAINAH MACRO
  20 5XSL ...      BZE   0  PTR5                [J IF ATTACHES NOT TO BE RETAINED
  20 5X_=          LDX   2  7(2)                [THEN DEAL WITH SUBSIDIARY CELLS 
  19 5YDW          CALL  7  TEST                [MOVE USER NAME UP THE IDF.  
   7 5YYG ...ZT5   ADN   5  7
   7 5_D6          LDX   2  5
   8 5_XQ          LDX   2  0(2) 
   8 62CB          BZE   2  PT4  
   8 62CP ...      LDCT  7  #377 
   7 62D4 ...      ORN   7  7
   8 62DC ...      SMO      FX2  
  21 62DQ ...      STO   7  AWORK3              [ALL WORDS IN CL CELLS ARE POINTERS  
   8 62F5 ...      CALL  7  TEST 
   8 62FD ...      LDX   2  FX2  
  15 62FR ...      STO   3  ACOMMUNE6(2)        [SAVE X3,X5  
  10 62G6 ...      STO   5  ACOMMUNE7(2) 
   7 62GF ...      LDN   0  6
  21 62GS ...      STO   0  AWORK3(2)           [COUNT FOR NO OF POSSIBLE ATTACHES...
  14 62H7 ...[                                   IN CELL 
   7 62HG ...      LDX   4  5
  21 62HT ...[                                   NOW UPDATE PTRS FROM ATTACHED IDS...
  20 62J8 ...[                                   SO THEY STILL POINT AT CL CELL  
   7 62JH ...NEXWD SMO      4
   7 62JW ...      LDX   3  0
  19 62K9 ...      BZE   3  NLAH                [J IF NO AH TO THIS CL IDENT 
  19 62KJ ...      CALL  5  NX1                 [FIND ID CELL OF ATTACHED ID 
   8 62KX ...      SMO      FX2  
  17 62L= ...      LDX   0  AWORK1              [POINTER TO CL CELL  
  17 62LK ...      SRL   0  9                   [GET RECORD NUMBER   
   9 62LY ...      ERX   0  BRECNO(2)
   7 62M? ...      ADX   3  2
   8 62ML ...      LDX   2  FX2  
  20 62M_ ...      BZE   0  ZAMRE               [J IF CL CEL IN SAME REC AS ID   
  16 62N# ...      LDCT  0  #400                [ELSE SET BIT 0  
   9 62NM ...ZAMRE ORX   0  AWORK1(2)
  18 62NP ...      LDCT  7  #200        [LOOK IF B1 OF CONCEPT POINTER   
  15 62NR ...      ANDX  7  CONTCEP(3)      [IN IDE CELLSET  
  17 62NT ...     BNZ   7  ZCCAH            [IF SO, JUMP TO DETACH   
  19 62P2 ...      STO   0  CONTCEP(3)          [STORE  CORRECT CL POINTER   
   7 62P* ...NLAH  LDN   0  1
   9 62PN ...      SBSC  0  AWORK3(2)
  18 62Q3 ...      BCS      ZNEXC               [J IF TESTED WHOLE CELL  
   7 62QB ...      ADN   4  1
   8 62QP ...      BRN      NEXWD
  19 62QQ ...ZCCAH STOZ     CONTCEP(3)     [ZERO CONCEP POINTER IN IDE CELL  
   7 62QR ...      SMO      4
  19 62QS ...      STOZ     0                [ZERO IDE POINTER IN SUBSID CELL
   7 62QT ...      LDN   7  1
  19 62QW ...      ADS   7  ACOMMUNE5(2)     [UPDATE COUNT OF IDES DETACHED  
  19 62QX ...      BRN      NLAH             [DEAL WITH NEXT PTR IN SUBSID   
  16 62R4 ...ZNEXC LDX   3  ACOMMUNE6(2)        [RESTORE X3,X5   
  10 62RC ...      LDX   5  ACOMMUNE7(2) 
  17 62RQ ...      BRN      ZT5                  [GO ON TO NEXT CELL 
  18 62S5 ...PTR5  STOZ     5(2)                 [CLEAR COUNT OF ATTACHES
  20 62SD ...      LDX   2  7(2)                [THEN DEAL WITH SUBSIDIARY CELLS 
  19 62SR ...      CALL  7  TEST                [MOVE USER NAME UP THE IDF.  
   7 62T6 ...PT5   ADN   5  7
   7 62TF ...      LDX   2  5
   8 62TS ...      LDX   2  0(2) 
   8 62W7 ...      BZE   2  PT4  
   8 62X2          CALL  7  TEST 
  19 63BL          LDX   6  5                   [NOW CLEAR OUT ALL ATTACHES. 
   7 63W=          ADN   6  1
   7 64*W          SMO      5
   7 64TG          STOZ     0
  21 65*6          MOVE  5  6                   [ALL ATTACHES CLEARED FROM CONCEPT.  
  15 65SQ          BRN      PT5                 [ANY MORE?   
   4 66#B    PT4 
  18 66S2          CALL  4  RESCUE              [RESCUE CONCEPTUAL CELL. 
   8 67?L          SMO      FX2  
   9 67R=          STOZ     AWORK3   
   8 67RM ...      SMO      FX2  
   9 67S4 ...      LDX   0  ACOMMUNE5
  19 67SF ...      SBS   0  5(2)             [DECREMENT COUNT OF SLOTS USED  
   8 67S_ ...      LDX   0  5(2) 
  18 67WN ...      BNZ   0  PT6A                 [J IF SOME IDS ATTACHED 
   8 67YC ...      LDX   0  6(2) 
   8 6826 ...      ANDN  0  #7777
   8 683T ...      BCT   0  PT6A 
   8 685J ...      LDN   0  #7777
  21 687? ...      ANDS  0  6(2)                [MASK OUT ONLY THE LOWEST SLOT NO.   
   4 6892 ...PT6A
   7 68=W          LDX   2  3
   7 68QG          LDN   4  0
   8 69=6          CALL  6  SFREE
  21 69PQ          BNZ   2  PT6                 [J. MORE CONCEPTUALS TO BE DEALT WITH
  18 6=9B    PART2                              [DEALT WITH CONCEPTUALS  
  17 6=P2          CALL  7  RECZERO             [HUNT ZERO RECORD.   
  19 6?8L          LDX   3  BUNIT(2)            [PTR. TO UNIT CHAIN (7007)   
   8 6?N=          BZE   3  XCONP
  16 6#7W    PT4A  CALL  5  NEXCELL             [HUNT UNIT CELL  
   7 6#MG          NGNC  0  1
   7 6*76          SRC   0  2
   7 6*LQ          SMO      3
  17 6B6B          ANDS  0  BNUMB(2)            [REMOVE ONLINE MARKER
   7 6BL2          SMO      3
   9 6C5L          LDX   3  BLINE(2) 
  15 6CK=          BZE   3  PT3                 [J. NO LINES 
   4 6D4W    PT2 
   8 6DJG          CALL  5  NX1  
   7 6F46          SMO      3
  10 6FHQ          STOZ     CONTCEP(2)   
   7 6G3B          SMO      3
   8 6GH2          LDX   3  0(2) 
  20 6H2L          BNZ   3  PT2                 [J. BACK TO FREE ATTACHED PTR.   
   4 6HG=    PT3 
  18 6H_W          CALL  4  RESCUE              [RESCUE PTR. IN AWORK1   
   8 6JFG          BNZ   3  PT4A 
   5 6J_6    XCONP   
  17 6KDQ          CALL  7  RECZERO             [HUNT FIRST RECORD.  
  18 6KYB          LDX   3  BUNIT(2)            [LD. BASE OF 7007 CHAIN  
  15 6LD2          BZE   3  ZNO7007             [J. NO 7007  
  16 6LXL    ZUN   CALL  5  NEXCELL             [HUNT UNIT CELL. 
  18 6LYK ...      ADX   3  2           [VALIDITY CHECKS FOR MXOR CELL   
   9 6L_J ...      LDX   7  BNO(3)   
  16 6M2H ...      BZE   7  ZZERR        [UNIT NO CANNOT BE ZERO 
   9 6M3G ...      LDCH  7  BTYPE(3) 
  16 6M4F ...      SBN   7  #13          [DEVICE TYPE MUST BE #13
   8 6M5D ...      BNZ   7  ZZERR
   9 6M6C ...      LDX  7  BNUMB(3)  
   8 6M7B ...      ANDN  7  #7777
   8 6M8* ...      SMO      FX2  
  16 6M9# ...      STO   7  ACOMMUNE4    [REMEBER HOW MANY LINES 
   7 6M=? ...      SBX   3  2
   8 6M?= ...      SMO      FX2  
   9 6M#9 ...      STOZ     ACOMMUNE5
   8 6M*8 ...      SMO      FX2  
   9 6MB7 ...      STOZ     ACOMMUNE6
   8 6MC=          SMO      FX2  
   9 6MWW          LDX   0  AWORK1   
   8 6NBG          SMO      FX2  
  19 6NW6          STO   0  AWORK3              [PRESERVE PTR. TO UNIT CELL  
   8 6P*Q          SMO      FX2  
   9 6PTB          STO   0  ACOMMUNE7
  21 6Q*2          CALL  7  ZBATT               [CALL ROUTINE TO REMOVE TEMP.ATTRIB. 
   7 6QSL          SMO      2
   9 6R#=          LDX   3  BLINE(3) 
  15 6RRW          BZE   3  ZNOLIN              [J.NO LINES. 
   4 6S?G    ZLIN
  16 6SR6          CALL  5  NEXCELL             [HUNT FOR A LINE 
  18 6SS7 ...      ADX   3  2           [VALIDITY CHECKS FOR LINE CELL   
  14 6ST8 ...      LDX   7  BNO(3)         [GET LINE NO  
   8 6SW9 ...      SMO      FX2  
  16 6SX= ...      LDX   6  ACOMMUNE6      [LOOK AT LAST LINE NO 
   8 6SY? ...      SMO      FX2  
   9 6S_# ...      STO   7  ACOMMUNE6
  17 6T2* ...      SBX   6  7              [CHECK INCREASING SEQUENCE
   8 6T3B ...      BPZ   6  ZZERR
   7 6T4C ...      LDN   7  1
   8 6T5D ...      SMO      FX2  
  17 6T6F ...      ADS   7  ACOMMUNE5      [INCREMENT NOOF LINES SEEN
   9 6T7G ...      LDX   7  BTYPE(3) 
  15 6T8H ...      BZE   7  ZZERR          [CANNOT BE ZERO   
   7 6T9J ...      SBX   3  2
   8 6T=Q          SMO      FX2  
   9 6TQB          LDX   0  AWORK1   
   8 6W=2          SMO      FX2  
  17 6WPL          STO   0  AWORK2              [PRESERVE LINE PTR.  
   8 6X9=          SMO      FX2  
   9 6XNW          STO   0  ACOMMUNE7
  21 6Y8G          CALL  7  ZBATT               [CALL ROUTINE TO REMOVE TEMP. ATTRIB.
   7 6YN6          SMO      2
   9 6_7Q          LDX   3  BLINE(3) 
  15 6_MB          BZE   3  ZNO7020             [J.NO 7020.  
   9 7272    Z7020 CALL  5  NEXCELL  
  16 728? ...      ADX   3  2        [VALIDITY CHECKS FO RJE CELL
   9 729J ...      LDX   7  BNO(3)   
  15 72=T ...      BZE   7  ZZERR          [CANNOT BE ZERO   
   9 72#6 ...      LDX   7  BTYPE(3) 
  15 72*C ...      BZE   7  ZZERR          [CANNOT BE ZERO   
   9 72BN ...      LDX   7  BLINE(3) 
  16 72C_ ...      ANDN  7  #77            [CHECK LINE NO CORRECT
   8 72F= ...      SMO      FX2  
   9 72GH ...      SBX   7  ACOMMUNE6
   8 72HS ...      BNZ   7  ZZERR
   7 72K5 ...      SBX   3  2
   8 72LL          SMO      FX2  
   9 736=          LDX   0  AWORK1   
   8 73KW          SMO      FX2  
   9 745G          STO   0  ACOMMUNE7
   8 74K6          CALL  7  ZBATT
   7 754Q          SMO      2
  20 75JB          LDX   3  0(3)                [LD. PTR. TO NEXT 7020 IN CHAIN. 
  19 7642          BNZ   3  Z7020               [J.BACK MORE TO DEAL WITH.   
   5 76HL    ZNO7020 
   8 773=          LDX   2  FX2  
   9 77GW          LDX   0  AWORK2(2)
   9 782G          STO   0  AWORK1(2)
  16 78G6          CALL  4  RESCUE              [RESCUE LINE PTR.
  20 78_Q          BNZ   3  ZLIN                [J.BACK TO DEAL WITH NEXT LINE.  
   8 794W ...      LDX   2  FX2  
  10 7982 ...      LDX   7  ACOMMUNE4(2) 
  18 79?6 ...      SBX   7  ACOMMUNE5(2)     [CHECK RIGHT NO LINES SEEN  
   8 79B= ...      BNZ   7  ZZERR
   5 79FB    ZNOLIN  
   8 79_2          LDX   2  FX2  
   9 7=DL          LDX   0  AWORK3(2)
   9 7=Y=          STO   0  AWORK1(2)
   9 7?CW          CALL  4  RESCUE   
   8 7?XG          BNZ   3  ZUN  
   5 7#C6    ZNO7007 
   9 7#WQ          CALL  7  RECZERO  
   9 7*BB          LDX   3  BIPB(2)  
  16 7*W2          BZE   3  PART4               [J.NO 7900 CHAIN 
   9 7B*L    ZUNT  CALL  5  NEXCELL  
  17 7BBK ...      ADX   3  2           [VALIDITY CHECKS FOR IPB CELL
  17 7BCJ ...      LDX   7  BNO(3)           [UNIT NO CANNOT BE ZERO 
  17 7BDH ...      BZE   7  ZZERR            [UNLESS V7900 FLAG SET  
   9 7BFG ...      LDCH  7  BTYPE(3) 
  17 7BGF ...      SBN   7  #30              [DEVICE TYPE MUST BE #30
   8 7BHD ...      BNZ   7  ZZERR
   9 7BJC ...      LDX  7  BNUMB(3)  
   8 7BKB ...      ANDN  7  #7777
   8 7BL* ...      SMO      FX2  
  17 7BM# ...      STO   7  ACOMMUNE4         [REMEMBER HOW MANY IDES
   7 7BN? ...      SBX   3  2
   8 7BP= ...      SMO      FX2  
   9 7BQ9 ...      STOZ     ACOMMUNE5
   8 7BR8 ...      SMO      FX2  
   9 7BS7 ...      STOZ     ACOMMUNE6
   8 7BT=          SMO      FX2  
   9 7C#W          LDX   0  AWORK1   
   8 7CSG          SMO      FX2  
  18 7D#6          STO   0  AWORK2              [PRESERVE PTR. TO 7900.  
   8 7DRQ          SMO      FX2  
   9 7F?B          STO   0  ACOMMUNE7
  21 7FR2          CALL  7  ZBATT               [CALL ROUTINE TO REMOVE CONS.PROP.   
   7 7G=L          SMO      2
   9 7GQ=          LDX   3  BLINE(3) 
   8 7H9W          BZE   3  ZNOID
   9 7HPG    ZID   CALL  5  NEXCELL  
  18 7HQ* ...      ADX   3  2            [VALIDITY CHECKS FOR IDE CELL   
  15 7HR8 ...      LDX   7  BNO(3)             [GET IDE NO   
   8 7HS3 ...      SMO      FX2  
  17 7HSW ...      LDX   6  ACOMMUNE6         [LOOK AT LAST IDE NO   
   8 7HTP ...      SMO      FX2  
   9 7HWJ ...      STO   7  ACOMMUNE6
   7 7HXC ...      SBX   6  7
  18 7HY= ...      BPZ   6  ZZERR             [CHECK INCREASING SEQUENCE 
   7 7H_5 ...      LDN   7  1
   8 7H_Y ...      SMO      FX2  
  17 7J2R ...      ADS   7  ACOMMUNE5         [INCREMENT NO IDES SEEN
   9 7J3L ...      LDX   7  BTYPE(3) 
  16 7J4F ...      BZE   7  ZZERR             [MUST BE NON ZERO  
   8 7J5# ...      LDCT  7  #200 
   9 7J67 ...      ANDX  7  BNUMB(3) 
  16 7J72 ...      BZE   7  ZZERR            [MUST HAVE B1 SET   
   7 7J7T ...      SBX   3  2
   8 7J96          SMO      FX2  
   9 7JNQ          LDX   0  AWORK1   
   8 7K8B          SMO      FX2  
   9 7KN2          STO   0  ACOMMUNE7
   8 7L7L          CALL  7  ZBATT
   7 7LM=          SMO      2
   8 7M6W          LDX   3  0(3) 
   8 7MLG          BNZ   3  ZID  
   8 7MPL ...      LDX   2  FX2  
  16 7MSQ ...      LDX   7  ACOMMUNE4(2)      [CHECK NO IDES SEEN
  14 7MXW ...      SBX   7  ACOMMUNE5(2)       [IS RIGHT 
   8 7N32 ...      BNZ   7  ZZERR
   8 7N66    ZNOID LDX   2  FX2  
   9 7NKQ          LDX   0  AWORK2(2)
   9 7P5B          STO   0  AWORK1(2)
   9 7PK2          CALL  4  RESCUE   
  16 7Q4L          BNZ   3  ZUNT                [J.BACK MORE 7900
   5 7QJ= ...PART4   
   9 7R3W ...      ACROSS   IDFCHK,1 
   3 7RHG ...
  10 7S36 ...ZZERR TRACE    2,IDFCLEAN   
  15 7SGQ ...      DOWN     SETIDF,3         [SET ALARM BIT  
  18 7T2B ...      GEOERR   1,IDFINVAL       [BEFORE FALLING IN A HEAP   
   4 7TG2 ...#   
  11 *C9G          MENDAREA 50,K99IDFCLEAN   
   4 *CP6    #END
   8 ____ ...42157131000400000000
