  14 22FL    #LIS  K0ACCESS>K0COMMAND>K0GREATGEO>K0ALLGEO
  13 22R2 ...      SEG      ACCESS,867,SECTION CENT  
   4 234B ...[   
  17 23*Q ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LIMITED  1982   
   4 23M6 ...[   
  21 23YG    # THIS SEGMENT DEALS WITH ACCESS COMMANDS WHICH ARE ISSUED BY SPECIAL   
   6 242D    #UNS AV7900S
   4 244B    (   
  20 246#    # SYSTEM JOBS CONCERNED WITH SCHEDULING OR ISSUED BY COMMUNICATIONS 
  19 248=    # MANAGER TO CONNECT TO A VIRTUAL 7900 . IT CHECKS FOR INCORRECT
   4 24=8    )   
   6 24#6    #UNS AV7900S
   4 24B4    #SKI
  19 24D6    # SYSTEM JOBS CONCERNED WITH SCHEDULING. IT CHECKS FOR INCORRECT
  21 24XQ    # COMMANDS AND THEN SETS UP A PSEUDO OFF-LINE PERI ACTIVITY WHICH CAN   
  12 25CB    # ACCESS CERTAIN GEORGE INFORMATION 
  21 25X2    # JOBS RUN UNDER :SYSTEM AND :MANAGER AND SYSTEM-STARTED JOBS ARE ABLE  
  21 26BL    # TO ACCESS ALL THE PASSIVE ROUTINES, I.E. NOT HLSTEMPQ OR SCHEDCONTROL 
  21 26W=    # - THIS HAS BEEN INTRODUCED TO COVER THE NEEDS OF 'GERONIMO' AND USER'S
   8 27*W    # LOGGING SYSTEMS.  
   4 27TG    #   
   8 28*6    #     ENTRY POINT   
   4 28SQ    #   
  10 29#B          SEGENTRY K1ACCESS,XK1 
   2 29S2
   9 2=?L    #DEF  X=GENIE<1$1*7+1   
   7 2=R=    #DEF  XED=6*X   
   6 2=YD    #UNS AV7900S
   7 2?5L    #DEF  XPB=28*X  
   7 2?=W    #DEF  XFR=50*X  
   7 2?QG    #DEF  XFW=51*X  
   7 2#2#    #DEF  XFH=52*X  
   7 2#=6    #DEF  XTM=#77*X 
   8 2#PQ    ZERO           +0   
   7 2*9B    XEXT  8HHLSEXT  
   2 2*P2
  20 2*T=    SINF                               [TABLE OF PERMISSIBLE INFORMATION
  19 2*_G                                       [PARAMETERS FOR AC COMMAND   
   8 2B5Q                   #111 
   8 2B=2                   +XFH 
  10 2BB=                   9HHLSACTIVE  
   8 2BGG                   +10  
   8 2BLQ                   +XFH 
  10 2BR2                   10HHLSPASSIVE
   8 2BX=                   +4   
   8 2C3G                   +XED 
  21 2C7W                   4HHLSQ              [  THE TABLE CONSISTS OF TRIPLETS OF 
  20 2CMG                   +4                  [  ITEMS. THE 1ST ITEM GIVES THE 
  21 2D76                   +XED                [  LENGTH (IN CHARACTERS) OF THE 3RD 
  20 2DLQ                   4HJOBQ              [  ITEM.  THE 2ND ITEM IS THE PER
  17 2F6B                   +8                   [  TYPE PERMITTED.  
  21 2FL2                   +XFR                   [BIT #100 OF 1ST ITEM INDICATES   
  20 2G5L                   8HHLSBLOCK                   [NON-PASSIVE ROUTINE.   
   8 2KH2                   +8   
   8 2L2L                   +XFR 
  10 2LG=                   8HJOBBLOCK   
   8 2L_W                   +8   
   8 2MFG                   +XFR 
  10 2M_6                   8HINSTPARA   
   8 2NDQ                   #110 
   8 2NYB                   +XFR 
  10 2PD2                   8HHLSTEMPQ   
   8 2PRP                   +8   
   8 2Q7D                   +XED 
  10 2QH7                   8HPERILIST   
   8 2QWW                   +10  
   8 2RBG                   +XED 
  10 2RW6                   10HGEORGECORE
   8 2S5Y                   #114 
   8 2S*Q                   +XFW 
  11 2SKJ                   12HSCHEDCONTROL  
   6 2SLM    #UNS AV7900S
   4 2SMQ    (   
   8 2SNT                   +5   
   8 2SPY                   +XPB 
   9 2SR3                   5HCOMMS  
   4 2SS6    )   
   4 2SS8 ...[   
  16 2SS= ...[  !! ANY NEW ENTRIES MUST GO IN BEFORE LPLIST !!   
  16 2SS# ...[  !! BECAUSE OF RESTORE-TIME MACRO  LPS       !!   
   4 2SSB ...[   
  18 2SSD ...      SEGENTRY K51ACCESS           [MEND POINT FOR MACRO LPS
  18 2SSG ...      0                            [MENDED BY LPS ON  TO +6,
  17 2SSJ ...[                                   AND BY LPS OFF  TO 0
   6 2SSQ          +XFH  
   7 2SSX          6HLPLIST  
  20 2STB                   +0                  [0 INDICATES THE END OF THE TABLE
   5 2T*2    SLINKS  
  17 2TR#          PAIR     HLSREAD,1           [HLSACTIVE CHANNEL   
  17 2W9L          PAIR     HLSREAD,2           [HLSPASSIVE CHANNEL  
  18 2WMY          PAIR     HLSREAD,15          [ROUTINE TO EXAMINE HLSQ 
  18 2X6=          PAIR     HLSREAD,7           [ROUTINE TO EXAMINE JOBQ 
  20 2XJJ          PAIR     HLSREAD,8           [ROUTINE TO READ NEXT HLSQ BLOCK 
  20 2Y2W          PAIR     HLSREAD,9           [ROUTINE TO READ NEXT JOB BLOCK  
  21 2YF8          PAIR     HLSREAD,10          [ROUTINE TO READ INSTALLATION PARS.  
  21 2YXG          PAIR     HLSREAD,11          [ROUTINE TO READ FROM TEMP TO HLSQ   
  21 2_*S          PAIR     HLSREAD,12          [ROUTINE TO READ FROM PERIPHERAL LIST
  19 2_S6          PAIR     HLSREAD,13          [ROUTINE TO READ GEORGECORE  
  19 32=D          PAIR     HLSREAD,14          [GENERAL SCHEDULING ROUTINES 
   6 32=K    #UNS AV7900S
  21 32=X          PAIR     VIPBPERI,1          [VIRTUAL 7900 COMMS(WMC TYPE) CHANNEL
   9 32=_          PAIR     FHLIST,1 
   7 32?2    #UNS  AV7900S   
   4 32?3    (   
   4 32?4    #   
  20 32?9    #     SUBROUTINE TO TEST IF DEVICE TYPE IS PB AND IF TRUE TO ASSUME 
  19 32?B    #     THE CHANNEL IS COMMS AND TO CHECK THAT THE THIRD PARAMETER
  14 32?H    #     SPECIFIES AN ACCESSABLE VIRTUAL 7900  
  20 32?N    #     IF THIRD PARAMETER IS NOT A VIRTUAL 7900, ERROR ^UNIT IS NOT A
   9 32?T    #     SUITABLE DEVICE^  
  21 32#2    #     IF THE VIRTUAL 7900 IS ALREADY ^INITIALISED^ OR IS BEING ACCESSED 
  18 32#7    #     BY A DIFFERENT JOB, ERROR ^UNIT IS ALREADY ALLOCATED^ 
  20 32##    #     NOTE IF A PB OF THE SAME UNIT NUMBER IS ALREADY BEING ACCESSED
  15 32#F    #     IT WILL BE FREED LATER BY MACRO PUTPERENT 
  18 32#L    #     ON ENTRY ACCOMMUNE3 = DEVICE TYPE(IN B3-7) ; X2 = FX2 
  21 32#R    #     AWORK3 WILL BE USED TO STORE THE VIRTUAL 7900 DEVICE LIST POINTER 
  11 32#Y    #     FOR SUBROUTINE VIRTUAL7900
   7 32*5    #     LINK IN X6
   5 32*=    VTYPEPB 
   8 32*C          LDCT  4  XPB  
  17 32*J          BXU   4  ACOMMUNE3(2),(6)    [DEVICE TYPE NOT PB  
   8 32*P          SBX   6  FX1  
  19 32*W          LDN   3  3                   [ANALYSE THE THIRD PARAMETER 
  10 32B3          PARSORTX XBREAKIN,3   
   8 32B8          ADX   6  FX1  
  10 32B*          HUNTW    3,CPAR,JSCE  
  21 32BG          BNG   3  VENDCOM             [UNIT NO. PARAMETER MISSING/INVALID  
  17 32BM          SAWCEJX  JSCEB(3),VLOCALUNIT,XJUNITERR,XJUNITERR  
   6 32BS    VLOCALUNIT  
   9 32B_          LDCH  0  JSCEA(3) 
   8 32C6          SBN   0  24   
  16 32C?          BNZ   0  XJUNITERR           [UNIT NOT A 7900 
  11 32CD          JV7900  JSCEP(3),VAV7900  
  18 32CK          BRN      XJUNITERR           [UNIT NOT A VIRTUAL 7900 
   5 32CQ    VAV7900 
  21 32CX          LDX   3  JSCEP(3)            [SET X3 -> VIRTUAL 7900 DEVICE LIST  
  18 32D4          STO   3  AWORK3(2)           [KEEP FOR VIRTUAL7900 S/R
  18 32D9          LDX   0  JK52H(3)            [COUNT OF GEORGE PERI'S  
  19 32DB          BNZ   0  XJALOC              [V7900 IS ALREADY INITIALISED
  10 32DH          LDN   0  AVOLPARING(3)
  21 32DN          BXE   0  AVOLPARING(3),VTYPEPBEXIT [OLPA RING EMPTY : NOT BEING   
  14 32DT                                       [ACCESSED
  10 32F2          LDX   3  AVOLPARING(3)
   9 32F7          SBN   3  BWORKRING
  20 32F#          LDX   0  JOBNO(3)            [JOB NUMBER OF AN AOLPT ON THE   
  15 32FF                                       [OLPA RING   
  19 32FL          LDX   4  JOBNO(2)            [JOB NUMBER OF THIS PROGRAM  
  21 32FR          BXU   0  4,XJALOC            [BEING ACCESSED BY A DIFFERENT JOB   
   6 32FY    VTYPEPBEXIT 
   9 32G5          MFREEW   CPAR,JSCE
   7 32G=          EXIT  6  0
   4 32GC    #   
  20 32GJ    #     SUBROUTINE TO TEST IF DEVICE TYPE IS PB AND IF TRUE TO ASSUME 
  12 32GP    #     THE CHANNEL IS COMMS AND TO : 
  20 32GW    #     1. INCREMENT COUNT OF REALTIME PERIPHERALS IF REALTIME IS ON  
  20 32H3    #     2. RING THE AOLPT TO THE BACK OF THE VIRTUAL 7900 OLPA RING   
  21 32H8    #        AFTER SETTING ITS CPPTR WITH THE VIRTUAL 7900 DEVICE LIST PTR  
  18 32H*    #        (COMMS CHANNELS ARE PROCESSED IN ORDER OF SETUP)   
  19 32HG    #     ON ENTRY X3 -> THE AOLPT ; X2 = FX2 ; AWORK1 = DEVICE TYPE
  20 32HM    #     IN B3-8 ; AND IF PB, AWORK3 -> THE VIRTUAL 7900 DEVICE LIST   
  15 32HS    #     EXITS+0 IF DEVICE TYPE IS PB, ELSE EXITS+1
   7 32H_    #     LINK IN X7
   6 32J6    VIRTUAL7900 
  18 32J?          LDCT  4  XTM                 [SET MASK FOR DEVICE TYPE
  17 32JD          ANDX  4  AWORK1(2)           [EXTRACT DEVICE TYPE 
   8 32JK          LDCT  0  XPB  
  17 32JQ          BXE   0  4,VRTTEST           [DEVICE TYPE IS PB   
  17 32JX          EXIT  7  1                   [DEVICE TYPE NOT PB  
   5 32K4    VRTTEST 
  19 32K9          FCJOB    1,2,,CPA            [FIND THIS CPA'S JOB BLOCK   
  21 32KB          RTTEST   VREON,1             [J TO VREON IF RE ON HAS BEEN ISSUED 
   9 32KH          BRN      VRINGOLPA
   5 32KN    VREON   
  21 32KT          LDN   0  1                   [UP ITS COUNT OF RE PERIPHERALS BY 1 
  21 32L2          ADS   0  AEXCO(1)            [THIS COUNT WILL BE DECREMENTED BY   
  21 32L7                                       [RELPER WHEN THE CHANNEL IS RELEASED 
   6 32L#    VRINGOLPA   
  20 32LF          LDX   0  AWORK3(2)           [VIRTUAL 7900 DEVICE LIST POINTER
  20 32LL          STO   0  CPPTR(3)            [SET AOLPT'S CPPTR -> THE VIRTUAL
  16 32LR                                       [7900 DEVICE LIST
   9 32LY          SMO      AWORK3(2)
  21 32M5          LDX   2  AVOLPARING+1        [PTR TO BWORKRING OF THE LAST AOLPT  
  19 32M=                                       [ON THE OLPA RING, IF ANY .  
  21 32MC          ENRING   BWORKRING(3),2      [ENRING NEW AOLPT ON THE BACK OF THE 
  21 32MJ                                       [VIRTUAL 7900 OLPA RING (AVOLPARING) 
  16 32MP          EXIT  7  0                   [X1=FX1 & X2=FX2 
   4 32MW    #   
   4 32N3    )   
   2 32NW
   4 338G    XK1 
  10 33N6    #SKI  K6ACCESSCOM>499-499   
   9 347Q          TRACE    K7,ACCESS
  14 34MB          CHEKPRIV JOB,XEXT(1),NOTPERM,NOTOPEN  
   5 3572    NICEJOB 
  21 35LL          PARANOT  1,ZERO(1)           [PASS 1ST PARAMETER AT ANAL LEVEL 0  
  10 366=          MHUNT    3,CPB,CUNI   
   9 36KW          NGX   4  ANUM(3)  
  17 375G          BPZ   4  NO1STP              [J IF NO FIRST PARAM.
   6 37K6          PERCON
  20 384Q          TESTREP  PARERR,PFERR        [J IF PERCON FOUND FORMAT ERROR  
  11 38JB          MHUNTW   2,CPAR,GPERCON   
   8 3942          LDX   4  A1(2)
   8 39HL          SMO      FX2  
   9 3=3=          STO   4  AWORK1   
  19 3=GW          LDCT  4  XTM                 [SET MASK FOR DEVICE TYPE.   
  17 3?2G          ANDX  4  A1(2)               [EXTRACT DEVICE TYPE 
  20 3?7N          LDCT  5  XFH                 [CHECK WHETHER FH IS SPECIFIED   
  14 3?#W          BXE   5  4,P1                [J IF FH 
  20 3?G6          LDCT  5  XED                 [CHECK WHETHER EDS IS SPECIFIED. 
  16 3?_Q          BXE   5  4,P1                [J IF IT IS EDS  
  19 3#FB          LDCT  5  XFR                 [CHECK WHETHER FILE READR.   
  15 3#_2          BXE   5  4,P1                [J.IF IT IS  
  19 3*DL          LDCT  5  XFW                 [CHECK WHETHER FILE WRITER.  
   6 3*G*    #UNS AV7900S
   4 3*J4    (   
  14 3*KR          BXE   5  4,P1                [J IF FW 
  20 3*MG          LDCT  5  XPB                 [CHECK WHETHER PB IS SPECIFIED   
  18 3*P9          BXU   5  4,WRONGP            [ERROR IF NOT PB EITHER  
   4 3*QY    )   
   6 3*SM    #UNS AV7900S
   4 3*WB    #SKI
  18 3*Y=          BXU   5  4,WRONGP            [ERROR IF NOT ED OR FW   
   2 3BCW
  16 3BXG    P1    LDX   5  A1(2)               [TYPE/UNIT WORD  
  10 3CC6          MFREE    CPAR,GPERCON 
  17 3CWQ          STO   5  AWORK2(2)           [KEEP FOR PUTPERENT  
  21 3DBB          PARANOT  2,ZERO(1)           [PASS 2ND PARAM AT ANALYSIS LEVEL 0  
  10 3DW2          MHUNT    1,CPB,CUNI   
   9 3F*L          LDX   6  ANUM(1)  
  21 3FT=          BNG   6  NO2NDP              [COMMAND ERROR IF NO 2ND PARAMETER   
  17 3G#W          BZE   6  NO2NDP              [J IF NULL PARAMETER 
  18 3GSG          ANDN  6  #7777               [NO OF CHARACTERS IN X6  
   8 3H#6          LDX   2  FX2  
   8 3HRQ          LDX   3  FX1  
  15 3J?B          STO   4  ACOMMUNE3(2)        [STO PERTYPE 
  20 3JR2          LDX   4  SINF(3)             [LOAD CH.CT. FROM TABLE OF PARAMS
  19 3K=L          STO   1  ACOMMUNE4(2)        [STORE PTR TO CPB/CUNI BLOCK 
  21 3KQ=          LDN   7  0                   [SET X7 TO 0 FOR 1ST POSSIBLE PARAM  
  21 3L9W    P5    STO   3  ACOMMUNE1(2)        [STORE INDEX FOR CHAPTER PARAM TABLE 
  20 3LPG          STO   4  ACOMMUNE2(2)        [STORE CH. CT. OF PARAM IN TABLE 
  21 3M96          ANDN  4  #77                 [TO GET RID OF 'NON-PASSIVE' MARKER. 
  19 3MNQ          BXU   4  6,P8                [J IF CH.CTS ARE DIFFERENT   
  21 3N8B    P6    LDCH  5  SINF+2(3)           [READ A CHAR FROM CHAPTER PARAM TABLE
  21 3NN2          LDCH  0  APARA(1)            [COMPARE WITH CHAR IN CPB/CUNI BLOCK 
  14 3P7L          BXU   5  0,P7                [J IF DIF
  18 3PM=          BCHX  3  /                   [ADVANCE INDEX IN TABLE  
  19 3Q6W          BCHX  1  /                   [ADVANCE INDEX IN CUNI BLOCK 
  18 3QLG          BCT   4  P6                  [COUNT DOWN ON CHARACTERS
  21 3R66          ADN   7  SLINKS              [IF PARAMS MATCH X7 SET PTG LINK PAIR
  10 3RKQ          LDX   3  ACOMMUNE1(2) 
   9 3S5B          SMO      SINF+1(3)
  18 3SK2          LDCT  5  0                   [PERMISSIBLE PERIF TYPE. 
  21 3T4L          BXU   5  ACOMMUNE3(2),WRNGPR [COMMAND ERROR IF WRONG PERIPHERAL   
  10 3TJ=          LDX   0  CONTEXT(2)   
  17 3W3W          BNG   0  P9                  [J IF SYSTEM-ISSUED  
  20 3WHG    # IF :MANAGER OR :SYSTEM OR SYSTEM-STARTED JOB, DISSALLOW HLSTEMPQ  
   8 3X36    # AND SCHEDCONTROL:-
   8 3XGQ          LDN   0  #100 
  10 3Y2B          ANDX  0  ACOMMUNE2(2) 
  21 3YG2          BNZ   0  NOTPERM             [J IF 'NON-PASSIVE' MARKER IS SET.   
  21 3Y_L          BRN      P9                  [NO COMMAND ERRORS. J TO OBEY COMMAND
  19 3_F=    P7    LDX   3  ACOMMUNE1(2)         [RESTORE PTR IN SLINKS TABLE
  20 3_YW          LDX   4  ACOMMUNE2(2)        [RESTORE CH.CT FOR CURRENT ITEM  
   8 428N          ANDN  4  #77  
  19 42DG          LDX   1  ACOMMUNE4(2)        [RESTORE PTR TO /CUNI BLOCK  
  18 42Y6    P8    ADN   4  11                  [ROUND UP,TO GET POINTER 
  17 43CQ          SRL   4  2                   [TO NEXT TABLE ENTRY 
   7 43XB          ADX   3  4
  20 44C2          LDX   4  SINF(3)             [LD COUNT OF CHARS OF NEXT ENTRY 
  21 44WL          BZE   4  NOMATCH             [COMMAND ERROR IF NO MATCHING PARAM  
   8 45TW          BUX   7  P5   
   2 47#Q
  18 47SB    P9                                 [SET UP ACTIVITY BLOCK   
   6 47SJ    #UNS AV7900S
   4 47SQ    (   
  20 47SY          CALL  6  VTYPEPB             [IF PB, ASSUME COMMS CHANNEL AND 
  18 47T6                                       [DEAL WITH 3RD PARAMETER 
   4 47T#    )   
   8 47TM          LDN   3  #100 
  10 47WY          ANDX  3  ACOMMUNE2(2) 
  16 47Y9          BZE   3  PASSIVE             [J IF NOT ACTIVE 
   9 47Y*          FSHCODE  B,XCODEB1
   4 47YF    (   
  20 47YK    #     THE ACTIVE CHANNEL IS NOT TO BE USED ON THE 'B' MACHINE IN A  
  11 47YP    #     SHARED FILESTORE SYSTEM.  
   4 47YT    #   
   9 47Y_          BRN      WRONGMACH
   4 47_5    #   
   5 47_9    XCODEB1 
   4 47_*    )   
  10 47_G          LDN   3  CONRECSIZE   
   5 482R    PASSIVE 
   9 4885          LDN   4  +AOLPT   
   8 48#2          SLL   4  12   
  16 48CX          LDN   5  AOLPTACLEN          [LENGTH OF AOLPT 
   7 48HS          ADX   5  3
   7 48J4          LDN   6  3
   6 48JS    #UNS AV7900S
   9 48K4          LDN   0  12+SLINKS
   6 48K#    #UNS AV7900S
   4 48KJ    #SKI
   9 48KS          LDN   0  11+SLINKS
  13 48L4          BXU   0  7,NOTNIP    [J IF NOT NIP
  20 48L#          LDN   5   AOLPTLEN+ASERLEN      [LENGTH OF OLPA IN NIP CASE   
  14 48LJ          LDN   6  10           [ATMARK FOR NIP 
   5 48LS    NOTNIP  
  17 48M6    [     CORRECTION FOR B-0818 - CHECK IF SAME AS PREVIOUS 
  13 48M8    [     UNIT NO. & IF SO REMOVE OLD ONE   
  14 48M=          FPCACA   3,2                 [FIND PCA
  11 48M#          HUNT2    3,APERI,APERIRES 
  20 48MB          FPERENTA 3,AWORK2(2),PB0818  [JUMP IF NO OLD UNIT NO. PRESENT 
  17 48MD          RELPERENT  AWORK2(2)         [ELSE REMOVE ENTRY   
   5 48MG    PB0818  
  10 48MJ    [     END OF B-0818 INSERT  
  20 48MP          GETACTX  4,5,1               [GET OFFLINE PERI ACTIVITY BLOCK 
  18 48RL                                       [& MARK AS LONGLOCK BLOCK
  17 49?=          LDX   3  BPTR(2)             [GET POINTER TO IT   
  17 49QW          LDX   0  JOBNO(2)            [READ JOBNO FROM PCA 
  21 4==G          STO   0  JOBNO(3)            [COPY JOBNO INTO OFF-LINE PERI ACT   
   7 4=Q6          ADX   1  7
   9 4?9Q          LINKSET  3,0(1)   
  19 4?BY          LDX   5  GSIGN               [MARK CHANNEL OPEN FOR ALLOT 
  10 4?J6          ORS   5  ALTMARK(3)   
   6 4?R_    #UNS AV7900S
   4 4?WJ    (   
  20 4?_7          CALL  7  VIRTUAL7900         [IF PB,PERFORM ADDITIONAL TASKS  
  19 4#3Q          LDN   6  9                   [PB : SET OLPA'S ATMARK = 9  
   4 4#6*    )   
   9 4#CF          STO   6  ATMARK(3)
  13 4#NL    #     SOME  OF HLSREAD CANNOT USE AWORK3
   9 4*8=          LDX   0  AWORK1(2)
  10 4*MW          STO   0  HLSPTYPE(3)  
  21 4B7G          LDX   0  GSIGN               [SET MARKER => EVENTS  TO BE CHAINED 
  10 4BM6          STO   0  FRECHADD(3)  
  18 4C6Q          PUTPEREN AWORK2(2),GSIGN     [MARK ALLOCATED IN PRB   
  18 4CLB          ROLPA                        [CHAIN AOLPT IN PCA CHAIN
   6 4CRJ    #UNS AV7900S
  21 4CYQ    VENDCOM                            [V7900 UNIT NO. PARAMETER MISSING/INV
  17 4D62    WRNGTP                             [   WRONG PER TYPE   
  18 4DKL    PFERR                              [   PERCON FORMAT ERROR  
   6 4F5=          ENDCOM
   6 4F78    #UNS AV7900S
   4 4F96    (   
   4 4F?4    #   
  21 4F*2    XBREAKIN                           [BREAKIN EVENT DURING PARSORT OF THE 
  21 4FBY                                       [VIRTUAL 7900 UNIT NUMBER PARAMETER  
   7 4FDW          COMBRKIN  
   4 4FGS    )   
  10 4FJW    # COMMAND ERROR REPORTING:  
  13 4G4G    XERRORTAB1     +INCDUMP            [A   
  13 4GJ6                   +JPARMIS            [B   
  13 4H3Q                   +JPARMIS            [C   
  13 4HHB                   +JPNAL              [D   
  13 4J32                   +JALOC              [E   
  13 4JGL                   +IMPER              [F   
   6 4JMS    #UNS AV7900S
  13 4JT2                   +JUNITERR           [G   
   6 4JWQ    #UNS  FSHB  
  13 4JYG                   +FSHACCESSER        [H   
  13 4K2=    XERRORTAB2     -1                  [A   
  13 4KFW                   +APER               [B   
  13 4K_G                   +JSETP3             [C   
  13 4LF6                   -1                  [D   
  13 4LYQ                   -1                  [E   
  13 4MDB                   -1                  [F   
   6 4MKJ    #UNS AV7900S
  13 4MQQ                   -1                  [G   
   6 4MSG    #UNS  FSHB  
  13 4MW=                   -1                  [H   
   6 4MY2    XERRORENT   
  18 4NCL    NOTPERM CALL 1 XER                 [A  'NOT FOR GENERAL USE'
  17 4NX=    NO1STP CALL 1  XER                 [B  NO FIRST PARAM   
  17 4PBW    NO2NDP CALL 1  XER                 [C  NO SECOND PARAM  
  19 4PWG    WRONGP CALL 1  XER                 [D  PER NOT *ED, *FR OR *FW  
   6 4Q3N    #UNS AV7900S
  17 4Q8W                                       [       OR *FH OR *PB
  16 4QB6    PERROR CALL 1  XER                 [E  PER IN USE   
   5 4QTQ    NOMATCH 
  18 4R*B    WRNGPR CALL 1  XER                 [F  NOT SCHEDULING INF   
   6 4RBJ    #UNS AV7900S
   4 4RCQ    (   
  21 4RDY    P3ERR  CALL 1  XER                 [G  UNIT %A IS NOT A SUITABLE DEVICE 
   4 4RF4    )   
   6 4RF8    #UNS  FSHB  
   4 4RF#    (   
   6 4RFD    WRONGMACH   
  21 4RFJ          CALL  1  XER                 [H COMMAND NOT FOR USE ON 'B' MACHINE
   4 4RFN    )   
   6 4RFS    #UNS AV7900S
   4 4RFY    (   
  21 4RG6    XJUNITERR                          [PARAMETER 3 IS NOT A VIRTUAL 7900   
  17 4RH#          LDX   5  JSCEQ(3)            [GET PARAMETER LENGTH
   8 4RJG          ANDN  5  #7777
  21 4RKN          OUTPARAM 5,JSCER,CPAR,JSCE   [OUTPUT PERIPHERAL DESCRIPTION PARAM 
   8 4RLW          BRN      P3ERR
  19 4RN4    XJALOC                             [UNIT %C IS ALREADY ALLOCATED
  21 4RP=          PARANOT  3,ZERO(1)           [PASS 3RD PARAM AT ANALYSIS LEVEL 0  
   9 4RQD          BRN      PERROR   
   4 4RRL    )   
  10 4RT2    XER   SBN   1  XERRORENT+1  
  10 4S#L          LDX   0  XERRORTAB1(1)
  10 4SS=          LDX   1  XERRORTAB2(1)
   8 4T?W          COMERRX  0,1  
   2 4TRG
   4 4WQQ    #END
   8 ____ ...62246501000100000000
