  15 22FL    #SEG  TESTMON                      [ROGER TERRY 
  12 22_=    #OPT  K0TESTMON=K0ALLGEO>K0GREATGEO 
   7 23DW    #LIS  K0TESTMON 
  12 23YG    #OPT  K6TESTMON=K6ALLGEO>K6GREATGEO 
   4 24D6    #   
   9 24XQ                   8HTESTMON
   4 25CB    #   
  21 25X2    # THIS SEGMENT HANDLES THE TESTMON MACRO WHEN AMON OF THE PCA INDICATES 
  19 26BL    # THAT MONITORING OF A PARTICULAR TYPE MAY BE REQUIRED.ON ENTRY 
  20 26W=    # ACOMMUNE5=TYPE OF MONITORING,ACOMMUNE6=TYPE/UNIT NO OF PERIPHERAL 
  16 27*W    # AND (ENTRY POINT 1 ONLY) ACOMMUNE4=ADDRESS OF PCA 
   4 27TG    #   
   7 28*6    # ENTRY POINTS  
   4 28SQ    #   
  19 29#B          SEGENTRY K1TESTMON,ZEP1      [ENTRY IF CURRENT ACT NOT PCA
  19 29S2          SEGENTRY K2TESTMON,ZEP2      [ENTRY IF CURRENT ACT IS PCA 
   4 2=?L    #   
   9 2=R=    TYPEMSK        #77000000
   9 2?=W    UNOMSK         #00770000
   9 2?QG    TYP13          #15000000
   4 2#=6    #   
  19 2#PQ    ZEP1                               [ENTRY IF CURRENT ACT NOT PCA
  10 2*9B          LDX   2  ACOMMUNE4(2) 
  19 2*P2    ZEP2                               [ENTRY IF CURRENT ACT IS PCA 
  11 2B8L          HUNT2    3,APED,AMONCAT,2 
   8 2BN=          LDX   2  FX2  
   9 2C7W    #SKI  K6TESTMON>599-599 
  12 2CMG          TRACE    ACOMMUNE5(2),TMNTYPE 
   9 2D76          LDX   0  BPALL(3) 
  10 2DLQ          ANDX  0  ACOMMUNE5(2) 
  21 2F6B          BNZ   0  UPFND               [ J IF MNTRG OF REQUD TYPE ON ALL PLS
  17 2FL2    #SKI  K6TESTMON>99-99              [REMOVE CHECK IN MK7 
   4 2G5L    (   
  10 2GK=          LDX   0  ACOMMUNE6(2) 
  10 2H4W          ANDX  0  ZFORMAT(1)   
  21 2HJG          BZE   0  ZFOK                [J IF PERIPHERAL NAME IN N/I FORMAT  
  10 2J46          GEOERR   1,PRLFRMAT   
   9 2JHQ ...ZFORMAT        #70077400
   4 2K3B    ZFOK
   4 2KH2    )   
   8 2L2L          LDCT  4  #777 
  10 2LG=          ANDX  4  ACOMMUNE6(2) 
  16 2L_W          SLL   4  3                   [PRL TYPE TO B0-5
  19 2MFG          BXU   4  TYP13(1),NOT13      [REPLACE TYPE 13 BY TYPE 6   
   8 2M_6          LDCT  4  #060 
   7 2NDQ    NOT13 NGN   5  1
  16 2NYB          LDX   6  ACOMMUNE5(2)        [MNTRG TYPE WORD 
  20 2PD2          STO   3  AWORK1(2)           [REMEMBER ADDR OF AMONCAT BLOCK  
  21 2PXL          CALL  7  TEMON               [J TO UPFND IF MNTRG OF REQUD TYPE ON
  20 2QC=                                       [ ALL UNIT NOS OF GIVEN PRL TYPE 
  20 2QWW          LDX   3  AWORK1(2)           [RESTORE ADDR OF AMONCAT BLOCK   
  10 2RBG          LDX   0  ACOMMUNE6(2) 
  20 2RW6          SLL   0  12                  [ISOLATE UNIT NO OF SPECD PERIPH 
  17 2S*Q          ORX   4  0                   [ AND INSERT IN X4   
   7 2STB          LDN   5  1
  21 2T*2          CALL  7  TEMON               [J TO UPFND IF MNTRG OF REQUD TYPE ON
  21 2TSL                                       [ SPECD TYPE/UNIT NO OF PEREIPHERAL  
   9 2W#=    #SKI  K6TESTMON>599-599 
  12 2WRW          TRACE    ACOMMUNE6(2),MNOTREQD
  18 2X?G          UPPLUS   3                   [ EXIT - MNTRG NOT REQUD 
   5 2XR6    UPFND   
   9 2Y=Q    #SKI  K6TESTMON>599-599 
  12 2YQB          TRACE    ACOMMUNE6(2),MREQUD  
  17 2_=2          UP                           [EXIT - MNTRG REQUD  
   4 2_PL    #   
  21 329=    # THIS SUBROUTINE CHECKS IF MONITORING OF SPECIFIED TYPE ON SPECIFIED   
  21 32NW    # TYPE/UNIT NO OF PERIPH IS SET IN 1  OF THE TABLES OF THE AMONCAT BLOCK
  20 338G    # ON ENTRY X3->AMONCAT BLOCK;X4 B0-5 TYPE B6-11 UNIT NO B12-23 ZERO 
  21 33N6    #          X5=+1 OR- 1 FOR TOP OR BOT TABLE  ; X6=MNTRG TYPE WORD;X7 LIN
  19 347Q    # ON EXIT  X3,X0 AND GEN6 DESTROYED. J TO UPFND IF MNTRG REQUD  
   4 34MB    #   
   5 3572    TEMON   
   8 35LL          STO   7  GEN6 
   8 366=          BNG   5  TE1  
  18 36KW          LDX   7  BTPOINT(3)          [COUNT OF TABLE ENTRIES  
  18 375G          ADN   3  BPTAB               [PNTR TO BASE OF TABLE   
   8 37K6          BRN      TE2  
  10 384Q    TE1   LDX   7  ALOGLEN(3)   
  10 38JB          SBN   7  BPTAB-A1+1   
  18 3942          SBX   7  BBPOINT(3)          [COUNT OF TABLE ENTRIES  
  18 39HL          ADX   3  ALOGLEN(3)          [PNTR TO BASE OF TABLE   
   8 3=3=          ADN   3  A1-1 
  17 3=GW    TE2   BZE   7  TE3                 [EXIT IF TABLE EMPTY 
   8 3?2G    TE5   LDX   0  0(3) 
   9 3?G6          ANDX  0  HALFTOP  
  19 3?_Q          BXU   0  4,TE4               [J IF TYPE/UNIT NOS NOT EQUAL
   8 3#FB          LDX   0  0(3) 
  19 3#_2          ANDX  0  6                   [IF TYPE/UNIT NOS COINCIDE,  
  20 3*DL          BNZ   0  UPFND               [ J IF SPECIFIED MNTRG BIT SET   
   7 3*Y=    TE4   ADX   3  5
   8 3BCW          BCT   7  TE5  
   9 3BXG    TE3   BRN      (GEN6)   
   4 3CC6    #   
   8 3CWQ          MENDAREA 10   
   4 3DBB    #END
   8 ____ ...25362475000200000000
