   4 22FL    #   
   7 22_=    #OPT  K0CHCH=0  
  11 23DW    #LIS  K0CHCH>K0KERNEL>K0ALLGEO  
  15 23YG    #SEG  CHCH                       [RUTH PORTER   
   4 24D6    #   
  19 24XQ    #     CHAPTER CHANGER DEALS WITH ACROSS(VAR), DOWN AND UP(PLUS) 
   9 25CB    #     CHAPTER CHANGES   
   4 25X2    #   
  11 26BL    #OPT  K6CHCH=K6KERNEL>K6ALLGEO  
   6 26W=    #SKI  IGP   
   4 27*W    (   
  18 27TG    # ENTRY FOR GETPROC IF MACRO DISCOVERS CHAPTER NOT IN CORE  
   9 28*6          LABFIX   HTPROC   
  19 28SQ    TPROC LDN   0  2(1)                [X0 -> INST AFTER LINK PAIR  
   9 29#B          LABFIX   HTPROX   
   5 29S2    TPROX   
  17 2=?L          SQUMP                        [DUMP ACCUMULATORS   
  18 2=R=          SQOSS2                       [CALCULATE & DEPOSIT LINK
   9 2?=W          LDX   0  ALINK1(2)
  15 2?QG          STO   0  ACOMMUNE9(2)        [SAVE LINK   
   8 2#=6          LDX   0  0(1) 
  15 2#PQ          STO   0  ALINK1(2)           [GETPROC LINK
   8 2*9B          LDCT  0  #20  
  17 2*P2          ORS   0  CLONG1(2)           [SET GETPROC MARKER  
   9 2B8L          TRANSFIX BRN,BCHR 
   4 2BN=    )   
  21 2C7W    [ENTRY FOR THE ACROSS CHAPTER CHANGE. DUMP ACCUMULATORS AND NEW LINK,   
  12 2CMG    [THEN ENTER CHAPTER ENTRY ROUTINE.  
   9 2D76          LABFIX   HTCHAP   
  19 2DLQ    TCHAP SQUMP                        [ DUMP ACCUMULATORS & SET X2.
   8 2F6B          LDX   0  0(1) 
  14 2FL2          STO   0  ALINK1(2)           [SET LINK
   9 2G5L          FIXTRA   CHSTAC   
  20 2GK=          BRN      TENT                [GO TO CHAPTER ENTERING ROUTINE. 
  21 2H4W    [ENTRY FOR THE UP CHAPTER CHANGE. RESTORE THE ACCUMULATORS AND LINK FROM
  17 2HJG    [THE LINK BLOCK, THEN ENTER THE CHAPTER ENTRY ROUTINE.  
   9 2J46          LABFIX   HTCHAPUP 
   5 2JHQ    TCHAPUP 
   7 2K3B          LDN   0  0
   8 2KH2          LABFIX   HTUP2
   4 2L2L    TUP2
   8 2LG=          LDX   2  FX2  
  17 2L_W          LDX   3  ALINKRING(2)        [ADDRESS OF LINKSTACK
  17 2MFG    #SKI  K6CHCH                       [ CHECK TYPE WORD.   
   4 2M_6    (   
  19 2NDQ          BXU   3  0(3),T6             [OK IF LINKSTACK BLOCK EXISTS
  19 2NYB          GEOERR   BRIEFPM,CHCH4       [ACTIVITY BLOCK IS ILLEGAL   
   4 2PD2    T6  
   4 2PXL    )   
  18 2QC=          SBN   3  BLINKRING           [GET TO START OF LINK BLK
  10 2QPM ...      LDX   4  ALOGLEN(3)   
  12 2R44 ...      SBN   4  ALINK    [NEW LOG LEN
  15 2RBF ...      BNG   4  (GEOERR)    [J IF UP TOO OFTEN   
  14 2RNW ...      STO   4  ALOGLEN(3)   [UPDATE LOG LEN 
  16 2S3? ...      ADX   3  4            [PTR TO START OF LINK   
  18 2S*Q          ADN   3  A1                  [START OF CURRENT LINK   
   9 2STB          LDN   4  ACC3(2)  
  19 2T*2          MOVE  3  ALINK               [MOVE LINK TO ACTIVITY BLOCK 
  18 2TSL          ADS   0  ALINK1(2)           [STEP LINK AS REQUIRED   
   9 2W#=          FIXTRA   CHSTUP   
   8 2WRW          BRN      TENT 
  21 2X?G    [ENTRY FOR THE DOWN CHAPTER CHANGE. DUMP THE ACCUMULATORS AND LINKS IN  
  20 2XR6    [THE ACTIVITY BLOCK AND A LINK BLOCK, THEN TRY TO ENTER THE CHAPTER.
  10 2Y=Q          LABFIX   HTCHAPDOWN   
  18 2YQB    TCHAPDOWN   SQUMP                  [X2 -> ACTIVITY BLOCK.   
  16 2_=2          LDN   0  HLINK(1)            [STEP PAST LINK  
   6 2_PL          SQOSS2
   7 329=    #SKI  K6CHCH>2-2
   4 32NW    (   
   8 338G          BPZ   0  T2   
  10 33N6          GEOERR   BRIEFPM,CHCH5
   8 347Q    T2    SBN   0  #7777
   8 34MB          BNG   0  T3   
  10 3572          GEOERR   BRIEFPM,CHCH6
   4 35LL    T3  
   4 366=    )   
   9 36KW          LABFIX   CHCHSPEC 
  16 375G          LDX   0  0(1)                [X0 CONTAINS LINK
  19 37K6          LDX   3  ALINKRING(2)        [POINTER TO LINKSTACK BLOCK  
  16 384Q          BXU   3  0(3),QD1            [J IF IT EXISTS  
   9 38JB          LDN   4  ALINK+2  
   4 3942    QD3 
  15 39HL          STO   0  CHTPRIV1            [KEEP COPY   
  18 3=3=          GETLINK                      [OF REQUIRED LENGTH IN X4
  17 3=GW          FINDCORE 3                   [ADDR OF NEW LINK BLK
   9 3?2G          LDCT  0  BLINK/8  
  16 3?G6          STO   0  ATYPE(3)            [NAME LINK BLOCK 
  10 3?_Q          LDX   2  ALINKRING(2) 
   8 3#FB          TXU   2  0(2) 
   8 3#_2          BCC      QD2  
  16 3*DL          SBN   2  BLINKRING           [ADJUST ->START  
  15 3*Y=          BLOCKCOPY                    [COPY ACROSS 
   9 3BCW          FIXTRA   CHSTFL   
   8 3BXG          BRN      QD5  
   4 3CC6    QD2 
  10 3CWQ          LDN   1  BLINKRING(3) 
   8 3DBB          ENRING   1,2  
   9 3DW2          FIXTRA   CHSTFLB  
   4 3F*L    QD5 
  10 3FT=          LDX   4  ALOGLEN(3)   
   9 3G#W          LDX   0  CHTPRIV1 
   9 3GSG          FIXTRA   CHSTGL   
   8 3H#6          BRN      QD4  
   4 3HRQ    QD1 
  18 3J?B          SBN   3  BLINKRING           [GET TO START OF LINK BLK
  10 3JR2          LDX   4  ALOGLEN(3)   
  19 3K=L          ADN   4  ALINK               [NEW LOGICAL LENGTH REQUIRED 
   9 3KQ=          LDX   5  ASIZE(3) 
  21 3L9W          SBN   5  A1                  [MAXIMUM POSSIBLE LOGICAL LENGTH NOW 
  15 3LPG          BXL   5  4,QD3               [J NOT ENOUGH
  18 3M96          STO   4  ALOGLEN(3)          [SET NEW LOGICAL LENGTH  
   9 3MNQ          FIXTRA   CHSTGLB  
   4 3N8B    QD4 
   8 3NN2          SBN   4  ALINK
  19 3P7L          ADN   4  A1(3)               [DESTINATION ADDR OF NEW LINK
   9 3PM=          LDN   3  ACC3(2)  
   8 3Q6W          MOVE  3  ALINK
   9 3QLG          STO   0  ALINK1(2)
   7 3QW# ...  LABFIX TENT   
   4 3R66    TENT
   6 3R=3 ...#UNS  ICTON 
   4 3R*Y ...#SKI
   4 3RFT ...(   
   8 3RKQ          LDN   1  K8   
   8 3S5B          TXU   1  K8   
   9 3SK2          TRANSFIX BCS,FLIST
   7 3T4L    #SKI  ANEWTENT  
   4 3TJ=    (   
   8 3W3W          LDN   1  K3   
   8 3WHG          TXU   1  K3   
   9 3X36          TRANSFIX BCS,FLIST
   4 3XGQ    )   
   4 3XK* ...)   
   6 3XMY ...#UNS  ICTON 
   4 3XQH ...(   
   7 3XT6 ...      CHCHCOOR  
   4 3XXP ...)   
   8 3Y2B          FIXTRA   FZCO 
   8 3YG2          LABFIX   FZCO 
  21 3Y_L                                       [WHEN IT WANTS TO ENTER THE CHAPTER  
  16 3_F=                                       [OF AN ACTIVITY  
  16 3_YW          NULL                         [GAP FOR MENDS   
  17 42DG          LDX   1  ALINK1(2)           [GET SEGMENT NUMBER  
   7 42Y6    #SKI  K6CHCH>9-9
   4 43CQ    (   
   9 43XB          BZE   1  (GEOERR) 
   4 44C2    SKO 
   4 44WL    )   
   9 45B=          FIXTRA   CHSTLK   
  19 45TW          SRL   1  12                  [X1 NOW HAS CHAPTER NUMBER   
  15 46*G          FIXTRA   ADPCHCH             [FOR DATAPASS
   9 46T6          LDXC  1  KTAB(1)  
   9 47#Q          TRANSFIX BCC,BCHR 
  19 47SB          BZE   1  SFIXED              [NO CHAINING IF FIXED CORE   
   6 48#2    #SKI  IGP   
   4 48RL    (   
   8 49?=          LDCT  0  #20  
   9 49QW          ANDX  0  CLONG1(2)
   8 4==G          BZE   0  XOK  
   9 4=Q6          ERS   0  CLONG1(2)
  10 4?9Q          LDX   0  ACOMMUNE9(2) 
   9 4?PB          STO   0  ALINK1(2)
   9 4#92          TRANSFIX BRN,FZCO 
   4 4#NL    )   
   4 4*8=    XOK 
   8 4*MW          TXU   1  BCHAP
  21 4B7G          BCC      XCHN                [DONT BOTHER TO RECHAIN IF AT FRONT  
  19 4BM6          LDX   2  FPTR(1)             [CHAIN AT FRONT OF CH CHAIN  
   9 4C6Q          LDX   3  BPTR(1)  
   9 4CLB          STO   2  FPTR(3)  
   9 4D62          STO   3  BPTR(2)  
   8 4DKL          LDX   2  BCHAP
   9 4F5=          STO   1  BPTR(2)  
   9 4FJW          STO   2  FPTR(1)  
   8 4G4G          LDN   0  BCHAP
   9 4GJ6          STO   0  BPTR(1)  
   8 4H3Q          STO   1  BCHAP
   8 4HHB          LDX   2  FX2  
   4 4HJH ...XCHN
  19 4HTR ...      FIXTRA   CURFIX1            [ LABEL FOR MACRO ^CONCURR^   
   9 4J73 ...      LABFIX   CURFIX1  
   4 4JD? ...#   
  16 4JFD ...      LDN   1  A1(1)               [GET X1 FOR FX1  
   5 4JGL    SFIXED  
   8 4K2=          STO   1  FX1  
   7 4KFW          LDN   3  1
   8 4K_G          LDN   0  #7777
  17 4LF6          ADSC  3  CHCHCOUNT           [UPDATE CT FO CHCHS  
   9 4LYQ          ANDX  0  ALINK1(2)
   8 4M8J ...      FIXTRA CHKP   
  10 4MDB          ADS   3  ACTCHCH(2)   
  17 4MY2          SUNDUMP                      [UNDUMP ACCUMULATORS 
  14 4NCL          ADX   0  1                   [DATUMIZE
  13 4NX=          EXIT  0  0                   [EXIT
   4 4NX? ...#   
  16 4NX# ...#     2960 DUAL DME EXEC/G3 CONCURRENCY ENHANCEMENT 
  16 4NX* ...#     SWITCHED ON BY RESTORE TIME MACRO ^CONCURR^   
   4 4NXB ...#   
  16 4NXC ...#     BIT 9 OF KTAB ENTRY IF SET INDICATES CHAPTER  
  16 4NXD ...#     NOT ALLOWED TO RUN CONCURRENTLY WITH EXECUTIVE
   4 4NXF ...#   
   9 4NXG ...      FIXTRA    CURFIX2 
   9 4NXH ...      LABFIX    CURFIX2 
   9 4NXJ ...      LDX   0  BACK2(1) 
   7 4NXK ...      SLL   0  9
  19 4NXL ...      BPZ   0  NEXECFLAG          [ JIF NEW CHAPTER IS UNTRUSTED
  18 4NXM ...      STO   0  FBIDMEM            [REMEMBER WE ARE TRUSTED  
  21 4NXN ...      ENTRUST  RUNIT,XECRUNS      [ MAKE US ^TRUSTED^-JIF CAN'T TO 'XECR
   6 4NXP ...NEXECFLAG   
  18 4NXQ ...      STOZ     FBIDMEM            [REMEMBER WE ARE UNTRUSTED
  17 4NXR ...      UNTRUST                     [ MAKE US ^UNTRUSTED^ 
   5 4NXS ...RUNIT   
  21 4NXT ...      NGS   1  ACTMEMORY          [ FORGET ANY ACT. NO. BEING REMEMBERED
  15 4NXW ...      LDN   1  A1(1)             [GET X1 FOR FX1
   9 4NXX ...      BRN      SFIXED   
   4 4NXY ...#   
  17 4NYJ ...#     EXECUTIVE IS RUNNING SO WE CANT RUN THIS ACTIVITY 
  17 4N_9 ...#     REMEMBER ACT NO. IF FIRST ONE THIS CHAPTER CHANGE 
  16 4N_W ...#     AND PUT IT BACK ON LIST. GET NEXT ACTIVITY.   
  18 4P2H ...#     IF FAILS AGAIN, RETURN HERE BUT NOW ACTMEMORY IS +VE  
  18 4P38 ...#     SO CHECK IF WE'RE BACK TO STARTING ACTIVITY. IF NOT,  
  16 4P3T ...#     GET NEXT ACTIVITY AGAIN, ELSE GIVE UP BY DOING
  11 4P4G ...#     UNCONDITIONAL ^ENTRUST^.  
   4 4P57 ...#   
   5 4P5S ...XECRUNS 
  17 4P6F ...      LDX   0  ACNUM(2)           [ CHECK ACT. NUMBER   
  19 4P76 ...      TXU   0  ACTMEMORY          [ WITH FIRST ONE THIS CH.CH.  
  19 4P7R ...      BCS      TRYNEXT            [ JIF NOT GOT TO 'END' OF LIST
  15 4P8D ...      ENTRUST                     [ ELSE GIVE UP
  20 4P95 ...      BRN      RUNIT              [ WE NOW KNOW EXEC IS NOT RUNNING 
   5 4P9Q ...TRYNEXT 
  20 4P=4 ...      STOZ     FBIDMEM              [REMEMBER WE ARE STILL UNTRUSTED
  19 4P=C ...      LDX   1  ACTMEMORY          [ CHECK IF FIRST TIME THROUGH 
  14 4P?4 ...      BPZ   1  NOTFIRST           [ JIF NOT 
  20 4P?P ...      STO   0  ACTMEMORY          [ ELSE REMEMBER STARTING ACT. NO. 
   5 4P#B ...NOTFIRST
  19 4P*3 ...      TRANSFIX BRN,CURFIX3        [ APPROX = 'FPUT' + 'COOR1'   
   4 4P*N ...#   
   4 4PBW    #   
   4 4PWG    #   
   4 4QB6    #   
   4 4QTQ    #   
   4 4R*B    #   
   4 4RT2    #   
   4 4S#L    #END
   8 ____ ...72023204001300000000
