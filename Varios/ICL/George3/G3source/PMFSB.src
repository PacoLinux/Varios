   7 22_=    #OPT  K0PMFSB=0 
  11 23DW    #LIS  K0PMFSB>K0POST>K0ALLGEO   
  17 23YG ...#SEG  PMFSB84                      [KAREL KOSKUBA - DIAG
   4 24XQ    #   
  21 25CB    #              THIS SEGMENT CONTAINS SUBROUTINES FOR FILE STORE POSTM.  
   4 25X2    #   
   9 26BL                   8HPMFSB  
   4 26W=    #   
   9 27*W    #     ENTRY POINT TABLE 
   4 27TG    #   
  11 28*6 ...      SEGENTRY K1PMFSB,XK1PMFSB 
  11 28F3 ...      SEGENTRY K2PMFSB,XK2PMFSB 
   4 28JY ...#   
   5 28NT ...#SKI G3 
   4 28SQ ...(   
   9 28YM ...      FIXTRA   JJRECBLK 
   9 294J ...      BRN      XJRECBLK1
   4 298F ...)   
   4 29#B    #   
   9 29S2 ...      FIXTRA   FFPRLIN  
   9 2=?L          BRN      XFPRLIN  
   4 2=R=    #   
   9 2?=W ...      FIXTRA   FFPRRCD  
   9 2?QG          BRN      XFPRRCD  
   4 2#=6    #   
   9 2#PQ ...      FIXTRA   JJGETFCB1
   9 2*9B          BRN      XGETFCB1 
   4 2*P2    #   
   9 2B8L ...      FIXTRA   JJGETFCB 
   9 2BN=          BRN      XGETFCB  
   4 2C7W    #   
   4 2DLQ    #   
  21 2F6B    XSTART                             [START ADDR. OF PSEUDO ACTIVITY BLOCK
  19 2FL2    #              THERE FOLLOW WORDS TO FILL PSEUDO ACTIVITY BLOCK 
   9 2G5L          FIXTRA   FREMAIN  
  19 2GK=    REMAIN         +0                  [REMAINDER OF WORDS IN BLOCK 
   9 2H4W          FIXTRA   FMODBLK  
  20 2HJG    MODBLK         +0                  [BLOCKLET MODIFIER FOR PRINTING  
   8 2J46    XOCT           +8   
  17 2JHQ    X128           +128                [128 WORDS PER BLOCK 
   8 2K3B    XLINK          +0   
   8 2KH2    WLBONE         +120 
   8 2L2L    X512           +512 
   4 2LG=    X   
   9 2L_W    #REP  CHAINADD-X+XSTART 
   8 2MFG                   +0   
   8 2M_6    XCHAD          +0   
  10 2NDQ    #     MORE FILLERS NOW!!!   
  18 2NYB    XBACK          +0                  [BACKWARD CHAINING IND.  
  19 2P5J ...XBFILE         +BFILE              [BASE OF FILE CHAIN : IN SHFS
  20 2P=Q ...                                   [TESTING VERSION IF B MACHINE WAS
  21 2PCY ...                                   [RUNNING AT TIME OF BREAK THIS WILL  
  20 2PK6 ...                                   [BE CHANGED TO BIPL WHEN PRINTING
  15 2PQ# ...                                   [RECALL FILE 
   8 2PXL    XLINK3         +0   
  16 2QC=    TEXT10         36HBLOCK NO. GIVES B/S RES. VIOLATION
  18 2QWW    TEXT11         40HPERIPHERAL TRANSFER FAILURE ON B/S DEV.   
  15 2RBG    PRH1           32H     RECORD HEADER OVERWRITTEN
   9 2RL# ...XPMFSBEND      +PMFSBEND
   4 2RW6    #   
   4 2S*Q    Y   
   8 2STB    #REP  CPPTR-Y+XSTART
   8 2T*2                   +0   
   9 2TSL          FIXTRA   JBSPTR   
  21 2W#=    XBSPTR         +0                  [THE PTR,SET UP AT BLOQSTART + CPPTR 
  21 2WRW    XBSRW1         +0                  [& REPLY WORD-FOR COMMUNICATION TO   
  15 2X?G                                       [& FROM EXEC.
   4 2X#3 ...#   
   5 2X#J ...#SKI G3 
   4 2X*5 ...(   
   9 2X*L ...MASK1          #177777  
   8 2XB7 ...XACT           #60  
  10 2XBN ...XCONS          +CONSA+CNTRAL
  10 2XC9 ...XACEND         +BACT+ACTRING
  11 2XCQ ...XCONB          +CONBUFF+ACTIVE  
   8 2XD? ...SAVE2          +0   
  10 2XDS ...XFRWB          +FILE+FRWB   
  11 2XF* ...XCNTRL         +CONBUFF+CNTRL   
   9 2XFW ...XSYST          12HSYSTEM
  11 2XGC ...XRECALL        12HRECALL FILE   
   8 2XGY ...SLINK          +0   
   9 2XHF ...NL             #76327474
   8 2XJ2 ...XDOLLER        #74  
  15 2XJH ...TEXT1          27HPRINT OF RECALL FILE ENSUES   
  17 2XK4 ...TEXT2          37H       CURRENT PAGE PRINTED FROM CORE 
  17 2XKK ...TEXT3          36H       STORED PAGE PRINTED FROM CORE  
  12 2XL6 ...TEXT4          18HCURRENT PAGE EMPTY
  21 2XLM ...TEXT6          52HBREAK IN ACTIVITY CHAIN : PRINT OF STORED PAGES ABAN  
   9 2XM8 ...               5HDONED  
  17 2XMP ...TEXT7          36HFILE/FRWB CORRUPT SEE ACTIVITY CHAIN  
  18 2XN= ...TEXT8          41HCONBUFF/ACTIVE CORRUPT SEE ACTIVITY CHAIN 
  21 2XNR ...TEXT9          52HBREAK IN ACTIVITY RING : PRINT OF RECALL FILE ABANDO  
   8 2XP# ...               3HNED
  15 2XPT ...TEXT12         27HEND OF PRINT OF RECALL FILE   
   4 2XQB ...)   
  11 2XR6    [     NOW TO FILL PSEUDO BLOCK  
   4 2Y=Q    #   
   5 2YQB    XK1PMFSB
   8 2_28 ...      LDX   1  FXPM1
  10 2_=2 ...      ADS   1  XPMFSBEND(1) 
  12 2_FS ...      PMOVE    PMFS,XPMFSBEND(1),1,2
   4 2_FW ...#   
  13 2_FY ...#     ENTRY POINT TO PRINT RECALL FILE  
   4 2_G2 ...#   
   5 2_G4 ...XK2PMFSB
   5 2_G6 ...#SKI G4 
   9 2_G8 ...      BRN      (JPMSEG) 
   5 2_G= ...#SKI G3 
   4 2_G# ...(   
  18 2_GB ...      JENVNOT  XC5,DME             [IF NOT DME NOTHING TO DO
  19 2_GD ...      LDX   1  FXPM1               [READ DOWN PMFS AFTER PMFSB  
  10 2_GG ...      ADS   1  XPMFSBEND(1) 
  12 2_GJ ...      PMOVE    PMFS,XPMFSBEND(1),,2 
  17 2_GL ...      LDX   1  FXPM2                [DATUMISE AFRESBS   
  10 2_GN ...      ADS   1  AFRESBS(1)   
   4 2_GQ ...#   
  20 2_GS ...#     SEARCH ACTIVITY RING STARTING AT BASE IN BLANKET FOR THE CONSA
  18 2_GW ...#     CNTRL BLOCK. SEARCH IS FORWARD UNLESS A BREAK IS FOUND
   4 2_GY ...#   
   8 2_H2 ...      LDX   1  FXPM1
  19 2_H4 ...      LDN   2  BACT+ACTRING        [LOAD BASE OF ACTIVITY RING  
   9 2_H6 ...XC1   LDX   2  FPTR(2)  
  18 2_H8 ...      TXU   2  XACEND(1)           [END OF ACTIVITY RING?   
  13 2_H= ...      BCC      XC5                 [YES 
  15 2_H# ...      PMCORES  2,XC2               [VALID FPTR? 
   9 2_HB ...      SBN   2  ACTRING  
   9 2_HD ...      LDX   4  ATYPE(2) 
   8 2_HG ...      SRL   4  12   
  17 2_HJ ...      TXU   4  XCONS(1)            [IS IT CONSA CNTRL   
  13 2_HL ...      BCC      XC6                 [YES 
   9 2_HN ...      ADN   2  ACTRING  
   8 2_HQ ...      BRN      XC1  
   4 2_HS ...#   
   9 2_HW ...#     SEARCH BACKWARDS  
  10 2_HY ...XC2   LDN   2  BACT+ACTRING 
   9 2_J2 ...XC3   LDX   2  BPTR(2)  
  17 2_J4 ...      TXU   2  XACEND(1)           [END OF ACTIVITY RING
  13 2_J6 ...      BCC      XC5                 [YES 
  15 2_J8 ...      PMCORES  2,XC4               [VALID BPTR  
   9 2_J= ...      SBN   2  ACTRING  
   9 2_J# ...      LDX   4  ATYPE(2) 
   8 2_JB ...      SRL   4  12   
  17 2_JD ...      TXU   4  XCONS(1)            [IS IT CONSA CNTRL   
  13 2_JG ...      BCC      XC6                 [YES 
   9 2_JJ ...      ADN   2  ACTRING  
   8 2_JL ...      BRN      XC3  
   4 2_JN ...#   
  11 2_JQ ...#     BREAK IN ACTIVITY CHAIN   
  10 2_JS ...XC4   PMESSAGE TEXT9(1),14  
   9 2_JW ...XC5   BRN      (JPMSEG) 
   4 2_JY ...#   
  12 2_K2 ...#     LOCATE AND PRINT CURRENT PAGE 
   4 2_K4 ...#   
   9 2_K6 ...XC6   LDX   2  FPTR(2)  
  16 2_K8 ...      PMCORES  2,XC9               [VALID POINTER   
  16 2_K= ...      LDCH  4  ATYPE(2)            [IS IT ACTIVITY  
   9 2_K# ...      TXL   4  XACT(1)  
  17 2_KB ...      BCC      XC5                 [YES END OF SEARCH   
   9 2_KD ...      LDX   4  ATYPE(2) 
   8 2_KG ...      SRL   4  12   
  16 2_KJ ...      TXU   4  XCONB(1)            [CONBUFF ACTIVE  
  13 2_KL ...      BCS      XC6                 [NO  
  14 2_KN ...      CALL  7  (JPRINT)            [NEW PAGE
  14 2_KQ ...      PMESSAGE TEXT1(1),7          [HEADING 
  10 2_KS ...      PMESSAGE TEXT2(1),10  
   8 2_KW ...      SMO      FXPM2
  19 2_KY ...      LDX   5  AFRESBS             [MOVE BLOCK TO END OF PMFS   
   8 2_L2 ...      LDN   4  A1(2)
   8 2_L4 ...      MOVE  4  256  
  15 2_L6 ...      STO   2  SAVE2(1)            [PRESERVE X2 
  15 2_L8 ...      CALL  0  XJRECBLK2           [PRINT BLK   
  13 2_L= ...      BRN      XC7                 [OK  
  14 2_L# ...      PMESSAGE TEXT8(1),11         [ERROR   
   4 2_LB ...#   
  20 2_LD ...#     SEARCH FOR FILE/FRWB BLOCKS AND PRINT ONLY THERE IF BREAK IN  
   6 2_LG ...#     EMS   
   4 2_LJ ...#   
  15 2_LL ...XC7   LDX   2  SAVE2(1)            [RESTORE X2  
   9 2_LN ...XC8   LDX   2  FPTR(2)  
  16 2_LQ ...      PMCORES  2,XC9               [VALID POINTER   
   9 2_LS ...      LDCH  4  ATYPE(2) 
  16 2_LW ...      TXL   4  XACT(1)             [IS IT ACTIVITY  
  17 2_LY ...      BCC      XC10                [YES END OF SEARCH   
   9 2_M2 ...      LDX   4  ATYPE(2) 
   8 2_M4 ...      SRL   4  12   
  16 2_M6 ...      TXU   4  XFRWB(1)            [IS IT FILE/FRWB 
  13 2_M8 ...      BCS      XC8                 [NO  
  19 2_M= ...      SMO      FXPM2               [MOVE BLOCK TO END OF PMFS   
   9 2_M# ...      LDX   5  AFRESBS  
   8 2_MB ...      LDN   4  A1(2)
   8 2_MD ...      MOVE  4  256  
  16 2_MG ...      PMESSAGE TEXT3(1),9          [PRINT HEADING   
  15 2_MJ ...      STO   2  SAVE2(1)            [PRESERVE X2 
  15 2_ML ...      CALL  0  XJRECBLK1           [PRINT BLOCK 
  13 2_MN ...      BRN      XC7                 [OK  
  14 2_MQ ...      PMESSAGE TEXT7(1),9          [ERROR   
   8 2_MS ...      BRN      XC7  
  20 2_MW ...XC9   PMESSAGE TEXT6(1),15         [ABANDON PRINT OF STORED PAGES   
   8 2_MY ...      BRN      XC10 
   4 2_N2 ...#   
  12 2_N4 ...#     CHECK IF RECALL FILE SET UP   
   4 2_N6 ...#   
  18 2_N8 ...XC10  LDX   2  CONSCNTRL           [POINTER TO CONBUFF/CNTRL
  16 2_N= ...      BZE   2  XC5                 [NO CONBUFF/CNTRL
   9 2_N# ...      LDX   4  ATYPE(2) 
   8 2_NB ...      SRL   4  12   
   9 2_ND ...      TXU   4  XCNTRL(1)
  16 2_NG ...      BCS      XC5                 [NO CONBUFF/CNTRL
   9 2_NJ ...      LDX   4  COVID(2) 
  16 2_NL ...      ANDN  4  #4000               [IS BIT 12 SET   
  18 2_NN ...      BZE   4  XC5                 [RECALL FILE NOT SET UP  
   4 2_NQ ...#   
  15 2_NS ...#     SEARCH FILE CHAIN FOR FCB OF RECALL FILE  
   4 2_NW ...#   
   6 2_NY ...#UNS FSHTEST
   4 2_P2 ...(   
   4 2_P4 ...#   
  16 2_P6 ...#     IN THE SHFS TESTING VERSION IF WHEN PM TAKEN  
  15 2_P8 ...#     MACHINE B WAS RUNNING RECALL FILE WILL BE 
  12 2_P= ...#     IN OTHER FILE CHAIN BASE BIPL 
   4 2_P# ...#   
   9 2_PB ...      LDX   4  INCWORD  
   8 2_PD ...      ANDN  4  #2000
  16 2_PG ...      BZE   4  XNOTB               [NOT IN B MACHINE
  19 2_PJ ...      LDN   2  BIPL                [CHANGE FILE CHAIN BASE FOR  
  17 2_PL ...      STO   2  XBFILE(1)           [SUBROUTINE XGETFCB  
   5 2_PN ...XNOTB   
   4 2_PQ ...)   
   8 2_PS ...      LDX   1  FXPM2
   9 2_PW ...      CALL  0  XGETFCB1 
   8 2_PY ...      BRN      XF1  
  20 2_Q2 ...      BRN      XC5                 [BREAK OR END CHAIN NOTHING TO DO
  16 2_Q4 ...XF1   LDX   2  JXFCB(1)            [ADDR FILE/FCB   
   8 2_Q6 ...      LDX   1  FXPM1
  17 2_Q8 ...      LDX   4  FUSER1(2)           [IS USER NAME SYSTEM 
   9 2_Q= ...      LDX   5  FUSER2(2)
   9 2_Q# ...      LDX   6  FUSER3(2)
   9 2_QB ...      TXU   4  XSYST(1) 
  10 2_QD ...      TXU   5  XSYST+1(1)   
  10 2_QG ...      TXU   6  XSYST+2(1)   
  13 2_QJ ...      BCS      XF2                 [NO  
  18 2_QL ...      LDX   4  FLOC1(2)            [IS FILE NAME RECALL FILE
   9 2_QN ...      LDX   5  FLOC2(2) 
   9 2_QQ ...      LDX   6  FLOC3(2) 
  10 2_QS ...      TXU   4  XRECALL(1)   
  10 2_QW ...      TXU   5  XRECALL+1(1) 
  10 2_QY ...      TXU   6  XRECALL+2(1) 
  13 2_R2 ...      BCC      XF3                 [YES 
   8 2_R4 ...XF2   LDX   1  FXPM2
   9 2_R6 ...      CALL  0  XGETFCB  
   8 2_R8 ...      BRN      XF1  
  18 2_R= ...      BRN      XC5                 [RECALL FILE NOT FOUND   
   4 2_R# ...#   
  10 2_RB ...#     PRINT BLOCKS FROM FILE
   4 2_RD ...#   
   8 2_RG ...XF3   LDX   1  FXPM2
   9 2_RJ ...      LDX   4  JMASK1(1)
  10 2_RL ...      STO   4  JXBITAB(1)   
  10 2_RN ...      LDN   4  JXBITAB(1)   
  10 2_RQ ...      LDN   5  JXBITAB+1(1) 
   8 2_RS ...      MOVE  4  20   
   8 2_RW ...      LDX   2  FXPM2
   9 2_RY ...      ADX   2  JZDECANT 
   8 2_S2 ...      CALL  0  (2)  
   7 2_S4 ...      BRN      /
   8 2_S6 ...      LDX   1  FXPM1
  10 2_S8 ...      PMESSAGE TEXT12(1),7  
  20 2_S= ...      SMO      FXPM1               [RESTORE DEVICE LIST IF NECESSARY
   9 2_S# ...      LDX   5  XBSPTR   
   8 2_SB ...      BZE   5  XC5  
   9 2_SD ...      LDN   4  JBSREC   
   8 2_SG ...      ADX   4  FXPM2
   8 2_SJ ...XF4   JBUSY    5,XF4
   8 2_SL ...      MOVE  4  K53  
   8 2_SN ...      BRN      XC5  
   4 2_SQ ...#   
  18 2_SS ...#              ROUTINE TO PRINT A PAGE FROM THE RECALL FILE 
   4 2_SW ...#   
  15 2_SY ...#     ENTRY XJRECBLK1 TO PRINT COMPLETED PAGES  
  14 2_T2 ...#     ENTRY XJRECBLK2  TO PRINT CURRENT PAGE
   9 2_T4 ...#     ENTRY    X0=LINK  
  13 2_T6 ...#              MODBLK=BLOCKLET MODIFIER 
  12 2_T8 ...#     EXIT  0  AFTER PRINTING PAGE  
  12 2_T= ...#              ALL ACCUMS UNDEFINED 
  11 2_T# ...#     EXIT  1  ERROR IN BLOCK   
   4 2_TB ...#   
   6 2_TD ...XJRECBLK1   
   9 2_TG ...      STO   0  SLINK(1) 
   8 2_TJ ...      SMO      FXPM2
   9 2_TL ...      LDX   2  AFRESBS  
  17 2_TN ...      ADX   2  MODBLK(1)           [BLOCKLET MODIFIER   
  16 2_TQ ...      LDX   4  0(2)                [CHARACTER COUNT 
   7 2_TS ...      SBN   4  1
  20 2_TW ...      BNG   4  XJREC8              [CHARACTER COUNT < OR = 0  ERROR 
   8 2_TY ...      SBN   4  1024 
  15 2_W2 ...      BPZ   4  XJREC8              [> 1024 ERROR
   8 2_W4 ...      LDX   4  0(2) 
  19 2_W6 ...      SRL   4  2                   [ADJUST COUNT TO ONLY INCLUDE
  14 2_W8 ...      LDX   3  1(2)                [WORDS IN
  13 2_W= ...      SBN   3  11                  [PAGE
   7 2_W# ...      SBX   4  3
  21 2_WB ...      LDN   0  0                   [SET SWITCH TO INDICATE COMPLETE BLK 
   5 2_WD ...XJREC1  
  19 2_WG ...      LDX   3  1(2)                [CHECK IF START OF PAGE VALID
   8 2_WJ ...      SBN   3  20   
  14 2_WL ...      BNZ   3  XJREC8              [NO-ERROR
   8 2_WN ...      LDX   3  1(2) 
   7 2_WQ ...      SBN   3  9
  17 2_WS ...      ADX   2  3                   [START OF MESSAGES   
  20 2_WW ...      CALL  7  (JPRINT1)           [BLANK LINE TO SEPERATE BLOCKLETS
   5 2_WY ...XJREC2  
  16 2_X2 ...      LDX   3  JLPMOD              [LP BUFFER ADDR  
  16 2_X4 ...      LDN   6  0                   [CLEAR WORD COUNT
   5 2_X6 ...XJREC3  
   8 2_X8 ...      LDX   5  0(2) 
  17 2_X= ...      ADN   6  1                   [COUNT WORDS IN MSG  
  16 2_X# ...      TXU   5  NL(1)               [IS IT N/L WORD  
  15 2_XB ...      BCC      XJREC4              [YES END MSG 
   8 2_XD ...      STO   5  0(3) 
   7 2_XG ...      ADN   2  1
   7 2_XJ ...      ADN   3  1
  20 2_XL ...      LDX   5  6                   [HAVE WE EXCEEDED WORDS IN LINE  
   8 2_XN ...      SBN   5  21   
  15 2_XQ ...      BPZ   5  XJREC8              [YES-ERROR   
   9 2_XS ...      BRN      XJREC3   
   5 2_XW ...XJREC4  
  14 2_XY ...      STO   6  XLINK(1)            [SAVE X6 
  20 2_Y2 ...      LDN   6  3                   [REMOVE $ PADDING OF LAST WORD   
   7 2_Y4 ...      SBN   3  1
   7 2_Y6 ...      BCHX  3  /
   8 2_Y8 ...      LDN   7  #20  
   5 2_Y= ...XJREC9  
   8 2_Y# ...      LDCH  5  0(3) 
  10 2_YB ...      TXU   5  XDOLLER(1)   
   9 2_YD ...      BCS      XJREC10  
   8 2_YG ...      DCH   7  0(3) 
   5 2_YJ ...XJREC10 
   7 2_YL ...      BCHX  3  /
   9 2_YN ...      BCT   6  XJREC9   
   4 2_YQ ...#   
  16 2_YS ...      CALL  7  (JPRINT1)           [PRINT MESSAGE   
  15 2_YW ...      LDX   6  XLINK(1)            [RESTORE X6  
  16 2_YY ...      BZE   0  XJREC5              [NOT CURRENT PAGE
  16 2__2 ...      LDN   6  1                   [COUNTING MSGS   
   5 2__4 ...XJREC5  
   7 2__6 ...      SBX   4  6
  14 2__8 ...      BZE   4  XJREC7              [FINISHED
   7 2__= ...      ADN   2  1
   9 2__# ...      BRN      XJREC2   
   5 2__B ...XJREC6  
  17 2__D ...      PMESSAGE TEXT4(1),5          [CURRENT PAGE EMPTY  
   5 2__G ...XJREC7  
   9 2__J ...      LDX   0  SLINK(1) 
  14 2__L ...      EXIT  0  0                   [OK EXIT 
   5 2__N ...XJREC8  
  15 2__Q ...      LDX   0  SLINK(1)            [ERROR EXIT  
   7 2__S ...      EXIT  0  1
   4 2__W ...#   
   6 2__Y ...XJRECBLK2   
   9 3222 ...      STO   0  SLINK(1) 
   8 3224 ...      SMO      FXPM2
   9 3226 ...      LDX   2  AFRESBS  
  10 3228 ...      LDN   4  HEIGHTPAGE   
  15 322= ...      SBX   4  2(2)                [NO OF MSGS  
  14 322# ...      BNG   4  XJREC8              [ERROR   
   9 322B ...      BZE   4  XJREC6   
  20 322D ...      LDN   0  1                   [SWITCH TO INDICATE CURRENT PAGE 
   9 322G ...      BRN      XJREC1   
   4 322J ...)   
   4 322L ...#   
   4 329=    #   
  18 32NW    #              ROUTINE FOR SETTING UP AND PRINTING A LINE   
   4 338G    #   
   9 33N6    #     ENTRY    X3=LINK  
  20 347Q    #              X4=ADDR. OF WORD TO BE OUTPUT RELATIVE TO START OF   
  15 34MB    #                 BLOCK(WILL TAKE VALUE 0-19)   
  12 3572    #          MODBLK=BLOCKLET MODIFIER 
   4 35LL    #   
  12 366=    #     EXIT     X4,MODBLK UNCHANGED  
  13 36KW    #              ALL OTHER ACCS. UNDEFINED
   4 375G    #   
   5 37K6    XFPRLIN 
  16 384Q          LDX   1  4                   [WORD MODIFIER   
   8 38#J ...      SMO      FXPM1
  15 38JB          STO   3  XLINK               [STORE LINK  
   8 3942 ...      CALL  7  (JUP)
   8 39HL                   +1   
   8 39RD ...NXTW  SMO      FXPM2
   9 3=3= ...      ADX   1  AFRESBS  
   8 3=?4 ...      SMO      FXPM1
   9 3=GW          ADX   1  MODBLK   
   8 3?2G          LDX   5  0(1) 
  17 3?G6 ...      CALL  7  (JOCT)                [CONVERT TO OCTAL  
   8 3?_Q          LDX   2  JPMOD
   7 3#FB          BCHX  2  /
  18 3#_2          LDN   7  4                   [COUNT OF 4 CHARACTERS   
  17 3*DL    STCHS LDCH  0  0(1)                [PICK UP CHARACTER   
  15 3*Y=          DCH   0  0(2)                [AND STORE   
   7 3BCW          BCHX  1  /
   7 3BXG          BCHX  2  /
  19 3CC6          BCT   7  STCHS               [REPEAT FOR ALL CHARACTERS   
  18 3CWQ          SBN   1  1                   [RESTORE WORD MODIFIER   
   7 3DBB          BCHX  2  /
  16 3DW2          BCHX  2  /                   [SPACE OUT GAP   
   7 3F*L          BCHX  2  /
   8 3FT=          STO   2  JPMOD
  18 3G#W          ADN   1  20                  [INCREASE WORD MODIFIER  
   8 3GJN ...      SMO      FXPM2
   9 3GSG          SBX   1  AFRESBS  
   8 3H4# ...      SMO      FXPM1
   9 3H#6          SBX   1  MODBLK   
   8 3HHY ...      SMO      FXPM1
  19 3HRQ ...      TXL   1  X128                [J IF MORE WORDS FOR PRINTING
   8 3J3J ...      BCS      NXTW 
   4 3J?B    #   
  20 3JR2    #     REMEMBER,ONLY 128 WORDS PER BLOCK,SO FIRST 8 ROWS 7 COLS. TO  
  15 3K=L    #     PRINT,NEXT 12 ROWS ONLY 6 COLS TO PRINT   
   4 3KQ=    #   
  15 3L3L ...      CALL  7  (JPRINT1)             [PRINT LINE
   8 3L*2 ...      SMO      FXPM1
   8 3LLB ...      LDX   3  XLINK
   7 3LXQ ...      EXIT  3  0
   4 3M96    #   
  20 3MNQ    #              ROUTINE FOR PRINTING A LINE IF LANG. B1B0,B5B1 ETC.  
   4 3N8B    #   
   9 3NN2    #     ENTRY    X3=LINK  
  11 3P7L    #              X1=WORD MODIFIER 
  13 3PM=    #     EXIT     0 - AFTER PRINTING RECORD
  13 3Q6W    #              X1=UPDATED WORD MODIFIER 
  13 3QLG    #              ALL OTHER ACCS. UNDEFINED
  17 3R66    #              1 - IF WHOLE OF BLOCK HAS BEEN PRINTED   
  14 3RKQ    #              2 - TO PRINT IN BLOCK FORMAT 
   4 3S5B    #   
   5 3SK2    XFPRRCD 
   8 3SSS ...      SMO      FXPM1
  15 3T4L          STO   3  XLINK               [STORE LINK  
   8 3T#D ...      SMO      FXPM1
  17 3TJ=          LDX   3  REMAIN              [N/W TO BE PRINTED   
   8 3W3W          BNZ   3  PR1  
   8 3WB? ...PR2   SMO      FXPM1
   8 3WNN ...      LDX   3  XLINK
  19 3X36          EXIT  3  1                   [EXIT AS WHOLE BLOCK PRINTED 
   8 3X*H ...PR1   SMO      FXPM2
   9 3XMY ...      LDX   2  AFRESBS  
   7 3Y2* ...      ADX   2  1
  15 3Y#Q ...      LDX   2  0(2)               [N/W IN RECORD
  17 3YM7 ...      BZE   2  PR2                 [J IF NO MORE RECORDS
  19 3Y_L          BNG   2  RHOVW               [J IF REC.HEADER OVERWRITTEN 
  14 3_F=          SBX   3  2                   [5/2-N/W 
  17 3_YW          BPZ   3  TRPRI               [J TO PRINT RECORD   
   5 42DG    RHOVW   
   8 42QX ...      LDX   3  FXPM1
  20 435# ...      PMESSAGE PRH1(3),8              ['RECORD HEADER OVERWRITTEN'  
   8 43CQ          LDN   4  512  
   8 43MJ ...      SMO      FXPM2
  17 43XB          STO   4  FNUMBER             [N/W TO BE PRINTED   
  18 44C2          LDN   4  0                   [STOZ PRINT LINE MODIFIER
   8 44LS ...      SMO      FXPM1
   8 44WL          LDX   3  XLINK
  20 45B=          EXIT  3  2                   [EXIT TO PRINT IN BLOCK FORMAT   
   8 45NM ...TRPRI SMO      FXPM1
  19 4634 ...      STO   3  REMAIN              [REMAINDER OF WORDS IN BLOCK 
   8 46*G          LDN   5  29   
  17 46T6          BXGE  2  5,RECLO             [J IF RECORD>29 WORDS
   7 47#Q          LDN   5  2
  17 47SB          BXE   2  5,SPLIN             [J IF RCD HEADER ONLY
   8 488W ...      SMO      FXPM2
   9 48KB ...      LDX   3  AFRESBS  
   8 48_W ...      LDN   5  2(3) 
   7 49BB ...      ADX   5  1
   9 49QW          LDX   6  JLPMOD   
   7 4==G          ADN   6  4
   7 4=Q6          SBN   2  2
  18 4?9Q          MOVE  5  0(2)                [MOVE RECORD INTO BUFFER 
   7 4?PB          ADN   2  2
   8 4#2Q ...SPLIN SMO      FXPM2
   9 4##6 ...      LDX   3  AFRESBS  
   7 4#KG ...      ADX   3  1
  16 4#WW ...      LDX   5  1(3)                [N/W IN RECORD   
   8 4*8=          LDX   6  ACES 
   7 4*MW          SRC   56 6
   9 4B7G          LDX   3  JLPMOD   
   8 4BM6          STO   5  2(3) 
   8 4C6Q          STO   6  3(3) 
   8 4CD6 ...      SMO      FXPM2
   9 4CPG ...      LDX   3  AFRESBS  
   7 4D2W ...      ADX   3  1
   8 4D#= ...      LDX   5  0(3) 
   9 4DKL          LDX   3  JLPMOD   
   8 4F5=          STO   3  JPMOD
  17 4FJW ...      CALL  7  (JDECN)               [CONVERT TO DECIMAL
   7 4G4G          LDX   5  1
   9 4GJ6          LDX   3  JLPMOD   
   7 4H3Q          SBN   3  1
   8 4HHB          STO   3  JPMOD
  17 4J32 ...      CALL  7  (JDECN)               [CONVERT TO DECIMAL
   7 4JGL          ADX   1  2
  15 4JS2 ...      CALL  7  (JPRINT1)             [PRINT LINE
   8 4K5B ...      SMO      FXPM1
   8 4KBQ ...      LDX   3  XLINK
   7 4KN6 ...      EXIT  3  0
  17 4K_G    RECLO SBN   2  28                  [SUBTRACT 28 WORDS   
   8 4LB2 ...      SMO      FXPM2
   9 4LQG ...      LDX   3  AFRESBS  
   8 4M72 ...      LDN   5  2(3) 
   7 4MHG ...      ADX   5  1
   9 4MY2          LDX   6  JLPMOD   
   7 4NCL          ADN   6  4
  19 4NX=          MOVE  5  26                  [MOVE 26 WORDS INTO BUFFER   
   7 4PBW ...      ADX   3  1
   8 4PWG ...      LDX   5  1(3) 
   8 4QB6          LDX   6  ACES 
   7 4QTQ          SRC   56 6
   9 4R*B          LDX   3  JLPMOD   
   8 4RT2          STO   5  2(3) 
   8 4S#L          STO   6  3(3) 
   8 4SL2 ...      SMO      FXPM2
   9 4SXB ...      LDX   3  AFRESBS  
   7 4T8Q ...      ADX   3  1
  16 4TG6 ...      LDX   5  0(3)                [N/W IN RECORD   
   9 4TRG          LDX   3  JLPMOD   
   8 4W?6          STO   3  JPMOD
  17 4WQQ ...      CALL  7  (JDECN)               [CONVERT TO DECIMAL
   7 4X=B          LDX   5  1
   9 4XQ2          LDX   3  JLPMOD   
   7 4Y9L          SBN   3  1
   8 4YP=          STO   3  JPMOD
  17 4_8W ...      CALL  7  (JDECN)               [CONVERT TO DECIMAL
  18 4_NG          ADN   1  28                  [INCREASE WORD MODIFIER  
  15 5286 ...      CALL  7  (JPRINT1)             [PRINT LINE
   8 52MQ    MOLIN LDN   0  27   
  20 537B          BXL   2  0,PRLL              [J TO PRINT REMAINDER OF RECORD  
   8 53M2          SBN   2  26   
  18 546L          LDX   5  1                   [INCREASE WORD MODIFIER  
   9 54L=          LDX   3  JLPMOD   
   7 555W          SBN   3  1
   8 55KG          STO   3  JPMOD
  17 55XX ...      CALL  7  (JDECN)               [CONVERT TO DECIMAL
   8 56=# ...      SMO      FXPM2
   9 56JQ          LDX   5  AFRESBS  
   7 574B          ADX   5  1
   9 57J2          LDX   6  JLPMOD   
   7 583L          ADN   6  4
  19 58H=          MOVE  5  26                  [MOVE 26 WORDS INTO BUFFER   
  15 592W ...      CALL  7  (JPRINT1)             [PRINT LINE
  18 59GG          ADN   1  26                  [INCREASE WORD MODIFIER  
  17 5=26          BRN      MOLIN               [J TO PRINT NEXT LINE
  18 5=FQ    PRLL  LDX   5  1                   [CONVERT WORD MODIFIER   
   9 5=_B          LDX   3  JLPMOD   
   7 5?F2          SBN   3  1
   8 5?YL          STO   3  JPMOD
  17 5#?3 ...      CALL  7  (JDECN)               [CONVERT TO DECIMAL
   8 5#KD ...      SMO      FXPM2
   9 5#XW          LDX   5  AFRESBS  
   7 5*CG          ADX   5  1
   9 5*X6          LDX   6  JLPMOD   
   7 5BBQ          ADN   6  4
  21 5BWB          MOVE  5  0(2)                [MOVE REMAINDER OF REC.INTO BUFFER   
  16 5CB2 ...      CALL  7  (JPRINT1)             [PRINT ONE LINE
  18 5CTL          ADX   1  2                   [INCREASE WORD MODIFIER  
   8 5D*= ...      SMO      FXPM1
   8 5DSW ...      LDX   3  XLINK
   7 5F#G ...      EXIT  3  0
  20 6=P2    #     THIS ROUTINE GETS THE NEXT FILE/FCB BLOCK ON THE FILE CHAIN   
  21 6?8L    #     DEPENDENT UPON DIRECTION OF CHAINING AND TESTING FOR END OF CHAIN.
  16 6?N=    #     THE POINTERS OF THIS BLOCK ARE THEN VALIDATED 
   4 6#7W    #   
   9 6#MG    #     ENTRY    X0=LINK  
  16 6*76    #            XFCB=ADDR. OF PRESENT FILE/FCB BLOCK   
  10 6*BY ...#              X1 = FXPM2   
   4 6*LQ    #   
  18 6B6B    #     EXIT     0 - OK,XFCB = ADDR. OF NEXT FILE/FCB BLOCK   
  15 6BL2    #              1 - DOUBLE BREAK OR END OF CHAIN 
   4 6C5L    #   
  14 6CK=    #     XGETFCB1   - USED FOR FIRST CALL ONLY.
  15 6D4W    #     XGETFCB    - USED FOR SUBSEQUENT CALLS.   
   4 6DJG    #   
   5 6F46    XGETFCB1
   8 6F?Y ...      SMO      FXPM1
   9 6FHQ ...      LDX   2  XBFILE   
   8 6FRJ ...      SMO      FXPM1
  19 6G3B          STOZ     XBACK               [CLEAR BACKWARD CHAINING IND.
   9 6GH2          LDX   3  FPTR(2)  
  17 6H2L ...XG1   STO   2  JXFCB(1)            [STORE BLOCK ADDR.   
  18 6HG=          PMCORES 3,XG3                [J IF FORWARD PTR INVALID
   5 6H_W    XGETFCB 
  18 6J#? ...      LDX   2  JXFCB(1)            [LOAD CURRENT BLOCK ADDR.
   8 6JLN ...      SMO      FXPM1
   8 6J_6          LDX   4  XBACK
  18 6KDQ          BNZ   4  XG4                 [J IF BACKWARD CHAINING  
  18 6KYB          LDX   2  FPTR(2)             [LOAD ADDR. OF NEXT BLOCK
   8 6L88 ...      SMO      FXPM1
   9 6LHX ...      TXU   2  XBFILE   
  18 6LXL          BCC      XG5                 [J IF BASE OF FILE CHAIN 
  19 6M7D ...      PMCORES  2,XG3               [J IF FORWARD POINTER INVALID
   9 6MC=          LDX   3  BPTR(2)  
   9 6MWW          LDX   4  FPTR(2)  
  19 6NBG ...XG2   TXU   3  JXFCB(1)            [J IF BACKWARD PTR OF BLOCK  
  21 6NW6          BCS      XG3                 [UNEQUAL TO ADDR. OF PREVIOUS BLOCK  
  18 6P*Q          PMCORES 4,XG3                [J IF FORWARD PTR INVALID
  17 6PTB ...      STO   2  JXFCB(1)            [STORE BLOCK ADDR.   
   9 6Q*2          LDX   4  ATYPE(2) 
   8 6QSL          SRL   4  12   
   9 6R#= ...      TXU   4  JTFCB(1) 
  18 6RRW          BCS      XGETFCB             [J IF NOT FILE/FCB BLOCK 
   7 6S?G          EXIT  0  0
   8 6SKX ...XG3   SMO      FXPM1
   8 6SY# ...      LDX   4  XBACK
  20 6T=Q          BNZ   4  XG5                 [J IF ALREAY BACKWARD CHAINING   
   7 6TQB          LDN   4  1
   8 6W28 ...      SMO      FXPM1
  19 6W=2          STO   4  XBACK               [SET BACKWARD CHAINING IND.  
   8 6WJC ...      SMO      FXPM1
   9 6WWS ...      LDX   2  XBFILE   
  16 6X9=          LDX   3  BPTR(2)             [LOAD FORWARD PTR
   8 6XNW          BRN      XG1  
  10 6Y8G    #     BACKWARD CHAINING CODE
  18 6YN6    XG4   LDX   2  BPTR(2)             [LOAD ADDR. OF NEXT BLOCK
   8 6YXY ...      SMO      FXPM1
   9 6_?M ...      TXU   2  XBFILE   
  18 6_MB          BCC      XG5                 [J IF BASE OF FILE CHAIN 
  17 7272          LDX   3  FPTR(2)             [LOAD BACKWORD PTR   
  16 72LL          LDX   4  BPTR(2)             [LOAD FORWARD PTR
   8 736=          BRN      XG2  
  20 73KW    XG5   EXIT  0  1                   [CHAIN BASE OR DOUBLE CHAIN BREAK
   4 745G    #   
   5 74K6 ...PMFSBEND
   4 75JB    #END
   6 ____ ...631706740012
