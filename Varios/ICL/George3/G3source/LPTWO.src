  11 22FL          SEG      LPTWO,7,7OPAE,OLP
  16 22_=    # THIS SEGMENT PROCESSES MODE 2 OFFLINE *LP PERIS   
  11 23DW          SEGENTRY K1LPTWO,Z1LPTWO  
  11 23YG          SEGENTRY K2LPTWO,Z2LPTWO  
  15 24D6    [     AWORK1 NON-ZERO MEANS STARTING NEW RECORD 
  17 24XQ ...[     AWORK3 IS COUNT OF RECORDS PRODUCED BY THE PERI   
  20 25CB ...[     AWORK2 IS SET UP BY ASSIGN WITH NUMBER OF WORDS IN PRINT LINE 
  12 25X2    [              B1-FILE FNEARLY FULL 
  12 26BL    [     AWORK4   B0-END OF MULTIFILE  
  16 26HS ...[              B2-22 COUNT OF RECS TO BE DISCARDED  
  17 26P2 ...[                   BECAUSE OF END OF MULTIFILE ELEMENT 
  18 26W=    [FI/FUTILITY   A1- 1ST WORD OF RED TAPE FOR FILESTORE RECORD
  14 27*W    [              A1+1- 2ND WORD OF RED TAPE   
  12 27TG    [              A1+2 ETC PRINT LINE  
   5 28*6    PLINK1  
   9 28SQ          PAIR     LPTWO,1  
   5 29#B    NEWCPT  
   8 29S2          LDCT  0  #200 
   9 2=?L          ANDX  0  AWORK4(2)
  16 2=R=          ERS   0  AWORK4(2)           [UNSET MARKERS   
   5 2?=W    Z1LPTWO 
   9 2?QG          LDX   0  ALIMEY(2)
   6 2#=6    #SKI  ANOLIM
  18 2#PQ          BNG   0  Q1                  [J IF NOLIMIT QUALIFIER  
  18 2*9B          BXL   0  ACOUNT(2),OUTL      [ELSE J IF LIMIT EXCEEDED
   4 2*P2    Q1  
  10 2B8L          LDX   3  APETADDR(2)  
  10 2BN=          LOCKCA   3,ACAP(2),1,2
  16 2C7W          LDX   4  0(1)                [TYPE/MODE WORD  
   8 2CMG          LDX   1  FX1  
   8 2D76          ANDN  4  #7777
   7 2DLQ          SBN   4  2
   9 2F6B          BZE   4  K2LPTWO  
   7 2FL2          ADN   4  2
   8 2G5L          BNZ   4  MOLE 
  16 2GK=          ACROSS   LPOBJ,2             [IF NOT MODE 2   
   5 2H4W    Z2LPTWO 
  21 2HJG          LDX   0  AWORK4(2)           [J IF END OF MULTIFILE ALREADY REACHE
  21 2J46          BNG   0  OUTF2               [SINCE COMPQU LEAVES NO FILES OPEN   
   7 2JHQ          LDN   4  4
   9 2K3B          LDX   6  ACAP(2)  
   9 2KH2          LDN   7  ACA1(2)  
  10 2L2L          LDX   3  APETADDR(2)  
  11 2LG=          FOUTMOVE 3,6,7,4,HLOCK1,2 
  16 2L_W                                       [MOVE CA TO AOLPT
  14 2MFG          LDX   6  ACA3(2)             [COUNT   
  14 2M_6          BZE   6  WC                  [ILLEGAL 
   8 2NDQ          LDCT  0  #777 
   7 2NYB          ANDX  0  6
  15 2PD2          BNZ   0  WC                  [COUNT>32K-1 
   8 2PXL          LDCT  7  #600 
   9 2QC=          ANDX  7  ACA4(2)  
  16 2QWW          SLC   7  2                   [GET CHAR ADDRESS
   7 2RBG          ADX   6  7
   7 2RW6          ADN   6  3
  17 2S*Q          SRL   6  2                   [ROUND COUNT TO WORDS
   9 2STB          LDX   5  ACA4(2)  
   9 2T*2          ANDX  5  BITS22LS 
  16 2TSL          PMODES   0,3                 [GET ADDRESS MODE
   8 2W#=          ANDN  0  #10  
  15 2WRW          BNZ   0  MOKA                [J IF 22AM   
  16 2X?G          ANDX  5  BSP16               [REDUCE TO 15BITS
   4 2XR6    MOKA
   5 2Y=Q    #SKI  G4
  18 2YQB          LDN   4  CHREAD              [NOT USED IN G3 CHECKB   
  15 2_=2          CHECKB   5,6,RV1,3,4,MOKB,HLOCK2,2,K1LPTWO
  16 2_PL    [ J TO RV1 IF RESERVATION OR PERMISSION VIOLATION   
  13 329=    [ RESTART AT K1LPTWO IF PAGETURN NEEDED 
   7 32NW    MOKB  SLL   5  2
   7 338G          ADX   5  7
   9 33N6          ADX   5  ACA3(2)  
  17 347Q          STO   5  ACA4(2)             [END ADDRESS IN CHARS
  15 34MB ...      STOZ     AWORK3(2)           [RECORD COUNT
   9 3572          LDX   6  ACA3(2)  
   7 35LL          LDN   5  0
  21 366=          SLL   56 12                  [DIVIDE X 4096 - CAN ONLY LOOP 1K WOR
   9 36KW          STO   5  ACA3(2)  
  21 375G          SRL   6  12                  [WILL PROCESS FDD CHARS 1ST IF ANY   
  10 37K6          HUNTW    3,FI,FUTILITY
   8 384Q          BPZ   3  OKBL 
   9 38JB ...      SMO      AWORK2(2)
   8 3942          LDN   5  2+FRH
  11 39HL          SETUPCOR 5,3,FI,FUTILITY  
   8 3=3=    OKBL  LDN   5  #77  
  21 3=GW    NXTR  BNZ   6  MORE                [J IF REMAINING CHARS FROM LAST LOOPC
   8 3?2G          CALL  7  MBUF 
  20 3?G6          BRN      SETB                [START ON NEXT CHUNK OF BUFFER   
  16 3?_Q          BRN      REP                 [END OF BUFFER   
  19 3#FB    MORE  CALL  7  MBUFOK              [CONTINUE WITH PRESENT CHUNK 
   4 3#_2    SETB
  16 3*DL          STOZ     A1+FRH+1(3)         [2ND REDTAPE WORD
   8 3*Y=          LDX   0  ACES 
  10 3BCW          STO   0  A1+FRH+2(3)  
   9 3BXG          ADN   3  A1+FRH+2 
   8 3CC6          LDN   4  1(3) 
   9 3CH3 ...      LDX   0  AWORK2(2)
   7 3CLY ...      SBN   0  1
  15 3CQT ...      BZE   0  NMOVE               [AUTO4 USED  
   9 3CWQ ...      SMO      AWORK2(2)
   8 3DBB          MOVE  3  511  
   5 3DL8 ...NMOVE   
   7 3DW2          SBN   4  1
  15 3F*L          STO   4  AWORK1(2)           [SET NON-ZERO
  21 3FT= ...      ADX   4  AWORK2(2)           [ADDRESS OF 1ST CHAR BEYOND END OF LI
  16 3G#W                                       [ASSEMBLY AREA   
  19 3GSG    NXTCH LDCH  7  0(1)                [LOAD NEXT CHAR FROM BUFFER  
  15 3H#6          BXE   7  5,NESC              [J IF ESCAPE 
  16 3HRQ    STCH  DCH   7  0(3)                [STORE IN BLOCK  
  17 3J?B          BCHX  3  /                   [STEP BLOCK ADDRESS  
  16 3JR2    TFULL BXE   3  4,YFULL             [J IF BLOCK FULL 
   5 3K=L    UNSET   
  18 3KQ=          STOZ     AWORK1(2)           [UNSET INITIALIZED SWITCH
  17 3L9W    NEXT  NEXTCH   1,6,NXTCH           [ADDRESS NEXT CHAR   
   8 3LPG          CALL  7  MBUF 
  16 3M96          BRN      NXTCH               [START NEXT CHUNK
   5 3M?P ...XPIRED  
  18 3MB# ...      CALL  7  STEPDUMMY            [LINE WITHOUT PRINTOUT  
  17 3MDY ...      BRN      REP                 [NO MORE - DISCARD   
  19 3MNQ    NLAST CALL  7  STEP                [APPEND LAST RECORD OF PERI  
   4 3N8B    REP 
   7 3NN2          LDN   6  0
   4 3P7L    REP2
  18 3PM=          CALL  7  REPLY               [STORE X6 IN REPLY WORD  
   6 3Q6W          OLPEND
   9 3QLG          BRN      K1LPTWO  
   2 3R66
  17 3RKQ    NESC  NEXTCH   1,6,NCC             [GET NEXT CHAR & J   
   8 3S5B          CALL  7  MBUF 
   8 3SK2          BRN      NCC  
  17 3T4L ...      BRN      XPIRED              [HALF CONTROL CHAR   
   8 3TJ=    NCC   LDCH  7  0(1) 
   8 3W3W          SBN   7  #76  
  18 3WHG          BNZ   7  NOTP                [J IF NOT PRINTOUT CODE  
   9 3X36          LDX   7  AWORK1(2)
  21 3XGQ          BNZ   7  NEXT                [J TO MAIN LOOP AFTER FORCED PRINT   
  17 3Y2B    YFULL REM      6                   [GET REMAINDER COUNT 
  18 3YG2          SBN   6  1                   [LESS CHAR JUST EXAMINED 
  16 3Y_L          CALL  7  STEP                [APPEND RECORD   
   7 3_F=          LDN   0  1
  17 3_YW ...      ADS   0  AWORK3(2)           [UPDATE RECORD COUNT 
  18 42DG          TESTREP2 COORED,NXTR         [J IF STEP COORDINATED   
  17 42Y6          COOR2                        [GO TO BACK OF QUEUE 
  10 43CQ          MHUNTW   3,FI,FUTILITY
   8 43XB          BRN      NXTR 
   2 44C2
   9 44WL    NOTP  SBN   7  #77-#76  
  15 45B=          BNZ   7  NOTD                [J IF NOT __ 
   8 45TW          LDN   7  #77  
  14 46*G          BRN      STCH                [STORE _ 
   2 46T6
   8 47#Q    NOTD  ADN   7  #74  
  14 47SB          BPZ   7  NOT0                [J IF >2 
   7 48#2          ADN   7  3
   5 48RL    NPFCC   
  20 49?=          ORN   7  #40                 [CONVERT PFCC TO STANDARD TYPE   
   7 49QW          LDX   0  4
   9 4==G ...      SMO      AWORK2(2)
   7 4=Q6          SBN   0  1
   7 4?9Q          SMO      0
  16 4?PB          LDX   5  0                   [2ND REDTAPE WORD
  21 4#92          BNZ   5  SECONDPFCC          [J IF ALREDAY HAD A PFCC IN THIS LINE
  16 4#NL          LDN   5  #77                 [RESTORE VALUE   
   7 4#YD ...      SMO      0
   7 4*8=          STO   7  0
  17 4*MW          BRN      UNSET               [RETURN TO MAIN LOOP 
   4 4B7G    [   
  20 4BM6    [ DEAL WITH MORE THAN 1 PFCC IN A LINE BY KEEPING LATEST AS CURRENT 
  21 4C6Q    SECONDPFCC                         [& PUTTING PREVIOUS IN A DUMMY RECORD
   9 4CLB ...      SBX   4  AWORK2(2)
  15 4D62          SBN   4  2                   [X4->A1+FRH  
  21 4DKL          SBX   3  4                   [GET CURRENT PRINT POSITION REL TO A1
   7 4F5=          SMO      4
  21 4FJW          STO   3  FRH                 [& SAVE IN SPARE (1ST) WORD OF FUTILI
  17 4G4G          LDX   5  7                   [SAVE NEW PFCC IN X5 
  17 4GJ6          REM      6                   [GET REMAINDER COUNT 
  16 4H3Q          SBN   6  1                   [LESS LAST CHAR  
  21 4HHB          CALL  7  STEPDUMMY           [APPEND 2WORD DUMMY RECORD WITH OLD P
   7 4J32          LDN   0  1
  18 4JGL ...      ADS   0  AWORK3(2)           [INCREMENT RECORD COUNT  
  11 4K2=          TESTREP2 COORED,SAMEREC   
  21 4KFW          COOR2                        [GO TO BACK OF QUEUE IF STEP DIDNT CO
  10 4K_G          MHUNTW   3,FI,FUTILITY
   5 4LF6    SAMEREC 
  20 4LYQ          BNZ   6  SAMEMORE            [J UNLESS BUFFER CHUNK EXPIRED   
  17 4MDB          CALL  7  MBUF                [GET ANOTHER CHUNK   
  13 4MY2          BRN      SAMESETB            [OK  
  10 4N=C ...      STO   5  A1+FRH+1(3)  
   9 4NJS ...      BRN      XPIRED   
   5 4NX=    SAMEMORE
  19 4PBW          CALL  7  MBUFOK              [CONTINUE WITH CURRENT CHUNK 
   5 4PWG    SAMESETB
   9 4QB6          ADN   3  A1+FRH   
   9 4QTQ ...      SMO      AWORK2(2)
  21 4R*B          LDN   4  2(3)                [GET X4->WORD BEYOND END OF ASSEMBLY 
  16 4RT2          STO   5  1(3)                [STORE NEW PFCC  
  18 4S#L          ADX   3  0(3)                [RECOVER CURREN POSITION 
  18 4SS=          STOZ     AWORK1(2)           [CLEAR PRINTOUT SWITCH   
  18 4T?W          LDN   5  #77                 [RESTORE ESCAPE CHAR FOR 
  15 4TRG          BRN      NXTCH               [MAIN LOOP   
   4 4YP=    [   
   7 4_2L ...NOT0  SBN   7  5
  15 4_#2 ...      BNG   7  XCC                 [J IF <#10   
  14 4_KB ...      BZE   7  UNSET                   [=#10
   7 4_WQ ...      SBN   7  1
  19 5286          BZE   7  PAGETHROW           [J IF THROW TO HEAD OF FORM  
   7 52MQ          SBN   7  7
  15 537B          BPZ   7  TAB1                [J IF >#17   
  18 53M2          ADN   7  #20                 [RESTORE CORRECT VALUE   
  16 546L          BRN      NPFCC               [& STORE AS PFCC 
   6 54L=    PAGETHROW   
   8 555W          LDCT  0  #200 
   9 55KG          ANDX  0  AWORK4(2)
  17 55SD ...      LDN   7  #11               [PFCC FOR PAGE THROW   
  20 563B ...      BZE   0  NPFCC             [J IF NOT FNEARLY ON PREVIOUS STEP 
  20 56=# ...      LDX   7  AWORK3(2)         [NO OF RECS GENERATED BY THIS PERI 
  18 56F= ...      ORS   7  AWORK4(2)         [RECS TO BE DISCARDED NEXT 
  18 56N8 ...                                 [TIME ROUND ON SAME PERI   
  15 56X6 ...      TESTMULT OUTF1             [J IF MULTIFILE
  21 5764 ...      ERS   7  AWORK4(2)         [NOT MULT SO ER ALL RECS FOR THIS PERI 
   8 57*2 ...      BRN      OUTF 
   2 57J2
   9 583L    TAB1  SBN   7  #54-#20  
  16 58H=          BZE   7  TABL                [J IF TAB CHAR L 
   7 592W          SBN   7  1
  16 59GG          BNZ   7  XCC                 [J - CHAR ILLEGAL
  14 5=26          LDN   7  100                 [TAB M   
  16 5=FQ    TABL  LDX   5  7                   [KEEP TAB VALUE  
  17 5=_B          NEXTCH   1,6,TAB2            [GET NEXT TAB CHAR   
   8 5?F2          CALL  7  MBUF 
   8 5?YL          BRN      TAB2 
   8 5#D=          BRN      XCC  
   2 5#XW
   8 5*CG    TAB2  LDCH  7  0(1) 
   8 5*X6          SBN   7  #52  
  15 5BBQ          BPZ   7  XCC                 [J IF >#51   
   9 5BWB          ADN   7  #52-#40  
  14 5CB2          BNG   7  XCC                 [OR <#40 
  13 5CTL          SLL   7  1                   [*2  
   7 5D*=          ADX   5  7
  13 5DSW          SLL   7  2                   [*8  
   7 5F#G          ADX   5  7
  15 5FS6          NEXTCH   1,6,TAB3            [GET 3RD TAB 
   8 5G?Q          CALL  7  MBUF 
   8 5GRB          BRN      TAB3 
   8 5H?2          BRN      XCC  
   2 5HQL
   8 5J==    TAB3  LDCH  7  0(1) 
  15 5JPW          SBN   7  #72                 [J IF >#71   
   8 5K9G          BPZ   7  XCC  
   9 5KP6          ADN   7  #72-#60  
  14 5L8Q          BNG   7  XCC                 [OR <#60 
   7 5LNB          ADX   5  7
   9 5M82 ...      LDX   7  AWORK2(2)
   7 5MML          SLL   7  2
  18 5N7=          BXGE  5  7,XCC               [J IF TAB OUT OF RANGE   
  17 5NLW          LDX   3  4                   [RESET BLOCK POINTER 
   9 5P6G ...      SBX   3  AWORK2(2)
   7 5PL6          SRC   5  2
   7 5Q5Q          ADX   3  5
   8 5QKB          LDN   5  #77  
  16 5R52          BRN      UNSET               [REJOIN MAIN LOOP
   2 5RJL
  20 5S4=    MBUF  LDX   6  ACA3(2)             [SET UP MORE BUFFER IF AVAILABLE 
  14 5SHW          BZE   6  MBUFX               [NO MORE 
   7 5T3G          SBN   6  1
   9 5TH6          STO   6  ACA3(2)  
  16 5W2Q          LDX   6  BIT11               [4096 - NEW COUNT
  17 5WGB    MBUFOK                             [RESET PRESENT CHUNK 
  17 5X22          LDX   1  ACA4(2)             [END ADDRESS IN CHARS
   7 5XFL          SBX   1  6
   9 5X_=          LDX   0  ACA3(2)  
   8 5YDW          SLL   0  12   
   7 5YYG          SBX   1  0
   7 5_D6          SRC   1  2
  13 5_XQ          LOOPCH   APETADDR(2),1,6,HLOCK2,2 
  13 62CB          EXIT  7  0                   [OK  
  14 62X2    MBUFX EXIT  7  1                   [NO MORE 
   4 63BL    [   
   6 63W=    STEPDUMMY   
   8 64*W          SBX   7  FX1  
   7 64TG          LDN   3  0
   8 65*6          BRN      STEPN
   8 65SQ    STEP  SBX   7  FX1  
   9 66#B ...      LDX   3  AWORK2(2)
   8 66S2          SBN   4  1(3) 
   7 67?L          LDX   2  4
   8 67R=          BRN      STEPL
   5 68=W    STEPM   
   8 68QG          BCT   3  STEPL
   8 69=6          BRN      STEPN
   7 69PQ    STEPL SMO      3
   8 6=9B          LDX   1  0(2) 
   9 6=P2          SPACECT  1,4,STEPM
   8 6?8L          BZE   4  STEPN
   7 6?N=          LDN   0  4
   7 6#7W          SBX   0  4
   7 6#MG          SRC   0  2
  21 6*76          ORS   0  0(2)                [ADDRESS OF 1ST UNUSED CHAR IN 2ND RE
   5 6*LQ    STEPN   
  15 6B6B          ADN   3  2                   [FOR RED TAPE
   7 6BL2          LDX   4  3
  10 6C5L          STEP     ,0(3),ANSWER 
  18 6CK=          TESTREP2 FILEFULL,OUTF,FNEARLY,NEARLYF,GLUTTON,TOOMUCH
   5 6D4W    STEPH   
  10 6DJG          MHUNTW   2,FI,FUTILITY
   3 6DML ...
   8 6DQQ ...      SMO      FX2  
   9 6DTW ...      LDEX  0  AWORK4   
  17 6D_2 ...      BZE   0  STEPG          [J IF NO RECS DISCARDED   
  20 6F46 ...      LDN   0  1              [FORGET THIS RECORD AS WE HAVE ALREADY
  20 6F7= ...      SMO      FX2            [DEALT WITH IT AS LAST RECS IN PREV   
  15 6F=B ...      SBS   0  AWORK4         [MULTIFILE ELEMENT
   8 6F*G ...      BRN      STEPX
   9 6FDL ...STEPG STO   4  FRH(3)   
   8 6FHQ          ADN   3  1+FRH
   9 6G3B          ADN   2  A1+FRH+1 
   7 6GH2          SMO      4
  17 6H2L          MOVE  2  511                 [MOVE DATA TO FUWB   
   8 6HG=          LDN   0  #40  
  15 6H_W          ORS   0  0(3)                [DEFAULT PFCC
   9 6JFG ...      SBN   2  A1+FRH+1 
   7 6J_6 ...STEPX LDX   3  2
   8 6KDQ          LDX   2  FX2  
   7 6KYB          ADX   7  1
  20 6LD2          EXIT  7  0                   [X2=FX2,X3->BLOCK,X5,X6 UNCHANGED
   5 6LXL    NEARLYF 
   8 6MC=          LDCT  0  #200 
   9 6MWW          ORS   0  AWORK4(2)
   9 6NBG          SETREP2  COORED   
   8 6NW6          BRN      STEPH
   5 6P*Q    TOOMUCH 
  10 6PTB          LDX   3  FILEBRING(2) 
  10 6Q*2          ADX   3  FBACKPOINT(3)
   9 6QSL          LDX   3  BPTR(3)  
   9 6R#=          LDX   6  FBLMOD(3)
  19 6RRW          CALL  7  NDEL                [DELETE RECORDS FOR THIS PERI
  20 6S?G          LDX   3  FILEBRING(2)        [USE BACKRING IN CASE MULTIFILE  
  10 6SR6          ADX   3  FBACKPOINT(3)
   9 6T=Q          LDX   3  BPTR(3)  
  19 6TQB          SBX   6  FBLMOD(3)           [NO. OF BLOCKS FREED BY NDEL 
   7 6W=2          ADN   6  1
  16 6WPL          CALL  5  TOPCA               [CHANGE TO PCA   
  15 6X9=          ACROSS   OLPODD,3            [TO INCREAMBS
   4 6XNW    WC  
   9 6Y8G          LDN   3  FYCOUNT  
   8 6YN6    XILL  CALL  5  TOPCA
  15 6_7Q          ACROSS   OLPODD,4            [FOR ILLEGAL 
   2 6_MB
   4 7272    MOLE
   9 72LL          LDN   3  FYMODE   
   8 736=          BRN      XILL 
   4 73KW    RV1 
   9 745G          LDN   3  FYNOBUF  
   8 74K6          BRN      XILL 
   2 754Q
   4 75JB    OUTF
  19 7642          CALL  7  NDEL                [DELETE RECORDS FOR THIS PERI
  19 76HL ...OUTF1 COMPQU   OUTF2               [J TO OUTF2 IF NOT MULTIPLE  
  19 773=          BRN      NEWCPT              [EXIT IF NEW COMPONENT OPENED
  18 77GW    [     EXIT+1 IF MULTIFILE FULL - MAY BE NO FILES OPEN AT ALL
   8 782G          LDX   0  GSIGN
   9 78G6          ORS   0  AWORK4(2)
   5 78_Q    OUTF2   
   9 79FB          LDX   0  ACA1(2)  
   7 79_2          SRL   0  3
   8 7=DL          ANDN  0  #7777
   8 7=Y=          SBN   0  #3000
  19 7?CW          BNZ   0  NMON                [J UNLESS ADDITIVE MODE#30000
  21 7?XG          LDCT  6  #50                 [B0 CLEAR B3 & B5 SET IS END OF FILE 
   8 7#C6          BRN      REP2 
   4 7#WQ    NMON
   9 7*BB          LDN   3  FYOUTFULL
   8 7*W2          BRN      OUTP 
   4 7B*L    OUTL
   9 7BT=          LDN   3  FYOUTPUT 
   4 7C#W    OUTP
   9 7CSG          LDX   4  FTYPNO(2)
   8 7D#6          BRN      XILL 
   7 7DRQ    XCC   REM      6
   8 7F?B          LDN   0  #400 
   9 7FR2          ADX   0  ACA3(2)  
   8 7G=L          SLL   0  12   
   7 7GQ=          ADX   6  0
  21 7H9W          CALL  7  REPLY               [STORE UNEXPIRED CHAR COUNT AS REPLY 
  19 7HPG          CALL  7  NDEL                [DELETE RECORDS FOR THIS PERI
   9 7J96          LDN   3  FYINVCC  
   8 7JNQ          BRN      XILL 
   2 7K8B
  15 7KN2 ...NDEL  LDX   4  AWORK3(2)           [RECORD COUNT
  17 7L7L          BZE   4  NDEL2               [J IF NO SUCH RECORDS
   8 7LM=          SBX   7  FX1  
  20 7M6W          WIND                         [SET READ POINTERS FOR DELETES   
   6 7MLG    NDEL1 DELETE
   8 7N66          BCT   4  NDEL1
   7 7NKQ          ADX   7  1
   7 7P5B          EXIT  7  0
   8 7PK2    NDEL2 LDX   1  FX1  
   7 7Q4L          EXIT  7  0
   2 7QJ=
   5 7R3W    REPLY   
  15 7RHG          LDX   3  ACAP(2)             [N(M) OF PERI
   7 7S36          ADN   3  1
  13 7SGQ          FADDRESS APETADDR(2),3,HLOCK1,2   
   8 7T2B          STO   6  0(3) 
   7 7TG2          EXIT  7  0
   2 7T_L
   5 7WF=    TOPCA   
   8 7WYW          LDX   1  FX1  
   9 7XDG          TOPCA1   PLINK1(1)
   7 7XY6          EXIT  5  0
   4 7YCQ    [   
  11 7YXB          MENDAREA AOLPGAP,K99LPTWO 
   4 7_C2    #END
   8 ____ ...64775112001100000000
