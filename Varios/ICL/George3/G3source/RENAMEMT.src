  11 22FL    #OPT  K0RENAMEMT=K0OLMT>K0ALLGEO
  11 22_=    #OPT  K6RENAMEMT=K6OLMT>K6ALLGEO
   7 23DW    #LIS  K0RENAMEMT
   4 23JR ...[   
  17 23NN ...[  (C)   COPYRIGHT  INTERNATIONAL COMPUTERS LTD  1982   
   4 23SK ...[   
  12 23YG          SEG      RENAMEMT,65,N.R.BOULT
  13 24D6          SEGENTRY K1RENAMEMT,QK1RENAMEMT   
  13 24XQ          SEGENTRY K2RENAMEMT,QK2RENAMEMT   
  13 25CB          SEGENTRY K3RENAMEMT,QK3RENAMEMT   
  21 25X2    [THIS SEGMENT RENAMES A MAG TAPE & RECORDS THE CHANGES IN :SYSTEM.DOCUME
  18 26BL    [:SYSTEM.SERIAL AND THE APPROPRIATE USER DIRECTORY,IF ANY.  
   4 26W=    [   
  21 27*W    [ON ENTRY THE CURRENT ACTIVITY IS THE CPA,AFTER WHICH THERE IS CHAINED  
  21 27TG    [A BLOCK TYPE AONBS/GREN WHICH CONTAINS WORDS 1-7 OF THE PERI CONTROL   
  13 28*6    [AREA FOR MODE #500 IN WORDS A1-A1+6.   
   4 28SQ    [   
  21 29#B    [ON EXIT THE CURRENT ACTIVITY IS STILL THE CPA,FOLLOWED BY THE AONBS/GRE
  18 29S2    [A1 OF AONB/GREN IS SET TO THE TAPE SERIAL NO IF SUCCESSFUL,
   9 2=?L    [SET NEGATIVE IF FAILED.
   9 2=R=    SHDDR          4HHDDR   
   8 2?=W    SLAN           4HC1 
  15 2?QG    TACT                               [TYPE OF CPA 
   9 2#=6    #HAL           CPAT,0   
   4 2#PQ    #   
   4 2*9B    #   
   4 2*P2    [   
   6 2B8L    [ENTRY POINT
   4 2BN=    [   
   6 2C7W    QK1RENAMEMT 
   9 2CMG          STOZ     AWORK1(2)
  19 2D76          LDX   0  ACOMMUNE3(2)        [WRITE/NOTWRITE HDDR MARKER  
   9 2DLQ          STO   0  AWORK4(2)
  16 2F6B          STOZ     AWORK3(2)           [SET ENTRY MARKER
   8 2FL2          BRN      START
   6 2G5L    QK2RENAMEMT 
   9 2GK=          STOZ     AWORK1(2)
   8 2H4W          BRN      Q1   
   6 2HJG    QK3RENAMEMT 
   7 2J46          LDN   0  1
   9 2JHQ          STO   0  AWORK1(2)
   4 2K3B    Q1  
   7 2KH2          LDN   0  1
  16 2L2L          STO   0  AWORK3(2)           [SET ENTRY MARKER
   9 2LG=          STOZ  AWORK4(2)   
   5 2L_W    START   
  10 2MFG          LDX   5  ACOMMUNE2(2) 
   9 2M_6          STO   5  AWORK2(2)
  20 2NDQ          JWPR     ,STAR1              [JUMP IF WRITE PERMIT RING FITTED
   5 2NYB ...NOWPR UP
   5 2PD2    STAR1   
   9 2PXL          LDX   0  AWORK1(2)
   8 2QC=          BNZ   0  Q2   
   9 2QWW          LDX   6  JOBNO(2) 
  21 2RBG          GETACT   AREWIND,,1          [GET ACTIVITY FROM WHICH TO CONTROL T
  19 2RW6          LDX   3  BPTR(2)             [LOAD POINTER TO NEW ACTIVITY
   9 2S*Q          STO   6  JOBNO(3) 
   9 2STB          STO   5  AWORK2(3)
   9 2T*2          LDX   6  AWORK3(2)
   9 2TSL          STO   6  AWORK3(3)
   9 2W#=          LDX   6  AWORK4(2)
   9 2WRW          STO   6  AWORK4(3)
   9 2X?G          STOZ     AWORK1(3)
  19 2XR6          CHANGEPER 3                  [GIVE MT TO AREWIND ACTIVITY 
  18 2Y=Q          HUNT     1,AONBS,GREN        [LOOK FOR RENAME BLOCK   
  17 2YQB          CHAIN    1,3                 [CHAIN AFTER ACTIVITY
  19 2_=2          CHANGEACT 3                  [AND CHANGE INTO NEW ACTIVITY
   4 2_PL    Q2  
  21 329=          LDN   6  0                   [SET MARKER FOR STAGE 0-REWIND TAPE  
   5 32NW    REWIND  
   8 32RF ...      JWPR     ,Q3CY
   7 32W4 ...      LDN   7  1
   8 32YM ...      BRN      P6   
   8 333= ...Q3CY  LDX   2  FX2  
   7 335T ...      LDN   7  0
  15 338G          LDN   0  7                   [REWIND MODE 
  10 33N6          DCA      ,DLA,0,MODE  
   4 347Q    PERI
   8 34MB          LOCKC    2,/  
   8 3572          GPERI    ,2   
   6 35LL          COOR1 
   7 366=          UNLOCK   2
  16 36KW          LDCH  4  CPRW1(2)            [REPLY CHARACTER 
   9 375G    #SKI  K6RENAMEMT>199-199
  10 37K6          TRACE    6,RNMTSWIT   
   7 384Q          SMO      6
   7 38JB          BRN      /
   8 3942          BRN      P1   
   6 39HL          NULL  
   8 3=3=          BRN      P3   
   8 3=GW          BRN      P4   
   8 3?2G          BRN      P5   
   4 3?G6    P1  
   7 3?_Q          ANDN  4  4
  17 3#FB          BNZ   4  PFAIL               [J TO EXIT IF FAILED 
   9 3#_2          LDX   4  AWORK2(2)
   8 3*DL          BNZ   4  P1A  
   8 3*Y=          LDN   4  20   
   4 3BCW    P1A 
  12 3BXG ...      SETUPCORE 4,3,AONBS,GHDDR,1   
   8 3B_P ...      STOZ     A1(3)
   7 3C3Y ...      SBN   4  1
   8 3C67 ...      LDN   0  A1(3)
   9 3C8B ...      LDN   1  A1+1(3)  
   7 3C=K ...      SMO      4
   7 3C#S ...      MOVE  0  0
   9 3CC6          LDX   4  AWORK3(2)
   8 3CWQ          BNZ   4  P1B  
  13 3D*4    [     GET HEADER FROM :SYSTEM.DOCUMENT  
  12 3DRB          OPENSYS  XBRKIN,DOCUMENT,READ 
  12 3F9N          SDSEARCH  NOSD,CPPTR,CPPTR(2) 
   7 3FN2          READAGAIN 
  10 3G6#          MHUNT    3,FILE,FRB   
  10 3GJL          MHUNT    1,AONBS,GHDDR
   9 3H2Y          LDN   4  A1+1(3)  
   8 3HF=          LDN   5  A1(1)
   8 3HXJ          MOVE  4  20   
   9 3J36 ...      LDXC  4  A1+1(1)  
  20 3J6N ...      BCS      /                   [TEST FOR XENOTAPE - REMOVE B0   
  15 3J== ...      STO   4  A1+1(1)             [RESTORE TSN 
   7 3J*W          CLOSETOP  
   8 3JS8          BRN      P3   
  10 3K=L    P1B   HUNT     1,AONBS,GREN 
   9 3KQ=          LDN   4  A1+1(1)  
   9 3L9W          LDN   5  A1+2(3)  
   7 3LPG          MOVE  4  6
   8 3M96          LDX   0  EDATE
   9 3MNQ          STO   0  A1+8(3)  
   8 3N8B          SMO      FX1  
   8 3NN2          LDX   0  SHDDR
   8 3P7L          STO   0  A1(3)
   9 3PM=          DFLOAD   ,TSN,0   
  18 3Q6W          BPZ   0  P1C                 [BRANCH UNLESS XENOTAPE  
   8 3QLG          ERX   0  GSIGN
   4 3R66    P1C 
   9 3RKQ          STO   0  A1+1(3)  
   7 3S5B          LDN   5  0
   8 3SK2          BRN      PA2A 
   4 3Y2B    P3  
  12 3_F=          SETNCORE 20,1,AONBS,HDRSAVE   
  10 3_YW          HUNT     3,AONBS,GHDDR
   8 42DG          LDN   4  A1(3)
   8 42Y6          LDN   5  A1(1)
  19 43CQ          MOVE  4  20                  [PRESERVE OLD HEADDER NAME   
  10 43XB          HUNT     1,AONBS,GREN 
   9 44C2 ...      LDX   0  A1+1(3)  
  19 44WL          STO   0  A1(1)               [PUT TSN INTO RENAME BLOCK   
   9 45B=          LDN   4  A1+1(1)  
   9 45TW          LDN   5  A1+2(3)  
   9 46*G          LDX   0  AWORK2(2)
   8 46T6          BZE   0  P3A  
   7 47#Q          SBN   0  2
   7 47SB          SMO      0
   7 48#2          MOVE  4  0
   8 48RL          BRN      P3B  
   4 49?=    P3A 
  19 49QW          MOVE  4  6                   [PUT NEW NAME INTO HDDR BLOCK
   8 4==G          LDX   0  EDATE
  20 4=Q6          STO   0  A1+8(3)             [STORE TODAYS DATE IN HDDR BLOCK 
   4 4?9Q    P3B 
  12 4?PB          OPENSYS  XBRKIN,SERIAL,READ   
   9 4#92          DFLOAD   ,TSN,5   
  16 4#NL          BNG   5  PA2                 [J IF XENOTAPE   
  20 4*8=          SSEARCH  PA2,5               [LOOK FOR ENTRY IN :SYSTEM.SERIAL
   8 4*MW          LDCT  0  #200 
  18 4B7G          ANDX  0  5(3)                [PICK UP TEMPORARY BIT   
  16 4BM6          LDX   4  3(3)                [LOAD USER NAME  
   8 4C6Q          LDX   5  4(3) 
   8 4CLB          LDX   3  2(3) 
   8 4D62          LDN   6  12   
  18 4DKL          BZE   0  P3B1                [BRANCH UNLESS WORK TAPE 
   7 4F5=          ADN   6  6
   4 4FJW    P3B1
   7 4G4G          CLOSETOP  
  11 4GJ6          SETUPCORE 6,2,FILE,FABSNB 
   7 4H3Q          SBN   6  2
   9 4HHB          STO   6  HDREC(2) 
  15 4J32          STO   3  HDREC+1(2)          [OWNERS NAME 
  10 4JGL          STO   4  HDREC+2(2)   
  10 4K2=          STO   5  HDREC+3(2)   
   8 4KFW          SBN   6  10   
  18 4K_G          BZE   6  P3B2                [BRANCH UNLESS WORK TAPE 
   8 4LF6          LDX   2  FX2  
  10 4LYQ          JOBLOCK  JOBNO(2),3   
  10 4MDB          MHUNT    2,FILE,FABSNB
   9 4MY2          LDN   4  JNAME(3) 
  10 4NCL          LDN   5  HDREC+4(2)   
  14 4NX=          MOVE  4  3                   [JOB NAME
   7 4PBW          LDN   0  1
  10 4PWG          STO   0  HDREC+7(2)   
  10 4QB6          STO   0  HDREC+8(2)   
   9 4QTQ          LDX   0  SLAN(1)  
  10 4R*B          STO   0  HDREC+9(2)   
   4 4RT2    P3B2
   7 4S#L          SMO      6
  10 4SS=          LDN   3  HDREC+4(2)   
   8 4T?W          LDN   4  1(3) 
   8 4TRG          STOZ     0(3) 
  16 4W?6          MOVE  3  5                   [ZERO LOCAL NAME 
   7 4WQQ          NGN   0  1
  17 4X=B          STO   0  6(3)                [NO RETENTION PERIOD 
  10 4XQ2          MHUNT    1,AONBS,GHDDR
   9 4Y9L          LDX   0  A1+1(1)  
  13 4YP=          STO   0  7(3)                [TSN 
   8 4_8W          LDN   0  #2001
   9 4_NG          DSA   0  ATYPE(2) 
  20 5286          CALL  6  RENAME              [CHANGE NAME IN DIRECTORY ENTRY  
   7 52MQ          PICKREP  5
   9 537B          TESTREPN OK,OUT   
   8 53M2          BRN      PA2A 
   4 546L    PA2 
   7 54L=          CLOSETOP  
  15 555W    PA2A  LDN   4  8                   [ERASE MODE  
  10 55KG          DCA      ,DLA,4,MODE  
   8 5656          LDN   4  #3777
  10 56JQ          DCA      ,STO,4,COUNT 
  18 574B          LDN   6  3                   [INDICATE ERASE TRANSFER 
   8 57J2          BRN      PERI 
   7 583L    P4    ANDN  4  4
  15 58H=          BNZ   4  PFA                 [J IF FAILURE
   9 592W          LDX   0  AWORK4(2)
   7 5984 ...#UNS  ILOGACC   
   7 59*= ...      ANDN  0  1
   8 59GG          BNZ   0  P5A  
  10 5=26          HUNT     3,AONBS,GHDDR
   9 5=FQ          LOCKC    3,SERCHA 
  10 5=_B          MHUNT    3,AONBS,GHDDR
  15 5?F2          LDN   4  1                   [WRITE MODE  
  10 5?YL          DCA      ,DLA,4,MODE  
   8 5#D=          LDN   0  20   
   9 5#XW          LDX   6  AWORK2(2)
   8 5*CG          BZE   6  P4A  
  15 5*X6          LDX   0  6                   [LABEL LENGTH
   4 5BBQ    P4A 
  13 5BWB          DCA      ,STO,0,COUNT        [HEAD
   8 5CB2          LDN   4  A1(3)
  14 5CTL          DCA      ,STO,4,CTSA         [ADDRESS 
   8 5D*=          LGEOG    ,0   
   8 5DSW          SLL   0  18   
  19 5F#G          STO   0  A1+9(3)             [SAVE GEOG NO IN CH 0 OF WD 9
  20 5FS6          LDN   6  4                   [INDICATE REWRITE HDDR TRANSFER  
   8 5G?Q          BRN      PERI 
   7 5GRB    P5    UNLOCK   3
   7 5H?2          ANDN  4  6
  15 5HQL          BNZ   4  PFA                 [J IF FAILURE
   4 5J==    P5A 
  19 5JPW          DFJUMPS  ,NONS,P5B           [JUMP IF NON-STANDARD TAPE   
  21 5K9G    #              SYSTEM DOCUMENT IS NOT UPDATED IF THE TAPE IS NON-STANDAR
  13 5KP6          OPENSYS  XBRKIN,DOCUMENT,GENERAL  
  12 5L8Q    PA5   SDSEARCH NOSD,CPPTR,CPPTR(2)  
   7 5LNB          READAGAIN 
  10 5M82          HUNT     3,FILE,FRB   
  10 5MML          HUNT     1,AONBS,GHDDR
   8 5N7=          LDN   4  A1(1)
  17 5NLW          LDN   5  A1+1(3)             [MOVE NEW NAME ACROSS
   8 5P6G          MOVE  4  20   
  10 5PL6          NAME     3,FILE,FWB   
  18 5Q5Q          REWRITE                      [REWRITE ENTRY IN SYS.DOC
  16 5QKB          CLOSETOP                     [CLOSE SYS.DOC   
   4 5R52    P5B 
  20 5RJL          KEEPREP  5,OK                [SET REPLY IF RENAME SUCCESSFUL  
   4 5S4=    OUT 
  10 5SHW          VFREE    AONBS,GHDDR  
  10 5T3G          VFREE    AONBS,HDRSAVE
   4 5T?# ...P6  
   8 5TH6          LDX   2  FX2  
   9 5W2Q          LDX   0  AWORK1(2)
   8 5WGB          BNZ   0  Q3   
   8 5X22          CALL  0  SCA  
  17 5XFL          CHANGEPER 3                  [GIVE MT BACK TO CPA 
  17 5X_=          HUNT     1,AONBS,GREN        [FIND RENAME BLOCK   
  18 5YDW          CHAIN    1,3                 [CHAIN RENAME AFTER CPA  
  20 5YYG          LDX   4  2                   [REMEMBER ADDRESS OF REWIND BLOCK
  16 5_D6          CHANGEACT 3                  [CHANGE INTO CPA 
   7 5_XQ          LOCK     4
  19 62CB          MURDER   4                   [GET RID OF REWIND ACTIVITY  
   8 62M8 ...     BNZ   7  NOWPR 
   4 62X2    Q3  
   7 63BL          PUTREP   5
  10 63W=          TRACE    5,RENMTREP   
   7 64*W          UPPLUS   1
  18 64N? ...PFA   BNZ   5  PFAIL               [J IF INSECURE ENTRANT   
   9 652N          DFLOAD   ,TSN,0   
  16 657W          BNG   0  PFAIL               [J IF XENOTAPE   
   9 65*6          LDX   0  AWORK3(2)
   8 65SQ          BNZ   0  PFAIL
  10 66#B          MFREE    AONBS,GHDDR  
  11 66S2          MHUNT    2,AONBS,HDRSAVE  
  10 67?L          NAMETOP  2,AONBS,GHDDR
  20 67R=          CALL  6  RENAME              [RESET NAME IN DIRECTORY ENTRY   
   5 68=W    PFAIL   
  18 68QG          KEEPREP  5,TAPEFAIL          [SET REPLY IF TAPE FAILED
  15 69=6          BRN      OUT                 [J TO EXIT   
   5 69PQ    XBRKIN  
  18 6=9B          GEOERR   1,XBRKIN            [GEOERR IF BREAKIN OCCURS
  10 6=P2    NOSD  GEOERR   1,NOSYSDOC   
  15 6?8L    [SUBROUTINE WHICH SEARCHES FOR COMMAND ACTIVITY 
   4 6?N=    SCA 
  20 6#7W          LDX   3  2                   [LOAD ADDRESS OF CURRENT ACTIVITY
  17 6#MG    SCA1  LDX   3  FPTR(3)             [LOOK AT NEXT BLOCK  
  15 6*76          LDX   4  ATYPE(3)            [TEST IF CPA 
   8 6*LQ          SMO      FX1  
  17 6B6B          BXU   4  TACT,SCA1           [BRANCH IF NOT CPAT  
   9 6BL2          LDX   4  JOBNO(3) 
  18 6C5L          TXU   4  JOBNO(2)            [TEST IF CORRECT JOB NO  
  17 6CK=          BCS      SCA1                [IF NOT,J FOR NEXT   
  15 6D4W          EXIT  0  0                   [IF OK,EXIT  
  18 6DJG    #     SUBROUTINE TO CHANGE THE NAME IN THE DIRECTORY ENTRY  
  17 6F46    #     TAPE SPECIFIED BY FILE/FABSNB (ZERO LOCAL NAME)   
  13 6FHQ    #     NEW NAME SPECIFIED BY AONBS/GHDDR 
   5 6G3B    RENAME  
   8 6GH2          SBX   6  FX1  
  14 6H2L          OPENDIR  XBRKIN,GENERAL,ERASING,QUERY 
   6 6H43 ...#UNS  FPGPT 
   4 6H5D ...(   
  10 6H6T ...      MHUNT    3,FILE,ENT   
  10 6H8= ...      LDX   0  EINCLOOK(3)  
  19 6H9M ...      BPZ   0  PROC1                  [J IF NOT IN USE BY DUMPER
   7 6H?4 ...      CLOSETOP  
  10 6H#F ...      KEEPREP  5,DUMPTAPE   
   8 6H*W ...      BRN      OUT  
   5 6HC? ...PROC1   
   4 6HDN ...)   
   7 6HG=          SETUPTAB  
   6 6H_W          TABSET
  11 6JFG          SETNCORE 6,1,FILE,FLOCNB  
  10 6J_6          MHUNT    2,AONBS,GHDDR
   9 6KDQ          LDN   4  A1+2(2)  
   8 6KYB          LDN   5  A1(1)
   7 6LD2          MOVE  4  3
   9 6LXL          STOZ     A1+3(1)  
   9 6MC=          STOZ     A1+4(1)  
   9 6MWW          STOZ     A1+5(1)  
  19 6NBG          GETDIR   2                   [IS NEW NAME IN USE BY FILE  
  12 6NW6 ...      TESTREP  NOFILE,S3,VRYWRONG,S3
   7 6P*Q ...S3B   CLOSETOP  
  10 6PTB          MFREE    FILE,FLOCNB  
   9 6Q*2          SETREP   ALREADY  
   8 6QSL          BRN      S7   
   4 6R#=    S3  
  10 6SR6          MHUNT    3,FILE,ENT   
  10 6TQB          MHUNT    2,AONBS,GHDDR
   9 6W=2          LDN   4  A1+2(2)  
   9 6WPL          LDN   5  ELOC1(3) 
  16 6X9=          MOVE  4  6                   [PUT IN NEW NAME 
  10 6YN6          MHUNT    1,FILE,FLOCNB
   7 6_7Q          LDN   5  1
  16 6_MB          ORS   5  ATYPE(1)            [SET MAG TAPE BIT
   8 7272          LDN   5  A1(1)
  19 72LL          MOVE  4  5                   [SET UP DETAILS FOR GETDIR   
   8 72_3 ...      LDCT  0  #600 
   8 73?D ...      ADN   0  #7777
  14 73KW          ANDX  0  A1+4(1)             [GEN NO. 
  18 745G          BNZ   0  S2F                 [J IF NOT ZERO OTHERWISE 
  19 74K6          LDCT  0  #100                [SET BIT INDICATING ZERO GEN.
   9 754Q    S2F   STO   0  A1+4(1)  
  18 755D ...      GETDIR   2                 [CALCULATE NEW NAME BEFORE 
  16 7566 ...      TESTREP  OK,S2G,NOFILE,S2H   [OLD ONE DELETED 
  10 756F ...      TESTREP  VRYWRONG,S3B 
  10 756S ...      TESTREPN WRGEN,XGDER  
   7 757G ...      CLOSETOP  
  10 7588 ...      MFREE    FILE,FLOCNB  
   8 758W ...      SETREP   WRGEN
   8 759J ...      BRN      S7   
   5 75== ...XGDER   
  10 75=Y ...      GEOERR   1,RENAMEGD   
   4 75?L ...S2G 
   9 75## ...      MFREEW   FILE,ENT 
   4 75*2 ...S2H 
  13 75*N ...      TABULATE [GET BACK TO OLD ENTRY   
   7 75BB ...      FREETAB   
  10 75C4 ...      MHUNT    3,FILE,ENT   
  10 75CQ ...      NAMETOP  3,FILE,FRB   
  10 75DD ...      MHUNT    1,FILE,FLOCNB
  19 75F6 ...      MHUNT    2,AONBS,GHDDR       [UPDATE GEN NO IN ENT & GHDDR
  15 75FS ...      LDX   0  A1+4(1)             [FROM FLOCNB 
   9 75GG ...      STO   0  EGEN(3)  
  19 75GM ...      LDCT  4  #600                      [ONLY UPDATE GEN. NO.  
  19 75GS ...      ANDX  4  A1+6(2)                   [IN GHDDR IF RELATIVE  
  18 75G_ ...      BZE   4  S2J                        [GEN. NO. GIVEN   
   9 75H8 ...      STO   0  A1+6(2)  
   4 75HR ...S2J 
   9 75JB          LDX   4  ENUSE(3) 
   7 7642          LDX   5  4
   7 76HL          ADN   5  1
  17 773=          BZE   4  S2C                 [J IF NO TRAP RECORDS
   4 77GW    S2A 
  15 782G          READ                         [READ ENTRY  
   8 78G6          BCT   4  S2A  
   4 78_Q    S2C 
   7 79FB          LDX   4  5
   4 79_2    S2B 
  15 7=DL          DELETE                       [DELETE ENTRY
   8 7=Y=          BCT   4  S2B  
  10 7?2F ...      MHUNT    1,FILE,FLOCNB
  19 7?4N ...      LDX   0  A1+4(1)                [RESET GEN. NO. IN FLOCNB 
  18 7?6X ...      BNZ   0  S2D                    [TO POSITION CORRECTLY
  15 7?96 ...      LDCT  0  #100                   [IF GEN. 0
   9 7??* ...      STO   0  A1+4(1)  
   4 7?*J ...S2D 
   7 7?CW          GETDIR   2
  11 7?XG ...      TESTREP  OK,S4,NOFILE,S45 
  10 7#C6          GEOERR   1,RENAMEMT   
   4 7#WQ    S4  
   9 7*25 ...      MFREEW   FILE,ENT 
   4 7*5D ...S45 
  10 7*8R ...      MFREE    FILE,FLOCNB  
   7 7*#8 ...#UNS  ILOGACC   
   4 7**H ...(   
   7 7*BW ...      LDX   4  5
  15 7*D9 ...S45A  HUNT2    2,FILE,FRB          [LOCATE ENT  
   8 7*FJ ...      BCT   4  S45A 
  10 7*GX ...      NAMETOP  2,FILE,FWB   
   6 7*J= ...      INSERT
  12 7*KK ...      TESTREP  OK,S45B,FNEARLY,S45B 
  10 7*LY ...      GEOERR   1,RENAMEMT   
  10 7*N? ...S45B  MHUNT    1,FILE,FWB   
  15 7*PL ...      NAMETOP  1,FILE,ENT          [SAVE AS ENT 
   7 7*Q_ ...      BCT   5  /
   4 7*S# ...)   
   7 7*W2    S4A   LDX   4  5
  10 7B*L    S5    HUNT2    2,FILE,FRB   
   8 7BT=          BCT   4  S5   
  10 7C#W          NAMETOP  2,FILE,FWB   
  19 7CSG          INSERT                       [PUT ENTRY IN NEW POSITION   
  11 7D#6          TESTREP  OK,S6,FNEARLY,S6 
  10 7DRQ          GEOERR   1,RENAMEMT   
   4 7F?B    S6  
   9 7FR2          MFREE    FILE,FWB 
   8 7G=L          BCT   5  S4A  
   7 7G#* ...#UNS  ILOGACC   
   4 7GB4 ...(   
   9 7GCR ...      LDX   0  AWORK4(2)
   8 7GFG ...      BPZ   0  S6A  
   9 7GH9 ...      LDN   5  CWRITE   
   8 7GJY ...      LOGACCESS 2,5 
   9 7GLM ...S6A   MFREE    FILE,ENT 
   4 7GNB ...)   
  16 7GQ=          CLOSETOP                     [CLOSE DIRECTORY 
   8 7H9W          SETREP   OK   
   4 7HPG    S7  
   8 7J96          ADX   6  FX1  
   7 7JNQ          EXIT  6  0
   5 7K8B    SERCHA  
  10 7KN2          HUNT     2,AONBS,GHDDR
   7 7L7L          EXIT  1  0
  11 7LM=          MENDAREA 50,K99RENAMEMT   
   4 7M6W    #END
   8 ____ ...14003355000100000000
