  10 22FL ...   SEG   IPBSTARB,,,,G269   
  16 22_=    [  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982  
  11 23DW    #LIS  K0IPBSTARB>K0IPB>K0ALLGEO 
   7 23NN ...#OPT G269 = 0   
   6 23YG ...#SKI G269&1 
  16 248# ...# WITH UGUG EDIT M269 (REMOTE LISTFILE DEBATCHERS)  
   4 24D6    [   
  13 24XQ          SEGENTRY  K4IPBSTARB,SK4IPBSTARB  
  13 25CB          SEGENTRY  K5IPBSTARB,SK5IPBSTARB  
  13 25X2          SEGENTRY  K7IPBSTARB,SK7IPBSTARB  
   4 26BL    [   
  15 26W=    [NOTE.      X3 = POINTER TO AMOP IPBIN ACTIVITY 
   4 27*W    [   
  21 27TG    # IF THE IPB/DCP BECOMES INOPERABLE WHILE THE SYSTEM IS TRYING TO USE IT
  21 28*6    # THE IPB IS DISENGAGED AND THE INPUT ACTIVITY IS LEFT WITH A LINK TO   
  20 28SQ    # K5IPBSTARB AND A -VE CHAINADD. ON SEEING THE ENGAGE BUTTON BLANKET
  21 29#B    # RESTARTS THE ACTIVITY. A SCANNING ACTIVITY IS SET UP WITH A LINK TO   
  21 29S2    # K7IPBSTARB, AND THIS CHECKS THE IDF AND ATTEMPTS TO START UP AGAIN.   
   4 2=?L    #   
  13 2=R=          SEGENTRY K8IPBSTARB,SK8IPBSTARB   
  21 2?=W    # K8IPBSTARB IS USED BY THE AMSCAN ACTIVITY SET UP BY A ^MOP ON^ COMMAND
  16 2?QG    # WHEN IT WISHES TO RESTART SYSTEM USE OF THE IPB.  
   4 2#=6    #   
   7 2#PQ    #UNS CRAMP<1$1  
   4 2*9B    (   
  21 2*P2    # K9IPBSTARB WILL (MARK 9?) BE USED BY THE AMSCAN ACTIVITY SET UP BY AN 
  21 2B8L    # ^UPDATE^ COMMAND, TO OBTAIN A NEW CONFIGURATION REPORT FROM THE DCP.  
   4 2BN=    )   
   6 2C7W    #UNS CRAMP  
   4 2CMG    (   
  13 2D76          SEGENTRY K9IPBSTARB,SK9IPBSTARB   
  20 2DLQ    # K9IPBSTARB IS USED BY THE AMSCAN ACTIVITY SET UP BY A ^MOP ON,,NS^
  21 2F6B    # COMMAND WHEN IT WISHES TO OBTAIN A NEW CONFIGURATION REPORT FROM THE  
  13 2FL2    # DCP AND RESTART SYSTEM USE OF THE IPB.
   4 2G5L    )   
   4 2GK=    #   
   4 2H4W    #   
  13 2HJG          SEGENTRY K10IPBSTARB,SK10IPBSTAR  
  12 2J46    # K10IPBSTARB IS FOR VIRTUAL 7900'S.
  20 2JHQ    # IT WAITS IF NECESSARY FOR A VIRTUAL 7900 TO BECOME OPERABLE BEFORE
  20 2K3B    # ATTEMPTING TO COMPLETE A MOP ON ACTION BY EMS OR MOP ON COMMAND . 
  20 2KH2    # A WMC READ OLPA INDICATES WHEN THE VIRTUAL 7900 IS OPERABLE AND   
  21 2L2L    # K10IPBSTART THEN SETS UP A SCANNING ACTIVITY W/ A LINK TO K11IPBSTART 
  18 2LG=    # IT SIMULATES A K5 RESTART AND RUNS IN THE INPUT ACTIVITY .
   4 2L_W    #   
  13 2MFG          SEGENTRY K11IPBSTARB,SK11IPBSTAR  
  13 2M_6    # K11IPBSTARB IS FOR VIRTUAL 7900'S .   
  17 2NDQ    # IT CHECKS THE IDF  AND COMPLETES THE MOP ON ACTION .  
  11 2NYB    # IT IS EQUIVALENT TO K7IPBSTARB
  21 2PD2    # EXCEPT THAT THE SYSTEM SUPERVISORY SEGMENTS WILL BE OUTPUT LATER BY   
  20 2PXL    # IPBOUT IN ORDER TO AVOID DUPLICATION OF V7900 LINK LEVEL CODE .   
  12 2QC=    # IT RUNS IN AN AMSCAN ACTIVITY .   
   9 2QWW    PAIR3 PAIR     IPBIN,1  
  10 2RBG    PAIR4 PAIR     IPBSTARB,5   
  10 2RW6    PAIR5 PAIR     IPBSTARB,7   
  10 2S*Q    PAIR6 PAIR     IPBSTARB,11  
   7 2STB    #UNS  CCERROR   
   4 2T*2    (   
   8 2TSL    SCCA  SBX   5  FX1  
   8 2W#=          CCERROR  6,7  
   8 2WRW          ADX   5  FX1  
   7 2X?G          EXIT  5  0
   8 2XR6    SCCE  CALL  5  SCCA 
  10 2Y=Q          MHUNTW   3,AMXOR,INBF 
   8 2YQB          ADN   3  A1   
   8 2_=2          BRN      REDOK
  10 2_PL    REJO  LDN   6  CCREJECTOT   
   9 329=          LDX   7  CPRW1(2) 
   8 32NW          CALL  5  SCCA 
   8 338G          BRN      ZINOP
   4 33N6    )   
   9 347Q    SEGINIT        #400:0   
   9 34MB                   #020:0   
  10 3572          SEGENTRY K50IPBSTARB  
  10 35LL    #HAL  INPUSIZE+3,INPUSIZE+3 
  20 366=    SEGGT          #02150000           [SUP. SEG. FOR GLOBAL TERMINATOR 
   9 36KW    SEGCONFIG      #01140000
   4 375G    #   
   9 37K6    SEGCLOSE       #01110000
   4 384Q    #   
  21 38JB    V7900SSSBLK    [CONTENTS OF THE VIRTUAL 7900 SYSTEM SUPERVISORY SEGMENTS
  11 3942                   [AMXOR/IOUT BLOCK
  14 39HL                   0                   [IMOPTY  
  13 3=3=                   #40000000           [IBSW
  16 3=GW                   #03000000           ['INITIALISE' SS 
  20 3?2G    #HAL           AVBUFFSIZE,AVBUFFSIZE[(INPUT AND OUTPUT BUFFER SIZES)
  16 3?G6                   4HGEO3              [(PROGRAM NAME)  
  12 3?_Q    #DEF  VSSSBLKLEN = 0? - V7900SSSBLK 
  20 3#FB    VGTBLOCK                           [CONTAINS GLOBAL TERM FOR V7900  
  15 3#_2                   0                         [IMOPTY
  15 3*DL                   #40000000                 [IBSW  
  18 3*Y=                   #02150000                 [SET GLOBAL TERM   
  11 3BCW    #DEF  VSSSGTERM = 0? - VGTBLOCK 
   7 3BXG                   0
  10 3CC6    #DEF  VGTLEN = 0? - VGTBLOCK
   4 3CWQ    #   
  18 3DBB    XMASK          #74777776           [MASK OUT BITS 4,5 & 23  
   9 3DW2    ZJINOP         +HUINOP  
   9 3F*L    ZJMOP3         +JMOP3   
   6 3FGS ...#SKI G269&1 
   9 3FN2 ...ZZZZ           4HZZZZ   
   6 3FT=    #UNS CRAMP  
  18 3G#W    ZJMOP4      +JMOP4            [UNIT %A : MOP ON ABANDONED   
   9 3GSG    ZJUPFAIL       +JUPFAIL 
  19 3H#6    ZJUPOK         +JUPOK              [UNIT %A : UPDATE SUCCESSFUL 
   7 3HRQ    #UNS   CCERROR  
   8 3J?B    XIN             4HIN
   4 3JR2    #   
   4 3K=L    [   
  20 3KQ=    [   SUBROUTINE TO SAVE SPACE BY PUTTING GETACT MACRO IN SUBROUTINE  
   4 3L9W    [   
   5 3LPG    XGETACT 
   8 3M96          SBX   7  FX1  
  11 3MNQ          GETACT   AMSCAN,IPBTYPE,1 
   8 3N8B          ADX   7  FX1  
   7 3NN2          EXIT  7  0
   4 3P7L    [   
  10 3PM=    [   SUBROUTINE TO SAVE SPAVE
  17 3Q6W    [   BY PUTTING SET UP OF AMXOR IOUT INTO A SUBROUTINE   
   4 3QLG    [   
   5 3R66    XSETIOUT
   8 3RKQ          SBX   7  FX1  
  12 3S5B          SETUPCORE  6,2,AMXOR,IOUT,1   
   8 3SK2          ADX   7  FX1  
   7 3T4L          EXIT  7  0
   4 3TJ=    [   
  11 3W3W    [     SUBROUTINE TO SAVE SPACE  
  14 3WHG    [     BY PUTTING IDFTPSCN INTO A SUBROUTINE 
   4 3X36    [   
   5 3XGQ    XIDFSCN 
   8 3Y2B          SBX   7  FX1  
  10 3YG2          IDFTPSCN  XBI,XIDFA   
   7 3Y_L          SETALLOC  
   7 3_F=          ADN   7  1
   5 3_YW    XIDFA   
   8 42DG          ADX   7  FX1  
   7 42Y6          EXIT  7  0
   4 43CQ    #   
  20 43XB    #     SUBROUTINE TO TEST IF AN IPB IS A VIRTUAL 7900 AND IF TRUE TO 
  20 44C2    #     SET UP AN AMXOR/IOUT BLOCK CONTAINING THE SYSTEM SUPERVISORY  
  21 44WL    #     SEGMENTS 'INITIALISE' AND 'SET GLOBAL TERMINATOR' AND TO SET UP   
  15 45B=    #     AN INPUT BUFFER FOR THE INPUT ACTIVITY .  
  20 45TW    #     THE SYSTEM SUPERVISORIES AMXOR/IOUT BLOCK WILL EVENTUALLY BE  
  14 46*G    #     OUTPUT BY THE AMOP/IPBOUT ACTIVITY .  
  16 46T6    #     ON ENTRY AND EXIT X3 -> THE IPB INPUT ACTIVITY
  17 47#Q    #     EXITS+1 IF IPB IS NOT A VIRTUAL 7900, ELSE EXITS+0
  15 47SB    #     HAVING COORDINATED WITH X6 & X7 UNDEFINED 
   7 48#2    #     LINK IN X5
   4 48RL    #   
   6 49?=    SVIOUTINBF  
  11 49QW          JV7900   CPPTR(3),SVSUPBLK
  21 4==G          EXIT  5  1                   [IPB IS NOT A VIRTUAL 7900 : EXIT +1 
   5 4=Q6    SVSUPBLK
   8 4?9Q          SBX   5  FX1  
  10 4?PB          LDN   6  VSSSBLKLEN   
   9 4#92          CALL  7  XSETIOUT 
  11 4#NL          LDN   6  V7900SSSBLK(1)   
   8 4*8=          LDN   7  A1(2)
  10 4*MW          MOVE  6  VSSSBLKLEN   
   9 4B7G          LDN   6  VGTLEN   
   9 4BM6          CALL  7  XSETIOUT 
  10 4C6Q          LDN   6  VGTBLOCK(1)  
   8 4CLB          LDN   7  A1(2)
   9 4D62          MOVE  6  VGTLEN   
  21 4DKL          LDX   6  GTERM               [MOVE IN THE GLOBAL TERMINATOR CHARS 
  11 4F5=          STO   6  A1+VSSSGTERM(2)  
  20 4FJW          LDN   6  AVBUFFSIZE          [SET UP INPUT BUFFER AND CHAIN   
  18 4G4G          SETUPCORE 6,2,AMXOR,INBF,1   [AFTER THE INPUT ACTIVITY
   8 4GJ6          CHAIN    2,3  
  14 4H3Q          ADX   5  FX1                 [EXIT +0 
   7 4HHB          EXIT  5  0
   4 4J32    #   
   4 4JGL    #   
   4 4K2=    [   
  13 4KFW    [   SUBROUTINES TO READ IN OR WRITE OUT 
   4 4K_G    [   
  17 4LF6    PERIN MHUNTW   3,AMXOR,INBF        [ENTRY FOR INPUT PERI
  10 4LYQ          SEGENTRY K52IPBSTARB  
  19 4MDB          LDN   5  INPUSIZE+3          [LENGTH OF INPUT TRANSFER.   
   7 4MY2          LOCK  3   
   8 4NCL          ADN   3  A1   
   8 4NX=    #SKI  K6IPB>100-100 
   8 4PBW          JSBUSY   ,PGE 
  10 4PWG          DSCA     ,STO,3,CTSA  
  10 4QB6          DSCA     ,STO,5,COUNT 
  10 4QTQ          DSCA     ,DLA,7,MODE  
  19 4R*B          LDN   5  0                   [SET INPUT SWITCH FOR WAIT1  
  15 4RT2    PERAG GSPERI   ,2                  [ISSUE READ  
   8 4S#L          BRN      PERY1
   4 4SS=    #   
  18 4T?W    PEROT LOCK  3                      [ENTRY FOR OUTPUT PERI   
   8 4TRG          ADN   3  A1   
   8 4W?6    #SKI  K6IPB>100-100 
   4 4WQQ    (   
   9 4X=B          JUNBUSY  ,PFREE   
   9 4XQ2    PGE   GEOERR   1,CABUSY 
   4 4Y9L    )   
  10 4YP=    PFREE DCA      ,STO,3,CTSA  
  10 4_8W          DCA      ,STO,5,COUNT 
  10 4_NG          DCA      ,DLA,4,MODE  
  20 5286          JANON    CPPTR(2),XIT61            [J. IF ANONYMOUS IF INOP   
  15 52MQ    PEROG GPERI    ,2                  [ISSUE WRITE.
  15 537B    PERY1 LDXC  0  CPRW1(2)            [REPLY WORD. 
  18 53M2          BCC      WAIT2               [J. IF ALREADY FINISHED. 
   7 546L          SLC   0  6
   7 54L=          ANDN  0  3
  21 555W          BZE   0  WAIT1               [J. IF DIRECT RESPONSE ^ACCEPTED^.   
   7 55KG          ANDN  0  1
  19 5656          BNZ   0  XIT61               [J. IF REJECTED - INOPERABLE.
  17 56JQ          EXIT  6  0                   [REJECTED - OPERABLE.
  21 574B    WAIT1 BZE   5  WAIT2               [AMOP/IPBIN IS IN FIXED CHAPTER AREA 
   7 57J2          LOCK  2   
   8 583L    WAIT2 SBX   6  FX1  
  21 58H=    WAIT3 COOR1                        [WAIT FOR NOTIFICATION OF END OF PERI
   9 592W          LDXC  0  CPRW1(2) 
   8 59GG          BCS      WAIT3
   7 5=26          SLC   0  5
   7 5=FQ          ADX   6  1
  17 5=_B          BPZ   0  XIT61               [J. IF INOPERABLE.   
   8 5?F2          ANDN  0  #13  
  17 5?YL          BZE   0  XIT63               [J. IF TRANFER O.K.  
   7 5#D=          SRC   0  2
   8 5#XW          BNG   0  XIT61
   7 5*CG          SRC   0  2
   8 5*X6          BNG   0  XIT62
   7 5BBQ          EXIT  6  0
   7 5BWB    XIT61 EXIT  6  1
   7 5CB2    XIT62 EXIT  6  2
   5 5CD4 ...XIT63   
   7 5CG6 ...      STOZ     0
   5 5CJ8 ...XIT63C  
   8 5CL= ...      LDX  5   1(3) 
  15 5CN# ...      BXE  5   XMSK1,XIT63A        [FOUND NULL  
   5 5CQB ...XIT63B  
  15 5CSD ...      EXIT 6   3                   [FOUND DATA  
   5 5CWG ...XIT63A  
  19 5CYJ ...      ADN  3   1                   [LOOK AT NEXT WORD OF DATA   
   7 5D2L ...      SBN  0   2
  21 5D4N ...      BZE  0   XIT63B              [FOUND 3 NULLS,NEXT WD MUST BE DATA  
  18 5D6Q ...      ADN  0   3                   [RESTORE COUNT OF NULLS  
   9 5D8S ...      BRN      XIT63C   
   7 5D=W ...XMSK1 #01570000 
   4 5D*=    #   
   8 5DSW    ROUT1 SMO      FX2  
   9 5F#G          LDX   3  IPBRING  
  20 5FS6          SBN   3  IPBRING             [GET ADDRESS OF OTHER ACTIVITY.  
   9 5G?Q          STOZ     ACC5(3)  
  17 5GRB          SBX   5  1                   [DEDATUMISE OWN LINK.
  21 5H?2    ROUT2 ADS   0  ACC5(3)             [STEP (OR SET) LINK IN OTHER ACTIVITY
   8 5HQL          SBN   2  A1   
  17 5J==          UNLOCK   2                   [UNLOCK THE BUFFER   
   7 5JPW    ROUT3 LDX   2  3
  18 5K9G          FPUT                         [ENLIST OTHER ACTIVITY.  
  17 5KP6          COOR1                        [WAIT TO BE WOKEN.   
   6 5L8Q    SK4IPBSTARB 
  16 5LNB          ADX   5  1                   [EXIT TO LINK.   
   7 5M82          EXIT  5  0
   4 5MML    #   
  18 5N7=    #     READ RUBBISH OR ^READY^ FOLLOWING INITIALISE COMMAND. 
   4 5NLW    #   
  18 5P6G    READY LDN   7  #12                 [END READ MODE FOR READY.
   8 5PL6          BRN      RENM 
  18 5Q5Q    READR LDN   7  2                   [READ MODE FOR RUBBISH   
  15 5QKB    RENM  CALL  6  PERIN               [DO THE READ.
  20 5R52          BRN      TINOP               [TREAT AS INOPERABLE IF REJECTED 
  16 5RJL          BRN      TINOP               [OR INOPERABLE   
  18 5S4=          BRN      TIMEO               [TIMED OUT : TRY TWICE   
   8 5SHW          ANDN  7  #10  
  17 5T3G          BZE   7  REDOK               [J. IF READ RUBBISH  
  20 5TH6          LDCT  6  #014                [J. IF MESSAGE IS ^READY^ TO WAKE
  18 5W2Q          ERX   6  1(3)                [UP THE E.M.S. ACTIVITY  
  19 5WGB          BNZ   6  READY               [NOT ^READY^ : REPEAT READ   
   7 5X22    REDOK LDX   2  3
   8 5XFL          SMO      FX2  
   9 5X_=          LDX   3  IPBRING  
  18 5YDW          LDN   0  1                   [STEP LINK TO EXIT +1.   
   9 5YYG          SBN   3  IPBRING  
  17 5_D6          BRN      ROUT2               [WAKE E.M.S. ACTIVITY
  18 5_XQ    TIMEO ERX   7  GSIGN               [TIMED OUT : TRY TWICE   
  15 62CB          BNG   7  PERAG               [TRY AGAIN   
  20 62X2          BRN      TINOP               [T.O. TWICE : TREAT AS INOPERABLE
   7 63BL    #UNS  JPGLTER   
   4 63W=    (   
   4 64*W    [   
  14 64TG    #  READ REPLY TO GLOBAL TERMINATOR SETTIMG  
   4 65*6    [   
   7 65SQ    RGTAK LDN   0  4
  20 66#B          ORS   0  IPBSW(2)            [SET GLOBAL TERMINATOR APPLICABLE
  13 66S2          LDN   7  #12                 [MODE
  13 67?L          CALL  6  PERIN               [READ
  17 67R=          BRN      REJOP               [REJECTED OPERABLE   
  15 68=W          BRN      TINOP               [INOPERABLE  
  18 68QG          BRN      PERAG               [TIMED OUT - TRY AGAIN   
   8 69=6          LDN   7  #141 
   9 69PQ          BRN      XCHREP   
   4 6=9B    )   
   4 6=P2    #   
  19 6?8L    #     READ CONFIGURATION REPORT FOLLOWING CONFIGURATION REQUEST.
   4 6?N=    #   
   7 6#7W    RCONF LDN   7  2
  15 6#MG          CALL  6  PERIN               [DO THE READ.
  17 6*76          BRN      REJOP               [REJECTED - OPERABLE.
  15 6*LQ          BRN      TINOP               [INOPERABLE. 
  18 6B6B          BRN      PERAG               [TIMED OUT : TRY AGAIN   
   8 6BL2          LDN   7  #154 
   5 6C5L    XCHREP  
   8 6CK=          LDX   6  1(3) 
   8 6D4W          SRL   6  12   
  17 6DJG          ERX   6  7                   [CHECK INITIAL REPLY 
  16 6F46          SBN   7  #154                [CALL FOR CONFIG 
  13 6FHQ          BZE   6  XCHB                [OK  
   9 6G3B          ERN   6  #245$#141
  19 6GH2          BNZ   6  SOSSS               [NOT ACKNOWLEDGE OR ILLEGSL  
   8 6H2L          LDX   0  2(3) 
   8 6HG=          SRL   0  12   
  20 6H_W          SBN   0  #2114               [INVALID CODE - CONFIG. REPORT   
  14 6JFG          BZE   7  XCHA                [J CONF. 
  19 6J_6          SBN   0  1                   [INVALID CODE - GOBAL WERM.  
   4 6KDQ    XCHA
  18 6KYB          BNZ   0  SOSSS               [J. OTHER ILLEAGAL CODE  
  15 6LD2          BZE   7  REDOK               [CALL FROM OK
   7 6LXL          LDN   0  4
  18 6MC=          ERS   0  IPBSW(2)            [CLEAR GOBAL TERM APPL.  
   8 6MWW          BRN      REDOK
   8 6NBG    XCHB  BZE   7  XCONF
   8 6NW6          BRN      REDOK
  19 6P*Q    SOSSS LDN   6  CCSSSWRONG           [ILLEGAL CODE NOT RECONIZED 
  16 6PTB          LDX   7  1(3)                [FIRST WD. OF SEG
   8 6Q*2          BRN      SCCE 
   5 6QSL    XCONF   
  17 6R#=          LDX   7  2(3)                [LENGTH OF REPORT.   
   6 6RRW    #UNS CRAMP  
   4 6S?G    (   
   7 6SR6          SBN   7  1
  19 6T=Q          BZE   7  REDOK               [NULL CONFIGURATION REPORT   
  18 6TQB          ADN   7  1                   [RESET LENGTH OF REPORT  
   4 6W=2    )   
  21 6WPL          STOZ     AWORK4(2)           [CURRENT POSITION IN OUTPUT BLOCK.   
   7 6X9=    #UNS  CCERROR   
   4 6XNW    (   
   7 6Y8G          LDX   6  7
   7 6YN6          ADN   6  4
  12 6_7Q          SETUPCORE  6,1,AMXOR,AJOURN   
   8 6_MB          SMO      FX1  
   8 7272          LDX   0  XIN  
   8 72LL          STO   0  A1(1)
   8 736=          LDX   0  FTIME
   9 73KW          STO   0  A1+1(1)  
   9 745G          LDX   0  FTIME+1  
   9 74K6          STO   0  A1+2(1)  
   8 754Q          LGEOG      ,6 
   9 75JB          STO   6  A1+3(1)  
   8 7642          LDN   4  A1(3)
   9 76HL          LDN   5  A1+4(1)  
  21 773=          MOVLONG  4,5,7              [COPY IDF IREPORT INTO AMXOR,AJOURN   
  17 77GW          CHAINJRN     [MACRO TO CHAIN AMXOR AJOURN BLOCK   
   4 782G    )   
   8 78G6          LDN   4  2(3) 
  21 78_Q          SETUPCORE 7,1,IDF,IREPORT    [SET UP BLOCK TO HOLD COMPLETE REPORT
  19 79FB          NGX   3  CPRW1(2)            [FIND LENGTH OF THIS MESSAGE.
  10 79_2          SEGENTRY K51IPBSTARB  
  10 7=DL          ADN   3  INPUSIZE+1   
   8 7=Y=          LDN   5  A1(1)
  21 7?CW          ANDN  3  #7777               [MASK OUT OPERABLE BIT IN CHARACTER 0
  21 7?XG          ADX   5  AWORK4(2)           [CURRENT POSITION IN OUTPUT BLOCK.   
  18 7#C6          ADS   3  AWORK4(2)           [UPDATE OUTPUT POINTER.  
   8 7#WQ    #SKI  K6IPB>99$99   
   4 7*BB    (   
  21 7*W2          BXGE  7  AWORK4(2),OKSIZ     [J. IF MESSAGE < LENGTH ANNOUNCED.   
   7 7B*L    #UNS  CCERROR   
   4 7BT=    #SKI
  10 7C#W          GEOERR   1,BIGREPRT   
   7 7CSG    #UNS  CCERROR   
   4 7D#6    (   
  10 7DRQ          LDN   6  CCBIGREPORT  
  15 7F?B          BRN      SCCE                [REPORT ERROR
   4 7FR2    )   
   5 7G=L    OKSIZ   
   4 7GQ=    )   
   9 7H9W          MOVLONG   4,5,3   
  17 7HPG          BXE   7  AWORK4(2),REPRT     [J. IF ALL RECEIVED. 
   7 7J96          LDN   7  2
  15 7JNQ          CALL  6  PERIN               [READ MORE.  
  17 7K8B          BRN      REJOP               [REJECTED - OPERABLE.
  15 7KN2          BRN      SINOP               [INOPERABLE. 
  18 7L7L          BRN      PERAG               [TIMED OUT : TRY AGAIN   
  21 7LM=          LDCT  0  #40                 [J. IF NOT CONTINUATION OF REPORT.   
   8 7M6W          ANDX  0  0(3) 
   8 7MLG          BZE   0  NCONT
  18 7N66          LDN   4  1(3)                [SET POINTER FOR MOVE.   
  20 7NKQ          NGX   3  CPRW1(2)            [FIND THE LENGTH OF THIS MESSAGE.
  19 7P5B          MHUNTW   1,IDF,IREPORT       [RELOCATE THE OUTPUT BLOCK.  
  18 7PK2          LDX   7  ALOGLEN(1)          [DECLARED REPORT LENGTH  
  10 7Q4L          BUX   3  K51IPBSTARB  
   7 7QJ=    #UNS  CCERROR   
   4 7R3W    #SKI
  20 7RHG    REJOP GEOERR   1,REJECTED          [TRANSFER REJECTED - OPERABLE.   
   7 7S36    #UNS  CCERROR   
   4 7SGQ    (   
  20 7T2B    REJOP LDN   6  CCREJECTIN          [INPUT TRANSFER REJECTED OPERABLE
   9 7TG2          LDX   7  CPRW1(2) 
   8 7T_L          BRN      SCCE 
   4 7WF=    )   
   7 7WYW    #UNS  CCERROR   
   4 7XDG    #SKI
  21 7XY6    NCONT GEOERR   1,CONFIG'N          [CONFIGURATION REPORT < LENGTH GIVEN.
   7 7YCQ    #UNS  CCERROR   
   4 7YXB    (   
  10 7_C2    NCONT LDN   6  CCNOCONTCR   
   8 7_WL          LDX   7  0(3) 
   8 82B=          BRN      SCCE 
   4 82TW    )   
   4 83*G    #   
   4 83T6    #   
  20 84#Q    REPRT LDX   3  IPBRING(2)          [RELOCATE THE ^WRITING^ ACTIVITY.
   9 84SB          SBN   3  IPBRING  
  21 85#2          CHAIN    1,3                 [THE E.M.S. ACTIVITY & WAKE IT UP.   
  10 85RL          MHUNTW   2,AMXOR,INBF 
   8 86?=          ADN   2  A1   
  18 86QW          LDN   0  2                   [STEP LINK TO EXIT AT +2.
   8 87=G          BRN      ROUT2
   4 87Q6    #   
   7 889Q    #UNS  CCERROR   
   4 88PB    #SKI
  21 8992    SINOP MFREEW   IDF,IREPORT         [FREE THE PARTLY FILLED REPORT BLOCK.
   7 89NL    #UNS  CCERROR   
   4 8=8=    (   
  10 8=MW    SINOP VFREEW   IDF,IREPORT  
   4 8?7G    )   
   8 8?M6    TINOP SBN   3  A1   
  16 8#6Q          FREECORE 3                   [AMXOR/INBF BLOCK
  19 8#LB          LDX   2  IPBRING(2)          [FIND THE ^WRITING^ ACTIVITY.
   9 8*62          SBN   2  IPBRING  
  16 8*KL          FPUT                         [SET IT GOING.   
  16 8B5=          DERINGD  IPBRING(2)          [CLEAR THE RING. 
  18 8BJW          EMPTY    IPBRING(2)          [RESET OUR RING ELEMENTS.
   5 8C4G    ZUNNO   
  21 8CJ6          NGS   1  CHAINADD(2)         [MARK TO TELL BLANKET WE'RE ASLEEP.  
  17 8D3Q          DISENGPER                    [DISENGAGE THE IPB.  
  18 8DHB          COOR1                        [WAIT FOR RE-ENGAGEMENT. 
   4 8F32    #   
   6 8FGL    SK5IPBSTARB 
   8 8G2=          JANON   ,ZUNNO
   9 8GFW          LDX   0  IPBSW(2) 
  14 8G_G          ANDX  0  BIT10               [BIT 10  
  18 8HF6          BZE   0  ZUOK                [IDF CONTAONS UNIT INFO  
  17 8HYQ          DOWN     IDFCHK,4            [INSERT INFO INTO IDF
   4 8JDB    ZUOK
  21 8JY2          CALL  7  XGETACT            [GET SCANNING ACTIVITY TO DO RESTART  
   9 8KCL          LDX   3  BPTR(2)  
  19 8KX=          LDX   4  CPPTR(2)            [ADDRESS OF IPB DEVICE LIST. 
  18 8LBW          LINKSET  3,PAIR5(1)          [SET LINK TO K7IPBSTART  
   9 8LWG          STO   4  CPPTR(3) 
  20 8MB6          STO   2  ACC3(3)             [SAVE ADDRESS OF INPUT ACTIVITY. 
  20 8MTQ          BRN      ROUT3               [J. TO SET IT ALL GOING AGAIN.   
   4 8N*B    #   
  20 8NT2    SK10IPBSTAR                        [RUNS IN A V7900 AMOP/IPBIN ACT  
   4 8P#L    #   
  20 8PS=    #              IF OR WHEN THE V7900 IS OPERABLE, SET UP AND FPUT A  
  20 8Q?W    #              SCANNING ACTIVITY (AMSCAN/IPBTYPE) WITH A LINK TO K11
  17 8QRG          JENG     ,VOPERABLE          [V7900 IS OPERABLE   
  20 8R?6                                       [IE THE PROGRAM'S FIRST WMC READ 
  15 8RQQ                                       [IS STORED   
  19 8S=B    #              VIRTUAL 7900 INOPERABLE : REPORT FACT TO OPERTOR 
  20 8SQ2          LGEOG    ,6                  [SET X6 = V7900 OPS UNIT NUMBER  
  20 8T9L          LDX   4  ZJINOP(1)           [UNIT %A HAS BECOME INOPERABLE   
   8 8TP=          CALL  5  OMES 
  15 8W8W          JENG     ,VOPERABLE          [OPERABLE NOW
  21 8WNG          NGS   1  CHAINADD(2)         [SET CHAINADD NEGATIVE TO SIMULATE   
  21 8X86                                       [AN INOPERABLE (,MOPPED ON) 7900 TO  
  18 8XMQ                                       [MOPCOM(AT LABEL UINOP)  
  21 8Y7B          LDX   0  XMASK(1)            [UNSET 'BEING MOPPED ON/OFF' FLAGS   
  21 8YM2          ANDS  0  IPBSW(2)            [IF SET AND WAKE UP ANY MOP ACTIVITY 
  16 8_6L          FON      #30                 [WAITING FOR US .
  10 8_L=          BRN      VIPBINSLEEP  
   4 925W    #   
  20 92KG    VOPERABLE                          [SIMULATE A K5IPBSTART RESTART   
  18 9356          CALL  7  XGETACT            [SET UP SCANNNING ACTIVITY
  18 93JQ                                       [NOTE IT IS NEVER LOCKED 
   9 944B          LDX   5  CPPTR(2) 
  20 94J2          LDX   2  BPTR(2)             [SET LINK TO K11IPBSCAN IN ACT   
  10 953L          LINKSET  2,PAIR6(1)   
  19 95H=          STO   5  CPPTR(2)            [SET DEVICE LIST PTR IN ACT  
  21 962W          FPUT                         [SET ACT RUNNING TO COMPLETE MOP ON  
  21 96GG          LDCT  0  #10                 [ENSURE ^MOPPING OFF^ FLAG IS UNSET  
  21 9726          ORS   0  IPBSW(2)            [(SET IF V7900 WAS INITIALLY OPERABLE
  20 97FQ          ERS   0  IPBSW(2)            [)AS PART OF THE K5 SIMULATION   
  17 97_B          COOR1                        [WAIT FOR NEW LINK   
   6 98F2    VIPBINSLEEP 
  20 98YL          LDCT  0  #100                [MARK THIS INPUT ACTIVITY ASLEEP 
   9 99D=          ORS   0  IPBSW(2) 
  21 99XW          COOR1                        [WAIT TIL THE V7900 BECOMES OPERABLE 
  21 9=CG                                       [(MOP OFF SIMPLY SETS CHAINADD +VE)  
  10 9=X6          BRN      SK10IPBSTAR  
   4 9?BQ    #   
  19 9?WB    #    ###########################################################
   4 9#B2    #   
  20 9#TL    #     CODE ABOVE IS OBEYED IN AMOP/IPBIN ; BELOW IN EMS ACTIVITY.   
   4 9**=    #   
  19 9*SW    #    ###########################################################
   4 9B#G    #   
  21 9BS6    # SUBROUTINE TO ISSUE ^INITIALISE^ COMMAND AND CHECK THE REPLY (IF ANY).
   4 9C?Q    #   
   8 9CRB    START SBX   4  FX1  
  10 9D?2          SEGENTRY K53IPBSTARB  
  20 9DQL          LDN   5  INPUSIZE+3          [SET UP INPUT BUFFER AND CHAIN   
  18 9F==          SETUPCORE 5,1,AMXOR,INBF,1   [AFTER THE INPUT ACTIVITY
  15 9FPW          STO   4  AWORK3(2)           [SAVE LINK.  
   8 9G9G          CHAIN    1,3  
  21 9GP6          ENRINGD  IPBRING(2),IPBRING(3)[RING IN THE IPBIN ACTIVITY(->X3)   
  10 9H8Q    TINIT MHUNTW   3,AMXOR,IOUT 
  19 9HNB          LDN   4  #15                 [END WRITE CANCEL READ MODE  
  15 9J82          LDN   5  3                   [WORD COUNT  
  20 9JML          CALL  6  PEROT               [OUTPUT THE ^INITIALISE^ MESSAGE 
  20 9K7=          BRN      RUBSH               [J. TO READ RUBBISH IF REJECTED. 
  15 9KLW          BRN      ZINOP               [INOPERABLE  
  17 9L6G          BRN      PEROG               [REPEAT IF TIMED OUT 
  19 9LL6          JANON   ,ZINOP               [OPERABLE BUT ANON UNIT NO SO
   7 9M5Q          LDX   2  3
  19 9MKB          LDN   0  READY               [SET LINK TO READ ^READY^.   
  21 9N52          CALL  5  ROUT1               [WAKE UP INPUT ACTIVITY & AWAIT REPLY
  21 9NJL          BRN      VINOP               [J. IF IT WENT INOPERABLE DURING READ
   7 9P4=    #UNS  JPGLTER   
   4 9PHW    (   
  10 9Q3G          MHUNTW   3,AMXOR,IOUT 
  19 9QH6          LDX   5  SEGGT(1)            [GLOBAL TERMINATOR SETTING   
   9 9R2Q          STO   5  A1+1(3)  
   8 9RGB          LDX   5  GTERM
   9 9S22          STO   5  A1+2(3)  
  14 9SFL          LDN   5  3                   [COUNT   
  13 9S_=          LDN   4  #15                 [MODE
  15 9TDW          CALL  6  PEROT               [SEND EEGMENT
   7 9TYG    #UNS  CCERROR   
   4 9WD6    #SKI
  17 9WXQ          BRN      REJOP               [REJECTED OPERABLE   
   7 9XCB    #UNS  CCERROR   
   4 9XX2    (   
  16 9YBL          BRN      REJO               [REJECTED OPERABLE
   4 9YW=    )   
  15 9_*W          BRN      ZINOP               [INOPERABLE  
  14 9_TG          BRN      PEROG               [TIMED   
   7 =2*6          LDX   2  3
  18 =2SQ          LDN   0  RGTAK               [LINK FOR INPUT ACTIVITY 
  17 =3#B          CALL  5  ROUT1               [SWITCH ACTIVITIES   
  15 =3S2          BRN   VINOP                  [INOPERABLE  
   4 =4?L    )   
  20 =4R=          SMO      AWORK3(2)           [OTHERWISE EXIT TO THE LINK + 1. 
   7 =5=W          EXIT  1  1
   4 =5QG    #   
  20 =6=6    RUBSH LDX   2  3                   [LET IPBIN READ IN RUBBISH BEFORE
  18 =6PQ          LDN   0  READR               [DOING INITIALISE AGAIN. 
  21 =79B          CALL  5  ROUT1               [WAIT FOR READ: RETURN IS TO ^TINIT^.
  16 =7P2          BRN      VINOP               [INOP DURING READ
  20 =88L          BRN      TINIT               [READ OK : TRY ^INITIALISE^ AGAIN
   4 =8N=    #   
  19 =97W    #     SUBROUTINE TO INFORM CONSOLE MESSAGE ; IPB GEOG NO IN X6 ;
  13 =9MG    #     MESSAGE ADDRESS IN X4 ; CALL ON X5
   4 ==76    #   
  15 ==LQ    OGE   LGEOG    CPPTR(3),6          [GET GEOG NO.
   8 =?6B    OMES  SBX   5  FX1  
   8 =?L2          GEOPACK  6,6,6
  10 =#5L          OUTPACK  6,1,GEOPER   
   8 =#K=          INFORMX  ,4,1 
   8 =*4W          ADX   5  FX1  
   7 =*JG          EXIT  5  0
   4 =B46    #   
  17 =BHQ    # SUBROUTINE TO ISSUE ^CLOSE DOWN^ COMMAND TO THE DCP.  
   4 =C3B    #   
   7 =CH2    SHUT  SBX   6  1
  15 =D2L          STO   6  AWORK3(2)           [SAVE LINK.  
   8 =DG=          BRN      OFF  
   4 =D_W    #   
   4 =FFG    #   
  21 =F_6    # SUBROUTINE TO ISSUE ^CONFIGURATION REQUEST^, USING THE REPLY TO UPDATE
  21 =GDQ    # THE IDF, AND THEN CLOSE THE DCP. V IS SET ON EXIT IF REPORT OBTAINED. 
   4 =GYB    #   
   7 =HD2    UPDAT SBX   6  1
  15 =HXL          STO   6  AWORK3(2)           [SAVE LINK.  
  10 =JC=          MHUNTW   3,AMXOR,IOUT 
  21 =JWW          LDX   5  SEGCONFIG(1)        [SET UP THE CONFIGURATION REQUEST.   
   9 =KBG          STO   5  A1+1(3)  
  15 =KW6          LDN   5  2                   [WORD COUNT. 
  14 =L*Q          LDN   4  #15                 [MODE.   
  18 =LTB          CALL  6  PEROT               [AND PERI IT TO THE IPB. 
   7 =M*2    #UNS  CCERROR   
   4 =MSL    #SKI
  17 =N#=          BRN      REJOP               [REJECTED - OPERABLE.
   7 =NRW    #UNS  CCERROR   
   4 =P?G    (   
   8 =PR6          BRN      REJO 
   4 =Q=Q    )   
  17 =QQB          BRN      ZINOP               [J. IF INOPERABLE.   
  15 =R=2          BRN      PEROG               [TIMED OUT   
   7 =RPL          LDX   2  3
  20 =S9=          LDN   0  RCONF               [SET LINK TO READ CONFIG. REPORT.
  21 =SNW          CALL  5  ROUT1               [WAKE UP INPUT ACTIVITY & AWAIT REPLY
  21 =T8G          BRN      VINOP               [J. IF IT WENT INOPERABLE DURING READ
  21 =TN6          BRN      OFF                 [J. IF CONFIG. REPORT NOT FORTHCOMING
  20 =W7Q          LDX   0  GSIGN               [SET B0 IN LINK (SETS V ON EXIT).
   9 =WMB          ORS   0  AWORK3(2)
  19 =X72          IDFTPUPD XBI                 [PASS THE REPORT TO THE IDF  
   4 =XLL    #   
   7 =Y6=    #UNS CRAMP<1$1  
  19 =YKW    OFF   MHUNTW   3,AMXOR,IOUT        [RELOCATE THE OUTPUT BUFFER. 
   6 =_5G    #UNS CRAMP  
   4 =_K6    (   
  21 ?24Q    OFF   LDX   0  AWORK4(2)           [TEST FOR CLOSE DOWN SUPPRESSION FLAG
  21 ?2JB          STOC  0  AWORK4(2)           [LEAVING IT UNSET(NO INOPERABILITY)  
  20 ?342          BCS      NBACK               [WAS SET : SKIP CLOSE DOWN & EXIT
  19 ?3HL          MHUNTW   3,AMXOR,IOUT        [RELOCATE THE OUTPUT BUFFER  
   4 ?43=    )   
  20 ?4GW          LDX   5  SEGCLOSE(1)         [SET UP THE CLOSE DOWN MESSAGE.  
   9 ?52G          STO   5  A1+1(3)  
  13 ?5G6          LDN   4  #15                 [MODE
  15 ?5_Q          LDN   5  2                   [WORD COUNT  
  20 ?6FB          CALL  6  PEROT               [OUTPUT THE CLOSE DOWN MESSAGE.  
   7 ?6_2    #UNS  CCERROR   
   4 ?7DL    #SKI
  17 ?7Y=          BRN      REJOP               [REJECTED - OPERABLE.
   7 ?8CW    #UNS  CCERROR   
   4 ?8XG    (   
   8 ?9C6          BRN      REJO 
   4 ?9WQ    )   
  17 ?=BB          BRN      ZINOP               [J. IF INOPERABLE.   
  15 ?=W2          BRN      PEROG               [TIMED OUT   
  18 ??*L          CALL  6  UINOP               [CLEAR DOWN THE IPBIN.   
  19 ??T=          STOZ     CHAINADD(3)         [UNSET ^WAKE ME UP^ MARKER.  
   7 ?##W    #UNS CRAMP<1$1  
   9 ?#SG          ADX   1  AWORK3(2)
   6 ?*#6    #UNS CRAMP  
   9 ?*RQ    NBACK ADX   1  AWORK3(2)
   7 ?B?B          EXIT  1  0
   4 ?BR2    #   
   8 ?C=L    UINOP SBN   3  A1   
  16 ?CQ=          UNLOCK   3                   [OUTPUT BUFFER.  
  10 ?D9W          LDX   3  IPBRING(2)   
   9 ?DPG          SBN   3  IPBRING  
  20 ?F96          LINKSET  3,PAIR4(1)          [SET LINK IN IPBIN TO K5IPBSTART 
  18 ?FNQ          HUNT2    1,AMXOR,INBF,3      [FREE THE INPUT BUFFER.  
   7 ?G8B          FREECORE 1
  16 ?GN2          DERINGD  IPBRING(2)          [CLEAR THE RING. 
  18 ?H7L          EMPTY    IPBRING(2)          [RESET OUR RING ELEMENTS.
  20 ?HM=          NGS   1  CHAINADD(3)         [MARK IPBIN ASLEEP FOR BLANKET.  
  17 ?J6W          DISENGPER                    [DISENGAGE THE IPB.  
   7 ?JLG          EXIT  6  0
   4 ?K66    #   
  16 ?KKQ    ZINOP CALL  6  UINOP               [RESET THE IPBIN.
   6 ?L5B    #UNS CRAMP  
   4 ?LK2    (   
  21 ?M4L    VINOP LDCT  0  #400                [IF ^SUPPRESS CLOSE DOWN^ FLAG SET,  
  20 ?MJ=          ANDX  0  AWORK4(2)           [I.E. K9 ENTRY,UNSET ^WAKE ME UP^
  16 ?N3W          ORS   0  CHAINADD(3)         [MARKER IF SET   
  10 ?NHG          ERS   0  CHAINADD(3)  
  21 ?P36          LDX   4  ZJINOP(1)           [REPORT UNIT HAS BECOME INOPERABLE   
   7 ?PGQ    #UNS  CCERROR   
   4 ?Q2B    (   
   7 ?QG2          LDN   0  1
   9 ?Q_L          ANDX  0  IPBSW(3) 
   8 ?RF=          BNZ   0  NSY  
  20 ?RYW    [                                   J IF ALREADY OUTPUT INOP MESSAGE
   4 ?SDG    )   
   7 ?SY6    #UNS CRAMP<1$1  
   4 ?TCQ    (   
   8 ?TXB    VINOP SMO      FX1  
  16 ?WC2          LDX   4  ZJINOP              [UNIT INOPERABLE 
   4 ?WWL    )   
   8 ?XB=          CALL  5  OGE  
  19 ?XTW          LDN   0  1                   [SET ^INOP MESS SENT^ FLAG   
   9 ?Y*G          ORS   0  IPBSW(3) 
   7 ?YT6    #UNS  CCERROR   
   4 ?_#Q    NSY 
   9 ?_SB          ADX   1  AWORK3(2)
   7 #2#2          EXIT  1  0
   4 #2RL    #   
   4 #3?=    #   
  20 #3QW    # SUBROUTINE TO SET UP AN AMXOR/ADEVS BLOCK OF THE REQUIRED LENGTH  
  15 #4=G    # AND CHAIN IT AFTER THE AMOP/IPBIN ACTIVITY.   
   4 #4Q6    #   
  16 #59Q    NDEVS SBX   4  1                   [RELATIVISE LINK.
  11 #5PB          MHUNTW   1,IDF,IDENTIFIERS
  10 #692          LDX   5  ALOGLEN(1)   
   6 #69F ...#SKI G269&1 
   4 #69Y ...(   
  17 #6=C ...      SBN   5  1                   [ NO. OF IDENTIFIERS 
  16 #6=W ...      LDN   7  0                   [ COUNT OF *LP'S 
   7 #6?* ...XCT1  SMO      5
   8 #6?S ...      LDEX  0  A1(1)
  15 #6#? ...      SBN   0  3                   [ *LP IDENT. 
   8 #6#Q ...      BNZ   0  XCT2 
   7 #6*9 ...      ADN   7  2
   8 #6*N ...XCT2  BCT   5  XCT1 
  18 #6B7 ...#     NOW SET UP NEW BLOCK TYPE IUSER/BATCHC WITH A TWO-WORD
  19 #6BL ...#     ... ENTRY FOR EACH REMOTE LP: IDENTIFIER,DEBATCHER WORD   
  11 #6C5 ...      SETUPCORE 7,2,IUSER,BATCHC
   9 #6CJ ...      LDX   6  ZZZZ(1)  
  11 #6D3 ...      MHUNTW   1,IDF,IDENTIFIERS
  10 #6DG ...      LDX   5  ALOGLEN(1)   
   7 #6D_ ...      SBN   5  1
   7 #6FD ...XSAT1 SMO      5
   8 #6FX ...      LDEX  0  A1(1)
   7 #6GB ...      SBN   0  3
   8 #6GT ...      BNZ   0  XSAT2
   7 #6H# ...      LDX   0  5
  16 #6HR ...      ADX   0  A1(1)               [ IDENTIFIER NO. 
   8 #6J= ...      STO   0  A1(2)
  16 #6JP ...      STO   6  A1+1(2)             [ INITIALIZE ZZZZ
   7 #6K8 ...      ADN   2  2
   8 #6KM ...XSAT2 BCT   5  XSAT1
  18 #6L6 ...      SBX   2  7                   [ RESET TO START OF BLOCK
  16 #6LK ...      LDX   5  ALOGLEN(1)          [ REINSTATE X5   
  17 #6M4 ...      CHAIN    2,3                 [ ONTO IPBIN ACTIVITY
   4 #6MH ...)   
   7 #6NL          LDX   6  5
  21 #78=          SLL   5  2                   [SET UP THE IDENTIFIER ROUTING BLOCK.
  12 #7MW          SETXCORE 5,1,AMXOR,ADEVS,,6,4 
  19 #87G          LDCT  5  #40                 [SET THE ^TO BE SCANNED^ BIT.
  16 #8M6          ADX   5  BIT9                [SET ^CLOSED^ BIT
   7 #96Q          LDX   2  1
  20 #9LB          NGN   7  A1                  [INITIALISE THE ROUTING BLOCK.   
  10 #=62    RESET STOZ     A1+BPTR(2)   
  10 #=KL          STO   7  A1+BSTART(2) 
  11 #?5=          STO   5  A1+FOURTHWD(2)   
   7 #?JW          ADN   2  4
   7 ##4G          SBN   7  4
   8 ##J6          BCT   6  RESET
  11 #*3Q          MHUNTW   2,IDF,IDENTIFIERS
   8 #*HB          LDX   0  A1(2)
  20 #B32          STO   0  A1+FOURTHWD(1)      [STORE LOWEST IDENTIFIER NO.-1   
  20 #BGL          CHAIN    1,3                 [CHAIN AFTER THE INPUT ACTIVITY. 
   7 #C2=          SMO      4
  16 #CFW          EXIT  1  0                   [EXIT TO LINK.   
   4 #C_G    [   
   4 #DF6    [   
   4 #DYQ    [   
  21 #FDB    SK11IPBSTAR                        [RUNS IN A V7900 AMSCAN/IPBTYPE ACT  
  21 #FY2          LAMOP    ,3                  [SET X3 -> THE V7900 AMOP/IPBIN ACT  
  20 #GCL    #              ENTER K7IPBSTART TO SET UP THE IDF/IDENTIFIERS BLOCK 
  20 #GX=    #              AND TO DEAL WITH MOPPING ON/OFF INTERLOCKS THEN CALL 
  21 #HBW    #              S/R SVIOUTINBF TO SET UP THE AMXOR/IOUT&AMXOR/INBF BLOCKS
  21 #HWG    #              AND RETURN TO LABEL NSSCAN TO SET UP THE AMXOR/ADEVS BLK,
  21 #JB6    #              SET THE INPUT ACT GOING AT K1IPBIN,AND EXIT TO K1IPBSCAN 
   4 #JTQ    #   
   6 #K*B    SK7IPBSTARB 
   9 #KT2          CALL  7  XIDFSCN  
   8 #L#L          BRN      XMOP 
   8 #LS=          LDCT  0  #10  
  20 #M?W          ANDX  0  IPBSW(3)            [J. IF ALREADY BEING MOPPED ON.  
   8 #MRG          BNZ   0  SUIC 
   8 #N?6          LDCT  0  #10  
  21 #NQQ          ORS   0  IPBSW(3)            [SET ^IPB BEING MOPPED ON^ FLAG(B5)  
   4 #P=B    #   
   6 #PQ2    SK8IPBSTARB 
  20 #Q9L          LDCT  7  #20                 [HAS THE ^MOP OFF^ BIT BEEN SET? 
   9 #QP=          ANDX  7  IPBSW(3) 
   8 #R8W          BNZ   7  XSET3
  21 #RNG          CALL  5  SVIOUTINBF          [IF V7900,JUST SET UP BLKS(DON'T O/P)
  20 #S86          BRN      NSSCAN              [V7900 : SKIP IPB INITIALISATION 
   7 #SMQ          LDN   6  3
   9 #T7B          CALL  7  XSETIOUT 
  10 #TM2          LDN   6  SEGINIT(1)   
   8 #W6L          LDN   7  A1(2)
   7 #WL=          MOVE  6  3
   8 #X5W          CALL  4  START
   8 #XKG          BRN      XSET2
   5 #Y56    NSSCAN  
  18 #YJQ          CALL  4  NDEVS               [SET UP THE ADEVS BLOCK. 
  17 #_4B          LDCT  7  #20                 [J. IF MOPPED OFF.   
   9 #_J2          ANDX  7  IPBSW(3) 
   8 *23L          BNZ   7  XSET1
   6 *2H=    #SKI  ICT   
  21 *32W          FON      ADEVLIN                 [START ACTIVITIES WAITING FOR ADE
  19 *3GG          LINKSET  3,PAIR3(1)          [SET IPBIN'S LINK TO K1IPBIN 
  21 *426          LDX   0  XMASK(1)            [UNSET THE MOPPING ON BIT, IF SET.   
   7 *4FQ          LDX   2  3
   9 *4_B          ANDS  0  IPBSW(3) 
  17 *5F2          FPUT                         [AND SET IT GOING.   
  19 *5YL          ACROSS   IPBSCAN,1           [GO TO START UP IDENTIFIERS. 
   4 *6D=    #   
  19 *6XW    XMOP  LDCT  7  #20                 [UNSET MOP OFF BIT, IF SET.  
   9 *7CG          ANDX  7  IPBSW(3) 
   8 *7X6          BZE   7  SUIC 
   9 *8BQ          ERS   7  IPBSW(3) 
   8 *8WB          BRN      XSET4
   4 *9B2    #   
   6 *9TL    #UNS CRAMP  
   4 *=*=    (   
   6 *=SW    SK9IPBSTARB 
  19 *?#G          YMOPOFF  XBI,/               [MARK UNIT MOPPED OFF IN IDF 
   7 *?S6          LDN   6  3
  21 *#?Q          CALL  7  XSETIOUT            [SEND INITIALISE + GOBAL TERMINATOR  
  17 *#RB          LDN   6  SEGINIT(1)          [SUPERVISORY SEGMENTS
   8 **?2          LDN   7  A1(2)
   7 **QL          MOVE  6  3
   8 *B==          CALL  4  START
  18 *BPW          BRN      NSINOP1             [INOPERABILITY DETECTED  
  21 *C9G          LDCT  7  #400                [SET ^SUPPRESS CLOSE DOWN^ FLAG (B0  
  17 *CP6          ORS   7  AWORK4(2)           [OF AWORK4) FOR UPDAT
  20 *D8Q          CALL  6  UPDAT               [SEND CONFIGURATION REQUEST AND  
  21 *DNB                                       [UPDATE THE IDF IF REPORT OBTAINED   
   8 *F82          LDX   1  FX1  
   9 *FML          LDX   7  AWORK4(2)
   8 *G7=          ANDX  7  GSIGN
  21 *GLW          BNZ   7  NSFAIL              [INOPERABILITY DETECTED & REPORTED   
  21 *H6G          BVCR     NSFAIL              [UPDATE FAILED:NO CONFIG. REP. OPTION
  21 *HL6                                       [OR A NULL CONFIGURATION REPORT GIVEN
  18 *J5Q          LDX   4  ZJUPOK(1)           [REPORT UPDATE SUCCESSFUL
   8 *JKB          CALL  5  OGE  
   8 *K52          LDCT  7  #20  
   9 *KJL          ANDX  7  IPBSW(3) 
  20 *L4=          BNZ   7  XSET1               [MOPPING OFF : CLOSE DOWN UNIT   
  19 *LHW          YMOPON   XBI,XSTAT,,IPBSW    [MARK UNIT MOPPED ON IN IDF  
  21 *M3G          SETALLOC                     [MARK UNIT MOPPED ON IN DEVICE LIST  
   7 *MH6    #SKI  JWPHASE4  
   4 *N2Q    (   
  17 *NGB          LGEOG    ,4                  [GEOGRAPHIC NUMBER   
  14 *P22          ORN   4  #4000               [MOP ON  
  18 *PFL          HLSINFORM XBI,MOP,,4         [INFORM HLS OF MOP EVENT 
   4 *P_=    )   
  18 *QDW          OUTBLOCN 4                   [OUTPUT ^ON^ PARAMETER   
   7 *QYG          LDN   4  1
  10 *RD6          OUTPACK  4,1,SWITCH   
  18 *RXQ          LDX   4  ZJMOP3(1)           [REPORT MOP ON COMPLETED 
   8 *SCB          CALL  5  OGE  
  18 *SX2          BRN      NSSCAN              [JOIN K8IPBSTART'S PATH  
   4 *TBL    #   
   5 *TW=    NSINOP1 
   8 *W*W          LDX   1  FX1  
   7 *WTG          LDN   7  0
  19 *X*6          ADSC  7  CHAINADD(3)         [UNSET ^WAKE ME UP^ MARKER   
   7 *XSQ          ORN   7  1
   9 *Y#B          ANDX  7  IPBSW(3) 
  20 *YS2          BNZ   7  NSFAIL              [INOPERABILITY ALREADY REPORTED  
  21 *_?L          LDX   4  ZJINOP(1)           [REPORT UNIT HAS BECOME INOPERABLE   
   8 *_R=          CALL  5  OGE  
  19 B2=W          LDN   7  1                   [SET ^INOP MESS SENT^ FLAG   
   9 B2QG          ORS   7  IPBSW(3) 
   5 B3=6    NSFAIL  
  17 B3PQ          LDX   4  ZJUPFAIL(1)         [REPORT UPDATE FAILED
   8 B49B          CALL  5  OGE  
  18 B4P2          LDX   4  ZJMOP4(1)           [REPORT MOP ON ABANDONED 
   8 B58L          CALL  5  OGE  
  21 B5N=          BNZ   7  XSET3               [INOPERABILITY REPORTED : SKIP ^SHUT^
   4 B67W    #   
   4 B6MG    )   
  19 B776    XSET1 CALL  6  SHUT                [CLOSE THE IPB DOWN AGAIN.   
   7 B7LQ    #UNS CRAMP<1$1  
  18 B86B          HUNT2    1,AMXOR,ADEVS,3     [FREE THE ROUTING BLOCK. 
   6 B8L2    #UNS CRAMP  
  21 B95L          HUNT2J   1,AMXOR,ADEVS,3,XSET3 [FREE ROUTING BLOCK IF IT EXISTS   
   7 B9K=          FREECORE 1
   8 B=4W          BRN      XSET3
   7 B=JG    XSET2 LDN   7  1
   9 B?46          ANDX  7  IPBSW(3) 
  17 B?HQ          BNZ   7  XSET3               [J IF INOP MESS SENT 
   8 B#3B          SMO      FX1  
  19 B#H2          LDX   4  ZJINOP              [UNIT HAS BECOME INOPERABLE  
   8 B*2L          CALL  5  OGE  
   7 B*G=          LDN   0  1
  18 B*_W          ORS   0  IPBSW(3)            [INDICATE INOP MESS SENT 
   8 BBFG    XSET3 SMO      FX1  
  21 BB_6          LDX   7  XMASK               [UNSET MOP OFF/ON BITS IF SET , TO   
  21 BCDQ          ORN   7  1                   [(BUT DON'T CLEAR INOP MESS SENT BIT)
  18 BCYB          ANDS  7  IPBSW(3)            [INDICATE ACTION FINISHED
  20 BDD2    XSET4 FON      #30                 [WAKE UP ANYONE WAITING FOR US.  
   4 BDXL    SUIC
   7 BFC=          SUICIDE   
  17 BFWW    XBI   GEOERR   1,BREAKIN!          [CANT HAVE BREAK-IN  
   6 BGBG    #UNS CRAMP  
   4 BGW6    XGEO
  20 BH*Q    XSTAT GEOERR   1,WRSTATE           [IPB MOPPED ON/ONLINED/WRONGED   
  11 BHTB          MENDAREA 50,K99IPBSTARB   
   4 BJ*2    #END
   6 ____ ...723671570002
