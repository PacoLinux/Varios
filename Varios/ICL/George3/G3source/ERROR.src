  16 22=X ...      SEG      ERROR,,CENT(COMMAND PROCESSOR),,G400 
   4 22HS ...[   
  15 22SP ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 235L ...[   
   6 23BH ...#UNS G400   
  16 23MD ...#      THIS CHAPTER IS MODIFIED FOR G3PLUS-IH MK2   
  11 23YG          SEGENTRY K1ERROR,SENTRY1  
  11 24D6          SEGENTRY K2ERROR,SENTRY2  
  11 24XQ          SEGENTRY K3ERROR,SENTRY3  
  15 257J ...      SEGENTRY K5ERROR,SENTRY5     [DOHIER MACRO
  21 25CB    #     THIS SEGMENT IS ENTERED BY 4 MACROS,ERROR AND ERRORX WHICH COME   
  21 25X2    #     DOWN,AND COMERR AND COMERRX WHICH COME ACROSS.THIS SEGMENT LOOKS  
  20 26BL    #     FOR A UNI-BLOCK TO FIND THE PARAMETER NUMBER AND COMPILES A   
  20 26W=    #     MESSAGE WHICH SAYS 'IN PARAMETER N'.IF THERE IS NO UNI BLOCK  
  19 27*W    #     THE MESSAGE IS OUTNULLED.IF A SUB-MESSAGE WAS SPECIFIED IN
  20 27TG    #     THE MACRO,THIS IS OUTPUT.IF NO SUB-MESSAGE,IT IS ASSUMED THAT 
  20 28*6    #     IF THERE IS A UNI-BLOCK IT CONTAINS A SUB MESSAGE,OTHERWISE   
  20 28SQ    #     THIS MESSAGE IS OUTNULLED.THE COMMAND PROCESSOR HIERARCHY IS  
  20 29#B    #     ALSO OUTPUT.THUS THE THREE CONSTITUENT PARTS AFOREMENTIONED   
  20 29S2    #     PLUS THE MAIN MESSAGE SPECIFIED IN THE MACRO FORM THE WHOLE   
   7 2=?L    #     MESSAGE   
   4 2=R=    #   
   4 2?=W    #   
   4 2?QG    #   
   4 2#=6    #   
  21 2#PQ    #     THIS SECTION COMPILES THE COMMAND PROCESSOR HIERARCHY UP TO AND   
  20 2*9B    #     INCLUDING COMMAND PROCESSOR LEVEL 1. WITH A BROKEN IN ON LINE 
  20 2*P2    #     JOB,UP TO AND INCLUDING THE TOP BREAK IN LEVEL .WITH ALL OTHER
  12 2B8L    #     CASES THE COMPLETE HIERARCHY. 
   4 2BN=    #   
   4 2C7W    #   
   4 2CMG    #   
  20 2D76    #   THIS ENTRY IS FOR THE SPERROR MACRO. THIS IS A SPECIAL PURPOSE  
  20 2DLQ    #   MACRO TO ASSEMBLE THE HIERARCHY,EXCEPT THE COMMAND AT THE LOWEST
  21 2F6B    #   LEVEL, AS %A IN A GMON/ASET BLOCK.THE ERROR BIT IS ALSO SET. THIS   
  14 2FL2    #   MACRO IS FOR USE BY THE COMERR COMMAND  
   4 2G5L    #   
   5 2GK=    SENTRY3 
   8 2H4W          LDX   4  GSIGN
  17 2HJG          STO   4  EXT+3(2)            [STORE SPERROR MARKER
   7 2J46          LDN   4  0
   8 2JHQ          BRN      SP1  
   6 2JJM ...#UNS G400   
   4 2JKJ ...#SKI
   4 2JLF ...(   
   5 2JMB ...SENTRY5 
   9 2JN? ...      BRN      (GEOERR) 
   4 2JP8 ...)   
   6 2JQ5 ...#UNS G400   
   4 2JR2 ...(   
  10 2JRX ...#     ENTRY FOR DOHIER MACRO
   5 2JSS ...SENTRY5 
   8 2JTP ...      LDX   4  GSIGN
   8 2JWL ...      ORN   4   1   
  17 2JXH ...      STO   4  EXT+3(2)            [MARKER FOR DOHIER   
   7 2JYD ...      LDN   4  0
   8 2J_* ...      BRN      SP1  
   4 2K2= ...)   
   5 2K3B    SENTRY1 
  18 2KH2          LDN   4  1                   [MARKER FOR ERROR/ERRORX 
   9 2L2L          STO   4  EXT+3(2) 
   8 2LG=          BRN      START
   5 2L_W    SENTRY2 
  19 2MFG          STOZ     EXT+3(2)            [MARKER FOR COMERR/COMERRX   
   5 2M_6    START   
   9 2NDQ          LDX   4  EXT(2)   
   8 2NYB          BPZ   4  WOOP 
  20 2PD2          GEOERR   1,NOMESSIN          [GEOERR IF NO COMERR PARAMETERS  
   4 2PXL    WOOP
  20 2QC=          LDN   4  1                   [ADD 1 AS FIRST LEVEL IS CPL 0   
  21 2QWW    SP1   ADX   4  CPLEV(2)            [X4=NO OF POSS ENTRIES IN HIERARCHY  
  20 2RBG          SLL   4  2                   [ALLOWS 4 WORDS FOR EACH LEVEL   
   7 2RL# ...      LDN   6  0
  15 2RW6 ...      JBC      NCCCOM,2,EXCANISS  [J IF NOT CC  
   8 2S5Y ...      LDCT  6  #400 
   5 2S*Q ...NCCCOM  
  15 2SKJ ...      JBC      NOPCOM,2,EXOPCOM   [J IF NOT OP  
   7 2STB ...      SRL   6  1
   8 2T58 ...      ORX   6  GSIGN
   5 2T*2 ...NOPCOM  
  17 2TJS ...      STO   6  AWORK2(2)          [REMEMBER FOR LATER   
   8 2TSL ...      BZE   6  N1   
  18 2W4D ...      ADN   4  5                  [ALLOW ENOUGH FOR 'CC OP' 
  18 2W#=    N1    ADN   4  4                   [ADD 4 FOR 'ON UNIT ?'   
  20 2WRW          SETUPCORE 4,3,COMDAT,CINC    [GET AND NAME A COMDAT CINC BLOCK
   6 2X?G    #SKI  ADELO 
   4 2XR6    (   
  20 2Y=Q ...      JANAL    NVB                 [J IF STILL PROCESSING COMMAND   
  18 2YQB ...      BRN      NVERB               [J IF 'VERB-BIT' CLEAR   
   9 2_=2 ...NVB   LDX   6  EXT+3(2) 
  17 329=          BNZ   6  NCOME               [J IF NOT 'COMMERRR' 
   7 32F4 ...      LDN   0  1
  16 32NW          ERS   0  EXT+9(2)            [ELSE CLEAR BIT  
   8 338G    NCOME MHUNTW   2,CLB
   7 33N6          LDN   0  4
   9 347Q          LDX   5  VERB(1)  
   9 34MB          LDX   6  VERB+1(1)
  15 3572          STO   0  ANUMCHA(2)          [COUNT OF 4  
  18 35LL          STO   5  CLBFIR(2)           [& ^VERB    ^ IN 1ST CLB 
  10 366=          STO   6  CLBFIR+1(2)  
   8 36KW          LDX   2  FX2  
   5 375G    NVERB   
   4 37K6    )   
  19 384Q          LDN   0  0                   [ZEROISE COUNT OF CHARACTERS 
   9 38JB          STO   3  AWORK1(2)
  21 3942          ADN   3  A1                  [POINTER TO FIRST WORD OF IN BLOCK   
   9 39HL          LDX   4  EXT+3(2) 
  18 3=3=          BPZ   4  X1                  [J IF NOT SPERROR MACRO  
  21 3=GW          MHUNTW   2,CPB,CALAS         [TO OMIT LOWEST LEVEL OF HIERARCHY   
   7 3?2G          LDN   0  0
  16 3?G6    X1    LDX   2  FPTR(2)             [   THIS SECTION 
  16 3?_Q          LDX   4  ATYPE(2)            [   SEARCHES FOR 
  15 3#FB          SRL   4  12                  [   THE NEXT 
  15 3#_2          ERN   4  CPB+CALAS           [   CPB/CALAS
  14 3*DL          BNZ   4  X1                  [   BLOCK
  20 3*Y=          LDX   7  2                   [STORE POINTER TO CPB/CALAS BLOCK
  10 3BCW          LDEX  6  CPBCPLEV(2)  
   8 3BXG          LDCT  5  #400 
  10 3CC6          ANDX  5  CPBCPLEV(2)  
  17 3CWQ          BNZ   6  SPLOD               [JUMP IF NOT CPL 0   
   8 3DBB          SMO      FX2  
  18 3DW2          LDX   4  CONTEXT             [LOAD THE CONTEXT BITS   
  18 3F*L          BNG   4  NOUGHT              [J IF A SYSTEM ISSUED JOB
   7 3FT=          ANDN  4  4
  17 3G#W          BNZ   4  NOUGHT              [JUMP IF OFFLINE JOB 
   8 3GSG          BRN      PCO  
  19 3H#6    SPLOD BNZ   5  PCO                 [JUMP IF INTERNALLY ISSUED   
  17 3HRQ          LDX   5  CPBFSD(2)           [LOAD MACRO INDICATOR
  16 3J?B          BPZ   5  PCO                 [JUMP IF MACRO   
  19 3JR2          LDN   6  0                   [PRETEND CPL 0 SINCE BREAK IN
   4 3K=L    PCO 
  18 3KQ=          LDX   2  FPTR(2)             [POINTER TO LINE BUFFER  
  10 3L9W          LDX   5  ANUMCHA(2)   
  17 3LPG          ADN   2  CLBFIR              [POINTER TO 1ST CHAR 
  18 3M96          LDX   4  WIN(1)              [THIS WORD SAYS ' NI '   
   7 3MNQ          LDN   1  4
  16 3N8B    X10   DCH   4  0(3)                [   THIS SECTION 
  16 3NN2          BCHX  3  /                   [   DEPOSITS THE 
  17 3P7L          ADN   0  1                   [   CHARACTERS ' IN '
  18 3PM=          SRL   4  6                   [   IN THE COMDAT/CINC   
  14 3Q6W          BCT   1  X10                 [   BLOCK
  15 3QLG          LDX   1  FX1                 [RESTORE X1  
  20 3R66          BRN      X2                  [GO TO STORE CHARACTERS OF VERB  
  19 3RKQ    X5    LDCH  4  0(2)                [THIS SECTION LOOKS FOR THE  
  18 3S5B          BCHX  2  /                   [TERMINATION OF THE VERB 
   9 3SK2          TXU   4  SPACE(1) 
   8 3T4L          BCS      XOP  
  21 3TJ=          DCH   4  0(3)                [DUMP SPACE CHARACTER AS SEPARATOR   
   8 3W3W          SMO      FX2  
   9 3WHG          LDXC  4  AWORK2   
  17 3X36          BCC      X4                  [J IF NO0 'CANCEL'   
   7 3X=Y ...      SLL   4  1
   8 3XGQ          SMO      FX2  
  15 3Y2B ...      STO   4  AWORK2              [CLEAR MARKER
  17 3YG2          BCHX  3  /                   [AND GO ROUND AGAIN  
   7 3Y_L          ADN   0  1
   8 3_F=          BCT   5  X6   
  10 3_YW          GEOERR   1,NOTCANCL   
   8 42DG ...X6    ORX   0  GSIGN
  15 42Y6 ...      BRN      X2                  [JUMP OUT NOW
  16 43XB    X3    LDCH  4  0(2)                [   THIS SECTION 
   7 44C2          BCT   5  /
  16 44WL          BCHX  2  /                   [   READS THROUGH
  15 45B=          TXU   4  SPACE(1)            [   THE LABEL
  16 45TW          BCS      X3                  [   IF PRESENT   
  16 46*G    X2    LDCH  4  0(2)                [   THIS SECTION 
   7 46T6          BCT   5  /
  16 47#Q          BCHX  2  /                   [   THROWS AWAY  
  16 47SB          TXU   4  SPACE(1)            [   SPACES UNTIL 
  16 48#2          BCC      X2                  [   IT ENCOUNTERS
  18 48RL          BNG   0  X7                  [IF REPEATING FOR CANCEL,
  17 49?=                                       [DON'T TEST FOR LABEL
  15 49QW          TXL   4  TEN(1)              [   A VERB OR
  15 4==G          BCS      X3                  [   A LABEL  
   7 4=Q6    X7    ADN   5  1
  16 4?9Q    XOP   DCH   4  0(3)                [   THIS SECTION 
  17 4?PB          BCHX  3  /                   [   DEPOSITS THE VERB
  18 4#92          ADN   0  1                   [   IN THE COMDAT/CINC   
   8 4#NL          BCT   5  X5   
  21 4*8=    X4    LDX   2  7                   [RESTORE POINTER TO CPB/CALAS BLOCK  
  16 4*MW          BNZ   6  X1                  [J IF NOT CPL 0  
   8 4B7G          SMO      FX2  
  15 4BM6          LDX   4  CONTEXT             [LOAD CONTEXT
   8 4C6Q          ANDN  4  32   
  21 4CLB          BZE   4  NOUGHT              [J IF NOT PAPER TAPE OR CARD READER  
  20 4D62          LDN   2  UNIT(1)             [   THIS SECTION MOVES 'ON UNIT' 
  18 4DKL          MVCH  2  9                   [   TO THE COMDAT/CINC   
  14 4F5=          ADN   0  9                   [   BLOCK
   8 4FJW    #SKI  1<CLUSPROPS-1 
   4 4G4G    (   
   8 4GJ6          LDX   2  FX2  
  15 4H3Q          LDX   5  CA5(2)              [LOAD GEOG NO
   8 4HHB          ANDN  5  #77  
  17 4J32          MPY   5  MAGIC(1)            [MULTIPLY BY 7036875 
  15 4JGL          LDN   7  7                   [COUNT OF 7  
   7 4K2=          LDN   4  0
   7 4KFW          LDN   1  0
  15 4K_G    PONT  CBD   5  1                   [CONVERT CHAR
  16 4LF6          BNZ   1  POGA                [J IF NOT ZERO   
  17 4LYQ          BZE   4  RETA                [J IF LEADING ZERO   
   7 4MDB    POGA  LDN   4  1
  19 4MY2          DCH   1  0(3)                [DEPOSIT CHAR IN COMDAT/CINC 
  15 4NCL          ADN   0  1                   [UPDATE COUNT
  16 4NX=          BCHX  3  /                   [UPDATE MODIFIER 
   8 4PBW    RETA  BCT   7  PONT 
  15 4PWG          LDX   1  FX1                 [RESTORE X1  
   4 4QB6    )   
   7 4QTQ    #SKI  CLUSPROPS 
   4 4R*B    (   
  16 4RT2          LDXC  4  0                   [COPY CHAR. COUNT
  15 4S#L          LDN   0  0                   [CLEAR 'C'   
   8 4SS=          LDX   2  FX2  
   9 4T?W          SBX   3  AWORK1(2)
   7 4TRG          OUTPERA   
  10 4W?6          MHUNTW   1,COMDAT,CINC
   7 4WQQ          ADX   3  1
  10 4X=B          LDX   7  ACOMMUNE1(2) 
  10 4XQ2          LDN   2  ACOMMUNE2(2) 
   8 4Y9L    POGA  LDCH  5  0(2) 
  15 4YP=          BCHX  2  /                   [UPDATE PTR  
  18 4_8W          DCH   5  0(3)                [DEPOSIT IN COMDAT/CINC  
  17 4_NG          BCHX  3  /                   [UPDATE /CINC POINTER
  16 5286          ADN   4  1                   [INCREASE COUNT  
   8 52MQ          BCT   7  POGA 
   8 537B          BRN      N2   
   4 53M2    )   
   5 546L    NOUGHT  
  20 54L=          LDXC  4  0                   [LOAD COUNT OF CHARS. TO OUTPUT  
  15 555W          LDN   0  0                   [CLEAR 'C'   
   4 55KG    N2  
  17 5656          BNZ   4  X30                 [BRANCH IF NOT ZERO  
   7 56JQ          OUTNULL   
   8 574B          BRN      QUATA
  18 57J2    X30   OUTPARAM 4,A1,COMDAT,CINC    [OUTPARAM'IN X IN Y' ETC 
   5 583L    QUATA   
   9 58H=          LDX   5  EXT+3(2) 
   6 58JS ...#UNS G400   
   4 58LB ...(   
  16 58MY ...      BPZ   5  QUAT        [J NOT SPERROR OR DOHIER 
   8 58PG ...      TXU   5  GSIGN
  15 58R4 ...      BCC      SP2         [JIF SPERROR MACRO   
  13 58SL ...      UP                   [DOHIER EXIT 
   4 58W8 ...)   
   6 58XQ ...#UNS G400   
   4 58_# ...#SKI
  17 592W          BNG   5  SP2                 [J IF SPERROR MACRO  
   4 59GG    #   
   4 5=26    #   
   4 5=FQ    #   
   4 5=_B    #   
  20 5?F2    #     THE FOLLOWING SECTION FORMS THE SUB MESSAGE 'IN PARAMETER N'  
  18 5?YL    #     AND THEN OUTPARAMS IT.IF NO UNI BLOCK THEN OUTNULLS   
   4 5#D=    #   
   4 5#XW    #   
   4 5*CG    #   
   4 5*X6    #   
   4 5BBQ    QUAT
  18 5BWB          LDX   2  FPTR(2)             [POINTER TO NEXT BLOCK   
  15 5CB2          LDX   5  ATYPE(2)            [LOAD TYPE   
  10 5CKS ...      BXGE  5  CACT,QNULL   
   8 5CTL          SRL   5  12   
   9 5D*=          LDN   0  CPB+CALAS
   7 5DSW          TXU   5  0
  17 5F#G          BCS      UNI                 [J IF NOT CPB/CALAS  
   5 5FJ# ...QNULL   
  19 5FS6          OUTNULL                      [OUTNULL BECAUSE NO-UNI BLOCK
   8 5G?Q          BRN      WICK 
   9 5GRB    UNI   ERN   5  CPB+CUNI 
  17 5H?2          BNZ   5  QUAT                [J IF NOT UNI-BLOCK  
  16 5HQL          LDN   4  13                  [COUNT OF CHARS  
  10 5J==          MHUNTW   3,COMDAT,CINC
  19 5JPW          LDX   5  JPARNUM(2)          [LOAD THE NUMBER OF PARAMETER
  18 5K9G          LDN   2  PAR(1)              [ADDRESS OF MESSAGE AREA 
   8 5KP6          ADN   3  A1   
  18 5L8Q          MOVE  2  4                   [MOVE INTO COMDAT,CINC   
  16 5LNB          ADN   3  3                   [   UPDATE THE   
  18 5M82          BCHX  3  /                   [   CHARACTER MODIFIER   
  21 5MML          MPY   5  MAGIC(1)            [   TURN INTO DOUBLE LENGTH NUMBER   
  20 5N7=          LDN   7  7                   [COUNT FOR NUMBER OF CHARACTERS  
   7 5NLW          LDN   1  0
  19 5P6G          LDN   0  0                   [MARKER FOR ZERO SUPPRESSION 
  14 5PL6    PINT  CBD   5  1                   [CONVERT 
  16 5Q5Q          BNZ   1  POG                 [J IF NON ZERO   
   9 5QKB          BZE   0  RETURN   
   8 5R52    POG   LDN   0   1   
  18 5RJL          DCH   1  0(3)                [   THIS SECTION STORES  
  18 5S4=          ADN   4  1                   [   THE PARAMETER NUMBER 
  19 5SHW          BCHX  3  /                   [   IN THE COMDAT/CINC BLOCK 
   5 5T3G    RETURN  
   8 5TH6          BCT   7  PINT 
  18 5W2Q          OUTPARAM 4,A1,COMDAT,CINC    [OUTPUT THE SUB MESSAGE  
  10 5WGB    WICK  VFREEW   COMDAT,CINC  
   4 5X22    #   
   4 5XFL    #   
   4 5X_=    #   
   4 5YDW    #   
  21 5YYG    #     THE FOLLOWING ROUTINE DETERMINES THE SUB-MESSAGE AND OUTPUTS IT   
  11 5_D6    #     IF NO SUB-MESSAGE OUTNULLS
   4 5_XQ    #   
   4 62CB    #   
   4 62X2    #   
   4 63BL    #   
  19 63W=          LDX   4  EXT+1(2)            [LOAD SUB-MESSAGE INDICATOR  
  17 64*W          BNG   4  ZY                  [J IF THERE ISN'T ONE
   7 64TG          OUTMESSX 4
   8 65*6          BRN      WP   
  16 65SQ    ZY    LDX   2  FPTR(2)             [   THIS SECTION 
   9 66#B          LDX   5  ATYPE(2) 
  16 66S2          SRL   5  12                  [   A CPB/CALAS  
  17 67?L          LDN   0  CPB+CALAS           [   BLOCK AND EXITS  
  15 67R=          TXU   5  0                   [   IF THERE 
  15 68=W          BCS      NOTCPB              [   IS ONE   
   7 68QG    NULLP OUTNULL   
   8 69=6          BRN      WP   
   5 69PQ    NOTCPB  
   9 6=9B          ERN   5  CPB+CUNI 
  17 6=P2          BNZ   5  ZY                  [J IF NOT UNI BLOCK  
  16 6?8L          LDX   5  ANUM(2)             [LOAD NO OF CHARS
  17 6?N=          BZE   5  NULLP               [J IF NULL PARAMETER 
  17 6#7W          BNG   5  NULLP               [J IF NO PARAMETER   
  17 6#MG          ANDN  5  #7777               [RETAIN NO OF CHARS  
  11 6*76          OUTPARAM 5,APARA,CPB,CUNI 
   4 6*LQ    WP  
   9 6B6B          MONOUTX  EXT(2)   
   8 6BL2    #SKI  K6ALLGEO>99-99
  10 6C5L          TRACE    EXT(2),COMERR
   4 6CK=    SP2 
   8 6D4W          LDX   6  GSIGN
   9 6DJG          ORS   6  EXT+8(2) 
   9 6F46          LDX   6  EXT+3(2) 
   4 6FG9 ...ZUP 
  17 6FHQ          BZE   6  WCOMERR             [J IF COMERR ENTRY   
   5 6G3B ...ZUP1  UP
   5 6GH2    WCOMERR 
   7 6H2L    #SKI  COMPROMK7 
   9 6HG=          ACROSS   COMPRO,11
   8 6H_W    #SKI  COMPROMK7<1$1 
  10 6JFG          ACROSS   COMPROB,11   
   8 6J_6    SPACE          #20  
   8 6K8Y ...XCOMMA          #34 
   8 6KDQ    TEN            +10  
   8 6KYB    WIN            4H NI
   9 6LD2    MAGIC          +7036875 
  11 6LXL    PAR            13HIN PARAMETER  
  10 6MC=    UNIT           9H ON UNIT   
   6 6MWW    #SKI  ADELO 
   9 6NBG    VERB           8HVERB   
  10 6NW6          MENDAREA 40,GAPERROR  
   4 6P*Q    #END
   8 ____ ...12205406000100000000
