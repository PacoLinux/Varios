  12 22FL          SEG      IPCONG,,JOHN BAILEY  
   4 22_=    #   
  11 23DW          SEGENTRY K1IPCONG,X1IPCONG
  11 23YG          SEGENTRY K2IPCONG,X2IPCONG
   4 24D6    #   
   8 24XQ    #     STORED   WORDS
   9 25CB    RESBITS        #777700  
   9 25X2    TMESSCODES     #04000040
   9 26BL                   #04000041
   9 26W=                   #04000042
   9 27*W                   #04000043
   9 27TG                   #04000046
   9 28*6                   #03000047
   9 28SQ                   #07000060
   9 29#B                   #07000061
   9 29S2                   #04000066
   9 2=?L                   #03000067
   9 2=R=                   #02000070
   9 2?=W                   #02000071
   9 2?QG                   #03000077
   9 2#=6    TBIT1          #20000000
   9 2#PQ    TBIT2          #10000000
   9 2*9B    TBIT3          #4000000 
   9 2*P2    TBIT4          #2000000 
   9 2B8L    TBIT5          #1000000 
   9 2BN=    TBIT11         #10000   
  16 2C7W    TTLIM          +7                  [MAX TASK NO + 1 
  17 2CMG    TIPBACT        +IPCA+IPCBX         [TYPE OF IPB ACTIVITY
   8 2D76    ROK            #0   
   9 2DLQ    RTO            #22000000
   9 2F6B    RNI            #02000000
   9 2FL2    RNINE          #02400000
   9 2G5L    OBJW  #40000*IPCG4+#11  
   9 2GK=    OBJR  #40000*IPCG4+#12  
  16 2H4W    #     SUBROUTINE USED TO SET OBJECT PROGRAM REPLIES 
   4 2HJG    #   
   4 2J46    SREL
  17 2JHQ          LDX 3 IPCPTR(2)              [FIND IPC ACTIVITY   
  16 2K3B          HUNT2    3,IPCD,IPSTACK      [FIND STACK BLOCK
  10 2KH2          LDX   1  ARINGNO(3)   
   8 2L2L          ANDN  1  #7777
  15 2LG=          BZE   1  SFR1                [STACK EMPTY 
   8 2L_W          ADN   3  A1   
   9 2MFG    SFR3  LDX   0  ALINUM(3)
  11 2M_6          BXE   0  ACOMMUNE1(2),SFR2
   9 2NDQ          ADN   3  ARECLEN  
   8 2NYB          BCT   1  SFR3 
   8 2PD2    SFR1  NGN   3  #7777
   8 2PXL    SFR2  LDX   1  FX1  
   7 2QC=          EXIT  7  0
  16 2QWW    #     S/R TO LOCATE ACTIVITY OWNING A GEORGE LINK   
  16 2RBG    SLACT LDX   2  FPTR(3)             [CONTROL BLOCK   
   8 2RW6          SBN   2  A1   
   5 2S*Q    SLACT1  
   9 2STB          LDX   2  BPTR(2)  
   9 2T*2          LDX   0  ATYPE(2) 
  10 2TSL          BXL   0  CACT,SLACT1  
   7 2W#=          EXIT  7  0
  14 2WRW    #     S/R TO SET REPLY ETC ON GEORGE LINK   
   7 2X?G    SGCLR LDX   4  7
   8 2XR6          CALL  7  SREL 
   9 2Y=Q          LDX   2  FPTR(3)  
  15 2YQB          STO   6  IPCGREP-A1(2)       [STORE REPLY 
   8 2_=2          LDX   0  GSIGN
   7 2_PL          ORN   0  1
  15 329=          ERS   0  ASTATUS1(3)         [UNSET T.I.P.
   9 32NW          STOZ     ATIMCT(3)
   9 338G          STOZ     ASTR(3)  
   9 33N6          STOZ     ALENTR(3)
   9 347Q          STOZ     ASTMEL(3)
   7 34MB          NGN   0  2
  10 3572          ANDS  0  ASTATUS2(3)  
   8 35LL          CALL  7  SLACT
  17 366=          DESUSPEND                    [DESUSPEND ACTIVITY  
  16 36KW          MFREE    IPCD,ATASK          [FREE TASK BLOCK 
   7 375G          EXIT  4  0
  12 37K6    #     ENTRY POINT FOR TRANSFER TASKS
   9 384Q    #     FOR GEORGE LINKS  
   4 38JB    #   
   5 3942    X1IPCONG
  10 39HL    TFRG  MHUNT    3,IPCD,ATASK 
   8 3=3=          LDX   0  A1(3)
  16 3=GW          STO   0  ACOMMUNE1(2)        [LINK IDENTIFIER 
  17 3?2G          CALL  7  SREL                [LOCATE RING ELEMENT 
  10 3?G6          LDX   0  ASTATUS1(3)  
   7 3?_Q          ANDN  0  1
   8 3#FB          BZE   0  P2   
   8 3#_2          LDX   0  GSIGN
  17 3*DL          ORS   0  ASTATUS1(3)         [MARK TR IN PROGRESS 
  13 3*Y=    #     SET UP TRANSFER REQUEST MESSAGE   
  10 3BCW          SMO      ALTPROG(3)   
  14 3BXG          LDX   0  TMESSCODES(1)       [HEADER  
   9 3CC6          STO   0  IPWRB(2) 
   9 3CWQ          LDX   0  ALINUM(3)
  16 3DBB          STO   0  IPWRB+1(2)          [LINK IDENTIFIER 
   9 3DW2          LDX   0  ALENTR(3)
  17 3F*L          STO   0  IPWRB+2(2)          [LENGTH OF TRANSFER  
   9 3FT=          LDN   7  IPWRB(2) 
   7 3G#W          SUM   6  3
  14 3GSG          NGS   6  IPWRB+3(2)          [CHECKSUM
  14 3H#6    #     SET UP CONTROL AREA AND SEND MESSAGE  
   4 3HRQ    #   
   8 3J?B    #     1-WRITE PART  
   8 3JR2          LDN   0  #31  
  10 3K=L          DCA      ,DLA,0,MODE  
   7 3KQ=          LDN   0  4
  10 3L9W          DCA      ,DLA,0,COUNT 
   9 3LPG          LDN   0  IPWRB(2) 
  10 3M96          DCA      ,STO,0,CTSA  
   8 3MNQ    #     2-READ PART   
   8 3N8B          LDN   0  #32  
  10 3NN2          DSCA     ,DLA,0,MODE  
   8 3P7L          LDN   4  11   
  10 3PM=          DSCA     ,DLA,4,COUNT 
   9 3Q6W          LDN   0  IPREB(2) 
  10 3QLG          DSCA     ,STO,0,CTSA  
   8 3R66    #     3-ISSUE PERI  
   8 3RKQ          GPERIIPC ,2   
  14 3S5B ...      TIPCREP  ,TF1,TF2,TF3,TF4,TF6,TF2,TF5 
  10 3SK2    #     VALDATE  REPLY MESSAGE
  17 3T4L          LDX   0  CPRW2(2)            [REPLY TO READ PART  
   8 3TJ=          ANDN  0  #7777
  17 3W3W          SBX   4  0                   [AMOUNT TRANSFERRED  
   9 3WHG          LDCH  0  IPREB(2) 
  15 3X36          BXU   0  4,TF6               [COUNT WRONG 
   9 3XGQ          LDN   7  IPREB(2) 
   7 3Y2B          SMO      4
  17 3YG2          SUM   6  0                   [CALCULATE CHECKSUM  
  15 3Y_L          BNZ   6  TF6                 [J IF WRONG  
  10 3_F=          LDX   0  IPREB+1(2)   
   8 3_YW          SRC   0  12   
  10 42DG          ERX   0  IPWRB+1(2)   
   8 42Y6          ANDN  0  #7777
  15 43CQ          BNZ   0  TF6                 [WRONG LINK  
  11 43XB    #     SWITCH ON TYPE OF REPLY   
   8 44C2          CALL  7  SREL 
   9 44WL          LDX   0  IPREB(2) 
   8 45B=          ANDN  0  #77  
   8 45TW          SBN   0  #47  
  15 46*G          BZE   0  TFC                 [NO TRANSFER 
   8 46T6          SBN   0  #20  
  14 47#Q          BZE   0  TFD                 [NO LINK 
   8 47SB          ADN   0  #21  
   8 48#2          BNZ   0  TF6  
   9 48RL    #     DO ACTUAL TRANSFER
   4 49?=    #   
  11 49QW    #     1-SWITCH ON TYPE OF ACCESS
   9 4==G          LDX   0  ASTMEL(3)
   7 4=Q6          SMO      0
   7 4?9Q          BRN      /
   8 4?PB          BRN      TFABS
   8 4#92          BRN      TFBLK
   8 4#NL          BRN      TFBFR
   8 4*8=          BRN      TFACT
  13 4*MW    #     2-PREPARE FOR TRANSFER : ABS CASE 
   9 4B7G    TFABS LDX   0  ASTR(3)  
   8 4BM6          BRN      TFB  
  13 4C6Q    #     3-PREPARE FOR TRANSFER : BLK CASE 
   8 4CLB    TFBLK CALL  7  SLACT
   9 4D62          LDX   6  ASTR(3)  
  18 4DKL          ANDX  6  HALFTOP             [TYPE OF BLOCK SPECIFIED 
  19 4F5=    TFBK1 LDX   2  FPTR(2)             [LOCATE SPECIFIED BLOCK ...  
   9 4FJW          LDX   0  ATYPE(2) 
   9 4G4G          ANDX  0  HALFTOP  
  18 4GJ6          BXU   0  6,TFBK1             [IF NOT FOUND,TRY NEXT   
  16 4H3Q          LDX   6  2                   [REMEMBER ADDRESS
   7 4HHB          LOCK     2
   9 4J32          LDX   0  ASTR(3)  
   8 4JGL          ANDN  0  #7777
  19 4K2=          ADX   0  6                   [CALCULATE ABSOLUTE ADDRESS  
   8 4KFW          BRN      TFB  
  13 4K_G    #     4-PREPARE FOR TRANSFER : BFR CASE 
   9 4LF6    TFBFR LDX   6  ASTR(3)  
   8 4LYQ          ANDN  6  #7777
  17 4MDB          SETNCORE 6,1,IPCD,ATASK      [SET UP BUFFER BLOCK 
   8 4MY2          CALL  7  SREL 
   9 4NCL          LDX   0  ASTR(3)  
   9 4NX=          ANDX  0  HALFTOP  
  14 4PBW          STO   0  ATYPE(2)            [RENAME  
   8 4PWG          CALL  7  SLACT
   7 4QB6          LDX   6  2
  17 4QTQ          CHAIN    1,2                 [CHAIN BUFFER BLOCK  
   7 4R*B          SMO      6
   8 4RT2          LDX   2  FPTR 
   7 4S#L          LOCK     2
   7 4SS=          SMO      6
   8 4T?W          LDX   0  FPTR 
   8 4TRG          ADN   0  A1   
   8 4W?6          BRN      TFB  
  13 4WQQ    #     5-PREPARE FOR TRANSFER : ACT CASE 
   8 4X=B    TFACT CALL  7  SLACT
   7 4XQ2          LDX   6  2
   7 4Y9L          LOCK     2
   9 4YP=          LDX   0  ASTR(3)  
   7 4_8W          ADX   0  6
  10 4_NG    #     6-SET UP CONTROL AREA 
  10 5286    TFB   LDX   6  ALTPROG(3)   
  15 52MQ          BZE   6  TFR                 [J IF READ   
   7 537B    #     WRITE PERI
  10 53M2          DCA      ,STO,0,CTSA  
   8 546L          LDN   0  #11  
  13 54L=          DCA      ,DLA,0,MODE         [MODE
  17 555W          LDX   6  IPREB+2(2)          [DIFFERENCE IN COUNTS
  14 55KG          BPZ   6  TFW1                [ENOUGH  
  16 5656          NGS   6  AWORK4(2)           [STORE REMAINDER 
   8 56JQ          LDCT  0  #100 
  17 574B          ORS   0  AWORK4(2)           [AND LONG BLOCK BIT  
  18 57J2          ADX   6  IPWRB+2(2)          [COUNT TO BE TRANSFERRED 
   8 583L          BRN      TFW2 
  15 58H=    TFW1  STOZ     AWORK4(2)           [NO REMAINDER
  14 592W          LDX   6  IPWRB+2(2)          [COUNT   
  15 59GG    TFW2  DCA      ,DLA,6,COUNT        [STORE COUNT 
   8 5=26    #     6-DO TRANSFER 
   8 5=FQ          GPERIIPC ,2,CT
  12 5=_B ...      TIPCREP  CT,TF1,TF2,TF7,TF1   
  14 5?5? ...      LDX   0  CPRW1(2)        [REPLY WORD  
  19 5?98 ...      ANDN  0  #7777                  [LOOK FOR REMAINDER COUNT 
  17 5?*5 ...      BNZ   0  TF6                   [MAJOR ERROR IF SO 
   8 5?F2          BRN      TFB1 
  16 5?YL    TFR   DSCA     ,STO,0,CTSA         [START ADDRESS   
   8 5#D=          LDN   0  #12  
  13 5#XW          DSCA     ,DLA,0,MODE         [MODE
  15 5*CG          LDX   6  IPREB+2(2)          [DIFFERENCE  
  15 5*X6          BPZ   6  TFR1                [SUFFICIENT  
   8 5BBQ          LDCT  0  #100 
  15 5BWB          STO   0  AWORK4(2)           [LONG BLOCK  
  14 5CB2          LDX   6  IPWRB+2(2)          [COUNT   
   8 5CTL          BRN      TFR2 
  15 5D*=    TFR1  STO   6  AWORK4(2)           [RENAINDER   
  10 5DSW          LDX   6  IPWRB+2(2)   
  14 5F#G          SBX   6  IPREB+2(2)          [COUNT   
   4 5FS6    TFR2
  15 5G?Q          DSCA     ,DLA,6,COUNT        [STORE COUNT 
   8 5GRB          GPERIIPC ,2,SP
  12 5H?2 ...      TIPCREP  SP,TF1,TF2,TF7,,TF1  
  10 5HQL    #     SET REPLY AND CLEAR UP
  15 5J==    TFB1  CALL  7  SREL                [LOCATE ENTRY
   9 5JPW          SMO      ASTMEL(3)
  14 5K9G          BRN      /                   [SWITCH  
  13 5KP6          BRN      TFB2                [ ABS
  13 5L8Q          BRN      TFB3                [ BLK
  13 5LNB          BRN      TFB3                [ BFR
  13 5M82          BRN      TFB4                [ ACT
   9 5MML    #     BLK & BFR CASES   
  17 5N7=    TFB3  CALL  7  SLACT               [FIND ACTIVITY BLOCK 
   9 5NLW          LDX   6  ASTR(3)  
  13 5P6G          ANDX  6  HALFTOP             [TYPE
   9 5PL6    TFB3A LDX   2  FPTR(2)  
   9 5Q5Q          LDX   0  ATYPE(2) 
   9 5QKB          ANDX  0  HALFTOP  
  13 5R52          BXU   0  6,TFB3A             [NEXT
   7 5RJL    TFB3B UNLOCK   2
   8 5S4=          BRN      TFB2 
   7 5SHW    #     ALT CASE  
  15 5T3G    TFB4  CALL  7  SLACT               [LOCATE ACT  
   8 5TH6          BRN      TFB3B
   7 5W2Q    #     SET REPLY 
   7 5WGB    TFB2  LDN   6  0
   8 5X22          JDISENG  ,TFD1
  15 5XFL          LDCT  6  #10                 [OPERABLE BIT
   9 5X_=    TFD1  ORX   6  AWORK4(2)
   8 5YDW          CALL  7  SGCLR
   9 5YYG          BRN      NEXTASK  
   8 5_D6    TFC   LDX   0  GSIGN
  10 5_XQ          ERS   0  ASTATUS1(3)  
  10 62CB          MFREE    IPCD,ATASK   
   9 62X2          BRN      NEXTASK  
   8 63BL    #     NO LINK CASE  
   8 63W=    TFD   LDCT  0  #240 
  16 64*W          ORS   0  ASTATUS2(3)         [MARK DISEST REP 
   8 64FR ...      LDN   0  #7777
  10 64KN ...      ANDS  0  ACOMMUNE1(2) 
   9 64PK ...      ANDS  0  ALINUM(3)
   8 64TG          LDCT  0  #24  
  15 65*6          STO   0  AWORK4(2)           [SET UP REPLY
  15 65SQ          BRN      TFB2                [INSERT REPLY
   8 66#B    #     ERROR CASES   
  14 66S2    TF1   LDX   6  TFREP(1)            [TIME-OUT
   8 67?L          CALL  7  SGCLR
   8 67R=          BRN      TIME 
  14 68=W    TF2   LDX   6  TFREP(1)            [RESET   
   7 68QG          ORN   6  1
   8 69=6          CALL  7  SGCLR
   9 69PQ          BRN      RESETA   
  14 6=9B    TF3   SIPCM    SI                  [INOP(A) 
  14 6=P2    TF5   LDX   6  TIREP(1)            [INOP(B) 
   8 6?8L    TF5A  CALL  7  SGCLR
   8 6?N=          BRN      NOP  
   8 6#7W    TF4   CALL  7  SREL 
   8 6#MG          LDX   0  GSIGN
  10 6*76          ERS   0  ASTATUS1(3)  
   8 6*LQ          BRN      NPUT 
   9 6B6B    TF6   LDX   6  TFREP(1) 
   7 6BL2          ORN   6  2
   8 6C5L          CALL  7  SGCLR
   9 6CK=          BRN      WHOOPS   
  14 6D4W    TF7   LDX   6  TFREP(1)            [INOP(C) 
   7 6DJG          ORN   6  3
   8 6F46          SIPCM    SI   
   8 6FHQ          BRN      TF5A 
   9 6G3B    TFREP          #3200000 
   9 6GH2    TIREP          #2000000 
  11 6H2L    NPUT           ACROSS  IPCONA,2 
   8 6HG=    NOPS  SIPCM    SI   
  18 6H_W    NOP   ACROSS   IPCONE,2            [DEAL WITH INOPERABLE IPB
   5 6JFG    WHOOPS  
  16 6J_6          ACROSS   IPCONE,1            [DEAL WITH ERROR 
   5 6KDQ    RESETA  
  18 6KYB          ACROSS   IPCONE,3            [DEAL WITH ERROR DETECTED
   4 6LD2    TIME
   9 6LXL          ACROSS   IPCONE,5 
  10 6MC=    P2    MFREE    IPCD,ATASK   
   5 6MWW    NEXTASK 
   9 6NBG          ACROSS   IPCON,1  
  14 6NW6    #     S/R TO SET REPLY TO GEORGE TRANSFER   
   4 6P*Q    SGSR
   8 6PTB          CALL  7  SREL 
   9 6Q*2 ...SGSR2 LDX   2  FPTR(3)  
  15 6QSL          STO   6  IPCGREP-IPCRNG(2)   [STORE REPLY 
   7 6R#= ...      NGNC  0  2
  10 6RRW ...      ANDS  0  ASTATUS1(3)  
   9 6SR6          STOZ     ATIMCT(3)
   8 6T=Q          STOZ  ASTR(3) 
   9 6TQB          STOZ     ALENTR(3)
   7 6W=2          NGN   0  2
  10 6WPL          ANDS  0  ASTATUS2(3)  
   9 6X9=          STOZ     ASTMEL(3)
  16 6XNW          CALL  7  SLACT               [LOCATE ACTIVITY 
  14 6Y8G          DESUSPEND                    [KICK IT 
   7 6YN6          EXIT  4  0
  12 6_7Q    #     TRANSFERS FOR GEORGE IPC LINKS
   4 6_MB    #   
   5 7272    X2IPCONG
   4 72LL    TGL 
   9 736=    #     SEND O.K. MESSAGE 
   8 73B4 ...      CALL  7  SREL 
  11 73KW          LDX   0  TMESSCODES+4(1)  
   9 745G          STO   0  IPWRB(2) 
  10 74K6          LDX   0  ACOMMUNE1(2) 
  16 754Q          STO   0  IPWRB+1(2)          [LINK IDENTIFIER 
  10 75JB ...      LDX   0  ALTPROG(3)   
  16 7642          BNZ   0  TGL1                [THIS END WRITING
   9 76HL          LDX   0  ALENTR(3)
  10 773=          SBX   0  IPREB+2(2)   
   8 77GW          BRN      TGL2 
  10 782G    TGL1  LDX   0  IPREB+2(2)   
   9 78G6          SBX   0  ALENTR(3)
  10 78_Q    TGL2  STO   0  IPWRB+2(2)   
   9 79FB          LDN   7  IPWRB(2) 
   7 79_2          SUM   6  3
  14 7=DL          NGS   6  IPWRB+3(2)          [CHECKSUM
  13 7=Y=    #     SET UP CONTROL AREA AND SEND REPLY
   8 7?CW          LDN   0  #11  
  10 7?XG          DCA      ,DLA,0,MODE  
   7 7#C6          LDN   0  4
  10 7#WQ          DCA      ,DLA,0,COUNT 
   9 7*BB          LDN   0  IPWRB(2) 
  10 7*W2          DCA      ,STO,0,CTSA  
   9 7B*L          GPERIIPC  ,2,CT   
  14 7BT=          TIPCREP  CT,TIME,RESETA,NOPS,WHOOPS   
  10 7C#W    #     PREPARE FOR TRANSFER  
   4 7CSG    #   
  12 7D#6    #     1 - SWITCH ON TYPE OF ACCESS  
  17 7DRQ          CALL  7  SREL                [LOCATE RING ELEMENT 
   9 7F?B          LDX   0  ASTMEL(3)
   7 7FR2          SMO   0   
   7 7G=L          BRN      /
   8 7GQ=          BRN      TGABS
   8 7H9W          BRN      TGBLK
   8 7HPG          BRN      TGBFR
   8 7J96          BRN      TGACT
   8 7JNQ    #     2 - ABS CASE  
   9 7K8B    TGABS LDX   0  ASTR(3)  
  16 7KN2          BRN      TGB                 [STARTOFTRANSFER 
   8 7L7L    #     3 - BLK CASE  
   8 7LM=    TGBLK CALL  7  SLACT
   9 7M6W          LDX   6  ASTR(3)  
   9 7MLG          ANDX  6  HALFTOP  
   5 7N66    TGBLK1  
   9 7NKQ          LDX   2  FPTR(2)  
   9 7P5B          LDX   0  ATYPE(2) 
   9 7PK2          ANDX  0  HALFTOP  
   9 7Q4L          BXU   0  6,TGBLK1 
  16 7QJ=          LDX   6  2                   [REMEMBER ADDRESS
  15 7R3W          LOCK     2                   [LOCK BLOCK  
   9 7RHG          LDX   0  ASTR(3)  
   8 7S36          ANDN  0  #7777
  18 7SGQ          ADX   0  6                   [CALCULATE ABS ADDRESS   
   8 7T2B          BRN      TGB  
   8 7TG2    #     4 - BFR CASE  
   9 7T_L    TGBFR LDX   6  ASTR(3)  
   8 7WF=          ANDN  6  #7777
  15 7WYW          SETNCORE  6,1,IPCD,ATASK     [BUFFER BLOCK
   8 7XDG          CALL  7  SREL 
   9 7XY6          LDX   0  ASTR(3)  
   9 7YCQ          ANDX  0  HALFTOP  
  14 7YXB          STO   0  ATYPE(1)            [RENAME  
  16 7_C2          CALL  7  SLACT               [LOCATE ACTIVITY 
  14 7_WL          LDX   6  2                   [ADDRESS 
  16 82B=          CHAIN    1,2                 [RECHAIN BLOCK   
   7 82TW          SMO      6
  16 83*G          LDX   2  FPTR                [RELOCATE BLOCK  
  14 83T6          LOCK     2                   [LOCK IT 
   7 84#Q          SMO      6
  15 84SB          LDX   0  FPTR                [RELOCATE IT 
   8 85#2          ADN   0  A1   
   8 85RL          BRN      TGB  
   8 86?=    #     5 - ACT CASE  
   9 86QW    TGACT LDX   2  FPTR(3)  
   8 87=G          SBN   2  A1   
  18 87Q6          CALL  7  SLACT               [LOCATE ACTIVITY BLOCK   
   7 889Q          LDX   6  2
  14 88PB          LOCK     2                   [LOCK IT 
   9 8992          LDX   0  ASTR(3)  
   7 89NL          ADX   0  6
   8 8=8=    #     DO TRANSFER   
   4 8=MW    #   
  11 8?7G    #     1 - SET UP CONTROL AREA   
  10 8?M6    TGB   LDX   6  ALTPROG(3)   
  15 8#6Q          BZE   6  TGR                 [J IF READ   
   7 8#LB    #     WRITE PERI
  16 8*62          DCA      ,STO,0,CTSA         [START ADDRESS   
   8 8*KL          LDN   0  #11  
  13 8B5=          DCA      ,DLA,0,MODE         [MODE
  17 8BJW          LDX   6  IPWRB+2(2)          [DIFFERENCE IN COUNTS
  15 8C4G          BPZ   6  TGW1                [SUFFICIENT  
  16 8CJ6          NGS   6  AWORK4(2)           [NOTE DIFFERENCE 
   8 8D3Q          LDCT  0  #100 
  16 8DHB          ORS   0  AWORK4(2)           [LONG BLOCK BIT  
  18 8F32 ...      LDX   6  IPREB+2(2)          [COUNT TO BE TRANSFERRED 
   8 8FGL          BRN      TGW2 
  15 8G2=    TGW1  STOZ     AWORK4(2)           [NO REMAINDER
  13 8GFW ...      LDX   6  ALENTR(3)          [COUNT
  15 8G_G    TGW2  DCA      ,DLA,6,COUNT        [STORE COUNT 
   9 8HF6    #     2A - DO TRANSFER  
   8 8HYQ          GPERIIPC ,2,CT
  12 8JDB ...      TIPCREP  CT,TG1,TG2,TG3,TG1   
  14 8JJ? ...      LDX   0  CPRW1(2)          [REPLY WORD
  18 8JN8 ...      ANDN  0  #7777                [LOOK FOR REMAINDER CT  
  17 8JS5 ...      BNZ   0  TG4                  [MAJOR ERROR IF SO  
   8 8JY2          BRN      TGB1 
   7 8KCL    #     READ PERI 
  16 8KX=    TGR   DSCA     ,STO,0,CTSA         [START ADDRESS   
   8 8LBW          LDN   0  #12  
  13 8LWG          DSCA     ,DLA,0,MODE         [MODE
  15 8MB6          LDX   6  IPWRB+2(2)          [DIFFERENCE  
  15 8MTQ          BPZ   6  TGR1                [SUFFICIENT  
   8 8N*B          LDCT  0  #100 
  15 8NT2          STO   0  AWORK4(2)           [LONG BLOCK  
  14 8P#L          LDX   6  IPREB+2(2)          [COUNT   
   8 8PS=          BRN      TGR2 
   9 8Q?W    TGR1  STO   6  AWORK4(2)
  10 8QRG          LDX   6  IPREB+2(2)   
  14 8R?6          ADX   6  IPWRB+2(2)          [COUNT   
  15 8RQQ    TGR2  DSCA     ,DLA,6,COUNT        [STORE COUNT 
  10 8S=B    #     2B - DO READ TRANSFER 
   8 8SQ2          GPERIIPC ,2,SP
  12 8T9L ...      TIPCREP  SP,TG1,TG2,TG3,,TG1  
  11 8TP=    #     3 - SET REPLY AND CLEAR UP
  15 8W8W    TGB1  CALL  7  SREL                [LOCATE ENTRY
   9 8WNG          SMO      ASTMEL(3)
  14 8X86          BRN      /                   [SWITCH  
  13 8XMQ          BRN      TGB2                [ABS 
  13 8Y7B          BRN      TGB3                [BLK 
  13 8YM2          BRN      TGB3                [BFR 
  13 8_6L          BRN      TGB4                [ACT 
   9 8_L=    #     BLK AND BFR CASES 
  17 925W    TGB3  CALL  7  SLACT               [FIND ACTIVITY BLOCK 
   9 92KG          LDX   6  ASTR(3)  
  13 9356          ANDX  6  HALFTOP             [TYPE
   9 93JQ    TGB3A LDX   2  FPTR(2)  
   9 944B          LDX   0  ATYPE(2) 
   9 94J2          ANDX  0  HALFTOP  
  13 953L          BXU   0  6,TGB3A             [NEXT
   7 95H=    TGB3B UNLOCK   2
   8 962W          BRN      TGB2 
   7 96GG    #     ACT CASE  
  16 9726    TGB4  CALL  7  SLACT               [LOCATE ACTIVITY 
   8 97FQ          BRN      TGB3B
   8 97_B    #     CLEAR DETAILS 
   7 9=X6    #     SET REPLY 
   7 9?BQ ...TGB2  LDN   6  0
   8 9?WB          JDISENG  ,TGD1
   8 9#B2          LDCT  6  #10  
   9 9#TL    TGD1  ORX   6  AWORK4(2)
   8 9**= ...      CALL  4  SGSR2
   9 9C?Q          BRN      NEXTASK  
   8 9CRB    #     ERROR CASES   
  14 9D?2    TG1   LDX   6  TFREP(1)            [TIME-OUT
   8 9DQL          CALL  4  SGSR 
   8 9F==          BRN      TIME 
  14 9FPW    TG2   LDX   6  TFREP(1)            [RESET   
   8 9G9G          CALL  4  SGSR 
   9 9GP6          BRN      RESETA   
  13 9H8Q    TG3   SIPCM    SI                  [INOP
   9 9HNB          LDX   6  TIREP(1) 
  15 9J82 ...      CALL  4  SGSR                [SET REPLY   
   8 9JML          BRN      NOP  
  17 9JRH ...TG4   LDX   6  TFREP(1)         [REMAINDER SEEN ON WRITE
   8 9JXD ...      CALL  4  SGSR 
   9 9K3* ...      BRN      WHOOPS   
  11 9K7=          MENDAREA 1023-0?,K9IPCONG 
   4 9KLW    #END
   8 ____ ...52356416001000000000
