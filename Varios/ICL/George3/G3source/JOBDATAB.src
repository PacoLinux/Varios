   6 22FL    #LIS    3   
   7 22_=    #SEG  JOBDATAB  
  10 23DW                   8HJOBDATAB   
  15 23YG    [ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 24D6    [   
   4 24XQ    [   
  19 25CB    [    THIS SEGMENT CONTAINS ROUTINES& SUBROUTINES WHICH USE TO BE
  19 25X2    [    IN 'JOBDATA'. THE LOGIC FLOW OF THIS SEGMENT IS IDENTICAL  
  19 26BL    [    TO THE OLD 'JOBDATA' SEGMENT AND SOME DUPLICATION OF CODE  
  13 26W=    [    EXISTS TO SAVE CHAPTER THRASHING.  
  18 27*W    [    THIS SEGMENT HAS BEEN CONSTRUCTED SO THAT ADDITIONAL   
  18 27TG    [    ROUTINES FROM 'JOBDATA' CAN BE ADDED AT A FUTURE DATE  
  11 28*6    [    IF NECESSARY:  OCT 1981.   
   4 28SQ    #   
   8 29#B    #     ENTRY POINT   
   4 29S2    #   
  15 2=?L          SEGENTRY K1JOBDATAB,XK1      ['P' ROUTINES
  15 2=R=          SEGENTRY K2JOBDATAB,XK2      [ERROR ENTRY 
  17 2?=W          SEGENTRY K3JOBDATAB,XK3      [STARTTIME ROUTINE   
   2 2?QG
  17 2#2# ...      BITDEFS  AWORK4,0,TMAXSIZE     [ B7270 CORRECTION 
  15 2#=6          BITDEFS  AWORK4,2,TFORMAT,STARTCOM,SIGNEED
  16 2#PQ          BITDEFS  AWORK4,5,SINGLE,TMINUS,TPLUS,PMINUS  
   9 2*9B    YER2        +JPARNULL   
   8 2*P2    YER3        +JPARMIS
   8 2B8L    YER4        +APFERR 
   8 2BN=    YER5        +ERMCH  
  17 2C7W    YER8        +JDOVERFLOW         [TOO MUCH SCHED, DATA   
  13 2CMG    YER9        +JDTSNER           [MT ERROR
   9 2D76    YER10       +JDNOCOUNT  
   7 2DLQ    TOOSMALL       3
   8 2F6B    TOOBIG         502  
  17 2FL2    Z2CLOCKANUM    #10005              [CORRESPONDS TO XX.XX
  18 2G5L    Z3CLOCKANUM    #10010              [CORRESPONDS TO XX.XX.XX 
   8 2GK=    ZHR            4HHRS
   9 2H4W    MIN            4HMINS   
   9 2HJG    SEC            4HSECS   
  10 2HL9 ...[ B7270 CORRECTION  06.08.82
   9 2HMY ...[ SET UP ROUNDING VALUES
   6 2HPM ...#SKIP  G3   
   8 2HRB ...#DEF   PROGRND = 64 
   6 2HT5 ...#SKIP  G4   
   9 2HWS ...#DEF   PROGRND = 1024   
   9 2HYH ...MAXSIZE       #20000000 
   9 2J2= ...PROGRNDN      -PROGRND  
   4 2J46    #   
   4 2JHQ    XK1 
   4 2K3B    [   
   7 2KH2          SMO      3
   9 2L2L          BRN      SWITCH   
   5 2LG=    SWITCH  
  15 2L_W          BRN      PERROR              [FACILITY 0  
  15 2MFG          BRN      PERROR              [FACILITY 1  
  15 2M_6          BRN      PCLOCK              [FACILITY 2  
  15 2NDQ          BRN      PDATELOAD           [FACILITY 3  
  15 2NYB          BRN      PINTEGER            [FACILITY 4  
  15 2PD2          BRN      PERROR              [FACILITY 5  
  15 2PXL          BRN      PERROR              [FACILITY 6  
  15 2QC=          BRN      PERROR              [FACILITY 7  
  15 2QWW          BRN      PDEADLINE           [FACILITY 8  
  15 2RBG          BRN      PSTRING             [FACILITY 9  
  15 2RW6          BRN      PERROR              [FACILITY 10 
  15 2S*Q          BRN      PTSN                [FACILITY 11 
  15 2STB          BRN      POCTAL              [FACILITY 12 
   5 2T*2    PERROR  
  10 2TSL          GEOERR   1,JOBDATAB   
  14 2W#=    [ ROUTINES TO PICK UP NEXT COMMAND PARAMETER
  10 2WRW    [ AND LEAVE IN A CPB/CUNI . 
  15 2X?G    [ X0 - ANUM,  X7 = LINK,  X3-> CPB/CUNI ON EXIT 
  11 2XR6    [ SPARA & RSPARAPASS KEEP SPACES
  11 2Y=Q    [ PARA & RPARAPASS REMOVE SPACES
   8 2YQB    SPARA ORX   5  GSIGN
   6 2_=2    RSPARAPASS  
  10 2_PL          JBS      RPS7,2,SINGLE
   8 329=          SBX   5  FX1  
   7 32NW          SPARAPASS 
   8 338G          BRN      RPS5 
   8 33N6    PARA  ORX   5  GSIGN
   6 347Q    RPARAPASS   
  17 34MB          JBS      PARA1,2,SINGLE      [J IF PARM IN CUNI   
   8 3572          SBX   5  FX1  
   7 35LL          PARAPASS  
   8 366=    RPS5  ADX   5  FX1  
  10 36KW    RPS7  MHUNT    3,CPB,CUNI   
  10 375G          STO   3  ACOMMUNE3(2) 
   8 37K6    RPS8  LDX   1  FX1  
   9 384Q          LDX   0  ANUM(3)  
   7 38JB          LDXC  5  5
   8 3942          BCS      RPS9 
  16 39HL          BZE   0  PNULL               [J IF NULL PARAM 
  17 3=3=          BNG   0  PABS                [J IF PARAM ABSENT   
   7 3=GW    RPS9  EXIT  5  0
  11 3?2G    [ REMOVE SPACES FROM CPB/CUNI   
  10 3?G6    PARA1 MHUNT    3,CPB,CUNI   
  10 3?_Q          STO   3  ACOMMUNE3(2) 
   7 3#FB          LDX   1  3
   8 3#_2          STO   5  GEN0 
   9 3*DL          LDX   5  ANUM(3)  
   8 3*Y=          ANDN  5  #7777
  15 3BCW          BZE   5  PARA7               [J IF NO DATA
   9 3BXG    PARA2 LDCH  0  APARA(1) 
  18 3CC6          DCH   0  APARA(3)            [KEEP NON-SPACE CHARS.   
   8 3CWQ          SBN   0  #20  
  15 3DBB          BZE   0  PARA6               [J IF SPACE  
   7 3DW2    PARA5 BCHX  3  /
   7 3F*L    PARA6 BCHX  1  /
   8 3FT=          BCT   5  PARA2
   7 3G#W          SLC   1  2
   7 3GSG          SLC   3  2
   7 3H#6          SBX   1  3
  16 3HRQ          LDX   3  ACOMMUNE3(2)        [PTR->CPB/CUNI   
  17 3J?B          SBS   1  ANUM(3)             [ADJUST CHAR. COUNT  
   8 3JR2    PARA7 LDX   5  GEN0 
   8 3K=L          BRN      RPS8 
  13 3KQ=    [ ROUTINE TO PUT DATA INTO ADATA/CSTORE 
   6 3L9W    [ X6 IS LINK
   9 3LPG    [ ON EXIT, X3 HOLDS DATA
  16 3M96    STOREX                             [DATA HELD IN X7 
   7 3MNQ          LDX   3  7
  19 3N8B          LDN   7  1                   [COUNT OF WORD TO BE STORED  
  18 3NN2          ORX   6  GSIGN               [STOREX ENTRY INDICATOR  
  18 3P7L    STOREN                             [DATA HELD IN CPB/CUNI   
  14 3PM=    [ IF ENTERED AT STOREN, DATA IS IN CPB/CUNI 
  13 3Q6W    [ X7 HOLDS COUNT OF WORDS TO BE STORED  
   8 3QLG          SBX   6  FX1  
  18 3R66          CALL  1  SCSTORE              [SET X2 ->ADATA/CSTORE  
  19 3RKQ          BPZ   2  STO2                 [J IF ADATA/CSTORE EXISTA   
  11 3S5B          SETNCORE 10,2,ADATA,CSTORE
   8 3SK2          LDN   0  A1+2 
  17 3T4L          STO   0  A1(2)               [PTR->NEXT FREE WORD 
   9 3TJ=          STO   0  A1+1(2)  
  10 3W3W    STO2  LDX   0  ALOGLEN(2)   
   8 3WHG          ADN   0  A1   
   8 3X36          SBX   0  A1(2)
   7 3XGQ          SBX   0  7
  19 3Y2B          BPZ   0  STO4                [J IF ENOUGH ROOM IN CSTORE  
  10 3YG2          LDX   4  ALOGLEN(2)   
   7 3Y_L          SBX   4  0
  18 3_F=          ALTLENG  2,4,SCSTORE         [INCREASE LEGTH OF CSTORE
  18 3_YW          CALL  1  SCSTORE              [SET X2->ADATA/CSTORE   
  19 42DG    STO4  ADS   7  A1(2)               [UPDATE PTR TO NEXT FREE WORD
   8 42Y6          ADX   2  A1(2)
   7 43CQ          SBX   2  7
  18 43XB          STO   3  0(2)                [STO DATA IN STOREX CASE 
   7 44C2          LDXC  6  6
  16 44WL          BCS      STO9                [J IF STOREX CASE
  10 45B=          MHUNT    1,CPB,CUNI   
   9 45TW          LDN   1  APARA(1) 
   7 46*G          SMO      7
  19 46T6          MOVE  1  0                   [STORE DATA IN STOREN CASE   
   8 47#Q    STO9  ADX   6  FX1  
   8 47SB          LDX   1  FX1  
   8 48#2          LDX   2  FX2  
   7 48RL          EXIT  6  0
   5 49?=    SCSTORE 
  11 49QW          MHUNTW   2,ADATA,CSTORE   
   7 4==G          EXIT  1  0
   4 4=Q6    #   
   5 4?9Q    RCLOCK  
  21 4?PB    # THIS S/R CONVERTS A TIME GIVEN IN 24HR CLOCK FORMAT,XX.XX,INTO MINUTES
   6 4#92    # MIDNIGHT. 
   4 4#NL    #   
   8 4*8=          SBX   6  FX1  
   9 4*MW          CALL  5  RPARAPASS
   8 4B7G          ADX   6  FX1  
  11 4BM6    RCL0  TXU   0  Z2CLOCKANUM(1)   
   8 4C6Q          BCC      RCL2 
  11 4CLB          TXU   0  Z3CLOCKANUM(1)   
   8 4D62          BCS      PTERR
   4 4DKL    RCL1
  20 4F5=          ORX   6  GSIGN               [SET BO OF RETURN ADDR AS A FLAG.
   7 4FJW          NGNC  2  1
  21 4G4G    RCL2  LDN   2  2                   [SET LOOP COUNT=2 FOR XX.XX;3 ELSE   
  20 4GJ6          ERX   6  GSIGN               [FLIP B0. NOW SET IF SHORT FORMAT
   8 4H3Q          LDCT  7  #400 
   8 4HHB          BRN      RCL3 
   9 4J32    RCL4  LDCH  5  APARA(3) 
   8 4JGL          SBN   5  #36  
  20 4K2=          BNZ   5  PTERR               [ERROR IF SEPERATOR NOT FULL STOP
   7 4KFW          BCHX  3  /
  18 4K_G    RCL3  LDN   5  2                   [NUMBER OF CHARS TO CDB  
   7 4LF6          LDN   0  0
   7 4LYQ          LDN   1  0
   9 4MDB    RCL5  CDB   0  APARA(3) 
   8 4MY2          BCS      PTERR
   7 4NCL          BCHX  3  /
   8 4NX=          BCT   5  RCL5 
   8 4PBW          LDN   5  60   
   7 4PWG          LDXC  7  7
  19 4QB6          BCC      RCL6                [J IF NOT FIRST NUMBER PAIR  
   8 4QTQ          LDN   5  24   
   7 4R*B    RCL6  TXL   1  5
  19 4RT2          BCC      PTERR               [NUMBER PAIR>PERMITTED MAX.  
   8 4S#L          LDN   5  60   
   7 4SS=    RCL7  MPY   7  5
   7 4T?W          LDX   7  1
  18 4TRG          ADX   7  0                   [ACCUMULATE TOTAL SO FAR 
  20 4W?6          BCT   2  RCL4                [REPEAT CONVERSION FOR NEXT PAIR 
   7 4WQQ          LDN   2  1
   7 4X=B          LDN   1  0
   7 4XQ2          LDXC  6  6
  20 4Y9L          BCS      RCL7                [J IF SHORT FORM TO MULT BY 60   
   7 4YP=          EXIT  6  0
   4 4_8W    #   
   4 4_NG    #   
   6 5286    RDATEMACRO  
  21 52MQ    # THIS S/R CONVERTS A DATE GIVEN IN XX/XX/XX FORMAT INTO A BINARY NO.OF 
   6 537B    # SINCE 1900
   4 53M2    #   
   8 546L          SBX   6  FX1  
   9 54L=          CALL  5  RPARAPASS
   8 555W          CHADCON  PTERR
  20 55KG          LDX   7  EXEC1(2)            [RESULT IS PASSED ACROSS IN EXEC1
   8 5656          ADX   6  FX1  
   7 56JQ          EXIT  6  0
   4 574B    #   
   6 57J2    RTIMEUNIT   
  17 583L    #  THIS S/R  CONVERTS N UNITS OF TIME INTO BINARY SECS. 
   4 58H=    #   
   8 592W          SBX   6  FX1  
  17 59GG          CALL  5  PARA                [GET NEXT PARAMETER  
   7 5=26    RTU0  PARSPLIT 5
   8 5=FQ          BNG   5  RTU1 
  15 5=_B          BNZ   5  RTU5                [J IF ERROR  
  15 5?F2    RTU1  LDX   7  ACOMMUNE1(2)        [BINARY NO.  
  20 5?YL          BZE   5  RTU3                [J IF NO LETTERS -SECS. ASSUMED  
   9 5#D=          TXU   5  SEC(1)   
  15 5#XW          BCC      RTU3                [J IF SECS   
   9 5*CG          TXU   5  MIN(1)   
  15 5*X6          BCC      RTU4                [J IF MINS   
   9 5BBQ          TXU   5  ZHR(1)   
  17 5BWB          BCS      RTU5                [J IF NOT RECOGNISED 
   8 5CB2          LDN   0  3600 
  15 5CTL    RTU2  MPY   7  0                   [RESULT IN X0
   7 5D*=          LDX   7  0
   8 5DSW    RTU3  ADX   6  FX1  
   7 5F#G          EXIT  6  0
   8 5FS6    RTU4  LDN   0  60   
   8 5G?Q          BRN      RTU2 
   8 5GRB    RTU5  ADX   6  FX1  
   7 5H?2          EXIT  6  1
   4 5HQL    #   
  20 5J==    PTUER BNG   5  XCOM                [J IF LETTER STRING UNRECOGNISED 
  15 5JPW          SBN   5  1                   [J IF NULL   
  15 5K9G          BZE   5  PNULL               [J IF NULL   
   7 5KP6          SBN   5  1
  17 5L8Q          BZE   5  PABS                [J IF PARAM MISSING  
   8 5LNB          BRN      XEROP
   5 5M82    PCLOCK  
   9 5MML          CALL  6  RCLOCK   
   8 5N7=          BRN      PLOAD
   6 5NLW    PDATELOAD   
  10 5P6G          CALL  6  RDATEMACRO   
   8 5PL6          BRN      PLOAD
   4 5Q5Q    PTSN
  18 5QKB          CALL  5  RPARAPASS           [X3->CUNI HOLDING MTNAME 
  16 5R52          DOWN     JDTWO,2       [ANALYSE TSN DETAILS   
  16 5RJL          BRN      XMTER             [ERROR FROM MT DESC
  17 5S4=          BRN      XEROP             [MESSAGE OUTPUT ALREADY
  10 5SHW    TSN9  MFREE    FILE,FABSNB  
  10 5T3G          LDX   7  ACOMMUNE1(2) 
   8 5TH6          BRN      PLOAD
   4 5W2Q    #   
   5 5WGB    PINTEGER
  21 5X22    # THIS ROUTINE ACCEPTS NUMBERS,SIGNED OR UNSIGNED,SIMPLE EXPRESSIONS,ETC
  11 5XFL    # SPECIFIED BY CHNUMCON 4.8.2.5 
   4 5X_=    #   
   9 5YDW          CALL  5  RPARAPASS
  20 5YYG          CHNUMCON 1                   [NUMBER CONVERSION ROUTINE,WITH  
  11 5_D6          TESTREP  CHNUMERR,PTERR   
  11 5_XQ          MHUNTW   3,CPAR,GNUMCON   
  21 62CB          LDX   7  A1+1(3)             [A1 HOLDS THE NUMBER OF EXPRESSIONS C
  10 62F# ...[ B7270 CORRECTION  06.08.82
   9 62H= ...[ ROUND UP MAXSIZE DATA 
  11 62K8 ...      JBC      PLOAD,2,TMAXSIZE 
   9 62M6 ...      ADN   7  PROGRND-1
  10 62P4 ...      ANDX  7  PROGRNDN(1)  
  11 62R2 ...      BXL   7  MAXSIZE(1),PLOAD 
  10 62SY ...      LDX   7  MAXSIZE(1)   
  21 62X2          BRN      PLOAD               [AS BLOCK WAS PARAPASSED THERE CAN   
  18 63BL                                       [BE ONLY ONE EXPRESSION. 
   5 63W=    POCTAL  
  15 64*W    # THIS ROUTINE ACCEPTS A SINGLE LENGTH NO. OF   
  14 64TG    # EXPEXTED TO BE OCTAL AND PRECEDED BY A #  
   9 65*6          CALL  5  RPARAPASS
  17 65SQ          STO   3  GEN0                [CONVERTSION ROUTINE 
   7 66#B          LDX   2  3
  15 66S2          LDEX  3  ANUM(2)             [NO OF CHARS 
   9 67?L          LDCH  1  APARA(2) 
  17 67R=          SBN   1  #23                 [FIRST CHAR MUST BE #
  15 68=W          BNZ   1  PTERR               [ELSE ERROR  
   7 68QG          SBN   3  2
  20 69=6          TXL   3  EIGHT               [NO MORE THAN 8 FIG NO ALLOWED   
  15 69PQ          BCC      PTERR               [ELSE ERROR  
   7 6=9B          ADN   3  1
   7 6=P2          LDN   0  0
   5 6?8L    XMCH1   
  17 6?N=          BCHX  2  /                   [CONVERT TO BINARY   
   9 6#7W          LDCH  1  APARA(2) 
   8 6#MG          TXL   1  EIGHT
   8 6*76          BCC      PTERR
   7 6*LQ          SLL   0  3
   7 6B6B          ORX   0  1
   8 6BL2          BCT   3  XMCH1
   8 6C5L          LDX   2  FX2  
  10 6CK=          STO   0  ACOMMUNE1(2) 
   8 6D4W          LDX   1  FX1  
   8 6DJG          LDX   3  GEN0 
  17 6F46          LDX   7  ACOMMUNE1(2)        [SAVE CONVERTED NO.  
   8 6FHQ          BRN      PLOAD
   4 6G3B    #   
   4 6GH2    #   
   4 6H2L    #   
   6 6HG=    PDEADLINE   
  21 6H_W    # THIS S/R PROVIDES THE FACILITY OF LOADING THREE PARAMETERS INTO THREE 
  21 6JFG    # CONSECUTIVE WORDS IN THE USERS ENTRY IN WJL.THE REQUIRED FORMAT FOR TH
  20 6J_6    # 1ST PARAM: DATE - DD/MM/YY *OPTIONAL*  FORMAT SAME AS FACILITY 3  
  18 6KDQ    # 2ND (1ST) PARAM: TIME - 24HR-CLOCK: HH.MM  SAME AS FAC 2  
  21 6KYB    # 3RD (2ND) PARAM: EST. RUN TIME: DIGITSTRING & UNITS CHARSTRING = FAC5.
   4 6LD2    #   
  17 6LXL          JBS      PTERR,2,SINGLE      [J IF SINGLE PARAM.  
  21 6MC=          PARANUMB 7                   [ESTABLISH THE NO.OF PARAMETERS-MAX=4
  16 6MWW          JBC      PD0,2,TFORMAT       [J IF 1ST FORMAT 
   7 6NBG          ADN   7  1
  21 6NW6    PD0   SBN   7  3                   [1ST PARAMETER=8.2ND(OPTIONAL)IS'DATE
  20 6P*Q          BZE   7  NODATE              [3RD IS 'CLOCK'.4TH IS 'TIME'.   
  10 6PTB          CALL  6  RDATEMACRO   
   9 6Q*2          MFREE    CPB,CUNI 
   5 6QSL    NODATE  
  19 6R#=          CALL  6  STOREX             [STORE CONVERTED DATE OR ZERO 
  16 6RRW          CALL  6  RCLOCK              [2ND PARAMETER.  
   9 6S?G          MFREE    CPB,CUNI 
  18 6SR6          CALL  6  STOREX             [STORE CONVERTED CLOCKTIME
  16 6T=Q          CALL  6  RTIMEUNIT           [LAST PARAMETER. 
   8 6TQB          BRN      PLOAD
  20 6W=2          BRN      PTUER               [J TO PTIMEUNIT'S ERROR ROUTINE. 
  17 6WPL                                       [X3->ADATA/CSTORE.   
   4 6X9=    #   
   4 6XNW    #   
  15 6Y8G    PSTRING                            [FACILITY 9  
  21 6YN6    # THIS ROUTINE PASSES OVER A CHAR STRING - INCLUDING SPACES, HAVING REMO
  21 6_7Q    # ONE LAYER OF PARENTHESES OR QUOTES - INTO AREA DEFINED BY :SYSTEM.JOBL
  21 6_MB    # FACILITIES TABLE. THE STRING IS EITHER TRUNCATED OR SPACE-FILLED AT TH
  21 7272    # AS APPROPRIATE, IF NOT AN EXACT FIT. FORMS LIKE  A(B)  ARE NOT ALLOWED
   4 72LL    #   
  17 736=          CALL  5  RSPARAPASS          [GET NEXT PARAMETER  
   8 73KW          SRL   0  12   
   7 745G          SBN   0  2
  10 74K6          BNG   0  STRINGREADY  
   8 754Q          BNZ   0  PTERR
  17 75JB          JBS      STRINGREADY,2,TFORMAT [J IF 2ND FORMAT   
   7 7642          LDN   1  2
   8 76HL          SPARANXT ,0,1 
   6 773=    STRINGREADY 
  10 77GW          MHUNT    3,CPB,CUNI   
  17 782G          CALL  1  SCSTORE             [SET X2->ADATA/CSTORE
   9 78G6          LDX   0  ANUM(3)  
   8 78_Q          SLL   0  22   
   9 79FB          SMO      A1+1(2)  
  20 79_2          ORS   0  0(2)                [STORE CHAR. PTR. IN REC.HEDR.   
   9 7=DL          LDX   7  ANUM(3)  
   8 7=Y=          ANDN  7  #7777
   7 7?CW          ADN   7  3
   7 7?XG          SRL   7  2
  17 7#C6          CALL  6  STOREN              [STORE DATA IN CSTORE
   8 7#WQ          BRN      RFIN 
   4 7*BB    XK3 
   6 7*W2    STARTTIME   
  13 7B*L    # THIS A SPECIAL ROUTINE TO EMULATE THE 
   8 7BT=    # STARTTIME MACRO   
  16 7C#W          CALL  5  PARA                [GET NEXT PARAM. 
   9 7CSG          LDX   7  ANUM(3)  
   8 7D#6          BZE   7  STTI2
   8 7DRQ          BNG   7  PTERR
  15 7F?B          CALL  6  RCL0                [CONVERT TIME
   8 7FR2          LDX   2  FX2  
   5 7G=L    STTI2   
  16 7GQ=          STO   7  AWORK2(2)           [KEEP THE TIME   
  15 7H9W          LDX   7  EDATE               [TODAY'S DATE
  11 7HPG          JBS      STDTE,2,SINGLE   
   9 7J96          MFREE    CPB,CUNI 
  16 7JNQ          CALL  5  PARA                [GET NEXT PARAM. 
   9 7K8B          NGX   0  ANUM(3)  
   8 7KN2          BPZ   0  STDTE
  15 7L7L          CHADCON  PTERR               [CONVERT DATE
  13 7LM=          LDX   7  ACOMMUNE7(2)        [DATE
   4 7M6W    #   
   5 7MLG    STDTE   
  15 7N66          CALL  6  STOREX              [STORE DATE  
   9 7NKQ          LDX   7  AWORK2(2)
  21 7P5B    PLOAD CALL  6  STOREX              [STORE TIME ALSO USED  BY JD NO ROUTI
   4 7PK2    RFIN
  11 7Q4L          MHUNT    3,ADATA,CSTORE   
   8 7QJ=          LDX   0  A1(3)
   9 7R3W          SBX   0  A1+1(3)  
  17 7RHG          BXGE  0  TOOSMALL(1),RFIN6   [J IF SOME JD DATA   
  19 7S36          SBS   0  A1(3)               [RESET PTR->NEXT FREE WORD   
   8 7SGQ          BRN      RFIN8
  11 7T2B    RFIN6 BXGE  0  TOOBIG(1),TOOMUCH
   9 7TG2          SMO      A1+1(3)  
  16 7T_L          DSA   0  0(3)                [STORE WORD COUNT
   9 7WF=          SMO      A1+1(3)  
   8 7WYW          LDX   0  1(3) 
  18 7XDG          BPZ   0  RFIN7               [J IF NOT FIXED RESOURCE 
   8 7XY6          ANDN  0  #4000
  16 7YCQ          BNZ   0  RFIN7               [J IF COUNT SET  
  17 7YXB          JMBS     NOCOUNT,2,TMINUS,TPLUS [ERROR IF SIGNED  
   8 7_C2    RFIN7 LDX   0  A1(3)
  16 7_WL          STO   0  A1+1(3)             [PTR->NEXT RECORD
   7 82B=    RFIN8 LDN   7  0
  17 82TW          JBC      RFIN81,2,STARTCOM   [J IF JOBDATA PARAM  
   7 83*G          LDN   7  3
   5 83T6    RFIN81  
   7 84#Q          PARAFREE 7
  17 84SB          JBS      NEXT0,2,TFORMAT     [J IF 2ND JD FORMAT  
   5 85#2    RFIN9   
  11 85RL          HUNTW    3,ADATA,CSTORE   
  19 86?=          BNG   3  THENDNEAR            [J IF NO JOB DATA SUPPLIED  
   9 86QW          MFREE    CPB,CUNI 
  18 87=G          ACROSS   JDWRITE,1           [UPDATE ENTRY IN JOBLIST 
   4 87Q6    #   
  13 889Q    #        RETURN LABEL TO JOBDATA SEGMENT
   5 88PB    NEXT0   
  10 8992          ACROSS   JOBDATA,10   
   4 89NL    #   
   9 8=8=    #     ERROR  REPORTING  
   4 8=MW    #   
   4 8?7G    #   
   9 8?M6    PNULL LDX   7  YER2(1)  
   9 8#6Q          BRN      RERROR   
   9 8#LB    PABS  LDX   7  YER3(1)  
   9 8*62          BRN      RERROR   
   8 8*KL    PTERR LDX   1  FX1  
   9 8B5=          LDX   7  YER4(1)  
   9 8BJW          BRN      RERROR   
   9 8C4G    XCOM  LDX   7  YER5(1)  
   9 8CJ6          BRN      RERROR   
   5 8D3Q    TOOMUCH 
   9 8DHB          LDX   7  YER8(1)  
   9 8F32          BRN      RERROR   
   9 8FGL    XMTER LDX   7  YER9(1)  
   9 8G2=          BRN      RERROR   
   5 8GFW    NOCOUNT 
   9 8G_G          LDX   7  YER10(1) 
   9 8HF6          BRN      RERROR   
   5 8HYQ    XEROP   
  17 8JDB          LDX   7  GSIGN               [ERROR ALREADY O/P   
   4 8JY2    XK2 
   5 8KCL    RERROR  
   7 8KX=          NGN   6  1
   5 8LBW    RERROR1 
   8 8LWG          LDX   2  FX2  
  17 8MB6          JBS      RERRORUP,2,STARTCOM  [J IF JOBDATA PARAM.
   8 8MTQ          BNG   7  THEND
   8 8N*B          COMERRX  7,6  
   6 8NT2    THENDNEAR   
   8 8P#L          LDX   2  FX2  
  11 8PS=          JBC      THEND,2,STARTCOM 
   9 8Q?W          MFREE    CPB,CUNI 
   7 8QRG          UPPLUS 1  
   6 8R?6    THEND ENDCOM
   5 8RQQ    RERRORUP
  18 8S=B          BNG   7  RERRORUP5           [J IF ERROR ALREADY O/P  
   8 8SQ2          ERRORX   7,6  
  18 8T9L          LDX   7  GSIGN               [ERROR ALREADY O/P IND.  
   6 8TP=    RERRORUP5   
   9 8W8W          MFREE    CPB,CUNI 
  10 8WNG          STO   7  ACOMMUNE1(2) 
   5 8X86          UP
   5 8XMQ    XBREAK  
   7 8Y7B          COMBRKIN  
   4 8YM2    #END
   6 ____ ...076230150006
