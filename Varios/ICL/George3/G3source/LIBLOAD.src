  12 22FL    #LIS  K0LIBLOAD>K0LIBRARY>K0ALLGEO  
  15 22_=    #SEG           LIBLOAD65           [N.R.BOULT   
   9 23DW                   8HLIBLOAD
   4 23JR ...[   
  17 23NN ...[  (C)   COPYRIGHT  INTERNATIONAL COMPUTERS LTD  1982   
   4 23SK ...[   
  12 23YG          SEGENTRY K1LIBLOAD,QK1LIBLOAD 
   4 24D6    #   
  20 24XQ    #     THIS SEGMENT IS ENTERED FROM ENGAGE WHEN AN INTERRUPT OCCURS  
  12 25CB    #     ON AN UNALLOCATED TAPE DECK.  
  16 25X2    #     THE TAPE IS REWOUND AND THE HEADER LABEL READ.
  19 26BL    #     THE PERIPHERAL LIST ENTRY IS UPDATED AND AN ENTRY FOR THE 
  13 26W=    #     TAPE IS MADE IN :SYSTEM.DOCUMENT. 
  17 27*W    #     FURTHER,CHECKS ARE MADE THAT THE TAPE HEADER LABEL
  14 27TG    #     (A)   HAS THE STANDARD 1900 FORMAT,   
  12 28*6    #     (B)   AGREES WITH OUR RECORDS.
   4 28SQ    #   
   4 29#B    #   
   7 29S2    #     CONSTANTS 
   4 2=?L    #   
  18 2=R=    THDDR          4HHDDR              [FIRST WORD OF TAPE LABEL
  19 2?=W    TEXCH          #74                 [LOWEST DISALLOWED CHARACTER 
  16 2?QG    REELMAX        +512                [REEL NRS <512   
   9 2#=6    REMASK         #777777  
   9 2#PQ    TINS           #20000000
   9 2*9B    TQU            #400000  
  10 2*P2    TPOOL          12HPOOL TAPE 
   7 2*W8 ...#UNS  JMODGCR   
   4 2B3B ...#SKI
   9 2B8L    TCHARM         #77617777
   4 2BN=    #   
  11 2C7W    #     LOCAL IDENTIER DEFINITIONS
   4 2CMG    #   
   7 2D76    #DEF  STM=HTM   
   7 2DLQ    #DEF  SSB=HSB   
   7 2F6B    #DEF  SWR=HFREE4
   7 2FL2    #DEF  SHORTPM=1 
   4 2G5L    #   
  15 2GK=    #                                  ENTRY POINT 1
   4 2H4W    #   
   6 2HJG    QK1LIBLOAD  
   9 2J46    #SKI  K6LIBLOAD>99-99   
  11 2JHQ          SWITRACE CPPTR(2),K1LIBLOD
  10 2K3B          DFJUMPS  ,LOAD,TALDED 
  16 2KH2          CLCOUNT   ,FAIL              [FAILS MAY HAVE..
  18 2L2L          CLCOUNT   ,REPEAT             [..OCCURRED ON UNLOADING
  19 2LG=          LDCT  0  #200                [SET PRIVILEGE TO OPEN FILES 
  15 2L_W          ORS   0  CLONG1(2)           [. IN A JAM  
  11 2MFG    #     A        REWIND THE TAPE  
  15 2M_6          LDN   4  7                   [REWIND MODE 
  10 2NDQ          DCA      ,DLA,4,MODE  
   8 2NYB          LOCKC    2,/  
  15 2PD2          GPERI    ,2                  [REWIND TAPE 
   6 2PXL          COOR1 
   7 2QC=          UNLOCK   2
  12 2QWW    #     B        CHECK REWIND REPLY   
  14 2RBG          LDCH  0  CPRW1(2)            [REPLY   
  15 2RW6          BNZ   0  PK                  [FAIL REPLY  
  11 2S*Q    #     C        READ HEADER LABEL
  11 2STB          SETNCORE 27,3,FLIB,FMAGH,1
   8 2T3L ...      LDN   0  18   
   7 2T9W ...Q1CY  SMO      0
   9 2TD6 ...      STOZ     A1+8(3)  
   8 2TLB ...      BCT   0  Q1CY 
  15 2TSL          LDN   4  0                   [READ MODE   
  10 2W#=          DCA      ,DLA,4,MODE  
  14 2WRW          LDN   4  20                  [COUNT   
  10 2X?G          DCA      ,STO,4,COUNT 
   9 2XR6          LOCKC    3,SERCHF 
  10 2Y=Q          MHUNT    3,FLIB,FMAGH 
  14 2YQB          LDN   4  A1+1(3)             [ADDRESS 
  10 2_=2          DCA      ,STO,4,CTSA  
   8 2_PL          LOCKC    2,/  
  15 329=          GPERI    ,2                  [READ HEADER 
   6 32NW          COOR1 
   7 338G          UNLOCK   3
   7 33N6          UNLOCK   2
  10 347Q          MHUNT    3,FLIB,FMAGH 
  11 34MB    #     D        CHECK READ REPLY 
   9 3572    #SKI  K6LIBLOAD>199-199 
  11 35LL          SWITRACE CPRW1(2),LBLDREP 
  16 366=          LDCH  0  CPRW1(2)            [REPLY CHARACTER 
  18 36KW          ANDN  0  #57                 [REMOVE LONG BLOCK BIT   
  16 375G          BZE   0  PE                  [SUCCESS REPLY   
  15 37K6          ANDN  0  #75                 [TAPEMARK BIT
   7 384Q          LDN   7  0
   8 38JB          BNZ   0  PF   
  14 3942          LDN   7  1                   [MARKER 1
   8 39HL          BRN      PG   
  13 3=3=    #     E        CHECK HEADER LABEL FORMAT
  15 3=GW    PE    LDX   0  CPRW1(2)            [REPLY WORD  
   9 3?2G          ANDX  0  REMASK(1)
  10 3?G6          DCA      ,SBX,0,CTSA  
  19 3?_Q          ANDX  0  REMASK(1)           [NUMBER OF WORDS TRANSFERRED 
   7 3#FB          SBN   0  9
   8 3#_2          BPZ   0  PE3  
  14 3*DL          NGN   7  1                   [MARKER 1
   8 3*Y=          BRN      PG   
  17 3BCW    PE3   LDX   0  A1+1(3)             [FIRST WORD OF LABEL 
  15 3BXG          TXU   0  THDDR(1)            [ 'HDDR' ?   
   8 3CC6          BCC      PE4  
  14 3CWQ    PE7   LDN   7  0                   [MARKER 1
   8 3DBB          BRN      PG   
  13 3DW2    PE4   LDX   4  A1+2(3)             [TSN 
   9 3F*L    #SKI  K6LIBLOAD>199-199 
   9 3FT=          SWITRACE 4,LBLDTSN
  14 3G#W          BNG   4  PE7                 [POSITIVE
   8 3GSG          BZE   4  PE7  
  18 3H#6          LDN   5  12                  [TEST 12 CHARACTERS OF   
  15 3HRQ          LDX   2  3                   [. TAPE NAME 
   9 3J?B    PE5A  LDCH  0  A1+3(2)  
   9 3JR2          TXL   0  TEXCH(1) 
   8 3K=L          BCC      PE7  
   7 3KQ=          BCHX  2  /
   8 3L9W          BCT   5  PE5A 
  14 3LPG          LDX   2  FX2                 [RESET X2
   9 3M96          LDX   5  A1+3(3)  
   9 3MNQ          LDX   6  A1+4(3)  
   9 3N8B          LDX   7  A1+5(3)  
   8 3NN2          TXU   5  ACES 
   8 3P7L          TXU   6  ACES 
   8 3PM=          TXU   7  ACES 
  17 3Q6W          BCC      PE7                 [J IF HDDR ALL SPACES
  13 3QLG          LDX   0  A1+6(3)             [REEL
  10 3R66          TXL   0  REELMAX(1)   
  13 3RKQ          BCC      PE7                 [>512
  10 3S5B    #     H        LONG BLOCK ? 
  16 3SK2          LDCH  0  CPRW1(2)            [REPLY CHARACTER 
   8 3T4L          ANDN  0  #20  
  16 3TJ=          BZE   0  PJ                  [NOT LONG HEADER 
  12 3W3W    #     I        LONG BLOCK MESSAGE   
  19 3WHG          LGEOG    ,0                  [LOAD GEOGRAPHIC NO. OF DECK 
   8 3X36          OUTNUM   0,0  
   9 3XGQ          MTINFORM HELONG   
  13 3Y2B    #     J        OPEN SERIAL AND DOCUMENT 
  13 3YG2    PJ    OPENSYS  XBRKIN,DOCUMENT,GENERAL  
  10 3Y_L          FINDPEREC 3,APTSER,4  
  20 3_F=          BNG   3  PJ1                 [BRANCH IF TSN NOT RECORDED IN DL
  20 3_YW          BXU   3  CPPTR(2),PAC        [BRANCH IF TAPE ON ANOTHER DECK  
   9 42DG          DFLOAD   3,LOAD,0 
  21 42Y6          BNZ   0  PAC                 [BRANCH UNLESS GENERAL RESTORE TAPE  
   4 43CQ    PJ1 
  12 43XB          OPENSYS  XBRKIN,SERIAL,GENERAL
  12 44C2    #     N        SEARCH :SYSTEM.SERIAL
  21 44WL          SSEARCH  PINS,4              [CHECK FOR AN ENTRY IN :SYSTEM.SERIAL
  12 45B=    #     P        TEST STATUS OF TAPE  
   8 45TW          LDX   0  5(3) 
   9 46*G    #SKI  K6LIBLOAD>99-99   
   9 46T6          SWITRACE 0,LBLDSS5
   7 47#Q          SLC   0  5
  18 47SB          BNG   0  PQ                  [TAPE WRONGED BY OPERATOR
   7 48#2          LDX   6  0
  17 48RL          SLC   6  1                   [<0 IF QUERY STATUS  
   8 49?=          LDX   0  5(3) 
   7 49QW          SLC   0  1
  15 4==G          BNG   0  PN4                 [WORK TAPE   
  15 4=Q6          LDX   0  2(3)                [USER NAME   
  15 4?9Q          BNZ   0  PR                  [TAPE OWNED  
  15 4?PB          BRN      PS                  [MUST BE POOL
   4 4#92    PN4 
  18 4#NL          DFSET    ,PWORK              [PERMANENT WORKTAPE BIT  
   7 4*8=          LDN   6  0
   8 4*MW          LDX   0  2(3) 
  19 4B7G          BNZ   0  POA                 [BRANCH IF WORKTAPE IN USE   
  14 4BM6          LDN   6  1                   [MARKER 3
   8 4C6Q          JWPR     ,POA 
  17 4CLB          BRN      PF1                 [J IF WPR NOT LOADED 
  16 4D62    #     S        TEST IF POOL TAPE IS CORRECTLY NAMED 
   4 4DKL    PS  
  18 4F5=          BPZ   6  PS2                 [BRANCH UNLESS Q STATUS  
   8 4FJW          LDX   0  5(3) 
   8 4G4G          ANDN  0  #7777
  21 4GJ6          BNZ   0  PNQ                 [J IF COUNT OF REQUESTERS NON-ZERO   
  10 4H3Q          MHUNT    3,FLIB,FMAGH 
   9 4HHB          LDX   0  A1+3(3)  
   9 4J32          TXU   0  TPOOL(1) 
   8 4JGL          BCS      PNQ  
   9 4K2=          LDX   0  A1+4(3)  
  10 4KFW          TXU   0  TPOOL+1(1)   
   8 4K_G          BCS      PNQ  
   9 4LF6          LDX   0  A1+5(3)  
  10 4LYQ          TXU   0  TPOOL+2(1)   
   8 4MDB          BCS      PNQ  
  14 4MY2          LDN   6  2                   [MARKER 3
  17 4NCL          BRN      PAD                 [REMOVE QUERY STATUS 
   4 4NX=    PNQ 
   9 4PBW          LDX   6  TQU(1)   
   8 4PWG          BRN      PINS1
   4 4QB6    PINS
   9 4QTQ          LDX   6  TINS(1)  
   5 4R*B    PINS1   
  10 4RT2          MHUNT    3,FLIB,FMAGH 
   9 4S#L    #SKI  K6LIBLOAD>199-199 
  10 4SS=          SWITRACE 6,LBLDSD21   
   9 4T?W          ORS   6  A1+21(3) 
   7 4TRG          LDN   6  0
   8 4W?6          BRN      PO   
  13 4WQQ    #     T        PUT TAPE TO QUERY STATUS 
  10 4X=B    PT    DOWN     LIBQUERY,1   
  10 4XQ2          LDX   6  ACOMMUNE1(2) 
   8 4Y9L          BRN      PV   
  14 4YP=    PS2   LDN   6  2                   [MARKER 3
   8 4_8W          JWPR     ,POA 
  17 4_NG          BRN      PF1                 [J IF WPR NOT LOADED 
   4 5286    POA 
  21 52MQ    #              STORE TAPE CHARACTERISTICS IF NOT ALREADY CORRECTLY KNOWN
   7 537B          STEPAGAIN 
   7 538R ...#UNS  JMODGCR   
   4 53=8 ...(   
  18 53?K ...      MBSXD    5,GSERRBTRA7,GSERRBPE,GSERRBNRZI,GSERRBGCR   
  10 53*2 ...      ANDX  5  GSERRPROP(3) 
   8 53BC ...      LPROP    ,0   
   8 53CS ...      MTHWSER  0,4  
   4 53F9 ...)   
   7 53GL ...#UNS  JMODGCR   
   4 53J3 ...#SKI
   4 53KD ...(   
   8 53M2          LDX   5  5(3) 
   8 546L          SRL   5  13   
   7 54L=          ANDN  5  7
   8 555W          LPROP    ,4   
   7 55KG          LDX   0  4
   8 5656          ANDN  0  #200 
  17 56JQ          BZE   0  PCHAR1              [BRANCH IF 7 TRACK   
   7 574B          ANDN  4  3
   9 57J2          BRN      PCHAR2   
   5 583L    PCHAR1  
   7 58H=          LDN   4  4
   5 592W    PCHAR2  
   4 59=N ...)   
  19 59GG          BXE   4  5,PCHAR3            [BRANCH IF CHAR ALREADY KNOWN
   7 5=26          READAGAIN 
  10 5=FQ          HUNT     3,FILE,FRB   
   7 5=J* ...#UNS  JMODGCR   
  17 5=LY ...      MBC      3,GSERBTRA7,GSERBPE,GSERBNRZI,GSERBGCR   
   7 5=PH ...#UNS  JMODGCR   
   4 5=S6 ...#SKI
   4 5=WP ...(   
   9 5=_B          LDX   0  TCHARM(1)
  18 5?F2          ANDS  0  A1+5(3)             [CLEAR ANY OLD TAPE CHAR.
   8 5?YL          SLL   4  13   
   4 5#8D ...)   
  15 5#D=          ORS   4  A1+5(3)             [SET CHAR.   
  10 5#XW          NAMETOP  3,FILE,FWB   
   9 5*CG    #SKI  K6LIBLOAD>199-199 
   4 5*X6    (   
  10 5BBQ          SWITRACE 4,LBLDNCHA   
  10 5BWB          SWITRACE 5,LBLDOCHA   
   4 5CB2    )   
   9 5CTL          LDX   4  A1+1(3)  
   7 5D*=          REWRITE   
   9 5DSW          MFREE    FILE,FWB 
   5 5F#G    PCHAR3  
  12 5FS6    #     O        CLOSE :SYSTEM.SERIAL 
  16 5G?Q    PO    CLOSETOP                     [:SYSTEM.SERIAL  
  15 5GRB    #     V        SET UP ENTRY FOR :SYSTEM.DOCUMENT
   4 5H?2    PV  
   9 5HQL    #SKI  K6LIBLOAD>199-199 
  10 5J==          SWITRACE 6,LBLDTYPE   
  10 5JPW          MHUNT    3,FLIB,FMAGH 
   8 5K9G          LDN   0  27   
  16 5KP6          STO   0  A1(3)               [RECORD HEADER   
  19 5L8Q          LGEOG    ,0                  [LOAD GEOGRAPHIC NO. OF DECK 
   9 5LNB          STO   0  A1+23(3) 
   9 5M82          LDX   1  CPPTR(2) 
   9 5MML          STO   1  A1+22(3) 
  13 5N7=          LDX   4  A1+2(3)             [TSN 
  10 5NLW          NAME     3,FILE,FAPB  
  12 5P6G    #     W        APPEND DOCUMENT ENTRY
  17 5PL6          APPEND                       [INSERT DOC RECORD   
  13 5Q5Q    #     X        UPDATE PERIPHERAL LIST   
   8 5QKB          BZE   6  PV1  
   7 5R52          LDX   0  6
   7 5RJL          SBN   0  1
   8 5S4=          BNZ   0  PV2  
   8 5SHW          DFSET    ,WORK
   8 5T3G          BRN      PV1  
   4 5TH6    PV2 
   8 5W2Q          DFSET    ,POOL
   4 5WGB    PV1 
   9 5X22          DFSET    ,TSN,4   
   8 5XFL          DFSET    ,LOAD
  13 5X_=    #     Y        CLOSE :SYSTEM.DOCUMENT   
  16 5YDW          CLOSETOP                     [:SYSTEM.DOCUMENT
   8 5YYG          LGEOG    ,5   
   4 5_D6    SJ1 
  11 5_XQ          OUTPACK  5,1,GEOPER,SJ1   
   4 62CB    SJ2 
  10 62X2          OUTPACKB 4,1,TSN,SJ2  
   4 63BL    SJ4 
  10 63W=          HUNT     3,FILE,FAPB  
  12 64*W          OUTPACKB A1+3(3),3,LOCNAME,SJ4
   4 64TG    SJ5 
  10 65*6          MHUNT    3,FILE,FAPB  
  12 65SQ          OUTPACKB A1+6(3),1,REELNUM,SJ5
   4 66#B    SJ6 
  10 66S2          MHUNT    3,FILE,FAPB  
  12 67?L          OUTPACKB A1+7(3),1,GENNUM,SJ6 
   9 67R=          MFREE    FILE,FAPB
   8 68=W          OUTPARB  SKIP 
   9 68QG          MONOUT   HLOADED  
  11 69=6    #     Z        DE-ALLOCATE DECK 
   9 69PQ          JWTBWR   ,TALDED  
   7 6=9B          CLALLOC   
   9 6=P2    #SKI  K6LIBLOAD>299-299 
   4 6?8L    (   
   8 6?N=          LADDP    ,1   
  10 6#7W          SWITRACE 1(1),LBLDAUX1
   4 6#MG    )   
   8 6#SN ...      LADDP    ,3   
  20 6#_W ...      STOZ     BMTELCT(3)    [CLEAR ERROR LOGGING COUNT IN APIA(+4) 
   9 6*76          STOZ     CPPTR(2) 
   9 6*LQ          STOZ     CPRW1(2) 
   9 6B6B          STOZ     CPRW2(2) 
   9 6BL2          STOZ     CPRSW(2) 
  15 6C5L    #     AA       WAKE UP APPROPRIATE ACTIVITIES   
   7 6CK=          SBN   6  2
   8 6D4W          BNZ   6  PAA2 
  17 6DJG          LONGON   #56                 [WAKE UP ACTIVITIES  
  18 6F46          BRN      PAB                 [WAITING FOR A POOL TAPE 
   7 6FHQ    PAA2  ADN   6  1
   8 6G3B          BNZ   6  PAA6 
  17 6GH2          LONGON   #63                 [WAKE UP ACTIVITIES  
  19 6H2L          BRN      PAB                 [. WAITING FOR A WORK TAPE   
   4 6HG=    PAA6
   6 6HH? ...#UNS FIRSTR 
   4 6HJ# ...(   
  17 6HK* ...      FDTENT    2,4,XNOFDT         [IS THIS TSN IN FDT??
  19 6HLB ...      JBCC     XDTA,2,BIFDTO       [J IF ENTRY ALREADY LOOKED AT
  17 6HMC ...                                   [OTHERWISE CLEAR BIT.
  19 6HND ...      BS       2,BIFDTJL           [SET 'TAPE JUST LOADED' BIT  
  19 6HNY ...      BS       BOLC,BIFDTLMEF      [SET 'LOOK AT FDT FIRST' BIT 
  17 6HPF ...      STARTACT RET                 [KICK DTS ACTIVITY   
  21 6HQG ...      BRN      PAB                 [RETRIEVAL SYSTEM WILL DEAL WITH THIS
  20 6HRH ...XDTA                               [FDT ENTRY BUT BIFDTO IS CLEAR   
  20 6HSJ ...      JBS      PAB,2,BIFDTJL       [J IF NO DTA SET UP YET BY DTSA  
  20 6HTK ...                                   [DTA HAS BEEN SET UP - KICK IT   
  21 6HWL ...                                   [......MOST PROBABLY IT IS DUMPER'S D
  19 6HXM ...XNOFDT                             [NO FDT ENTRY - SO CONTINUE  
   4 6HYN ...)   
  17 6H_W          LONGON   #61,4               [. OR FOR THIS TSN   
  16 6JFG          LONGON   #61                 [. OR FOR ANY TSN
   9 6J_6    #     AB       TERMINATE
   4 6KDQ    PAB 
   9 6KYB    #SKI  K6LIBLOAD>99-99   
   9 6LD2          GSCAN    4,TAPEUP 
   7 6LXL          SUICIDE   
  16 6MC=    #     G        OUTPUT NON-STANDARD HEADER MESSAGE   
  10 6MWW ...PG    ACROSS   LIBLOADB,1   
  10 6NBG ...PF1   ACROSS   LIBLOADB,2   
  11 7272    #     F        ATTEMPT REWIND   
  10 72LL ...PF    ACROSS   LIBLOADB,3   
  16 77GW    #     K        RECORD FAILURE AND INFORM OPERATORS  
  10 782G ...PK    ACROSS   LIBLOADB,4   
  13 7B*L    #     Q        OUTPUT 'WRONGED' MESSAGE 
  10 7BT= ...PQ    ACROSS   LIBLOADB,5   
  12 7JNQ    #     R        OPEN USERS DIRECTORY 
   4 7K8B    PR  
   8 7KN2          LDN   0  2(3) 
   9 7L7L          LDN   1  AWORK1(2)
  17 7LM=          MOVE  0  3                   [REMEMBER USER NAME  
  11 7M6W          SETNCORE 12,3,FILE,FABSNB 
   8 7MLG          LDN   0  10   
  15 7N66          STO   0  A1(3)               [RECORD HEAD 
   8 7NKQ          LDN   0  #2001
   9 7P5B          ORS   0  ATYPE(3) 
   9 7PK2          LDN   0  AWORK1(2)
   9 7Q4L          LDN   1  A1+1(3)  
  15 7QJ=          MOVE  0  3                   [USER NAME   
   9 7R3W          STOZ     A1+4(3)  
  17 7RHG          LDN   0  A1+4(3)             [LOCAL NAME,REEL AND 
  16 7S36          LDN   1  A1+5(3)             [. GENERATION ALL
  15 7SGQ          MOVE  0  5                   [. SET ZERO  
   7 7T2B          NGN   0  1
   9 7TG2          STO   0  A1+10(3) 
   9 7T_L          STO   4  A1+11(3) 
  13 7WF=          OPENDIR  XBRKIN,GENERAL,ERASING   
   9 7WYW          TESTREPNOT OK,XLIB
  18 7XDG    #     U        FIND DIRECTORY ENTRY,TEST IF TAPE IS 'NEW'   
  10 7XY6          MHUNT    3,FILE,ENT   
  10 7YCQ          MHUNT    2,FLIB,FMAGH 
   9 7YXB          LDX   0  ELOC1(3) 
   9 7_C2    #SKI  K6LIBLOAD>199-199 
  10 7_WL          SWITRACE 0,LBLDDENT   
   8 82B=          BNZ   0  PU6  
  10 82TW          MFREE    FILE,FABSNB  
  18 83*G          SETNCORE 6,1,FILE,FLOCNB     [SET UP LOCAL NAME BLOCK 
   9 83T6          STOZ     A1+3(1)  
   9 84#Q          STOZ     A1+4(1)  
   9 84SB          STOZ     A1+5(1)  
  19 85#2          MHUNT    2,FLIB,FMAGH        [MOVE TAPE NAME INTO BLOCK   
   9 85RL          LDN   0  A1+3(2)  
   8 86?=          LDN   1  A1(1)
   7 86QW          MOVE  0  3
   7 87=G          SETUPTAB  
   6 87Q6          TABSET
   7 889Q          GETDIR   2
  13 88PB          TESTREP  NOFILE,PU4,VRYWRONG,PU4  
   9 8992          MFREE    FILE,ENT 
  17 89NL          BRN      PU7A                [FILE ALREADY EXISTS 
   4 8=8=    PU4 
   7 8=MW          TABULATE  
   7 8?7G          FREETAB   
  10 8?M6          MHUNT    3,FILE,ENT   
  10 8#6Q          MHUNT    2,FLIB,FMAGH 
   9 8#LB          LDN   0  A1+3(2)  
   9 8*62          LDN   1  ELOC1(3) 
   7 8*KL          MOVE  0  4
  10 8B5=          MHUNT    1,FILE,FLOCNB
   7 8BJW          LDN   5  1
  16 8C4G          ORS   5  ATYPE(1)            [SET MAG TAPE BIT
  16 8CJ6          LDN   4  A1+3(2)             [POINTER TO FMAGH
  17 8D3Q          LDN   5  A1(1)               [POINTER TO FLOCNB   
  19 8DHB          MOVE  4  5                   [SET UP DETAILS FOR GETDIR   
   8 8F32          LDN   0  #7777
  14 8FGL          ANDX  0  A1+4(1)             [GEN. NO.
  18 8G2=          BNZ   0  PU41                [J IF NOT ZERO OTHERWISE 
  19 8GFW          LDCT  0  #100                [SET BIT INDICATING ZERO GEN.
   9 8G_G    PU41  STO   0  A1+4(1)  
  19 8HF6          LDX   0  A1+7(2)             [TRUNCATE GENERATION NUMBER  
   8 8HYQ          ANDN  0  #7777
   9 8JDB          STO   0  EGEN(3)  
  10 8JY2          NAMETOP  3,FILE,FRB   
   9 8KCL          LDX   3  ENUSE(3) 
   8 8KX=          LDN   5  1(3) 
  17 8LBW          BZE   3  PU52                [J IF NO TRAP RECORDS
   4 8LWG    PU51
  17 8MB6          READ                         [READ TRAP RECORDS   
   8 8MTQ          BCT   3  PU51 
   4 8N*B    PU52
   7 8NT2          LDX   3  5
   5 8P#L    PU51A   
  18 8PS=          DELETE                       [DELETE DIRECTORY ENTRY  
   8 8Q?W          BCT   3  PU51A
   7 8QRG          GETDIR   2
  12 8R?6          TESTREP  OK,PU53,NOFILE,PU53  
   9 8RQQ          GEOERR   1,LIBLOAD
   4 8S=B    PU53
  10 8SQ2          MFREE    FILE,FLOCNB  
   4 8T9L    PU54
   7 8TP=          LDX   3  5
   4 8W8W    PU55
  10 8WNG          HUNT2    2,FILE,FRB   
   8 8X86          BCT   3  PU55 
  10 8XMQ          NAMETOP  2,FILE,FWB   
   6 8Y7B          INSERT
  12 8YM2          TESTREP  OK,PU56,FNEARLY,PU56 
   9 8_6L          GEOERR   1,LIBLOAD
   4 8_L=    PU56
   9 925W          MFREE    FILE,FWB 
   8 92KG          BCT   5  PU54 
   8 9356          BRN      PU7  
  18 93JQ    PU6   LDN   1  3                   [TEST EQUALITY OF NAMES  
   7 944B    PU6A  SMO      1
   9 94J2          LDX   0  A1+3(2)  
   7 953L          SMO      1
   9 95H=          TXU   0  ELOC1(3) 
   8 962W          BCS      PU8  
   7 96GG          SBN   1  1
   8 9726          BPZ   1  PU6A 
  15 97FQ          LDX   0  A1+7(2)             [GENERATION  
  14 97_B          ANDN  0  #7777               [TRUNCATE
   9 98F2          TXU   0  EGEN(3)  
   8 98YL          BCS      PU8  
   8 99D=          BPZ   6  PU9  
   7 99XW          CLOSETOP  
   7 9=CG          LDN   6  0
   8 9=X6          BRN      PAD  
   4 9?BQ    PU9 
  10 9?WB          NAME     3,FILE,FWB   
   4 9#B2    PU7 
   7 9#TL    PU7A  CLOSETOP  
   7 9**=          LDN   6  0
   8 9*SW          BRN      POA  
  17 9B#G    PU8   BPZ   6  PT                  [REPORT QUERY STATUS 
   7 9BS6          CLOSETOP  
   8 9C?Q          BRN      PNQ  
  10 9CRB    #     AC       DUPLICATE TSN
   8 9D?2 ...PAC   LGEOG    3,5  
  10 9DQL ...      ACROSS   LIBLOADB,8   
  12 9KLW    #     AD       REMOVE QUERY STATUS  
   7 9L6G    PAD   READAGAIN 
  10 9LL6          MHUNT    3,FILE,FRB   
   9 9M5Q          LDX   0  TQU(1)   
   9 9MKB          ERS   0  A1+5(3)  
   9 9N52    #SKI  K6LIBLOAD>99-99   
  11 9NJL          SWITRACE A1+5(3),LBLDREMQ 
  10 9P4=          NAME     3,FILE,FWB   
   7 9PHW          REWRITE   
   9 9Q3G          MFREE    FILE,FWB 
   8 9QH6          BZE   6  POA  
   8 9R2Q          BRN      PS2  
   5 9RGB    TALDED  
   9 9S22          UNLOAD   ,,,UNL   
   7 9SFL          SUICIDE   
   5 9S_=    SERCHF  
  10 9TDW          HUNT     2,FLIB,FMAGH 
   7 9TFW ...      EXIT  1  0
   4 9WD6    #   
   7 9WXQ    #     GEOERRS   
   4 9XCB    #   
  11 9XX2    XBRKIN GEOERR  SHORTPM,XBRKIN   
  10 9YBL    XLIB  GEOERR   SHORTPM,XLIB 
   9 9YW=    XGDE  GEOERR   1,GETDIR 
   4 9_*W    #   
  12 9_TG          MENDAREA 1023-0?,K99LIBLOAD   
   4 =2*6    #END
   8 ____ ...25003065000200000000
