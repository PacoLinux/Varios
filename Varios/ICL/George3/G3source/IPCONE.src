  12 22FL          SEG      IPCONE,,JOHN BAILEY  
   4 22_=    #   
  11 23DW          SEGENTRY K1IPCONE,X1IPCONE
  11 23YG          SEGENTRY K2IPCONE,X2IPCONE
  11 24D6          SEGENTRY K3IPCONE,X3IPCONE
  11 24XQ          SEGENTRY K4IPCONE,X4IPCONE
  11 25CB          SEGENTRY K5IPCONE,X5IPCONE
  11 25X2          SEGENTRY K7IPCONE,X7IPCONE
  11 26BL          SEGENTRY K9IPCONE,X9IPCONE
   4 26W=    #   
   8 27*W    #     STORED WORDS  
   4 27CS ...#   
   8 27FQ ...      FSHCODE  AORB 
   4 27HN ...(   
   5 27KL ...SHFSLK  
  10 27MJ ...      PAIR     FSHTRAND,10  
   4 27PG ...)   
   4 27RD ...#   
   9 27TG    XOOX           #40000001
   9 28SQ    RESBITS        #777700  
  10 29S2    TIPBACT        +IPCA+IPCBX  
  18 2=?L    XERR           +4                  [MAX NR OF ERRORS ALLOWED
   9 2=R=    TMESSCODES     #04000040
   9 2?=W                   #04000041
   9 2?QG                   #04000042
   9 2#=6                   #04000043
   9 2#PQ                   #04000046
   9 2*9B                   #03000047
   9 2*P2                   #07000060
   9 2B8L                   #07000061
   9 2BN=                   #04000066
   9 2C7W                   #03000067
   9 2CMG                   #02000070
   9 2D76 ...               #04000071
   9 2DLQ                   #03000077
   4 2DMY ...#   
   4 2DP6 ...#   
  13 2DQ# ...# COME HERE AFTER TIMING OUT ANY PERI   
  13 2DRG ...# TIME OUT MUST MEAN OTHER SIDE IS DEAD 
  11 2DSN ...# THUS DO A RESET AND TRY AGAIN 
   5 2DTW ...XTIMEOUT
   7 2DX4 ...      RESETIPC  
   8 2DY= ...      BZE   0  NOPI 
   8 2D_D ...      CALL  7  SWRC 
   8 2F2L ...      BRN      RIP  
   8 2F3S ...      BRN      NOPI 
   8 2F52 ...      BRN      RIP  
  14 3CC6    #     S/R TO WAIT FOR RESET STATUS TO CLEAR 
  11 3CWQ    #     LINK IS CONTAINED IN X7   
  21 3DBB    #     EXITS+2 WHEN RESET CLEAR,EXITS+1 IF TIMED-OUT,EXITS IF INOPERABLE 
  19 3DW2    #     ON ENTRY : X1=FX1,X2=FX2=ADDRESS OF APPROPRIATE IPCA/IPCBX
   4 3F*L    #   
   8 3FT=    SWRC  SBX   7  FX1  
   9 3G#W          LDX   0  CLEANCT  
   9 3GSG          ADN   0  5000/K9  
  15 3H#6          STO   0  IPCPTIME(2)         [TIME LIMIT  
  19 3HRQ          SIPCM    RW                  [^WAITING FOR RESET CLEAR^   
   9 3J?B    SW1   LDX   0  XOOX(1)  
  16 3JR2          STO   0  CHAINADD(2)         [READY FOR EVENT 
  16 3K=L          JDISENG  ,SW2                [GONE INOPERABLE 
  16 3KQ=          JIPCM    TO,SW3              [WAITING TOO LONG
  18 3L9W          JRESET   ,SW4                [RESET STATUS STILL SET  
   8 3LPG    SW6   CIPCM    RW   
  10 3M96          STOZ     IPCPTIME(2)  
   7 3MNQ          NGNC  0  1
  10 3N8B          ANDS  0  CHAINADD(2)  
  10 3NN2          LDX   0  CHAINADD(2)  
   7 3P7L          SBN   0  1
   8 3PM=          BZE   0  SW5  
   6 3Q6W          COOR1 
   8 3QLG    SW5   ADX   7  FX1  
   7 3R66          EXIT  7  2
   6 3RKQ    SW4   COOR1 
   8 3S5B          BRN      SW1  
   8 3SK2    SW3   CIPCM    TO   
   7 3T4L          SBN   7  1
   7 3TJ=    SW2   SBN   7  1
   8 3W3W          BRN      SW6  
  14 5F#G    #     DEAL WITH ERROR IN MESSAGE SEQUENCE : 
  11 5FS6    #     WAIT FOR RESET TO CLEAR   
   4 5G?Q    #   
   5 5GRB    X1IPCONE
   5 5H?2    WHOOPS  
  11 5HQL          TRACE    IPWRB(2),IPCONE1 
   7 5J==          RESETIPC  
  17 5JPW          BPZ   0  NOPS                [REJECTED(INOPERABLE)
  15 5K9G          DFLOAD   ,ERCT,5             [ERROR COUNT 
   7 5KP6          ADN   5  1
   9 5L8Q          TXL   5  XERR(1)  
  16 5LNB          BCC      RIPE                [TOO MANY ERRORS 
  18 5M82          DFSET    ,ERCT,5             [STORE INCREMENTED COUNT 
  18 5MML          CALL  7  SWRC                [WAIT FOR RESET TO CLEAR 
  18 5N7=          BRN      RIPE                [WAIT BEYOND TIME LIMIT  
  15 5NLW          BRN      NOPS                [INOPERABLE  
   9 5P6G    #     SEND ERROR MESSAGE
   4 5PL6    WH3 
  20 5Q5Q          LDN   5  0                   [NO 'RESET ACCEPTED' MESSAGE --  
  15 5QKB    WH5   LDX   0  TMESSCODES+12(1)    [RECEIVED YET
  14 5R52          STO   0  IPWRB(2)            [HEADER  
  16 5RJL          STOZ     IPWRB+1(2)          [NULL DIAGNOSTIC 
  17 5S4=          NGS   0  IPWRB+2(2)          [NEGATIVE CHECKSUM   
   8 5SHW          LDN   0  #11  
  10 5T3G          DCA      ,DLA,0,MODE  
   7 5TH6          LDN   0  3
  10 5W2Q          DCA      ,DLA,0,COUNT 
   9 5WGB          LDN   0  IPWRB(2) 
  10 5X22          DCA      ,STO,0,CTSA  
   8 5XFL          GPERIIPC ,2,CT
  17 5X_= ...      TIPCREP  CT,XTIMEOUT,RIPF,NOPS,WH1   [AWAIT REPLY 
   9 5YDW          BNZ   5  NEXTASK  
   7 5YYG          SIPCM    R
   9 5_D6          BRN      NEXTASK  
  15 5_XQ    #     DEAL WITH POSSIBLE RESET ACCEPTED MESSAGE 
   8 62CB    WH1   LDN   0  #12  
  10 62X2          DSCA     ,DLA,0,MODE  
   8 63BL          LDN   4  11   
  10 63W=          DSCA     ,DLA,4,COUNT 
   9 64*W          LDN   0  IPREB(2) 
  10 64TG          DSCA     ,STO,0,CTSA  
  13 65*6          GPERIIPC ,2,SP               [READ
  14 65SQ ...      TIPCREP  SP,XTIMEOUT,RIPF,NOPS,,RIPE  
   9 66#B          LDX   0  CPRW2(2) 
   8 66S2          ANDN  0  #777 
   7 67?L          SBX   4  0
   9 67R=          LDN   7  IPREB(2) 
   7 68=W          SMO      4
  17 68QG          SUM   6  0                   [CALCULATE CHECKSUM  
  14 69=6          BNZ   6  RIPE                [FAILED  
   9 69PQ          LDX   0  IPREB(2) 
   8 6=9B          ANDN  0  #77  
   8 6=P2          SBN   0  #70  
  16 6?8L          BZE   0  WH6                 [RESET ACCEPTED  
   7 6?N=          SBN   0  1
  15 6#7W          BZE   0  PINIT               [INITIALISE  
   7 6#MG          SBN   0  6
  14 6*76          BZE   0  RESA                [ERROR   
  16 6*LQ          BRN      RIPE                [ANYTHING ELSE   
  17 6B6B    WH6   LDN   5  1                   [NOTE RESET ACCEPTED 
  18 6BL2          BRN      WH5                 [GO TO SEND ERROR MESSAGE
  14 6C5L    #     DEAL WITH RESET SEQUENCES INITIATED   
   9 6CK=    #     FROM THE OTHER END
   4 6D4W    #   
   5 6DJG    X3IPCONE
   5 6F46    RESETA  
  11 6FHQ          TRACE    IPWRB(2),IPCONE2 
  18 6G3B          CALL  7  SWRC                [WAIT FOR RESET TO CLEAR 
  18 6GH2          BRN      RIPE                [WAIT BEYOND TIME LIMIT  
  15 6H2L          BRN      NOPS                [INOPERABLE  
  16 6HG=    #     RESET IS NOW CLEAR : A'RESET ACCEPTED' MESSAGE
  16 6H_W    #     MUST BE SENT TO INTERRUPT THE REMOTE PROCESSOR
  11 6JFG          LDX   0  TMESSCODES+10(1) 
  14 6J_6          STO   0  IPWRB(2)            [HEADER  
  14 6KDQ          NGS   0  IPWRB+1(2)          [CHECKSUM
   8 6KYB          LDN   0  #11  
  10 6LD2          DCA      ,DLA,0,MODE  
   7 6LXL          LDN   0  2
  10 6MC=          DCA      ,DLA,0,COUNT 
   9 6MWW          LDN   0  IPWRB(2) 
  10 6NBG          DCA      ,STO,0,CTSA  
  19 6NW6          GPERIIPC ,2,CT               [SEND RESET ACCEPTED MESSAGE 
  14 6P*Q ...      TIPCREP  CT,XTIMEOUT,RIPF,NOPS,RESA2  
   8 6PTB    RESA2 LDN   0  #12  
  10 6Q*2          DSCA     ,DLA,0,MODE  
   8 6QSL          LDN   4  11   
  10 6R#=          DSCA     ,DLA,4,COUNT 
   9 6RRW          LDN   0  IPREB(2) 
  10 6S?G          DSCA     ,STO,0,CTSA  
  15 6SR6          GPERIIPC ,2,SP               [READ ERROR  
  14 6T=Q ...      TIPCREP  SP,XTIMEOUT,RIPF,NOPS,,RIP   
   9 6TQB          LDX   0  CPRW2(2) 
   8 6W=2          ANDN  0  #777 
  18 6WPL          SBX   4  0                   [NR OF WDS TRANSFERRED   
   9 6X9=          LDN   7  IPREB(2) 
   7 6XNW          SMO      4
  17 6Y8G          SUM   6  0                   [CALCULATE CHECKSUM  
  18 6YN6          BNZ   6  RIP                 [IF CHECKSUM FAIL,GIVE UP
  10 6_7Q    #     FURTHER  INPUT CHECKS 
   9 6_MB          LDCH  0  IPREB(2) 
  15 7272          BXU   0  4,RIPE              [COUNT WRONG 
   9 72LL    #     TEST MESSAGE TYPE 
   9 736=          LDX   0  IPREB(2) 
   8 73KW          ANDN  0  #77  
   8 745G          SBN   0  #71  
  15 74K6          BZE   0  PINIT               [INITIALISE  
   7 754Q          SBN   0  6
   8 75JB          BNZ   0  RIPE 
   9 7642    RESA  LDX   0  IPREB(2) 
   8 76HL          ANDN  0  #1000
  17 773=          BZE   0  NEXTASK             [NOT GEORGE BROKEN   
  20 773K ...      FSHCODE  A,SNOTA             [ FOR SHFS M/C A MUST CHECK THE  
  19 773Y ...(                                  [ MESSAGE FROM PM IN M/C B   
  10 774? ...      LDX   6  IPREB+1(2)   
  21 774L ...      BZE   6  SNOTA2              [ J. IF THE NORMAL GEORGE DOWN MESS. 
  19 774M ...      NGN   0  2                   [CODE -2 MEANS MAJOR ERROR   
   7 774N ...      TXU   0  6
  15 774P ...      BCS      NOD1              [PMMBRK MACRO  
  19 774Q ...      '142   0  0                 [CODE -2 MEANS EXEC PM WANTED 
   9 774R ...      BRN      NODUMP   
  18 774S ...NOD1  NGN   0  3                  [CODE -3 MEANS GEORGE PM  
   9 774T ...      BCS      NODUMP   
  17 774W ...      GEOERR 1,IPCBDOWN            [STOP FORCED BY SLAVE
   5 774X ...NODUMP  
  21 774_ ...      JBS      SNOTA2,,FSHCLOSDOWN [ OR IF ALREADY INITIATED UNANT.CLOSE
  19 775# ...      BC       ,FSHRUNNING         [ ELSE CLEAR LINK RUNNING BIT
  20 775M ...      BS       ,FSHCLOSDOWN        [ AND SET CLOSEDOWN STARTED BIT  
  21 775Q ...      MBC      ,JSVIRTMOPB,JSVIRTBACKB [CLEAR 'B' M/C VIRTUAL JOB SLOTS 
  10 775T ...      BS       ,JSPREFERA   
  21 7762 ...      FINDACTF 2,FX2,IPCA,FSHBPM,NOBPM,BPTR [ SEE IF ALREADY GOT /FSHBPM
  17 776* ...      BRN      SGOTBPM             [ J. IF WE HAVE ELSE 
   5 776N ...NOBPM   
  20 7773 ...      GETACT   IPCA,FSHBPM         [ SET UP ACT TO CONTROL CLOSEDOWN
   4 7775 ...#   
  20 7777 ...#     IF THERE ARE TWO IPB'S, WE MAY BE INTERFERING WITH THE DATA   
  20 7779 ...#     STRUCTURE FOR THE IPB ACTIVITIES BY CREATING THE /FSHBPM, SO  
  21 777? ...#     WHEN IT IS SET UP, WE MOVE IT BEHIND BLANKET (OUT OF THE WAY!!)   
   4 777* ...#   
  17 777C ...      LDX   1  BPTR(2)             [ADDRESS OF /FSHBPM  
  17 777F ...      LDN   2  BACT                [ADDRESS OF BLANKET  
  17 777H ...      RINGACT  1,2                 [RE-SITE THE /FSHBPM 
  19 777K ...      LDX   2  BACT+BPTR           [RESTORE POINTER TO /FSHBPM  
   4 777M ...#   
  20 777P ...      LINKSET  2,SHFSLK(1)         [ SET INITIAL LINK FSHTRAND,10   
  19 7784 ...      BS       2,FSHBPMACTWT       [ MARK TO WAIT FOR OP. ACTION
  21 778C ...      STO   6  ACOMMUNE1(2)        [ GIVE IT THE ACT/JOB NO OF CURRENT  
  21 778Q ...      FPUT                         [ IN B AT BREAK AND START UP THE ACT 
   9 7795 ...      BRN      SWAITGB2 
  19 779D ...SGOTBPM                            [ ALREADY GOT /FSHBPM ACT SO 
  21 779R ...      JBS      NOBPM,2,FSHBPMKILL  [ FIRST CHECK IF ITS ABOUT TO SUICIDE
  21 77=6 ...      STO   6  ACOMMUNE1(2)        [ J. IF IT IS ELSE STORE ACT/JOB NO &
  21 77=F ...      BC       2,FSHBPMLKCOM       [ CLEAR LK COMMAND MARKER & CARRY ON 
   5 77=S ...SWAITGB2
   4 77?7 ...#   
  19 77?G ...#     AT THIS POINT WE NEED TO SET UP A QUIESCENT READ AND WAIT 
  15 77?N ...#     FOR THE SECOND MESSAGE FROM PM IN 'B'.#   
   4 77?W ...#   
  15 77#4 ...      SIPCM    Q                [SET QUIESCENT  
   8 77#= ...      LDN   0  #12  
  15 77#D ...      DSCA     ,DLA,0,MODE          [READ MODE  
   8 77#L ...      LDN   4  11   
  15 77#S ...      DSCA     ,DLA,4,COUNT          [11 WORDS  
   9 77*2 ...      LDN   0  IPREB(2) 
  10 77*8 ...      DSCA     ,STO,0,CTSA  
   8 77*B ...      GPERIIPC ,2,SP
  14 77*J ...      TIPCREP  SPQ,TOPM,RESETA,TOPM,,TOPM   
   4 77*X ...#   
   4 77B= ...TOPM
   8 77BK ...      BRN      RIPF 
  20 77BY ...SNOTA2                             [ SECOND MESSAGE FROM PM IN 'B'  
  20 77C? ...      FINDACTF 2,FX2,IPCA,FSHBPM,PMGONE,BPTR  [ FIND THE /FSHBPM AND
  21 77CL ...      BC       2,FSHBPMACTWT       [ CLEAR 'WT FOR OP. ACTION' MARKER   
   5 77CR ...PMGONE  
   8 77C_ ...      LDX   2  FX2  
   5 77D# ...SNOTA   
   4 77DM ...)   
  21 77F2 ...      FSHCODE  B,SNOTB             [ FOR SHFS M/C B MUST BREAK AS SOON  
  20 77F* ...(                                  [ IT DETECTS PM ENTERED ON M/C A 
  16 77FN ...      GEOERR   1,ZOMBIE            [ SO GEOERR !!!  
   5 77G3 ...SNOTB   
   4 77GB ...)   
   8 77GW          SIPCM    GB   
   8 782G          DFCLEAR  ,RGM 
   9 78G6          DFCLEAR  ,IDENT1  
   9 78_Q          DFCLEAR  ,IDENT2  
   8 79FB          BRN      RIP  
   9 79_2          BRN      NEXTASK  
  14 7=DL    #     DEAL WITH INITIALISE MESSAGE RECEIVED 
  16 7=Y= ...PINIT DOWN     IPCONF,1         [DOWN TO CLEAR UP   
   5 7?CW    X9IPCONE
   9 7?XG          LDX   0  IPREB(2) 
   8 7#C6          ANDN  0  #1000
  15 7#WQ          BZE   0  PI2                 [NOT GEORGE  
  17 7*BB          DFSET    ,RGM                [NOTE REMOTE GEORGE  
  10 7*W2          LDX   0  IPREB+1(2)   
  20 7B*L          DFSET    ,IDENT1,0           [STORE IDENTITY OF REMOTE GEORGE 
  10 7BT=          LDX   0  IPREB+2(2)   
   9 7C#W          DFSET    ,IDENT2,0
  14 7CSG    PI2   LDX   0  TMESSCODES+11(1)    [HEADER  
   8 7D#6          ORN   0  #1000
   9 7DRQ          STO   0  IPWRB(2) 
  14 7F?B          LDX   0  IDENTITY            [IDENTITY
  10 7FR2          STO   0  IPWRB+1(2)   
  10 7G=L          LDX   0  IDENTITY+1   
  10 7GQ=          STO   0  IPWRB+2(2)   
   9 7H9W          LDN   7  IPWRB(2) 
   7 7HPG          SUM   6  3
  14 7J96          NGS   6  IPWRB+3(2)          [CHECKSUM
   7 7JNQ          LDN   0  4
  10 7K8B          DCA      ,DLA,0,COUNT 
   8 7KN2          LDN   0  #11  
  10 7L7L          DCA      ,DLA,0,MODE  
   9 7LM=          LDN   0  IPWRB(2) 
  10 7M6W          DCA      ,STO,0,CTSA  
  18 7MLG          GPERIIPC ,2,CT               [SEND INITIALISE RESPONSE
  14 7MW# ...      TIPCREP  CT,XTIMEOUT,RIPF,NOPI,RIPE   
  18 7N66          JIPCM    DS,PI1              [J IF DIALOGUE SUSPENDED 
   9 7P5B          BRN      NEXTASK  
   9 7PK2    PI1   JIPCMN   SM,PI3   
   8 7Q4L          LGEOG    ,4   
  10 7QJ=          OUTPACK  4,1,GEOPER   
  20 7R3W          MONOUT   IPCDR               [OUTPUT DIALOGUE RESUMED MESSAGE 
   8 7RHG          CIPCM    SM   
  19 7S36    PI3   CIPCM    DS                  [CLEAR'DIALOGUE SUSPENDED'   
  13 7SGQ          CIPCM    GB                  [ETC 
   8 7T2B          CIPCM    ER   
   8 7TG2          CIPCM    AO   
   9 7T_L          BRN      NEXTASK  
  10 7WF=    #     TIMED-OUT ENTRY POINT 
   4 7WYW    #   
   5 7XDG    X5IPCONE
  11 7XY6          TRACE    IPWRB(2),IPCONE3 
   8 7YCQ          CIPCM    TO   
  10 7YXB          STOZ     IPCPTIME(2)  
   9 7_C2          JENG     ,WHOOPS  
   8 7_WL          BRN      NOPS 
  10 82B=    #     MAJOR DISASTER SECTION
   4 82TW    #   
  16 83*G    #              ENTRY POINT FOR SUSPENDED DIALOGUE   
   4 83T6    #   
   7 84#Q    RIPF  RESETIPC  
   8 84SB          BZE   0  NOPI 
   8 85#2          CALL  7  SWRC 
   8 85RL          BRN      RIP  
   8 86?=          BRN      NOPI 
   8 86QW          BRN      RIP  
   8 87=G    RIPE  SIPCM    ER   
   5 87Q6    X7IPCONE
  20 889Q    RIP   SIPCM    DS                  [SET 'DIALOGUE SUSPENDED' MARKER 
  11 88PB          TRACE    IPWRB(2),IPCONE4 
  17 8992          DFCLEAR  ,ERCT               [CLEAR ERROR COUNT   
   8 89NL          SIPCM    SM   
  16 8=8= ...      DOWN     IPCONF,1         [DOWN TO CLEAR UP   
   8 8=MW          LGEOG    ,4   
  10 8?7G          OUTPACK  4,1,GEOPER   
   8 8?M6          LGPERIS  ,4   
  10 8#6Q          OUTPACK  4,1,TRANSCT  
   8 8#LB          LREPEATS ,4   
   8 8*62          BZE   4  PIX  
  10 8*KL          OUTPACK  4,1,REPEATS  
   9 8B5=          CLCOUNT  ,REPEAT  
   8 8BJW    PIX   LFAILS   ,4   
   8 8C4G          BZE   4  PIY  
  10 8CJ6          OUTPACK  4,1,FAILCT   
   8 8D3Q          CLCOUNT  ,FAIL
  21 8DHB    PIY   MONOUT   IPCDS               [OUTPUT DIALOGUE SUSPENDED MESSAGE   
   8 8F32          JRESET   ,RI2 
   8 8FGL          BRN      RI1  
  10 8G2=    RI2   ACROSS   IPCSTART,7   
  18 8GFW    #              ENTRY POINT FOR WAITING FOR THE OTHER END TO 
  10 8G_G    #              INITIALISE.  
  19 8HF6    #              A READ IS PUT UP TO DETECT A RESET,OR READ THE   
   9 8HYQ    #              MESSAGE  
  14 8JDB    #              'DS' SHOULD BE SET ON ENTRY  
   4 8JY2    #   
   5 8KCL    X4IPCONE
  11 8KX=          TRACE    IPWRB(2),IPCONE5 
   8 8LBW    RI1   LDN   0  #12  
  10 8LWG          DSCA     ,DLA,0,MODE  
   8 8MB6          LDN   4  11   
  10 8MTQ          DSCA     ,DLA,4,COUNT 
   9 8N*B          LDN   0  IPREB(2) 
  10 8NT2          DSCA     ,STO,0,CTSA  
  16 8P#L          SIPCM    Q                   [SET QUIESCENT   
   8 8PS=          GPERIIPC ,2,SP
  12 8Q?W ...      TIPCREP  SPQ,RI1,RI2,NOP,,RI1 
   8 8QRG          LDN   0  #77  
   9 8R?6          ANDX  0  IPREB(2) 
   8 8RQQ          SBN   0  #71  
  16 8S=B          BNZ   0  RI1                 [NOT INITIALISE  
   9 8SQ2          LDX   0  CPRW2(2) 
   8 8T9L          ANDN  0  #777 
   7 8TP=          SBX   4  0
   9 8W8W          LDN   7  IPREB(2) 
   7 8WNG          SMO      4
  17 8X86          SUM   6  0                   [CALCULATE CHECKSUM  
  18 8XMQ          BNZ   6  RI1                 [WRONG,SO IGNORE MESSAGE 
   9 8Y7B          BRN      X9IPCONE 
  19 8YM2    #     SET APPROPRIATE MARKER BEFORE DEALING WITH IPB INOPERABLE 
   8 8_6L    NOPI  SIPCM    DS   
   8 8_L=          BRN      NOP  
   8 925W    NOPS  SIPCM    SI   
  11 92KG    #     DEAL WITH IPB INOPERABLE  
   4 9356    #   
   5 93JQ    X2IPCONE
   4 944B    NOP 
   9 94J2          ACROSS   IPCOND,1 
   5 953L    NEXTASK 
   9 95H=          ACROSS   IPCON,1  
  11 962W          MENDAREA 1023-0?,K99IPCONE
   4 96GG    #END
   8 ____ ...37134674000300000000
