  12 22FL ...      SEG   CDCIPB,,PHILIP TURNER   
  10 22_= ...#LIS K0CDCIPB>K0IPB>K0ALLGEO
  10 23DW ...               8HCDCIPB70   
   4 23YG ...#   
   4 24D6 ...#   
  15 24XQ ...# THIS SEGMENT CLOSES DOWN THE CONCEPTUAL IPB   
   4 25CB ...#   
  10 25X2 ...      SEGENTRY K1CDCIPB,TIDY
  11 26BL ...      SEGENTRY K2CDCIPB,X2CDCIPB
  11 26W= ...      SEGENTRY K3CDCIPB,X3CDCIPB
   5 27*W ...PAIRC3  
   9 27TG ...      LINK     CDCIPB,3 
   9 28*6 ...TDGE  GEOERR   1,BRIN   
   9 28SQ ...PAIR  LINK     IPBSCAN,5
   8 28X* ...      FSHCODE  AORB 
   4 28_Y ...(   
  10 294H ...PAIRB LINK     IPBSCANB,5   
   4 2976 ...)   
   4 299P ...[   
   9 29#B ...SCSS           #40000000
   9 29S2 ...               #01110000
  17 2=3S ...ZCLBT          #56757777           [TO CLEAR BITS 1,5,10
   4 2=?L ...#   
  13 2=R= ...# THIS ROUTINE FINDS THE REQUIRED BLOCKS
   4 2?=W ...#   
  17 2?QG ...RHRC  LDX   3  0(3)                [ADDRESS OF CONDCB   
   9 2#=6 ...      ADX   3  BSTART(3)
  17 2#PQ ...      STO   3  ACOMMUNE4(2)        [ADDRESS OF CONDCB   
   9 2*9B ...      HUNTACT  AMOP,3   
  17 2*P2 ...      STO   3  ACOMMUNE2(2)        [ADDRESS OF IPBCCA   
   7 2B8L ...      EXIT  5  0
   4 2BN= ...#   
   7 2C7W ...TDMR  LDX   2  5
   8 2CMG ...      SBX   7  FX1  
   8 2D76 ...      BRN      TENT 
  10 2DLQ ...TDM   LDX   0  BACKCHAN(2)  
   8 2F6B ...      BZE   0  TDMD 
   8 2FL2 ...      COOR3    #62  
   7 2G5L ...      LDX   2  5
   3 2GK= ...
   9 2H4W ...TENT  LDEX  4  ATYPE(2) 
   8 2HJG ...      BZE   4  TDM  
   7 2J46 ...      UNQUEUE   
   7 2J83 ...TDMD  LDX   2  5
  11 2J?Y ...      HUNT2    2,AMXOR,CSTATE   
  15 2JCT ...      LDX   3  A1+BPTR(2)      [RING TO CONDCB  
  14 2JHQ ...      BZE   3  TDMX            [JUMP NO RING
  16 2JMM ...      STOZ     BPTR(3)         [CLEAR CONDCB RING   
  16 2JRJ ...      STOZ     A1+BPTR(2)      [CLEAR CSTATE RING   
  15 2JXF ...TDMX  MURDER   5               [KILL OFF OLPA   
   8 2K3B ...      ADX   7  FX1  
   7 2KH2 ...      EXIT  7  0
   4 2L2L ...#   
   8 2LG= ...SUCB  SBX   7  FX1  
  11 2L_W ...      SETNCORE 3,1,AMXOR,IOUT   
   8 2MFG ...      ADX   7  FX1  
   7 2M_6 ...      EXIT  7  0
   4 2NDQ ...#   
  10 2NYB ...RFND  MHUNTW   2,IDF,CONCEP 
   7 2PD2 ...      EXIT  1  0
   4 2PXL ...#   
  13 2QC= ...SUCS1 LDX   3  AWORK1(2)           [OLPA
  11 2QWW ...      HUNT2    3,AMXOR,CSTATE   
   8 2RBG ...      LDX   2  FX2  
   8 2RW6 ...      ADN   3  A1   
  10 2S*Q ...      STO   3  ACOMMUNE1(2) 
   7 2STB ...      EXIT  6  0
   4 2T*2 ...#   
  16 2TSL ...# THIS ROUTINE CHAINS A DATA BLOCK BEHIND THE CONDCB
   4 2W#= ...#   
   8 2WRW ...SCDB1 CALL  6  SUCS1
   8 2X?G ...      CALL  5  RHRC 
   8 2XR6 ...      CHAIN    1,3  
   3 2Y=Q ...
   7 2YQB ...      EXIT  7  0
   4 2_=2 ...#   
  15 2_PL ...TIDY  STO   3  AWORK1(2)           [OLPA ADDRESS
  16 329= ...      STOZ     AWORK2(2)           [CLEAR MARKERS   
   4 32=M ...#   
  20 32#4 ...#     THE CORRECTION TO BUG 8179 MEANS THAT WE CAN NO LONGER RELY   
  17 32*F ...#     ON THE DEVICE NOT BEING BUSY AT RELEASE TIME!!!!  
   4 32BW ...#   
  21 32D? ...#     THEREFORE WE HAVE TO WAIT FOR OUTSTANDING TRANSFERS TO COMPLETE   
   4 32FN ...#   
  19 32H5 ...XBUSY CONCISBY 3,TDHT              [JIF NO TRANSFERS IN PROGRESS
  19 32JG ...      COOR3    #41                 [ELSE WAIT A WHILE AND THEN  
  17 32KX ...      BRN      XBUSY               [GO TO CHECK AGAIN   
   4 32M# ...#   
   8 32NW ...TDHT  CALL  6  SUCS1
   8 338G ...      CALL  5  RHRC 
   8 33?5 ...      LDCT  5  #200 
  14 33*N ...      SMO      ACOMMUNE1(2)        [CSTATE  
   9 33D? ...      ORS   5  IPSTATUS 
  19 33GW ...[                                   SET CPA CLOSING DOWN TO ... 
  19 33KF ...[                                   ... PREVENT FURTHER ATTACHES
   6 33N6 ...#UNS  CLSKI 
   4 347Q ...(   
   9 34MB ...      LDXC  0  CONREL(3)
  17 3572 ...      BCS      TDSR                [J IF SET RELEASING  
   8 35LL ...      LDCT  0  #400 
  15 366= ...      ORS   0  CONREL(3)           [TELL IPBCCA 
  13 36KW ...      BRN      TDCR                [WAIT
   7 375G ...TDSR  ADXC  0  0
  17 37K6 ...      BCS      TDBC                [J IF IPBCCA READY   
   4 384Q ...)   
   8 38JB ...TDCR  COOR3    #62  
   8 3942 ...      BRN      TDHT 
  14 39HL ...TDBC  LDX   3  ACOMMUNE4(2)        [CONDCB  
  18 3=3= ...      HUNTW    1,IDF,CONCEP        [IS THERE AN IDF BLOCK   
  14 3=GW ...      BPZ   1  TDCS                [-> YES  
  18 3?2G ...      LDX   7  ARGNO(3)            [NO. OF CONDCB ENTRIES   
   7 3?G6 ...      SBN   7  1
   8 3?_Q ...      ANDN  7  #7777
   7 3#FB ...      SLL   7  1
  11 3#_2 ...      SETUPCORE 7,1,IDF,CONCEP  
   8 3*DL ...      CALL  6  SUCS1
   8 3*Y= ...      CALL  5  RHRC 
   9 3BCW ...      STOZ     AWORK3(2)
  14 3BXG ...TDCS  LDX   3  ACOMMUNE4(2)        [CONDCB  
   8 3CC6 ...      LDN   6  A1(1)
  19 3CWQ ...      LDX   2  6                   [POINT TO START OF BLOCK +A1 
   8 3DBB ...      SMO      FX2  
   9 3DW2 ...      ADX   6  AWORK3   
  10 3F*L ...      LDX   5  ALOGLEN(3)   
  16 3FT= ...      LDN   3  A1(3)               [START OF ENTRIES
  16 3G#W ...TDSU  SBN   5  CONDCBLEN           [ANY ENTRIES LEFT
   3 3GSG ...
  14 3H#6 ...      BZE   5  TDCL                [-> NO   
   9 3HRQ ...      ADN   3  CONDCBLEN
  21 3J?B ...      LDX   1  BPTR(3)             [POINT TO ADEVS ENTRY IF NOT EMPTY   
  16 3JR2 ...      BZE   1  TDSU                [-> IF NULL ENTRY
   6 3K=L ...#UNS  CLSKI 
   4 3KQ= ...(   
   8 3L9W ...      LDN   0  #400 
  10 3LPG ...      ANDX  0  IDSTATUS(3)  
  20 3M96 ...      BNZ   0  TDSU                [J IF IPBCCA DEALING WITH THIS   
   4 3MNQ ...)   
  18 3MRW ...      SEGENTRY K91CDCIPB         [MENDED BY RETAINAH MACRO  
   9 3MX2 ...TDCOM LDN   0  AHRETREL 
  18 3N26 ...      BZE   0  NRETAH              [J IF AH NOT BE RETAINED 
   8 3N5= ...      LDCT  0  #10  
   9 3N8B ...      BRN      TCLEAR   
   5 3N?G ...NRETAH  
   8 3NBL ...      LDCT  0  #210 
  21 3NFQ ...[                                   UNSET AH AS WELL IF NOT RETAINING   
   5 3NJW ...TCLEAR  
  17 3NN2 ...      ERS   0  FOURTHWD(1)         [CLEAR ONLINE MARKER 
  17 3P7L ...      STOZ     BPTR(1)             [CLEAR ADEVS ENTRY   
  17 3PM= ...      STOZ     BPTR(3)             [CLEAR CONDCB ENTRY  
   8 3PR7 ...      LDN   0  #1000
  17 3PX4 ...      ANDX  0  IDSTATUS(3)       [X3 - ENTRY IN CONDCB  
  18 3Q2_ ...      BNZ   0  TACT                [J. IF IDENTIFIER INOP   
  16 3Q6W ...      LDX   0  IDADIN(3)           [ADDRESS OF AMOP 
   7 3QLG ...      SMO      6
  19 3R66 ...      STO   0  0                   [STORE IN IDF/CONCEP1 BLOCK  
  17 3RKQ ...      LDX   0  IDNUM(3)            [ACTUAL IDENTIFIER   
   7 3S5B ...      SMO      6
  19 3SK2 ...      STO   0  1                   [STORE IN IDF/CONCEP1 BLOCK  
   7 3T4L ...TDUD  ADN   6  2
  16 3TJ= ...      BRN      TDSU                [GET NEXT ENTRY  
  16 3W3W ...TDCL  SBX   6  2                   [LENGTH OF BLOCK 
   8 3WHG ...      SBN   2  A1   
   8 3X36 ...      BZE   6  TDFC 
   9 3XGQ ...      ALTLENG  2,6,RFND 
   8 3Y2B ...      CALL  6  SUCS1
   8 3YG2 ...TDRT  LDX   7  GSIGN
   9 3Y_L ...      ERS   7  AWORK2(2)
  14 3_F= ...      LDX   3  ACOMMUNE1(2)        [CSTATE  
   8 3_YW ...      SBN   3  A1   
  10 42DG ...      LDX   5  ALOGLEN(3)   
   8 42Y6 ...      ADN   3  A1   
   9 43CQ ...TDCE  ADN   3  CSTATELEN
   9 43XB ...      SBN   5  CSTATELEN
  14 44C2 ...      BZE   5  TDRF                [-> NO   
  18 44WL ...      LDX   1  BPTR(3)             [POINT TO CROUTE ENTRY   
   9 45B= ...      LDX   6  AWORK2(2)
  17 45TW ...      SLL   6  1                   [FIRST TIME THROUGH  
  14 46*G ...      BPZ   6  TDIY                [-> YES  
   7 46T6 ...TDBG  LDX   2  1
   9 47#Q ...      ADX   2  BSTART(1)
   9 47SB ...      HUNTACT  AMOP,2   
   7 48#2 ...      LDCT  0  4
   3 48RL ...
  19 49?= ...      ANDX  0  IPBSW(2)            [ONLINE USING CROUTE BLOCK   
  14 49QW ...      BZE   0  TDAM                [-> NO   
   8 4==G ...      SMO      FX2  
   9 4=Q6 ...      SBX   3  ACOMMUNE1
   8 4?9Q ...      COOR3    #62  
   7 4?PB ...      LDX   7  3
  17 4#92 ...      CALL  6  SUCS1               [FIND CSTATE BLOCK   
  16 4#NL ...      ADX   3  7                   [ADD DISPLACEMENT
  18 4*8= ...      LDX   1  BPTR(3)             [POINT TO CROUTE ENTRY   
   8 4*MW ...      BRN      TDBG 
   9 4B7G ...TDAM  LDX   6  BSTART(1)
  20 4BM6 ...      ADX   6  1                   [POINT TO START OF CROUTE BLOCK  
   7 4C6Q ...      SMO      6
   9 4CLB ...      LDX   7  ALOGLEN  
  16 4D62 ...      SBN   7  CROUTELEN           [ ONLY ONE ENTRY 
  14 4DKL ...      BZE   7  TDFB                [-> YES  
  16 4F5= ...      SUBRELT  1                   [REMOVE ELEMENT  
   3 4FJW ...
   8 4G4G ...      BRN      TDCE 
   8 4GBX ...TDFB  SUBRELT  1,1  
   7 4GP# ...      FREECORE 6
   8 4H3Q ...      BRN      TDCE 
   7 4HHB ...TDFC  FREECORE 2
   8 4J32 ...      BRN      TDRT 
  10 4JGL ...TDIY  SBX   3  ACOMMUNE1(2) 
  14 4K2= ...      STO   3  AWORK3(2)           [SAVE X3 
  14 4KFW ...      STO   5  AWORK4(2)           [SAVE X5 
   8 4K_G ...      CALL  7  SUCB 
  10 4LF6 ...      NAME     1,IDF,CONNAME
   8 4LYQ ...      CALL  6  SUCS1
   8 4MDB ...      CALL  5  RHRC 
  15 4MY2 ...      LDX   3  AWORK3(2)           [RESTORE X3  
  10 4NCL ...      ADX   3  ACOMMUNE1(2) 
  17 4NX= ...      LDN   7  A1(1)               [START OF DATA AREA  
   9 4PBW ...      LDX   1  BPTR(3)  
   9 4PWG ...      LDN   6  CONNM(1) 
  17 4QB6 ...      MOVE  6  3                   [MOVE TO NEW BLOCK   
  15 4QTQ ...      LDX   5  AWORK4(2)           [RESTORE X5  
   8 4R*B ...      LDCT  6  #200 
  19 4RT2 ...      ORS   6  AWORK2(2)           [SET NOT FIRST TIME THROUGH  
   8 4S#L ...      BRN      TDBG 
   7 4SS= ...#UNS  IDFMARK8  
  18 4SXP ...      SEGENTRY K92CDCIPB         [MENDED BY RETAINAH MACRO  
   9 4T38 ...TDRF  LDN   0  AHRETREL 
  19 4T6M ...      BNZ   0  TLEAVEAH           [J IF ATTACHES TO BE RETAINED 
  15 4T=6 ...      IDFOFF   (GEOERR),TDGE,IDF,CONNAME,A1,1   
   9 4T*K ...      BRN      TDRELED  
   5 4TF4 ...TLEAVEAH
  14 4TJH ...      IDFOFF   (GEOERR),TDGE,IDF,CONNAME,A1 
   5 4TN2 ...TDRELED 
   3 4TRG ...
  10 4W?6 ...      MFREEW   IDF,CONNAME  
   8 4WQQ ...      CALL  6  SUCS1
   8 4X=B ...      CALL  5  RHRC 
   6 4XQ2 ...#UNS  CLSKI 
   4 4Y9L ...(   
   7 4YP= ...      LDX   2  3
  15 4_8W ...      FPUT                        [WAKE IPBCCA  
   4 4_NG ...)   
  13 5286 ...      LDX   5  AWORK1(2)           [OLPA
  15 52MQ ...      CALL  7  TDMR                [MURDER OLPA 
  10 537B ...      HUNT     1,IDF,CONCEP 
   8 53M2 ...      BNG   1  TDUP 
  11 546L ...      GETACT   AMSCAN,IPBTYPE   
   7 54BD ...      FSHSKIP   
  11 54L= ...      LINKSET  BPTR(2),PAIR(1)  
   8 54RD ...      FSHCODE  AORB 
  11 54YL ...      LINKSET  BPTR(2),PAIRB(1) 
  10 555W ...      MHUNTW   3,IDF,CONCEP 
   3 55KG ...
   9 5656 ...      CHAIN    3,BPTR(2)
   9 56JQ ...      HUNTACT  AMSCAN,2 
   6 574B ...      FPUT  
   4 5762 ...TDUP
  17 577L ...      RTTEST   RTIME,,,CPA         [J. IF PROG REALTIME.
   4 579= ...XIT 
   5 57=W ...      UP
   5 57#G ...RTIME   
   9 57B6 ...      FCJOB    2,,,CPA  
  16 57CQ ...      LDN   0  1                     [FIND JOB BLOCK
   9 57FB ...      SBS   0  AEXCO(2) 
  20 57H2 ...      BRN      XIT                  [DECREMENT CT. OF REALTIME PERIS
  13 57JM ...[IDENTIFIER HAS BEEN FOUND TO BE INOP   
  15 57K# ...[ ROUTINE TO CALL SETIDENT FOR INOP. IDENTIFIER 
  14 57K_ ...[ACCUMULATORS REQUIRED ON EXIT X2,X3,X5,X6  
  16 57LC ...TACT  SBX   6  2                   [SAVE X2 IN X7   
   8 57LT ...      LDX   2  FX2  
   8 57M? ...      SBN   3  A1   
  19 57MY ...      IPBNULL  3                   [NULLIFY ADEV'S - CONDCB LINK
   7 57NK ...      LDX   0  3
  10 57P= ...      SBX   0  ACOMMUNE4(2) 
  18 57PX ...      STO   0  ACOMMUNE5(2)        [DISPLACEMENT TO CONDCB  
  19 57QJ ...      SETIDENT 3                    [RE-INITIALISE IDENTIFIER   
   7 57RW ...      LDX   7  6
   8 57SH ...      CALL  6  SUCS1
   7 57T8 ...      LDX   6  7
   7 57TT ...      LDX   7  5
   8 57WG ...      CALL  5  RHRC 
   7 57X7 ...      LDX   5  7
  10 57XS ...      LDX   3  ACOMMUNE4(2) 
  10 57YF ...      ADX   3  ACOMMUNE5(2) 
  20 57_6 ...                                         [DISPLACEMENT DOWN BLOCK   
   8 57_R ...      ADN   3  A1   
  10 57_Y ...      MHUNTW   1,IDF,CONCEP 
   8 5825 ...      LDN   2  A1(1)
   7 582= ...      ADX   6  2
  20 582Y ...      BRN      TDSU          [ENSURE ENTRY REMOVED FROM IDF CONCEP  
  19 583L ...X2CDCIPB                            [ENTRY FOR SETIDENT MACRO   
  13 58H= ...      LDX   5  ACOMMUNE8(2)        [AMOP
  15 592W ...      LDX   6  ACOMMUNE9(2)        [IDENT NO.   
  11 59GG ...      GETACT   AMSCAN,IPBTYPE   
  16 5=26 ...      LDX   2  BPTR(2)             [POINT TO AMSCAN 
  17 5=FQ ...      LINKSET  2,PAIRC3(1)           [LINK TO K3CDCIPB  
  16 5=_B ...      STO   5  ACC5(2)             [STORE AMOP ADDR 
  16 5?F2 ...      STO   6  ACC6(2)             [AND IDENT NUMBER
   6 5?YL ...      FPUT  
   5 5#D= ...      UP
   5 5#XW ...X3CDCIPB
  10 5*2F ...      FSHCODE  B,SNOTINA1   
   4 5*54 ...(   
   7 5*7M ...      FSHACTPR  
   5 5*== ...SNOTINA1
   4 5*#T ...)   
  17 5*B7 ...      STOZ     AWORK1(2)               [CLEAR MARKER WD.
  19 5*CG ...      LDN   7  1                   [SHOW WE HAVE NOT CRASHCLOSED
   5 5*M# ...RTVWT   
  12 5*X6 ...      HUNT2J   3,AMXOR,ADEVS,5,X3NA 
  15 5BBQ ...      LDX   4  6                   [IDENT NO.   
   3 5BWB ...
  18 5CB2 ...      SBX   4  A1+FOURTHWD(3)      [SUBTRACT LOWEST NUMBER  
  18 5CTL ...      SLL   4  2                  [DISP OF ENTRY IN ADEVS   
  16 5D*= ...      ADX   3  4                 [ADDRESS OF ENTRY  
  10 5DSW ...      LDX   0  A1+BPTR(3)   
  17 5F#G ...      BNZ   0  X3NA                [J IF ALREADY RINGED 
   8 5FS6 ...      LDCT  0  #160 
  11 5G?Q ...      ANDX  0  A1+FOURTHWD(3)   
  19 5GRB ...      BNZ   0  X3NA                [J IF MOP OFF,OR REINITIALIS 
   8 5H?2 ...      LDX   0  BIT10
  11 5HQL ...      ANDX  0  A1+FOURTHWD(3)   
  19 5J== ...      BZE   0  X3CL                [J IF READY TO REINITIALISE  
   8 5JB7 ...      LDCT  0  #200 
  11 5JG4 ...      ANDX  0  A1+FOURTHWD(3)   
  19 5JK_ ...      BNZ   0  X3AHD               [J IF B10 SET WHEN ATTACHED  
  15 5JPW ...X3WT  COOR3    #62                 [WAIT 4 SECS 
   8 5K9G ...      BRN      RTVWT
   4 5KK9 ...X3CL
  19 5KML ...      LDX   0  AWORK1(2)           [TEST IF DEALING WITH UNAV   
   9 5KQ3 ...      BNG   0  X3UNAV   
  11 5KSF ...      LDX   0  A1+FOURTHWD(3)   
  19 5L3P ...      BNG   0  X3UNAV                [J. IF UNAVAILABLE BIT SET 
   5 5L=_ ...X3CLA   
   8 5LG9 ...      LDX   0  BIT9 
  11 5LPF ...      ANDX   0  A1+FOURTHWD(3)  
  17 5LYP ...      BNZ   0  X3ZE                       [J. IF CLOSED 
  20 5M82 ...      BZE   7  X3WT                [J IF ALREADY SENT CRASH CLOSE   
   8 5MML ...      LDN   0  #127 
  10 5N7= ...      STO   0  ACOMMUNE1(2) 
  16 5NLW ...X3CC  CCOUT    6,5                [SEND CRASH CLOSE 
  20 5P6G ...      LDN   7  0                [AND RESET MARKER TO SHOW WE HAVE   
   8 5PL6 ...      BRN      X3WT 
   7 5Q5Q ...X3ZE  LDCT  0  1
  11 5QKB ...      ORS   0  A1+FOURTHWD(3)   
  14 5R52 ...      ERS   0  A1+FOURTHWD(3)      [UNSET B8
  17 5RJL ...      ACROSS   IPBSCAN,5           [GO TO REINITIALISE  
   4 5RLQ ...X3NA
  10 5RNW ...      FSHCODE  B,SNOTINA2   
   4 5RR2 ...(   
   7 5RT6 ...      FSHKILL   
   5 5RX= ...SNOTINA2
   4 5R_B ...)   
   7 5S3G ...      SUICIDE   
  18 5S5M ...X3UNAV                              [IDENTIFIER UNAVAILABLE 
   8 5S6* ...      BZE   7  X3CLA
   8 5S74 ...      LDX   0  BIT9 
  11 5S8F ...      ANDX  0  A1+FOURTHWD(3)   
  16 5S9W ...      BZE   0  X3OP               [J. IF NOT CLOSED 
  19 5S=* ...      LDX   0  AWORK1(2)           [TEST IF OPEN ALREADY SENT   
  15 5S=S ...      BNG   0  X3WT                [J. TO WAIT  
   8 5S?? ...      LDN   0  #120 
  10 5S#N ...      STO  0  ACOMMUNE1(2)  
  17 5SB5 ...      CCOUT    6,5                 [SEND OPEN IDENTIFIER
   8 5SBD ...      LDX   0  GSIGN
  21 5SBR ...      STO   0  AWORK1(2)           [MARK WD TO SHOW OPENING UNAV IDENT  
  17 5SC6 ...      BRN      X3WT                [J TO WAIT  FOR OPEN 
  20 5SCG ...X3OP  LDN   0  #227                [2-WORD CRASH CLOSE AUTO-ANSWER  
  10 5SDX ...      STO   0  ACOMMUNE1(2) 
  19 5SFK ...      STOZ      AWORK1(2)           [CLEAR MARKER WD CLOSING NOW
   8 5SG# ...      BRN      X3CC 
  10 5SHW ...      MENDAREA 50,K100CDCIPB
  21 5SJK ...X3AHD                              [B10 SET WHEN AHED-ONLINE BROKEN..   
  16 5SK# ...[                                         ...IN ON  
   8 5SL3 ...      LDX   0  BIT10
  17 5SLQ ...      ERS   0  A1+FOURTHWD(3)            [CLEAR BIT10   
   7 5SMF ...      LDX   2  5
  21 5SN8 ...      WHICHCON (GEOERR),NAHNOW,,6        [SEE IF STILL AHED AND WHERE TO
   7 5SNX ...      LDX   7  6
   7 5SPL ...      SMO      5
  19 5SQ* ...      LDX   6  CPPTR                     [SET UP ACS FOR K4AHB  
  20 5SR4 ...      ACROSS   ATTACHB,4                 [GO TO COMPLETE ATTACHMENT 
  18 5SRR ...NAHNOW                                   [NO LONGER ATTACHED
  12 5SSG ...      HUNT2J   3,AMXOR,ADEVS,5,X3NA 
   7 5ST9 ...      LDX   4  6
  11 5STY ...      SBX   4  A1+FOURTHWD(3)   
   7 5SWM ...      SLL   4  2
  20 5SXB ...      ADX   3  4                         [FIND CORRECT ADEVS ENTRY  
   8 5SY5 ...      SMO      FX1  
   8 5SYS ...      LDX   0  ZCLBT
  20 5S_H ...      ANDS  0  A1+FOURTHWD(3)            [CLEAR ATTACHMENT MARKERS  
  20 5T2= ...      BRN      X3WT                      [CONTINUE REINITIALISATION 
   4 5T3G ...#END
   8 ____ ...42367525001600000000
