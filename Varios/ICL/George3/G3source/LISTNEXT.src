   8 22FL    #OPT  K0LISTNEXT=0  
  12 23DW    #LIS  K0LISTNEXT>K0LISTFILE>K0ALLGEO
   7 23YG    #SEG  LISTNEXT  
  10 24D6                   8HLISTNEXT   
  15 24MY ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
  11 24XQ ...      SETENTRY LISTNEXT,POINT,11
  12 25CB          SEGENTRY K51LISTNEXT,POINT51  
  14 25X2 ...      FSHENTRY K41LISTNEXT,POINT41,,POINT41 
  14 266S ...      FSHENTRY K52LISTNEXT,,POINT52,POINT52 
  12 26=P ...      SEGENTRY K12LISTNEXT,POINT12  
  21 26BL    [THIS SEGMENT CONTAINS MOST OF THE CODE FOR GENERATING ERROR MESSAGES   
  11 26W=    [AND SYSTEM JOURNAL INFORMATION.
   4 27*W    [   
  20 27TG    [K1LISTNEXT    IS ENTERED FROM LISTFREE WHEN A FILE HAS BEEN LOST   
   4 28*6    [   
  19 28SQ    [K3LISTNEXT    CONTINUES WITH THE LFDEL MACRO FROM K5LISTFIND   
   4 29#B    [   
  14 29S2    [K4LISTNEXT    IMPLEMENTS THE LFEND MACRO   
   4 2=?L    [   
  18 2=R=    [K2LISTNEXT    IS ENTERED FROM THE S/R TESTBLOCK IN LISTPROC
  19 2?=W    [              WHEN THE SCHEDULER HAS FOUND A GOUT/INFO STRING  
  13 2?QG    [              INVOLVING A LOST FILE.   
   4 2#=6    [   
  20 2#PQ    [K5LISTNEXT    IS ENTERED BY AN ACTLF WHICH WISHES TO BE CONSIDERED 
  10 2*9B    [              AS AVAILABLE 
   4 2*P2    [   
  20 2B8L    [K6LISTNEXT    IS ENTERED BY AN ACTLF TO FREE ITS DEVICE AND SUICIDE
  17 2BN=    [K7LISTNEXT    IS ENTERED BY AN ACTLF TO COMMIT SUICIDE 
   4 2C7W    [   
  19 2CMG    [K8LISTNEXT    IS ENTERED FROM LISTFREE BY AN ONLINE CPA WHICH  
  17 2D76    [              HAS RECIEVED A NOPER REPLY TO ITS REQUEST
   4 2DLQ    [   
  21 2F6B    [K9LISTNEXT    IS ENTERED FROM LISTOPEN WHEN OPEN OR RETRIEVE FOR THE   
  21 2FL2    [              FILE TO BE LISTED HAS INDICATED THAT THE FILE IS LOST.   
   4 2G5L    [   
  20 2GK=    [K10LISTNEXT   INFORMS THE OPERATORS OF PAPER LOW AND ALSO INFORMS  
  14 2H4W    [              THEM IF THE LP REMAINS BUSY. 
   4 2H8R ...[   
  20 2H#N ...[K11LISTNEXT   ENTERED BY NIPREL MACRO IF ACTLF HAS 'BEFORE FIRST   
  16 2HDK ...[              LINE OF LISTING' FLAG SET IE FLAG Q  
   4 2HJG    [   
  16 2HNC ...[K12LISTNEXT  ENTERED FROM LISTOPEN TO OUTPUT THE   
  13 2HS# ...[             START OF LISTFILE MESSAGE 
   4 2HY9 ...[   
  21 2J46    [***********************************************************************
   6 2JHQ    [DATA AREA  
   8 2JW6    RESTARTBIT     #4000
   9 2K3B    PROPBIT        #4000000 
   9 2KH2    SETUPBIT       #10000000
  17 2L2L    SMULTSETBIT    #10                 [MULTIFILE WITH SETUP
   8 2L=D    SMULTFILE      #2000
   8 2LG=    SIX            +6   
   8 2L_W    THIRTEEN       +13  
  10 2MFG    [MONITORING FILE MESSAGES   
   9 2M_6    WJLFLOST       +JLFLOST 
  10 2N8Y                   +JLFMULTAB   
   9 2NDQ    WJLFOL         +JLFOL   
  17 2NYB    WMESS          +JLFCOMP            [LISTFILE COMPLETED  
  17 2PD2                   +JLFTERM            [LISTFILE TERMINATED 
  14 2PXL                   +JLFCANTDO          [CANTDO  
   5 2QC=    WOLGETP 
  13 2QWW    [ERROR MESSAGES WHEN ONLINE GETPER FAILS
  16 2RBG                   +JPERMX             [PERMEXCL REPLY  
  15 2RW6                   +JLFNOP             [NOPER REPLY 
  15 2S*Q                   +JOLNPROP           [NOPROP REPLY
   9 2STB    WJLFNOPER      +JLFNOPER
   9 2T*2    WJLFPERMX      +JLFPERMX
   9 2TSL    WJLFSLIST      +JLFSLIST
   7 2T_S ...#UNS  ASTRTJL   
   9 2W72 ...WJLFSTRT      +JLFSTRT  
  16 2W#=    XLIST          12H#LISTING OF      [FIRST HEADING   
   9 2WRW    WELOST         +ELOST   
   9 2X?G    WJSETP15       +JSETP15 
   4 2XR6    [   
  21 2Y=Q    [***********************************************************************
   7 2YQB    [SUBROUTINE AREA
   5 2_=2    SMONOUT 
  18 2_PL    [THIS S/R DOES A MONOUT. X4 JOB NUMBER X5 MESSAGE NUMBER.   
   8 329=          SBX   6  FX1  
   8 32NW          MONOUTX  5,4  
   8 338G          ADX   6  FX1  
   7 33N6          EXIT  6  0
   5 347Q    OUTBLOCK
  20 34MB    [THIS S/R SETS UP A GMON/ASET BLOCK TO HOLD PARAMETERS FOR MESSAGE  
   8 3572    [BLOCK SIZE IN X6 . 
   8 35LL          SBX   7  FX1  
   7 366=          OUTBLOCK 6
   8 36KW          ADX   7  FX1  
   7 375G          EXIT  7  0
   6 37K6    PARAMETER   
  18 384Q    [THIS S/R OUTPUTS A PARAMETER IN A MONITORING FILE MESSAGE. 
   8 38JB          SBX   6  FX1  
  10 3942          OUTPACKX 3,4,5,PARM   
   7 39HL          ADN   6  1
   8 3=3=    PARM  ADX   6  FX1  
   7 3=GW          EXIT  6  0
   4 3?2G    [   
   5 3?G6    OUTNAME 
  15 3?_Q    [THIS S/R EXTRACTS AN ENTRANT NAME FROM A FABSNB
  14 3#FB    [AND OUTPUTS IT AS A PARAMETER TO A MESSAGE.
  14 3#_2    [WE MUST SET UP A LARGE ENOUGH ADATA/CREADL.
  16 3*DL    [WE SHALL NEED 24 + 13N CHARACTERS WHERE N IS THE   
  11 3*Y=    [NUMBER OF NAMES IN DESCRIPTION.
  18 3BCW    [WE CALCULATE N FROM THE FABSNB.IF M IS LENGH OF FABSNB THEN
   9 3BXG    [     N = (M-4)/6 + 1   
   4 3CC6    [   
   8 3CWQ          SBX   7  FX1  
  10 3DBB          MHUNT    3,FILE,FABSNB
   9 3DW2          LDX   4  HDREC(3) 
   7 3F*L          SBN   4  4
   9 3FT=          DVS   3  SIX(1)   
   8 3G#W    #SKI  K99LISTPROC   
   4 3GSG    (   
   9 3H#6          BZE   3  NOREMAIN 
   9 3HRQ          TRACE    3,REMAIN 
   5 3J?B    NOREMAIN
   4 3JR2    )   
   7 3K=L          ADN   4  1
  10 3KQ=          MPY   4  THIRTEEN(1)  
   8 3L9W          ADN   5  24   
   7 3LPG          LDN   0  3
  19 3M96          ANDX  0  5                   [SEE IF WHOLE NUMBER OF WORDS
   7 3MNQ          SRL   5  2
   8 3N8B          BZE   0  WHOL 
   7 3NN2          ADN   5  1
  11 3P7L    WHOL  SETUPCORE 5,2,ADATA,CREADL
   9 3QLG    [SPACEFILL ADATA-CREADL 
   8 3R66          LDX   0  ACES 
  10 3RKQ          STO   0  A1+FRH+1(2)  
   7 3S5B          LDX   4  5
   7 3SK2          SBN   4  2
  10 3T4L          LDN   5  A1+FRH+1(2)  
  10 3TJ=          LDN   6  A1+FRH+2(2)  
   7 3W3W          SMO      4
   7 3WHG          MOVE  5  0
   4 3XGQ    NOSP
  19 3Y2B          STOZ     A1+FRH(2)           [INITIALISE DATA CHAR. COUNT 
   6 3YG2          UNNORM
  11 3Y_L    OUTN1 MHUNTW   3,ADATA,CREADL   
   9 3_F=          LDX   4  A1+FRH(3)
   7 3_YW          LDN   0  3
   7 42DG          ANDX  0  4
   7 42Y6          SRL   4  2
   8 43CQ          BZE   0  WHO  
   7 43XB          ADN   4  1
   9 44C2    WHO   ADN   3  CPDATA   
  10 44WL          LDN   5  JPDENTRANT   
   9 45B=          CALL  6  PARAMETER
   8 45TW          BRN      OUTN1
  11 46*G          MHUNTW   1,ADATA,CREADL   
   7 46T6          FREECORE 1
   8 47#Q          ADX   7  FX1  
   7 47SB          EXIT  7  0
   6 48#2    STANDMESS   
  16 48RL    [THIS S/R PRODUCES A STANDARD LISTFILE LOST MESSAGE 
   8 49?=    [FROM A GOUT/INFO   
   4 49QW    [   
   8 4==G          SBX   7  FX1  
  10 4=Q6    STAG1 MHUNTW   3,GOUT,INFO  
   9 4?9Q          LDX   0  GMODE(3) 
  15 4?PB          BNG   0  XTOL                [J IF ONLINE 
  15 4#92          LDEX  4  GSUSE(3)            [NO OF CHARS 
   7 4#NL          LDN   0  3
   7 4*8=          ANDX  0  4
  16 4*MW          SRL   4  2                   [CONVERT TO WORDS
   8 4B7G          BZE   0  WHOLE
  14 4BM6          ADN   4  1                   [ROUND UP
   8 4C6Q    WHOLE ADN   3  GHEAD
  16 4CLB          LDN   5  JPDENTRANT          [ENTRANT NAME PDC
   9 4D62          CALL  6  PARAMETER
  15 4DKL          BRN      STAG1               [COORDINATED 
  10 4F5=    STAG2 MHUNTW   3,GOUT,INFO  
   8 4FJW          ADN   3  GUSER
  18 4G4G          LDN   4  3                   [NO OF WORDS IN PARAMETER
  15 4GJ6          LDN   5  JPDUSERNAME         [USERNAME PDC
   9 4H3Q          CALL  6  PARAMETER
  15 4HHB          BRN      STAG2               [COORDINATED 
  10 4J32    STAG3 MHUNTW   3,GOUT,INFO  
   8 4JGL          ADN   3  GJOBN
  15 4K2=          LDN   5  JPDJOBNAME          [JOB NAME PDC
   9 4KFW          CALL  6  PARAMETER
   8 4K_G          BRN      STAG3
   8 4LF6    XTOL  ADX   7  FX1  
   7 4LYQ          EXIT  7  0
   5 4MDB    USERJOB 
  18 4MY2    [THIS SUBROUTINE SETS UP USERNAME-JOBNAME FOR OP TO SJ ETC  
   8 4NCL          SBX   7  FX1  
   8 4NX=          BRN      STAG2
   5 4PBW    STANDLOG
  15 4PWG    [THIS S/R OUTPUTS STANDARD LOGGING PARAMETERS   
   8 4QB6          SBX   7  FX1  
   6 4QCN ...#UNS  ANIP  
   4 4QF= ...(   
  11 4QGS ...      SAWCEJ   SL7,SL7,SL7,SL8  
  11 4QJB ...[ SPECIAL FORMAT FOR NIP ACTLF  
   8 4QKY ...SL8   LDCT  0  #100 
   8 4QMG ...      STO   0  A5(2)
   8 4QP4 ...      BRN      SL1  
   4 4QQL ...SL7 
   4 4QS8 ...)   
  15 4QTQ          GEOPACK  A5(2)               [UNIT NUMBER 
   8 4R*B    SL1   LDN   3  A5(2)
   7 4RT2          LDN   4  1
   9 4S#L          LDN   5  JPDGEOPER
   9 4SS=          CALL  6  PARAMETER
  15 4T?W          BRN      SL1                 [COORDINATED 
  10 4TRG ...      MHUNTW   3,GOUT,INFO  
  14 4W?6          LDCH  0  GOUTKEY1(3)         [URGENCY 
   8 4WQQ          DCH   0  A5(2)
   8 4X=B    SL2   LDN   3  A5(2)
  10 4XQ2          LDN   5  JPDURGENCY   
   9 4Y9L          CALL  6  PARAMETER
   8 4YP=          BRN      SL2  
  16 4_8W    SL3   LDN   3  ALFTRANS(2)         [NO OF TRANSFERS 
  10 4_NG          LDN   5  JPDTRANSCT   
   9 5286          CALL  6  PARAMETER
   8 52MQ          BRN      SL3  
  15 537B    SL4   LDN   3  ALFWORDS(2)         [NO OF WORDS 
   9 53M2          LDN   5  JPDNUMA  
   9 546L          CALL  6  PARAMETER
   8 54L=          BRN      SL4  
  18 555W    [WE NOW HAVE TO SET UP A LIST OF ANY PROPERTIES ATTRIBUTED  
  11 55*N ...      HUNT     3,APERI,APROPNOS 
  18 55KG ...      IF       3,PZ                [ PROPERTIES REQUESTED   
   6 55T# ...      THEN  
  18 55W? ...         LDX   3  ALOGLEN(3)       [LENGTH OF PROPERTY BLOCK
  20 55X= ...         SETUPCOR 3,2,GOUT,INFO4   [CREATE COPY OF PROPERTY BLOCK   
  12 55Y9 ...         MHUNT    1,APERI,APROPNOS  
   9 55_8 ...         LDN   5  A1(1) 
   9 5627 ...         LDN   6  A1(2) 
   9 5636 ...         MOVE  5  0(3)  
  21 5645 ...         LDX   3  2                [USE COPY TO CHANGE TO OUTPUT FORMAT 
  17 5656 ...         IF       +CHPERM(3),NZ       [ PERMANENT PROPS.
   7 56#Y ...         THEN   
  18 56JQ ...            IF       +CHTEMP(3),NZ       [ TEMPORARY PROPS. 
   7 56SJ ...            THEN
  19 574B ...               LDN   2  BPROPS(3)           [ CONCATENATE NAMES 
  21 57#8 ...               LDX   1  3                   [ MOVE PERM PROPS. TO ENSURE
  18 57J2 ...               ADX   1  ADPERM(3)           [ ROOM FOR ^&^  
  12 57RS ...               SMO      CHPERM(3)   
  10 583L ...               MVCH  1  0   
  10 58?D ...               LDN   0  #26 
  16 58H= ...               DCH   0  0(2)                [ ^&^   
  10 58R4 ...               BCHX  2  /   
  10 592W ...               LDX   1  3   
  12 59=N ...               ADX   1  ADTEMP(3)   
  12 59GG ...               SMO      CHTEMP(3)   
  10 59Q# ...               MVCH  1  0   
  12 5=26 ...               LDX   0  CHTEMP(3)   
  16 5=9Y ...               ADN   0  1                   [ ^&^   
  21 5=FQ ...               ADS   0  CHPERM(3)           [ COUNT OF CHARS IN NAMES   
  11 5=PJ ...               LDN   0  BPROPS  
  19 5=_B ...               STO   0  ADPERM(3)           [ ADDRESS OF NAMES  
   7 5?98 ...            FI  
   7 5?F2 ...         ELSE   
  11 5?NS ...            LDX   0  CHTEMP(3)  
  17 5?YL ...            STO   0  CHPERM(3)           [ CHAR COUNT   
  11 5#8D ...            LDX   0  ADTEMP(3)  
  16 5#D= ...            STO   0  ADPERM(3)           [ ADDRESS  
   6 5#N4 ...         FI 
   8 5#XW ...         LDX   2  3 
  10 5*7N ...         ADX   2  ADPERM(3) 
   8 5*CG ...         SLC   2  2 
  10 5*M# ...         ADX   2  CHPERM(3) 
   8 5*X6 ...         LDX   5  2 
   8 5B6Y ...         ANDN  5  3 
  19 5BBQ ...         SRC   2  2                   [ -> BEYOND LAST USED CHAR
  18 5BLJ ...         IF       5,NZ                [ UNUSED CHARS IN WORD
   7 5BWB ...         THEN   
   9 5C68 ...            SBN   5  4  
   9 5CB2 ...            NGX   5  5  
  10 5CKS ...            LDN   1  ACES   
   9 5CTL ...            SMO      5  
  20 5D5D ...            MVCH  1  0                   [ SPACEFILL REST OF WORD   
   6 5D*= ...         FI 
   4 5DK4 ...SL5 
  10 5DSW ...         LDN   5  JPDNULL   
  19 5F4N ...         CALL  6  PARAMETER           [ INCLUDE ^PR^ PARAMETER  
   9 5F#G ...         BRN      SL5   
   4 5FJ# ...SL6 
  11 5FS6 ...         MHUNT    3,GOUT,INFO4  
  10 5G3Y ...         LDX   4  CHPERM(3) 
   8 5G?Q ...         ADN   4  3 
  15 5GHJ ...         SRL   4  2                   [ WORDS   
  10 5GRB ...         ADX   3  ADPERM(3) 
  10 5H38 ...         LDN   5  JPDVARCHAR
  10 5H?2 ...         CALL  6  PARAMETER 
   9 5HGS ...         BRN      SL6   
  19 5HQL ...         MFREE    GOUT,INFO4       [FREE COPY OF PROPERTY BLOCK 
   5 5J2D ...      FI
   8 5J==          ADX   7  FX1  
   7 5JPW          EXIT  7  0
   4 5JR7 ...[   
   6 5JSD ...STANDLOGA   
  19 5JTP ...[THIS S/R FREES ANY APERI/APROPNOS FOR FINISHED MESSAGES ETC.   
   8 5JX2 ...      SBX   7  FX1  
  11 5JY? ...      HUNT     3,APERI,APROPNOS 
  17 5J_J ...      IF       3,PZ                [PROPERTIES REQUESTED
   6 5K2T ...      THEN  
  11 5K46 ...         MFREE    APERI,APROPNOS
   5 5K5C ...      FI
   8 5K6N ...      ADX   7  FX1  
   7 5K7_ ...      EXIT  7  0
   4 5K9G    [   
   5 5KP6    SCLOSE  
  14 5L8Q    [THIS S/R CLOSES A FILE FOR THE LFEND MACRO 
   8 5LNB          SBX   7  FX1  
   9 5M82          LDX   0  AWORK1(2)
  18 5MML          BNG   0  SCD                 [J IF NOT FINISHED YET   
   7 5N7=          CLOSETHAW 
   8 5NLW          BRN      SXT  
   6 5P6G    SCD   CLOSE 
   8 5PL6    SXT   ADX   7  FX1  
   7 5Q5Q          EXIT  7  0
   4 5QKB    [   
   5 5R52    RAISECPA
  14 5RJL    [THIS S/R LOCATES A CPA WAITING FOR ONLINE  
  16 5S4=          JOBLOCK  AWORK3(2),3         [X3-> JOBQ BLOCK 
  14 5SHW          FCAJO 3                      [X3-> CPA
  18 5T3G    [ WE NOW UNSET THE 'ONLINE REQUEST BEING SERVICED' MARKER   
   7 5TH6          LDN   0  2
   9 5W2Q          ERS   0  EXT+9(3) 
   9 5WGB          ANDX  0  EXT+9(3) 
  18 5X22          BNZ   0  ROLERR              [ERROR IF FLAG NOT SET   
   7 5XFL          EXIT  7  0
   5 5X_=    WAKECPA 
  12 5YDW    [THIS S/R WAKES UP A CPA FOR ONLINE 
   9 5_D6          CHAIN    3,BMISC+1
  11 5_XQ          LONGON1  #27,GOUTKEY1(3)  
   7 62CB          EXIT  7  0
  12 62X2    [                          ******** 
   4 63BL    [   
  21 63W=    [***********************************************************************
   9 64*W    [MAIN BODT OF SEGMENT   
  21 64TG    [***********************************************************************
   5 65*6    POINT1  
  18 65SQ    [THIS IS THE ENTRY FROM LISTFREE WHEN A FILE HAS BEEN LOST. 
   9 66#B    [IT IS OBEYED BY A CPA. 
   9 67?L          CALL  7  OUTNAME  
   6 67R=          LFTHAW
   4 67R_ ...#   
   5 67SN ...XENDB1  
   9 68P= ...      COMERR   ERLOSTA  
   4 69=6    [   
  21 69PQ    [***********************************************************************
   5 6=9B    POINT2  
  20 6=P2    [THIS ENTRY POINT IS USED BY THE SCHEDULER FROM THE TESTBLOCK S/R   
  20 6?8L    [IN LISTPROC.THE SCHEDULER IS PROCESSING A FABSNB FOR A LIST FILE   
  21 6?N=    [AND HAS FOUND A CORRESPONDING FABSNB IN A GOUT/INFO STRING IN THE LFCHA
  12 6#7W    [     ACOMMUNE4=1 FOR LOST MAIN FILE
  13 6#MG    [              =2 FOR LOST SETUP FILE.  
  10 6*76          LDX   0  ACOMMUNE4(2) 
   9 6*LQ          STO   0  AWORK1(2)
   4 6B6B    [   
  21 6BL2    [***********************************************************************
   5 6C5L    POINT3  
   8 6CK=          LDN   6  24   
  20 6D4W          CALL  7  OUTBLOCK            [SET UP BLOCK TO HOLD PARAMETERS.
   9 6DJG          CALL  7  STANDMESS
  10 6F46          MHUNTW   3,GOUT,INFO  
   9 6FHQ          LDX   0  GJOBS(3) 
   9 6G3B          STO   0  AWORK3(2)
   9 6GH2          LDX   7  GMODE(3) 
  15 6H2L          BNG   7  NFRE                [J IF ONLINE 
   7 6HG=          FREECORE 3
   6 6HMD ...#UNS  AWLSZ 
  10 6HSL ...      VFREEW   GOUT,GESIZE  
   9 6H_W    NFRE  LDX   0  AWORK1(2)
   8 6J9N ...      LDX   1  FX1  
  10 6JFG          LDX   4  SETUPBIT(1)  
  18 6J_6          ANDX  4  7                   [PREPARE CHECK ON SETUP  
  17 6KDQ          BZE   0  NLOST               [J IF NOT LOST FILE  
   7 6KYB          SBN   0  3
   8 6LD2          BPZ   0  NLOST
  10 6LXL          MHUNT    3,FILE,FABSNB
  16 6MC=          NAMETOP  3,GOUT,INFO4        [PRESERVE FABSNB 
  15 6MWW          BNG   7  TOLI                [J IF ONLINE 
  10 6NBG          MHUNT    3,GOUT,INFO2 
  10 6NW6          NAMETOP  3,FILE,FABSNB
  16 6P*Q          BZE   4  NSTUP               [J IF NO SETMP   
   9 6PTB          LDX   0  AWORK1(2)
   7 6Q*2          SBN   0  1
  20 6QSL          BNZ   0  TOLI                [J IF IT IS SETUP THAT IS LOST   
  19 6R#=          CHAIN    3,2                 [GET MAIN FILE FABSNB NEARER 
  10 6RRW    TOLI  MHUNT    3,GOUT,INFO3 
  10 6S?G          NAMETOP  3,FILE,FABSNB
  15 6SR6    NSTUP BPZ   7  TNOLI               [J NOT ONLINE
  10 6T=Q          LDX   5  WJSETP15(1)  
   5 6TQB    ONLINERR
  10 6W=2          MHUNTW   3,GOUT,INFO  
   9 6WPL          CALL  7  WAKECPA  
   9 6X9=          STO   5  GMODE(3) 
  10 6XNW          LDX   0  JSOURCE3(2)  
  10 6Y8G          STO   0  GSOURCE1(3)  
  16 6YN6          BZE   4  NTHAW               [J IF NO SETUP   
   6 6_7Q    THUP  LFTHAW
  17 6_MB    NTHAW MHUNT    3,GOUT,INFO4        [LOST FILES FABSNB   
   8 7272          BNG   3  NOFAB
  10 72LL          NAMETOP  3,FILE,FABSNB
   5 736=    NOFAB UP
   9 73KW    TNOLI CALL  7  OUTNAME  
  10 745G          LDX   5  WJLFLOST(1)  
  15 74K6    TMON  LDX   4  AWORK3(2)           [JOB NUMBER  
   9 754Q          CALL  6  SMONOUT  
   8 75=Q          BRN      THUP 
  17 75DQ    NLOST HUNT     3,FILE,FABSNB       [FREE 'EXTRA' FABSNB 
  16 75LQ          BNG   3  TXOL1               [IF ONE EXISTS   
   7 75SQ          FREECORE 3
  17 762Q    TXOL1 BZE   4  NOPAT               [J IF NO SETUP FILE  
  10 768Q          MHUNT    3,GOUT,INFO3 
  10 76BQ          NAMETOP  3,FILE,FABSNB
  18 76JQ          LDX   0  SMULTSETBIT(1)      [IF WE ARE STOPLISTING A 
  18 76QQ          ANDX  0  7                   [MULTIFILE WITH A SETUP  
  18 76YQ          BZE   0  NOPAT               [FILE,WE ONLY THAW THE SE
  18 776Q          FREECORE 3                   [FILE ONCE WHEN THAWING  
  17 77#Q    [                                   THE LAST MF COMP.   
  16 77GW    NOPAT BPZ   7  TXOL                [J IF NOT ONLINE 
  15 782G          LDX   0  AWORK2(2)           [GETPER REPLY
   7 78G6          PUTREP2  0
   7 78_Q          LDN   5  0
  15 79FB      TESTREP2 NOPER,NOP,PERMEXCL,PEX,NOPROP,NOPROP 
   9 79_2          GEOERR   1,GETREP 
   5 7=DL    NOPROP  
   7 7=Y=          ADN   5  1
   7 7?CW    NOP   ADN   5  1
   7 7?XG    PEX   SMO      5
  16 7#C6          LDX   5  WOLGETP(1)          [ERROR MESSAGE   
   9 7#WQ          BRN      ONLINERR 
  10 7*BB    TXOL MHUNT    3,GOUT,INFO2  
  10 7*W2          NAMETOP  3,FILE,FABSNB
   9 7DRQ          LDX   0  AWORK1(2)
   8 7F?B          BZE   0  SGETP
  10 7FR2          LDX   5  WJLFSLIST(1) 
   8 7G=L          BRN      TMON 
   9 7GQ=    SGETP LDX   0  AWORK2(2)
   7 7H9W          PUTREP2  0
  10 7HPG          TESTREPN NOPER,SGPMX  
  16 7J96    SG1   LDN   3  GSWORK3(2)          [CONSOLE PROPERTY
   7 7JNQ          LDN   4  1
   9 7K8B          LDN   5  JPDPROP  
   9 7KN2          CALL  6  PARAMETER
   8 7L7L          BRN      SG1  
  10 7LM=          LDX   5  WJLFNOPER(1) 
   8 7M6W          BRN      TMON 
  10 7MLG    SGPMX LDX   5  WJLFPERMX(1) 
   8 7N66          BRN      TMON 
   4 7NKQ    [   
  21 7P5B    [***********************************************************************
   4 7P64 ...#   
   4 7P6Q ...#   
   5 7P7D ...POINT41 
   4 7PDB ...#   
   4 7PF4 ...#   
   4 7PFQ ...#   
  10 7PGD ...#DEF  XAPIALEN=BOLEN>BLLEN+1
   4 7PH6 ...#   
   4 7PHS ...#   
   5 7PK2    POINT4  
  18 7Q4L    [THIS ENTRY POINT TIDIES UP WHEN A LISTFILE HAS FINISHED.   
   6 7QJ=    [IT MUST:   
  13 7R3W    [1.SEND A MESSAGE TO THE SYSTEM JOURNAL 
  12 7RHG    [2.MAKE THE PERIPHERAL AVAILABLE.   
  12 7S36    [3.GET THE ENTRY IN SYSOUT UPDATED. 
   6 7SGQ    [ON ENTRY,  
  11 7T2B    [     AWORK1 > 0   FOR TERMINATE
  12 7TG2    [     AWORK1=  0  FOR COMPLETION.   
  10 7T_L    [     AWORK1 < 0  FOR CANTDO
  20 7WF=    [IF CANTDO THEN AWORK2 CONTAINS RESTART LINE NUMBER OR IS NEGATIVE  
  16 7WYW    [IF NO START HAS YET BEEN MADE ON LISTING MAIN-FILE.
  18 7XDG    [IF ENHANCED SJ MESSAGES ARE INCLUDED,THEN THE ACTLF OWNS   
  20 7XY6    [A GOUT GABSNB WHICH WE WILL USE TO DERIVE THE NORMALISED ENTRANT   
  20 7YCQ    [NAME,AND A GOUT INFO WHICH WE WILL USE TO DERIVE USERNAME JOBNAME  
   4 7YXB    [   
   4 7YXQ ...#   
   4 7YY6 ...#   
  10 7_C2 ...      MHUNTW   3,GOUT,INFO  
   9 7_WL          LDX   5  GMODE(3) 
   9 82B=          LDX   6  AWORK1(2)
  20 82TW          STOZ     GMODE(3)            [GET ENTRY DELETED FROM SYSOUT   
  16 83*G          BPZ   6  NOUPDATE            [J IF NOT CANTDO 
   9 83T6          STO   5  GMODE(3) 
   9 84#Q          LDX   0  AWORK2(2)
   9 84SB          BNG   0  NOUPDATE 
  17 85#2          STO   0  GFLI(3)             [RESTART LINE NUMBER 
  10 85HR          LDX   0  RESTARTBIT(1)
  19 85LB          ORS   0  GMODE(3)            [SET BIT  GMODE IF A RESTART 
   9 85M3 ...[ FOR LFALLTRANS MACRO  
  11 85MN ...[ NORMAL RESTART BUT NOT 'ALL', 
  16 85N* ...[ SET REST2 BIT, WILL NOT CHARGE FOR SKIPS TO START 
   9 85P2 ...[ OF LISTFILE OF FILE   
  10 85PM ...      BS       3,BLFREST2   
  12 85Q# ...      JBC      NOUPDATE,3,BLFALLR   
  10 85Q_ ...      BC       3,BLFREST2   
   5 85RL    NOUPDATE
  15 86?=          BNG   5  SOL                 [J IF ONLINE 
  15 86QW          CALL  7  SCLOSE              [CLOSE FILE  
  10 87=G    SOL   LDX   0  SETUPBIT(1)  
   7 87Q6          ANDX  0  5
  17 889Q          BZE   0  SNS                 [J IF NO SETUP FILE  
  20 88PB    [IF THIS IS THE SETUP FILE FOR A MULTIFILE THEN IT WILL ONLY HAVE   
  19 8992    [BEEN FROZEN ONCE. THUS WE ONLY THAW IF WE HAVE JUST LISTED THE 
  12 89NL    [LAST COMPONENT OF THE MULTIFILE.   
  18 89RQ    [WE NOLONGER THAW THE SETUP FILE ASSOCIATED WITH A MULTIFILE
  20 89WW    [IN THIS CHAPTER.IT IS NOW DONE IN LISTPROC BEFORE THE LAST ELEMENT 
  20 8=22    [IS DELETED FROM SYSOUT.NECCESSARY AS ELEMENTS MAY BE LISTED IN ANY 
  10 8=56    [ORDER-REMEMBER QUERIES!!   
  10 8=8=          LDX   0  SMULTFILE(1) 
  17 8=MW          ANDX  0  5                   [OTHER THAN THE LAST 
   9 8?7G          BZE   0  THAWOK   
  19 8?M6          LDN   7  SNS                 [ADDRESS FOR EXIT FROM S/R   
  16 8#6Q          BRN      SCD                 [ 'CALL' SCLOSE  
   5 8#LB    THAWOK  
   9 8*62          CALL  7  SCLOSE   
  12 8*KL ...SNS            MHUNTW 3,GOUT,INFO   
   9 8B5= ...      LDX   0  GJOBS(3) 
   9 8BJW ...      STO   0  AWORK3(2)
  16 8C4G          BPZ   5  RNOLIN              [J IF NOT ONLINE 
  19 8CJ6          BNG   6  NOTYET              [ELSE NO MESSAGE IF NO DEVICE
   5 8D3Q    RNOLIN  
  15 8DHB          LDN   6  12                  [SET UP BLOCK
  15 8G2=          BNG   5  OLPATH              [J IF ONLINE 
   8 8GFW          ADN   6  12   
  10 8HYQ          MHUNTW   3,GOUT,GABSNB
  10 8JDB          NAME   3,FILE,FABSNB  
   5 8KCL    OLPATH  
  17 8KX=          CALL  7  OUTBLOCK            [TO HOLD PARAMETERS  
  15 8LBW          BPZ   5  SNOLIN              [J NOT ONLINE
   9 8LWG          CALL  7  STANDLOG 
   9 8M2C ...      CALL  7  STANDLOGA
   9 8M6# ...      LDX   4  AWORK3(2)
   9 8MB6          LDX   5  WJLFOL(1)
   9 8MTQ          CALL  6  SMONOUT  
  10 8N*B ...      MHUNTW   3,GOUT,INFO  
   9 8NT2          CALL  7  WAKECPA  
   9 8P#L          LDX   0  CPPTR(2) 
   8 8PS=          ORX   0  GSIGN
   9 8Q?W          STO   0  GMODE(3) 
  17 8QRG          STOZ     CPPTR(2)            [NO LONGER OWN DEVICE
  10 8R?6          STOZ     JSOURCE1(2)  
  10 8RQQ          STOZ     JSOURCE2(2)  
  20 8S=B          CALL  7  RAISECPA            [INFORM CPA THAT ONLINE REQUEST  
  20 8SQ2                                       [IS NO LONGER BEING IMPLEMENTED  
   7 8T9L          SUICIDE   
   5 8TP=    NOTYET  
   9 8W8W          CALL  7  RAISECPA 
  17 8WNG          LDEX  0  CLNG1(3)            [CHECK WAITING STYLE 
   8 8X86          SBN   0  #27  
  14 8XMQ          BNZ   0  ROLERR              [WRONG!  
  20 8Y7B          LDX   0  JOBEVENTS(3)        [CHEC2 IN CASE CPA HAS IGNORED   
  19 8YM2          BPZ   0  XLFEND              [A PREVIOUS BREAK IN FOR OUR 
  14 8_6L          SLL   0  2                   [BENEFIT.
  18 8_L=          BNG   0  XLFEND              [NOT Y&T DONE TESTBRKIN  
   9 925W          LDX   0  ATYPE(3) 
   8 92KG          ANDN  0  #77  
  16 9356          BZE   0  ROLERR              [J IF NOT WAITING
  17 93JQ          TERMON   3,#27               [SIMULATE BREAK IN   
   9 944B          BRN      XLFEND   
   5 94J2    ROLERR  
   9 953L          GEOERR   1,FLAGOFF
   5 95H=    SNOLIN  
  17 9726          CALL  7  OUTNAME             [EXTRACT ENTRANT NAME
  19 97FQ          CALL  7  USERJOB              [EXTRACT USERNAME JOBNAME   
   8 97_B          BRN      SLOG 
   4 98YL    SNOL
  10 99D= ...      MHUNTW   3,GOUT,INFO  
  15 99XW ...      LDN   3  GJOBS(3)            [JOB NUMBER  
   7 9=CG          LDN   4  1
   9 9=X6          LDN   5  JPDNUMB  
   9 9?BQ          CALL  6  PARAMETER
   9 9?WB          BRN      SNOLIN   
   4 9#B2    SLOG
   9 9#TL          CALL  7  STANDLOG 
   9 9*5D ...      CALL  7  STANDLOGA
   7 9**=          LDN   5  0
   9 9*SW          LDX   0  AWORK1(2)
  18 9B#G          BZE   0  SCOMP               [J IF LISTING COMPLETE   
   7 9BS6          ADN   5  1
  16 9C?Q    SCOMP BPZ   0  NCAND               [J IF NOT CANTDO 
   7 9CRB          ADN   5  1
   7 9D?2    NCAND SMO      5
  17 9DQL          LDX   5  WMESS(1)            [PICK UP MESSAGE NO. 
   9 9F2D ...      LDX   4  AWORK3(2)
   9 9F==          CALL  6  SMONOUT  
  10 9FPW ...      MFREE    FILE,FABSNB  
   5 9KLW    XLFEND  
  10 9KS4 ...      MHUNT    1,GOUT,INFO  
  11 9K_= ...      NAME     1,GOUT,GUPDATE   
  10 9L6G          LFCHAIN  GOUT,GUPDATE 
   4 9LL6    [   
  21 9M5Q    [***********************************************************************
   5 9MKB    POINT5  
   8 9N52          STARTACT OUT  
   4 9NJL    [   
  21 9P4=    [***********************************************************************
   5 9PHW    POINT51 
  16 9Q3G    [ENTRY POINT TO CHECK THAT DEVICE IS STILL USABLE   
   9 9QH6          LDX   0  FINISH   
   4 9QN# ...#   
   6 9QTG ...XHOMEFIN2   
   7 9R2Q          SLL   0  1
  15 9RGB          BNG   0  XFREE               [J IF FINISH 
   6 9RMJ ...#UNS  ANIP  
   4 9RSQ ...#SKI
  17 9S22          SAWCEJ   XCEN,XREM           [CHECK DEVICE OWNED  
   6 9S78 ...#UNS  ANIP  
  17 9S#B ...      SAWCEJ   XCEN,XREM,,XLP       [CHECK DEVICE OWNED 
   4 9S*M ...XREM
   5 9SW# ...POINT52 
   8 9SXK ...      RJWTBFR  XFREE
   8 9S_=          LFBITCLR F,2  
   4 9T36 ...XEN 
   8 9TD= ...      LFEND    EB   
   4 9TF3 ...[   
   6 9TF8 ...#UNS  ANIP  
   4 9TF* ...(   
  12 9TFG ...[ NIP LF CLOSED DOWN - NOT FINISH   
  10 9TFM ...XLP   ACROSS   LISTOPEN,8   
   4 9TFS ...)   
   4 9TG7 ...#   
   4 9THD ...XCEN
   6 9THK ...#UNS  ANIP  
   4 9THQ ...(   
  10 9THX ...[ ZEROISE REPERTOIRE DATA   
   8 9TJ4 ...      SMO      FX2  
   9 9TJ9 ...      STOZ     JSOURCE3 
   4 9TJB ...)   
  18 9W=G ...      JWTBWR   ,XFREE              [J WAITING TO BE WRONGED 
   4 9W?R ...#   
   5 9W*4 ...XCENY   
   8 9WD6          BRN      XEN  
   4 9WXQ    [   
  21 9XCB    [***********************************************************************
   5 9XX2    POINT6  
   5 9XXR ...XFREE   
   6 9XXW ...#UNS  ANIP  
   4 9XX_ ...(   
  13 9XY4 ...      SAWCEJ   XFRE2,XFRE2,XFRE2,XLP2   
  13 9XY7 ...[ CLOSE DOWN ACTLF - FINISH HAS OCCURRED
  10 9XY= ...XLP2  ACROSS   LISTOPEN,7   
   5 9XY* ...XFRE2   
   4 9XYD ...)   
   9 9Y*4 ...      LFBBUS   G,2,NOREM
   4 9Y*T ...#SKI
   9 9YBL    XFREE LFBBUS   G,2,NOREM
   8 9YLD ...      LFBITCLR  G,2 
  11 9YW=          MHUNTW   1,GOUT,GPROPNOS  
   7 9_*W          FREECORE 1
   5 =2*6    NOREM   
   7 =2SQ          FREEPER   
   4 =3#B    [   
  21 =3S2    [***********************************************************************
   5 =4?L    POINT7  
   4 =4#P ...#   
   4 =4PT ...#   
   7 =4R=          SUICIDE   
   4 =5=W    [   
  21 =5QG    [***********************************************************************
   5 =6=6    POINT8  
  18 =6PQ    [THIS IS THE ENTRY POINT FROM LISTFREE WHEN A CPA WAITING   
  11 =79B    [FOR ONLINE GETS A NOPER REPLY. 
  18 =7P2          STO   5  AWORK1(2)           [PRESERVE CONSOLE PROP.  
   7 =88L          LDN   4  1
  10 =8N=          LDN   5  JPDPERITYPE  
   9 =97W          STO   6  AWORK2(2)
   9 =9MG    OSGN  LDN   3  AWORK2(2)
   9 ==76          CALL  6  PARAMETER
   8 ==LQ          BRN      OSGN 
   9 =?6B          LDX   0  AWORK1(2)
   8 =?L2          BZE   0  SENTR
   7 =#5L          LDN   4  0
   9 =#K=          LDN   5  JPDNULL  
   9 =*4W          CALL  6  PARAMETER
   7 =*JG          LDN   4  1
   9 =B46    OSGNA LDN   3  AWORK1(2)
   9 =BHQ          LDN   5  JPDPROPS 
   9 =C3B          CALL  6  PARAMETER
   8 =CH2          BRN      OSGNA
   7 =D2L    SENTR COMERRX  7
   4 =DG=    [   
   4 =D_W    [   
  21 =FFG    [***********************************************************************
   5 =F_6    POINT9  
  16 =GDQ    [ENTRY POINT FROM LISTOPEN WHEN A FILE HAS BEEN LOST
   9 =GYB    [AWORK2 CONTAINS GMODE  
  15 =HD2    [THERE IS A GOUT INFO FOR THE REQUEST,AND A COPY
  15 =HXL    [OF THE FABSNB FOR THE LOST FILE IN A GOUT INFO4
  10 =JC=    [ALL FILES HAVE BEEN THAWED 
  10 =JWW          MHUNT    1,GOUT,INFO4 
   9 =KBG          LDX   7  AWORK2(2)
  15 =KW6          BPZ   7  WNOLI               [J NOT ONLINE
   7 =L*Q          FREECORE 1
  10 =LTB          LDX   5  WJSETP15(1)  
  10 =M*2          MHUNTW   3,GOUT,INFO  
   9 =MSL          STO   5  GMODE(3) 
   9 =N#=          CALL  7  WAKECPA  
   8 =NRW          BRN      XCEN 
  10 =P?G    WNOLI NAME     1,FILE,FABSNB
   9 =PR6          CALL  7  STANDMESS
   9 =Q=Q          CALL  7  OUTNAME  
  10 =RPL          MHUNTW   3,GOUT,INFO  
   9 =S9=          LDX   4  GJOBS(3) 
  17 =SCY ...      LDN   0  #2000               [BLFMULTI = BIT 13   
  16 =SLL ...      ANDX  0  AWORK2(2)           [COPY OF GMODE   
  17 =ST# ...      BZE   0  XNOMULT             [J. FILE NOT MULTI   
   7 =T42 ...      LDN   6  1
  19 =T=N ...      SMO      6                  [PICK UPMULTI ERROR MESSAGE   
   5 =TFB ...XNOMULT 
  10 =TN6          LDX   5  WJLFLOST(1)  
   9 =W7Q          CALL  6  SMONOUT  
  10 =WCJ ...      MFREE    FILE,FABSNB  
   9 =WMB          BRN      XLFEND   
   4 =X72    [   
   4 =XLL    [   
  21 =Y6=    [***********************************************************************
   5 =YKW    POINT10 
  17 =_5G    [THIS EN0RY POINT ATTEMPTS TO DEAL WITH A BUSY PRINTER  
  17 =_K6    [THIS SEEMS TO OCCUR AFTER A PAGE THROW WHEN PAPER LOW  
  19 ?24Q          OUTPER                       [INFORM OPERATOR OF PAPER LOW
   9 ?2JB          INFORM   1,ILOAD,1
   9 ?342          JUNBUSY  ,STOPD   
  17 ?3HL          COOR4    #77                  [WAIT A SPRINGCLEAN 
   9 ?43=          JUNBUSY  ,STOPD   
   6 ?4GW          OUTPER
  17 ?52G          INFORM   1,JLFBUSY,1         [TELL OPERATOR BUSY  
   9 ?5G6          LOCKC    2,SLOCKC 
  19 ?5_Q          STOZ     GZDATA(2)           [PREPARE FOR NULL TRANSFER   
  17 ?6FB          LDCT  4  #600                [TO SET US WAITING   
  18 ?6_2          ADN   4  GZDATA(2)           [UNTIL DEVICE OPERABLE   
  10 ?7DL          DCA      ,STO,4,CTSA  
   8 ?7Y=          GPERI    ,2   
   6 ?8CW          COOR1 
   7 ?8XG          UNLOCK   2
   7 ?9C6    STOPD DISENGPER 
   5 ?9WQ          UP
   4 ?=BB    [   
   5 ?=W2    SLOCKC  
  12 ??*L    [S/R USED BY CORE ALLOCATION SYSTEM 
   8 ??T=          LDX   2  FX2  
   7 ?##W          EXIT  1  0
   4 ?#*_ ...[   
   4 ?#C4 ...[   
  21 ?#D7 ...[***********************************************************************
   5 ?#F= ...POINT11 
  21 ?#G* ...[  ENTRY POINT TO LISTFILE ROUTINES TO CLOSE DOWN A NIP LF FOR THE NIPRE
  18 ?#HD ...[  MACRO WHEN ONLY A NIPSTART MACRO HAS SO FAR BEEN ISSUED  
  16 ?#JH ...[  IE FLAG Q IS SET - ENTRY POINT SIMULATES A CANTDO
   4 ?#KL ...[   
   7 ?#LP ...      NGN   0  1
   9 ?#MS ...      STO   0  AWORK1(2)
   9 ?#NX ...      STO   0  AWORK2(2)
   9 ?#Q2 ...      BRN      POINT4   
   4 ?#R5 ...[   
   4 ?#R6 ...[   
  15 ?#R7 ...[ ENTRY POINT TO OUTPUT THE START OF LF MESSAGE 
   4 ?#R8 ...[   
   5 ?#R9 ...POINT12 
   7 ?#R= ...#UNS  ASTRTJL   
   4 ?#R? ...#SKI
  10 ?#R# ...      GEOERR   1,NOSTRTJL   
   7 ?#R* ...#UNS  ASTRTJL   
   4 ?#RB ...(   
  10 ?#RC ...      MHUNTW   3,GOUT,GABSNB
  10 ?#RD ...      NAME     3,FILE,FABSNB
   8 ?#RF ...      LDN   6  24   
   9 ?#RG ...      CALL  7  OUTBLOCK 
   9 ?#RH ...      CALL  7  OUTNAME  
   9 ?#RJ ...      CALL  7  USERJOB  
   9 ?#RK ...      CALL  7  STANDLOG 
  10 ?#RL ...      MHUNTW   3,GOUT,INFO  
   9 ?#RM ...      LDX   4  GJOBS(3) 
  10 ?#RN ...      LDX   5  WJLFSTRT(1)  
   9 ?#RP ...      CALL  6  SMONOUT  
  10 ?#RQ ...      MHUNTW   3,FILE,FABSNB
  10 ?#RR ...      NAME     3,GOUT,GABSNB
   5 ?#RS ...      UP
   4 ?#RT ...)   
   4 ?#SG    #END
   8 ____ ...43251507000100000000
