  14 22FL ...      SEG   INCFIN,864,FILESTORE  ,INCDUMP  
   4 22_=    #   
  11 23DW          SEGENTRY K1INCFIN,X1INCFIN
  11 23NN ...      SEGENTRY K2INCFIN,X2INCFIN
   4 23YG    #   
  21 24D6    #  THIS SEGMENT FINISHES CLEARING UP AFTER AN INCREMENT HAS BEEN DUMPED.
  21 24XQ    #  IT OUTPUTS VARIOUS MESSAGES TO DUMPER'S MON. FILE & THEN GOES DOWN TO
  12 254Y ...#  INCRV IF FILES NEED RETRIEVING.  
   4 26W=    #   
   4 273C ...#   
  11 2756 ...#  AWORK1 SWITCH WORD DEFINITION
   4 276T ...#   
   3 278J ...
  10 27=? ... BITDEFS AWORK1,0,MBFINAL   
   6 27?F ...#UNS AMTGR  
  19 27#M ... BITDEFS AWORK1,2,MBPGENRES [ POTENTIALLY RESTORABLE INCREMENT  
   4 27*W    #   
   6 27TG    #  MESSAGES 
   4 28*6    #   
  14 28SQ    STAT           28HPOTENTIAL COMPRESSED SIZE 
  14 29#B                   28HSIZE OF SYS. FILES READ   
  14 29J7 ...               28HDIRS SCANNED UNNECESSARILY
  15 29LQ ...               28HDIRS SCANNED FOR TERM FILES   
  12 29S2                   28HSIZE OF INCREMENT 
   5 2=?L    STATSIZE
  14 2=R=                   28HNO. OF FILES IN INCREMENT 
  15 2=X7                   28HTIME WAITING FOR DIRECTORIES  
  15 2?34                   28HTIME WAITING FOR BS TRANSFRS  
  15 2?6_                   28HTIME WAITING FOR MT TRANSFRS  
  12 2?=W                   28HTOTAL TIME TO DUMP
  14 2?QG                   28HTIME TO WRITE SENTINELS   
   5 2#=6    STATTIME
  13 2#PQ                   28HTIME TO WRITE FILES   
  16 2#WY    XFIVE           1000/K9             [CLEANCTS/SEC   
   8 2*46    SIXTY          +60  
   9 2*9B    XBLOCKS        6HBLOCKS 
   9 2*F8 ...XENDINC        8HENDINC 
   4 2*P2    #   
  21 2B8L    #  THIS SUBROUTINE OUTPUTS A 24-CHAR. STRING FROM THE TABLE ABOVE AS THE
  11 2BN=    #  1ST PARAMETER OF A MESSAGE.  
  20 2C7W    #  ON ENTRY, X1=FX1,X2=FX2,X5=ADDRESS OF MESSAGE RELATIVE TO ^STAT^.
  17 2CMG    #                                      X6 IS S/R LINK.  
  20 2D76    #  ON EXIT,  X1=FX1,X2=FX2, X3-> FINC BLOCK. USES X0. COORDINATES   
   4 2DLQ    #   
   5 2F6B    OUTBL   
   8 2FL2          SBX   6  FX1  
  16 2G5L          MHUNTW   2,FILE,FINTER     [FIND BUFFER BLOCK 
   7 2GK=          SMO      5
  18 2H4W          ADN   1  STAT                [X1 -> 28-CHAR. MESSAGE  
   8 2HJG          ADN   2  A1   
   7 2J46          MOVE  1  7
  19 2JHQ          OUTPARN  28,A1,FILE,FINTER   [OUTPUT CHAR.PART OF MESSAGE 
  10 2JQ2 ...      MHUNTW   3,FILE,FGET  
   8 2KH2          ADX   6  FX1  
   7 2L2L          EXIT  6  0
   4 2LG=    #   
   5 2L_W    X1INCFIN
   4 2MFG    #   
  19 2M_6          SETNCORE 7,3,FILE,FINTER     [SET UP MESSAGE BUFFER BLOCK 
   4 2NDQ    #   
  19 2NYB    #  FIRST PUT OUT  SIZE^ MESSAGES - VALUES ARE ALL IN FINC BLOCK 
   4 2PD2    #   
  18 2PXL          LDN   5  STATSIZE-STAT       [INITIALISE MESSAGE MOD  
  16 2QC=          LDN   7  STATSIZE-STAT/7+1   [INITIALISE COUNT
   5 2QWW    SLOOP1  
  18 2RBG          CALL  6  OUTBL               [MESSAGE AS 1ST PARAMETER
   7 2RW6          SMO      7
  20 2S*Q          LDX   6  INCCMPSIZE-1(3)     [PICK UP VALUE FROM FINC BLOCK   
  20 2STB          OUTPACK  6,1,NUMA            [ SIZE IN BLOCKS AS 2ND PARAMETER
  11 2T*2          LDN   0  STATSIZE-STAT/7+1
  16 2TSL          BXE   7  0,SKIP              [J IF 1ST MESSAGE
   9 2W#=          ADN   1  XBLOCKS  
   9 2WRW          ADN   2  ACOMMUNE1
   7 2X?G          MOVE  1  2
  19 2XR6          OUTPARN  6,ACOMMUNE1         [^BLOCKS^ AS 3RD PARAMETER   
   4 2Y=Q    SKIP
  15 2YQB          MONOUT   EQUALS              [ %A = %B %C 
  19 2_=2          SBN   5  7                   [DECREASE MESSAGE MODIFIER   
  10 2_D= ...      MHUNTW   2,FILE,FGET  
  17 329=          JBS   NEXTMESS,2,BINCGENRES  [J IF GENERAL RSTABLE
  19 32NW          SBN   7  3                   [OTHERWISE DON'T WANT LAST 2 
  20 338G          BZE   7 SLOOP1OUT           [MESSAGES (SYS FILES READ STUFF)  
   7 33N6          ADN   7  3
   5 347Q    NEXTMESS
   9 34MB          BCT   7  SLOOP1   
   6 3572    SLOOP1OUT   
   4 35LL    #   
  18 366=    #  NOW PUT OUT ^TIME^ MESSAGES - AGAIN,VALUES IN FINC BLOCK.
   4 36KW    #   
  18 375G          LDN   5  STATTIME-STAT       [INITIALISE MESSAGE MOD  
  16 37K6          LDN   7  STATTIME-STATSIZE/7 [INITIALISE COUNT
   5 384Q    SLOOP2  
  19 38JB          CALL  6  OUTBL               [MESSAGE AS FIRST PARAMETER  
   7 3942          SMO      7
  21 39HL          LDX   4  INCDIRWAIT-1(3)     [PICK UP NEXT TIME FROM FINC BLOCK   
   8 39X*          LDX   1  FX1  
  16 3=?4          DVS   3  XFIVE(1)            [X4=TIME IN SECS 
  16 3=LR          DVS   3  SIXTY(1)            [X4=MINS,X3=SECS 
   9 3?2G          OUTPACK  4,1,NUMA 
   9 3?G6          OUTPACK  3,1,NUMB 
  18 3BXG          MONOUT   EQUTIME             [ %A = %B MINS %C SECS   
  19 3CC6          SBN   5  7                   [DECREASE MESSAGE MODIFIER   
   9 3CWQ          BCT   7  SLOOP2   
  17 3DBB          MFREEW   FILE,FINTER         [FREE BUFFER BLOCK   
   4 3DW2    #   
  14 3F*L    #  NOW  GO TO RETRIEVE FILES,IF NECESSARY   
   4 3FT=    #   
  11 3FXS ...      MHUNTW   1,FILE,FINCPROC  
  18 3GSG          LDX   5  INCRV(1)            [NO OF FILES TO BE RVED  
  15 3H#6          BZE   5  NORVS               [J IF NONE   
  19 3HRQ          BBS   3,FINISH,YFIN          [DON'T DO RVS IF FINISHED HAS
  14 3J?B                                       [OCCURRED
  21 3JR2          JBS   YFIN,,BDOGENRES        [DON'T DO RVS IF FILESTORE CORRUPT   
  16 3K=L          DOWN  INCRV,1                [GO AND DO THEM  
   5 3KQ=    NORVS   
   4 3KQL ...#   
  20 3KQQ ...#  IF THE RESTORE TIME MACRO 'INCSTEND' IS INCLUDED AND WE ARE NOT  
  20 3KQW ...#  PART-WAY THRU A PARALLEL DUMP THEN WE ISSUE THE J.D. MACRO ENDINC
   4 3KR2 ...#   
  10 3KR6 ...      MHUNTW   3,FILE,FINC  
  18 3KR= ...      BPZ   3  NOCOM               [J IF MIDWAY THRU PL DUMP
  10 3KRB ...      SEGENTRY  K50INCFIN   
  20 3KRG ...      BRN      NOCOM               [IF INCSTEND MACRO INCLUDED ISSUE
   6 3KRH ...#UNS  AMTGR 
   4 3KRJ ...(   
  19 3KRK ...      JBC      YESCOM,,BMTGR      [ NO PROBLEM IF NOT USING MTGR
  19 3KRL ...      JBC      NOCOM,2,MBFINAL    [NOT FINAL GEN. REST. INCR.   
   5 3KRN ...YESCOM  
   4 3KRP ...)   
  21 3KRQ ...      SETNCORE CPDATA+2-A1,2,ADATA,CREADL [COMMAND 'ENDINC'. THIS ALLOWS
  21 3KRR ...      LDN   0  6                   [SITES TO TAKE SPECIAL ACTION IF NECE
   8 3KRW ...      STO   0  A1(2)
   9 3KS2 ...      ADN   1  XENDINC  
   9 3KS6 ...      ADN   2  CPDATA   
   7 3KS= ...      MOVE  1  2
   8 3KSB ...      ISSUCOM  NOCOM
   5 3KSG ...NOCOM   
  21 3KT6 ...#      *****************************************************************
  15 3KT9 ...#     WE NOW DECIDE WHETHER WE MAY NEED THE FINC
   9 3KT# ...      MHUNTW 3,FILE,FGET
  21 3KTC ...      JBS      ZFREEFINC,3,BINCEOT [ WE CAN'T USE IT IF HIT EOT OR FAIL 
  11 3KTG ...      JBS      NOFREE1,,BNOWD   
  21 3KTK ...      JBS      ZFREEFINC,2,MBFINAL [ FREE IT IF NO BSU REQUEST AND FINAL
   5 3KTN ...NOFREE1 
   9 3KTR ...      HUNTW  1,FILE,FINC
   9 3KTW ...      BNG   1  NOFREE3  
  17 3KT_ ...      LDX   0  INCNO(1)            [ MAY BE PARALLEL INC
  18 3KW4 ...      BXGE  0  INCNO(3),ZFREEFINC  [ HIGHER FINC RETAINED   
   5 3KW7 ...NOFREE3 
   9 3KW= ...      NAME  3,FILE,FINC 
  19 3KW* ...      CHAIN 3,BMISC+1              [ CHAIN ON END OF MISC CHAIN 
   9 3KWD ...      BRN      NOFREE2  
   6 3KWH ...ZFREEFINC   
  19 3KWL ...      FREEFINC 3                   [ WE HAVE NO USE FOR TAPES   
   5 3KWP ...NOFREE2 
  21 3KWS ...#     ******************************************************************
   5 3KXD ...UP    UP
   4 3SK2    YFIN
  16 3SQ8 ...      STOZ     INCRV(1)            [CLEAR RV COUNT  
   5 3SXB ...YFIN2   
  21 3T4L          MFREEW   FILE,FABSNB         [FREE ALL THE FABS, SINCE WONT USE   
  13 3TJ=                                       [THEM
   8 3W3W ...      BCT   5  YFIN2
   8 3WHG          BRN      NORVS
   4 3X36    #   
  21 3X3B ...#     ******************************************************************
   4 3X3L ...#   
  14 3X3W ...#                                  X2INCFIN 
  14 3X46 ...#                                  ******** 
   5 3X4B ...X2INCFIN
  16 3X4L ...#     THIS ENTRY POINT IS AN OVERFLOW FROM INCEND   
   4 3X4W ...#   
  20 3X56 ...#     BEFORE CARRYING ON WE MUST CLEAR THE 'DUMP' BIT FOR ANY TAPE  
   9 3X5B ...#     CLUSTERS WE USED. 
   4 3X5L ...#   
   9 3X5W ...      LDX   2  JPTI+5   
   9 3X66 ...      LDX   5  JPTI+6   
  15 3X6B ...      SBX   5  2                   [NO OF DECKS 
   5 3X6L ...UNSETBIT
  17 3X6W ...      LDX   3  0(2)                [X3->DEVLIST FOR DECK
   9 3X76 ...      DFCLEAR  3,DUMP   
   7 3X7B ...      ADN   2  1
  18 3X7L ...      BCT   5  UNSETBIT            [J IF MORE TAPE DEX ON MC
  10 3X7W ...      MFREEW   FILE,FQUAL   
   4 3X86 ...#   
  16 3X8B ...#  IF WE ARE FINISHED WE ENDCOM OTHERWISE WE REENTER
  20 3X8L ...#  SEGMENT INCWAIT TO COMMENCE PREPARATIONS FOR ANOTHER INCREMENT   
   4 3X8W ...#   
  18 3X96 ...      HUNTW    3,FILE,FINC         [J IF STILL PRODUCING INC
   9 3X9B ...      BPZ   3  NFINAL   
  15 3X9L ...      MFREEW   FI,FDUMPFAB         [FREE FABSNB 
  19 3X9W ...      BC       ,BPARALLEL          [IF TWAS PARALLEL TAINT NOW  
  18 3X=6 ...      JBC      NFINAL,2,MBFINAL    [J IF MORE INCS NEEDED   
  18 3X=B ...      JBS   XCORRPT,,BDOGENRES     [J IF FILESTORE CORRUPT  
  20 3X=L ...      PERMITBRK                    [ALLOW BREAKIN FOR JOB DESCRIPTS 
  14 3X=W ...                                   [BENEFIT 
  19 3X?6 ...      VFREEW   FILE,FINCPROC       [DISPOSE OF FINCPROC, IF ONE 
   6 3X?B ...#UNS  JSKI33
  18 3X?L ...      BC       2,EXDUMPER          [CLEAR 'DUMPER CPAT' BIT 
  21 3X?W ...      BC    ,BIDCOM                [END OF INCDUMP DUMP OR NOW COMMAND  
   5 3X#6 ...#UNS IDC
   4 3X#B ...(   
   4 3X#L ...#   
   4 3X#W ...#   
  20 3X*6 ...      JBS      ZIGNORE,,BNOWD       [ JUMP IF NO UNJAMMER REQUEST   
  20 3X*B ...      JBCC  ZIGNORE,,BDCISS         [ JUMP IF NO STORED DUMP COMMAND
  10 3X*L ...      HUNTMISB  1,FI,FDUCOM 
   8 3X*W ...      FREECORE  1   
  20 3XB6 ...      MONOUT  INCIGNORED            [ STORED DUMP COMMAND IGNORED[  
  12 3XBB ...#     NO FURTHER INCREMENTS NEEDED  
   5 3XBL ...ZIGNORE 
  18 3XBN ...      BITDEFS   CLONG1,1,XBSPRIV    [ BS PRIVILEGED MARKER  
  18 3XBQ ...      BC       2,XBSPRIV              [ CLEAR BS PRIVILEGE  
   4 3XBW ...#   
   4 3XC6 ...)   
   6 3XCB ...      ENDCOM
   5 3XCL ...XCORRPT 
  11 3XCW ...      GEOERR   BRIEFPM,BAD FS?? 
   5 3XD6 ...NFINAL  
  18 3XDB ...      LDCT  7  #400                ['DO NOT WAIT ' MARKER   
  19 3XDL ...      ACROSS   INCWAIT,1           [GO ACROS FOR ANOTHER INCR   
   4 3XDW ...#   
  21 3XF6 ...#     ******************************************************************
   4 3XFB ...#   
  10 3XGQ          MENDAREA 20,K99INCFIN 
   4 3Y2B    #   
   4 3YG2    #END
   8 ____ ...43367630000200000000
