  14 22FL    #OPT  K0DEAD=K0LIBRARY>K0ALLGEO>K0GREATGEO  
   6 22_=    #LIS  K0DEAD
  13 23DW          SEG      DEAD,1,N.R.BOULT,LIBRARY 
   4 23L4 ...[   
  15 23R= ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23YG    #   
  11 24D6          SEGENTRY K1DEAD,QK1DEAD   
  11 24XQ          SEGENTRY K2DEAD,QK2DEAD   
  11 25CB          SEGENTRY K3DEAD,QK3DEAD   
   4 25X2    #   
  15 26BL          FSHENTRY K4DEAD,QK4DEAD,QK4DEAD,QK4DEAD   
  15 26W=          FSHENTRY K5DEAD,QK5DEAD,QK5DEAD,QK5DEAD   
  15 27*W          FSHENTRY K7DEAD,QK7DEAD,QK7DEAD,QK7DEAD   
   4 27TG    #   
  13 28*6    #   ENTRY FOR UNANTICIPATED CLOSEDOWN   
   4 28SQ    #   
  13 29#B          FSHENTRY K8DEAD,QK8DEAD,,QK8DEAD  
   4 29S2    #   
  17 2=?L    #     THE DEAD SEGMENT ELIMINATES FROM THE SYSTEM TAPES 
  18 2=R=    #     WHOSE SERIAL NUMBERS ARE HELD IN AN AONBS/GLIST BLOCK 
   9 2?=W    ZNOTOWN        +ERNOTOWN
  10 2?QG    ZNOTKNOW       +ERUNKNOWN   
   9 2#=6    ZQUERY         +ERQUERY 
   9 2#PQ    ZNOTPW         +ERNOTPW 
   9 2*9B    ZUDUMP         +HDINUSE 
   9 2*P2    ZNOTRAP        +ERNOTRAP
   9 2C7W    ZG3PT          +ERG3PT  
   9 2CMG    NDUMPER        12HDUMPER
  11 2D76    NPROCTP        12HGEO3PROCTAPE  
   8 2F6B    NLANG          4HC1 
   4 2FL2    #   
   4 2LG=    #   
   4 2L_W    #   
   5 2MFG    QK1DEAD 
  13 2QC=          OPENSYS  YBRKIN,DOCUMENT,GENERAL  
  12 2QWW          OPENSYS  XBRKIN,SERIAL,GENERAL
  10 2RBG          MHUNT    1,AONBS,GLIST
   9 2RW6          STOZ     AWORK2(2)
   7 2S*Q          LDN   0  1
   9 2STB          STO   0  AWORK3(2)
   5 2T*2    TSERLOOP
  10 2TSL          LDX   0  ALOGLEN(1)   
   9 2W#=          LDX   6  AWORK2(2)
   5 2WRW    TSER1   
   7 2X?G          ADN   6  1
  18 2XR6          BXE   6  0,TEND              [BRANCH IF END OF GLIST  
   7 2Y=Q          SMO      6
   8 2YQB          LDX   7  A1(1)
   8 2_=2          BZE   7  TSER1
   9 2_PL          STO   6  AWORK2(2)
  10 329=          LDX   0  CONTEXT(2)   
   7 32NW          ANDN  0  1
  19 338G          BZE   0  TSER3               [IF COMMAND IN USER CONTEXT  
  20 33N6          LDX   3  A1(1)               [THE APPROPIATE ADJUNCTS BLOCK   
  19 347Q          LDX   1  2                   [MUST BE CHAINED NEXT TO CPAT
   5 34MB    TSER2   
  12 3572          HUNT2J   1,FILE,ADJUNCTS,,XADJ
   8 35LL          BCT   3  TSER2
   8 366=          CHAIN    1,2  
   5 36KW    TSER3   
  10 375G          SSEARCH  NOTKNOWN,7   
   9 37K6          STO   7  AWORK1(2)
   8 384Q          LDX   0  5(3) 
   7 38JB          SLL   0  2
  18 3942          BNG   0  NOTKNOWN            [BRANCH IF 'TO BE DEADED'
   7 39HL          SLL   0  4
  20 3=3=          BNG   0  QUERY               [BRANCH IF TAPE AT QUERY STATUS  
  10 3=GW          LDX   0  CONTEXT(2)   
   7 3?2G          ANDN  0  1
  18 3?G6          BNZ   0  USER                [BRANCH IF USER CONTEXT  
   8 3?_Q          LDX   0  2(3) 
   8 3#FB          BZE   0  TDEL 
   8 3#_2          LDCT  0  #200 
   8 3*DL          ANDX  0  5(3) 
  20 3*Y=          BZE   0  NOTPW               [BRANCH IF NOT A USED WORKTAPE   
   8 3BCW          BRN      USED1
   4 3BXG    TDEL
  19 3CC6          DELETE                       [DELETE ENTRY FOR THIS TAPE  
  13 3CWQ          LDX   7  AWORK1(2)           [TSN 
  19 3GSG          FINDPEREC 3,APTSER,7         [FIND PERIPHERAL LIST ENTRY  
   8 3QLG          BNG   3  NXT  
   9 3S5B          DFCLEAR  3,POOL   
   9 3SK2          DFCLEAR  3,WORK   
  16 3W3W          LDX   4  3                   [SDSEARCH USES X3
  18 3WHG          LDN   5  0                   [:SYSTEM.DOCUMENT IS OPEN
  20 3X36          LDX   0  AWORK3(2)           [AT LEVEL 2 IF :SYSTEM.INCINDEX  
  18 3XGQ          BNZ   0  SDOC                [HAS BEEN OPENED HERE  - 
  17 3Y2B          ADN   5  1                   [OTHERWISE AT LEVEL 1
  10 3Y_L          SDSEARCH USD1,TSN,7,2 
   8 42DG          BRN      SDFND
   4 42Y6    SDOC
  10 43XB          SDSEARCH USD1,TSN,7,1 
   5 45B=    SDFND   
   7 45TW          LDX   1  5
   8 46*G          READAGAIN 1(1)
  10 46T6          MHUNT    3,FILE,FRB   
   8 47#Q          LDCT  0  #200 
  16 47SB          ORS   0  A1+21(3)            [SET INSECURE BIT
  10 48#2          NAMETOP  3,FILE,FWB   
   7 4?9Q          LDX   1  5
   8 4?PB          REWRITE  1(1) 
   9 4#NL          MFREE    FILE,FWB 
   4 4*8=    USD1
   7 4*MW          REWIND   1
  17 4GJ6          JALLOC   4,NXT               [J IF DECK ALLOCATED 
  16 4H3Q          SETALLOC 4                   [ALLOCATE DECK   
  16 4HHB          UNLOAD   4,,,AUT,UNL         [AND UNLOAD IT   
   4 4J32    NXT 
  10 4LF6          MHUNT    1,AONBS,GLIST
   8 4LYQ          LDX   2  FX2  
   9 4MDB          SMO      AWORK2(2)
  16 4MY2          STOZ     A1(1)               [TAPE DEALT WITH 
   7 4NCL          LDN   0  1
   8 4NX=          SBS   0  A1(1)
   9 4PBW          BRN      TSERLOOP 
  14 4PWG    #     TAPE IS BEING DEADED FROM DIRECTORY   
   4 4QB6    USER
  15 4QTQ          LDX   4  2(3)                [USER NAME   
   8 4R*B          LDX   5  3(3) 
   8 4RT2          LDX   6  4(3) 
  14 4S#L          LDX   7  5(3)                [ODD BITS
  11 4SS=    #SKI           K6DEAD>199-199   
  10 4T?W          TRACE    5(3),DEADSS  
  19 4TRG          BZE   4  NOTOWN              [BRANCH IF POOL OR WORK TAPE 
  21 4W?6          BNG   7  NOTOWN              [BRANCH IF TAPE MARKED TO BE RETURNED
   7 4WQQ          SLL   7  1
  18 4X=B          BNG   7  NOTOWN              [BRANCH IF USED WORK TAPE
  10 4Y9L          TXU   4  CPREFIX(2)   
  10 4YP=          TXU   5  CPREFIX+1(2) 
  10 4_8W          TXU   6  CPREFIX+2(2) 
  21 4_NG          BCC      USER1               [BRANCH IF TAPE IN PROPER USERS DIR  
  11 5286          SETNCORE 4,3,FILE,FABSNB  
   7 52MQ          LDN   1  4
   8 537B          STO   1  A1(3)
   9 53M2          LDN   2  A1+1(3)  
  20 546L          MOVE  1  3                   [MOVE USER NAME TO FILE/FABSNB   
   7 54L=          CHECKOWNER
   9 555W          TESTREPN OK,NOTOWN
  18 5656    USER1                              [UPDATE SPACEMT BUDGET   
   5 56JQ    USER3   
   8 574B          CALL  7  OPDIR
  10 57J2          MHUNT    1,FILE,ENT   
  10 583L          LDX   0  EINCLOOK(1)  
  19 58H=          BNG   0  UDUMP               [BRANCH IF DUMP TAPE IN USE  
   8 592W          LDX   3  FX1  
  16 59GG          TXU   4  NDUMPER(3)          [CHECK USERNAME  
  15 5=26          TXU   5  NDUMPER+1(3)        [FOR :DUMPER 
  10 5=FQ          TXU   6  NDUMPER+2(3) 
   9 5=_B          BCS      PROCOK   
   9 5?F2          LDX   0  ELOC1(1) 
  16 5?YL          BXU   0  NPROCTP(3),PROCOK   [CHECK LOCAL NAME
  16 5#D=          LDX   0  ELOC2(1)            [FOR GEO3PROCTAPE
  12 5#XW          BXU   0  NPROCTP+1(3),PROCOK  
   9 5*CG          LDX   0  ELOC3(1) 
  12 5*X6          BXU   0  NPROCTP+2(3),PROCOK  
   9 5BBQ          LDX   0  AWORK3(2)
  18 5BWB          BZE   0  PROC4               [J IF INCINDEX OPENED OK 
  17 5CB2          BNG   0  PROC1               [J IF CLUDGE REPLY   
  10 5CTL    #     OPEN :SYSTEM.INCINDEX 
  15 5D*=          CLOSETOP                     [DIRECTORY   
  16 5DSW          CLOSETOP                     [:SYSTEM.SERIAL  
  14 5F#G          OPENSYS  XBRKERR,INCINDEX,READ,QUERY  
   9 5FS6          TESTREP  OK,PROC3 
  11 5G?Q          TESTREPN CLUDGE,XINCERR   
  18 5GRB          NGN   0  1                   [SET CLUDGE REPLY MARKER 
   9 5H?2          STO   0  AWORK3(2)
  13 5HQL          OPENSYS  XBRKERR,SERIAL,GENERAL   
   8 5J==          BRN      PROC2
   5 5JPW    PROC1   
   7 5K9G          CLOSETOP  
   5 5KP6    PROC2   
  19 5L8Q          LDX   4  ZG3PT(1)            [SET ID FOR CLUDGE ERROR MSG 
   8 5LNB          BRN      NOT  
   5 5M82    PROC3   
  20 5MML          STOZ     AWORK3(2)           [SET INCINDEX OPENED OK MARKER   
  13 5N7=          OPENSYS  XBRKERR,SERIAL,GENERAL   
  17 5NLW          LDN   0  1                   [STEP BACK POINTER TO
  16 5P6G          SBS   0  AWORK2(2)           [AONBS/GLIST BY 1
  10 5PL6          MHUNT    1,AONBS,GLIST
   9 5Q5Q          BRN      TSERLOOP 
   5 5QKB    PROC4   
   9 5R52          LDX   7  AWORK1(2)
  19 5RJL          TSININC  UDUMP,7,2           [CHECK FOR TSN IN INCINDEX   
   5 5S4=    PROCOK  
   7 5SHW          SETUPTAB  
  18 5T3G          TABSET                       [REMEMBER POSITION IN DIR
   7 5TH6          CHECKTRAPS
   9 5W2Q          LDN   0  CERASET  
  10 5WGB          ANDX  0  ACOMMUNE7(2) 
   8 5X22          BZE   0  XNERT
  19 5XFL          TABULATE                     [GET BACK TO OLD PLACE IN DIR
   7 5X_=          FREETAB   
  10 5YDW          MFREE    FILE,FABSNB  
  18 5YYG          SETNCORE 4,3,FILE,FABSNB     [SET UP A FABSNB TO HOLD 
  19 5_D6          LDN   1  4                   [THE USERNAME OF THE OWNER   
  16 5_XQ          STO   1  A1(3)               [OF THE ENTRANT  
  17 62CB          LDN   2  A1+1(3)             [TO UPDATE THE BUDGET
   7 62X2          MOVE  1  3
   8 63BL          OPENBUDO XBUD 
   7 63W=          NGN   3  1
  10 64*W          BUDGINCR GSPACEMT,3   
   7 64TG          CLOSBUDG  
   8 65*6          LOGACCESS 5   
   8 65SQ          STEPAGAIN 1   
   8 66#B          LDX   0  5(3) 
  19 66S2          ANDN  0  #7777               [EXTRACT COUNT OF REQUESTERS 
  18 67?L          BNZ   0  USED                [BRANCH IF TAPE IN USE   
  10 67R=          MHUNT    1,FILE,ENT   
  18 68=W          LDX   4  ENUSE(1)            [NUMBER OF TRAP RECORDS  
   9 68QG          LDX   5  ELOC1(1) 
  11 69=6    #SKI           K6DEAD>299-299   
  10 69PQ          TRACE    4,DEADTRAP   
   7 6=9B          FREECORE 1
  17 6=P2          BZE   4  USER4B              [J IF NO TRAP RECORDS
   7 6?8L          LDX   7  4
   5 6?N=    USER4   
   6 6#7W          STEP  
   8 6#MG          BCT   7  USER4
   5 6*76    USER4B  
   7 6*LQ          ADN   4  1
   5 6B6B    USER4A  
   6 6BL2          DELETE
   9 6C5L          BCT   4  USER4A   
   5 6CK=    USER5   
   7 6D4W          CLOSETOP  
   8 6JFG          LONGON   #12,5
  18 6Q*2          CALL  5  MONDEAD             [OUTPUT OPERATOR MESSAGE 
  10 6QSL          MFREEW   FILE,ADJUNCTS
   8 6R#=          BRN      TDEL 
   4 6RRW    USED
  10 6S?G          MFREEW   FILE,ADJUNCTS
  10 6SR6          MHUNT    3,FILE,ENT   
   8 6T=Q          LDCT  0  #40  
  20 6TQB          ORS   0  EINF2(3)            [MARK DIR. ENTRY 'TO BE ERASED'  
  11 6W=2    #SKI           K6DEAD>199-199   
  11 6WPL          TRACE    EINF2(3),DEDEINF 
  10 6X9=          NAMETOP  3,FILE,FWB   
  17 6XNW          REWRITE                      [REWRITE DIR. ENTRY  
   7 6Y8G          CLOSETOP  
   7 6YN6          STEPAGAIN 
   5 6_7Q    USED1   
   7 6_MB          STEPWRITE 
   8 7272          LDCT  0  #100 
  18 72LL          ORS   0  5(3)                [MARK TAPE 'TO BE DEADED'
   8 736=          BRN      NXT  
  10 73KW    XBUD  GEOERR   1,NOUSERDD   
   9 745G    XADJ  GEOERR   1,NOADJDD
  21 74K6    #     SET UP A FILE/FABSNB BLOCK. USER NAME IN X4,5,6 AND TSN IN AWORK2 
   5 754Q    OPDIR   
   8 75JB          SBX   7  FX1  
  11 7642          SETNCORE 12,2,FILE,FABSNB 
   8 76HL          LDN   0  10   
   8 773=          STO   0  A1(2)
   8 77GW          LDN   0  #2001
   9 782G          DSA   0  ATYPE(2) 
   8 78G6          SMO      FX2  
  13 78_Q          LDX   0  AWORK1              [TSN 
   9 79FB          STO   0  A1+11(2) 
   7 79_2          NGN   0  1
   9 7=DL          STO   0  A1+10(2) 
   7 7=Y=          LDN   1  4
   8 7?CW          ADN   2  A1+1 
   7 7?XG          MOVE  1  3
   8 7#C6          LDN   1  3(2) 
   8 7#WQ          LDN   2  4(2) 
   8 7*BB          STOZ     0(1) 
   7 7*W2          MOVE  1  5
  17 7B*L          OPENDIR  XBRKERR,GENERAL     [OPEN TAPES DIRECTORY
   9 7BT=          TESTREP  OK,OPD1  
   9 7C#W          GEOERR   1,DEADREP
   4 7CSG    OPD1
   8 7D#6          ADX   7  FX1  
   7 7DRQ          EXIT  7  0
   4 7F?B    TEND
   9 7FR2          LDX   0  AWORK3(2)
   8 7G=L          BNZ   0  TEND1
   7 7GQ=          CLOSETOP  
   5 7H9W    TEND1   
   7 7HPG          CLOSETOP  
   7 7J96          CLOSETOP  
   6 7JNQ          ENDCOM
   5 7K8B    XNERT   
   7 7KN2          CLOSETOP  
  10 7L7L          LDX   4  ZNOTRAP(1)   
   8 7LM=          BRN      NOT  
   5 7M6W    UDUMP   
   7 7MLG          CLOSETOP  
   9 7N66          LDX   4  ZUDUMP(1)
   8 7NKQ          BRN      NOT  
   5 7P5B    NOTPW   
   8 7PK2          SMO      FX1  
   9 7Q4L          LDX   4  ZNOTPW   
   8 7QJ=          BRN      NOT  
   5 7R3W    NOTOWN  
   8 7RHG          SMO      FX1  
   9 7S36          LDX   4  ZNOTOWN  
   8 7SGQ          BRN      NOT  
   5 7T2B    NOTKNOWN
   8 7TG2          SMO      FX1  
   9 7T_L          LDX   4  ZNOTKNOW 
   8 7WF=          BRN      NOT  
   5 7WYW    QUERY   
   8 7XDG          SMO      FX1  
   9 7XY6          LDX   4  ZQUERY   
   4 7YCQ    NOT 
   8 7YXB          SMO      FX2  
   9 7_C2          LDX   5  AWORK2   
   7 7_WL          SPARANOX 5
   7 82B=          ERRORX   4
  10 82TW          LDX   0  CONTEXT(2)   
  20 83*G          ANDN  0  1                   [FREE ADJUNCTS IF USER CONTEXT   
   8 83T6          BZE   0  NOTUS
  10 84#Q          MFREEW   FILE,ADJUNCTS
   5 84SB    NOTUS   
   9 85#2          MFREE    CPB,CUNI 
   8 85RL          BRN      NXT  
   5 86?=    QK2DEAD 
  13 86QW    [   MOVED TO DEADA FOR REASONS OF SPACE 
   5 87=G    QK3DEAD 
  13 87Q6    [    MOVED TO DEADA FOR REASONS OF SPACE
   9 889Q          GEOERR   1,NOTDEAD
   5 88PB    XINCERR 
   9 8992          GEOERR   1,DDNOINC
   5 89B8 ...XBRKERR 
   9 89HB ...      GEOERR   1,DEADBRK
   5 89NL    ZBRKIN  
   7 8=8=          CLOSETOP  
   5 8=MW    XBRKIN  
   7 8?7G          CLOSETOP  
   5 8?M6    YBRKIN  
   7 8#6Q          COMBRKIN  
   5 8#LB    MONDEAD 
   8 8*62          SBX   5  FX1  
   8 8*KL          LDX   2  FX2  
   9 8B5=          LDX   7  AWORK1(2)
  10 8BJW          CONSERNO 7,ACOMMUNE1  
  13 8C4G          OUTPARAM ACOMMUNE1(2),ACOMMUNE2   
   8 8CJ6          MTINFORM GDEAD
   8 8D3Q          ADX   5  FX1  
   7 8DHB          EXIT  5  0
   4 8F32    #   
  20 8FGL    #    ENTRY POINTS TO CHECK 'OTHER MACHINE IN SHARED FILESTORE ENVIR.
   4 8G2=    #   
  10 8XMQ          MENDAREA 50,K99DEAD   
   4 8Y7B    #END
   6 ____ ...626703140002
