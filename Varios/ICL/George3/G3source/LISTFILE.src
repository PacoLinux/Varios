  10 22FL    #OPT           K0LISTFILE=0 
  12 22_=    #LIS           K0LISTFILE>K0ALLGEO  
  11 233N ... SEG LISTFILE,,,,G253,G502,G503 
   4 2366 ...[   
  15 238J ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  13 23?2 ...[     THIS EXCLUDES CODE UNDER #SKI G253
  13 23*D ...[                    AND UNDER #SKI G502
  13 23CW ...[                    AND UNDER #SKI G503
   4 23G# ...[   
   7 23JQ ...#OPT G155 = 0   
   7 23M8 ...#OPT G253 = 0   
   6 23PL ...#SKI G253&1 
  16 23S4 ...# WITH UGUG EDIT M253 (LISTFILE IN NO-USER CONTEXT) 
   7 23WG ...#OPT G254 = 0   
   7 23YY ...#OPT G502 = 0   
   6 243B ...#SKI G502&1 
  17 245S ...# WITH UGUG EDIT M502 (SHIFT AND GRAPHICS OUTPUT ON MOP)
   7 248= ...#OPT G503 = 0   
   6 24=N ...#SKI G503&1 
  18 24*6 ...# WITH UGUG EDIT M503 (PAGING AND FRAMING ON MOP LISTFILES) 
  11 24D6          SEGENTRY K1LISTFILE,POINT1
  11 24MY ...      SEGENTRY K2LISTFILE,POINT2
   4 24XQ    [   
  21 25CB    [THIS SEGMENT IS ENTERED BY THE COMMAND PROCESSOR. IT UNPACKS ALL THE   
  19 25X2    [PARAMETERS AND CHECKS ALL BUT THE FILENAME,PROPERTY OR SETUP.  
  21 26BL    [A GOUT/INFO BLOCK IS SET UP TO CONTAIN THE RESULTS OF THES ANALYSIS.   
   4 26W=    [   
  21 27*W    [***********************************************************************
   6 27TG    [DATA AREA  
   4 28*6    [   
  19 28SQ    [LIST OF PARAMETER CHARACTER STRINGS AND CORRESPONDING MODIFIERS
  20 29#B    [!! ** NONE OF THESE WORDS MUST BE ALTERED, REMOVED, SHUFFLED ETC   
   5 29S2    PARSTR  
  17 2=?L          +6                           [ NUMBER PARAMETER   
   7 2=R=          8HNUMBER  
   7 2?=W          +NUMBER   
   5 2?QG    QMONF   
  17 2#=6          +7                           [ MONFILE PARAMETER  
  21 2#PQ          8HMONFILE                    [ NOTE-THIS IS ASSUMED TO BE NOT THE 
  17 2*9B          +PMONF                       [ TABLE ENTRY USED   
   5 2*P2          +2
   6 2B8L          4HTO  
   5 2BN=          +0
   6 2C7W          +STO  
  16 2CMG          +4                           [ FROM PARAMETER 
   6 2D76          4HFROM
   4 2DLQ    TM  
  18 2F6B          #7700000                     [ PERIPHERAL TYPE MASK   
   6 2FL2          +SFROM
   5 2G5L          +5
  16 2GK=          8HLINES                      [ LINES PARAMETER
   7 2H4W          +SLINES   
  18 2HJG          +4                           [ PAGE RESTART PARAMETER 
   6 2J46          4HPAGE
   5 2JHQ    OUTMK   
   6 2K3B          #26   
   7 2KH2          +RESTPAGE 
  18 2L2L          +3                           [ ALL RESTART PARAMETER  
   6 2LG=          4HALL 
   5 2L_W    SPECMK  
  16 2MFG          #05                          [ SPECIAL MASK   
   7 2M_6          +RESTALL  
  17 2NDQ          +7                           [ SPECIAL PARAMETER  
   7 2NYB          8HSPECIAL 
   7 2PD2          +SPECIAL  
  19 2W#=          +1                           [ PERIPHERAL PARAMETER MUST  
  20 2WRW          4H*                          [  ALWAYS BE AT BOTTOM OF TABLE  
   5 2X?G    NUMBMK  
  15 2XR6          #25                          [ NUMBER MASK
   7 2Y=Q          +PARPER   
   4 2YQB    [   
  17 2_=2    [ADDRESS OF MONFILE PARAMETER,WHICH IS ONLY RELEVANT FOR
  10 2_PL    [INTERNALLY-ISSUED COMMANDS 
   4 329=    [   
   5 32NW    XQMONF  
   8 338G          +QMONF-PARSTR 
   6 33*N ...#SKI G502&1 
  11 33GW ...XQALL          +SPECMK-2-PARSTR 
   4 39HL    [   
  21 3=3=    [***********************************************************************
   4 3=GW    [   
   6 3?2G    SCHNUMCOD   
   4 3?G6    [   
  21 3?_Q    [***********************************************************************
   4 3#FB    [   
   4 3#_2    [   
  11 3*DL    [ CONVERT PARAMETER TO BINARY   
  12 3*Y=    [ REPLY CHNUMERR IF NOT VALID NUMBER
  14 3BCW    [ IF VALID NUMBER CHECK 0 < NUMBER < 2_21   
   4 3BXG    [   
   8 3CC6          SBX   6  FX1  
   7 3CWQ          CHNUMCOD 1
  10 3DBB          MHUNT    3,GOUT,INFO  
   8 3DW2          ADX   6  FX1  
  10 3F*L          TESTREP2 CHNUMERR,(6) 
  10 3FT=          LDX   4  ACOMMUNE1(2) 
  16 3G54          BNG   4  NEGNO               [ J IF NEGATIVE  
   7 3G#W          LDX   0  4
   7 3GSG          SLC   0  3
   7 3H#6          ANDN  0  7
  15 3HRQ          BNZ   0  SOUTOFRANGE         [ J IF > 2_21
   7 3J?B          EXIT  6  0
   4 3JR2    [   
   4 3K=L    [   
  21 3KQ=    [***********************************************************************
   9 3L9W    [CENTRAL BODY OF SEGMENT
  21 3LPG    [***********************************************************************
   7 3LS5 ...#SKI  G253&1$1  
   4 3LWN ...(   
   5 3L_? ...POINT2  
   9 3M3W ...      GEOERR   1,M253   
   4 3M6F ...)   
   5 3M96    POINT1  
   6 3M=7 ...#SKI G253&1 
   4 3M?8 ...(   
  10 3M#9 ...      BCONTEXT USER,2,USER  
  19 3M*= ...      ACROSS   LIST,1              [ TREAT LF IN NO-USER CONTEXT
  20 3MB? ...USER                               [  AS EQUIVALENT TO LIST COMMAND 
  21 3MC# ...      STOZ     AWORK2(2)           [ INDICATES STANDARD LF, NOT 'LIST'  
  19 3MD* ...      STOZ     AWORK4(2)           [ NO PARAMETERS ACCESSED YET 
   4 3MFB ...#   
  17 3MGC ...#     ENTRY FROM 'LIST' - AWORK2 = 1, PARAM 1 PROCESSED 
  15 3MHD ...#     AWORK4 = 1 (OR 2 IF 'PASSWORD' PROCESSED) 
   5 3MJF ...POINT2  
   4 3MKG ...)   
   6 3MLH ...#SKI G502&1 
  18 3MMJ ...      STOZ     AWORK3(2)           [ NO PERIPHERAL PARAMETER
   4 3MNQ    [   
  19 3N8B    [THE GOUT INFO BLOCK IS SET UP AND THE RELEVENT LOCATIONS ARE   
  16 3NN2    [ZEROISED OR FILLED WITH APPROPRIATE INFORMATION.   
   4 3P7L    [   
  10 3QLG    [SECURITY INTERFACE MACRO   
   8 3R66          SINMAC3  LF   
   7 3S5B          PARANUMB 7
   6 3S=J ...#SKI G253&1 
  17 3SCQ ...      SBX   7  AWORK4(2)           [ NUMBER ALREADY DONE
  18 3SK2          BZE   7  NOFN                [ BRANCH IF NO PARAMETERS
  19 3T4L          SBN   7  1                   [ STORE NUMBER OF PARAMETERS 
  18 3TJ=          STO   7  AWORK4(2)           [ MINUS 1 IN WORK AREA   
  18 3W3W          STOZ     AWORK1(2)           [ CHECK FOR TO & LINES   
   7 3WHG          SPARAPAS  
  19 3X36          COMPACT                      [ REMOVE REDUNDANT SPACES.   
  18 3XGQ          MHUNT    3,CPB,CUNI          [ FIND FILENAME PARAMETER
  17 3Y2B          NAMETOP  3,FILE,FNAME        [ AND NAME ITS BLOCK 
  21 3YG2          LDX   4  ANUM(3)             [ CALCULATE HOW MANY WORDS IT TAKES  
  13 3Y_L          ANDN  4  #7777               [ UP 
   8 3_F=          BZE   4  NOFN 
  18 3_YW          ADN   4  GHEAD-A1*4+3        [ GET TOTAL LENGTH NEEDED
   7 42DG          SRL   4  2
  18 42Y6          SETUPCORE 4,2,GOUT,INFO      [ SETUP GOUT INFO BLOCK  
  16 437Y          STO   4  A1(2)               [ RECORD HEADER  
   9 43CQ          LDN   5  A1+1(2)  
   9 43XB          LDN   6  A1+2(2)  
   9 44C2          STOZ     A1+1(2)  
  10 44WL          MOVE  5  GHEAD-A1-1   
  19 45B=          MHUNT    3,FILE,FNAME        [ RELOCATE FILE NAME BLOCK   
  20 45TW          LDX   5  ANUM(3)             [ GET NO OF CHARS IN FILE NAME   
   8 46*G          ANDN  5  #7777
  18 46T6          STO   5  GSUSE(2)            [ STORE WITH USER NAME   
   9 47#Q          SBN   4  GHEAD-A1 
   9 47SB          LDN   5  APARA(3) 
   9 48#2          LDN   6  GHEAD(2) 
   7 48RL          SMO      4
   7 49?=          MOVE  5  0
   8 49QW          LDX   2  FX2  
   6 49SD ...#SKI G253&1 
   4 49W2 ...(   
   9 49XJ ...      LDX   0  AWORK2(2)
  21 49_6 ...      BZE   0  NLIST               [ NO SPECIAL ACTION IF STANDARD LF   
  16 4=2N ...      JBS      NLIST,2,CXTUSER     [  OR CAFE LIST  
  21 4=4= ...      FNORM    3                   [ USING CPREFIX IF NO-USER CONTEXT   
   9 4=5S ...      TESTRPN2 OK,SOFIN 
   5 4=7B ...NLIST   
   4 4=8Y ...)   
  18 4==G          LDX   6  AWORK4(2)           [ TEST IF ONLY FILENAME  
  18 4=Q6          BZE   6  XENFJ               [ PARAMETER IS PRESENT   
  20 4?9Q          LDN   7  32                  [ SET PARAMETER STRINGS MODIFIER 
   4 4?PB    [   
   4 4*8=    SAG 
  20 4*MW          TXU   7  XQMONF(1)           [ IF ^MONFILE^ IS THE NEXT CHAR  
  18 4B7G          BCS      SAG01               [ STRING IN THE TABLE,   
   8 4B#N ...#SKI G155$1&G253&1  
  21 4BFW ...      BRUSEN   RET,2               [ AVOID FJOCA IF IN NO-USER CONTEXT  
  20 4BM6          MHUNT    3,CPB,CALAS         [ AVOID PROCESSING IT UNLESS THE 
  20 4C6Q          LDX   0  CPBCPLEV(3)         [ LF COMMAND IS INTERNALLY ISSUED
  11 4CLB          [        OR SYSTEM STARTED
  16 4D62          BNG   0  SAG01               [ J TO PROCESS   
  18 4DKL          FJOCA    3,FX2               [ GET POINTER TO JOBLOCK 
  17 4F5=          SPRIVJUMP JSISSUE,3,SAG01    [ J IF SYSTEM ISSUED 
  21 4FJW          SPRIVTEST JSSTART,3,RET      [ J IF JOB NOT SYSTEM STARTED TO RET 
   5 4G4G    SAG01   
   6 4G6D ...#SKI G502&1 
   4 4G8B ...(   
   7 4G=# ...      LDN   0  0
  20 4G#= ...      TXU   7  XQALL(1)            [ 'ALL' RESTART NOT APPLICABLE   
  18 4GB8 ...      TXU   0  AWORK3(2)           [  TO ONLINE LISTING -   
  19 4GD6 ...      BCC      RET                 [  AVOID CLASH WITH 'ALLCHAR'
   4 4GG4 ...)   
  21 4GJ6          ADX   1  7                   [ SET MODIFIER FOR PARAMETER TABLE   
  18 4H3Q          LDX   5  PARSTR(1)           [ GET LENGTH OF PARAMETER
   5 4HHB    SAG02   
  17 4J32          LDN   3  0                   [ SET ANALYSIS LEVEL 
  12 4JGL          PARABEG  1,5,PARSTR+1(1),3,0  
  10 4K2=          MHUNT    3,CPB,CUNI   
   9 4KFW          LDX   4  ANUM(3)  
   8 4K_G          BNG   4  XFREE
  16 4LF6          LDX   4  JPARNUM(3)          [ GET ITS NUMBER 
   8 4LYQ          ANDN  4  #77  
  10 4MDB          MHUNTW   2,GOUT,INFO  
  18 4MY2          STO   4  GJOBS(2)            [ STORE PARAMETER NUMBER 
   9 4NCL          LDX   4  ANUM(3)  
   8 4NX=          ANDN  4  #7777
   7 4PBW          LDN   6  1
   8 4PWG          LDX   2  FX2  
   9 4QB6          SBS   6  AWORK4(2)
  18 4QTQ          BNZ   4  NOFREE              [ J IF MORE CHARACTERS   
  19 4R*B          FREECORE 3                   [ NO CHARS AFTER PARAMETER   
   5 4RT2    NOFREE  
  20 4S#L          SMO   7                      [ FIND WHICH ROUTINE TO JUMP TO  
  10 4SS=          LDX   6  PARSTR+3(1)  
   7 4T?W          ADX   6  1
  16 4TRG          MHUNTW   3,GOUT,INFO         [ FIND INFO BLOCK
  18 4W?6          BRN      (6)                 [ J TO CORRECT ROUTINE   
   5 4WQQ    XFREE   
  20 4X=B          FREECORE 3                   [ NO PARAMETER FOUND: FREE BLOCK 
  20 4XQ2          SBN   5  3                   [ J IF PERIPHERAL OR SHORTENED   
  20 4Y9L          BNG   5  RETA                [ PARAMETER HAS BEEN LOOKED FOR; 
  21 4YP=          LDN   5  2                   [ OTHERWISE SET PARAMETER LENGTH TO 2
  16 4_8W          ADX   1  7                   [ RESET MODIFIER 
  16 4_NG          BRN      SAG02               [ AND BRANCH BACK
   4 5286    [   
   4 52MQ    [   
  10 537B    [ANALYSIS OF CODE PERAMETER 
   4 53M2    [   
   9 5KP6    [PERIPHERAL PARAMETER   
   4 5L8Q    [   
   5 5LNB    PARPER  
   9 5M82          LDX   5  GJOBS(3) 
  20 5MML          STO   5  AWORK3(2)           [ PRESERVE PERI PARAMETER NUMBER 
   8 5N7=          BZE   4  RET  
   7 5NLW          PHOTO    5
   6 5NNS ...#SKI G253&1 
   4 5NQQ ...(   
  10 5NSN ...      IF       BC,2,CXTUSER 
   6 5NWL ...      THEN  
  11 5NYJ ...         VFREEW   FILE,ADJUNCTS 
   5 5P2G ...      FI
   4 5P4D ...)   
  17 5P6G          PERCON                       [ CONVERT PARAMETER  
  17 5PL6          TESTREP  PARERR,SOFIN        [ J IF ERROR FOUND   
  20 5Q5Q          MHUNTW   2,CPAR,GPERCON      [ FIND CONVERTED PARAMETER BLOCK 
  19 5QKB          LDX   4  A1(2)               [ LOAD CONVERTED PARAMETER   
  17 5R52          ANDX  4  TM(1)               [ CUT OUT UNIT NUMBER
  15 5RJL          FREECORE 2                   [ FREE BLOCK 
   9 5S4=          TESTMOVE 5,NOTMD  
  21 5SHW          MHUNTW   3,GOUT,INFO         [ RELOCATE INFO BLOCK IF NECESSARY   
   5 5T3G    NOTMD   
   7 5TH6          SLL   4  3
   9 5W2Q          STO   4  GOPTY(3) 
  19 5WGB          LDCH  4  GOPTY(3)            [ SET OUTPUT TYPE AS MODIFIER
   7 5X22          LDN   5  1
  18 5XFL          SMO   4                      [ SET OUTPUT TYPE SWITCH 
   7 5X_=          SLL   5  0
  16 5YDW          ANDX  5  OUTMK(1)            [ CHECK ITS VALID
  19 5YYG          BZE   5  SPERER              [ J TO REPORT ERROR IF NOT   
   8 5_D6          BRN      SN06 
   4 5_XQ    [   
   9 62CB    [ALL RESTART PARAMETER  
   4 62X2    [   
   5 63BL    RESTALL 
  18 63W=          BNZ   4  SEREST              [ J TOO MANY CHARACTERS  
   6 63YT ...#SKI G503&1 
   4 643D ...(   
  20 6463 ...      LDX   0  GOPTY(3)            [ IF ONLINE, IGNORE USER'S 'ALL' 
  21 648L ...      BZE   0  RET                 [  SO WE CAN USE 'ALL' BIT OURSELVES 
   4 64?9 ...)   
  19 64*W          LDCT  4  1                   [ SET MARKER FOR ALL RESTART 
   9 64TG          ORS   4  GMODE(3) 
  18 65*6          BRN      RET                 [ J TO GET NEXT PARAMETER
   4 65SQ    [   
   9 66#B    [PAGE RESTART PARAMETER 
   4 66S2    [   
   5 67?L    RESTPAGE
   6 67#? ...#SKI G503&1 
   4 67#Y ...(   
  16 67*K ...[ ACL MEND 0057 VARIABLE LENGTH PAGED OUTOUT ON MOP 
  18 67B= ...      JBC      YACL1,2,CXTMOP      [STANDARD CODE IF NOT MOP
   9 67BX ...      LDCH  5  GOPTY(3) 
  19 67CJ ...      BNZ   5  YACL1               [J IF PERIPHERAL SPECIFIED   
  17 67D9 ...      BZE   4  YACL2               [J IF NO PAGE LENGTH 
  18 67DW ...      CALL  6  SCHNUMCOD           [CONVERT ASSUMING DECIMAL
  15 67FH ...      TESTREP  CHNUMERR,SOFIN      [J IF ERROR  
   7 67G8 ...      LDX   5  4
   8 67GT ...      BNZ   4  YACL3
  17 67HG ...      BRN      SOUTOFRANGE         [ ZERO PAGE LENGTH   
  16 67J7 ...YACL2 LDN   5  18                  [DEFAULT 18 LINES
  17 67JS ...YACL3 STO   5  GCOUNT(3)           [STORE PAGE LENGTH   
  16 67KF ...      BS       3,BLFPAGER          [SET 'PAGE' BIT  
  16 67L6 ...      BNZ   4  SN06                [FREE CPB-CUNI   
   8 67LR ...      BRN      SN07 
   4 67MD ...#   
  19 67N5 ...SLA            4H*+*+              [ DEFAULT 'STOP' TERMINATOR  
   4 67NQ ...#   
   5 67PC ...YACL1   
   4 67Q4 ...)   
  18 67R=          BNZ   4  SEREST              [ J TOO MANY CHARACTERS  
   7 68=W          LDCT  4  1
  19 68QG          ANDX  4  GMODE(3)            [ J RESTART ALREADY SPECIFIED
   8 69=6          BNZ   4  SERT1
  20 69PQ          LDCH  4  GOPTY(3)            [ CHECK THAT OUTPUT PERIPHERAL IS
  17 6=9B          SBN   4  2                   [ THE LINE PRINTER   
  16 6=P2          BNZ   4  SERT2               [ ILLEGAL IF NOT 
  20 6?8L          LDCT  4  2                   [ SET MARKER FOR TOTAL RESTART   
   9 6?N=          ORS   4  GMODE(3) 
  18 6#7W          BRN      RET                 [ J TO GET NEXT PARAMETER
   4 6#MG    [   
   8 6*76    [ LINES PARAMETER   
   4 6*LQ    [   
   5 6B6B    SLINES  
   7 6BL2          LDN   0  1
   9 6C5L          STO   0  AWORK1(2)
  16 6CK=          BZE   4  SPARANULL           [ J IF NO NUMBER 
  16 6D4W          CALL  6  SCHNUMCOD           [ CONVERT NUMBER 
  16 6DJG          TESTREP  CHNUMERR,SNUMERR    [ J IF NOT NUMBER
   9 6F46          STO   4  GNLI(3)  
   8 6FHQ          BRN      SN06 
   4 6G3B    [   
   7 6GH2    [ FROM PARAMETER
   4 6H2L    [   
   5 6HG=    SFROM   
  17 6H_W          BZE   4  SPARANULL           [ J IF NUMBER NULL   
   9 6JFG          CALL  6  SCHNUMCOD
  15 6J_6          TESTREPN   CHNUMERR,SFROM1   [ J IF NUMBER
  16 6KDQ          LDX   4  GSIGN               [ ^FROM^ MARKER  
  19 6KYB          DOWN     LISTRING,1          [ ANALYSE CONTEXT SELECTION  
   8 6LD2          BRN      RET  
   5 6LXL    SFROM1  
   9 6MC=          STO   4  GFLI(3)  
   8 6MWW          BRN      SN06 
   4 6NBG    [   
   7 6NW6    [ TO PARAMETER  
   4 6P*Q    [   
   4 6PTB    STO 
   9 6Q*2          LDX   0  AWORK1(2)
  17 6QSL          BNZ   0  TOLIN               [ J IF LINES AS WELL 
  17 6R#=          BZE   4  SPARANULL           [ J IF NULL STRING   
  16 6RRW          CALL  6  SCHNUMCOD           [ CONVERT NUMBER 
  15 6S?G          TESTREPN CHNUMERR,STO1       [ J IF NUMBER
  15 6SR6          LDN   4  0                   [ ^TO^ MARKER
  19 6T=Q          DOWN     LISTRING,1          [ ANALYSE CONTEXT SELECTION  
   8 6TQB          BRN      RET  
   4 6W=2    STO1
  17 6WFS          JBS      STO2,3,BLFFRCTEXT   [ J IF ^FROM^ CONTEXT
   9 6WPL          NGX   0  GFLI(3)  
   7 6X9=          ADX   0  4
  15 6XNW          ADN   0  1                   [ INCLUSIVE  
  16 6Y8G          BNG   0  SOUTOFRANGE         [ J IF TO < FROM 
   9 6YN6          STO   0  GNLI(3)  
   8 6YR=          BRN      SN06 
   4 6YWB    STO2
   9 6Y_G          STO   4  GNLI(3)  
  15 6_4L          BS       3,BLFTO             [ ^TO^ NUMBER
   4 6_7Q    SN06
  18 6_MB          MHUNT    1,CPB,CUNI          [ FIND PARAMETER BLOCK   
  15 7272          FREECORE 1                   [ AND FREE IT
   4 72LL    SN07
   8 736=          LDX   1  FX1  
   4 73KW    RET 
  19 745G          LDX   6  AWORK4(2)           [ TEST IF NO MORE PARAMETERS 
   8 74K6          BZE   6  XENFJ
   4 754Q    RETA
  21 75JB          BZE   7  XENFJ               [ J IF ALL LEGAL PARAMELERS LOOKED FO
  21 7642          SBN   7  4                   [ UPDATE POINTER TO PARAMETER TABLE  
   8 76HL          BRN      SAG  
   4 773=    [   
  13 77GW    [INTERNALLY ISSUED ^MONFILE^ PARAMETER  
   4 782G    [   
   5 78G6    PMONF   
  20 78PY          BZE   4  SN07                [ J IF PARAMETER NULL - IGNORE   
   7 78_Q          PHOTO    6
  18 79FB          CHNUMCOO 1                   [ CONVERT MONITORING MASK
  19 79_2          TESTREP  OK,P02              [ IF THIS RESULTS IN ERRORS, 
  21 7=DL          NGN   5  1                   [ CARRY ON BUT SELECT ALL MESSAGES   
  16 7=Y=          BRN      P03                 [ OTHERWISE JUST 
   4 7?CW    P02 
  18 7?XG          LDX   5  ACOMMUNE1(2)        [ SELECT MESSAGE SIEVE   
   4 7#C6    P03 
  21 7#WQ          TESTMOVE 6,STAT              [ PROBABLY NOT MOVED, BUT COULD BN   
  18 7*BB          MHUNTW   3,GOUT,INFO         [ IF SO WE MUST RELOCATE 
   4 7*W2    STAT
  18 7B*L          STO   5  GMFMSK(3)           [ STORE THE MESSAGE SIEVE
   7 7BT=          LDN   5  4
  21 7C#W          ORS   5  GMODE(3)            [ SET B21 OF GMODE (MONFILE MARKER)  
   8 7CSG          BRN      SN06 
   4 7DRQ    [   
  10 7F?B    [NUMBERED LISTING PARAMETER 
   4 7FR2    [   
   5 7G=L    NUMBER  
  18 7L7L          BNZ   4  SEREST              [ J TOO MANY CHARACTERS  
  20 7LM=          LDCH  2  GOPTY(3)            [ TEST AND J IF NOT ALLOWED ON   
  19 7M6W          LDN   5  1                   [ SPECIFIED OUTPUT PERIPHERAL
   8 7MLG          SLL   5  0(2) 
   8 7N66          LDX   2  FX2  
   9 7NKQ          ANDX  5  NUMBMK(1)
   9 7P5B          BZE   5  SNOTAL   
  17 7PK2          LDN   5  2                   [ SET NUMBER MARKER  
   9 7Q4L          ORS   5  GMODE(3) 
   8 7QJ=          BRN      XENFI
   4 7R3W    [   
  10 7RHG    [SPECIAL LISTING PARAMETER  
   4 7S36    [   
   5 7SGQ    SPECIAL 
  18 7YCQ          BNZ   4  SEREST              [ J TOO MANY CHARACTERS  
  20 7YXB          LDCH  2  GOPTY(3)            [ TEST AND T SPECIAL NOT ALLOWED 
  19 7_C2          LDN   5  1                   [ WITH SPECIFIED OUTPUT TYPE 
   8 7_WL          SLL   5  0(2) 
   8 82B=          LDX   2  FX2  
   9 82TW          ANDX  5  SPECMK(1)
  17 83*G          BZE   5  SNOTAL2             [ J IF NOT ALLOWED   
   7 83T6          LDN   5  1
   9 84#Q          ORS   5  GMODE(3) 
   8 84SB          BRN      RET  
   4 85#2    [   
  19 85RL    [NOW THAT MOST OF THE TRIVIAL PARAMETERS HAVE BEEN ANALYSED,THE 
  19 86?=    [JOB BLOCK IS ACCESSED TO EXTRACT THE JOB-NAME AND USER-NAME AND
  16 86QW    [TO CONSTRUCT A PAIR OF INDEX KEYS FOR THE REQUEST. 
   4 87=G    [   
   9 87?6 ...#SKI G254_G502_G503&1   
   4 87?Q ...(   
  18 87#B ...#     OPTIONAL EXTRA PARAMETERS FOR ADDITIONAL LF FACILITIES
  19 87*2 ...#     M254 = NOPA ON *LP ONLY; OTHERS FOR MOP ONLINE LF'S ONLY  
   4 87*L ...#   
  17 87B= ...#     TABLE WITH 2-WORD ENTRIES: 4H<KEY>, <BIT PATTERN> 
   5 87BW ...XTRAPARS
   6 87CG ...#SKI G503&1 
   8 87D6 ...SSTOP XTRALFPAR STOP
   6 87DQ ...#SKI G502&1 
   4 87FB ...(   
   9 87G2 ...      XTRALFPAR GRAP,GR 
  10 87GL ...      XTRALFPAR NORM,SHIF   
  11 87H= ...      XTRALFPAR ALLC,SHIF,NOPA  
   8 87HW ...      XTRALFPAR GR  
   8 87JG ...      XTRALFPAR SHIF
  10 87K6 ...      XTRALFPAR NOLF,NOPA   
   8 87KQ ...      XTRALFPAR NOBC
   9 87LB ...      XTRALFPAR NBC,NOBC
  12 87M2 ...      XTRALFPAR PICT,NOPA,SHIF,NOBC 
   4 87ML ...)   
   5 87N= ...XTRAPARE
   4 87NW ...)   
   5 87Q6    XENFJ   
  10 889Q          MHUNTW   3,GOUT,INFO  
   5 88PB    XENFI   
   9 88PJ ...#SKI G254_G502_G503&1   
   4 88PQ ...(   
  18 88PY ...      LDX   6  AWORK4(2)           [ NUMBER OF PARAMS LEFT  
  16 88Q6 ...      BZE   6  XPARS               [ NO PARAMS LEFT 
  19 88Q# ...      LDCH  4  GOPTY(3)            [ X4 = 0 ONLINE LF, 2 *LP LF 
   7 88QG ...#SKI G502_G503&1
   4 88QN ...(   
  17 88QW ...      LBW      2,CXTOFFLINE,0      [ X0 # 0 IF NOT MOP  
  20 88R4 ...      ORX   4  0                   [ PARAMS FOR MOP ONLINE LF ONLY  
   4 88R= ...)   
  21 88RD ...      BNZ   4  XPARS               [ IGNORE EXTRA PARAMS IF NOT ALLOWED 
  17 88RL ...PALL  LDN   4  XTRAPARE-XTRAPARS   [ FULL TABLE LENGTH  
   5 88RS ...TABOK   
  20 88S2 ...      STOZ     AWORK4(2)           [ GETS RESET TO NO. UNPROCESSED  
  18 88S8 ...      LDN   7  0                   [ TO HOLD SPECIAL BITS   
   6 88SB ...#SKI G503&1 
  20 88SJ ...      LDX   5  ACES                [ IMPOSSIBLE 'STOP' TERMINATOR   
   7 88SQ ...NXTP  PARUNACC  
  10 88SY ...      MHUNT    1,CPB,CUNI   
  18 88T6 ...      NGX   0  ANUM(1)             [ -VE SHOULDN'T HAPPEN   
  16 88T# ...      BPZ   0  MPTY                [ IGNORE EMPTY   
  16 88TG ...      LDX   3  4                   [ TABLE LENGTH   
  15 88TN ...      LDX   0  APARA(1)            [ 1ST 4 CHARS
   7 88TW ...NXTPS SBN   3  1
   8 88W4 ...      SMO      FX1  
  16 88W= ...      TXU   0  XTRAPARS-1(3)       [ FIND IN TABLE  
   8 88WD ...      BCC      XTRPF
   8 88WL ...      BCT   3  NXTPS
   9 88YG ...      ZERRORX  APFERR   
   4 88YN ...#   
   8 88YW ...XTRPF SMO      FX1  
  15 88_4 ...      ORX   7  XTRAPARS(3)         [ BIT PATTERN
   6 88_= ...#SKI G503&1 
   4 88_D ...(   
   8 88_L ...      SMO      FX1  
   8 88_S ...      TXU   0  SSTOP
   8 8922 ...      BCS      MPTY 
   9 8928 ...      LDX   0  ANUM(1)  
   8 892B ...      ANDN  0  #7777
   7 892J ...      SBN   0  5
   8 892Q ...      SMO      FX1  
  19 892Y ...      LDX   5  SLA                 [ DEFAULT 'STOP' TERMINATOR  
   8 8936 ...      BNG   0  MPTY 
  19 893# ...      LDX   5  APARA+1(1)          [ PARAMETER GIVEN TO 'STOP'  
   4 893G ...)   
   7 893N ...MPTY  FREECORE 1
   8 893W ...      BCT   6  NXTP 
  10 8944 ...PDONE MHUNTW   3,GOUT,INFO  
   9 894= ...      ORS   7  GMODE(3) 
   6 894D ...#SKI G503&1 
   4 894L ...(   
   8 894S ...      TXU   5  ACES 
  17 8952 ...      BCC      XPARS               [ NO 'STOP' PARAMETER
  17 8958 ...      STO   5  GHEAD(3)            [ 'STOP' TERMINATOR  
   4 895B ...)   
   5 895J ...XPARS   
   4 895Q ...)   
   6 895Y ...#SKI G253&1 
   4 8966 ...(   
   9 896# ...      LDX   0  AWORK2(2)
   8 896G ...      BZE   0  STAND
  21 896N ...      MHUNTW   2,IUSER,JBL         [ GET PRETEND JOB & USER NAMES ETC.  
   8 896W ...      BRN      NONST
   5 8974 ...STAND   
   4 897= ...)   
  18 8992          FJOCA    2                   [ GET POINTER TO JOBLOCK 
   6 89B8 ...#SKI G253&1 
   5 89HB ...NONST   
  18 89NL          LDN   5  JUSER(2)            [ COPY ACROSS USER NAME  
   9 8=8=          LDN   6  GUSER(3) 
   7 8=MW          MOVE  5  3
  17 8?7G          LDN   4  JNAME(2)            [ COPY ACROSS JOBNAME
   9 8?M6          LDN   5  GJOBN(3) 
   7 8#6Q          MOVE  4  3
  20 8#LB          LFKEY    2,3                 [ SET UP INDEX KEYS IN GOUT/INFO 
  17 8*62          LDX   4  ALOGLEN(3)          [ SET RECORD HEADER  
   8 8*KL          STO   4  A1(3)
  10 8B5=          ACROSS   LISTPARS,2   
   4 8BJW    [   
  21 8C4G    [***********************************************************************
   4 8CJ6    [   
  20 8D3Q    [THE FOLLOWING CODE DEALS WITH GENERATING VARIOUS ERROR MESSAGES.   
  20 8DHB    [WHEN THE ERROR HAS BEEN REPORTED,A RETURN IS MADE TO THE'COMMAND   
   6 8F32    [PROCESSOR'.
   4 8FGL    [   
   5 8G2=    PARER   
   8 8GFW          SBX   4  FX1  
  19 8G_G    [THIS S/R RECONSTRUCTS THE PERIPHERAL PARAMETER FOR PART OF AN  
  11 8HF6    [ERROR MESSAGE. X3 -> GOUT/INFO 
  17 8HYQ          SPARANOX GJOBS(3)            [ GET CUNI BLOCK BACK
   8 8JDB          ADX   4  FX1  
   7 8JY2          EXIT  4  0
  16 8KCL          ZERRORY                      [ SET UP COMERR  
   5 8KX=    SEREST  
   8 8LBW          CALL  4  PARER
  16 8LWG          ZERRORX  JEXCHARS            [ TOO MANY CHARS 
   5 8MB6    SERT1   
   8 8MTQ          CALL  4  PARER
  20 8N*B          ZERRORX  JREST1              [ ALL RESTART ALREADY SPECIFIED  
   5 8NT2    SERT2   
  21 8P#L          ZERRORX  JRPAGE              [ PAGE RESTART NOT ALLOWED UNLESS *LP
   5 8PS=    SPERER  
   8 8Q?W          CALL  4  PARER
  17 8QRG          ZERRORX  JNOTAL              [ NOT OUTPUT DEVICE  
   4 8R?6    [   
   6 8RQQ    SPARANULL   
   8 8S=B          CALL  4  PARER
  16 8SQ2          ZERRORX  JNULLPAR            [ PARAMETER NULL 
   4 8T9L    [   
   6 8TP=    SOUTOFRANGE 
   8 8W8W          CALL  4  PARER
  20 8WNG          ZERRORX  JOUTOFRANGE         [ NUMBER OUTSIDE ALLOWED RANGE   
   4 8X86    [   
   5 8XMQ    SNUMERR 
  17 8Y7B          ZERRORX  CHNUMERR            [ NOT VALID NUMBER   
   4 8YM2    [   
  13 8_6L    [ERROR MESSAGES FOR GS CODE PERAMETER   
   5 98YL    SNOTAL  
  19 99D=          LDX   2  AWORK3(2)           [ PERIPHERAL PARAMETER NUOBER
   9 99XW          STO   2  GJOBS(3) 
   8 9=CG          CALL  4  PARER
  21 9=X6          ZERRORX  JNUMBLF             [ NUMBER NOT ALLOWED ON THIS DEVICE  
   5 9?BQ    SNOTAL2 
  19 9?WB          LDX   2  AWORK3(2)           [ PERIPHERAL PARAMETER NUMBER
   9 9#B2          STO   2  GJOBS(3) 
   8 9#TL          CALL  4  PARER
  21 9**=          ZERRORX  JSPECLF             [ SPECIAL NOT ALLOWED ON THIS DEVICE 
   5 9*SW    NEGNO   
   8 9B#G          CALL  4  PARER
  19 9BS6          ZERRORX  JNEGNO              [ NEGATIVE NUMBER NOT ALLOWED
   5 9C?Q    TOLIN   
  19 9CRB          ZERRORX  ALFTOLIN            [ LINES & TO IN SAME COMMAND 
   4 9D?2    NOFN
  17 9DQL          COMERR   JPARMIS,JFNAME      [ FILE NAME MISSING  
   5 9F2D    SOFIN   
   6 9F==          ENDCOM
   4 9FPW    #END
   6 ____ ...302370310003
