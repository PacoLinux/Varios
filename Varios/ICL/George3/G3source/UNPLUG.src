  17 22FL ...#SEG  UNPLUG850                    [DEK BEASLEY    :CENT
   4 22KH ...[   
  15 22PD ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   4 22T* ...[   
   7 22_=    #OPT  K0UNPLUG=0
  11 23DW    #LIS  K0UNPLUG>K0ALLGEO>K0LLS   
   7 23YG          8HUNPLUG  
   4 24D6    #   
  21 24XQ    #     THIS SEGMENT IMPLEMENTS THE UNPLUG,NOTBUSY,UNPLUGRT AND UNPLUGRT  
   8 25CB    #     'PUC' MACROS  
   4 25X2    #   
  13 26BL    #     THE ENTRY POINTS ARE AS FOLLOWS:  
   4 26W=    #   
  20 27*W    #     K1,K2 :  THE NOTBUSY MACRO,WHICH MERELY UNBUSIES A PROGRAM'S  
  10 27TG    #              PERIPHERALS  
  12 28*6    #     K3    :  THE UNPLUGRT MACRO   
  13 28SQ    #     K4    :  THE UNPLUGRT 'PUC' MACRO 
  19 29#B    #     K5    :  ENTRY TO UNPLUG FOR SPEC SWAPOUT NO SFWT REQUIRED
  18 29S2    #     K7    :  ENTRY TO UNPLUG FOLLOWED BY SWAPOUT(IN PCA)  
  13 2=?L    #     K8,K18,K28,K38 : THE UNPLUG MACRO 
  16 2=R=    #     K9    :  ENTRY TO UNBUSY AND UNPLUG FOR MOVE  
   4 2?=W    #   
  10 2?QG          SEGENTRY K2UNPLUG,XX2U
  10 2#=6          SEGENTRY K3UNPLUG,XX3U
  10 2#PQ          SEGENTRY K4UNPLUG,XX4U
  10 2*9B          SEGENTRY K5UNPLUG,XX5U
  10 2*P2          SEGENTRY K7UNPLUG,XX7U
  10 2B8L          SEGENTRY K8UNPLUG,XX8U
  11 2BN=          SEGENTRY K18UNPLUG,XX18U  
  11 2C7W          SEGENTRY K28UNPLUG,XX28U  
  11 2CMG          SEGENTRY K38UNPLUG,XX38U  
  10 2D76          SEGENTRY K9UNPLUG,XX9U
  11 2DLQ          SEGENTRY K10UNPLUG,XX10U  
   4 2F6B    [   
   4 2FL2    [   
  16 2G5L    [  BITS TO DEFINE TYPE OF UNPLUG,HOPEFULLY MNEMONIC 
   4 2GK=    [   
  20 2H4W          BITDEFS  AWORK1,0,RETURN,NOTBUSY,STEPBAC,NOSFWT,SWAPOUT,REPLY 
  11 2HJG          BITDEFS  AWORK1,6,TIMEOUT 
   2 2J46
   4 2JHQ    [   
   4 2K3B    [   
   4 2KH2    #   
   4 2L2L    #   
   6 2LG=    TON   +100  
   6 2L_W    PK9   +K9   
   9 2MFG    THOUSK9        1000/K9  
  20 2M_6    P17   PAIR     PERFAIL,2           [FOR ON-LINE PERIPHERAL FAILURE  
  17 2NDQ    P18   PAIR     EVENTS,5            [FOR PROGRAM TIMEUP  
   5 2NYB    SPECLINK
   9 2PXL          LINK     SWAP,4   
   5 2RBG    SWAPLINK
   9 2T*2          LINK     SWAP,2   
   4 2TSL    #   
   4 2W#=    #   
   8 2WRW    UGEO3 MELUNRING   3 
   9 2X?G    #SKI  K6LLSTEST>499-499 
   4 2XR6    (   
   9 2Y=Q          LDX   0  ABW1(3)  
   9 2YQB          ADX   0  ABW2(3)  
   9 2_=2          ADX   0  ABW3(3)  
   9 2_PL          ADX   0  ABW4(3)  
  19 329=          BNZ   0  (GEOERR)             [QUICK CHECK THAT DERINGED  
   4 32NW    )   
   7 338G          EXIT  4  0
   2 33N6
   4 347Q    #   
  20 34MB    #  SUBROUTINE TO SET FAIR WAIT TIME AND TIME LAST RUN,X3-> JOBBLOCK 
  15 3572    #  LINK X0,USES X4,5,6,7,ON EXIT X1=FX1 X2=FX2  
   4 35LL    #   
  20 35P9 ...[     WITH IGNOREFWT RESTORE TIME MACRO SWAITCHED ON (WHICH IS AALSO
  21 35RS ...[     THE DEFAULT); JCFWT IS SET TO ZERO RATHER THAN CALCULATED(WHICH   
  20 35WC ...[     IMPLIES FWT NOT USED WHEN SELECTING PROGRAM TO SWAP IN) UNLESS
  21 35_2 ...[     HLS IS OFF FOR TYPE OF JOB WHOSE PROGRAM IS BEING UNPLUGGED AND   
  10 363K ...[     CPIFORTIMES IS IN USE.
   4 366=    #   
   8 36KW    SFWT  STO   0  GEN0 
   8 375G          LDX   1  FX1  
   9 37K6          LDX   5  HTIMES(3)
  14 384Q          ADN   5  K9-1                [ROUND UP
  16 38JB          DVS   4  PK9(1)              [X5 HAS RESULT   
   9 3942          LDX   7  JCPI(3)  
  17 3?7= ...      JBS XBACK,3,JBOFFL           [J IF BACKGROUND JOB 
   9 3?7D ...      SEGENTRY K44UNPLUG
   9 3?7S ...      JBS XK42,,HLSMFS  
  16 3?9L ...      LDX   7  IMOPCPI             [HLS OFF FOR MOP 
   7 3?=S ...      LDN   2  0
   8 3?#2 ...      BRN      MOP  
   5 3?*8 ...XBACK   
   9 3?*B ...      SEGENTRY K45UNPLUG
   9 3?*Q ...      JBS XK42,,HLSBFS  
  18 3?CJ ...      LDX   7  IMOPCPI             [HLS OFF FOR BACKGROUND  
   7 3?G6          LDN   2  1
   8 3?_Q          SBN   7  100  
  15 3#FB          NGX   7  7                   [=100-MOPCPI 
   4 3#_2    MOP 
   9 3*4X ...      SEGENTRY K40UNPLUG
  19 3*#P ...      BSOFF CPIBIT,XK42            [J IF CPIFORTIMES NOT IN USE 
  10 3*DL          DVS   6  HLSCOUNT(2)  
  16 3*Y=          LDX   6  7                   [ MAKE FRACTION  
   7 3BCW          LDN   7  0
   9 3BXG          DVD   6  TON(1)   
   9 3C25 ...      SEGENTRY K41UNPLUG
   9 3C7? ...ONHLS BXGE  5  7,XK42   
  16 3CWQ ...      LDCH  0  APROCNO             [NO OF PROCESSORS
  21 3DBB ...      DVS   4  0                     [> 1 PROCESSOR,CPI MULTIPLE EFFECTI
  19 3DW2 ...                                     [DIVIDEND + QUOTIENT IN X5 
   9 3FT=    ONE   STO   7  JCPI(3)  
   9 3FWH ...      SEGENTRY K42UNPLUG
   4 3G2B ...XK42
   6 3G3M ...      NULL  
   6 3G4Y ...      NULL  
  18 3G69 ...      LDN   6  0                   [SET FWT = 0 TO IGNORE   
  18 3HRQ    MEET  STO   6  JCFWT(3)            [SET FAIR WAITING TIME   
   9 3J?B          LDX   0  APROCTIME
  17 3JR2          STO   0  JCTLR(3)            [AND TIME LAST RUN   
   8 3K=L          LDX   2  FX2  
   9 3KQ=          BRN      (GEN0)   
   9 3L24 ...      SEGENTRY K43UNPLUG
   7 3L9W    SMALL LDX   6  5
  16 3LPG          LDX   5  HOLDUP              [ADJUST BY HOLDUP
  10 3M96          MPA   5  THOUSK9(1)   
   8 3MNQ          BRN      MEET 
   4 3N8B    #   
  21 3NN2    #           UNPLUG  THE PROGRAM WHOSE JOB BLOCK ADDRESS IS IN X3 ON ENTR
  20 3P7L    #     LINK X7.NO COORDINATION.ON EXIT,X1=FX1,X2=FX2,X3 IS UNCHANGED.
  13 3PM=    #     OTHER ACCUMULATORS ARE DESTROYED. 
   4 3Q6W    #   
   5 3QLG    SUNPLUG1
  17 3R66          LDX   5  3                   [STORE JOB BLOCK ADDR
   7 3RKQ          FPCAJO   3
  11 3S5B          PCATEST  3,NORMAL,TP,PUC  
   8 3SK2    TP    FINDPUC  3,3  
  15 3T4L    PUC   CALL  4  UGEO3               [DERIND PUC  
   8 3TJ=          FINDTP   3,3  
   5 3W3W    NORMAL  
  16 3WHG          CALL  4  UGEO3               [DERING TP/NORMAL
   4 3X36    XM4 
  11 3XGQ          TRACE    JOBNO(3),UNPLUGGD
  17 3Y2B          LDX   3  5                   [RESTORE JOBLOCK ADDR
   7 3YG2          EXIT  7  0
   4 3Y_L    #   
   4 3_F=    #   
  21 3_YW    #     SUBROUTINE TO UNLOCK THE PRB AND ANY OLPAS.LINK X7.ON EXIT,X1=FX1,
  16 42DG    #     X2=FX2,ALL OTHER ACCUMULATORS ARE DESTROYED.  
   4 42Y6    #   
   5 43CQ    SUNPLUG2
  18 43XB          LDX   5  3                   [PRESERVE JOB BLOCK ADDR 
  14 44C2          FPCAJO   3                   [FIND PCA
   7 44WL          LDX   2  3
  14 45B=          HUNT2    1,APERI,APERIRES,3  [X1 ->PRB
  11 45TW          PCATEST  3,NORM,TP1,PUC1  
  14 46*G    TP1   FINDPUC  2,2                 [X2 ->PUC
   8 46T6    PUC1  LDN   0  A1(1)
  17 47#Q          SBS   0  APRB(2)             [ADJUST PUCS PRB PTR 
  14 47SB          FINDTP   2,3                 [X3 -> TP
  15 48#2    NORM  UNLOCK   1                   [UNLOCK PRB  
   8 48RL          BRN      PUN3 
   4 48_W ...PUN2
   7 4986 ...#UNS  CAFSINT   
  21 49BB ...      JCAFSBSY 3,PUN3              [OMIT UNLOCK FOR BUSY CAFS CHANNEL   
   7 49JL ...      UNLOCK   3
  10 49QW    PUN3  LDX   3  ACTRING(3)   
   9 4==G          SBN   3  ACTRING  
   9 4=Q6          LDCH  0  ATYPE(3) 
   9 4?9Q          SBN   0  AOLPT/64 
  16 4?PB          BZE   0  PUN2                [UNLOCK ANY OLPAS
  18 4?_8 ...      LDX   3  5                   [RELOAD JOB BLOCK ADDR.  
   7 4#92          EXIT  7  0
   4 4#NL    #   
   4 4*8=    #   
  21 4*MW    #     ROUTINE TO JBUSY AN ONLINE PERIPHERAL.ENTERED WITH X4 -> DEVICE   
  21 4B7G ...#     LIST.X6 IS LINK.DOES JBUSY AND EXITS 0 IF BUSY.IF FAILED,EXIT 1 IS
  12 4BM6    #     OBEYED.EXIT 2 IS OBEYED IF THE
  15 4C6Q    #     PERIPHERAL IS NOT BUSY AND HAS NOT FAILED.
  21 4CLB    #           NO COORDINATION. ON EXIT, X1=FX1,X2=FX2;X0,X5,X6 ARE DESTROY
   4 4D62    #   
   5 4DKL    UNBUSY  
   8 4F5=          LDX   1  FX1  
   8 4FJW          LDX   2  FX2  
  16 4G4G ...      JBUSY    4,(6)               [EXIT 0 IF BUSY  
   8 4GJ6 ...      JSBUSY   4,(6)
   7 4H3Q ...      ADN   6  1
  16 4HHB ...      JFAIL    4,(6)               [EXIT 1 IF FAILED
  15 4J32          EXIT  6  1                   [EXIT 2 IF OK
   4 4JGL    #   
   4 4K2=    #   
  21 6=R= ...[ SUBROUTINE TO DECANT PROGRAM TIMERS, CONVERT TO MILLISECONDS,ACCUMULAT
  13 6=RS ...[ AND UPDATE TIME USED FOR EACH MEMBER. 
  21 6=SB ...[ X1 -> PCA, LINK X7 (MOVED), [ X1 -> PCA, LINK X7 (MOVED), X4,X5,X6,X7 
   5 6=SY ...TIMERS  
  16 6=TG ...      STO   7  GEN2                [REMEMBER LINK   
  18 6=W4 ...                                   [STARTING AT LAST MEMBER 
  18 6=WL ...      LDN   7  ATM4-ATM1           [-> MEL RELATIVE TO ATM1 
  16 6=X8 ...      LDN   6  4                   [NUMBER OF MEMBER
   7 6=XQ ...TM1   SMO      7
   9 6=Y# ...      LDXC  5  ASU1(1)  
  15 6=YW ...      BCS      TM2                 [NULL MEMBER 
   7 6=_D ...      SMO      7
  15 6?22 ...      LDX   5  ATM1(1)             [TIME USED   
  18 6?2J ...      DVS   4  BEATTIME            [CONVERT TO MILLISECONDS 
   7 6?36 ...      SMO      7
  16 6?3N ...      STO   4  ATM1(1)             [LEAVE REMAINDER 
   7 6?4= ...      SMO      6
  15 6?4S ...      ADS   5  ATU1-1(1)           [TIME USED   
  17 6?5B ...      ADS   5  GEN1                [ACCUMULATE TIME USED
  17 6?5Y ...TM2   SBN   7  ATM2-ATM1           [UPDATE TO NEXT MEL  
  15 6?6G ...      BCT   6  TM1                 [NEXT MEMBER 
   9 6?74 ...      BRN      (GEN2)   
   4 6?7N ...#   
  19 6?7P ...#     FOR B0233: S/R TO ZEROISE POINTER IN DEVICE LIST,FOR EXEC 
  19 6?7Q ...#     TO TEST TO SEE IF PROGRAM UNPLUGGED, SO AVOIDING TROUBLE  
  14 6?7R ...#     FROM SPURIOUS INTERRUPTS - SEE B9610. 
  16 6?7S ...#     UNNECCESSARY BUT HARMLESS FOR NOTBUSY MACRO.  
   4 6?7Y ...#   
   5 6?82 ...SDPTR   
   7 6?86 ...      SMO      4
   9 6?88 ...      STOZ      JCCA5   
   7 6?8= ...      EXIT  6  0
   4 6?8# ...#   
   4 6?8L    [   
   4 6?N=    [   
  17 6#7W    [ SUBROUTINE TO UNBUSY AND CHECK FOR TIMEUP AND PORFAIL 
  14 6#MG    [     LINK X7,EXPECTS X7-> PCA,USES X6,X3,X5
   4 6*76    [   
   4 6*LQ    [   
   5 6B6B    NOTBUSY1
   8 6BL2           SBX   7  FX1 
  11 6C5L          PCATEST  2,NBNORM,NBNORM  
  14 6CK=          FINDTP   2,2                 [X2 -> TP
   5 6D4W    NBNORM  
  18 6DJG          LDX   3  APRB(2)             [X3 ->1ST ENTRY IN PRB   
   8 6F46          SMO      FX2  
   9 6FHQ          STO   2  AWORK2   
   8 6FMM ...      LDX   2  FX2  
   4 6FRJ ...[   
  18 6FXF ...[     THE FOLLOWING CORRECTS B3341 WHICH MEANT WE ONLY TRIED
  12 6G3B ...[      TO UNBUSY A PERIPHERAL ONCE  
   4 6G7? ...[   
   4 6G?8 ...NEXT
   9 6GC5 ...      LDX   5  IREVIEW  
   4 6GH2 ...[   
   5 6GLX ...NEXT1   
   8 6GQS ...      LDXC  0  0(3) 
   4 6GWP ...[   
   9 6H2L          BCS      PRBEND   
   8 6HG=          LDXC  1  1(3) 
  15 6H_W          BCC      ONLINE1              [J IF ONLINE
   9 6JFG          ANDX  1  BITS22LS 
  15 6JK5 ...      SBN   1  CHAINADD            [X1 -> OLPA  
  18 6K4P ...      JCBB     1,XIPCCHECK        [J IF IPC AND CHANNEL BUSY
   7 6K*Y ...#UNS  CAFSINT   
  17 6KM7 ...      JCAFSBSY 1,XCAFSCHECK       [JIF CAFS CHANNEL BUSY
   8 6KYB          LDX   0  0(3) 
   7 6LD2          SLC   0  9
   8 6LXL          ANDN  0  #177 
   8 6MC=          SBN   0  #30  
   9 6MWW          BNZ   0  NOTIPB   
  18 6NBG          CONCISBY 1,NBACK             [J IF CONC IPB NOT BUSY  
  16 6NL# ...      BRN      XIPBCHECK               [FOR B8179   
   4 6NW6 ...WAIT
  15 6PTB    UNPL  COOR3    #41                 [WAIF K9MS   
  21 6Q*2 ...      JBC      NEXT1,2,TIMEOUT               [IF NO TIMEOUT WE LOOP TILL
   8 6QSL ...      BCT   5  NEXT1
  21 6R#=          BRN      NBUP                [IF NOTBUSY INFORM THEM OF BUSY PER  
   7 6R#Q ...#UNS  CAFSINT   
   6 6R*= ...XCAFSCHECK  
   6 6R*S ...XIPBCHECK   
   6 6RCB ...XIPCCHECK   
  18 6RDY ...      JBS      WAIT,2,NOTBUSY         [J IF NOT UNPLUG MACRO
   8 6RGG ...      FJOPCA   1,FX2
  18 6RKL ...      JBS      NBACK,1,JBREEF       [OK IF PROG IS REALTIME 
   8 6RNQ ...      BRN      WAIT 
   5 6RRW    NOTIPB  
   9 6S?G          JAUTOFF  1,NBACK  
   9 6SR6          JAUTFAIL 1,NBACK  
   8 6T=Q          BRN      WAIT 
   5 6TQB    ONLINE1 
   9 6W=2          ANDX  1  BITS22LS 
  18 6WPL          CHECKFLA 1,NOTFLAG           [WE IGNORE FLAG WAVERS   
  15 6X9=    NBACK BDX   3  NEXT                [GET NEXT PER
   5 6XNW    NOTFLAG 
  18 6Y8G          JCANTENG 1,NBACK             [IGNORE NON ENGAGEABLES  
   7 6YN6          LDX   4  1
   9 6_7Q          CALL  6  UNBUSY   
  13 6_L4 ...      BRN      WAIT                [BUSY
  14 724B ...      BRN      PHAIL               [FAILED  
  17 72GP ...#   THE FOLLOWING LINES INVOLVING NPTR DEAL WITH B9610  
   8 72WD ...      BRN      NPTR 
  10 73=7 ...PHAIL JBC      NPTR,2,RETURN
  17 73KW          CLFAIL   4                   [CLEAR FAILURE COUNT 
   4 73TN ...NPTR
   8 745G ...      CALL  6  SDPTR
   8 74*# ...      BRN      NBACK
   5 74K6    PRBEND  
   8 754Q          LDX   1  FX1  
  10 75JB          JBC      STOP,2,RETURN
  11 7642          JBC      NOCHECK,2,REPLY  
  19 76HL    STOP                               [CHECK FOR PERFAIL & TIME UP 
  17 773=          LDX   2  AWORK2(2)           [X2 ->PCA(TP IF RCTP)
   7 77?4 ...      LDX   3  2
  11 77GW          PCATEST  2,USUAL,,(GEOERR)
  15 782G          FINDPUC  2,3                 [X3 -> PUC   
   9 78G6          LDEX  0  ASTOP(3) 
   9 78_Q          BNZ   0  PERFAIL  
   9 79FB    USUAL LDEX  0  ASTOP(2) 
   8 79_2          BZE   0  NOPF 
   5 7=DL    PERFAIL 
  15 7=Y=          LDN   3  P17(1)              [PERFAIL LINK
   8 7?CW          BRN      MEET1
  11 7?MN ...NOPF  BBS      1,ASTOP(3),TIMEUP
  17 7?XG ...      BBUS     1,ASTOP(2),NOCHECK    [J IF NO TIME UP   
   5 7#7# ...TIMEUP  
  17 7#C6          LDN   3  P18(1)              [FOR ILLEGAL TIMEUP  
  21 7#WQ    MEET1 JMBAS    UP,FX2,REPLY,NOTBUSY      [J IF TO BE REPORTED TO CALLING
  21 7*BB ...      JBSC     XLINK,FX2,REPLY    [CLEAR REPLY SWITCH TO REMMBER FAIL   
   8 7B*L          SMO      FX2  
   9 7BT=          LDX   2  AWORK2   
  12 7C#W          JBS      USEPCA,FX2,SWAPOUT   
   7 7CSG          FINDLINK 2
   5 7D#6    USEPCA  
   9 7DRQ          LINKSET  2,0(3)   
   8 7DW* ...      BRN      XTEST
   5 7DYY ...XLINK   
   8 7F3H ...      LDX   0  0(3) 
  10 7F66 ...      STO   0  ACOMMUNE1(2) 
   5 7F8P ...XTEST   
  17 7F?B          JBC      NOSB,FX2,STEPBAC    [J IF NO STEPBAC REQD
   7 7FR2          DATUMA   3
  15 7G=L          STEPBAC1 ,3                  [ADJUST ONR  
  10 7GQ=          TRACE    8(3),WORD8ALT
   5 7H9W    NOCHECK 
   8 7HPG ...NOSB  LDX   1  FX1  
   8 7J96          LDX   2  FX2  
   8 7JNQ          ADX   7  FX1  
  15 7K8B          EXIT  7  0                   [AT LAST !   
   4 7KN2    [   
  10 7L7L    [     ENTRIES  FOR NOTBUSY  
   4 7LM=    [   
   5 7M6W    XX10U   
   9 7MLG          STOZ     AWORK1(2)
  13 7N66          MBS      2,RETURN,REPLY,NOTBUSY   
   9 7NKQ          BRN      NBJOIN   
   9 7P5B    XX2U STOZ     AWORK1(2) 
  14 7PK2         MBS      2,RETURN,NOTBUSY,REPLY,TIMEOUT
   5 7Q4L    NBJOIN  
  14 7QJ=          LDX   2  ACOMMUNE1(2)        [X2-7 PCA
   9 7R3W          CALL  7  NOTBUSY1 
  13 7RHG          UPPLUS   1                   [OK  
   8 7S36    UP    LDX   0  0(3) 
  10 7SGQ          STO   0  ACOMMUNE1(2) 
  19 7T2B    NBUP  UP                           [REPORT TO CALLING ROUTINE   
   4 7TG2    [   
  12 7T_L    [ ENTRY FOR VARIOUS UNPLUGS SEE PRM 
   4 7WF=    [   
   4 7WYW    XX8U
   9 7XDG          BSXD     0,STEPBAC
   8 7XY6          BRN      XJOIN
   7 7YCQ    XX18U LDN   0  0
   8 7YXB          BRN      XJOIN
   9 7_C2    XX28U BSXD     0,RETURN 
   8 7_WL          BRN      XJOIN
  11 82B=    XX38U MBSXD    0,RETURN,REPLY   
   9 82TW    XJOIN STO   0  AWORK1(2)
   8 83*G          FJOPCA   3,2  
  10 85#W ...[ EMPTY PROGRAM MILL TIMERS 
  18 85*5 ...      STOZ     GEN1                [TO ACCUMULATE TIME YSED 
  16 85*# ...      LDX   1  2                   [ADDRESS OF PCA  
  15 85*H ...      PCATEST  2,NORM3,,PUC3       [CHECK RCTP  
  15 85*Q ...      FINDPUC  1,1                 [OBEYED IF TP
  17 85*_ ...PUC3  CALL  7  TIMERS              [DECANT FOR PUC PCA  
  17 85B8 ...      FINDTP   1,1                 [SET UP FOR TP PCA   
  16 85BC ...NORM3 CALL  7  TIMERS              [DECANT TIMERS   
  16 85BL ...      LDX   5  GEN1                [TOTAL MIL USED  
   7 85BT ...      LDN   4  0
  20 85C4 ...      ADSC  5  GPROGTIME+1         [STEP TOTAL OBJECT PROGRAM TIME  
   9 85C? ...      ADS   4  GPROGTIME
  11 85CG ...      ADSC  5  HCLOCKTIME+1(3)  
  10 85CP ...      ADS   4  HCLOCKTIME(3)
   4 85CY ...[   
  12 85D7 ...[ CHARGE TIME SPENT IN PCA AND OLPAS
   7 85DB ...      LDN   6  0
  11 85DK ...      PCATEST  2,NORM1,TP2,PUC2 
   8 85DS ...TP2   FINDPUC  2,2  
  10 85F3 ...PUC2  ADX   6  ACTCHCH(2)   
  10 85F= ...      STOZ     ACTCHCH(2)   
   8 85FF ...      FINDTP   2,2  
  10 85FN ...NORM1 ADX   6  ACTCHCH(2)   
  16 85FX ...      STOZ     ACTCHCH(2)          [CHARGE FOR PCAS 
  15 85G6 ...      LDX   1  APRB(2)             [X1 -> PRB   
   8 85G* ...TOUR  LDXC  0  0(1) 
   8 85GJ ...      BCS      PEND 
   8 85GR ...      LDXC  2  1(1) 
  16 85H2 ...      BCC      ONLINE              [J IF ONLINE PERI
  12 85H9 ...      ADX   6  ACTCHCH-CHAINADD(2)  
  12 85HD ...      STOZ     ACTCHCH-CHAINADD(2)  
   5 85HM ...ONLINE  
  16 85HW ...      BDX   1  TOUR                [BACK FOR NEXT   
  15 85J5 ...PEND  FCAJO    2,3                 [X2 -> CPA   
   9 85J# ...PEND1 LDX   7  ATUSE(2) 
   9 85JH ...      MPA   6  CHAPTIME 
  14 85JQ ...      DVD   6  BEATTIME            [CONVERT 
  14 85J_ ...      ADX   5  7                   [TOTAL   
  15 85K8 ...      STO   6  ATUSE(2)            [REMAINDER   
  15 85KC ...      FPCAJO   2,3                 [X2 -> PCA   
   4 85KL ...[   
  20 85KT ...[ UPDATE TIMERS IN JOB BLOCK AND SET STOP WORD IF TIME-UP CONDITION 
   7 85L4 ...      LDN   4  0
  10 85L? ...      ADSC  5  HTIMEJ+1(3)  
  16 85LG ...      ADS   4  HTIMEJ(3)           [STEP JOB TIME   
  11 85LP ...      SBSC  5  APROGTIME+1(3)   
  17 85LY ...      SBS   4  APROGTIME(3)        [STEP PROGRAM TIME   
  16 85M7 ...      ADS   5  HTIMES(3)           [STEP SLOT TIME  
   5 85MB ...#SKI  G4
  20 85MK ...      SBS   5  HRTIMES(3)          [REDUCE TIME LEFT TO END OF SCI  
  17 85MS ...      LA       ASTOPTIM,5          [SET PROGRAM TIMEUP  
  18 85N3 ...      LDX   6  APROGTIME(3)        [TEST IF PROGRAM TIMEUP  
  13 85N= ...      BNG   6  TC3                 [YES 
  16 85NF ...      LA       ASTOPJOB,5          [SET JOB TIME UP 
  10 85NN ...      LDXC  7  HTIMEJ+1(3)  
   9 85NX ...      LDX   6  HTIMEJ(3)
  18 85P6 ...      TXL   7  ATIMEJ+1(3)         [TEST IF TIME UP FOR JOB 
   9 85P* ...      TXL   6  ATIMEJ(3)
  13 85PJ ...      BCC      TC3                 [YES 
  16 85PR ...      LDN   5  0                   [SET NO TIMEUPS  
   4 85Q2 ...TC3 
  18 85Q9 ...      ORS   5  ASTOP(2)            [SET ANY TIMEUP STOP BIT 
   9 85RL          CALL  7  NOTBUSY1 
  16 86?=          FJOTP    3,2                 [X3 -> JOBBLOCK  
   8 86QW          CALL  0  SFWT 
   9 87=G          BS       3,JBSTOP 
   9 87Q6          BC       3,JBPLUG 
  10 88PB          JBS      REAL,3,JBREEF
  18 8992          CALL  7  SUNPLUG1            [ONLY DERING NON REALTIME
  15 89NL    REAL  CALL  7  SUNPLUG2            [UNLOCK BLOX 
  10 8=8=          JBS      UPPL,2,REPLY 
   5 8=MW          UP
   7 8?7G    UPPL  UPPLUS   1
   4 8?M6    [   
  13 8#6Q    [ ENTRIES FOR UNPLUG PRIOR TO SWAPOUT   
   4 8#LB    [   
   9 8*62    XX7U  STOZ     AWORK1(2)
   9 8*KL          BS       2,SWAPOUT
   8 8B5=          BRN      SPEC 
   9 8BJW    XX5U  STOZ     AWORK1(2)
  11 8C4G          MBS      2,SWAPOUT,NOSFWT 
   4 8CJ6    SPEC
  16 8D3Q          CALL  7  NOTBUSY1             [DO UNBUSY ETC  
   8 8DHB          FJOTP    3,2  
   9 8F32          BS       3,JBSTOP 
   9 8FGL          BC       3,JBPLUG 
  11 8G2=          JBS      NOFWT,2,NOSFWT   
   8 8GFW          CALL  0  SFWT 
   9 8G_G          BRN      NOTSPEC  
   5 8HF6    NOFWT   
  11 8HYQ          MBI      3,JBSTOP,JBPISO  
   5 8JDB    NOTSPEC 
  11 8JY2          JBS      (GEOERR),3,JBREEF
   9 8KCL          CALL  7  SUNPLUG1 
  16 8KX=          CALL  7  SUNPLUG2            [DO ACTUAL UNPLUG
  10 8LBW          LDN   3  SPECLINK(1)  
  11 8LWG          JBS      SPECO,2,NOSFWT   
  10 8MB6          LDN   3  SWAPLINK(1)  
  15 8MTQ    SPECO ACROSSVA 0(3)                [DO SWAPOUT  
   4 8N*B    [   
  18 8NT2    [ ENTRY FOR USE BY ASWINT TO UNPLUG AND NOTBUSY A PLUGGED IN
  12 8P#L    [     PROGRAM PRIOR TO PROGRAM MOVE 
   4 8PS=    [   
   4 8Q?W    [   
   9 8QRG    XX9U  STOZ     AWORK1(2)
  13 8R?6          MBS      2,RETURN,NOTBUSY,TIMEOUT 
  15 8RQQ          STO   3  AWORK4(2)           [KEEP PCA PTR
   7 8S=B          LDX   2  3
   9 8SQ2          CALL  7  NOTBUSY1 
  10 8T9L          FJOTP    3,AWORK4(2)  
   9 8TP=          CALL  7  SUNPLUG1 
   9 8W8W          CALL  7  SUNPLUG2 
  16 8WNG          MBI      3,JBPLUG,JBSTOP     [SET UNPLUGGED   
   7 8X86          UPPLUS   1
  18 8XMQ    #     THIS SECTION IMPLEMENTS THE UNPLUGRT MACRO TO ACTUALLY
  14 8Y7B    #     UNPLUG (MELUNRING) A REALTIME PROGRAM 
   4 8YM2    #   
   4 8_6L    XX3U
   7 8_L=          FPCACA   2
  12 8_LP ...      LDX   5  2       [SAVE PCA PTR
  11 8_M9 ...      HUNT2    2,APERI,APERIRES 
   8 8_N8 ...       LDX   1  2   
  13 8_NM ...      LDN   7  0        [SET OLPA COUNT 
   5 8_P7 ...UPRTX0  
   8 8_Q6 ...      LDXC  0 A1(2) 
  16 8_R5 ...      BCS      UPRTX1               [J IF END OF PRB
   9 8_S4 ...      LDXC  0  A1+1(2)  
   9 8_T3 ...      BCC      UPRTX2   
  13 8_TC ...      ADN   7  1      [UPDATE OLPA COUNT
   7 8_TR ...      LDX   6  7
  14 8_TT ...                           [FIND NTH OLPA   
   7 8_TX ...      LDX   3  5
  16 8_T_ ...                                 [ADDRESS OF APET   
   5 8_W3 ...NOLPA   
  11 8_W5 ...      LDX   3  ACTRING+FPTR(3)  
   9 8_W7 ...      SBN   3  ACTRING  
   8 8_W9 ...      TXU   3  FX2  
   9 8_W? ...      BCC      (GEOERR) 
   9 8_W* ...      LDCH  0  ATYPE(3) 
   9 8_WC ...      SBN   0  AOLPT/64 
   8 8_WF ...      BNZ   0  NOLPA
  14 8_WH ...      BCT   6  NOLPA            [X3->OLPA   
  19 8_W_ ...      JCBB     3,UPRTX3             [J IF IPC AND CHANNEL BUSY  
   7 8_X3 ...#UNS  CAFSINT   
  18 8_X5 ...      JCAFSBSY 3,UPRTX3             [JIF CAFS CHANNEL BUSY  
  14 8_X8 ...#                                [FOR B8179 
  19 8_X9 ...      LDX   0  A1(2)                [CHECK IF THIS AN IPB DEVICE
   7 8_X= ...      SLC   0  9
   8 8_X? ...      ANDN  0  #177 
  19 8_X# ...      SBN   0  #30                  [JIF NOT AN IPB - CAN'T DO A
  20 8_X* ...      BNZ   0  UPRTX2               ['CONCISBY' FOR OTHER DEVICES!!!
  19 8_XC ...      CONCISBY    3,UPRTX2               [J IF CONC IPB NOTBUSY 
   9 8_XL ...      BRN      UPRTX3   
   5 8_XY ...UPRTX2  
   9 8_YX ...      BDX   2  UPRTX0   
   5 8__W ...UPRTX3  
   8 922T ...      COOR3    #41  
   8 923S ...      BRN      XX3U 
   5 924R ...UPRTX1  
  11 9356 ...      HUNT2B   2,AOBJPROG,0,1   
  21 93JQ          LDX   0  ALOGLEN(2)          [DECREMENT CORE USED FOR REALRIME PRO
   9 944B          SBS   0  COBJUSRT 
   7 94J2          NGNC  0  1
  21 953L          ANDS  0  JRETI(2)            [ERASE 'REALTIME' MARK IN PROGRAM BLO
   7 962W ...      LDX   2  1
   8 96GG    UPRT1 LDX   0  A1(2)
  18 9726          BNG   0  UPRT3               [JUMP IF NO MORE ENTRIES 
   9 97FQ          LDXC  4  A1+1(2)  
  16 97_B          BCS      UPRT2               [JUMP IF OFFLINE 
   9 98F2          ANDX  4  BITS22LS 
   9 98YL          JCANTENG 4,UPRT15 
  18 99D=          CHECKFLAG   4,UPRT2          [JUMP UNLESS FLAGWAVER   
   5 99XW    UPRT15  
   8 9=CG          DISENGPER   4 
  15 9=M# ...      CALL  6  SDPTR               [FOR B9610   
  16 9=X6    UPRT2 BDX   2  UPRT1               [BACK FOR NEXT   
   5 9?BQ    UPRT3   
   8 9?WB          FJOCA    3,FX2
  19 9#B2          CALL  7  SUNPLUG1            [UNPLUG AND FREE EXEC SOCKET 
  20 9#TL          BC       3,JBREEF            [UNSET REALTIME EFFECTED MARKER  
   5 9**=          UP
   4 9*SW    XX4U
   8 9B#G          FINDPUC  2,3  
  16 9BS6          CALL  4  UGEO3               [UNPLUG THE PUC  
   5 9C?Q          UP
  10 9CRB          MENDAREA 50,K100UNPLUG
   4 9D?2    #END
   6 ____ ...600434500003
