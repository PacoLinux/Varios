  14 22_=    #LIS  K0QPRINT>K0ALLGEO>K0GREATGEO>K0COMMAND
  12 236D ...      SEG   QPRINT,867,SECTION CENT 
   4 23?L ...[   
   4 23DS ...[   
  16 23L2 ...[  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982 
   4 23R8 ...[   
   4 23YG    [   
  18 24D6    [              THIS SEGMENT OUTPUTS THE REQUIRED INFORMATION
  17 24XQ    [              OF A JOB WHOSE BASIC DETAILS ARE GIVEN IN
  18 25CB    [              THE CQPRINT/CQ11 BLOCK. THE O/P IS DEPENDENT 
  18 25X2    [              ON THE CATEGORIES (IE.ZGH) SET IN CQPRINT/CQ1
  19 26BL    [              AND IS WRITTEN TO THE FILE OPEN AT THE TOP LEVEL 
   4 26W=    [   
  18 27*W          SEGENTRY K1QPRINT,Q1ENTRY    [WRITE JOB'S 'WS' INFO   
  17 27TG          SEGENTRY K4QPRINT,Q4ENTRY      [WRITE 'NO JOBS'   
   9 2847          SEGENTRY K50QPRINT
   8 286B    SWITCH   #40000000  
  17 288K          BITDEFS  SWITCH,0,TPWSWITCH,WSNARROWOP,WSNARROWALL
  19 28?_    WSNBITS   #00340000      [BITS IF UNSET MEAN NOT PROG OR FULL   
  12 28*6    [     DEFINES THE CQPRINT/CQ1 BLOCK 
  17 28SQ    #DEF  ZGA = A1                     [GROUP A INDICATORS  
  17 29#B    #DEF  ZGB = ZGA+1                  [GROUP B INDICATORS  
  17 29S2    #DEF  ZGC = ZGB+1                  [GROUP C INDICATORS  
  17 2=?L    #DEF  ZGD = ZGC+1                  [GROUP D INDICATORS  
  17 2=R=    #DEF  ZGE = ZGD+1                  [GROUP E INDICATORS  
  17 2?=W    #DEF  ZGF = ZGE+1                  [GROUP F INDICATORS  
  17 2?QG    #DEF  ZGG = ZGF+1                  [GROUP G INDICATORS  
  17 2#=6    #DEF  ZGH = ZGG+1                  [GROUP H INDICATORS  
  14 2#PQ    #DEF  ZNAME =  ZGH+1               [JOB NAME
  15 2*9B    #DEF  ZUSER =  ZNAME+3             [USER NAME   
  16 2*P2    #DEF  ZPROP =  ZUSER+3             [PROPERTY NUMBER 
  18 2B8L    #DEF  ZLCOUNT= ZPROP+1             [COUNT OF JOB NOS. STORED
  15 2BN=    #DEF  ZLJOB =  ZLCOUNT+1           [JOB NUMBER  
  11 2C7W    [     DEFINES THE CQPRINT/CQ11  
  15 2CMG    #DEF  WJOBNO  = A1                  [JOB NUMBER 
   9 2D76    #DEF  WNAME   = WJOBNO+1
   9 2DLQ    #DEF  WUSER   = WNAME+3 
   9 2F6B    #DEF  WMISC   = WUSER+3 
   9 2FL2    #DEF  WJLSTAT = WMISC   
   9 2G5L    #DEF  WYONQ   = WMISC+1 
   9 2GK=    #DEF  WCONTEXT= WYONQ+1 
  10 2H4W    #DEF  WSOURCE1= WCONTEXT+1  
  10 2HJG    #DEF  WSOURCE2= WSOURCE1+1  
  10 2J46    #DEF  WSOURCE3= WSOURCE2+1  
  10 2JHQ    #DEF  WURGE   = WSOURCE3+1  
   9 2K3B    #DEF  WCPI    = WURGE+1 
   9 2KH2    #DEF  WTIMEJ  = WCPI+1  
   9 2L2L    #DEF  WARRIVED= WTIMEJ  
  10 2LG=    #DEF  WPROGTIME =WTIMEJ+2   
  10 2L_W    #DEF  WARRIVET= WPROGTIME   
  10 2MFG    #DEF  WCSIZE  = WPROGTIME+2 
   9 2M_6    #DEF  WFWT    = WCSIZE+1
   8 2N?G    #DEF  WJLSTATX=WFWT 
   9 2NDQ    #DEF  WTLR    = WFWT+1  
   9 2T_Q    #DEF  WTSWAPS = WTLR+1  
  10 2W33    #DEF  WMAXSIZE= WTSWAPS+1   
  10 2W4#    #DEF  WWT1    = WMAXSIZE+1  
   9 2W5K    #DEF  WWT2    = WWT1+1  
   9 2W6W    #DEF  WWT3    = WWT2+1  
   9 2W87    #DEF  WEXTRA  = WWT3+1  
   9 2W9D    #DEF  WPERPTR = WEXTRA+1
  10 2W=T    #DEF  WCOREPRI = WPERPTR+1  
  10 2W?#    #DEF  WMILLPRI = WCOREPRI+1 
   4 2W?R    [   
  16 2W#=          BITDEFS     WJLSTAT,2,XNOTCAND,XABAND,XHARDUP 
  18 2WCH          BITDEFS  WJLSTAT,5,XBREAK,XGENRES,XBREAKGR,XABANDON   
  15 2WDL          BITDEFS  WJLSTAT,9,XBMAP,XREST,XGRES,XGRNR
  18 2WFP          BITDEFS  WJLSTAT,13,XGRQUERY,XRERUN,XRUNDUMP,,XFROZEN 
   4 2WGJ    [   
  13 2WHD          BITDEFS  WJLSTATX,0,XPARTRES,XPRES
  12 2WJ4          BITDEFS  WEXTRA,0,XWELL,XLIMBO
  11 2WLL          BITDEFS  WEXTRA,2,XEXPR   
  12 2WM_          BITDEFS  WYONQ,0,SAVED,SAVING 
   9 2WRW    ZAWSHEAD       +AWSNAME 
   9 2X?G    ZAWSNAME       +AWSNAME 
   9 2XR6    ZAWSBREAK      +IWSPERIS
  10 2Y=Q    ZAWSCONTEXT    +AWSCONTEXT  
   9 2YQB    ZAWSCONTXTS    +AWSNAME 
   9 2_=2    ZAWSWAIT       +AWSNAME 
   9 329=    ZAWSCORE       +JWSPERIS
   9 32NW    ZAWSNOCOR      +AWSPERIS
  10 338G    ZAWJNJOB2       +AWJNJOB2   
  10 33D?    ZAWSCOREHD1    +AWSCOREHD1  
  10 33GW    ZAWSCOREHD2    +AWSCOREHD2  
   6 33L*    #UNS  ICT   
   8 33M9    ZAWSACTPR  +AWSACTPR
   4 33N6    [   
   4 347Q    [   
   4 34MB    [   
  13 3572    WL             4HWL          [WELL FIELD
  16 35LL    NC             4HNC                  [NON CANDIDATE 
  15 366=    SV             4HSV              [SAVED FIELD   
   9 36KW    PDAY           8H> A DAY
  10 375G    PTIME          8H-VE TIME   
  16 37K6    XDAYL          +10                 [DAY OF MILLISECS
   9 384Q    XDAYM          +2513920 
   9 389X    ROUNDUP        #20000000
   8 38?L    N100           +100 
   8 38**    XCOMMA         4H,  
   8 38C4    XBRA           4H(  
   8 38DR    XKET           4H)  
   8 38JB    N1000        +1000  
   9 3942    TENK           +10000   
   9 39HL    MAGIC          +7036875 
   8 3=3=    SPACE          #20  
  17 3=GW    TWENTFOURHR   +60*60*24/K9*1000   [1 DAY OF SC COUNTS   
  10 3?2G    WAIT           10HWAITING:- 
  16 3?5L    XPRESS         #34631463           [CPI OF EXP'D JOB
  20 3?5W    OPLURG         #10630404           [ZGH IN CQ1 IF O/P LEVEL = URGE  
  20 3?66    OPLFULL        #10777774           [   ^    ^    ^    ^     = FULL  
   4 3?6B    [   
   4 3?7N    (   
  15 3?8Q    XCATWELL       #77650400       [WELL CATEGORIES 
  16 3??W    XCATSAVE       #77040400         [SAVE CATEGORIES   
  16 3?C2    XCATNCOR       #77474400        [NO CORE CATEGORIES 
  18 3?G6    WHMASK         #77700000              [1ST LINE CATEGORIES  
   4 3?H5    )   
   9 3?_Q    WINH           +AQPJNO  
   9 3#FB                   +AQPMOP  
   9 3#_2                   +AQPUSER 
   9 3*DL                   +AQPNAME 
   9 3*Y=                   +AQPUR   
   9 3BCW                   +AQPCP   
   9 3BXG                   +AQPJT   
   9 3CC6                   +AQPTI   
   9 3CWQ                   +AQPSIZE 
   9 3CY3    [WSNARROW HEADING TABLE 
   7 3C_#    WINH1 +AQPJNO1  
   7 3D2K          +AQPMOP1  
   7 3D3W          +AQPUSER  
   7 3D57          +AQPNAME  
   6 3D6D          +AQPUR
   6 3D7P          +AQPCP
   6 3D92          +AQPJT
   6 3D=?          +AQPTI
   7 3D?J          +AQPSIZE  
   9 3D#T    SEVENCH        +1000000 
   9 3DBB    [     J TO PRINT ROUTINE
  16 3DW2    WIN   BRN      TJNO                [1. JOB NUMBER   
  14 3F*L          BRN      TMOP                [2. MOP? 
  15 3FT=          BRN      TUSE                [3. USERNAME 
  15 3G#W          BRN      TNAM                [4. JOBNAME  
  15 3GSG          BRN      TUR                 [5. URGENCY  
  14 3H#6          BRN      TCP                 [6. CPI  
  16 3HRQ          BRN      TJT                 [7. JOB TIME USED
  17 3J?B          BRN      TTI                 [8. PROGRAM TIME LEFT
  15 3JR2          BRN      TSIZ                [9. CORE/SIZE
  16 3K=L          BRN      TEOL                [10. END OF LINE 
  18 3KQ=          BRN      TBRK                [11. BEING BROKEN IN ON  
  15 3L9W          BRN      TCON                [12. CONTEXT 
  16 3LPG          BRN      TWT                 [13. WAITING FOR 
  17 3M96          BRN      TCOR                [14. CORE INFORMATION
  19 3MNQ          BRN      TPER                [15. PERIPHERAL INFORMATION  
  19 3N8B          BRN      TFIN                [16. END OF JOB INFORMATION  
  20 3NN2    TCONTEXT                           [ADDRESSES OF CNTX SUBMESSAGES   
   8 3P7L                   +TCB9
   9 3PM=                   +TCBNULL 
   9 3Q6W                   +TCB11   
   9 3QLG                   +TCB12   
   9 3R66                   +TCB13   
   9 3RKQ                   +TCB14   
   9 3S5B                   +TCB15   
   9 3SK2                   +TCB16   
   9 3T4L                   +TCBNULL 
   9 3TJ=                   +TCB18   
   9 3W3W                   +TCB19   
   9 3WHG                   +TCB20   
   9 3X36                   +TCB21   
   9 3XGQ                   +TCB22   
   9 3Y2B                   +TCB23   
   5 3YG2    SCONTEXT
   9 3YPR                   +TJB18   
   9 3YSB                   +TJB19   
   8 3Y_L                   +TJB3
   8 3_F=                   +TJB4
   8 3_H_                   +TJB5
   8 3_JW                   +TJB6
   8 3_KR                   +TJB7
   8 3_LN                   +TJB8
   8 3_MK                   +TJB9
   9 3_NG                   +TJB10   
   9 3_PC                   +TJB11   
   9 3_Q#                   +TJB12   
   9 3_R9                   +TJB13   
   9 3_S6                   +TJB14   
   9 3_T3                   +TJB15   
   9 3_TY                   +TCBNULL 
   9 3_WT                   +TJB17   
   8 3_YW    TCBNULL        +0   
  11 42DG    TCB9           11H:BROKEN IN,   
  10 42Y6    TCB11           9H8PROGRAM, 
  12 43CQ    TCB12          15H>FULLY STARTED,   
  13 43XB    TCB13          21H/TENTATIVELY STARTED, 
  11 44C2    TCB14          12H;CORE IMAGE,  
  12 44WL    TCB15          15H>NO CORE IMAGE,   
  10 45B=    TCB16           8H7REMOTE,  
  10 45TW    TCB18           8H7READER,  
  10 46*G    TCB19          10H9OPERATOR,
   9 46T6    TCB20           5H4MOP, 
  11 47#Q    TCB21          14H=OFF-LINE JOB,
  10 47SB    TCB22           9H8NO USER, 
   9 48#2    TCB23           6H5USER,
   4 48RL    [   
  14 49?=    TJB3           23H&TEMPORARILY ABANDONED,   
  12 49QW    TJB4           16H?USER OVERDRAWN,  
  11 49TK    TJB5        16H?RESTART(BREAK), 
   9 49WG    TJB6        8H7GENRES,  
  11 49XC    TJB7        15H>GENRES(BREAK),  
  12 49Y#    TJB8        18H!RESTART(ABANDON),   
  10 49_9    TJB9        11H:BACKMAPNC,  
   9 4=26          SEGENTRY K90QPRINT
  10 4=33    TJB10       20H<RESTARTABLE,
  11 4=3Y    TJB11       20H RESTARTABLE(GR),
  11 4=4T    TJB12       20H=NO RERUN(GR),   
  11 4=5C    TJB19       20H RESTARTABLE(PR),
  11 4=5Q    TJB13       15H>GENRES(QUERY),  
  11 4=6M    TJB14       15H>GENRES(RERUN),  
  11 4=7J    TJB15       17H GENRES(RUNDUMP),
  12 4=8F    TJB17          18H!DIRECTORY FROZEN,
   9 4==6    TJB18       9H8PARTRES, 
  12 4==G    [     S/R TO WRITE RECORD TO WS FILE
  13 4=Q6    [     AWORK4 = 0 IF WS FILE AT DEPTH 0  
  13 4?9Q    [            = 1 IF WS FILE AT DEPTH 1  
   8 4?PB    WSW   SBX   6   FX1 
   8 4#92          ASSMESS   5   
  10 4#NL          MHUNTW   3,GMON,AOUT  
  10 4*8=          NAME     3,FILE,FAPB  
   8 4*MW          LDX   0  A1(3)
   8 4B7G          ADN   0  11   
   7 4BM6          SRL   0  2
   8 4C6Q          STO   0  A1(3)
  15 4CLB          LDX   0  AWORK4(2)          [FILE DEPTH   
   8 4D62          BZE   0  WSW3 
  18 4DKL          APPEND   1,WSW9              [WRITE WS INFO TO WS FILE
   8 4F5=          BRN      WSW8 
  16 4FJW    WSW3  APPEND   0,WSW9              [WRITE TO WS FILE
   9 4G4G    WSW8  MFREE    FILE,FAPB
   9 4GJ6          TESTRPN2 OK,WSW10 
  13 4H3Q          ADN   6  1                   [OK  
  14 4HHB    WSW9  ADN   6  1                   [BREAK IN
  14 4J32    WSW10 ADX   6  FX1                 [ERROR   
   7 4JGL          EXIT  6  0
   4 4JQD    [   
   5 4K2=    OUTMESS 
  11 4KFW    [     S/R TO OUTPUT SUBMESSAGE  
   7 4K_G          LDX   1  4
   8 4LF6    OUTM2 SBX   4  FX1  
   8 4LYQ          OUTMESSX 0(1) 
   8 4MDB          ADX   4  FX1  
   7 4MY2          EXIT  4  1
   4 4N7S    [   
   5 4NCL    STOREM  
  16 4NX=    [     S/R TO FIND ADATA/CSTORE AND PUT ADDRESS IN X3
  16 4PBW    [     IF IT DOESN EXIST, A BLOCK OF CORE IS SET UP  
   8 4PWG          SBX   4  FX1  
  11 4QB6          HUNTW    3,ADATA,CSTORE   
  15 4QTQ          BPZ   3  STRM9               [J IF FOUND  
  11 4R5J          SETNCORE 60,3,ADATA,CSTORE
   8 4RT2    STRM9 ADX   4  FX1  
   8 4S#L          LDX   2  FX2  
   7 4SS=          EXIT  4  0
   4 4ST5    [   
   5 4STY    XOUTNUM 
   8 4SWG          SBX   7  FX1  
   8 4SX4          OUTNUM   5,7,1
   8 4SXL          ADX   7  FX1  
   7 4SY8          EXIT  7  0
   4 4SYQ    [   
  16 4S_#    [     S/R TO CONVERT BINARY INTEGER(<100) TO DECIMAL
   7 4S_W    [     X0 IS LINK
   7 4T2D    [     X1 = FX1  
   8 4T32    [     X2 -> O/P AREA
   9 4T3J    [     X5 HOLDS INTEGER  
   5 4T46    TODEC   
   9 4T4N          DVR   5  N100(1)  
   8 4T5=          STO   0  GEN0 
   8 4T5S          STO   7  GEN1 
   8 4T6B          LDCT  7  #200 
   7 4T6Y          LDN   0  2
   7 4T7G          MODE     0
   8 4T84    TD4   CBD   6  0(2) 
   7 4T8L          BCHX  2  /
   8 4T98          BCT   0  TD4  
   8 4T9Q          LDX   7  GEN1 
   9 4T=#          BRN      (GEN0)   
   4 4T=H    [   
   4 4T=Q    [   
   5 4T?2    WSNCHECK
  11 4T?3    [ SUBR TO CHECK USE OF WSNARROW 
  15 4T?4    [ EXIT 1 IF WIDTH OF OUTPUT IS TO BE RESTRICTED 
   8 4T?5    [ EXIT 0 OTHERWISE  
   8 4T?6          STO   0  GEN0 
  10 4T?7          MHUNTW   2,CQPRINT,CQ1
  17 4T?*          LDX   0  ZGH(2)             [PICK UP OUTPUT LEVEL 
   8 4T?C          SMO      FX1  
  18 4T?D          ANDX  0  WSNBITS            [IF NO MOP OR NAMES OUTPUT
  16 4T#?          BZE   0  WSNEX0             [NO SHORTENINGG   
  21 4T*6          JBS     WSNEX1,FX1,WSNARROWALL  [J IF NO  CHECK OF CONTEXT REQD   
  18 4T*_          JBC      WSNEX0,FX1,WSNARROWOP   [J IF NOT WSNARROW OP
  19 4TBS          JBC      WSNEX0,FX2,CXTOPERATOR    [J IF NOT OP CONTEXT   
   4 4TCM    [   
  11 4TDG    [     PICK UP ROUTING PARAMETER 
   9 4TF*          LDX   0  ZGG(2)   
  16 4TG8          ANDN   0  1              [MASK OUT ALL BUT B23
  16 4TH3          BNZ   0  WSNEX0              [J IF NOT ^HERE^ 
   4 4THW    [   
   5 4TJP    WSNEX1  
   7 4TKJ          LDN   0  1
   8 4TLC          ADS   0  GEN0 
   5 4TM=    WSNEX0  
   8 4TN5          LDX   2  FX2  
   9 4TNY          BRN      (GEN0)   
   4 4TPR    [   
  11 4TQL    [      OUTPUT THE 'WS' HEADING  
   8 4TRG    START ORX   0  GSIGN
   9 4W?6          STO   0  ZPROP(3) 
   8 4WQQ          OUTBLOCN  23  
  18 4X=B          ANDX  5  WHMASK(1)           [CATEGORIES OF FIRST LINE
   8 4XQ2          BRN      THED4
   8 4Y9L    THED2  ADN   6  1   
   7 4YP=          SLL   5  1
  18 4_8W    THED4 BZE   5  THED9                [J IF NO MORE CATEGORIES
  17 4_NG          BPZ   5  THED2                [J IF NOT REQ'D CAT 
   9 5286          LDN   1  WINH(1)  
  11 52=*    [ NOW CHECK IF WSNARROW IN USE  
   9 52#J          CALL  0  WSNCHECK 
  15 52BR          BRN      THED5       [J IF FULL OP REQD   
   8 52F2          SMO      FX1  
   8 52H9          LDN  1   WINH1
   5 52KD    THED5   
   7 52MQ          ADX   1  6
  16 537B          CALL  4  OUTM2               [GET SUB HEADING 
   8 53M2                   +0   
   8 546L          BRN      THED2
   5 54?S    THED9   
  10 54YJ          LDX   5  ZAWSHEAD(1)  
  15 555W          CALL  6  WSW                [O/P HEADING  
   8 55KG          BRN      XER  
   8 5656          BRN      XBRK 
   8 56JQ          BRN      NEX  
   5 574B    Q1ENTRY 
  10 57J2          LDX   0  ACOMMUNE1(2) 
  16 583L          STO   0  AWORK4(2)           [WS FILE DEPTH   
   4 58H=    NEX 
  17 592W    [     SELECT THE NEXT LEVEL OF WS INFORMATION REQUIRED  
  10 59GG          MHUNTW   3,CQPRINT,CQ1
   9 5=26          LDX   5  ZGH(3)   
   7 5=FQ          SLL   5  6
   7 5=_B          LDN   6  0
   9 5?F2          LDXC  0  ZPROP(3) 
   8 5?YL          BCC      START
  11 5?_X          MHUNTW     3,CQPRINT,CQ11 
  18 5#38          LDX   4  XCATWELL(1)          [CAT. REQ'D BY WL JOB   
   9 5#4F          LDX   0  WEXTRA(3)
  14 5#5Q          BNG   0  NEX05          [J IF WELL JOB
  10 5#73          LDX   4  XCATSAVE(1)  
  15 5#8Y          JMBS     NEX05,3,SAVED,SAVING [J IF SAVED 
  10 5#=W          LDX   4  XCATNCOR(1)  
   9 5##7          LDEX  0  WMISC(3) 
   8 5#*D          BNZ   0  NEX4 
   7 5#BP    NEX05 ANDX  5  4
   8 5#D=          BRN      NEX4 
   8 5#XW    NEX1  LDX   1  FX1  
   8 5*CG          LDX   2  FX2  
   9 5*X6          LDX   5  AWORK1(2)
   9 5BBQ          LDX   6  AWORK2(2)
  19 5BWB    NEX2  ADN   6  1                   [UPDATE O/P LEVEL INDICATOR  
  16 5CB2          SLC   5  1                   [UPDATE CATEGORY 
  18 5CTL    NEX4  BPZ   5  NEX2                [J IF NOT REQ'D O/P LEVEL
  17 5D*=          STO   5  AWORK1(2)           [SAVE CATEGORY BITS  
  18 5DSW          STO   6  AWORK2(2)           [SAVE CURRENT O/P LEVEL  
  19 5F#G          MHUNTW   3,CQPRINT,CQ11      [FIND JOB'S INFORMATION BLOCK
   9 5FS6          LDX   0  WNAME(3) 
  19 5G?Q          BZE   0  PLJ                 [J IF JOB NO LONGER EXISTS   
  18 5GRB          LDX   0  WEXTRA(3)           [WELL/STARTED IND. (B0)  
   7 5H?2          SMO      6
  18 5HQL          BRN      WIN                 [O/P SUBSET OF WS INFO.  
   4 5J==    TJNO
   8 5JPW    [     O/P JOB NUMBER
   9 5K9G          LDX   6  WJOBNO(3)
  10 5K*Y    [NOW CHECK USE OF WSNARROW  
   7 5KGB          LDN   4  7
  12 5KLS          LDN   5  0      [SET MARKER   
  14 5KR=          MPY   6  MAGIC(1)       [READY FOR CBD
   7 5KXN          MODE     1
   9 5L46          CALL  0  WSNCHECK 
  14 5L8J          BRN      TJNO1    [J IF FULL OP REQD  
   9 5L*2          LDX   0  WJOBNO(3)
  16 5LFD          BXGE  0  SEVENCH(1),TJNO1     [J IF LARGE NO  
  17 5LKW          CBD   6  ACOMMUNE1(2)     [GET RID OF EXTRA CHAR  
  15 5LQ#          LDN   4  6                   [SET COUNT   
  15 5LWQ          LDN   5  #20                 [AND SPACER  
   5 5M38    TJNO1   
  18 5M82    TJNO4 CBD   6  ACOMMUNE1(2)        [CONV. JOB NO. TO DEC.   
   7 5MML          BCHX  2  /
   8 5N7=          BCT   4  TJNO4
   9 5NLW          CALL  4  STOREM   
   8 5P6G          LDN   0  A1+1 
  16 5PL6          STO   0  A1(3)             [INITIAL CHAR. PNTR
  14 5PR#    [NOW SET COUNT AND SPACER IF WSNARROW IN USE
   7 5PYG          LDX   6  5
   7 5Q5N          LDN   5  7
   9 5Q=W          CALL  0  WSNCHECK 
  16 5QD4          BRN      STN         [J IF FULL OUTPUT REQD   
  21 5QK=          BZE   6  STN2                [IF ZERO SPACER & WSNARROW THEN 7 CHA
  15 5QQD          LDN   5  6                   [OTHERWISE 6 
   8 5QXL          BRN      STN2 
   4 5R52    TMOP
   7 5RJL    [     O/P MOP?  
   8 5S4=          LDX   0  ACES 
  10 5SHW          STO   0  ACOMMUNE1(2) 
  10 5T3G          STO   0  ACOMMUNE2(2) 
  10 5TH6          STO   0  ACOMMUNE3(2) 
  15 5W2Q          LDN   4  ACOMMUNE1           [PTR FOR O/P 
  13 5WGB          LDN   0  #4351               [CI  
  10 5X22          DSA   0  ACOMMUNE1(2) 
  10 5XFL          LDCH  0  WSOURCE1(3)  
   8 5X_=          SBN   0  #74  
  14 5YDW          BZE   0  TMOP8               [J IF CI 
  13 5YYG          LDN   0  #5620               [N   
  10 5_D6          DSA   0  ACOMMUNE1(2) 
  10 5_XQ          LDX   6  WSOURCE3(3)  
  16 62CB          ANDX  6  BITS22LS            [PROPERTY NUMBER 
  16 62X2          JMBAC    TMOP4,3,SAVED,SAVING [J IF NOT SAVED 
  16 63BL    TMOP3 STOZ     ACOMMUNE8(2)        [FOR ORIGIN MACRO
  18 64*W          BZE   6  TMOP8               [J IF CENTRAL AND SAVED  
  10 64TG          STO   6  ACOMMUNE9(2) 
  13 65*6          LDN   0  #6220               [R   
  10 65SQ          DSA   0  ACOMMUNE1(2) 
   7 66#B          SBN   4  1
   8 66S2          ORX   4  GSIGN
   8 67?L          BRN      TMOP7
   9 67R=    TMOP4 LDXC  0  WEXTRA(3)
  17 68=W          BCC      TMOP43              [J IF 'STARTED' JOB  
  10 68QG          LDX   0  WJLSTAT(3)   
   7 69=6          ANDN  0  1
  14 69PQ          BNZ   0  TMOP6               [J IF MOP
   9 6=9B          BRN      TMOP45   
   5 6=P2    TMOP43  
   9 6?8L          LDXC  0  WMISC(3) 
  14 6?N=          BCC      TMOP6               [J IF MOP
   5 6#7W    TMOP45  
  17 6#MG          BZE   6  TMOP8               [J IF CENTRAL & BACK 
   8 6*76          BRN      TMOP3
  10 6*LQ    TMOP5 DSA   0  ACOMMUNE1(2) 
   8 6B6B          BRN      TMOP8
  18 6BL2    TMOP6 JBC      TMOP65,3,XLIMBO         [J IF NOT IN LIMBO   
  13 6D4W          LDN   0  #5420               [L   
   8 6DJG          BRN      TMOP5
   5 6F46    TMOP65  
  10 6FHQ          LDN   1  WSOURCE1(3)  
  10 6G3B          LDN   2  ACOMMUNE7(2) 
   7 6GH2          MOVE  1  3
   6 6H2L    TMOP7 ORIGIN
  10 6H_W          LDN   3  ACOMMUNE2(2) 
   9 6J52          BPZ   4  TMOP75   
   7 6J86          SBN   3  1
   8 6J?=          ORX   3  GSIGN
   5 6JBB    TMOP75  
  10 6JFG          LDN   4  ACOMMUNE1(2) 
   7 6J_6          MVCH  3  7
  15 6KDQ    TMOP8 LDN   5  7                   [FIELD WIDTH 
  10 6KKY    [NOW CHECK USE OF WSNARROW  
   9 6KR6          CALL  0 WSNCHECK  
  15 6KY#          BRN      STN          [J IF FULL OP REQD  
  16 6L5G    TMOP9 LDN   6  0                   [SET NO SPACER   
   8 6L=N          BRN      STN2 
   4 6LD2    TUSE
   8 6LXL    [     O/P USER NAME 
   8 6MC=          LDX   4  ACES 
  10 6MWW          STO   4  ACOMMUNE1(2) 
  10 6NBG          STO   4  ACOMMUNE2(2) 
  10 6NW6          STO   4  ACOMMUNE3(2) 
  19 6P*Q          LDN   4  WUSER(3)                 [SAVE POS. OF USERNAME  
   8 6PN7          LDN   5  12   
   8 6Q2J          LDN   6  13   
   9 6Q*2    TUSE3 LDCH  0  WUSER(3) 
   7 6QSL          BCHX  3  /
  15 6R73          BXE   0  SPACE(1),TUSE5     [J IF SPACE   
   7 6RFD          LDX   6  5
   8 6RRT    TUSE5 BCT   5  TUSE3
   7 6S6=          SBN   6  1
  10 6SDM          LDN   2  ACOMMUNE1(2) 
   7 6SR6          SLC   2  2
   7 6T=Q          ADX   2  6
   7 6TQB          SRC   2  2
  13 6W=2          LDN   0  #12                 [:   
   8 6WPL          DCH   0  0(2) 
   7 6X9=          BCHX  2  /
   8 6XNW          LDN   5  12   
   7 6Y8G          SBX   5  6
   8 6YN6          BZE   5  TUSE8
   7 6_7Q          LDX   1  4
   7 6_MB          SMO      5
  17 7272          MVCH  1  0                 [PLACE USERNAME AFTER :
  15 72LL    TUSE8 LDN   5  13                [FIELD WIDTH   
  15 736=          LDN   6  #36               [SPACER = '.'  
   8 73KW          BRN      STN2 
   4 745G    TNAM
   8 74K6    [     O/P JOB NAME  
   9 754Q          LDN   4  WNAME(3) 
  10 75JB          LDN   5  ACOMMUNE1(2) 
  15 7642          MOVE  4  3               [STORE JOB NAME  
  14 76HL          LDN   5  12              [FIELD WIDTH 
   8 773=          BRN      STN  
   4 77GW    TUR 
  11 782G    [     O/P URGENCY/SAVED OR WELL 
  13 78G6          LDX   0  WURGE(3)         [URGENCY
  10 78_Q          DCH   0  ACOMMUNE1(2) 
   7 79FB          LDN   5  1
  17 79ML          JBC      STN,3,XEXPR         [J IF NOT EXPRESSED  
  16 7?CW          LDN   6  #45               [E FOR EXPRESSED   
   8 7?XG          BRN      STN2 
   4 7#C6    TCP 
   7 7#WQ    [     O/P CPI   
  16 7*BB          LDN   5  2                     [FIELD WIDTH   
  17 7*W2          BNG   0  TNC                 [J IF STILL IN WELL  
  15 7B*L          JMBS     TCP4,3,SAVED,SAVING [J IF SAVED  
   9 7C#W          LDX   6  WCPI(3)  
  17 7CG3          JBC      WHICHCP4,1,TPWSWITCH[J IF WSCOREPRI OFF  
  10 7CHD          LDEX  5  WCOREPRI(3)  
   7 7CJW          LDN   6  0
   9 7CL2          DVR   5  N100(1)  
   7 7CM8          LDN   5  2
   5 7CNX    WHICHCP4
   8 7CSG          LDCT  7  #200 
   7 7D#6          LDN   4  2
   7 7DRQ          MODE     0
  18 7F?B    TCP2  CBD   6  ACOMMUNE1(2)               [CONV.CPI TO DEC. 
   7 7FR2          BCHX  2  /
   8 7G=L          BCT   4  TCP2 
   9 7GQ=          LDX   0  WMISC(3) 
   8 7H9W          ANDN  0  #600 
  15 7HPG          BZE   0  STN           [J IF NOT REALTIME 
  15 7J96          LDN   6  #62           [R FOR REAL TIME   
   8 7JNQ          BRN      STN2 
   8 7K8B    TCP4  LDX   0  SV(1)
  16 7KN2          STO   0  ACOMMUNE1(2)           ['SV' FIELD   
   8 7L7L          BRN      STN  
  10 7LM=    [     O/P IF DEGENERATE JOB 
  16 7M6W    TNC   LDX   4  NC(1)                 [NON CANDIDATE 
  16 7MLG          JBS      TNC2,3,XNOTCAND      [J IF DEGENERATE
  20 7N66          LDX   4  WL(1)                       [STORE 'WL' IN CPI FIELD 
  10 7NKQ    TNC2  STO   4  ACOMMUNE1(2) 
   8 7P5B          BRN      STN  
   4 7PK2    TJT 
  18 7Q4L    [     O/P JOB TIME OR DATE OF ENWELLMENT IF STILL IN WELL   
  16 7QJ=          BNG   0  TARD                [J IF WELL JOB   
  10 7R3W          LDX   4  WTIMEJ+1(3)  
   9 7RHG          LDX   3  WTIMEJ(3)
   8 7S36    TJT1  BNG   3  TJT5 
  15 7SGQ          BZE   3  TJT3                [<DAY IF ZERO
   9 7T2B          SBXC  4  XDAYL(1) 
   9 7TG2          SBX   3  XDAYM(1) 
  15 7T_L          BPZ   3  TJT7                [J IF >DAY   
   9 7WF=          ADXC  4  XDAYL(1) 
  15 7WYW          ADX   3  XDAYM(1)            [RESET TIMES 
   8 7XDG    TJT3  LDN   0  K9   
   7 7XY6          DVD   3  0
  14 7YCQ          TIMECON  4                   [CONVERT 
  13 7YXB    TJT4  LDN   5  8            [FIELD WIDTH
   8 7_C2          BRN      STN  
  15 7_WL    TJT5  LDN   3  PTIME(1)               [-VE TIME 
   8 82B=          BRN      TJT8 
  14 82TW    TJT7  LDN   3  PDAY(1)             [> A DAY 
  10 83*G    TJT8  LDN   4  ACOMMUNE1(2) 
   7 83T6          MOVE  3  2
   8 84#Q          BRN       TJT4
   4 84SB    TARD
  10 85#2    [     O/P DATE OF ARRIVAL   
  10 85RL          LDX   4  WARRIVED(3)  
  18 86?=          DATECON  4                   [CONVERT TO CHAR. FORM   
  16 86QW          BRN      TJT4                [J TO O/P DATE   
   4 87=G    TTI 
  17 87Q6    [     O/P PROGRAM TIME OR DATE OF ENWELLMENT IF WELL JOB
  17 889Q          BNG   0  TART                [J IF STILL IN WELL  
  13 88PB          LDX   4  WPROGTIME+1(3)      [GET 
  19 8992          LDX   3  WPROGTIME(3)        [   PROGRAM TIME REMAINING   
   8 89NL          BRN      TJT1 
   4 8=8=    TART
  10 8=MW    [     O/P TIME OF ARRIVAL   
  18 8?7G          LDX   3  WARRIVET(3)         [MILLISECS SINCE MIDNIGHT
   9 8?M6          MPY   3  N1000(1) 
   8 8#6Q          BRN      TJT3 
   4 8#LB    TSIZ
   8 8*62    [     O/P CORE SIZE 
   9 8*KL          SMO      WCSIZE(3)
  16 8B5=          LDN   5  0                   [CLEAR TOP 2 BITS
  17 8BJW          BXGE  5  TENK(1),TSIZ2       [J IF >=10000 WORDS  
   9 8C4G          DVR   5  TENK(1)  
   8 8CJ6          LDCT  7  #200 
   7 8D3Q          LDN   4  4
   7 8DHB          MODE     1
  16 8F32    TSIZ1 CBD   6  ACOMMUNE1(2)      [CONV. CORE TO DEC.
   7 8FGL          BCHX  2  /
   8 8G2=          BCT   4  TSIZ1
   7 8GFW          LDN   5  4
   8 8G_G          BRN      STN  
  18 8HF6    TSIZ2 ADN   5  1023                [ROUND UP AND DIVIDE BY K
   8 8HYQ          SRL   5  10   
   9 8JDB          MPY   5  MAGIC(1) 
   7 8JY2          MODE     0
   7 8KCL          LDN   0  0
  16 8KX=          LDN   1  7                   [MAX. CHAR. WIDTH
   7 8LBW    TSIZ4 CBD   5  0
  19 8LWG          BNZ   0  TSIZ6               [J IF FIRST NON-ZERO CHAR.   
   8 8MB6          BCT   1  TSIZ4
  18 8MTQ          STOZ     ACOMMUNE1(2)        [SET '0' IF ONLY CHAR.   
   8 8N*B          BCHX  2  TSIZ9
  10 8NT2    TSIZ6 DCH   0  ACOMMUNE1(2) 
   8 8P#L          BCHX  2  TSIZ8
  18 8PS=    TSIZ7 CBD   5  ACOMMUNE1(2)        [CONVERT TO RIGHT PLACE  
   7 8Q?W          BCHX  2  /
   8 8QRG    TSIZ8 BCT   1  TSIZ7
  13 8R?6    TSIZ9 LDN   6  #53                 [K   
   8 8RQQ          SBX   2  FX2  
   7 8SQ2          SLC   2  2
   7 8T9L          LDX   5  2
   8 8TP=          BRN      STN2 
  12 8W8W    [ PUT INFORMATION INTO ADATA/CSTORE 
  16 8WNG    STN   LDN   6  #20         [DEFAULT SPACER = SPACE  
  18 8X86    STN2  CALL  4  STOREM             [CREATE/FIND ADATA/CSTORE 
  17 8XMQ          LDX   0  3                   [SAVE ADDR. OF CSTORE
  17 8Y7B          ADX   3  A1(3)               [PTR. TO END OF INFO.
   9 8YM2          ADN   2  ACOMMUNE1
   7 8_6L          SMO      5
  18 8_L=          MVCH  2  0                    [STORE INFO. IN CSTORE  
  16 8_W4          BZE   6  STN3               [J IF  NO SPACER  
  15 925W          DCH   6  0(3)                [STORE SPACER
   7 92KG          BCHX  3  /
   4 92T#    STN3
   7 9356          SBX   3  0
   7 93JQ          SMO      0
  17 944B          STO   3  A1                 [PTR. TO END OF INFO. 
   8 94J2          BRN      NEX1 
   4 953L    TEOL
  16 95H=    [     COMPLETE ASSEMBLY OF MESSAGE AND WRITE AWAY   
   8 962W    [     TO 'WS' FILE  
   9 96GG          CALL  4  STOREM   
   8 9726          LDX   4  A1(3)
   8 97FQ          SBN   4  A1+1 
   7 97_B          SLC   4  2
  12 98F2          OUTPARAM 4,A1+1,ADATA,CSTORE  
  10 98YL          LDX   5  ZAWSNAME(1)  
   6 98_K    #UNS  ICT   
   4 992J    (   
   8 993H          CALL  6  WSW  
   8 994G          BRN      XER  
   8 995F          BRN      XBRK 
   9 996D          LDX   5  AWORK1(2)
   8 997C          ANDN  5  #20  
   8 998B          BZE   5  NEX1 
  10 999*          JBC      NEX1,,ICTSW  
   4 999D    [   
  14 999H    [     OUTPUT ACTIVITY PRIORITY IF REQUIRED  
  10 999L          MHUNTW   3,CQPRINT,CQ1
  18 999P          LDX   5  ZGH(3)              [GET O/P LEVEL PARAMETER 
  15 999S          BXE   5  OPLURG(1),TACTPR    [J IF URGE   
  18 999X          BXU   5  OPLFULL(1),NEX1     [J IF NOT URGE OR FULL   
   4 99=2    [   
   5 99=5    TACTPR  
  11 99=#          MHUNTW   3,CQPRINT,CQ11   
  10 99??          LDCH  5  WCOREPRI(3)  
  17 99#=          ANDN  5  #37        [IGNORE FIXED BIT OF ACT PRI  
   8 99*9          OUTNUM   5,1,1
  14 99B8          LDX   5  ZAWSACTPR(1)  [GET MESSAGE   
   4 99C7    )   
   8 99D=          BRN      TWRL 
   4 99XW    TBRK
  10 9=CG    [     O/P BREAK-IN MESSAGE  
   9 9?WB          LDX   0  WMISC(3) 
   7 9#B2          SLL   0  2
  19 9#TL          BPZ   0  NEX1                [J IF NOT BEING BROKEN-IN ON 
  10 9**=          LDX   5  ZAWSBREAK(1) 
   8 9*SW          BRN      TWRL 
   4 9B#G    TCON
   8 9BS6    [     O/P CONTEXTS  
  10 9C?Q          LDX   0  WCONTEXT(3)  
  18 9CRB          BNZ   0  TCON1           [J IF CONTEXT IE.STARTED JOB 
  13 9D5Q          JMBS     PRSET,3,XPRES,XPARTRES   
  21 9D75          JMBAC    NEX1,3,XABAND,XHARDUP,XBREAK,XGENRES,XBREAKGR,XABANDON,X-
  16 9D?2    REST,XGRES,XGRNR,XGRQUERY,XRERUN,XRUNDUMP,XFROZEN   
   5 9D#Y    PRSET   
  10 9DBX          JBC      TCW1,3,XABAND
  10 9DGS          BC       3,XABANDON   
  17 9DLP    TCW1  JMBAC    TCW2,3,XBREAKGR,XGRQUERY,XRERUN,XRUNDUMP 
   9 9DQL          BC       3,XGENRES
   4 9DWH    TCW2
  11 9FPW    TCON1 LDX   5  ZAWSCONTEXT(1)   
   8 9G9G          CALL  6  WSW  
   8 9GP6          BRN      XER  
   8 9H8Q          BRN      XBRK 
  16 9HNB          CALL  4  STOREM              [X3  ADATA/CSTORE
   8 9J82          ADN   3  A1   
  19 9JML          LDN   4  0                   [COUNT OF CHARS IN MESSAGE   
  11 9K7=          MHUNTW   2,CQPRINT,CQ11   
   9 9KLW          LDX   0  WEXTRA(2)
  16 9L6G          BNG   0  TCON8               [J IF 'WELL' JOB 
  15 9LL6          LDX   6  WCONTEXT(2)         [CPAT CONTEXT
  19 9M5Q          LDN   7  15                  [POSN OF CURRENT CONTEXT BIT 
   7 9MKB    TCON2 SRC   6  1
  18 9N52          BPZ   6  TCON4               [J IF CONTEXT BIT NOT SET
   7 9NJL          SMO      7
  10 9P4=          LDX   2  TCONTEXT-1(1)
  19 9PHW    TCON3  ADX   2  FX1                 [X2   CONTEXT MESSAGE PART  
   8 9Q3G          LDCH  0  0(2) 
  17 9QH6           BZE   0  TCON4         [J IF NOT REQD CONTEXT BIT
  17 9R2Q          ADX   4  0                   [UPDATE CHAR. COUNT  
   7 9RGB          BCHX  2  /
   7 9S22          SMO      0
  19 9SFL          MVCH  2  0                   [MESSAGE PART  ADATA/CSTORE  
   8 9S_=    TCON4 BCT   7  TCON2
  17 9TDW    TCON6 SBN   4  1                   [REMOVE LAST COMMA   
  20 9TYG          BNG   4  NEX1                  [J IF NO CONTEXTS   EG. SAVED  
  11 9WD6    TCON7 OUTPARAM 4,A1,ADATA,CSTORE
  11 9WXQ          LDX   5  ZAWSCONTXTS(1)   
   8 9XCB          BRN      TWRL 
  16 9XX2    TCON8 LDX   6  WJLSTAT(2)          [JOBLIST JLSTAT  
   7 9Y6S          SRC   6  6
   8 9Y7M          ANDX  6  BSP16
   8 9Y7Y          LDCT  0  #600 
  10 9Y89          ANDX  0  WJLSTATX(2)  
   7 9Y8G          SRL   0  7
   7 9Y8R          ORX  6  0 
   8 9Y94          LDN   7  17   
  11 9YNN          ADN   1  SCONTEXT-TCONTEXT
   8 9_8#          BRN      TCON2
   4 9_MY    TWT 
   9 =27J    [     O/P WHY WAITING   
   9 =2M8          LDX   6  WWT1(3)  
  16 =36S          BZE   6  NEX1                [J IF NOT WAITING
  10 =3LD          STO   6  ACOMMUNE6(2) 
   9 =464          DOWN     WTSTAT,1 
  14 =4KN          OUTPARAM ACOMMUNE7(2),A1,ADATA,CSTORE 
  10 =55#          LDX   5  ZAWSWAIT(1)  
   8 =5JY          BRN      TWRL 
   4 =64J    TCOR
  11 =6J8    [     O/P PROGRAM INFORMATION   
   9 =73S          LDX   0  WCSIZE(3)
  17 =7HD          BZE   0  TCOR9                [J IF NO CORE IMAGE 
  11 =834          LDX   5  ZAWSCOREHD1(1)   
  17 =8GN          JBC      TCORECS1,1,TPWSWITCH [J IF WSCOREPRI OFF 
  11 =92#          LDX   5  ZAWSCOREHD2(1)   
   5 =9FY    TCORECS1
   8 =9_J          CALL  6  WSW  
   8 ==F8          BRN      XER  
   8 ==YS          BRN      XBRK 
  11 =?DD          MHUNTW   3,CQPRINT,CQ11   
   9 =?Y4          LDX   5  WFWT(3)  
   9 =#CN          LDX   6  WTLR(3)  
   7 =#X#          LDX   4  5
  16 =*BY          BPZ   4  TCOR3               [J IF ALREADY RUN
   7 =*WJ          LDN   5  0
   7 =BB8          SLL   4  1
  18 =BTS          BNG   4  TCOR3               [J IF NOT RUN SINCE LOAD 
   8 =C*D          LDX   0  ACES 
  10 =CT4          STO   0  ACOMMUNE2(2) 
   7 =D#N          SBN   0  1
  10 =DS#          STO   0  ACOMMUNE1(2) 
  15 =F?Y          OUTPARN  7,ACOMMUNE1         [O/P 7H   ?  
   9 =FRJ          BRN      TCOR35   
   9 =G?8    TCOR3 CALL  7  XOUTNUM  
   5 =GQS    TCOR35  
  18 =H=D          BNG   4  TCOR4               [J IF NOT RUN SINCE LOAD 
   8 =HQ4          BPZ   6  TCOR6
  11 =J9N          ADX   6  TWENTFOURHR(1)   
   8 =JP#          BPZ   6  TCOR6
   8 =K8Y    TCOR4 LDX   0  ACES 
  10 =KNJ          STO   0  ACOMMUNE1(2) 
  10 =L88          STO   0  ACOMMUNE2(2) 
  13 =LMS          LDN   0  #17                 [?   
   8 =M7D          BPZ   4  TCOR5
  13 =MM4          LDN   0  #54                 [L   
  10 =N6N    TCOR5 DEX   0  ACOMMUNE1(2) 
   8 =NL#          BRN      TCOR7
   7 =P5Y    TCOR6 TIMECON  6
  10 =PKJ    TCOR7 OUTPARN  8,ACOMMUNE1  
  11 =R4D          MHUNTW   3,CQPRINT,CQ11   
  10 =RJ4          LDX   4  WMILLPRI(3)  
  10 =S3N          LDEX  5  WCOREPRI(3)  
  17 =SH#          JBC      TCORECS2,1,TPWSWITCH[J IF WSCOREPRI OFF  
   9 =T2Y          LDX   5  WCPI(3)  
   9 =TGJ          MPY   5  N100(1)  
  10 =W28          ADXC  6  ROUNDUP(1)   
   7 =WFS          ADN   5  0
   5 =W_D    TCORECS2
   9 =XF4          CALL  7  XOUTNUM  
  10 =XYN          LDN   2  ACOMMUNE1(2) 
   8 =YD#          LDX   0  ACES 
   8 =YXY          STO   0  0(2) 
   8 =_CJ          LDN   3  1(2) 
   7 =_X8          MOVE  2  3
  16 ?2BS          LDX   0  4                   [MILL PRIORITY   
   7 ?2WD          LDN   3  0
   7 ?3B4          SLL   0  6
  18 ?3TN          BZE   0  TMILLS2            [J IF ONLY 1ST MEMBER REQ 
   9 ?4*#          LDCH  0  XBRA(1)  
  17 ?4SY          DCH   0  0(2)                [O/P ( FOR MILL TEXT 
   7 ?5#J          LDN   7  4
   9 ?5S8          BCHX  2  TMILLS3  
  16 ?6?S    TMILLS2 LDCH  5  4(3)              [PICK UP MILLP   
   8 ?6RD          CALL  0  TODEC
   9 ?7?4          BRN      TMILLS9  
  10 ?7QN    TMILLS25 LDCH  0  XCOMMA(1) 
   8 ?8=#          DCH   0  0(2) 
   7 ?8PY          BCHX  2  /
  16 ?99J    TMILLS3 LDCH  5  4(3)              [GET NEXT MILLP  
   9 ?9P8          BZE   5  TMILLS5  
   8 ?=8S          CALL  0  TODEC
   8 ?=ND    TMILLS5 BCHX  3  /  
  10 ??84    TMILLS6 BCT   7  TMILLS25   
   9 ??MN          LDCH  0  XKET(1)  
   8 ?#7#          DCH   0  0(2) 
   7 ?#LY          BCHX  2  /
   5 ?*6J    TMILLS9 
  10 ?*L8          OUTPARN  13,ACOMMUNE1 
  10 ?JYJ    TCOR8 LDX   5  ZAWSCORE(1)  
   8 ?KD8          BRN      TWRL 
  10 ?KXS    TCOR9 MHUNTW   3,CQPRINT,CQ1
   9 ?LCD          LDX   0  ZGF(3)   
   8 ?LX4          ANDN  0  #6000
  19 ?MBN          BZE   0  NEX1               [J IF NO CORE JOBS SELECTED   
  10 ?MW#          LDX   5  ZAWSNOCOR(1) 
   8 ?N*Y          BRN      TWRL 
   4 ?NTJ    TPER
  11 ?P*8    [     O/P PERIPHERAL INFORMATION
   9 ?PSS          LDX   0  WCSIZE(3)
  17 ?Q#D          BZE   0  NEX1               [J IF NO CORE IMAGE   
   9 ?QS4          LDX   0  AWORK4(2)
  18 ?R?N          STO   0  ACOMMUNE1(2)             [DEPTH OF 'WS' FILE 
   9 ?RR#          DOWN     WSPERIS,1
   8 ?S=Y          BRN      XER  
   8 ?SQJ          BRN      XBRK 
   8 ?T=8          BRN      NEX1 
  17 ?TPS    TWRL  CALL  6  WSW              [O/P INFO. TO 'WS' FILE 
   8 ?W9D          BRN      XER  
   8 ?WP4          BRN      XBRK 
   8 ?X8N          BRN      NEX1 
   4 ?XN#    TFIN
  13 ?Y7Y    [     END OF THIS JOBS WS INFORMATION   
  13 ?YMJ    QND   UPPLUS   2                    [OK 
  14 ?_78    XER  UPPLUS   1                    [ERROR   
  14 ?_LS    XBRK  UP                           [BREAK IN
   4 #26D    PLJ 
  13 #2L4    [     O/P JOB NO LONGER EXISTS MESSAGE  
  10 #35N          OUTNUM   WJOBNO(3),7,1
  16 #3K#          OUTMESS  BWSPERIS               [JOB DELETED  
  10 #44Y          LDX   5  ZAWSNAME(1)  
   8 #4JJ          CALL  6  WSW  
   8 #548          BRN      XER  
   8 #5HS          BRN      XBRK 
   8 #63D          BRN      QND  
   9 #72N    [    NO JOB(S) SELECTED 
   5 #7G#    Q4ENTRY 
  18 #?C#          STOZ     AWORK4(2)                  ['WS' FILE DEPTH  
   8 #?WY          OUTBLOCN 23   
  18 ##BJ          OUTMESS  AWJNJOB2            [NO SUCH JOBS IN SYSTEM  
  10 ##W8          MHUNTW   3,CQPRINT,CQ1
  17 #**S          LDX   6  ZGA(3)                [SELECTION DETAILS 
  10 #*TD          STO   6  ACOMMUNE6(2) 
  19 #B*4          DOWN     WSPARAM,2                  [OUTPARAM SEL. DETAILS
  10 #BSN          LDX   5  ZAWSNAME(1)  
   8 #C##          CALL  6  WSW  
   8 #CRY          BRN      XER  
   8 #D?J          BRN      XBRK 
   8 #DR8          BRN      QND  
   4 #FQD    #END
   6 ____ ...643701030001
