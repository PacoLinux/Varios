  14 22FL ...      SEG   PERION,860,FRANK BLUNDELL,PERION
   4 22T* ...#   
  16 2394 ...#     COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982  
   4 23JR ...#   
   4 23YG    #   
  12 24D6          SEGENTRY K1PERION,QK1PERION   
   2 24XQ
  12 25CB ...      SEGENTRY  K2PERION,QK1PERION  
   4 25X2    #   
   9 26BL    #     PROGRAM  ILLEGAL  
   4 26W=    #   
   9 27*W    XJMOD          +JYMODE  
   9 27TG    XJMR           +JYMODER 
   9 28*6    XJRV           +JYNOCA  
   8 28SQ    #DEF   XCA=EVENT2   
  16 29#B    #DEF  TREP=EXEC3                   [REPLY FROM BSTS 
   4 29S2    #   
   4 2=?L    #   
   5 2=R=    PLINK   
   9 2?=W    ZA    PAIR     PERION,1 
   4 2?QG    #   
  13 2#=6    #     CHECK IF S-TRUSTED AND MODES 4,5,6
  13 2#PQ    XST   LDX   0  6                   [MODE
   8 2*9B          ANDN  0  #4777
   7 2*P2          SBN   0  7
  19 2B8L          BPZ   0  XRV                 [ILLEGAL IF MODE NOT 4,5 OR 6
  20 2BN=          TRUSTED  APETADDR(2),PA3,S   [J TO PA3 IF PROG NOT S-TRUSTED  
  15 2C7W          LDX   0  EXMODE(2)           [OPENING MODE
   8 2CMG          SBN   0  #200 
  19 2D76          BZE   0  PA4                 [J IF ONLY OPEN FOR READING  
  18 2DLQ    PA5   ACROSS   SPERION,1           [DEAL WITH MODES 4,5,6   
  18 2F6B    PA3   TRUSTED  APETADDR(2),XRV,Q   [ILLEGAL IF NOT Q-TRUSTED
  13 2FL2    PA4   LDX   0  6                   [MODE
   7 2G5L          SBN   0  5
  19 2GK=          BZE   0  PA5                 [O.K IF ONLY CHECK READING   
  16 2H4W    #     PUT  OLPA TO SLEEP WAKE UP PCA TO DO ILLEGAL  
   8 2HJG    XRV   TOPCA1   ZA(1)
   6 2J46          UNPLUG
  14 2JHQ          ILLEGALX 3                   [ILLEGAL 
   4 2K3B    #   
  14 2KH2    #     EXEC1 CONTAINS   SUBPROGRAM  NUMBER   
  15 2L2L    #     EXEC2 CONTAINS   ADDRESS OF CONTROL AREA  
   4 2LG=    #   
   4 2L_W    #   
   6 2MFG    QK1PERION   
  10 2M_6          LDX   1  APETADDR(2)  
  17 2NDQ          LOCKCA   1,XCA(2)            [LOCK CONTROL AREA   
   9 2NYB ...      LDX   6  EVENT4(2)
   8 2RW6          LDX   1  FX1  
   8 2S*Q    #SKI  K6PERION>99-99
   4 2STB    (   
  18 2T*2          LDX   0  6                   [CHECK TYPE IF TRACING   
   8 2TSL          SRL   0  15   
  17 2W#=          ANDN  0  #377                [MUST BE 6,7,13 OR 26
  12 2WRW          SBN   0  6                   [
   8 2X?G          BZE   0  XAA  
   7 2XR6          SBN   0  1
   8 2Y=Q          BZE   0  XAA  
   7 2YQB          SBN   0  6
   8 2_=2          BZE   0  XAA  
  15 2_PL          SBN   0  13                  [OR EDS 30   
   8 329=          BZE   0  XAA  
  10 32NW          GEOERR   1,WRNGTYPE   
   4 338G    XAA 
   4 33N6    )   
  16 347Q          ANDX  6  XM2(1)              [MASK OUT TYPE   
   7 34MB          LDX   4  6
   9 3572          ANDX  4  XM1(1)   
  18 35LL          BZE   4  XB                  [JUMP IF TRANSFER MODE   
   7 35MC ...#UNS  CAFSINT   
   4 35N8 ...(   
  18 35N_ ...#     FOR CAFS, WE SPLIT OUT THE SPECIAL CAFS MODE TRANSFERS
  18 35PQ ...#     AT THIS POINT.   THE MODES IN QUESTION ARE #12 & #13. 
   4 35QH ...#   
  20 35R# ...      LDX   0  CAFSPTR             [MODES ARE ILLEGAL WITHOUT CAFS  
  17 35S5 ...      BZE   0  SNOTCAFS            [JIF NO CAFS SYSTEM  
   4 35SW ...#   
  21 35TM ...      LDEX  4  6                   [ISOLATE NON-ADDITIVE PART OF MODE   
   8 35WD ...      SBN   4  #12  
  17 35X9 ...      BZE   4  SCAFSMODE           [JIF CAFS MODE #12   
   7 35Y2 ...      SBN   4  1
  17 35YR ...      BNZ   4  SNOTCAFS            [JIF NOT A CAFS MODE 
   4 35_J ...#   
   6 362* ...SCAFSMODE   
  19 3636 ...      ACROSS   CAFSPERI,1          [GO TO HANDLE CAFS PERI MODES
   4 363X ...#   
   5 364N ...SNOTCAFS
   4 365F ...)   
  19 366=          LDX   3  XJMOD(1)            [MODE IN ERROR INDENTIFIER   
  12 36KW          LDX   4  6                   [
  12 375G          ANDX  4  XM3(1)              [
  17 37K6          BNZ   4  XRV                 [JUMP IF ILLEGAL MODE
   7 384Q          LDX   4  6
   8 38JB          ANDN  4  #1777
   7 3942          LDX   5  4
  17 39HL          SBN   4  #1600               [JUMP IF ILLEGAL MODE
   8 3=3=          BPZ   4  XRV  
  15 3=GW          LDX   0  EXMODE(2)           [OPENING MODE
   8 3?2G          SBN   0  #1600
  18 3?G6          BZE   0  PA1                 [J IF OPENING MODE #1600 
   8 3?_Q          SBN   0  #100 
  16 3#FB          BNZ   0  PA2                 [J IF NOT #1700  
  15 3#_2    PA1   LDX   4  5                   [ACTUAL MODE 
   8 3*DL          SBN   4  #1000
   8 3*Y=          BZE   4  PA2  
  21 3BCW          SBN   4  #400                [ONLY MODES #1000 AND #1400 ALLOWED  
  21 3BXG          BNZ   4  XRV                 [IF OPENING MODES EITHER #1600 OR #17
   8 3CC6    PA2   SRL   6  10   
  17 3CWQ          ANDN  6  3                   [ADDITIVE BITS IF ANY
   7 3DBB          LDX   4  5
  20 3DW2          SBN   4  #401                [JUMP IF MODE #100-#400 (OPEN)   
   8 3F*L          BNG   4  XC   
  19 3FT=          SBN   4  #177                [JUMP IF #600-OPEN SCRATCH   
   8 3G#W          BZE   4  XC   
  17 3GSG          SBN   4  #400                [JUMP IF CREATE #1200
   8 3H#6          BZE   4  XC   
   8 3HRQ          LDCT  0  #400 
  18 3J?B          ORS   0  GIVE(2)             [SET OPEN ILLEGAL MARK   
   8 3JR2          BRN      XD   
   8 3K=L    XC    LDCT  0  #400 
   9 3KQ=          ANDX  0  GIVE(2)  
  17 3L9W          BNZ   0  XRV                 [JUMP IF OPEN ILLEGAL
  17 3LPG          LDX   3  XJMR(1)             [MODE INCOMPATIBLE   
   7 3M96          LDX   0  5
   8 3MNQ          SBN   0  #401 
   8 3N8B          BNG   0  XE   
  19 3NN2          TXU   5  EXMODE(2)           [JUMP IF MODE INCOMPATIBLE   
   8 3P7L          BCS      XRV  
   8 3PM=          BRN      XD   
   9 3Q6W    XE    LDX   0  EXMODE(2)
   8 3QLG          SBN   0  #401 
  19 3R66          BPZ   0  XRV                 [JUMP IF MODE INCOMPATIBLE   
   7 3RKQ    XD    LDX   2  5
   9 3S5B    #SKI  K6PERION>199-199  
  10 3SK2          TRACE    2,PERIMODE   
   8 3T4L          SBN   2  #400 
   8 3TJ=          BPZ   2  XF   
  19 3W3W          LDN   2  0                   [SET X2 MODIFER TO PAIR TABLE
   9 3WHG    XF    SRL   2  7-HLINK  
   8 3X36          ADX   2  FX1  
   9 3XGQ          ACROSSVAR XLINK(2)
   4 3Y2B    #   
   8 3YG2    #     LINK  TABLE   
   4 3Y_L    #   
   5 3_F=    XLINK   
  17 3_YW          PAIR     ANTIC,2             [MODES 100-400 OPEN  
   9 42DG          PAIR     EXTEND,1 
  16 42Y6          PAIR     ANTIC,3             [MODE 600 SCRATCH
   9 43CQ          PAIR     RENAMEX,1
   9 43XB          PAIR     CLOSBAC,1
  10 44C2          PAIR     READSPEC,2   
  16 44WL          PAIR     ANTIC,4             [MODE 1200 CREATE
   9 45B=          PAIR     CLOSBAC,1
  10 45TW          PAIR     READSPEC,1   
  17 46*G          PAIR     EXINONE,1           [EXTEND INTO(#1500)  
   4 46T6    #   
   4 47#Q    #   
   4 47SB    #   
   9 48#2    XM1            #47730   
   9 48RL    XM2            #77777   
   9 49?=    XM3            #70077   
   4 49QW    #   
  13 4==G    #     CHECK TRANSFER MODE FOR LEGALITY  
   4 4=Q6    #   
   4 4?9Q    #   
   9 4?PB    XB    LDX   3  XJRV(1)  
   7 4#92          LDN   4  5
  19 4*8=          CHECKEXT XCA(2),4,XRV,APETADDR(2),W,XBA,HLOCK1,,K1PERION  
   4 4BM6    XBA 
   9 4C6Q          LDX   3  XJMOD(1) 
   7 4CLB          LDX   4  6
   8 4D62          ANDN  4  #47  
   7 4DKL          SBN   4  4
  17 4F5=          BNG   4  YA                  [JUMP IF MODE 0,1,2,3
   7 4FJW          SBN   4  3
  13 4G4G          BZE   4  YA                  [OR 7
   8 4GJ6          SBN   4  26   
   8 4H3Q          BZE   4  YA   
  16 4HHB          SBN   4  2                   [OR #41 OR #43   
   8 4J32          BNZ   4  XST  
   7 4JGL    YA    LDX   5  6
  17 4K2=          ANDN  5  7                   [X5 HOLDS BASIC MODE 
   7 4KFW          LDX   4  6
   7 4K_G          SRL   4  9
  17 4LF6          ANDN  4  #30                 [MODES #20000,#30000 
  14 4LYQ          ANDN  6  #40                 [MODE #40
   7 4MDB          ORS   4  6
   8 4MY2          SBN   4  #10  
  20 4NCL          BZE   4  XRV                 [JUMP IF ADD MODE #10000ONLYSET  
   8 4NX=          LDCT  0  #400 
  17 4PBW          ORS   0  GIVE(2)             [SET OPEN ILLEGAL BIT
   7 4PWG          LDX   0  5
   8 4QB6          BZE   0  YB   
   7 4QTQ          SBN   0  2
  15 4R*B          BZE   0  YB                  [JUMP IF READ
   7 4RT2          SBN   0  5
  12 4S#L          BZE   0  YB                  [
   9 4SS=          LDX   0  EXMODE(2)
   8 4T?W          SBN   0  #200 
  20 4TRG          BZE   0  XRV                 [JUMP IF FILE OPEN FOR READING   
   8 4W?6          SBN   0  #1500
  21 4WQQ          BZE   0  XRV                 [NO WRITING ALLOWED IF OPENED AS #170
   8 4X=B          BRN      YB   
   4 4XQ2    #   
   4 4Y9L    #   
   9 4YP=    #     TRANSFER ROUTINE  
   4 4_8W    #   
   9 4_NG    QMASK          #777777  
   9 5286    RMASK          #100000  
   8 52MQ    TWO            +2   
   8 537B    TEN            +10  
   9 53M2    SESS           +CHWRITE 
   9 546L                   +CHREAD  
   4 54L=    #   
  14 555W    #     MONITORING FILE MESSAGE IDENTIFIERS   
   4 55KG    #   
  17 5656    NCT            +JYCOUNT            [COUNT OUT OF RANGE  
  20 56JQ    NOBUF          +JYNOBUF            [BUFFER OUT OF PROGS RESERVATIONS
  15 574B    RFAIL          +EWOWN              [BLOCK N FAIL
   6 57J2    #FID  1 1 1 
  16 583L    PERF           +JYPERFAIL          [ONLINE %A FAIL  
   8 592W    #DEF  TAC1=EWORK6   
   8 5=FQ    #DEF  TAC2=TAC1+1   
   8 5=_B    #DEF  TAC3=TAC2+1   
   8 5?F2    #DEF  TAC4=TAC3+1   
   8 5?YL    #DEF  TAC5=TAC4+1   
   8 5#D=    #DEF  TAC6=TAC5+1   
   9 5#XW    #DEF  PB=BSET+BONL+BOBJ 
   4 5*CG    #   
  14 5*X6    #     THE SUBROUTINE MERELY RELSTX'S THE SU 
   4 5BBQ    #   
  21 5BWB    RELSTX                             [IF SU SERIAL NO IS IN   CA3 OF AOLPT
   9 5CB2          LDX   6  EWORK3(2)
  16 5CTL    RELSTY                             [IF ALREADY IN X6
   8 5D*=          SBX   0  FX1  
  15 5DN_ ...      STO   0  ACOMMUNE1(2)        [DUMP LINK   
  15 5GRB ...      RELSTX   6,PA6               [RELEASE SU  
   8 5H?2          BRN      PBA6 
   8 5HQL    PA6   LDX   1  FX1  
   8 5J==          LDX   2  FX2  
  10 5JPW ...PBA6  LDX   0  ACOMMUNE1(2) 
   8 5K9G          ADX   0  FX1  
   7 5KP6          EXIT  0  0
   4 5L8Q    #   
   4 5LNB    #   
  13 5M82    #     SUBROUTINE TO ISSUE AUTSTART MACRO
   4 5MML    #   
  11 5N7=    #     LINK X7    NO COORDINATION
   4 5NLW    #   
   5 5P6G    XAUTS   
  15 5PL6          AUTSTART                     [MAKE AUTO   
   7 5Q5Q    X1    EXIT  7  0
   4 5QKB    #   
   4 5R52    #   
  19 5RJL    YB    STOZ     AWORK4(2)           [CLEAR TO HOLD REPLYS TO PROG
  18 5S4=          STO   5  AWORK3(2)           [REMEMBER TRANSFER MODE  
  17 5SHW          STO   6  AWORK1(2)           [AND ADDITIVE MODES  
  15 5T3G          LDX   0  EWCHAN(2)           [CHANNEL NO. 
  14 5TH6          STO   0  AWORK2(2)           [REMEMBER
  21 5W2Q          LDX   6  XCA(2)              [GET ADDRESS OF PROGS CONTROL AREA   
  15 5WGB          STO   6  TAC1(2)             [AND REMEMBER
   9 5X22    #SKI  K6PERION>199-199  
  10 5XFL          TRACE    6,PERICAAD   
  19 5X_=          LDN   4  5                   [LENGTH OF PROGS CONTROL AREA
  20 5YDW          LDX   3  APETADDR(2)         [LOAD ADDRESS OF ASSOCIATED PCA  
  21 5YYG          LDN   7  EWORK1(2)           [START OF AREA IN AOLPT TO MOVE CA TO
  21 5_XQ          FOUTMOVE 3,6,7,4,HLOCK1,2    [MOVE PROG'S CA TO EWORK1-5 OF AOLPT 
   9 63BL    #SKI  K6PERION>199-199  
   4 63W=    (   
  11 64*W          TRACE    EWORK1(2),PERICA1
  10 64TG          TRACE    EWORK2(2),   
  10 65*6          TRACE    EWORK3(2),   
  10 65SQ          TRACE    EWORK4(2),   
  10 66#B          TRACE    EWORK5(2),   
   4 66S2    )   
  19 67?L          LDX   1  ANM(3)              [MEMBER NO OF PROG FROM PCA  
  16 67R=          ANDN  1  #3                  [BOTTOM TWO BITS 
  21 68=W          PMODES   1,3                 [GIVES ADDRESSING MODE OF PROG IN X1 
  19 68QG          ANDN  1  #10                 [SEE IF EXTENDED STORE MODE  
  17 69=6          BNZ   1  NP                  [AND JUMP IF IT IS   
  17 69PQ          LDX   0  BSP16               [GET MASK FOR 15AM   
   8 6=9B          BRN      NP1  
  16 6=P2    NP    LDX   0  BITS22LS            [MASK FOR 22AM   
  18 6?8L    NP1   ANDX  0  EWORK4(2)           [FORGET ANY SPURIOUS BITS
  20 6?N=          STO   0  TAC4(2)             [REMEMBER PROGS BUFFER ADDRESS   
  21 6#7W          MFINDEXOF 3,EXNUM(2)         [GET ADDRESS OF ASSOCIATED EWDAS/EXOF
  19 6#MG          LDCH  0  FIP+8(3)            [GET FLAW INDECATOR FROM EXOF
  18 6*76          ANDN  0  #2                  [I.E B4=1 NO FLAW ACTION 
  15 6*LQ          ORS   0  AWORK1(2)           [AND REMEMBER
  17 6B6B          BNZ   0  NP2                 [J IF NO FLAW ACTION 
  20 6BDR ...      LDN   0  #10                 [ ADD. MODE #30000 HAS PRECEDENCE
  17 6BR8 ...      ANDX  0  AWORK1(2)           [ OVER FLAW ACTION   
  19 6C5K ...      BZE   0  NP2                 [J IF NO MODE #30000  ....   
  21 6CD2 ...      LDN   0  #2                  [ ... OTHERWISE IGNORE FLAW -ACTION- 
  18 6CQC ...      ORS   0  AWORK1(2)           [ -INDICATOR IN THE EXOF 
   8 6D4W    NP2   LDCT  0  #200 
  10 6DJG          ANDX  0  EXMAYBE(2)   
  21 6F46          BNZ   0  PA6A                [J IF NOT S-TRUSTED WITH SYS FILE OFW
  15 6FHQ          LDX   0  EXMODE(2)           [OPENING MODE
   8 6G3B          SBN   0  #1700
  15 6GH2          BNZ   0  PA7                 [J NOT #1700 
  21 6H2L    PA6A  LDN   0  #4002               [B12=S-TRUSTED AND OPENED IN #1700   
  19 6HG=          ORS   0  AWORK1(2)           [B22=NO FLAW ACTION REQUIRED 
  19 6H_W    PA7   LDX   0  EWORK3(2)           [GET LENGTH OF PROGS TRANSFER
   8 6JFG          LDX   1  FX1  
  17 6J_6          BZE   0  RONGLEN             [ZERO LENGTH ILLEGAL 
  20 6KDQ          TXL   0  RMASK(1)            [TEST IF LENGTH -VE OR > 32767   
  16 6KYB          BCC      RONGLEN             [ILLEGAL IF YES  
  16 6LD2          STO   0  TAC3(2)             [REMEMBER LENGTH 
  19 6LXL          LDX   7  EWORK5(2)           [GET LOGICAL BUCKET NUMBER   
  16 6MC=          BZE   7  RONGLBN             [ILLEGAL IF ZERO 
  15 6MWW          BNG   7  RONGLBN             [OR NEGITIVE 
  17 6NBG          LDX   0  EXMODE(2)           [MODE FILE WAS OPENED
   8 6NW6          SBN   0  #400 
  17 6P*Q          BNZ   0  N400                [J IF NOT MODE #400  
  16 6PTB          ADX   7  FIP+10(3)           [JUGGLE WITH LBN 
  21 6Q*2          SBN   7  1                   [TO MAKE COMPATABLE WITH OTHER MODES 
  17 6QSL    N400  LDN   1  FDCELLS(3)          [POINTER TO FD CELLS 
  15 6R#=          SBN   1  7                   [STEP BACK 7 
   9 6RRW          LDEX  6  FIP+5(3) 
  16 6S?G    NEXT  BZE   6  RONGLBN             [LBN OUT OF RANGE
  15 6SR6          SBN   6  1                   [REDUCE COUNT
  19 6T=Q          ADN   1  6                   [UPDATE POINTER TO NEXT CELL 
  17 6TQB          TXL   7  6(1)                [LOOK AT NEXT CELL   
  21 6W=2          BCC      NEXT                [AND J IF SPECIFIED LBN NOT IN THIS C
  21 6WPL          SBX   7  0(1)                [GET BUCKET NO. RELATIVE TO START OF 
   9 6X9=          LDX   4  FIP+8(3) 
  18 6XNW          ANDN  4  #77                 [BUCKET SIZE FROM EXOF   
  18 6Y8G          MPY   7  4                   [BLOCK SIZE IN X7 AND X0 
  21 6YN6          DVD   7  5(1)                [CONVERT TO CYLINDER X0 AND BLOCK X7 
  20 6_7Q          ADX   0  2(1)                [ADD ST CYL RELATIVE TO THE SU   
  19 6_MB          STO   0  EWORK1(2)           [REMEMBER IN EWORK1 OF AOLPT 
  19 7272          LDX   6  5(1)                [BLOCKS/SEEK AREA FROM FDCELL
  20 72LL          SBX   6  7                   [BLOCK REMAINING TO BE TRANSFERED
  21 736=          SLL   6  7                   [X 128 TO GIVE MAX LENGTH (MODES 2 OR
  17 73KW          ADX   7  3(1)                [ADD STARTING SEGMENT
  15 745G          STO   7  EWORK2(2)           [AND STORE   
  19 74K6          LDX   0  1(1)                [SERIAL NO. OF STORAGE UNIT  
   9 754Q          STO   0  EWORK3(2)
  16 773= ...      SUSESTX  1(1),NOTON          [SEE IF SU ONLINE
  20 77GW          LDX   0  ABSUN(1)            [X1 POINTS TO ASTUC SO GET BSUN  
   8 782G          LDX   2  FX2  
  17 78G6          STO   0  TAC2(2)             [REMEMBER FOR BSTS   
  18 78_Q          LDX   0  ASUBASE(1)          [STARTING CYLINDER OF SU 
  17 79FB          ADS   0  EWORK1(2)           [AND ADD TO OTHER CYL
   4 79GJ ...[   
   9 79HQ ...[     EMULATION CHANGE: 
  17 79JY ...[           STORE CYL*BLOCKS PER CYL+BLOCK PART INTO X7 
  16 79L6 ...[           CONVERTX THEN RETURNS IN X0 THE ABSOLUTE
  13 79M# ...[           BLOCK ADDRESS WITH X0 SET   
   4 79NG ...[   
  17 79PN ...      LDX   7  EWORK1(2)           [CYL PART OF ADDRESS 
  18 79QW ...      JENVNOT  NEMU,MEEP           [J IF NOT MEEP EMULATION 
  18 79S4 ...      LDX   0  EWORK2(2)           [BLOCK PART OF ADDRESS   
  20 79T= ...      MPA   7  HBLOCK(1)           [CALCULATE ABSOLUTE BLOCK ADDRESS
  17 79WD ...      STO   0  7                   [FOR CONVERTX MACRO  
   4 79XL ...NEMU
  17 79_2          LDX   0  HTYPE(1)            [TYPE/SUBTYPE OF SU  
   9 7=DL          STO   0  EWORK4(2)
   9 7=Y=    #SKI  K6PERION>199-199  
   4 7?CW    (   
  11 7?XG          TRACE    EWORK1(2),PERICYL
  11 7#C6          TRACE    EWORK2(2),PERIBLK
  11 7#WQ          TRACE    EWORK3(2),PERISU 
   4 7*BB    )   
  19 7*W2 ...      CONVERTX 7,EWORK2(2),EWORK4(2) [GET BS HARDWARE ADDRESS   
   8 7B*L          LDX   2  FX2  
  17 7BT=          STO   0  TAC5(2)             [STORE FOR BACKTRAN  
   8 7C#W          LDX   1  FX1  
   9 7CSG          TXL   5  TWO(1)   
  17 7D#6          BCS      NP4                 [JUMP IF MODES 0 OR 1
   7 7DRQ          SBN   5  7
  17 7F?B          BNG   5  NP5                 [J IF MODES 2 OR 3   
  18 7FR2          LDX   3  TAC1(2)             [REMEMBER CONTROL ADDRESS
  18 7G=L          CALL  7  XAUTS               [MAKE AUTO IF NECESSARY  
  16 7GQ=          BRN      SEVEN               [MUST BE MODE 7  
  15 7H9W    NP4   LDN   6  128                 [BLOCK SIZE  
  17 7HPG          MPY   6  4                   [MAX LENGLH ALLOWED  
   7 7J96          LDX   6  7
  21 7JNQ    NP5   TXL   6  TAC3(2)             [COMPARE MAX LENGTH WITH LENGTH NEEDE
  19 7K8B          BCC      NP6                 [J IF MAX COUNT NOT EXCEEDED 
  21 7KN2          STO   6  TAC3(2)             [SET NEW COUNT EQUAL TO MAX LENGTH   
  17 7L7L          BPZ   5  NP6                 [J IF MODES 0 OR 1   
  18 7LM=          LDCT  0  #200                [END OF SEEK AREA REPLY  
   9 7M6W          ORS   0  AWORK4(2)
  14 7MLG    NP6   LDX   6  TAC3(2)             [COUNT   
  18 7N66          ADX   6  TAC4(2)             [PLUS START ADDRESS TO   
   9 7NKQ          ANDX  6  QMASK(1) 
  14 7P5B          ORS   6  AWORK4(2)           [REPLY WD
  18 7P=J ...#SKI  G4                     [RELEASE SU IN G4 IN CASE QUOTA
  16 7PCQ ...      CALL  0  RELSTX        [NEEDS TO BE INCREASED 
  16 7PK2          LDX   5  AWORK3(2)           [MODE OF TRANSFER
   7 7Q4L          ANDN  5  1
  17 7QJ=          SMO      5                   [USE AS MOD TO OBTAIN
  20 7R3W          LDX   7  SESS(1)             [READ OR WRITE ACCESS IDENTIFIER 
  21 7RHG ...      CHECKB   TAC4(2),TAC3(2),OUT,APETADDR(2),7,NP8,HLOCK2,,QK2PERION  
   9 7T2B          [_ CHECK BUFFER   
  17 7T=8 ...#SKI  G4                       [GET SU BACK ONLINE IN G4
   4 7TG2 ...(   
  11 7TPS ...NP8   SUSESTX  EWORK3(2),NOTON  
   8 7T_L ...      LDX   1  FX1  
   8 7W9D ...      LDX   2  FX2  
   4 7WF= ...)   
   5 7WP4 ...#SKI  G3
   4 7WYW ...NP8 
  17 7X8N ...      LDX   0  AWORK1(2)           [GET ADDITIVE MODES  
   8 7XDG          ANDN  0  #40  
  21 7XY6          BZE   0  NP9                 [J IF NOT WRITE WITHOUT CHECK READ   
  16 7YCQ          LDN   5  9                   [SET TO MODE 9   
  21 7YXB    NP9   LDX   3  TAC1(2)             [REMEMBER PROGS CONTROL AREA ADDRESS 
  20 7_C2          STO   5  TAC1(2)             [STORE MODE AND DO BS TRANSFER   
   7 7_WL    #SKI  ADRMK7&G4 
   4 82B=    (   
  17 82TW    #     STORAGE UNIT MAY HAVE GONE OFFLINE DURING CHECKA  
   4 83*G    #   
   8 83T6          JNOTFDRM XGO  
   2 84#Q
  10 84SB          MFINDSU 3,EWORK3(2)   
  10 85#2          JENG   HUNIT(3),XGO   
   9 85RL          CALL  0  RELSTX   
   9 86?=          BRN      NOTONDRM 
   4 86QW    XGO 
   4 87=G    )   
  18 87D* ...      CALL  7  XAUTS               [MAKE AUTO IF NECESSARY  
  19 87L8 ...      LDX   0  AWORK1(2)           [GET ADDITIVE MODES IF ANY   
  18 87S3 ...      ANDN  0  #30                 [ISOLATE #20000 OR #30000
  18 87_W ...      SLL   0  9                   [ENSURE X0 CONTAINS MODE 
  18 887P ...      STO   0  4                   [STORE ADD. MODE OR ZERO 
  21 88*J ...      BACKTRAN TAC1(2),PB,TAC3(2),,TAC2(2),TAC5(2),TAC4(2),APETADDR(2),-
   9 88HC ...                    EX4 
  18 88PB          CHTRAN   EWORK4(2),TFAIL,TFAIL [CHECK TRANSFER IS OK  
  15 8992    SEVEN CALL  0  RELSTX              [RELEASE SU  
   4 89NL    NP10
  21 8=8=          AUTFIN   AWORK4(2)           [ELSE SEE IF ANYTHING UP THE SPOUT   
  16 8=MW          BRN      K1PERION            [AND GO FOR NEXT 
   4 8?7G    #   
  12 8?M6    #     TRANSFER HAS ILLEGAL LENGTH   
   4 8#6Q    #   
   5 8#LB    RONGLEN 
  21 8*62          LDX   7  NCT(1)              [GET MONITORING FILE MESSAGE IDENTIFI
  17 8*KL          BRN      PATH                [JOIN ILLEGAL PATH   
   4 8B5=    #   
  14 8BJW    #     BUFFER IS OUT OF PROGRAMS RESERVATIONS
   4 8C4G    #   
  20 8CJ6    OUT   LDX   7  NOBUF(1)            [RESERVATION VIOLATION MESSAGE   
  15 8D3Q          CALL  0  RELSTX              [RELEASE SU  
  16 8DHB    PATH  TOPCA1   PLINK(1)            [CHANGE TO PCA   
  18 8F32          UNPLUG                       [UNPLUG PROG FROM EXEC   
  18 8FGL    NP15  ILLEGALX 7                   [PUMP OUT ILLEGAL MESSAGE
   4 8G2=    #   
  16 8GFW    #     PROGRAMS LOGICAL BUCKET NUMBER OUT OF RANGE   
   4 8G_G    #   
   5 8HF6    RONGLBN 
  19 8HYQ          LDCT  0  #10                 [REPLY FOR LBN OUT OF RANGE  
  17 8JDB          STO   0  AWORK4(2)           [IN AWORK4 OF AOLPT  
  16 8JY2          LDX   3  TAC1(2)             [PROGS CA ADDRESS
   9 8KCL    #SKI  K6PERION>199-199  
  10 8KX=          TRACE    7,PERIBLBN   
  18 8LBW          CALL  7  XAUTS               [MAKE AUTO IF NECESSARY  
   8 8LWG          BRN      NP10 
   4 8MB6    #   
  11 8MTQ    #     STORAGE UNIT NOT ONLINE   
   4 8N*B    #   
   8 8NT2    NOTON LDX   2  FX2  
   6 8P#L    #SKI  ADRMK7
  16 8PS=          FDRMQUERY NOTONDRM           [CHANNEL IN DRM  
  19 8Q?W          LDX   3  AWORK1(2)           [REMEMBER IF S-TRUSTED OR NOT
  19 8QRG          LDX   4  TAC1(2)             [PROGS CONTROL AREA ADDRESS  
  17 8R?6          LDX   5  EWCHAN(2)           [CHANNEL NO. OF FILE 
  19 8RQQ          LDX   6  EWORK3(2)           [REMEMBER SERIAL NO. OF SU   
   8 8S=B          LDX   1  FX1  
  18 8SQ2          TOPCA1   PLINK(1)            [CHANGE TO APET ACTIVITY 
  18 8T9L          UNPLUG                       [UNPLUG PROG FROM EXEC   
  20 8TP=          CAPCA                        [CHANGE TO CPAT I.E. SWAPOUT PROG
  16 8XMQ ...      SMUSEST  6,NONL,REAKIN       [GET SU ONLINE   
  15 8Y7B          CALL  0  RELSTY              [RELEASE SU  
  17 8YM2          STEPBACK                     [STEP PROGS ONR BACK 
  16 8_6L          RUNPROG                      [AND TRY AGAIN   
   6 8_L=    #SKI  ADRMK7
   4 925W    (   
  13 92KG    #     SU UNIT ONLINE WHEN CHANNEL IN DRM
   4 9356    #   
   5 93JQ    NOTONDRM
  14 944B          LDCT  0  #410                [1**M*1  
  19 94J2          STO   0  AWORK4(2)           [REJECTED/INOPERABLE REPLY   
  10 953L          LDX   1  APETADDR(2)  
   9 95H=          LDX   3  EVENT2(2)
   7 962W          ADN   3  1
  10 9726          FADDRESS 1,3,HLOCK1,2 
  15 98F2    #     NOW HAVE PTR TO THE REPLY WD DESTINATION  
  15 98YL          LDX   4  AWORK4(2)           [GET REPLY   
  16 99D=          STO   4  0(3)                [INSERT REPLY WD 
  14 99XW          OLPFIN   APETADDR(2)         [& CAPUT 
  15 9=CG          BRN      K1PERION            [NEXT PLEASE 
   4 9=X6    )   
   4 9?BQ    #   
  19 9?WB    #     BREAK IN OCCURRED WHILE WAITING FOR UNIT TO COME ONLINE   
   4 9#B2    #   
   5 9#TL    REAKIN  
  17 9**=          STEPBACK                     [STEP PROGS ONR BACK 
  16 9*SW          PROGBRKIN                    [REPORT BREAKIN  
   4 9B#G    #   
  14 9BS6    #     OPERATOR TYPES CANDO LOAD STORAGE UNIT
   4 9C?Q    #   
   8 9CRB    NONL  ANDN  3  #4000
  21 9D?2          BNZ   3  PA8                 [J IF S-TRUSTED BS SET ALSO IN REPLY 
  20 9DQL          LDCT  3  #40                 [FAILED REPLY B3=1 NOT S-TRUSTED 
   8 9F==          BRN      PA9  
  21 9FPW    PA8   LDCT  3  #50                 [FAILED REPLY B3-1 B5=1 FOR S-TRUSTED
  21 9G9G    PA9   NGN   7  1                   [INDICATE PROG NOT TO BE RESTARTED   
   8 9GP6          BRN      ZMESS
   4 9H8Q    #   
  16 9HNB    #     FAILED OR OR INOPERABLE REPLY FROM BACKTRAN   
   4 9J82    #   
  20 9JML    TFAIL LDX   4  3                   [REMEMBER CONTROL AREA ADDRESS   
  17 9K7=          MFINDSU  3,EWORK3(2)         [GET POINTER TO ASTUC
  16 9KLW          LDX   5  AWORK2(2)           [CHANNEL NUMBER  
  16 9L6G          LDX   0  TREP(2)             [REMEMBER REPLY  
  15 9LL6          STO   0  AWORK2(2)           [FROM BSTS   
  15 9M5Q          LDX   6  EWORK3(2)           [SERIAL NO.  
  16 9MKB          STO   6  EWORK5(2)           [STORE FOR FLAW  
  14 9N52          LDX   7  EWORK1(2)           [CYLINDER
  10 9NJL          SBX   7  ASUBASE(3)   
  19 9P4=          MPY   7  HBLOCK(3)           [MULTIPLY BY DEPTH OF UNIT   
  15 9PHW          ADX   0  EWORK2(2)           [ADD BLOCK NO
  18 9Q3G          STO   0  EWORK3(2)           [ACTUAL BLOCK THAT FAILED
  21 9QH6          NGN   7  1                   [SET PROGRAM NOT TO BE RESTARTED MARK
   9 9R2Q          LDX   0  AWORK1(2)
   8 9RGB          ANDN  0  #2   
  19 9S22          BZE   0  RUG                 [J IF FLAW ACTION REQUIRED   
  15 9SFL          CALL  0  RELSTY              [RELEASE SU  
  18 9S_=          BADBLOCK TAC2(2)             [UNSET FAIL BIT IN BSA   
  21 9TDW          KEEPMESS RFAIL(1),6,EWORK3(2) [OUTPUT FAIL MESSAGE TO MONITORING F
   5 9TYG    SETREP  
  16 9WD6          LDX   3  AWORK1(2)           [ADDITIVE MODES  
   7 9WXQ          LDN   0  0
   8 9XCB          ANDN  3  #4000
  17 9XX2          BZE   3  PA10                [J IF NOT S-TRUSTED  
  15 9YBL          LDX   0  AWORK2(2)           [BSTS REPLY  
   8 9YW=          SRL   0  18   
  17 9_*W          ANDN  0  #14                 [TEST BITS 2 AND 3   
  14 9_TG          BNZ   0  PA10                [J IF SET
  19 =2*6          ORN   0  1                   [SET B5 IF OTHER THAN 2 OR 3 
  15 =2SQ    PA10  ORN   0  4                   [B3 OF REPLY 
  15 =3#B          SLL   0  18                  [SHIFT TO TOP
  17 =3S2    NP13  STO   0  AWORK4(2)           [SAVE REPLY TO PROG  
   9 =4?L          LDX   0  AWORK1(2)
  19 =4R=          ANDN  0  #20                 [SEE IF ADDITIVE MODE #20000 
  17 =5=W          BNZ   0  RESTART             [NO NEED TO STOP PROG
  18 =5QG    NP11  HUNTW    1,EWDAS,EWMESS      [LOOK FOR MESSAGE BLOCK  
  16 =6=6          BNG   1  NP10                [BLOCK NOT THERE 
  17 =#K=          STO   5  AWORK2(2)           [REMEMBER CHANNEL NO 
  16 =*4W          STO   7  AWORK3(2)           [ & RESTART FLAG 
  15 =*JG          AUTFAIL  AWORK4(2)           [TERMINATE   
  15 =B46          BRN      K1PERION            [NEXT PLEASE 
  19 =C3B    ZMESS STO   4  AWORK1(2)           [SAVE CONTROL AREA ADDRESS   
  16 =CH2 ...XPAR  LDCT  4  #006                [ TYPE IN B0-8   
  17 =D2L ...      ADX   4  EWCHAN(2)           [ CHANNEL IN B9-23   
  19 =DG= ...      OUTPACK  4,1,PERINAME,XPAR   [ ASSEMBLE PARAM FOR ILLEGALX
   9 =JC=          LDX   4  AWORK1(2)
   4 =Q=Q    NP12
  21 =QQB          ADN   4  1                   [X4 PTS TO REPLY WD REL TO PROG DATUM
  17 =R=2          GETWORD  4,4                 [REL TO GEORGE DATUM 
  10 =RPL          TESTREP  RESVIOL,RERR 
   7 =S9=          SMO      4
  16 =SNW          STO   3  0                   [REPLY INSERTED  
   7 =T8G          WORDFIN   
  20 =W7Q          BNG   7  NP16                [J IF PROG NOT TO BE RESTARTED   
  18 =WMB          RUNPROG                      [CARRY ON RUNNING PROG   
  12 =X72    NP16  LDX   7  PERF(1)             [
   8 =XLL          BRN      NP15 
   4 =Y6=    #   
   4 =YKW    #   
  19 =_5G    RUG   DOWN     FLAW,1              [DEAL WITH FAILED TRANSFER   
  15 =_K6          PICKREP  3                   [PICKUP REPLY
  15 ?24Q          CALL  0  RELSTY              [RELEASE SU  
  11 ?2JB          TESTREPX 3,EXFAIL,SETREP  
  11 ?342          TESTREPX 3,ACAFULL,REPLY  
  10 ?3HL          TESTREPX 3,OK,RESTART 
  10 ?43=          GEOERR   1,WRONGREP   
   5 ?4GW    RESTART 
  20 ?52G          LDN   7  0                   [INDICATE PROG TO BE RESTARTED   
   7 ?5G6          LDX   3  4
   8 ?5_Q          BRN      NP11 
  15 ?6FB    REPLY LDN   0  8                   [B2 OF REPLY 
   8 ?6_2          BRN      PA10 
   4 ?7DL    #   
  10 ?7Y=    RERR  GEOERR   1,RESVIOLA   
   4 ?8CW    #   
   6 ?9C6    QK2PERION   
   9 ?=BB          BRN      K1PERION 
   4 ?=W2    #   
  12 ??*L          MENDAREA 1023-0?<50,K99PERION 
   4 ??T=    #END
   8 ____ ...06622763000100000000
