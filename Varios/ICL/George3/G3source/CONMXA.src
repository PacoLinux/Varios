  15 22FL    #SEG  CONMXA                       [JOHN BAILEY.
   7 22S3 ...#OPT  K0CONMXA=0
  14 236D ...#LIS  K0CONMXA>K0CONMX>K0MULTIPLX>K0ALLGEO  
  10 23DW                   8HCONMXA80   
   4 23YG    #   
  18 24D6    # THIS SEGMENT PERFORMS THE INITIALISATION AND CLOSE DOWN   
  14 24XQ    # PROCEDURES FOR CONCEPTUAL MULTIPLEXORS.   
   4 25CB    #   
   4 25X2    #   
  11 26BL          SEGENTRY K1CONMXA,X1CONMXA
  11 26W=          SEGENTRY K2CONMXA,X2CONMXA
   6 273D ...#SKI JSKI14 
  12 278L ...      SEGENTRY K92CONMXA,X92CONMXA  
   4 27*W    #   
   4 27TG    #   
   4 28*6    #   
  20 28SQ    # K1CONMXA MONITORS THE FIRST OBJECT PROGRAM PERI TO THE CONCEPTUAL,
  20 29#B    # CHECKS THAT THE MODE ISSUED WAS #4000 AND, IF SO, INITIALISES THE 
  20 29S2    # OLPA, ITS DATA BLOCK (AMXOR/CONLCB) AND THE RELEVANT ENTRIES IN   
  20 2=?L    # THE AMXOR/ALINES BLOCK ASSOCIATED WITH EACH MULTIPLEXOR ACTIVITY. 
   4 2=R=    #   
  20 2?=W    # K2CONMXA IS ENTERED FROM RELPER TO RELEASE THE CONCEPTUAL FROM THE
  20 2?QG    # OBJECT PROGRAM. IT UNBUSIES ALL THE LINES, RESETS THE AMXOR/ALINES
  21 2#=6    # ENTRIES, MARKS THE CONCEPTUAL FREE IN THE IDF AND MURDERS THE OLPA.   
   4 2#PQ    #   
   4 2*9B    #   
  21 2*P2    PAIR1 LINK     CONMXA,1            [REENTRY POINT AFTER ILLEGAL 1ST.PERI
  20 2B8L    PAIR2 LINK     CONMXC,1            [ENTRY POINT FOR TRANSFER PERIS. 
   4 2BN=    #   
   4 2C7W    #   
  18 2CMG    RESV  CALL  1  XILL                [CALL ILLEGAL ROUTINE.   
  20 2D76                   +JYNOFA             [FLAG AREA OUT OF RESERVATIONS.  
  18 2DLQ    MODIL CALL  1  XILL                [CALL ILLEGAL ROUTINE.   
  16 2F6B                   +JYMODE             [MODE IN ERROR.  
  18 2FL2    XILL  MACCS    ,APETADDR(2),3      [SET REJECTED RESPONSE.  
  21 2G5L          LDX   5  0(1)                [GET IDENTIFIER FOR ILLEGAL MESSAGE. 
   8 2GK=          STOZ     9(3) 
   8 2H4W          LDX   1  FX1  
   9 2HJG          TOPCA1   PAIR1(1) 
  16 2J46          UNPLUG                       [OBEYED FROM PCA.
  16 2JHQ          ILLEGALX 5                   [FAIL THE O.P.   
   6 2JJM ...#SKI JSKI14 
   4 2JKJ ...(   
   5 2JLF ...ZCHAPLEN
   8 2JMB ...      BNG   1  TISIN
   8 2JP8 ...      SRL   1  15   
   7 2JQ5 ...      SLL   1  2
   7 2JR2 ...      ADX   2  1
   8 2JRX ...      BNZ   1  (0)  
   8 2JSS ...      ADN   2  1024 
   7 2JTP ...      EXIT  0  0
  10 2JWL ...TISIN ADX   2  ALOGLEN(1)   
   7 2JXH ...      EXIT  0  0
  11 2JYD ...SEEK  MHUNTW   2,AMXOR,CHAPCOPY 
   7 2J_* ...      EXIT  1  0
   4 2K2= ...)   
   4 2K3B    #   
   4 2KH2    #   
   4 2L2L    #   
  21 2LG=    X1CONMXA                           [ENTRY FOR OLPA TO ANALYSE MODE #4000
  15 2L_W          LDX   1  EVENT1(2)           [MEL ADDRESS 
  18 2MFG          LDCH  0  ARM1-AFW1(1)        [CONTROLLING MEMBER NO,  
  10 2M_6          STO   0  CMEMBER(2)   
  21 2NDQ          LDX   6  EVENT2(2)           [RELATIVE ADDRESS OF O.P. CONTR. AREA
  20 2NYB    XCOOR LDX   1  APETADDR(2)         [GET PCA ADDRESS INTO A MODIFIER.
  21 2PD2          LOCKCA   1,6,3               [LOCK DOWN C.A., AND GET ABS. ADDRESS
  17 2PXL          LDX   4  0(3)                [GET TYPE/MODE WORD. 
  18 2QC=          ANDN  4  #4000               [J. IF NOT MODE #4000.   
   8 2QWW          BZE   4  MODIL
  21 2RBG          LDX   0  CMAXLINE(2)         [FIND LENGTH OF INTERRUPT FLAG AREA. 
  15 2RW6          BZE   0  UPX1                [J. IF UPX.  
   7 2S*Q          LDN   4  4
   8 2STB          SBN   0  64   
   8 2T*2          BNG   0  MPX1 
   8 2TSL          LDN   4  16   
   8 2W#=          BRN      MPX1 
   7 2WRW    UPX1  LDN   4  1
  16 2X?G    MPX1  ADN   3  1                   [2ND. WD. OF C.A.
  21 2XR6          LDX   5  0(3)                [RELATIVE ADDRESS OF 1ST. WD. OF F.A.
  19 2Y=Q          LDX   3  CMEMBER(2)          [GET CURRENT MEMBER NUMBER.  
  21 2YQB          PMODES   3,APETADDR(2)       [EXTRACT PROGRAM MODES OF THIS MEMBER
  18 2_=2          ANDN  3  #10                 [J. IF 22 BIT ADDRESSING.
   8 2_PL          BNZ   3  X15AM
  19 329=          ANDX  5  BSP16               [MASK FOR 15 BIT ADDRESSING. 
  21 32NW    X15AM CHECKA   5,4,RESV,APETADDR(2),W,XCOOR[CHECK F.A. WITHIN O/P & LOCK
  21 338G          STO   5  CFLAG(2)            [SAVE RELATIVE ADDRESS OF 1ST. WORD. 
  20 33N6          MHUNTW   1,AMXOR,CONLCB      [FIND CONCEPTUAL LINE DATA BLOCK.
  19 347Q          LDX   4  CMAXLINE(2)         [HIGHEST LINE NO. (0 IF UPX).
  19 34MB          LDN   3  0                   [CURRENT CONCEPTUAL LINE NO. 
   8 3572          LDX   7  GSIGN
  20 35LL    NLINE LDCT  5  0(3)                [INSERT CURRENT CONCEP. LINE NO. 
  21 366=          LDX   2  A1+ADDMOP(1)        [GET ADDRESS OF LINE'S AMOP(IF ANY), 
  21 36KW          ORS   5  A1+ACTUAL(1)        [IT HAS BEEN SET WHEN IDFONLINE FOUND
  20 375G          BZE   2  NULL1               [J. IF NO LINE IS YET ATTACHED.  
  10 37K6          ERS   7  A1+ADDMOP(1) 
  17 384Q          HUNT2    2,AMXOR,ALINES      [GET ALINES BLOCK.   
  20 38JB          LDEX  0  A1+ACTUAL(1)        [GET LINE NO FROM CONLCB ENTRY   
   7 3942          SLL   0  2
   7 39HL          ADX   2  0
  21 3=3=          RINGTWO  A1(1),A1(2)         [ENRING ALINES AND CONLCB ENTRIES.   
  16 3=GW    NULL1 BZE   4  OUT                 [J. IF FINISHED. 
  20 3?2G          ADN   1  CONRECLEN           [STEP TO NEXT ENTRY IN CONLCB.   
   7 3?G6          ADN   3  1
   7 3?_Q          SBN   4  1
   8 3#FB          BRN      NLINE
   7 3#P8 ...#SKI JSKI14<1$1 
  19 3#_2    OUT   LDX   2  FX2                 [SET ^ACCEPTED^ RESPONSE IN  
   6 3#_H ...#SKI JSKI14 
   4 3*24 ...(   
   7 3*2K ...OUT   LDN   2  1
   9 3*36 ...      LDX   1  ADMULTACT
  10 3*3M ...      LDX   1  IMOPADDR(1)  
  10 3*48 ...      ADX   2  ICONCEP(1)   
  10 3*4P ...      STOC  2  ICONCEP(1)   
   8 3*4_ ...      BCS      SETED
  17 3*5= ...      BCT   2  SETED               [J IF BLOCK EXISTS   
  17 3*5R ...      LDX   1  KTAB+KCONMXC        [GET LTH. OF BLOCK   
   9 3*6# ...      CALL  0  ZCHAPLEN 
  10 3*6T ...      LDX   1  KTAB+KPROFLAG
   8 3*7B ...      ADN   2  A1+A1
   9 3*7X ...      CALL  0  ZCHAPLEN 
   7 3*8= ...      LDX   3  2
  12 3*8K ...      SETUPCORE 3,3,AMXOR,CHAPCOPY,1
  15 3*8_ ...      LOCKC    3,SEEK              [LOCK BLOCK  
  11 3*9G ...      MHUNTW   3,AMXOR,CHAPCOPY 
   8 3*=3 ...      SMO      FX2  
  17 3*=J ...      STO   3  AWORK4              [SAVE BLOCK ADDRESS  
   8 3*?5 ...      ADN   3  A1   
  10 3*?L ...      ACROSS   PROFLAG,92   
   6 3*#7 ...X92CONMXA   
   9 3*#N ...      LDX   2  AWORK4(2)
   9 3**9 ...      LDX   3  ADMULTACT
  10 3**Q ...      LDX   3  IMOPADDR(3)  
  17 3*B? ...      CHAIN    2,3                 [CHAIN AFTER IMOPQ   
   5 3*BS ...SETED   
   8 3*C* ...      LDX   2  FX2  
   4 3*CW ...)   
  18 3*DL          LDX   1  FX1                 [OBJECT PROGRAM'S WORD 9.
  11 3*Y=          MACCS    ,APETADDR(2),3   
   8 3BCW          ORS   7  9(3) 
   9 3BXG          OLPFIN1  PAIR2(1) 
   4 3CC6    #   
   4 3CWQ    #   
   4 3DBB    #   
  21 3DW2    X2CONMXA                           [ENTRY FROM RELPER: OBEYED FROM CPA. 
   7 3F*L          LDN   6  4
  19 3FT=          STO   3  AWORK1(2)           [SAVE ADDRESS OF LOCKED OLPA 
   9 3G#W          LDX   5  JOBNO(2) 
  20 3GSG          HUNTMISB 1,AMXOR,INTRPT      [CHECK FOR AN INTERRUPT BLOCK.   
  18 3H#6          BNG   1  START               [NONE - NOTHING TO DO.   
   9 3HRQ          LDX   0  INTENT(1)
  10 3J?B          LDX   7  ALOGLEN(1)   
   7 3JR2          LDX   6  1
   8 3K=L          ANDN  4  #77  
  21 3KQ=          ADN   7  -1(1)               [ADDRESS OF END OF TABLE (MOD 512).  
  21 3L9W    TICK1 BXU   5  INTJOBNO(1),TICK2   [CHECK FOR AN ENTRY FOR THIS UNIT.   
  21 3LPG          BXE   4  INTUNIT(1),TICK3    [X4 HOLDS UNIT NO. (C. F. RELPER).   
   9 3M96    TICK2 ADN   1  INTRECLEN
   8 3MNQ          BCT   0  TICK1
  20 3N8B          BRN      START               [NO ENTRIES, OR ENTRY DELETED.   
  21 3NN2    TICK3 SBN   7  INTRECLEN(1)        [DISTANCE TO END OF TABLE (MOD 512). 
   8 3P7L          ANDN  7  -1   
   8 3PM=          BZE   7  TICK4
  21 3Q6W          LDN   4  INTTIME+INTRECLEN(1)[OTHERWISE MOVE REST OF TABLE DOWN   
  18 3QLG          LDN   5  INTTIME(1)          [OVER DISCARDED ENTRY.   
   7 3R66          SMO      7
   7 3RKQ          MOVE  4  0
   7 3S5B    TICK4 LDX   1  6
  16 3SK2          LDX   0  INTENT(1)           [SHORTEN BLOCK.  
   7 3T4L          SBN   0  1
  21 3TJ=          BZE   0  TICK5               [NO ENTRIES LEFT : J. TO FREE BLOCK. 
  10 3W3W          LDX   7  ALOGLEN(1)   
   9 3WHG          STO   0  INTENT(1)
   9 3X36          SBN   7  INTRECLEN
   8 3XGQ          ALTLENG  1,7  
   8 3Y2B          BRN      START
   7 3YG2    TICK5 FREECORE 1
   8 3Y_L          BRN      START
  21 3_F=    PLOOP COOR3    #41                 [WAIT FOR 1/5 SEC. THEN TRY AGAIN.   
  16 3_YW          LDX   3  AWORK1(2)           [OLPA ADDRESS.   
  16 42DG    START STOZ     AWORK2(2)           [CLEAR MARKER.   
  19 42Y6          LDX   7  CMAXLINE(3)         [HIGHEST LINE NO. (0 IF UPX).
  20 43CQ          HUNT2    3,AMXOR,CONLCB      [FIND THE CONCEPTUAL LINE BLOCK. 
   8 43XB          ADN   3  A1   
  15 44C2          BZE   7  UPX2                [J. IF UPX.  
  16 44WL    QLOOP SBN   7  1                   [STEP POINTERS.  
   9 45B=          ADN   3  CONRECLEN
  17 45TW    UPX2  LDX   4  BPTR(3)             [J. IF NOT RINGED.   
   8 46*G          BZE   4  STEP 
  15 46T6          LDX   2  ADDMOP(3)           [AMOP ADDRESS
  21 47#Q          BZE   2  NULL2               [J. IF NULL LINE WITH STORED PERIS   
  15 47SB          LDEX  4  ACTUAL(3)           [LINE NUMBER 
   8 48#2          FINDLCA  ,4   
  21 48RL          SRC   4  9                   [ISSUE CANCEL REGARDLESS OF WHETHER  
  21 49?=          ORN   4  4                   [LINE APPEARS BUSY, SO THAT EXEC. CAN
  19 49QW          DCA      ,STO,4,MODE         [RESET ITS INTERNAL MARKERS. 
  14 4==G          GLPERI                       [CANCEL  
  21 4=Q6          JLUNBUSY 1,NULL2             [J. IF ACCEPTED [NO CURRENT TRANSFERS
  18 4?9Q          LDX   2  FX2                 [IN PROGRESS ON THE LINE 
  20 4?PB          STO   6  AWORK2(2)           [SET ^LINE STILL BUSY^ MARKER.   
  20 4#92    STEP  BNZ   7  QLOOP               [J. IF MORE LINES IN THIS PASS.  
  19 4#NL          LDX   0  AWORK2(2)           [J. IF SOME LINES STILL BUSY.
   8 4*8=          BNZ   0  PLOOP
   4 4*MW    #   
  21 4B7G          SETNCORE 4,1,IDF,CONNAME     [SET UP BLOCK CONTAINING CONCEP. NAME
  19 4BM6          LDX   3  AWORK1(2)           [GET OLPA (LOCKED) ADDRESS.  
   9 4C6Q          LDN   0  CNAME(3) 
   8 4CLB          ADN   1  A1+1 
   7 4D62          MOVE  0  3
  10 4DKL          LDX   7  CMAXLINE(3)  
   7 4DNQ ...#UNS  IDFMARK8  
  21 4DRW ...      IDFOFF   (GEOERR),VAB,IDF,CONNAME,A1+1,1[UNSET ^ONLINE^ & ^ATTACHE
   7 4DX2 ...#UNS  IDFMARK8  
   4 4F26 ...#SKI
  21 4F5=          IDFOFF   VAB,IDF,CONNAME,A1+1,1[UNSET ^ONLINE^ & ^ATTACHED^ MARKER
  21 4FJW          MFREEW   IDF,CONNAME         [IN IDF THEN GO ROUND SETTING UP ACTS
  20 4G4G                                       [TO RESTART SYSTEM USE OF LINES  
  20 4GJ6          HUNT2    3,AMXOR,CONLCB      [OLPA IS LOCKED : ADDRESS IN X3  
   7 4H3Q          LDN   6  0
   8 4HHB          ADN   3  A1   
   8 4J32          BNZ   7  RW   
   8 4JGL          BUX   7  RLOOP
   9 4K2=    RW    ADN   3  CONRECLEN
   4 4KFW    #   
  19 4K_G    RLOOP LDXC  2  ADDMOP(3)           [J. IF LINE WAS NOT ATTACHED.
   8 4LF6          BZE   2  STOP 
   7 4LYQ          ADN   6  1
  11 4MDB          HUNT2    2,AMXOR,ALINES   
   9 4MY2          LDX   0  ACTUAL(3)
   8 4NCL          ANDN  0  #7777
   7 4NX=          SLL   0  2
   7 4PBW          ADX   2  0
  20 4PWG          LDCT  0  #634                [SET UP THE ^BUSY^, ^SETTING UP^ 
  19 4QB6          ORS   0  A1+FOURTHWD(2)      [AND ^NEEDS ACTIVITY^ BITS.  
  21 4QTQ          LDCT  0  #210                [UNSET ^ATTACHED^ & ^ONLINE^ BITS.   
  11 4R*B          ERS   0  A1+FOURTHWD(2)   
   8 4RT2    STOP  BCT   7  RW   
   8 4S#L          BZE   6  NOMPX
  18 4SS=          LDX   1  ADMULTACT           [J.IF NO ACTUAL PLEXORS  
  21 4T?W          LDX   2  IMOPADDR(1)         [WAKE UP THE CORE REQUEST ACTIVITY   
  21 4TRG          ADS   6  IWAKE(2)            [ADD TO IWAKE IN IMOPQUE THE NO OF   
  19 4W?6          LONGCLEAR 2,#125,NOMPX       [ACTIVITIES TO BE WOKEN UP   
  19 4WQQ    NOMPX CJOBLOCK 1                   [SOMETHING MORE ELABORATE MAY
  20 4X=B          LDN   0  1                   [BE NEEDED HERE WHEN JOBS RUNNING
  21 4XQ2          SBS   0  AEXCO(1)            [CONCEPTUALS ARE NOT NECESS. REALTIME
   6 4XRJ ...#SKI JSKI14 
   4 4XT6 ...(   
   9 4XTD ...      SMO      AWORK1(2)
   8 4XTQ ...      LDX   3  CFLAG
   8 4XW4 ...      BNG   3  NIVK 
   8 4XWB ...      BZE   3  NIVK 
   9 4XWN ...      LDX   3  ADMULTACT
  10 4XY= ...      LDX   3  IMOPADDR(3)  
  10 4X_S ...      LDX   0  ICONCEP(3)   
   8 4Y3B ...      BCT   0  SMLFT
   8 4Y4Y ...      ORX   0  GSIGN
  10 4Y6G ...SMLFT STO   0  ICONCEP(3)   
   4 4Y7? ...NIVK
   4 4Y84 ...)   
   9 4Y9L          LDX   3  AWORK1(2)
  20 4YP=          MURDER   3                   [GET RID OF THE OLPA & CONLCB.   
   5 4_8W          UP
  17 4_NG    NULL2 LDX   1  BPTR(3)             [DERING CONLCB ENTRY.
   9 5286          STOZ     BPTR(3)  
   9 52MQ          STOZ     BPTR(1)  
   8 537B          LDX   2  FX2  
  17 53M2          BRN      STEP                [J. FOR NEXT LINE.   
   4 546L    #   
   4 54L=    #   
   4 555W    #   
   4 55KG    #   
   4 5656    #   
   4 56JQ    #   
  21 574B    VAB   GEOERR   1,IDFERROR          [ERROR IF NAME NOT RECOGNISED BY IDF.
   4 57J2    #   
   4 583L    #   
   8 58H=    #SKI  K6ALLGEO>99-99
   4 592W    (   
  10 59GG          MENDAREA 20,K100CONMXA
   4 5=26    )   
   4 5=FQ    #END
   8 ____ ...72127537000200000000
