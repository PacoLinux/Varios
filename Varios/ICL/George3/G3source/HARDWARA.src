  16 22FL    #SEG  HARDWARA8                    [G CONSTANTINIDES
   8 22_=    #OPT  K0HARDWARA=0  
  16 23DW    #LIS  K0HARDWARA>K0ALLGEO>K0GREATGEO>K0COMMAND>K0IDF
  10 23YG                   8HHARDWARA   
   7 24D6    # ENTRY POINTS  
  12 24XQ          SEGENTRY K1HARDWARA,Z1HARDWARA
  11 25CB          MENDAREA 30,K99HARDWARA   
   7 25X2    TABLE          0
   8 26BL                   #7420
   8 26W=                   #7430
   8 27*W                   #7400
   8 27TG                   #7410
   8 28*6                   #7440
   8 28SQ                   #7450
   8 29#B                   #7460
   8 29S2                   #7470
   8 2=?L                   #7540
   8 2=R=                   #7550
   8 2?=W                   #7560
   8 2?QG                   #7570
   6 2?WC ...#SKI JSKI17 
  13 2#2# ...TERT           20HMOP TER SEN NUL SPL   
   7 2#69 ...#SKI JSKI17<1$1 
  12 2#=6    TERT            16HMOP TER SEN NUL  
   9 2#PQ    TOPH           #77770000
   8 2*9B    TYTAB          #5460
   8 2*P2                   #4362
   8 2B8L                   #6467
   8 2BN=                   #6462
   8 2C7W                   #6460
   4 2CMG    #   
  21 2D76    #     SUBROUTINE TO GET NEXT DEVICE MNEMONIC , CONVERT IT TO TYPE NO. & 
  21 2DLQ    #     STORE IT IN IDF/CONDATA BLOCK ; ENSURE THAT CONSOLE IS DEFINED FOR
  15 2F6B    #     INPUT AND OUTPUT , IF AT ALL ; CALL ON X7 
   4 2FL2    #   
   8 2G5L    NDEV  SBX   7  FX1  
   7 2GK=          SPARAPAS  
  16 2H4W          LDN   5  0                   [NULL TYPE NO.   
  10 2HJG          MHUNT    3,CPB,CUNI   
   9 2J46          LDX   4  ANUM(3)  
  19 2JHQ          BNG   4  STOTY               [MISSING:NO MORE INTERFACES  
  15 2K3B          BZE   4  STOTY               [NULL DEVICE 
   7 2KH2          SBN   4  2
  18 2L2L          BNZ   4  PFER                [MNEMONIC NOT 2 LETTERS  
   9 2LG=          LDX   4  APARA(3) 
   8 2L_W          SRL   4  12   
   7 2MFG          LDN   0  6
   7 2M_6    TRYAG ADN   5  1
   7 2NDQ          TXU   5  0
  18 2NYB          BCC      PFER                [MNEMONIC NOT RECOGNISED 
   7 2PD2          SMO   5   
  20 2PXL          TXU   4  TYTAB-1(1)          [COMPARE WITH STANDARD MNEMONICS 
   8 2QC=          BCS      TRYAG
   7 2QWW    STOTY FREECORE 3
  10 2RBG          MHUNTW   3,IDF,CONDATA
   8 2RW6          BNG   4  NOMIN
  16 2S*Q          SMO   6                      [INTERFACE NUMBER
  19 2STB          STO   5  A1+3(3)             [STORE PERIPHERAL TYPE NO.   
   7 2T*2          ADN   6  1
   8 2TSL          BZE   5  NOTC 
   7 2W#=          LDN   4  3
   7 2WRW          TXU   5  4
  15 2X?G          BCS      NOTC                [NOT CONSOLE 
   9 2XR6          NGX   4  AWORK4(2)
  20 2Y=Q          BNG   4  NOTF                [J.IF ODD NO INTERFACES SO FAR   
  17 2YQB          STO   6  AWORK4(2)           [STORE INTERFACE NO  
   8 2_=2          BRN      NOTC 
   7 2_PL    NOTF  ADX   4  6
   7 329=          ANDN  4  1
  20 32NW          BZE   4  PCON                [J LO ERROR NOT INPUT AND OWTPWT 
  18 338G          NGS   6  AWORK4(2)           [MARK BOTH INPUT & OUTPUT
   4 33N6    NOTC
   8 347Q          ADX   7  FX1  
   7 34MB          EXIT  7  0
   4 3572    #   
  18 35LL    #     GET PARAMETER BEGINNING WITH U,L,I OR T , CALL ON X7  
   4 366=    #   
   7 36KW    NOTL  LDN   4  1
   8 375G          SBX   7  FX1  
   8 37K6          SPARABEG 1,4  
  10 384Q          MHUNT    3,CPB,CUNI   
   8 38JB          ADX   7  FX1  
   7 3942          EXIT  7  0
   4 39HL    #   
  21 3=3=    #     GET PARAMETER FROM CPB/CUNI BLOCK & CONVERT TO BINARY , CALL ON X7
   4 3=GW    #   
  20 3?2G    PABEG NGX   0  ANUM(3)             [ENTRY AFTER SPARABEG : X3 SET UP
  19 3?G6          BPZ   0  MISS                [PARAMETER NULL OR MISSING   
   8 3?_Q          SBX   7  FX1  
   7 3#FB          PHOTO 4   
   8 3#_2          CHNUMCOD  ,3  
  18 3*DL          TESTREP  CHNUMERR,URET       [ENDCOM IF INVALID FORMAT
  19 3*Y=          LDX   6  ACOMMUNE1(2)        [PICK UP CONVERTED PARAMETER 
   9 3BCW          TESTMOVE 4,NOMO   
  19 3BXG          MHUNT    3,CPB,CUNI          [RELOCATE AFTER COORDINATION 
   4 3CC6    NOMO
   7 3CWQ          FREECORE 3
   8 3DBB          ADX   7  FX1  
   7 3DW2          EXIT  7  0
   6 3F*L    URET  ENDCOM
   4 3FT=    #   
   4 3G#W    #   
   4 3GSG    #   
  21 3H#6    #     HARDWARE COMMAND IS E.G. HW U29.L2(4),TER(IAA,T4,D(CR,LP,TW,TW,,) 
  21 3HRQ    #     SET UP IDF/CONDATA BLOCK CONTAINING GEOG NO. & TERM TYPE/LINE NO. 
  21 3J?B    #     FOR EACH LINE ; 7020 TERMINALS ALSO HAVE IDENTIFIER/POLLING ADD.  
  17 3JR2    #     & PERIPHERAL DESCRIPTION IN ORDER OF INTERFACES   
   4 3K=L    #   
   9 3KQ=    XBR   GEOERR   1,BREAKIN
   6 3L9W    Z1HARDWARA  
  10 3LPG          PARSORT  XBR,NOCLUS   
  10 3M96          HUNTW    3,CPAR,JSCE  
  20 3MNQ          BNG   3  URET                [J IF ERROR DETECTED BY PARSORT  
   9 3N8B          LDX   5  JSCEB(3) 
   9 3NN2          LDX   1  JSCEP(3) 
  16 3P7L          ANDX  1  BITS22LS            [DEVICE LIST PTR 
  14 3PM=          LGEOG    1,6                 [GEOG NO 
   9 3Q6W          STO   6  AWORK4(2)
  15 3QLG          LTYPE    1,4                 [DEVICE TYPE 
   8 3R66          SBN   4  10   
  14 3RKQ          BZE   4  XUX                 [J IF UPX
  15 3S5B          BCT   4  NOTX                [J IF NOT MPX
  15 3SK2          BPZ   5  MISS                [NO LINE NO  
   7 3T4L          LDX   4  5
   7 3TJ=          SRL   4  9
  14 3W3W          LDCH  4  4                   [LINE NO 
   8 3WHG          BRN      X1   
   7 3X36    XUX   LDN   4  0
  14 3XGQ    X1    STO   4  AWORK3(2)           [LINE NO 
   7 3Y2B          LDN   0  1
  15 3YG2          STO   0  AWORK2(2)           [NO OF LINES 
  16 3Y_L          BPZ   5  TYPE                [ONLY ONE LINE   
   9 3_F=          LDX   0  JSCEK(3) 
   8 3_YW          BNG   0  TYPE 
  16 42DG          BZE   0  TYPE                [ASSUME 1 LINE   
  16 42Y6          ADX   4  0                   [LAST LINE NO+1  
   8 43CQ          SBN   4  65   
  16 43XB          BNG   4  XLOK                [OK-LESS THAN 64 
   8 44C2          LDN   0  #100 
   9 44WL          SBX   0  AWORK3(2)
   4 45B=    XLOK
  15 45TW          STO   0  AWORK2(2)           [NO OF LINES 
  16 46*G    TYPE  LDN   6  0                   [ANALYSIS LEVEL  
   8 46T6          SPARANXT ,6   
   7 472# ...#UNS  ANSTOOMANY
  11 477G ...      TESTREP2  TOOMANY,PTMANY  
  10 47#Q          MHUNT    3,CPB,CUNI   
   9 47SB          NGX   0  ANUM(3)  
   8 48#2          BPZ   0  MISS 
  19 48RL          LDX   0  APARA(3)            ['NULL','MOP','TER' OR 'SEN' 
  17 49?=          LDN   4  0                   [TERMINAL TYPE NUMBER
   9 49QW          TXU   0  TERT+3(1)
   8 4==G          BCC      STYPE
   7 4=Q6          LDN   4  3
   9 4?9Q          TXU   0  TERT+2(1)
   8 4?PB          BCC      STYPE
   7 4#92          LDN   4  1
   9 4#NL          TXU   0  TERT(1)  
   8 4*8=          BCC      STYPE
   9 4*MW          TXU   0  TERT+1(1)
   7 4*XN ...#SKI JSKI17<1$1 
   8 4B7G          BCS      PFER 
   6 4B94 ...#SKI JSKI17 
   4 4B=L ...(   
   8 4B#8 ...      BCC      NSPL 
   9 4B*Q ...      TXU   0  TERT+4(1)
   8 4BC# ...      BCS      PFER 
   8 4BDW ...      LDX   4  GSIGN
   9 4BGD ...      ORS   4  AWORK4(2)
   4 4BJ2 ...NSPL
   4 4BKJ ...)   
   4 4BM6    #   
   7 4C6Q          LDN   4  2
  18 4CLB          LDN   6  7                   [8 WORDS PER 7020 RECORD 
  21 4D62    STYPE ADN   6  1                   [1 WORD PER MOP OR SENTINEL RECORD   
  16 4DKL          MPY   6  AWORK2(2)           [NUMBER OF LINES 
  17 4F5=          FREECORE 3                   [TERMINAL TYPE BLOCK 
  18 4FJW          ADN   7  2                   [FOR FIRST AND LAST WORDS
  17 4G4G          SETUPCORE 7,3,IDF,CONDATA    [SET UP INFO BLOCK   
   9 4GJ6          LDX   6  AWORK3(2)
   9 4H3Q          STO   6  A1+1(3)  
   9 4HHB          DCH   4  A1+1(3)  
   9 4J32          LDX   6  AWORK4(2)
  17 4JGL          STO   6  A1(3)               [GEOGRAPHIC UNIT NO. 
   7 4K2=          SBN   4  2
  19 4KFW          LDN   1  1                   [LENGTH OF MOP OR SENT RECORD
   6 4KM4 ...#SKI JSKI17 
   9 4KS= ...      STOC  6  AWORK4(2)
  14 4K_G          BNZ   4  REP                 [NOT 7020
   4 4LF6    #   
  21 4LYQ    #     ANALYSE 7020 PARAMETERS : IDENTIFIER , PHYSICAL TERMINAL NUMBER , 
  18 4MDB    #     DEVICE ON EACH INTERFACE GIVEN IN ORDER OF INTERFACES 
   4 4MY2    #   
  18 4NCL          LDN   6  2                   [ANALYSE SECOND PARAMETER
  20 4NX=          PARALYSE #34,1,6             [X1 CONTAINS 1 : ANALYSIS LEVEL  
   7 4P4D ...#UNS  ANSTOOMANY
  11 4P9L ...      TESTREP2  TOOMANY,PTMANY  
  17 4PBW          LDCT  5  #510                ['I' FOR IDENTIFIER  
   8 4PWG          CALL  7  NOTL 
   9 4QB6          NGX   0  ANUM(3)  
  17 4QTQ          BPZ   0  MISS                [IDENTIFIER MISSING  
   7 4R*B          ADN   0  2
  17 4RT2          BNZ   0  PFER                [NOT TWO CHARACTERS  
   9 4S#L          LDX   6  APARA(3) 
  16 4SS=          ANDX  6  TOPH(1)             [ONLY TOP HALF   
  19 4T?W          LDCT  0  #770                [NOW CHECK IDENTIFIER VALID  
   7 4TRG          ANDX  0  6
  21 4W?6          SLC   0  6                   [MOVE FIRST CHAR OF IDENT.INTO B18-23
   8 4WQQ          SBN   0  #41  
  19 4X=B          BNG   0  NTER2               [ERROR NON-ALPHA FIRST LETTER
   8 4XQ2          SBN   0  #32  
  19 4Y9L          BPZ   0  NTER2               [ERROR NON-ALPHA FIRST LETTER
  17 4YP=          STO   6  AWORK4(2)           [PRESERVE IDENTIFIER 
   7 4_8W          FREECORE 3
  18 4_NG          LDCT  5  #640                ['T' FOR PHYS. TERM. NO. 
   8 5286          CALL  7  NOTL 
  19 52MQ          CALL  7  PABEG               [GET PHYSICAL TERMINAL NUMBER
  16 537B          BZE   6  NTER1               [ZERO TERM.NO!   
   7 53M2          SMO   6   
  16 546L          LDX   5  TABLE(1)            [POLLING ADDRESS 
   8 54L=          SBN   6  13   
  15 555W          BNG   6  T112                [J IF T1-T12 
   7 55KG          SBN   6  4
  16 5656          BNG   6  NTER                [T13-T16 -ERROR  
   8 56JQ          SBN   6  12   
  15 574B          BPZ   6  NTER3               [329+  -ERROR
   8 57J2          ADN   6  13   
   7 583L          SMO      6
   9 58H=          LDX   5  TABLE(1) 
  16 592W          ADN   5  1                   [POLLING ADDRESS 
   7 59=N ...      LDN   7  0
   4 59GG    T112
   9 5=26          ADX   5  AWORK4(2)
  10 5=FQ          MHUNTW   3,IDF,CONDATA
   8 5=H_ ...      BNZ   7  XSIOK
   8 5=L8 ...      LDX   4  A1(3)
   8 5=NC ...      BPZ   4  XSIOK
   8 5=QL ...      ADN   6  16   
   8 5=ST ...      BRN      NTER1
   5 5=X4 ...XSIOK   
   9 5=_B          STO   5  A1+2(3)  
  16 5?F2          STOZ     A1+3(3)             [ZEROISE 6 WORDS 
  17 5?YL          ADN   3  A1+3                [WHICH WILL CONTAIN  
  16 5#D=          LDN   4  1(3)                [PERIPHERAL TYPE 
  16 5#XW          MOVE  3  5                   [NUMBERS FOR 7020
  17 5*CG          LDN   4  3                   [PARAMETER POSITION  
  20 5*X6          SPARANXT ,,4                 [SPLIT E.G. D(CR,LP,TW,TW,TR,TP) 
   7 5B4# ...#UNS  ANSTOOMANY
  11 5B9G ...      TESTREP2  TOOMANY,PTMANY  
  10 5BBQ          MHUNT    3,CPB,CUNI   
   9 5BWB          NGX   0  ANUM(3)  
   8 5CB2          BPZ   0  MISS 
  20 5CTL          LDCH  0  APARA(3)            [FIRST CHARACTER SHOULD BE 'D'   
   8 5D*=          ERN   0  #44  
   8 5DSW          BNZ   0  PFER 
   7 5F#G          FREECORE 3
  17 5FS6          LDN   4  2                   [PARAMETER POSITION  
   9 5G?Q          PARALYSE #34,,4   
   7 5GDY ...#UNS  ANSTOOMANY
  11 5GL6 ...      TESTREP2  TOOMANY,PTMANY  
   4 5GRB    #   
  21 5H?2    #     VERIFY THAT INTERFACES 1 & 3 HAVE 7022=CR OR 7023=OUTPUT CONSOLE ,
  21 5HQL    #     THAT 2 & 4 HAVE 7021=LP OR 7023=INPUT CONSOLE , THAT 5 HAS 7024=TR
  20 5J==    #     AND THAT 6 HAS 7025=TP ; X6=INTERFACE NO. , X5=DEVICE TYPE NO.
   4 5JPW    #   
   7 5K9G          LDN   6  0
   9 5KP6          STOZ     AWORK4(2)
  19 5L8Q    TWICE CALL  7  NDEV                [DEVICE TYPE MAY BE 0,2 OR 3 
   8 5LNB          BZE   5  N1   
   7 5M82          ANDN  5  2
  15 5MML          BZE   5  RONG                [NOT 2 OR 3  
  19 5N7=    N1    CALL  7  NDEV                [DEVICE TYPE MAY BE 0,1 OR 3 
   7 5NLW          SBN   5  2
  14 5P6G          BZE   5  RONG                [TYPE 2  
   7 5PL6          SBN   5  2
  15 5Q5Q          BPZ   5  RONG                [TYPE 4 OR 5 
   7 5QKB          LDN   4  2
  19 5R52          TXU   4  6                   [X6 CONTAINS INTERFACE NUMBER
  21 5RJL          BCC      TWICE               [REPEAT SECTION FOR INTERFACES 3 & 4 
   8 5S4=          CALL  7  NDEV 
   8 5SHW          BZE   5  N2   
   7 5T3G          SBN   5  4
  16 5TH6          BNZ   5  RONG                [X5 SHOULD BE 4  
   8 5W2Q    N2    CALL  7  NDEV 
   8 5WGB          BZE   5  NOMIN
   7 5X22          SBN   5  5
  16 5XFL          BNZ   5  RONG                [X5 SHOULD BE 5  
   9 5X_=    NOMIN LDX   0  AWORK4(2)
  20 5YDW          BPZ   0  PCON                [NO CONSOLE FOR BOTH I/P & O/P   
   4 5YYG    #   
  18 5_D6          LDN   1  8                   [LENGTH OF 7020 RECORD   
   8 5_XQ    REP   ADN   3  A1+1 
   7 62CB          LDX   4  3
  16 62X2          LDX   6  AWORK2(2)           [NUMBER OF LINES 
   7 63BL          SBN   6  1
  16 63W=          BZE   6  TERM                [QUALIFIER WAS 1 
  17 64*W          LDN   5  1                   [TO UPDATE LINE NO   
   7 64TG    VAG   ADX   4  1
  19 65*6          MOVE  3  0(1)                [CREATE NEXT LINE DESCRIPTION
   7 65SQ          ADX   3  1
  17 66#B          ADS   5  0(3)                [UPDATE LINE NUMBER  
   8 66S2          BCT   6  VAG  
   7 67?L    TERM  ADX   3  1
   8 67R=          NGS   3  0(3) 
   9 68=W          ACROSS   HARDIDF,1
   4 68QG    #   
  11 69=6    #     VARIOUS  COMMAND ERRORS   
   4 69PQ    #   
  21 6=9B    ONUM  SBX   3  FX1                 [ROUTINE TO ^OUTNUM & THEN COMERRX   
   8 6=P2          OUTNUM   6,0  
   8 6?8L          ADX   3  FX1  
   7 6?N=          EXIT  3  0
   4 6#7W    #   
   4 6#MG    #   
   7 6*76          ZERRORY   
  17 6*LQ    MISS  ZERRORX  JPARMIS             [PARAMETER MISSING   
   8 6B6B    NTER3 ADN   6  29   
   8 6BL2          BRN      NTER1
   8 6C5L    NTER  ADN   6  17   
   8 6CK=    NTER1 CALL  3  ONUM 
  18 6D4W          ZERRORX  JLARGETERM          [INVALID TERMINAL NUMBER 
  19 6DJG    NTER2 ZERRORX  ITERMIN             [INVALID TERMINAL IDENTIFIER 
  16 6F46    NOTX  LDX   6  AWORK4(2)           [PICK UP GEOG NO 
   8 6FHQ          CALL  3  ONUM 
   9 6G3B          ZERRORX  JNOTMULT 
  21 6GH2    PCON  ZERRORX  JCOS                [REMOTE CONSOLE NOT INPUT AND OUTPUT 
  18 6H2L    PFER  ZERRORX  APFERR              [PARAMETER FORMAT ERROR  
   8 6HG=    RONG  CALL  3  ONUM 
  20 6H_W          ZERRORX  JDEVILL             [ILLEGAL DEVICE ON INTERFACE N   
   7 6J5R ...#UNS  ANSTOOMANY
   5 6J9N ...PTMANY  
   9 6J*K ...      ZERRORX  JMAXPAR  
   4 6JFG    #   
   4 6J_6    #END
   8 ____ ...43425460000700000000
