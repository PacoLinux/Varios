  13 22FL ...      SEG   INCSET,864,P.SMITH,INCDUMP  
   4 22_=    #   
  11 23DW ...      SEGENTRY K1INCSET,X1INCSET
   5 28T8 ...#UNS IDC
   4 28T9 ...(   
  10 28T= ...XFILES         12HFILES-ONLY
  10 28T? ...XREST          12HRESTORABLE
  21 28T# ...#     ******************************************************************
  14 28T* ...#                                  ZDEMAND  
  14 28TB ...#                                  *******  
   4 28TC ...#   
  14 28TD ...#     THIS SUBROUTINE ISSUES A DEMAND FOR   
  17 28TF ...#     ^PLEASE ISSUE DUMP COMMAND FOR NEXT INCREMENT ^   
  13 28TG ...#     IF ONE HAS NOT ALREADY BEEN GIVEN 
  14 28TH ...#     CANTDO CAUSES INCREMENT TO BE ABANDED 
   4 28TJ ...#   
   5 28TK ...ZDEMAND 
   8 28TL ...      SBX   4  FX1  
  20 28TM ...      JBS      ZFDUCOM,,BDCISS     [ JUMP IF COMMAND ALREADY ISSUED 
   3 28TN ...
  21 28TP ...      CLOSETOP              [[ CLOSE INCINDEX ,DONT WAIT WITH IT OPEN   
  17 28TQ ...      LONGSET  ADUMPWAIT,SET       [ SET WAITING STYLE  
   4 28TR ...SET 
   8 28TS ...#     ISSUE DEMAND  
   8 28TT ...      LDX   1  FX1  
  17 28TW ...      LDN   1  XFILES(1)                 [ FILES ONLY   
  17 28TX ...      BBS      1,6,XXFILES          [ JUMP IF FILES ONLY
   8 28TY ...      LDX   1  FX1  
  18 28T_ ...      LDN   1  XREST(1)            [ OTHERWISE RESTOREABLE  
   5 28W2 ...XXFILES 
  16 28W3 ...      LDN   2  ACOMMUNE2(2)        [ MOVE PARAMETERS
  17 28W4 ...      MOVE  1  3                   [ TO ACCOMMUNE WORDS 
  15 28W5 ...      LDX   2  FX2                  [ OUTPUT IT 
  10 28W6 ...      OUTPARN   10,ACOMMUNE2
  17 28W7 ...      DEMAND   1,INCDEMAND,YINCABAND,(GEOERR),REENTER,1 
  19 28W8 ...      JBC      REENTER,,BDCISS     [ JUMP IF SPURIOUS WAKE UP   
  11 28W9 ...      DEMANDQUIT    [ TIDY UP   
  15 28W= ...      LONGOFF                      [ CLEAR FIELD
  15 28W? ...      OPENINC    ,GENERAL     [ OPEN IT AGAIN ! 
   5 28W# ...ZFDUCOM 
   8 28W* ...      ADX   4  FX1  
   7 28WB ...      EXIT  4  0
   4 28WC ...#   
  21 28WD ...#     ******************************************************************
   4 28WF ...)   
  18 28WG ...X1INCSET           [ SET UP AND LOAD TAPES FOR A NEW INCR.  
  16 28WH ...                   [CALLED ON X4 AND COORDINATES.   
  20 28WJ ...                   [ON ENTRY B1 OF X6 SET IMPLIES A FILE ONLY INC IS
  14 28WR ...                   [NEEDED. X7=LAST INCR NO 
  20 28X8 ...                   [THERE MAY BE A FINTER AND FINTER1 ABOUT TO HELP 
  16 28XK ...                   [WITH THE INITIAL TAPE SELECTION.
  17 28Y2 ...                   [ON EXIT X7=X7+1 AND FINTERS FREED   
   5 28YT ...#UNS IDC
   4 28YW ...(   
  21 28YX ...#     ******************************************************************
   4 28YY ...#   
  16 28Y_ ...#     THE FOLLOWING CODE EFFECTS THE NEW IDC RTMS   
  13 28_2 ...#     DTFILEINC DTRESTINC DTAFTERREST   
   9 28_3 ...#     NEWDTASK NEWDTPOOL
   4 28_4 ...#   
  15 28_5 ...      MBC      2,IDCRTMOLD,IDCRTMASK,IDCRTMNEW  
  17 28_6 ...      BBS      1,6,RTMDTFILE       [ JUMP IF FILES ONLY 
  17 28_7 ...      SEGENTRY K85INCSET           [ FOR DTRESTINC RTM  
  17 28_8 ...      BS    2,IDCRTMOLD            [ TO BE CHANGED TO   
  13 28_9 ...#     BS    2,IDCRTMASK            [ OR 
  13 28_= ...#     BS    2,IDCRTMNEW            [  BY
  11 28_? ...#     DTRESTINC OLD ASK OR NEW  
   9 28_# ...      BRN      RTMDTREST
   4 28_* ...#   
  21 28_B ...#     ******************************************************************
  19 28_C ...RTMDTFILE                          [ FOR FILE ONLY INCREMENTS   
  17 28_D ...      SEGENTRY  K86INCSET          [ USED BY DTRESTINC  
  17 28_F ...      BS    2,IDCRTMOLD            [ TO BE CKANGED TO   
  13 28_G ...#     BS    2,IDCRTMASK            [ OR 
  13 28_H ...#     BS    2,IDCRTMNEW            [ BY 
  11 28_J ...#     DTFILEINC OLD ASK OR NEW  
   4 28_K ...#   
   6 28_L ...RTMDTREST   
  21 28_M ...#     ******************************************************************
   4 28_N ...#   
  17 28_P ...#     FOR INCREMENT FOLLOWING A RESTOREABLE INCREMENT   
  13 28_Q ...#     DTAFTERREST CONTROLS THIS CHOICE  
  21 28_R ...      HUNTW    1,FILE,FINTER1      [ CONTAINS LAST RESTOREABLE INC IF TH
  14 28_S ...      BNG   1  NOTAFTEREST        [ IS ONE  
  21 28_T ...      JBS      NOTAFTEREST,1,BINEWDT  [INC TERM BY EOT OR TAPEFAIL -NEW 
  17 28_W ...      JBC   NOTREST,1,BIGENRES     [ JUMP IF NOT RESTBLE
  10 28_X ...      SEGENTRY  K82INCSET   
  16 28_Y ...      BS    2,IDCRTMOLD                [ CHANGED TO 
  13 28__ ...#     BS    2,IDCRTMASK            [ OR 
  13 2922 ...#     BS    2,IDCRTMNEW            [ BY 
  12 2923 ...#     DTAFTERREST OLD ASK OR NEEW   
   8 2924 ...      BRN   NOTREST 
   6 2925 ...NOTAFTEREST 
  20 2926 ...      BS    2,IDCRTMNEW            [ NO FINTER1 SO NEW TAPES NEEDED 
   5 2927 ...NOTREST 
  11 2928 ...      JBS   RTMASK,2,IDCRTMNEW  
  11 2929 ...      JBC   RTMASK,2,IDCRTMASK  
  17 292= ...#     DONT ASK IF NEW TAPES NEEDED OR ASK NOT SPECIFIED 
   9 292? ...      CALL  4  ZDEMAND  
   5 292# ...RTMASK  
  21 292* ...#     ******************************************************************
  17 292B ...#      IF NEW TAPES REQUIRED, FREE FINC AND OLD TAPES   
  11 292C ...      JBC   NOFINCLA,2,IDCRTMNEW
  10 292D ...      HUNTMISB 3,FILE,FINC  
   9 292F ...      BNG   3  NOFINCLA 
   8 292G ...      FREEFINC  3   
   5 292H ...NOFINCLA
   4 292J ...#   
  21 292K ...#     ******************************************************************
   4 292L ...#   
   9 292M ...#     FOR NEWDTASK RTM  
  21 292N ...#     IF NEW TAPES ARE NEEDED  EITHER BECAUSE OF ONE OF THE OTHER TTMS  
  14 292P ...#     OR A TAPE EVENT ON THE LAST INCREMENT 
  16 292Q ...#     AND NO EXPLICIT DUMP COMMAND HAS BEEN ISSUED  
  16 292R ...#     DUMPER WILL REQUEST PLEASE ISSUE DUMP COMMAND 
  10 292S ...      SEGENTRY  K83INCSET   
   9 292T ...      BRN      NOTNDTASK
  17 292W ...#     DEFAULT OFF BUT BRN NULLED OUT BY ' ON ' PARAMETER
  18 292X ...      JBS      NOTNDTASK,,BDCISS   [ JUMP IF COMMAND GIVEN  
  21 292Y ...      JBC   NOTNDTASK,2,IDCRTMNEW  [ DONT ASK UNLESS NEW TAPES NEEDED   
   9 292_ ...      CALL  4  ZDEMAND  
   6 2932 ...NOTNDTASK   
  10 2933 ...      SEGENTRY  K84INCSET   
   3 2934 ...
  21 2935 ...      JBCC     NOFDUCOM,,BDCISS    [ IF A DUMP COMMAND HAS BEEN ISSUED  
  20 2936 ...#     CLAIM THE FDUCOM IN THE MISC CHAIN AND VERIFY ANY TSNS IN IT  
  10 2937 ...      HUNTMISB 3,FI,FDUCOM  
   9 2938 ...      CHAIN    3,FPTR(2)
  18 2939 ...      BWZ      FDUTSNCT(3),NOFDUCOM   [ IF THERE ARE NO TSNS
  19 293= ...      DOWN     DUMP,3              [ WE DONT NEED TO CHECK THEM 
   5 293? ...NOFDUCOM
   4 293# ...)   
  16 293* ...      LDX   5  AINCCOPIES          [NO OF MTS WANTED
   7 293B ...      LDX   4  5
   7 293C ...      SLL   4  1
  17 293D ...      ADN   4  FGETSER-INCNO       [X4=FGET(FINC) LENGTH
  17 293F ...      SETUPCORE 4,3,FILE,FGET      [SET UP FGET (FINC)  
  15 293G ...      ADN   7  1                   [NEW INCR NO 
   9 293M ...      STO   7  INCNO(3) 
  10 2944 ...      STO   5  INCCOPIES(3) 
  17 294F ...      STOZ     INCREPLY(3)         [ZERO REST OF BLOCK  
  10 294W ...      LDN   0  INCREPLY(3)  
  10 295? ...      LDN   1  INCREPLY+1(3)
   7 295N ...      SBN   4  3
   7 2965 ...      SMO      4
   7 296G ...      MOVE  0  0
  17 296X ...      LDN   1  FGETSER(3)          [INITIALISE TSN WORDS
  17 297# ...      NGN   0  1                   [-1 SHOWS TSN NEEDED 
   5 297P ...TSNINIT 
   8 2986 ...      STO   0  0(1) 
   9 298H ...      ADN   1  FGETLEN  
   9 298Y ...      BCT   5  TSNINIT  
  20 299* ...      BBS      1,6,NREST           [J IF NOT TO BE RESTORABLE INCR  
  20 299Q ...      BS       3,BINCGENRES        [SET POTENTIALLY RESTORABLE BIT  
   5 29=7 ...NREST   
   4 29=J ...#   
  20 29=_ ...#  NOW THE BASIC FINC(FGET) BLOCK IS SET UP WE MUST DECIDE WHETHER  
  13 29?B ...#  THIS IS TO BE A TOTAL DUMP OR NOT.   
  20 29?R ...#  THIS IS SO IF EITHER 1) TOTAL BIT IS SET IN INCWORD IN FIXED AND 
  20 29#8 ...#                         START OF FILESTORE SCAN (FIRST FILE ONLY  
  21 29#K ...#                         INCR IF IN PLLL MODE OR EMPTY INCBATCH LIST   
  12 29*2 ...#                         IF NOT)   
  21 29*C ...#                OR     2) LAST INCR WAS TOTAL AND WE ARE NOT AT THE STA
  15 29*S ...#                         OF A FILESTORE SCAN   
   4 29B9 ...#   
   6 29B* ...#UNS  AMTGR 
   4 29BF ...#SKI
  17 29BL ...      JBS      PMODE,,BPARALLEL      [J IF IN PLL MODE  
  11 29C3 ...      MHUNTW   3,FILE,FINCPROC  
  10 29CD ...      LDX   0  INCBATCH(3)  
  17 29CT ...      BZE   0  SBATCH              [J IF START OF BATCH 
   6 29CY ...#UNS  AMTGR 
  14 29D3 ...#SKI         [ NOT TRUE ANY MORE FOR MTGR   
   4 29D6 ...(   
  14 29D= ...      BRN      NSBATCH             [J IF NOT
   5 29DM ...PMODE   
  20 29F4 ...      HUNTW    3,FILE,FINC         [IN PMODE START OF BATCH IF FINC 
  17 29FF ...      BNG   3  SBATCH              [NOT ALREADY SET UP  
   4 29FW ...#   
   4 29G4 ...)   
  19 29G? ...#  WE ARE NOT STARTING A BATCH SO THE LAST RECORD IN INCINDEX   
   7 29GN ...#  SETTLES IT   
   4 29H5 ...#   
   5 29HG ...NSBATCH 
  18 29HX ...      WIND                         [POSITION ON LAST RECORD 
   7 29J# ...      STEPAGAIN 
   6 29JD ...#UNS  AMTGR 
  20 29JJ ...      JBS      SBATCH,3,BIENDBATR [ JUMP IF LAST INC END OF BATCH   
  18 29JP ...      JBC      NTOT,3,BITOTDR      [J IF LAST INC NOT TOTAL 
  10 29K6 ...      MHUNTW   3,FILE,FGET  
  15 29KH ...      BS       3,BINCTOTD          [ITS TOTAL   
   8 29KY ...      BRN      NTOT 
   4 29L* ...#   
  19 29LQ ...#  WE ARE AT THE START OF A BATCH SO THE TOTAL BIT DETERMINES IT
   4 29M7 ...#   
   5 29MJ ...SBATCH  
   6 29MN ...#UNS FNSAD2 
  20 29MS ...      BS        ,BATCHSTART        [ SHOW START BATCH FROM BEGINNING
  19 29M_ ...      BC     ,BNOWD                [TAKE OUT 'PLEASE DUMP' BIT  
  10 29N4 ...      JBS      YTOT,,BTOTD  
   5 29N7 ...#UNS IDC
   4 29N= ...(   
   9 29N* ...      HUNTW  1,FI,FDUCOM
   8 29ND ...      BNG   1  NTOT 
  17 29NH ...      JBS      YTOT,1,BFDTOTAL        [ JUMP IF DU TOTAL
   4 29NL ...)   
  17 29P8 ...      BRN      NTOT                [NULLED BY NEWDUMP   
   4 29PK ...YTOT
  10 29Q2 ...      MHUNTW   3,FILE,FGET  
  16 29QC ...      BS        3,BINCTOTD         [SET BIT IN FINC 
   4 29QS ...NTOT
   4 29R9 ...#   
  18 29RL ...#  WE NOW SET UP A FILE FWB TO HOLD THE NEW INCREMENT RECORD
   4 29S3 ...#   
  10 29SD ...      MHUNTW   2,FILE,FGET  
  10 29ST ...      LDX   3  INCCOPIES(2) 
   9 29T= ...      LDN   0  INCMAGLEN
   7 29TM ...      MPY   3  0
  21 29W4 ...      ADN   4  AMAGR               [X4= REQUIRED LENGTH OF NEW INCINDEX 
  16 29WF ...      SETUPCORE 4,3,FILE,FWB       [X3-> NEW RECORD 
  16 29WW ...      STO   4  AINCRH(3)           [SET REC. HEADER 
  20 29X? ...      LDN   0  #41                 [SET 2ND WORD OF RECORD HEADER   
  19 29XN ...      STO   0  AINCHD(3)           [   FOR BASIC PERIPH. FILE   
  10 29Y5 ...      STOZ     AINCRH+2(3)  
  10 29YG ...      LDN   0  AINCRH+2(3)  
  10 29YX ...      LDN   1  AINCRH+3(3)  
   7 29_# ...      SMO      4
  21 29_P ...      MOVE  0  509                 [ZEROISE ALL OF FWB EXCEPT REC. HEADE
  16 2=26 ...      STO   7  AINCNO(3)           [SET UP INCR NO. 
  10 2=2H ...      MHUNTW   2,FILE,FGET  
  10 2=2Y ...      LDX   0  INCCOPIES(2) 
  16 2=3* ...      STO   0  AMTS(3)             [ AND NO OF MTS. 
  19 2=3Q ...      JBC   NOTTOT,2,BINCTOTD      [J IF NO ^DUMP TOTAL^ COMMAND
  21 2=47 ...      BS    3,BITOTD               [SET 'DUMP TOTAL' BIT IN NEW RECORD  
   5 2=4J ...NOTTOT  
  17 2=4_ ...      MBS   3,BIREDUMP,BINSUITRV,BIUNFIN [INDICATE TO BE
  19 2=5B ...                                   [REDUMPED,NOT RETRIEVABLE,   
  16 2=5R ...                                   [AND IN PROGRESS 
   7 2=68 ...      LDN   0  5
  18 2=6K ...      ORS   0  AINCSTATE(3)        [FORMAT VERSION NUMBER   
   4 2=72 ...#   
  21 2=7C ...#  THIS IS THE INITIAL TAPE SELECTION CODE.  TRANSFER AS MANY TAPES AS  
  21 2=7S ...#  AVAILABLE OR REQUIRED ACROSS FROM LAST INCINDEX RECORD TO FILE.FGET  
  13 2=89 ...#  BLOCK, UNLESS ALL NEW TAPES REQUIRED.
   5 2=8= ...#UNS IDC
   4 2=8? ...(   
  21 2=8# ...#     IF ANY OF SPARE POOL OR TSNS HAVE BEEN GIVEN - WE IGNORE THE CURRE
  18 2=8* ...#     DUMP TAPES - UNLESS THEY HAVE BEEN EXPLICITLY GIVEN   
  10 2=8B ...      HUNTW    1,FI,FDUCOM  
  16 2=8C ...      BNG   1  OLDTAPES            [ J IF NO FDUCOM 
  21 2=8D ...#     ******************************************************************
  16 2=8F ...#     IF ANY TSNS HAVE BEEN GIVEN IN DUMP COMMAND   
  21 2=8G ...#     AND THEY ARE IN FINC BLOCK IN MISC CHAIN , TRANSFER THEM TO FGET  
   4 2=8H ...#   
   4 2=8J ...#   
  12 2=8K ...      HUNTMISB 3,FILE,FINC         [
  19 2=8L ...      BNG   3  NOFINC2             [ JUMP IF NO FINC RETAINDED  
  21 2=8M ...      BWZ      FDUTSNCT(1),NOFINC2 [ OR IF NO TSNS GIVEN IN DU COMMAND  
  12 2=8N ...      MHUNTW   2,FILE,FGET         [
   4 2=8P ...#   
  17 2=8Q ...#     IN THE FOLLOWING LOOP , THE ACCUMULATORS ARE USED 
  18 2=8R ...#     X1 = ENTRY IN FDUCOM         X6 IS MODIFIER DOWN BLOCK
  19 2=8S ...#     X2 = ENTRY IN FGET           X4 IS COUNT OF TAPES IN FGET 
  19 2=8T ...#     X3 = ENTRY IN FINC           X5 IS COUNT OF TAPES IN FINC 
   4 2=8W ...#   
   7 2=8X ...      LDN   6  0
  12 2=8Y ...      LDX   5  INCCOPIES(3)        [
   6 2=8_ ...TAPEAGAIN   
  12 2=92 ...      SMO      6                   [
  18 2=93 ...      LDX   0  FDUTSN(1)           [ PICK UP REQUESTED TSN  
  18 2=94 ...      BXU   0  FGETSER(3),NOTTSN   [ JUMP UNLESS FOUND ENTRY
  17 2=95 ...      STO   0  FGETSER(2)          [ STORE TSN IN FGET  
  17 2=96 ...      LDX   0  FGETSTATE(3)        [ TRANSFER STATE WORD
  12 2=97 ...      STO   0  FGETSTATE(2)        [
  18 2=98 ...      LDCT  0  #400                [ CLEAR TSN IN OLD FINC  
  12 2=99 ...      STO   0  FGETSER(3)          [
  19 2=9= ...      ADN   2  FGETLEN             [ UPDATE POINTER IN NEW FGET 
   5 2=9? ...NOTTSN  
  18 2=9# ...      ADN   3  FGETLEN             [ TRY NEXT TAPE IN FINC  
  12 2=9* ...      BCT   5  TAPEAGAIN           [
  18 2=9B ...      HUNTMISB 3,FILE,FINC         [ RESET POINTERS TO FINC 
  17 2=9C ...      LDX   5  INCCOPIES(3)        [ AND NUMBER OF TAPES
   7 2=9D ...      ADN   6  1
  21 2=9F ...      BXU   6  FDUTSNCT(1),TAPEAGAIN [ JUMP UNLESS FINISHED FDUCOM TAPE 
  21 2=9G ...      FREEFINC 3                   [ TRANSFERRED ALL TAPES NEEDED FREE R
   3 2=9H ...
   5 2=9J ...NOFINC2 
   4 2=9K ...#   
  21 2=9L ...#     ******************************************************************
   4 2=9M ...#   
  10 2=9N ...      MHUNTW    1,FI,FDUCOM 
  15 2=9P ...      JMBS     TAPES,1,BFDPOOL,BFDSPARE,BFDTSN  
   5 2=9Q ...OLDTAPES
  11 2=9R ...      JBS   TAPES,FX2,IDCRTMNEW 
   4 2=9S ...)   
   4 2=9T ...#   
  20 2=9W ...      HUNTW    3,FILE,FINTER1      [X3-> FINTER1 BLOCK (LAST INCR.) 
  19 2=9X ...      BNG   3  TAPES               [J IF NOT GIVEN (NEW TAPES)  
  18 2=9Y ...      LDX   6  INCCOPIES(2)        [X6= NO. OF MTS REQUIRED 
  21 2=== ...      LDX   5  AMTS(3)             [X5= NO. OF MTS IN LAST INCR. REC.   
  21 2==M ...      LDN   4  0                   [INITIALISE MOD DOWN LAST INCR. LIST 
  21 2=?F ...      JBS   TAPES,3,BINEWDT       [J IF LAST INCR HIT EOT OR WRITE FAIL 
  21 2=?W ...                                   [   & SO INCR. HAD TO BE FORCIBLY TER
  10 2=#? ...      MHUNTW   2,FILE,FGET  
   5 2=#N ...MORESER 
   7 2=*5 ...      SMO      4
  21 2=*G ...      LDX   0  AMAG(3)             [INSERT NEXT TSN IN LAST INCR. INTO  
  20 2=*X ...      STO   0  FGETSER(2)          [   NEXT POSITION IN FGET BLOCK  
  19 2=B# ...      ADN   4  INCMAGLEN           [UPDATE MODIFIERS IN FINTER1 
  19 2=BP ...      ADN   2  FGETLEN             [               & FGET BLOCKS
  21 2=C6 ...      SBN   5  1                   [REDUCE NO. OF TAPES AVAILABLE FROM L
  16 2=CH ...      BZE   5  TAPES2               [J IF NONE LEFT 
  18 2=CY ...      BCT   6  MORESER             [J IF MORE MTS REQUIRED  
   5 2=C_ ...TAPES2  
   5 2=D2 ...#UNS IDC
   4 2=D3 ...(   
  21 2=D4 ...#     ******************************************************************
   3 2=D5 ...
  15 2=D6 ...#     TRANSFER ANY TAPES IN OLD FINC TO NEW FGET
  10 2=D7 ...      HUNTMISB 3,FILE,FINC  
  16 2=D8 ...      BNG   3  NOFINC              [ JUMP IF NO FINC
  17 2=D9 ...      CHAIN    3,FX2               [ OTHERWISE CHAIN IT 
  12 2=D= ...      MHUNTW   2,FILE,FGET         [
  17 2=D? ...      LDX   4  INCCOPIES(2)        [ NO OF TAPES NEEDED 
  18 2=D# ...      LDX   5  INCCOPIES(3)        [ NO OF TAPES IN OLD FINC
   6 2=D* ...NEXTFGETSN  
  17 2=DB ...      LDX   0  FGETSER(2)          [ TSN OF WANTED TAPE 
  17 2=DC ...      BXU   0  FGETSER(3),NOTHIS1  [ JUMP UNLESS FOUNR  
   4 2=DD ...#   
  14 2=DF ...#     TRANSFER STATE WORD AND OVERWRITE TSN 
   4 2=DG ...#   
  12 2=DH ...      LDX   0  FGETSTATE(3)        [
  12 2=DJ ...      STO   0  FGETSTATE(2)        [
   4 2=DK ...#   
  12 2=DL ...      LDCT  0  #400                [
  12 2=DM ...      STO   0  FGETSER(3)          [
   4 2=DN ...#   
   5 2=DP ...NOTHIS1 
  16 2=DQ ...      ADN   2  FGETLEN             [ TRY NEXT TSN   
  12 2=DR ...      BCT   4  NEXTFGETSN          [
   4 2=DS ...#   
  18 2=DT ...      MHUNTW   2,FILE,FGET         [ RESET POINTER TO FGET  
  12 2=DW ...      LDX   4  INCCOPIES(2)        [
  17 2=DX ...      ADN   3  FGETLEN             [ POINTS TO NEXT TAPE
  10 2=DY ...      BCT   5  NEXTFGETSN   
  19 2=D_ ...#     ANNY TAPES NEEDED HAVE MOVED TO FGET SO THE FINC IS FREED 
  10 2=F2 ...      MHUNTW   3,FILE,FINC  
   7 2=F3 ...      FREEFINC 3
   5 2=F4 ...NOFINC  
  21 2=F5 ...#     ******************************************************************
   4 2=F6 ...)   
   5 2=F8 ...TAPES   
   5 2=F9 ...#UNS IDC
   4 2=F= ...(   
  10 2=F? ...      HUNTMISB 3,FILE,FINC  
  10 2=F# ...      BNG   3  NOFINCTAPE   
  20 2=F* ...      FREEFINC 3               [ FREE THE FINC IF ONE IN MISC CHAIN 
   6 2=FB ...NOFINCTAPE  
  20 2=FC ...      BS      ,BCANAB          [ SHOW THERE IS A CANCELLABLE DUMP   
   4 2=FD ...)   
   8 2=FF ...      LDX   2  FX2  
  19 2=FJ ...      STOZ     3                   [SET 'MTS OBTAINED' INDICATOR
  19 2=F_ ...      STOZ     AWORK1(2)           [CLEAR INCTAPES MARKER WORD  
  20 2=GB ...      STOZ  AWORK2(2)              [CLEAR TSN MARKER FOR INCTAPES   
  19 2=GR ...      VFREEW   FILE,FINTER1        [NO FINTER1 IF INCINDEX EMPTY
  21 2=H8 ...      DOWN  INCTAPES,1             [OBTAIN & POSITION FOR DUMPING MTS   
  20 2=HK ...                                   [       MENTIONED IN FGET BLOCK  
  21 2=J2 ...                                   [UP -'NO MTS OBTAINED' EXIT FROM INCT
  21 2=JC ...                                   [                       ABANDON INCR.
  18 2=JS ...      ADN   3  1                   [SET 'NO MTS' INDICATOR  
  21 2=K9 ...                                   [UPPLUS 1   - NORMAL EXIT FROM INCTAP
   5 2=K= ...#UNS IDC
   4 2=K? ...(   
  18 2=K# ...      BC      ,BCANAB         [ NO LONGER CANCELLABLE DUMP  
  10 2=K* ...      VFREEW  FI,FREJTAPES  
  10 2=KB ...      VFREEW FI,FDUMPTAPES  
   9 2=KC ...      VFREEW  FI,FDUCOM 
   4 2=KD ...)   
   9 2=KL ...      MFREEW   FILE,FWB 
  10 2=L3 ...      VFREEW   FILE,FINTER  
  18 2=LD ...      BNZ   3  YINCABAND           [J IF ABANDONING INCR.   
  20 2=LT ...      MHUNTW   3,FILE,FGET       [FGET IS CALLED FINC FOR REST OF   
  14 2=M= ...      NAME     3,FILE,FINC         [ITS LIFE
   7 2?6W ...      UPPLUS   1
   4 79FB    #   
  21 79_2    #  IF NO TAPES ARE AVAILABLE FOR DUMPING (I.E. THE OPS HAVE REPLIED NO T
  21 7=DL    #  ^DO YOU STILL WISH TO DUMP ON 1 MT?^), ABANDON THE INCREMENT, PUT OUT
  19 7=Y=    #  A MESSAGE, FREE BLOCKS AND GO BACK ACROSS TO INCWAIT TO WAIT.
  20 7?2T ...#  BEFORE LETTING THE ABANDON TAKE PLACE WE CHECK THAT THE INCRMENT 
  20 7?5D ...#  IS NOT THE THIRD OR LATER OF A PARALLEL DUMP. (IF IT IS BPARALLEL
  20 7?83 ...#  WILL BE SET AND THERE WIL BE A RESTORABLE FINC ABOUT.) IF THIS   
  21 7?=L ...#  IS THE CASE WE OUTPUT AN ADMONITORY MESSAGE AND REPEAT THE OPERATION 
   7 7?*9 ...#  OF INCSTART. 
   4 7?CW    #   
   6 7?XG    YINCABAND   
  20 7G?Q ...      JBC      NPL,,BPARALLEL      [IS THIS A PLL DUMP. J IF NOT.   
  20 7G#3 ...      HUNTW    3,FILE,FINC        [IS THERE AN INCR SET UP ALREADY  
  14 7G## ...      BNG   3  NPL                 [J IF NOT
  19 7G#K ...      JBC      TWOINCS,3,BINCGENRES    [J IF ITS NOT RESTORABLE 
  16 7G#W ...      MONOUT   ANOINCAB            [NAUGHTY NAUGHTY!
   9 7G*7 ...      MHUNTW 3,FILE,FGET
  21 7G*D ...      BNG   3  NOFGETCD            [ THERE WILL BE NO FGET IF CD ISSUE  
  21 7G*P ...      FREEFINC 3,NODUMP            [ RETURN DTA'S TO RETRIEVAL SYSTEM   
   5 7GB2 ...NOFGETCD
  16 7GB? ...      OPENINC  ,GENERAL            [REOPEN INCINDEX 
  16 7GBJ ...      CALL  4  ZDEMAND  [[ PLEASE ISSUE DUMP COMMAND
  16 7GBT ...      UP                           [AND TRY AGAIN   
   4 7GC6 ...#   
  19 7GCC ...TWOINCS                            [WE HAVE TWO INCRS TO ABANDON
  16 7GCN ...      OUTPACK  INCNO(3),1,INCNUM   [OUTPUT MESSAGE  
  18 7GC_ ...      MONOUT   INCABAND            [FOR THE FILE ONLY INCR  
   4 7GD= ...#   
  21 7GDH ...#  THE FIRST INCREMENT IS ALREADY SET UP SO WE HAVE TO RELEASE THE DTAS 
  18 7GDS ...#  AND POSSIBLY UPDATE THE DIRENT IN :DUMPER FOR THE TAPES  
   4 7GF5 ...#   
  12 7GFB ...      MHUNTW 3,FILE,FINC           [
  19 7GFM ...      FREEFINC 3,NODUMP            [ FREE THE FGET AND ANY TAPES
   4 7GFY ...NPL 
  21 7GG9 ...      FON   BSUJWT                 [RELEASE BS UNJAMMER IF IT IS WAITING
  19 7GGG ...                                   [        FOR END OF THIS DUMP
  19 7GGR ...      BC    ,BNOWD                 [REMOVE 'DUMP NOW BIT' IF SET
  10 7GH4 ...      MHUNTW   2,FILE,FGET  
  17 7GH* ...      BPZ   2  YCD                 [ JUMP IF FGET THERE 
  10 7GHL ...      VFREEW  FILE,FINTER   
  10 7GHX ...      VFREEW  FILE,FINTER1  
  17 7GJ8 ...      ADN   7  1                   [ OTHERWISE OUTPUT   
  17 7GJF ...      OUTPACK  7,1,INCNUM          [ INCREMENT NUMBER   
  15 7GJQ ...      MONOUT   INCABAND            [ ABANDONED  
  10 7GK3 ...      HUNTMISB 3,FILE,FINC  
   8 7GK# ...      BNG   3  NPL2 
  17 7GKK ...      FREEFINC 3            [ FREE FINC IF THERE IS ONE 
   8 7GKW ...      BRN      NPL2 
   4 7GL7 ...YCD 
  19 7GLD ...      OUTPACK  INCNO(2),1,INCNUM   [OUTPUT INCR NO. AS PARAMETER
  21 7GLP ...      MONOUT   INCABAND            [OUTPUT TO MON. FILE & OPS. CONSOLE  
  21 7GM2 ...                                   [^INCREMENT %A ABANDONED AND OBLITERA
   9 7GM? ...      MHUNTW 3,FILE,FGET
   9 7GMJ ...      FREEFINC 3,NODUMP 
   4 7GMT ...NPL2
  10 7GN6 ...      MFREEW   FILE,FQUAL   
  18 7GNC ...      LDCT  7  #200                [SET 'GO WAITING' MARKER 
  21 7GNN ...      PERMITBRK                    [ALLOW BREAK-IN SO CAN DEAL WITH ^FIN
  10 7GN_ ...      BC       ,BPARALLEL   
  21 7GPK ...      UP                           [THIS INCR RECORD NOW OBLITERATED (AC
  21 7GQ=                                       [   DELETED FROM INCINDEX IN INCTAPES
   4 7H9W    #   
   4 7HPG    #   
  10 7J96 ...      MENDAREA 30,K99INCSET 
   4 7JNQ    #   
   4 7K8B    #END
   8 ____ ...02552663000300000000
