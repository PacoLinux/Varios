  12 22FL ...      SEG      MOPPM,,PHIL. TURNER  
  11 23YG    #LIS  K0MOPPM>K0POST>K0ALLGEO   
  11 248# ...      SEGENTRY K1MOPPM,Z1MOPPM  
   4 24D6    #   
   4 24XQ    #   
   7 25CB    #DEF  PMERRCNT=5
   4 25X2    #   
   4 26BL    #   
  10 2*P2    XCACT          +GENERAL/8:0 
   9 2B8L    XHALFTOP       #77770000
   8 2BN=    XMIN1          -1   
   9 2C7W    SIGN           #40000000
   7 2CMG    WORKWORD       0
  19 2D76    XFX2           0                   [POINTER TO CURRENT ACTIVITY.
  20 2DLQ    YFX2           0                   [POINTER TO LAST ACTIVE ACTIVITY.
  21 2F6B    PCTTWMASK      #40404040           [DEFAULT FOR TW OUTPUT IS INTERFACE 4
  16 2FL2    PCTADDRESS     #7420,#7430,#7400,#7410,#7440,#7450  
  16 2G5L                   #7460,#7470,#7540,#7550,#7560,#7570  
   4 2GK=    #   
   5 2H4W    PINFORM 
  11 2HJG    #HAL           A7020+INFORM,0   
   4 2J46    MLIN
  11 2JHQ    #HAL           AMXOR+ALINES,0   
  15 2K3B    TENSECONDS     10*K9/1000          [TEN SECONDS.
  11 2KH2    ONEDAY         3600*24/K9*1000  
   9 2KSB ...      FIXTRA K50MOPPM   
  20 2L5Q ...X50MP          +1*60*1000/K9        [CRUNCHTIME FOR EACH TRANSFER.  
   9 2LC6 ...      FIXTRA K51MOPPM   
  21 2LNG ...X51MP          +2*60*1000/K9        [CRUNCHTIME FOR WHOLE PM. ROUTINE.  
   9 2L_W    MODEX          #40004   
   7 2MFG    TOTALTIME      0
   8 2M_6    XALPHA         #74  
   8 2NDQ    XDELMAX        #40  
   9 2NR7 ...#DEF  TABLELIMIT=ENDFIX 
  10 2P5J ...YAMOP       +ADMULTACT-ADMP 
   4 2PD2    #   
  21 2PXL    OUTMOP         54H_3_*_-_*_ THE SYSTEM HAS TEMPORARILY CLOSED DOWN_*_-_8
  21 2QC=    OUTPCT         52H_^_*_*_ THE SYSTEM HAS TEMPORARILY CLOSED DOWN_,_4_#  
  18 2QWW    OUTCAN         16H_^  CANCEL_*_4_# [STX   CANCEL N/L DC4 ETX
  17 2RBG    OUTENQ         #76247000,#76250000 [EOT (ADDRESS) ENQ   
  18 2RW6    OUTLP          #76227634           [STX.FORMFEED   )MUST BE 
  18 2S*Q    OUTEND         #76047623           [DC4 ETX        )IN THIS 
  18 2STB    OUTTP          #76227673,#76737673 [STX DEL DEL DEL)ORDER.  
   4 2T*2    #   
  21 2TSL    #DEF  OUTTPX=OUTTP+31              [TP OUTPUT BUFFER = 32 WORDS (128CH.)
  21 2W#=    #DEF  PCTDUSTBIN=OUTTPX+1          [OVERWRITE K1MOPPM WHEN FINISHED WITH
   4 2WRW    #   
   4 2X?G    #   
   5 2XR6    Z1MOPPM 
   8 2Y7L ...      LDX   3  FXPM1
   7 2YJ6 ...      LDX   2  3
   9 2YYL ...PM1   LDX   1  YAMOP(2) 
  18 2_*6 ...PM1A   LDX   1  ADMP(1)             [GET NEXT MOP ACTIVITY. 
  17 2_PL ...      BZE   1  START               [J. IF END OF LIST.  
   9 3266 ...      PMCORES  1,START  
   9 32GL ...      LDX   6  CPPTR(1) 
   9 32X6 ...      PMCORES  6,PM1A   
   9 33?L ...      STO   1  YAMOP(2) 
   8 33N6          LGEOG    6,7  
  10 347Q          FINDPEREC 1,APGEOG,7  
  21 34MB          BXU   1  6,PM1               [J. UNLESS CPPTR POINTS TO DEV. LIST 
   9 3572 ...      LDX   1  YAMOP(2) 
   9 35LL    PM3   LDX   1  FPTR(1)  
   8 366=          PMCORES  1,PM1
   9 36KW          LDX   0  ATYPE(1) 
  18 375G          BXGE  0  CACT,PM1            [J. IF NO ALINES BLOCK   
   9 37K6 ...      SBX   0  MLIN(2)  
   8 384Q          BNZ   0  PM3  
   8 38JB          LNLI     6,7  
   7 3942          SLL   7  2
  19 39HL ...      STO   1  TOTALTIME(2)           [USE AS TEMPORARY DUMP.   
   7 3=3=    PM4   ADX   1  7
  11 3=GW          LDEX  0  A1+FOURTHWD(1)   
  11 3?2G          LDCH  5  A1+FOURTHWD(1)   
  10 3?G6          LDX   1  A1+BPTR(1)   
  18 3?_Q          ERN   0  CONSMACH            [J. IF NOT MOP CONSOLE.  
   8 3#FB          BNZ   0  PM11 
  20 3#_2          ANDN  5  #20                 [J. IF NOT RINGED & NOT ATTACHED.
   7 3*DL          ORX   5  1
   8 3*Y=          BZE   5  PM10 
  21 3BCW ...      LDN   4  MOPLEN-1(3)           [J. OUT IF NOT ENOUGH CORE LEFT.   
  11 3BXG          BXGE  4  TABLELIMIT,START 
  18 3CC6 ...      LDN   0  MOP(2)                 [LINK FOR MOP CONSOLE.
  15 3CWQ    PM5   SRC   7  11                  [LINE NO.: 0 
  21 3DBB ...      SBN   4  TABLE-1               [RELATIVE PTR. TO NEXT TABLE ENTRY.
   9 3DW2          STO   7  XLINE(3) 
  17 3F*L          STO   6  XDEV(3)             [DEVICE LIST POINTER.
   9 3FT=          STO   4  XFPTR(3) 
  21 3G#W ...      STO   3  YFX2(2)                [REMEMBER CURRENT ENTRY ADDRESS.  
   9 3GSG          STO   0  XLINK(3) 
  15 3H#6          SLC   7  9                   [LINE NUMBER.
   8 3HRQ          FINDLCA  6,7  
  17 3J?B          STO   1  X3(3)               [SAVE LCA ADDRESS.   
   7 3JR2          LDX   3  4
   7 3K=L          SLL   7  2
  16 3KQ=    PM10  BZE   7  PM1                 [J. IF UNIPLEXER 
   7 3L9W          SBN   7  4
  19 3LPG          BZE   7  PM1                 [J. IF ALL LINES DEALT WITH  
  10 3M96 ...      LDX   1  TOTALTIME(2) 
   8 3MNQ          BRN      PM4  
  16 3N8B    PM11  ERN   0  APCTMACH$CONSMACH   [J. IF NOT 7020. 
   8 3NN2          BNZ   0  PM10 
  21 3P7L ...      LDN   4  PCTLEN-1(3)           [J. IF NOT ENOUGH ROOM FOR 7020 ENT
  11 3PM=          BXGE  4  TABLELIMIT,PM10  
  20 3Q6W ...      LDX   0  PCTTWMASK(2)           [DEFAULT FOR TW IS INTERFACE 4
  16 3QLG          STO   0  XOPMASK(3)          [ON ALL DROPS.   
  10 3R66          STO   0  XOPMASK+1(3) 
  10 3RKQ          STO   0  XOPMASK+2(3) 
  21 3S5B ...      LDX   0  OUTENQ(2)              [SET UP ENQ BUFFER FOR POLL & SELE
   9 3SK2          STO   0  XENQ(3)  
  10 3T4L ...      LDX   0  OUTENQ+1(2)  
   9 3TJ=          STO   0  XENQ+1(3)
  19 3W3W          STOZ     XDROPPTR(3)         [START POLLING AT ADDRESS 0. 
  18 3WHG          LDX   0  JTIME               [REMEMBER CURRENT TIME.  
   9 3X36          STO   0  XTIME(3) 
  21 3XGQ ...      LDN   0  NEXTDROP(2)            [LINK FOR PCT WHEN NO INTERFACE SE
  17 3Y2B          BZE   1  PM5                 [J. IF NOT RINGED.   
   7 3Y=8 ...      SRC   5  6
  21 3YG2          BNG   5  PM5                 [J. IF RINGED BUT ATTACHED (ONLINE). 
  15 3Y_L          SBN   1  A1                  [PTR. TO LCB.
  15 3_F=          PMCORES  1,PM5               [J. IF NOT   
  19 3_YW          LDEX  0  GENQ1(1)            [GET LAST ENQ ADDRESS SENT.  
   9 42DG ...      ORX   0  OUTENQ(2)
  18 42Y6          BXU   0  GENQ1(1),PM12       [J. IF DOESN'T LOOK VALID
  17 43CQ          DEX   0  XENQ(3)             [OTHERWISE SAVE IT.  
  20 43XB          LDX   0  GTIM(1)             [SAVE TIME PERI WAS INITIATED.   
  19 44C2          BNG   0  PM12                [IF STILL (POSSIBLY) ACTIVE. 
   9 44WL          STO   0  XTIME(3) 
   9 45B=    PM12  LDX   1  BPTR(1)  
  20 45TW          PMCORES  1,PM18              [J. IF CAN'T FIND INFORM BLOCK.  
   9 46*G          LDX   0  ATYPE(1) 
  10 46T6 ...      BXGE  0  XCACT(2),PM18
  10 47#Q ...      SBX   0  PINFORM(2)   
   8 47SB          BNZ   0  PM12 
  19 48#2          LDN   4  APCTRLEN            [CHECK INFORM BLOCK LOOKS OK.
  20 48RL          LDN   5  APCTSTAT-A1+1       [I.E. ALOGL CORRESPONDS TO NO. OF
  21 49?=          MPA   4  APCTNUM(1)          [DROPS RECORDED IN APCTNUM, & LAST   
  15 49QW          BXU   5  ALOGLEN(1),PM17     [ENTRY = -1. 
   9 4==G          PMCORES  5,PM17   
   7 4=Q6          SMO      5
   9 4?9Q          NGX   0  A1-1(1)  
   8 4?PB          BCT   0  PM17 
  20 4#92          LDX   5  APCTNUM(1)          [LOOK THROUGH EACH DEFINED DROP. 
  10 4#NL ...      STO   3  WORKWORD(2)  
  21 4*8=    PM13  LDCH  4  APCTX(1)            [J. UNLESS CONSOLE OUTPUT INTERFACE  
  15 4*MW          ERN   4  #10                 [IS NO. 2.   
   8 4B7G          BNZ   4  PM16 
  20 4BM6          LDX   0  APCTHADD(1)         [IS SO FIND TERMINAL ADDRESS NO. 
   8 4C6Q          LDN   3  12   
  12 4CLB    PM14  BXE   0  PCTADDRESS-1(3),PM15 
   8 4D62          BCT   3  PM14 
  15 4DKL          BRN      PM16                [IF POSSIBLE 
  20 4F5=    PM15  SBN   3  1                   [AND OVERWRITE DEFAULT SETTING.  
   8 4FJW          LDN   4  #10  
   7 4G4G          SRC   3  2
  10 4GJ6 ...      SMO      WORKWORD(2)  
  10 4H3Q          DCH   0  XOPMASK(3)   
   9 4HHB    PM16  ADN   1  APCTRLEN 
  17 4J32          BCT   5  PM13                [J. FOR NEXT DROP.   
  10 4JGL ...      LDX   3  WORKWORD(2)  
  16 4K2= ...PM17  LDN   4  PCTLEN-1(3)           [RESET POINTER 
  18 4KFW ...PM18  LDN   0  PCTACTIVE(2)           [AND LINK FOR STORING.
   8 4K_G          BRN      PM5  
   4 4LF6    #   
   8 4LQG ...START TXU   3  FXPM1
   8 4M3W ...      BCC      PMDIS
   8 4M*= ...      LDX   1  FXPM1
  19 4MLL ...      LDX   0  OUTEND(1)              [SET UP TP OUTPUT BUFFER. 
   8 4N36 ...      LDX   2  FXPM1
  16 4NCL          LDX   4  JTIME               [SET START TIME. 
  10 4NX= ...      LDN   6  OUTTP+1(1)   
  10 4PBW ...      LDN   7  OUTTP+2(1)   
   9 4PWG ...      STO   0  OUTTPX(1)
   8 4QB6          MOVE  6  29   
  19 4QTQ ...      SMO      YFX2(1)                [COMPLETE ACTIVITY LIST.  
   8 4R*B ...      STO   2  XFPTR
  10 4RT2 ...      STO   4  TOTALTIME(1) 
  18 4S#L          BRN      XC3                 [ENTER THE TIME SHARER.  
   4 4SS=    #   
  19 4T?W ...#SKI  0?-PCTDUSTBIN-32&#40000000[PCTDUSTBIN - OVERWRITES K1MOPPM
  20 4TRG ...#GAP  PCTDUSTBIN+32-0?       [IF WE HAVE TO READ IN ANY GASH BLOCK  
   4 4W?6    #   
   7 4WQQ    PMDIS LDN   1  2
   8 4X=B    PMDS1 LDN   7  9(1) 
  11 4XQ2          FINDPEREC 2,APFOTY,7,PMDS2
   8 4Y9L          BNG   2  PMDS3
  16 4YP=          JWRONG   2,PMDS2             [J. IF WRONGED.  
  21 4_8W          DISENGPE 2                   [DIS EXTRACODE: CLEARS EVERYTHING UP.
   8 4_NG          BRN      PMDS2
   8 5286    PMDS3 BCT   1  PMDS1
   9 52MQ ...      BRN      (JPMSEG) 
   4 537B    #   
   4 53M2    #   
   6 546L    MODE40004   
   8 54BD ...      SMO      FXPM1
   8 54L=          LDX   1  MODEX
  15 555W          BRN      MD1                     [CANCEL  
  16 55KG    MODE4 LDN   1  4                       [SUPER CANCEL
   9 5656    MD1   ADX   1  XLINE(2) 
  12 574B          DCA      XDEV(2),STO,1,MODE   
   7 57#8 ...      LDX   1  0
  17 57J2          GLPERI   XDEV(2)             [ISSUE CANCEL PERI.  
  12 583L ...      BRN      (1)          [EXIT.  
   4 58H=    #   
  19 592W    OUTPUT                             [SET UP & ISSUE WRITE PERI.  
  21 59GG          DLCA     3,DLA,0,COUNT       [X0=COUNT, X5=BUFFER START ADDRESS,  
  14 5=26          DLCA     3,STO,1,CTSA        [X7=LINK.
   8 5=FQ          LDN   0  #11  
  10 5=_B          DLCA     3,DLA,0,MODE 
   8 5?F2          BRN      PERI 
   4 5?YL    #   
  20 5#D=    OUTIN DLCA     3,DLA,0,MODE        [SET UP & ISSUE WRITE/READ PERI. 
  21 5#XW          DLCA     3,DLA,1,COUNT       [X0=WRITE MODE, X1=COUNT, X5=OUTPUT  
  20 5*CG          DLCA     3,STO,5,CTSA        [BUFFER START ADDRESS, X7=LINK.  
  10 5*X6          LDN   0  XSTATUS(2)   
   7 5BBQ          LDN   1  2
   8 5BWB          LDN   5  80   
   4 5CB2    #   
  21 5CTL    READB DLCA     3,STO,0,CTSA2       [FINISH SETTING UP & ISSUE DOUBLE PER
  21 5D*=          DLCA     3,DLA,1,MODE2       [X0=START ADDRESS FOR 2ND. TRANSFER, 
  21 5DSW          DLCA     3,DLA,5,COUNT2      [X1=MODE FOR 2ND. TRANSFER, X5=COUNT,
  14 5F#G          LDN   0  XREP2(2)            [X7=LINK.
  10 5FS6          DLCA     3,STO,0,CREP2
   4 5G?Q    #   
   9 5GRB    PERI  LDN   0  XREP1(2) 
  10 5H?2          DLCA     3,STO,0,CREP 
   9 5HQL          LDX   0  XLINE(2) 
  12 5J==          DCA      XDEV(2),STO,0,MODE   
   9 5JPW          GLPERI   XDEV(2)  
   4 5K9G    #   
  21 5KP6          LDX   0  JTIME               [REMEMBER WHEN TRANSFER(S) INITIATED.
   9 5L8Q          STO   0  XTIME(2) 
  20 5LNB    WAIT  COORDINATE                   [LET OTHER ACTIVITIES HAVE A GO. 
   4 5M82    #   
  21 5MML          LDXC  1  XREP1(2)            [J. UNLESS (1ST.) TRANSFER FINISHED. 
   9 5N7=          BCS      WAITCHECK
  16 5NLW          ADXC  1  1                   [J. IF TIMED-OUT,
   8 5P6G          BCS      SCRUB
  16 5PL6          SLC   1  4                   [OR INOPERABLE   
   8 5Q5Q          BPZ   1  SCRUB
   7 5QKB          ANDN  1  3
   7 5R52          EXIT  6  0
   4 5RJL    #   
   6 5S4=    WAITCHECK   
  21 5SHW          CALL  1  TIMECHECK           [CHECK WHETHER TRANSFER HAS LASTED   
  19 5T3G          BRN      WAIT                [FOR TOO LONG - J. IF NOT.   
   4 5TH6    #   
  19 5W2Q    SCRUB JLUNBUSY 3,(7)               [OTHERWISE EXIT IF LINE IDLE,
  21 5WGB          CALL  0  MODE4               [OTHERWISE CRUNCH ANY TRANSFERS STILL
  18 5X22          EXIT  7  0                   [OUTSTANDING THEN EXIT.  
   4 5XFL    #   
   5 5X_=    READWAIT
  18 5YDW          LDXC  0  XREP2(2)            [J. IF READ STILL BUSY.  
   9 5YYG          BCS      WAITCHECK
  16 5_D6          ADXC  0  0                   [J. IF TIMED-OUT,
   8 5_XQ          BCS      (7)  
  16 62CB          SLC   0  4                   [OR INOPERABLE.  
   8 62X2          BPZ   0  (7)  
   7 63BL          ANDN  0  3
  18 63W=          SRC   0  1                   [J. IF READ IMPLEMENTED. 
   8 64*W          BPZ   0  (5)  
  20 64TG          EXIT  5  1                   [2ND. TRANSFER NOT IMPLEMENTED.  
   4 65*6    #   
  20 65SQ    TIMECHECK                          [CHECK WHETHER LINE HAS BEEN BUSY
  18 66#B          LDX   0  JTIME               [FOR TOO LONG. X1=LINK.  
   9 66S2          SBX   0  XTIME(2) 
  20 67?L          BPZ   0  TODAY               [J. UNLESS WRAPPED ROUND THROUGH 
   8 67HD ...      SMO      FXPM1
  15 67R=          ADX   0  ONEDAY              [MIDNIGHT.   
   8 685M ...TODAY SMO      FXPM1
  16 68D4 ...      SBXC  0  K50MOPPM            [J. IF STILL O.K.
   8 68QG          BCS      (1)  
  19 69=6          EXIT  1  1                   [HAVE TO CRUNCH TRANSFER(S). 
   4 69PQ    #   
  21 6=9B    SENDENQ                            [POLL, SELECT OR SEND DATA. X7=LINK, 
  21 6=P2          CALL  6  OUTIN               [X0=WRITE MODE, X1=COUNT, X5=OUTPUT  
  18 6?8L          BZE   1  NERR1               [BUFFER START ADDRESS.   
  21 6?N=          CALL  0  MODE40004           [EXIT 0 IF TIMED-OUT OR INOPERABLE,  
  21 6#7W    NERR1 CALL  5  READWAIT            [EXIT 1 IF WRITE FAILED &/OR READ WAS
  21 6#MG          BRN      NERR2               [NOT IMPLEMENTED, OR NO CHARS. READ I
  20 6*76    XIT71 EXIT  7  1                   [OTHERWISE EXIT 2, WITH ERROR BIT
  16 6*LQ    NERR2 LDEX  5  XREP2(2)            [IN B23 OF X0.   
   8 6B6B          SBNC  5  80   
   8 6BL2          BCC      XIT71
   7 6C5L          EXIT  7  2
   4 6CK=    #   
  21 6D4W    READGASH                           [READ BLOCKS FROM SOURCE INTERFACE.  
  21 6DJG          LDN   4  PMERRCNT            [X7=LINK, EXIT WHEN TIMED-OUT, INOPER
  21 6F46    READ1 LDN   0  XSTATUS(2)          [(INCL. MULTIPLE ERRORS), OR EOT SEEN
   8 6FHQ          LDN   1  #42  
   8 6G3B          LDN   5  80   
  20 6GH2          DLCA     3,STO,0,CTSA        [SET UP 1ST. PART OF DOUBLE READ.
  17 6H2L          DLCA     3,DLA,1,MODE        [READ WITH ^WARNING^.
  10 6HG=          DLCA     3,DLA,5,COUNT
   8 6HQ4 ...      SMO      FXPM1
  10 6H_W          LDN   0  PCTDUSTBIN   
   8 6JFG          LDN   1  #22  
   8 6J_6          LDN   5  128  
   8 6KDQ          CALL  6  READB
  20 6KYB          CALL  5  READWAIT            [WAIT FOR 2ND. PART TO FINISH.   
  21 6LD2          BRN      READ2               [LONG BLOCK - J. TO CHECK FOR ERROR. 
  21 6LXL          BNZ   1  READ3               [SHORT BLOCK - J. IF ERROR OR NOT IMP
  19 6MC=          LDX   0  XSTATUS(2)          [GET 1ST. 2 CHARS. READ IN.  
   8 6MWW          SRL   0  12   
  20 6NBG          SBN   0  #7624               [READ AGAIN UNLESS EOT RECEIVED. 
   9 6NW6          BNZ   0  READGASH 
   7 6P*Q          EXIT  7  0
   4 6PTB    #   
  18 6Q*2    READ2 ANDN  0  1                   [J. TO READ ANOTHER BLOCK
  17 6QSL          BZE   0  READGASH            [UNLESS ERROR SEEN.  
  18 6R#=    READ3 BCT   4  READ1               [DO WE HAVE ANOTHER GO?  
  16 6RRW          EXIT  7  0                   [NO - GIVE UP.   
   4 6S?G    #   
  21 6SR6    TERMSTATUS                         [DECODE STATUS REPLY TO POLL OR SELEC
   7 6T=Q          LDX   1  2
  10 6TQB          LDCH  5  XSTATUS(2)   
   8 6W28 ...      SMO      FXPM1
  20 6W=2          BXL   5  XALPHA,STAT1        [J. IF ALPHA TO SET UP ALPHA PAIR
  21 6WPL          SLL   5  6                   [OTHERWISE USE 1ST. 2 CHARS IN BUFFER
   8 6X9=          BCHX  1  STAT2
   8 6XNW    STAT1 LDN   5  #7400
  10 6Y8G    STAT2 LDCH  6  XSTATUS(1)   
   7 6YN6          ORX   5  6
  21 6_7Q          LDX   6  XENQ(2)             [CHECK AGAINST ADDRESS SENT IN ENQ.  
   8 6_MB          ANDN  6  -1   
  19 7272          BXU   5  6,(7)               [EXIT 7 0 IF WRONG ADDRESS.  
   7 72LL          BCHX  1  /
  15 736=          SRL   6  6                   [SAVE SHIFT. 
  19 73KW          LDCH  5  XSTATUS(1)          [GET 1ST. STATUS CHARACTER.  
  17 745G          ADN   7  1                   [STEP LINK ON BY 1.  
   8 74*# ...      SMO      FXPM1
  19 74K6          BXL   5  XALPHA,STAT5        [J. IF NO CHANGE IN SHIFT.   
  16 754Q          LDX   6  5                   [SAVE NEW SHIFT. 
  20 75JB          BCHX  1  STAT5               [J. TO GET 2ND. STATUS CHARACTER.
   4 7642    #   
  21 76HL    PRIPHSTATUS                        [DECODE STATUS REPLY IN DATA PHASE.  
  19 773=          LDEX  1  XREP2(2)            [GET STATUS READ REPLY COUNT.
  21 77GW          SBN   1  79                  [GET POINTER TO LAST 2 CHARS. READ IN
  17 782G          BNG   1  STAT4               [J. UNLESS ONLY 1.   
  16 78G6          LDCH  5  XSTATUS(2)          [MUST BE ALPHA.  
  20 78_Q    STAT3 SBN   5  #40                 [CONVERT ALPHA SHIFT STATUS CHAR.
   7 79FB          EXIT  7  0
   8 79RR ...STAT4 SMO      FXPM1
   8 7=68 ...      ERX   1  XMIN1
   7 7=DL          SRC   1  2
   7 7=Y=          ADX   1  2
  21 7?CW          LDCH  6  XSTATUS(1)          [GET PENULTIMATE CHAR. - MAY BE SHIFT
  21 7?XG          BCHX  1  STAT5               [IF NOT THEN DEFAULT GIVES ALPHA ANYH
   4 7#C6    #   
  21 7#WQ    STAT5 LDCH  5  XSTATUS(1)          [GET LAST CHAR - GIVES ACTUAL STATUS.
  17 7*BB          SBNC  6  #75                 [J. IF ALPHA SHIFT.  
   8 7*W2          BCS      STAT3
  21 7B*L          BZE   6  (7)                 [J. IF BETA SHIFT - DO NO CONVERSION.
  20 7BT=          SBN   5  #30                 [MUST BE DELTA SHIFT : CONVERSION
   8 7C54 ...      SMO      FXPM1
  20 7C#W          BXL   5  XDELMAX,(7)         [RULE SAY SUBTRACT #30 IF < #70  
  16 7CSG          ADN   5  #34                 [ADD 4 OTHERWISE.
   7 7D#6          EXIT  7  0
   4 7DRQ    #   
   4 7F?B    #   
   9 7FR2    MOP   JLUNBUSY 3,MOP1   
  16 7G=L          CALL  0  MODE4               [BASH IT IF BUSY 
   9 7GQ=    MOP1  LDN   4  PMERRCNT 
  17 7H9W    MOP2  LDN   0  55                  [SEND VALEDICTION.   
   8 7HFN ...      SMO      FXPM1
   9 7HPG          LDN   1  OUTMOP   
   8 7H_# ...      SMO      FXPM1
  20 7J96          LDN   7  SUICIDE             [LINK TO SUICIDE IF INOPERABLE.  
   9 7JNQ          CALL  6  OUTPUT   
  21 7K8B          BZE   1  SUICIDE             [J. IF IMPLEMENTED & NOT ERRORED: END
  18 7KN2          BCT   4  MOP2                [J. TO REPEAT TRANSFER   
  17 7L7L    SUICIDE                             [ACTIVITY FINISHED  
   8 7LJL ...      LDX   1  FXPM1
  19 7L_L ...      SMO      XFX2(1)                [J. TO END IF THIS WAS THE
  16 7MBL ...      LDX   2  XFPTR               [LAST ACTIVITY,  
  10 7MRL ...      BXE   2  XFX2(1),PMDIS
  20 7N8L ...      SMO      YFX2(1)                [OTHERWISE JUST EXCLUDE THIS  
  20 7NKQ          STO   2  XFPTR               [ACTIVITY FROM THE ACTIVITY LIST.
  19 7P5B          BRN      XC1                 [RE-ENTER THE TIME SHARER.   
  15 7PK2    XCOOR                              [CO-ORDINATOR
   8 7PXC ...      LDX   1  FXPM1
   9 7Q9S ...      LDX   2  XFX2(1)  
  15 7QJ=          STO   0  XLINK(2)            [DUMP LINK.  
  21 7R3W ...      STO   2  YFX2(1)                [REMEMBER THIS AS LAST ACTIVE ACTI
  15 7RHG          LDN   0  3                   [AND X3-X7   
   8 7S36          LDN   1  X3(2)
   7 7SGQ          MOVE  0  5
  21 7T2B          LDX   2  XFPTR(2)            [MOVE ON TO THE NEXT ACTIVITY ON LIST
   8 7T=8 ...      LDX   1  FXPM1
   8 7TG2    XC1   LDX   0  JTIME
  10 7T_L ...      SBX   0  TOTALTIME(1) 
   8 7WF=          BPZ   0  XC2  
   9 7WYW ...      ADX   0  ONEDAY(1)
  10 7XDG ...XC2   SBX   0  K51MOPPM(1)  
  19 7XY6          BPZ   0  PMDIS               [J. IF 2 MINUTES HAVE ELAPSED
   9 7YCQ ...XC3   STO   2  XFX2(1)  
   8 7YXB          LDN   0  X3(2)
   7 7_C2          LDN   1  3
  16 7_WL          MOVE  0  5                   [RESTORE X3-X7   
   9 82B=          LDX   0  XLINK(2) 
  15 82TW          EXIT  0  0                   [JUMP TO LINK
   4 83*G    #   
   4 83T6    #   
   7 84#Q    PCT0  COORDINATE
  19 84SB    PCTACTIVE                          [ENTRY IF TERMINAL IS ACTIVE 
  18 85#2          JLUNBUSY 3,PCT1              [J. IF NO BUSY TRANSFERS.
  20 85RL          CALL  1  TIMECHECK           [WAIT FOR TRANSFER(S) TO FINISH. 
   8 86?=          BRN      PCT0 
  18 86QW          CALL  0  MODE4               [UNTERMINATED - SCRUB IT.
  15 87=G          BRN      NEXTDROP            [J. TO POLL. 
  19 87Q6    PCT1  LDX   6  XENQ(2)             [GET LAST ADDRESS SENT OUT.  
   7 889Q          ANDN  6  7
  21 88PB          BZE   6  NEXTDROP            [IF ZERO MUST HAVE BEEN POLL - QUIESC
   7 8992          ANDN  6  1
  21 89NL          BZE   6  SENDING             [IF EVEN MUST HAVE BEEN DOING OUTPUT,
  19 8=8=    #                                  [OTHERWISE INPUT IS SELECTED.
   8 8=D4 ...      LDX   1  FXPM1
  20 8=MW          LDX   4  JTIME               [SET XTIME SO THAT WE WAIT FOR   
  21 8?7G ...      ADX   4  K50MOPPM(1)            [AT MOST 10 SECS. TO SEE WHETHER  
  20 8?M6 ...      SBX   4  TENSECONDS(1)          [TERMINAL IS SHOWING SIGNS OF 
  15 8#6Q ...      BXL   4  ONEDAY(1),SAMEDAY      [ACTIVITY.
   9 8#LB ...      SBX   4  ONEDAY(1)
   5 8*62    SAMEDAY 
   9 8*KL          STO   4  XTIME(2) 
  20 8B5=    TESTACTIVE                         [THESE CHECKS WILL ONLY CURRENTLY
  20 8BJW          LDCT  0  #200                [WORK ON WEST GORTON EXECUTIVES. 
  19 8C4G          DLCA     3,ANDX,0,FLAG       [J. IF TIMED-OUT MARKER SET. 
   9 8CJ6          BNZ   0  TWINCHECK
  21 8D3Q          LDN   6  #140                [J. IF LOST INPUT, OR READ REQUEST SE
  10 8DHB          DLCA     3,ANDX,6,FLAG
   9 8F32          BNZ   6  READING  
  19 8FGL          CALL  1  TIMECHECK           [CHECK WHETHER TO GIVE UP.   
  19 8G2=          BRN      STILLINACT          [J. TO WAIT A LITTLE LONGER. 
  21 8GFW          BRN      TWINCHECK           [GIVE UP AND POLL: EOT ALREADY RECEIV
   6 8G_G    STILLINACT  
   8 8H?X ...      LDX   1  FXPM1
  15 8HL# ...      COORDINATE TESTACTIVE(1)        [WAIT & J.
  21 8HYQ    READING                            [SOURCE INTERFACE SELECTED & SENDING.
  20 8JDB          CALL  7  READGASH            [READ DATA UNTILL EOT IS SEEN.   
   6 8JY2    TWINCHECK   
  21 8KCL          LDX   4  XENQ(2)             [J. IF INTERFACE 5 (*TR) WAS SELECTED
   7 8KX=          ANDN  4  4
   9 8LBW          BNZ   4  NEXTDROP 
  21 8LWG          LDX   7  XENQ(2)             [CONVERT TERMINAL ADDRESS (IN RANGE  
  21 8MB6          LDX   1  XENQ(2)             [#7400 TO #7570) TO TERMINAL NUMBER  
  19 8MTQ          ANDN  7  #140                [(IN RANGE 1 TO 12) MINUS 1. 
   8 8N*B          ANDN  1  #170 
   8 8NT2          BNZ   7  NLE4 
   8 8P#L          ERN   1  #20  
   8 8PS=    NLE4  ANDN  7  #100 
   7 8Q?W          SRC   1  5
   8 8QRG          BZE   7  NGE9 
   7 8R?6          ERN   1  1
  21 8RQQ    NGE9  SMO      2                   [GET TW OUTPUT MASK FOR THIS TERMINAL
  10 8S=B          LDCH  0  XOPMASK(1)   
  21 8SQ2          LDX   4  XENQ(2)             [SET UP INTERFACE BIT MASK FOR ^PAIR^
  19 8T9L          LDN   5  #40                 [OF SOURCE WHICH IS SELECTED.
  19 8TP=          ANDN  4  2                   [TW INPUT INTERFACE IS 1 OR 3
  19 8W8W          BNZ   4  TWIS4               [J. IF INTERFACE 3 SELECTED. 
  18 8WNG          LDN   5  #10                 [INTERFACE 1 SELECTED.   
  21 8X86    TWIS4 ANDX  5  0                   [IF NON ZERO THEN TW INPUT MUST HAVE 
  16 8XMQ          BZE   5  NEXTDROP            [BEEN SELECTED   
  21 8Y7B          SRL   5  1                   [SO SET MARKER SO WE OUTPUT ^CANCEL^.
   7 8YM2          ORX   0  5
  10 8_6L          BRN      SETOPMASK2   
   4 8_L=    #   
  19 925W    SENDING                            [ACCEPTOR INTERFACE SELECTED.
   8 92*N ...      LDX   1  FXPM1
  19 92KG          LDX   0  XENQ(3)             [CHECK WHETHER LP, TW OR TP. 
   7 9356          ORN   0  6
  21 93JQ ...      LDN   5  OUTLP(1)               [IF TW OR LP SEND FORM FEED TO CLE
  18 944B          BXU   0  XENQ(2),NOTTP       [UP SO WE CAN SEND EOT.  
  18 94J2 ...      LDN   5  OUTTP(1)               [IF TP SEND DELETES.  
   7 953L    NOTTP LDN   0  1
   8 95H=          LDN   1  128  
  18 962W          CALL  7  SENDENQ             [OUTPUT CLEAN UP MESSAGE.
  18 96GG          BRN      NEXTDROP            [LINE INOPERABLE - POLL. 
  17 9726          BRN      SENDING             [NOT IMP. - REPEAT.  
  17 97FQ          CALL  7  PRIPHSTATUS         [GET STATUS REPLIED. 
   7 97_B          ANDN  5  1
  18 98F2          BZE   5  NEXTDROP            [J. IF DEVICE INOPERABLE.
  15 98YL          BZE   0  NEXTDROP            [J. IF O.K.  
  16 99D=          BRN      SENDING             [ERROR - REPEAT. 
   4 99XW    #   
   5 9=CG    POLLERR 
  20 9=X6          BCT   4  POLL                [POLL AGAIN IF NOT COUNTED OUT.  
   5 9?BQ    PCTINOP 
  21 9?WB          LDN   0  0                   [SET INOPERABLE MARKER FOR THIS DROP.
   6 9#B2    SETOPMASK1  
  10 9#TL          LDX   1  XDROPPTR(2)  
   6 9**=    SETOPMASK2  
   7 9*SW          SMO      2
  19 9B#G          DCH   0  XOPMASK(1)          [SET STATUS FOR THIS DROP.   
   8 9BS6          BCHX  1  NDRP3
  21 9C?Q    NDRP1 BXU   1  XDROPPTR(2),NDRP2   [GET NEXT DROP, J. UNLESS BEEN RIGHT 
  19 9CRB          LDN   6  #7620               [ROUND & NOTHING LEFT TO DO. 
  17 9D?2          LDN   0  #11                 [SEND EOT & FINISH.  
   7 9DQL          LDN   1  7
   9 9F==          LDN   5  XENQ(2)  
   9 9FPW          DSA   6  XENQ(2)  
   9 9G9G          CALL  7  SENDENQ  
   9 9GP6          BRN      SUICIDE  
   9 9H8Q          BRN      SUICIDE  
   9 9HNB          BRN      SUICIDE  
   4 9J82    #   
   5 9JML    SOPERR  
  21 9K7=          BCT   4  SOP2                [ERROR ON SENDING DATA - POLL IF TOO 
  19 9KLW    NEXTDROP                           [STEP ON TO THE NEXT DROP.   
   4 9L6G    #   
  10 9LL6          LDX   1  XDROPPTR(2)  
   7 9M5Q    NDRP2 BCHX  1  /
   7 9MKB    NDRP3 SBN   1  3
   8 9N52          BZE   1  NDRP4
   7 9NJL          ADN   1  3
  19 9P4=    NDRP4 SMO      2                   [PICK UP THE ^ACTIVE^ SWITCH.
  10 9PHW          LDCH  0  XOPMASK(1)   
  19 9Q3G          BZE   0  NDRP1               [J. IF MARKED AS INOPERABLE. 
  17 9QH6          STO   1  XDROPPTR(2)         [REMEMBER THIS DROP. 
   7 9R2Q          SLC   1  2
   7 9R=J ...      SMO      2
  21 9RGB          LDX   0  PCTADDRESS(1)       [SET UP TERMINAL ADDRESS FOR THIS DRO
   9 9S22          DSA   0  XENQ(2)  
   9 9SFL          LDN   4  PMERRCNT 
  20 9S_=    POLL  LDN   0  #11                 [SEND POLL MESSAGE FOR THIS DROP.
   7 9TDW          LDN   1  7
   9 9TYG          LDN   5  XENQ(2)  
   9 9WD6          CALL  7  SENDENQ  
  14 9WXQ          BRN      PCTINOP             [INOP.   
  16 9XCB          BRN      POLLERR             [NOT IMP. ) TRY  
  17 9XX2          BNZ   0  POLLERR             [ERROR    )  AGAIN?  
  17 9YBL          CALL  7  TERMSTATUS          [GET TERMINAL STATUS.
  20 9YW=          BRN      POLLERR             [ERROR IN STATUS BUFFER CONTENTS.
  21 9_*W          BZE   5  NEXTDROP            [J. FOR NEXT DROP IF WAITING FOR TW I
  10 9_TG          LDX   1  XDROPPTR(2)  
   7 =2*6          SMO      2
  21 =2SQ          LDCH  6  XOPMASK(1)          [GET TW INPUT/OUTPUT INTERFACE MASK. 
   7 =3#B          LDX   0  6
  18 =3S2          ANDN  0  #50                 [TW OUTPUT INTERFACE BIT.
   7 =4?L          ANDX  0  5
  19 =4R=          BNZ   0  SELECTOUTPT         [J. IF TW OUTPUT OPERABLE.   
  15 =5=W          ANDN  6  #50                 [TW OUTPUT.  
   7 =5QG          LDX   0  6
  15 =6=6          SRL   6  1                   [TW INPUT.   
   7 =6PQ          ANDX  6  5
  18 =79B          BNZ   6  SELECTINPUT         [J. IF TW INPUT OPERABLE 
  17 =7P2          ERN   0  #50                 [LP INTERFACE BIT.   
   7 =88L          ANDX  0  5
  21 =8N=          BZE   0  SETOPMASK2          [J. IF LP INOPERABLE - MARK DROP INOP
  20 =97W    SELECTOUTPT                        [SELECT INTERFACE & SENT GOODBYE.
  21 =9MG          ANDN  6  #24                 [REMEMBER TW INPUT SWITCH SETTING.   
   9 ==76          DEX   6  XENQ+1(2)
  18 ==LQ          ANDN  0  #40                 [GET INTERFACE NUMBER.   
   7 =?6B          LDN   5  4
   8 =?L2          BNZ   0  SOP1 
   7 =#5L          LDN   5  2
  21 =#K=    SOP1  ORS   5  XENQ(2)             [SET UP BUFFER FOR SELECTING OUTPUT. 
   9 =*4W          LDN   4  PMERRCNT 
   7 =*JG    SOP2  LDN   0  1
   7 =B46          LDN   1  7
   9 =BHQ          LDN   5  XENQ(2)  
  18 =C3B          CALL  7  SENDENQ             [SELECT OUTPUT INTERFACE.
  14 =CH2          BRN      NEXTDROP            [INOP.   
  14 =D2L          BRN      SOPERR              [NOT IMP.
  18 =DG=          CALL  7  TERMSTATUS          [GET PERIPHERAL STATUS.  
  16 =D_W          BRN      SOPERR              [RUBBISH REPLY.  
  19 =FFG          ANDN  5  1                   [J. IF DEVICE NOW INOPERABLE.
   9 =F_6          BZE   5  NEXTDROP 
   9 =GDQ          BNZ   0  SOPERR   
  19 =GYB          LDEX  6  XENQ+1(2)           [J. IF TW INPUT MARK NOT SET.
  10 =HD2          BZE   6  SENDGOODBYE  
  21 =HXL    SENDCANCEL                         [SEND ^CANCEL^ TO TYPEWRITER IF JUST 
  19 =JC=          LDN   0  1                   [RECEIVED AN INPUT MESSAGE.  
   8 =JWW          LDN   1  17   
   8 =K6N ...      SMO      FXPM1
   9 =KBG          LDN   5  OUTCAN   
   9 =KW6          CALL  7  SENDENQ  
  15 =L*Q          BRN      NEXTDROP            [LINE INOP.  
  14 =LTB          BRN      SENDCANCEL          [NOT IMP.
  18 =M*2          CALL  7  PRIPHSTATUS         [GET PERIPHERAL STATUS   
  15 =MSL          SRC   5  1                   [J. IF INOP. 
   9 =N#=          BPZ   5  NEXTDROP 
  17 =NRW          BNZ   0  SENDCANCEL          [J. IF LINE ERROR.   
  17 =P?G          ANDN  5  2                   [J. IF DEVICE ERROR. 
  10 =PR6          BZE   5  SENDCANCEL   
  21 =Q=Q          LDX   0  XENQ+1(2)           [CLEAR TW INPUT MARKER IF OUTPUT O.K.
  21 =QQB          ADXC  0  0                   [NOT PARTICULARLY INTERESTED IN V OR 
  10 =R=2          BRN      SETOPMASK1   
  21 =RPL    #                                  [SEND ^THE SYSTEM HAS CLOSED DOWN^ TO
  15 =S9=    SENDGOODBYE                        [TW OR LP.   
   7 =SNW          LDN   0  1
   8 =T8G          LDN   1  53   
   8 =TD# ...      SMO      FXPM1
   9 =TN6          LDN   5  OUTPCT   
   9 =W7Q          CALL  7  SENDENQ  
  13 =WMB          BRN      NEXTDROP            [INOP
  14 =X72          BRN      SENDGOODBYE         [NOT IMP.
  15 =XLL          CALL  7  PRIPHSTATUS         [GET STATUS. 
   7 =Y6=          SRC   5  1
  14 =YKW          BPZ   5  NEXTDROP            [INOP.   
  14 =_5G          BNZ   0  SENDGOODBYE         [ERROR.  
  14 =_K6          ANDN  5  2                   [ERROR   
  10 ?24Q          BZE   5  SENDGOODBYE  
  21 ?2JB          BRN      PCTINOP             [FINISHED - MARK INOP. TO SAVE REPEAT
   4 ?342    #   
  21 ?3HL    SELECTINPUT                        [SELECT TW INPUT - HAVE TO READ FROM 
  20 ?43=          ANDN  6  #20                 [TW BEFORE OUTPUT INTERFACES WILL
  14 ?4GW          LDN   5  3                   [RESPOND.
   8 ?52G          BNZ   6  SINP1
  21 ?5G6          LDN   5  1                   [SET TW INPUT ADDRESS IN ENQ BUFFER. 
   9 ?5_Q    SINP1 LDN   4  PMERRCNT 
   9 ?6FB          ORS   5  XENQ(2)  
   7 ?6_2    SINP2 LDN   0  7
   9 ?7DL          LDN   1  XENQ(2)  
   8 ?7ND ...      SMO      FXPM1
  18 ?7Y=          LDN   7  NEXTDROP            [REPOLL IF IT GOES INOP. 
  18 ?8CW          CALL  6  OUTPUT              [SEND SELECTION MESSAGE. 
  17 ?8XG          BZE   1  READING             [J. IF WENT OUT O.K. 
  16 ?9C6          BCT   4  SINP2               [J. TO TRY AGAIN 
  19 ?9WQ          BRN      NEXTDROP            [OR NOT, AS THE CASE MAY BE. 
   4 ?=BB    #   
   4 ?=W2    #   
   7 ??*L    #SKI  ADEVELOP  
   5 ??T=    #GAP  20
   4 ?##W    #   
  16 ?#SG    #                                  [ACTIVITY FORMAT.
   5 ?*#6    TABLE   
   8 ?*RQ    XFPTR          +0   
   8 ?B?B    XLINK          +0   
   8 ?BR2    X3             +0   
   8 ?C=L    X4             +0   
   8 ?CQ=    X5             +0   
   8 ?D9W    X6             +0   
   8 ?DPG    X7             +0   
   8 ?F96    XREP1          +0   
   8 ?FNQ    XREP2          +0   
   8 ?G8B    XDEV           +0   
   8 ?GN2    XLINE          +0   
   8 ?H7L    XTIME          +0   
   8 ?HM=    #DEF  MOPLEN=XTIME+1
   8 ?J6W    XDROPPTR       +0   
   9 ?JLG    XOPMASK        +0,+0,+0 
   8 ?K66    XENQ           +0,+0
   8 ?KKQ    XSTATUS        +0   
   9 ?L5B    #DEF  PCTLEN=XSTATUS+21 
   4 ?LK2    #END
   8 ____ ...23456667001900000000
