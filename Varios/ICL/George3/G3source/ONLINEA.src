  15 22FL    #SEG  ONLINEA8                     [A C PUTMAN  
  16 22LS ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983.   
   8 22S3    #OPT  K0ONLINEA=0   
  14 236D    #LIS  K0ONLINEA>K0ONLINE>K0COMMAND>K0ALLGEO 
   7 23DW          8HONLINEA 
   4 23YG    #   
  12 24D6          SEGENTRY K1ONLINEA,SK1ONLINEA 
   4 24XQ    #   
  16 25CB    TBL1           #00010100           [TABLE OF BRANCH 
  19 25X2                   #01020303           [MODIFIERS - TYPE DEPENDENT  
   9 26BL                   #05120404
   9 26W=                   #02030506
   9 27*W                   #06060606
   9 27TG                   #05050505
   9 28*6                   #10120404
   9 28SQ                   #11060606
   9 29#B                   #06060606
   9 29S2                   #06060606
   9 2=?L                   #06060606
   9 2=R=                   #06060606
   7 2=YD    #SKI  JSKI19<1$1
   4 2?5L    (   
   9 2?=W                   #06060606
   9 2?QG                   #06050505
   4 2?SP    )   
   6 2?WY    #SKI  JSKI19
   4 2?_7    (   
   9 2#3B                   #06061314
   9 2#5K                   #14050505
   4 2#7S    )   
   9 2#=6                   #05050605
   9 2#PQ                   #07060606
   8 2*9B    TBL2  BRN      SI   
   8 2*P2          BRN      SO   
   8 2B8L          BRN      MT   
   8 2BN=          BRN      XBS  
   8 2C7W          BRN      MX   
  20 2CMG          BRN      XOTIC               [BRANCH FOR EXOTIC PERIPHERALS   
  21 2D76          BRN      XOTIC               [^SPECIAL^ PERI. : TREAT AS EXOTIC   
   8 2DLQ          BRN      OLCI 
   8 2F6B          BRN      XIPB 
   8 2FL2          BRN      XIPC 
   8 2HJG          BRN      XDR  
   6 2HM5    #SKI  JSKI19
   4 2HPN    (   
   8 2HS?          BRN      XFH  
   8 2HWW          BRN      XFH  
   4 2H_F    )   
   4 2H_P    #   
   7 2H__    #SKI  JWPHASE4  
   4 2J29    (   
   5 2J2F    XONLINE 
   8 2J2P          SBX   5  FX1  
  18 2J2_          HLSXONCH XBI,AWORK1(2),SLHON [TELL HLS OF TYPE/UNIT NO
   8 2J39          ADX   5  FX1  
   7 2J3F          EXIT  5  0
   4 2J3P    )   
   4 2J46    #   
  21 2JHQ    #     ANALYSE PARAMETERS , GIVE COMMAND ERROR IF INVALID . GO TO ROUTINE
  21 2K3B    #     APPROPRIATE TO PERIPHERAL TYPE ; SLOW PERIPHERALS DEALT WITH HERE 
   4 2KH2    #   
   6 2L2L    SK1ONLINEA  
   6 2L7S    #UNS  ASI   
  19 2L*2          SINMAC3  OL                  [MACRO FOR SECURITY INTERFACE
   7 2LG=          LDN   4  1
   8 2L_W          LDCT  5  #320 
  18 2MFG          PARABEG  1,4                 [GET PERIPHERAL PARAMETER
  10 2M_6          MHUNT    3,CPB,CUNI   
   9 2NDQ          LDX   4  ANUM(3)  
  18 2NYB          BNG   4  OUTP                [BRANCH IF NO PARAMETER  
   9 2PD2          LDX   4  ATYPE(3) 
  17 2PXL          ANDN  4  #7777               [ISOLATE PARAM. NO.  
   7 2QC=          SBN   4  1
  19 2QWW          BNZ   4  PERR                [PARAM. ERROR IF NOT 1ST PAR.
  19 2RBG          PERCON                       [CONVERT PERIPHERAL PARAM.   
  17 2RW6          TESTREP  PARERR,XIT          [ERROR IN PERI PARAM 
   9 2S*Q          MFREE    CPB,CUNI 
  18 2STB          MHUNTW   3,CPAR,GPERCON      [GET PERCON REPLY BLOCK. 
  16 2T*2          LDX   3  A1(3)               [TYPE / UNIT NO. 
   9 2TSL          STO   3  AWORK1(2)
  20 2W#=          RELPERENT 3                  [RELEASE REPIPHERAL IF NECESSARY 
  16 2WRW          SRL   3  15                  [ISOLATE TYPE.   
  20 2X?G          SRC   3  2                   [MAKE TYPE A CHARACTER MODIFIER  
   7 2XR6          SMO      1
  21 2Y=Q          LDCH  4  TBL1(3)             [ACCESS THE RELEVANT BRANCH MODIFIER 
  16 2YQB          SLC   3  2                   [RESTORE TYPE.   
  21 2_=2          LDX   7  4                   [0 IF SLOW INPUT ; 1 IF SLOW OUTPUT  
  13 2_PL          SMO      4                   [AND 
  21 329=          BRN      TBL2                [BRANCH TO THE NECESSARY SUBROUTINE  
   4 32NW    #   
  18 338G    MT    ACROSS   OLMTA,1             [MAGNETIC TAPE PERIPHERAL
  20 33N6    XBS   ACROSS   SOUP,1              [BRANCH FOR MARK 7 BACKING STORE 
   4 33SX    MX  
  19 34?=          ACROSS   ONLINEB,1           [MULTIPLEXORS AND UNIPLEXORS.
   4 34D3    XIPB
   9 34WB          ACROSS   ONLINEB,2
   5 3537    XOTIC   
   7 357Y    #SKI  JWPHASE4  
   9 35#P          CALL  5  XONLINE  
  17 35FG          ACROSS   ONLINEC,1           [EXOTIC PERIPHERALS  
  20 35LL    XJS   ACROSS   ONLINEC,2           [PSEUDO OFF-LINE SLOW PERIPHERALS
  16 366=    OLCI  ACROSS   ONLINEC,3           [COMMAND ISSUER  
   4 36?L    XIPC
   7 36F2    #SKI  JWPHASE4  
   9 36LB          CALL  5  XONLINE  
  19 36RQ          ACROSS   IPCOPENB,1          [INTER PROGRAM COMMUNICATOR  
   4 36_6    XDR 
   7 376G    #SKI  JWPHASE4  
   9 37?W          CALL  5  XONLINE  
   9 37F=          ACROSS   DRUM,4   
   6 37LT    #SKI  JSKI19
   4 37NJ    (   
   4 37Q?    #   
  19 37S2    #     ONLINE FILE READER,WRITER,HANDLER : SET UP X7 FOR ONLINEC 
   4 37TP    #   
   8 37XD    XFH   SBN   7  #13  
   8 37_7          BRN      XJS  
   4 382W    )   
   4 384Q    #   
  18 38JB    #     ONLINE SLOW PERIPHERAL ; CHECK DOCUMENT NAME FORMAT   
   4 3942    #   
  21 39HL    SI    LDN   7  0                   [SET SW. FOR SLOW INPUT PERIPHERALS. 
  18 3=3=    SO    PARANUMB 6                   [SLOW OUTPUT PERIPHERALS.
   7 3=GW          SBN   6  2
  19 3?2G          BNG   6  XJS                 [NO DOCUMENT NAME SPECIFIED  
   7 3?7N    #SKI  JWPHASE4  
   9 3?#W          CALL  5  XONLINE  
  20 3?G6          BZE   7  NPRO                [DO ISPER FOR INPUT PERIPHERALS  
  19 3?_Q          STO   6  AWORK4(2)           [USED BY LISTFILE ROUTINES   
  21 3#FB          BZE   6  NOIS                [2 PARAMETERS ONLY : NO PROPERTIES   
  20 3#_2          PROPERTY NOTIS,2,,1,1        [ANALYSE PROPERTIES FOR OUTPUT   
  20 3*DL    NOTIS LDN   7  1                   [PROPERTY MAY HAWE ZEROISED X7   
   8 3*Y=          BRN      NOIS 
  20 3BCW    NPRO  LDCT  0  #200                [B1 INDICATES 'ONLINE' TO ISPER  
  19 3BXG          ORX   3  0                   [PERIPHERAL TYPE IN B18-23   
   8 3CC6          ISPER    3,XBI
  10 3CWQ          TESTREP  NOTAV,NOIS   
  16 3DBB          TESTREPNOT OK,PERNA          [BRANCH IF NOT.  
  14 3DW2    NOIS  LDN   5  0                   [SWITCH  
  10 3F*L          MFREEW   CPAR,GPERCON 
  18 3FT=          SPARANOT 2                   [OBTAIN THE DOCUMENT NAME
  10 3G#W          MHUNT    3,CPB,CUNI   
   9 3GSG          LDX   6  ANUM(3)  
  16 3H#6          BZE   6  PN1                 [DOC NAME NULL   
   8 3HRQ    #SKI  K6ONLINE>99-99
   8 3J?B          BNG   6  PN   
  18 3JR2          LDCH  4  APARA(3)            [J. IF DOC. NAME IS NOT  
  17 3K=L          SBN   4  #12                 [PREFIXED BY A COLON.
   8 3KQ=          BNZ   4  SIM  
  15 3L9W          LDN   5  2                   [PARAM. NO.  
  19 3LPG          PARALYSE #36,,5              [SPLIT DOC. NAME ABOUT ^.^.  
   7 3LWN ...#UNS ANSTOOMANY 
  11 3M3W ...      TESTREP2 TOOMANY,XTOOMANY 
  19 3M96          PARANUMB 4                   [J. IF NOT JUST TWO PARTS.   
   8 3MNQ    #SKI  K6ONLINE>99-99
   8 3N8B          BZE   4  PN   
   7 3NN2          SBN   4  2
   8 3P7L          BNZ   4  XDOCN
  15 3PM=          LDN   4  1                   [CHAR. CT.   
  14 3Q6W          LDCT  5  #120                [COLON.  
   8 3QLG          SPARABEG 1,4  
   8 3R66          BRN      PSIM2
  21 3RKQ    PSIM1 SPARANOT 2                   [2ND. PARAM. FROM CPB/CMULTI BLOCK.  
  10 3S5B          MFREE    CPB,CMULTI   
  16 3SK2          LDN   4  0                   [CLEAR SWITCH.   
  10 3T4L    PSIM2 MHUNT    3,CPB,CUNI   
  21 3TJ=    SIM   CHEKLFN2 XDOCN,XDOCN,3       [%A IS ACTUALLY GEOERR IN MOST CASES.
   8 3W3W          BZE   5  SPS  
  17 3WHG          FREECORE 3                   [FREE CPB/CUNI BLOCK.
  19 3X36          BNZ   4  PSIM1               [J. FOR 2ND. TIME THROUGH.   
  20 3XGQ          ANDN  6  #7777               [MASK OUT DOC. NAME CHARACTER CT.
   8 3Y2B    SPS   BNZ   7  NMOK 
   4 3YG2    #   
  21 3Y_L    #     INPUT : LOOK FOR DOCUMENT REQUESTED ; INFORM OPERATORS IF MISSING 
   4 3_F=    #   
  21 3_YW    Z1    FINDOC   XBI                 [FIND THE DOCUMENT: INITIALLY X7=0.  
  10 42DG          MHUNTW   3,ADATA,ADOC 
  17 42Y6          LDX   4  AWORK1(2)           [TYPE / UNIT NUMBER  
  20 43CQ          LDX   5  A1(3)               [J. IF ZERO HEADER : RECORD NOT  
  21 43XB          BZE   5  NOFD                [FOUND AND FINDOC HAS LONGSET #60.   
   9 44C2          LDX   0  A1+1(3)  
  20 44WL          SRL   0  12                  [POINTER TO PERI WDS IN DOC BLOCK
   8 45B=    #SKI  K6ONLINE>99-99
   4 45TW    (   
   7 46*G          SMO      0
  20 46T6          ERX   4  0(3)                [COMPARE WITH TYPE IN ADOC BLOCK.
   8 47#Q          SRL   4  15   
  19 47SB          BNZ   4  RONGPER             [DIFFERENT : FINDOC ERROR.   
   4 48#2    )   
   7 48RL          SMO   0   
  21 49?=          LDEX  6  1(3)                [GEOG NO. FROM 2ND PERIPHERAL WORD   
  18 49QW          MFREE    CPB,CUNI            [DOCUMENT NAME PARAMETER 
  16 4==G          FREECORE 3                   [ADATA/ADOC BLOCK
  17 4?9*          FINDPEREC 3,APGEOG,6         [POINT TO DEVICE LIST
  18 4?9Q          PERON 3                      [MARK PERIPHERAL ONLINE  
  19 4?PB          PUTPERENT AWORK1(2),3        [INSERT ONLINE ENTRY IN PRB  
   6 4#92    XIT   ENDCOM
   4 4#NL    #   
  17 4*8=    NOFD  SRL   4  15                  [GET TYPE IN B15-23  
  16 4*MW          FREECORE 3                   [ADATA/ADOC BLOCK
   8 4B7G    OUT   ISPER    4,XBI
  20 4BM6          TESTREP  OK,PAV              [PERIPHERAL NOT ALLOCATED EXISTS 
  20 4C6Q          TESTREPN NOTAV,NOP           [READERS WRONGED SINCE LAST ISPER
  21 4CLB          LONGSTOP XBI                 [WAIT FOR PERIPHERAL OF REQUIRED TYPE
  19 4D62          BRN      Z1                  [WAS EVENT REQUIRED DOCUMENT?
  11 4DKL    PAV   OUTPARAM 6,APARA,CPB,CUNI 
  21 4F5=          INFORM   1,ALODOC,0          [PLEASE LOAD DOCUMENT TO CENTRAL CONS
  16 4FJW          OUTPAUSE XBI,Z1,Z3,2,RETY    [WAIT FOR 2 MINS.
  18 4G4G          BRN      OUT                 [LONGSET STILL OPERATIVE 
   4 4GJ6    #   
  19 4H3Q    #     OUTPUT : PREPARE GOUT/INFO BLOCK FOR PASSING TO LISTPARS  
   4 4HHB    #   
  19 4J32    NMOK  LDX   5  6                   [NO. OF CHARS. IN DOC. NAME. 
  11 4JGL          ADN   5  GHEAD-A1+1*4+3   
   7 4K2=          SRL   5  2
  19 4KFW          SETUPCORE 5,2,GOUT,INFO      [OBTAIN LISTPARS CORE BLOCK  
  10 4KJF          STOZ     GOUTKEY1(2)  
  10 4KM4          LDN   3  GOUTKEY1(2)  
  10 4KPM          LDN   4  GOUTKEY2(2)  
   8 4KS=          MOVE  3  11   
   4 4KWT    [   
  16 4K_G          STO   5  A1(2)               [REC. HEADER SET 
   8 4LF6          LDCT  4  #400 
  20 4LYQ          STO   4  GMODE(2)            [MARKER TO SAY ENTRY FOR ONLINE  
  17 4MDB          MHUNT    1,CPB,CUNI          [DOCUMENT NAME BLOCK 
  16 4MY2          STO   6  GSUSE(2)            [CHARACTER CT.   
  20 4NCL          LDN   3  APARA(1)            [POINT TO START OF DOCUMENT NAME 
   9 4NX=          LDN   4  GHEAD(2) 
   7 4PBW          SMO      6
   7 4PWG          MVCH  3  0
  20 4QB6          LDX   5  2                   [SAVE POINTER TO GOUT/INFO BLOCK 
   7 4QTQ          FREECORE 1
   9 4R*B          LDX   7  AWORK1(2)
   9 4RT2          LDX   4  JOBNO(2) 
  21 4S#L          LDX   2  5                   [RESTORE POINTER TO GOUT/INFO BLOCK  
  19 4SS=          STO   7  GOPTY(2)            [PERIPHERAL TYPE / UNIT NO.  
  16 4T?W          STO   4  GJOBS(2)            [STORE JOB NUMBER
  16 4TRG          FJOCA    3,FX2               [FIND JOB BLOCK  
   7 4W?6          LDX   2  5
   9 4WQQ          LDN   4  JUSER(3) 
   9 4X=B          LDN   5  GUSER(2) 
  16 4XQ2          MOVE  4  3                   [INSERT USER NAME
   9 4Y9L          LDN   4  JNAME(3) 
   9 4YP=          LDN   5  GJOBN(2) 
  16 4_8W          MOVE  4  3                   [INSERT JOB NAME 
  19 4_NG          STOZ     GCOUNT(2)           [INITIALISE LISTFIND SWITCH  
  19 5286          ACROSS   LISTPARS,1          [OBTAIN THE OUTPUT PERIPHERAL
   4 52MQ    #   
  15 537B    #     VARIOUS COMMAND ERRORS AND GEORGE ERRORS  
   4 53M2    #   
  16 546L    PJDOCN         +JDOCN              [DOCUMENT NAME.  
  15 54L=    PAPER          +APER               [PERIPHERAL. 
   7 555W          ZERRORY   
   4 55KG    #   
   7 5656    XBI   COMBRKIN  
   4 56JQ    #   
   9 574B    XDOCN LDX   5  PJDOCN(1)
   8 57J2          CALL  1  OUT2X
  16 583L                   +ADOCER             [%C FORMAT ERROR.
   9 58H=    PN1   LDX   5  PJDOCN(1)
   8 592W    PMIS  CALL  1  OUT2X
  18 59GG                   +JPARMIS            [%C PARAMETER MISSING.   
   9 5=26    OUTP  LDX   5  PAPER(1) 
   8 5=FQ          BRN      PMIS 
   8 5=_B    OUT2X LDX   4  0(1) 
  20 5?F2          COMERRX  4,5                 [OUTPUT RELEVANT ERROR MESSAGE   
   7 5?YL    NOP   LONGOFF   
   7 5#D=    PERNA SPARANOT 1
  18 5#XW          ZERRORX  JPERNA              [PERIPHERAL NOT AVAILABLE
  19 5*CG    PERR  ZERRORX  JSEQ                [PARAMETER NOT IN SEQUENCE   
  17 5*X6    Z3    ZERRORX  JONLINE             [REQD NAME NOT FOUND 
   7 5B4#    #SKI  JWPHASE4  
  18 5B9G    SLHON ZERRORX  ERHLSONL            [HLS FORBIDS ACQUISITION 
   7 5B=B ...#UNS ANSTOOMANY 
   4 5B?= ...(   
   5 5B#6 ...XTOOMANY
   9 5B*2 ...      ZERRORX  JMAXPAR  
   4 5B*W ...)   
   4 5BBQ    #   
   8 5BWB    #SKI  K6ONLINE>99-99
   4 5CB2    (   
  20 5CTL    PN    GEOERR   1,RONGPATH          [PARAMETER MISSING AFTER PARANUMB
   5 5D*=    RONGPER 
  20 5DSW          GEOERR   1,WRONGPER          [DOCUMENT ON INCORRECT PERIPHERAL
   4 5F#G    )   
  11 5FS6          MENDAREA 10,K100ONLINEA   
   4 5G?Q    #END
   8 ____ ...45615731000100000000
