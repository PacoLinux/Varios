  14 22FL          SEG      BMSJRNL,7,GEORGE PORTER,BMAP 
  12 22_=          SEGENTRY K1BMSJRNL,Z1BMSJRNL  
  12 23DW          SEGENTRY K2BMSJRNL,ZBMCHECKRF 
  12 23NN ...      SEGENTRY K3BMSJRNL,Z3BMSJRNL  
   8 23QL ...#DEF CURREC=ASUPUSER
   9 23SJ ...#DEF ENDUSED=CURREC+1   
   9 23WG ...#DEF ENDBLOCK=ENDUSED+1 
   4 23YG    [   
   9 245N ...NSWAPID        +ASWAPID 
   4 24=W ...[   
   8 24D6    NJOURNAL       +4   
  10 24XQ                   12HJOURNAL   
   8 25CB    NSYSJ          +10  
   9 25X2                   12HSYSTEM
  10 26BL ...               12HJOURNAL   
   9 26W=                   1,1,4HC1 
   9 27*W    NLOC           12HSJFILE
   8 27TG                   1,1  
   8 28*6    NLAN           4HB1 
   5 28G# ...XJRNLINK
   9 28MG ...      LINK     BMSJRNL,3
   9 28NB ...PMSJ           4HPMSJ   
   9 28NS ...PMSQ           4HPMSQ   
   4 28P= ...[   
   4 28Q6 ...SUB1
  10 28R2 ...      MHUNTW   2,FI,FUTILITY
   7 28RW ...      EXIT  1  0
   4 28SQ    [   
   6 29#B    STEPWRITE   
   8 29S2          SBX   6  FX1  
   7 2=?L          STEPWRITE 
   8 2=R=          ADX   6  FX1  
   7 2?=W          EXIT  6  0
   4 2?QG    [   
   5 2#=6    RDELETE 
   8 2#PQ          SBX   6  FX1  
   7 2*9B          LDX   3  7
   7 2*P2          ADN   7  1
   9 2B8L          BZE   3  RDLOOP   
   6 2BN=          READ  
  10 2C7W          MHUNTW   1,FILE,FRB   
  14 2CMG          NGN   0  #100                [B0-17   
  11 2D76          ANDX  0  EDESCRIBE+A1(1)  
  17 2DLQ          BNZ   0  RNOTBLOCKS          [J IF INDEX OR TRAPS 
  10 2F6B          NAME     1,BSTB,FULLB 
   8 2FL2          FULLBPAS 1,2  
   9 2G5L          BRN      RDSKIP   
   6 2GK=    RNOTBLOCKS  
   7 2H4W          FREECORE 1
   5 2HJG    RDSKIP  
   7 2J46          SBN   3  1
   9 2JHQ          BZE   3  RDLOOP   
   8 2K3B          SKIP     ,0(3)
   5 2KH2    RDLOOP  
   6 2L2L          DELETE
   9 2LG=          BCT   7  RDLOOP   
   7 2L_W          ADX   6  1
   7 2MFG          EXIT  6  0
   4 2M_6    [   
  14 2NDQ    Z1BMSJRNL      [ENTRY TO SORT OUT SJFILES   
   4 2NYB    [   
  10 2PD2          MHUNT    2,FILE,FABSNB
   9 2PXL          ADN   1  NJOURNAL 
   8 2QC=          ADN   2  A1   
   7 2QWW          MOVE  1  4
  16 2RBG          OPEN     XBR,GENERAL,QUERY   [OPEN :JOURNAL   
  16 2RW6          TESTREP2 OK,XISTS            [J IF IT EXISTS  
  10 2S*Q          MHUNTW   2,FILE,FABSNB
   8 2STB          ADN   1  NSYSJ
   8 2T*2          ADN   2  A1   
   8 2TSL          MOVE  1  10   
   7 2W#=          CREATEB  2
   9 2WRW          ADN   2  CEUSE1   
  10 2X?G          ADN   1  NJOURNAL+1   
   7 2XR6          MOVE  1  3
  19 2Y=Q          NEWDIR   XBR                 [ATTEMPT TO CREATE :JOURNAL  
  20 2YQB          TESTRPN2 OK,XBR              [GEOERR IF NEWDIR UNSUCCESSFUL   
  13 2YXH ...      TRACEIF  K6BMSJRNL,99,,2,CE JDIR  
   9 2Y_= ...      BRN      Z1BMSJRNL
   5 33N6    STARTACT
   8 347Q          LDX   2  GSIGN
   9 34MB    #SKI  K6BMSJRNL>199-199 
   4 3572    (   
   9 35LL          ANDX  2  JSJPTR   
  19 366=          BZE   2  XBR                 [SHOULD BE -VE AT THIS STAGE 
   4 36KW    )   
  14 375G          ERX   2  JSJPTR              [CLEAR B0
  16 37K6          FPUT                         [JOURNAL ACTIVITY
   9 384Q          ADN   2  FPROPUSER
   9 38JB          SMO      JSJPTR   
   9 3942          LDN   3  JNLUSER  
   7 39HL          MOVE  2  3
   7 3=3= ...      SUICIDE   
   5 3=GW    XISTS   
  10 3=J4 ...      GETACT   BSA,AUTOMAP  
   9 3=K= ...      LDX   3  BPTR(2)  
  10 3=LD ...      LINKSET  3,XJRNLINK(1)
   7 3=ML ...      LOCK     3
   7 3=NS ...      CHANGEFI 3
   9 3=Q2 ...      LDX   3  BPTR(2)  
   7 3=R8 ...      UNLOCK   3
   7 3=SB ...      LDX   2  3
   6 3=TJ ...      FPUT  
   9 3=WQ ...      ACROSS   BMJLIST,1
   6 3=XY ...Z3BMSJRNL   
   4 3=Y3 ...[   
  20 3=Y6 ...[******************************************************  Z3BMSJRNL  
  15 3=Y9 ...[  FIRST TRY TO RECOVER JOURNAL CHAIN AND USAGE 
  15 3=Y# ...[  BLOCKS FROM THE LOWEST NUMBER SWAPFILE - P.M.
  12 3=YC ...[  SHOULD HAVE WRITTEN THEM THERE.  
   4 3=YG ...[   
   4 3=_= ...[   
  13 3=_* ...[  FIRST FIND LOWEST NUMBER SWAPFILE.   
   4 3=_D ...[   
  17 3=_H ...      LDX   3  ALFTP               [ ALFTABLE POINTER   
  10 3=_L ...      LDX   5  ALOGLEN(3)   
  17 3=_P ...      ADX   5  3                   [  => TO END OF BLOCK
  16 3=_S ...      LDN   6  0                   [  RESIDENCE NO. 
   5 3=_X ...TRYNXT  
   8 3?22 ...      LDX   0  A1(3)
  19 3?25 ...      BNG   0  NXTNTRY             [ J. IF NO RES OF THIS NUMBER
  18 3?28 ...      LDCT  0  #200                [ CHECK IF SWAP RESIDENCE
   8 3?2? ...      ANDX  0  A1(3)
  19 3?2B ...      BNZ   0  SWAPFND             [ J. IF LOWEST SWAP RESIDENCE
   5 3?2F ...NXTNTRY 
  18 3?2J ...      ADN   6  1                   [ ELSE UPDATE RES. NO.   
  15 3?2M ...      ADN   3  2                   [ AND POINTER
  19 3?2Q ...      TXL   3  5                    [ CHECK IF PAST END OF BLOCK
  19 3?2T ...      BCS      TRYNXT               [ J. BACK TO TRY NEXT IF NOT
  19 3?2Y ...      CALL  4  XBR                  [ IF NO SWAP RESIDENCE FOUND
  20 3?33 ...SWAPFND                             [ THEN J. TO GEOERR OTHERWISE   
  20 3?36 ...                                    [ SWAP RESIDENCE NO. NOW IN X6  
  17 3?39 ...      STO   6  AWORK1(2)            [ REMEMBER RES. NO. 
  20 3?3# ...      STOZ     AWORK2(2)            [ INITIAL BLOCK NUMBER TO READ  
  20 3?3C ...      SETNCORE 2048,3,FI,FUTILITY,1 [ SET UP BUFFER BLOCK FOR READS 
  18 3?3G ...      LOCKC    3,SUB1               [ LOCK IT FOR DURATION  
   8 3?3K ...      CALL  1  SUB1 
  19 3?3N ...      LDN   5  A1(2)                [ ADDRESS OF START OF BLOCK 
  19 3?3R ...      LDN   6  A1+2048(2)           [ ADDRESS OF END OF BLOCK   
   8 3?3W ...      LDX   2  FX2  
  21 3?3_ ...      STO   5  CURREC(2)            [ INITIALISE PTR. TO CURRENT REC.   
  20 3?44 ...      STO   5  ENDUSED(2)           [ AND PTR. TO END OF USED SPACE 
  19 3?47 ...      STO   6  ENDBLOCK(2)          [ AND PTR. TO END OF BLOCK  
  18 3?4= ...      LDN   7  0                    [ SWITCH WORD SET ZERO  
   5 3?4* ...READBLOX
  18 3?4D ...      LDX   0  ENDBLOCK(2)          [ CALCULATE SPARE SPACE 
  15 3?4H ...      SBX   0  ENDUSED(2)           [ IN BUFFER 
  17 3?4L ...      SBN   0  512                  [ MORE THAN 512 ?   
  19 3?4P ...      BPZ   0  ROOMENUFF            [ J. IF THERE IS ENOUGH ELSE
  20 3?4S ...                                    [ MOVE CURRENT RECORD ETC. TO   
  18 3?4X ...                                    [ START OF THE BUFFER   
  13 3?52 ...      CALL  1  SUB1                 [   
   8 3?55 ...      LDN   5  A1(2)
  20 3?58 ...      LDX   2  FX2                  [ SET UP POINTERS IN X4 AND X5  
  20 3?5? ...      LDX   4  CURREC(2)            [ TO MOVE USED DATA TO START OF 
  15 3?5B ...      NGN   3  513                  [ OF BLOCK  
  18 3?5F ...      ADX   3  ENDUSED(2)           [ CALCULATE AMOUNT OF   
  16 3?5J ...      SBX   3  CURREC(2)            [ DATA TO MOVE  
   6 3?5M ...SEEIFMORE   
  17 3?5Q ...      BNG   3  NOTMORE              [ J. IF < 513 ELSE  
  16 3?5T ...      MOVE  4  512                  [ MOVE 512 WORDS
   8 3?5Y ...      ADN   4  512  
  17 3?63 ...      ADN   5  512                  [ UPDATE POINTERS   
  18 3?66 ...      SBN   3  512                  [ LESS 512 ALREADY MOVED
   9 3?69 ...      BRN      SEEIFMORE
   5 3?6# ...NOTMORE 
  18 3?6C ...      ADN   3  513                  [ RESET AMOUNT TO MOVE  
  18 3?6G ...      MOVE  4  0(3)                 [ MOVE REST OF USED DATA
   7 3?6K ...      ADX   5  3
  20 3?6N ...      STO   5  ENDUSED(2)           [ UPDATE END OF USED SPACE PTR. 
   8 3?6R ...      CALL  1  SUB1 
   8 3?6W ...      LDN   0  A1(2)
   8 3?6_ ...      LDX   2  FX2  
  19 3?74 ...      STO   0  CURREC(2)            [ UPDATE CURRENT RECORD PTR.
   6 3?77 ...ROOMENUFF   
   4 3?7= ...[   
  19 3?7* ...[*************************************************************  
  17 3?7D ...[ THERE IS NOW ENOUGH SPACE IN THE BUFFER FOR THE NEXT  
  14 3?7H ...[ 512 WORDS TO BE READ IN FROM THE SWAPFILE 
   4 3?7L ...[   
  16 3?7P ...      BACKREAD BSET,BSBS,RFAIL,AWORK1(2),,ENDUSED(2)
   8 3?7S ...      LDN   0  512  
  20 3?7X ...      ADS   0  ENDUSED(2)           [ UPDATE END OF USED SPACE PTR. 
   7 3?82 ...      LDN   0  4
  20 3?85 ...      ADS   0  AWORK2(2)            [ UPDATE NEXT BLOCK NO. TO READ 
  19 3?88 ...      BNG   7  NXTRECORD            [ J. IF NOT ON FIRST BLOCK  
  18 3?8? ...      ORX   7  GSIGN                [ SHOW FIRST BLOCK READ 
   8 3?8B ...      CALL  1  SUB1 
  17 3?8F ...      LDX   0  A1(2)                [ FIRST WORD OF DATA
   8 3?8J ...      LDX   1  FX1  
  17 3?8M ...      SBX   0  PMSJ(1)              [ CHECK IF 'PMSJ'   
  16 3?8Q ...      BNZ   0  RFAIL                [ J. TO GIVE UP 
  19 3?8T ...      LDX   0  A1+1(2)              [ PICK UP CONVERSION IDENT. 
  18 3?8Y ...      SBX   0  NSWAPID(1)           [ IF NOT SAME AS CURRENT
  16 3?93 ...      BNZ   0  RFAIL                [ J. TO GIVE UP 
  16 3?94 ...      LDX   0  A1+2(2)             [IS THIRD WORD   
  13 3?95 ...      SBX   0  PMSQ(1)             [PMSQ
  14 3?96 ...      BNZ   0  NOPMSQ              [J IF NO 
  18 3?97 ...      LDX   0  A1+3(2)             [ELSE STORE SEQUENCE NO  
  14 3?98 ...      STO   0  ERLSEQNO            [IN FIXED
  17 3?99 ...      LDN   0  A1+4(2)             [ADDRESS FIRST RECORD
   8 3?9= ...      BRN      UPPTR
   5 3?9? ...NOPMSQ  
  19 3?9# ...      LDN   0  A1+2(2)              [ ADDRESS OF FIRST RECORD   
   5 3?9* ...UPPTR   
   8 3?9B ...      LDX   2  FX2  
  19 3?9C ...      STO   0  CURREC(2)            [ UPDATE CURRENT RECORD PTR.
  17 3?9D ...[  IF GENERAL (OR PARTIAL INVOLVING :JOURNAL) RESTORE   
  16 3?9F ...[  IS BEING DONE, DON'T RECOVER DATA FROM SWAPFILE. 
   4 3?9G ...[   
   9 3?9H ...      LDX   0  GINCTSN  
  17 3?9J ...      BNZ   0  NDOFSWAP            [IF GEN. RES. DONE   
  18 3?9K ...      TOPFCB   3                   [OR IF PARTIAL RESTORE   
  17 3?9L ...      JBS      NDOFSWAP,3,BFPARTRES  [INVOLVING :JOURNAL
   4 3?9M ...#   
   6 3?9N ...NXTRECORD   
   9 3?9P ...      LDX   3  CURREC(2)
  19 3?9Q ...      LDX   0  0(3)                 [ IF FIRST WORD IS 'PMSJ'   
  19 3?9R ...      BXE   0  PMSJ(1),NDOFSWAP     [ THEN NO MORE DATA TO CHECK
  19 3?9S ...      LDX   0  ASIZE(3)             [ CHECK SIZE OF THIS BLOCK  
  17 3?9W ...      BZE   0  RFAIL                [ ASIZE CANT BE ZERO
  15 3?=5 ...      BNG   0  RFAIL               [OR NEGATIVE 
  18 3?=* ...      SBN   0  1024                 [ CHECK IF ASIZE > 1024 
  19 3?=D ...      BPZ   0  RFAIL                [ TREAT AS FAIL IF CORRUPT  
   9 3?=F ...      LDX   0  ASIZE(3) 
   7 3?=G ...      ADX   0  3
  20 3?=H ...      BXGE  0  ENDUSED(2),READBLOX  [ J.IF REC. GOES INTO NEXT BLOCK
   9 3?=J ...      LDX   0  ASIZE(3) 
  20 3?=K ...      ADS   0  CURREC(2)            [ UPDATE CURRENT RECORD POINTER 
  19 3?=L ...      LDX   4  ALOGLEN(3)           [ LOGICAL LENGTH OF RECORD  
  17 3?=M ...      BZE   4  RFAIL                [ ALOGL CANT BE ZERO
  14 3?=P ...      LDN   6  8                    [ AND   
  18 3?=S ...      ADX   6  4                    [ MUST BE < OR = ASIZE-9
  18 3?=X ...      BXGE  6  ASIZE(3),RFAIL       [ IF NOT, TREAT AS FAIL 
   8 3??2 ...      LDN   0  1024 
  19 3??5 ...      SBX   0  4                    [ CHECK IF ALOGLEN < 1024   
  20 3??8 ...      BNG   0  RFAIL                [ TREAT AS READFAIL IF CORRUPT  
  18 3??? ...      SETUPCOR 4,2,FILE,FTAB        [ SET UP BLOCK FOR DATA 
  15 3??B ...      LDX   0  ATYPE(3)             [ BLOCK TYPE
  18 3??F ...      ANDX  0  HALFTOP              [ MASK OUT TOP 12 BITS  
  21 3??J ...      BXE   0  FILEPLUSFCB,NORENAME [ DONT RENAME IF ITS FCB AS 'OPEN'  
  20 3??M ...                                    [ MAY FREE FCBS FROM ACT. CHAIN 
  16 3??Q ...      NAMEX    2                    [ RENAME BLOCK  
   5 3??T ...NORENAME
   9 3??Y ...      LDX   0  BACK1(3) 
  18 3?#3 ...      STO   0  BACK1(2)             [ TRANSFER BACKING STORE
  18 3?#6 ...      LDX   0  BACK2(3)             [ HOME FOR THIS BLOCK   
   9 3?#9 ...      STO   0  BACK2(2) 
   8 3?## ...      LDN   4  A1(3)
  18 3?#C ...      LDN   5  A1(2)                [ SET UP PTRS. FOR MOVE 
  20 3?#G ...      LDX   3  ALOGLEN(2)           [ LOGICAL LENGTH OF USED DATA   
  20 3?#K ...      SBN   3  513                  [ CHECK IF > 512 WORDS TO MOVE  
   5 3?#N ...NYMORE  
  18 3?#R ...      BNG   3  NMORE                [ J. IF < 512 TO MOVE   
  16 3?#W ...      MOVE  4  512                  [ ELSE MOVE 512 
   8 3?#_ ...      ADN   4  512  
  18 3?*4 ...      ADN   5  512                  [ AND UPDATE POINTERS   
   8 3?*7 ...      SBN   3  512  
   9 3?*= ...      BRN      NYMORE   
   5 3?** ...NMORE   
  18 3?*D ...      ADN   3  513                  [ RESET AMOUNT TO MOVE  
  17 3?*H ...      MOVE  4  0(3)                 [ MOVE REST OF DATA 
   8 3?*L ...      LDX   2  FX2  
  18 3?*P ...      BRN      NXTRECORD            [ LOOP FOR NEXT RECORD  
   5 3?*S ...RFAIL   
  20 3?*X ...      MONOUT   JSJTRUNC             [INFORM OF LOST DATA IF FAILED  
   5 3?B2 ...NDOFSWAP
   4 3?B5 ...[   
  19 3?B8 ...[***************************************************  NDOFSWAP  
  16 3?B? ...[ ALL DATA HAS BEEN READ FROM THE SWAPFILE - FIRST  
  14 3?BB ...[ OVERWRITE RECOVERED DATA ON SWAPFILE, THEN
  15 3?BF ...[ FREE THE BACKING STORE ON ALL SWAPFILES, NEXT 
  15 3?BJ ...[ CHAIN ANY GMON/JRNL BLOCKS AT FRONT OF JOURNAL
  16 3?BM ...[ CHAIN.(N.B. WHEN READ BACK AND SET UP IN ACTIVITY 
  15 3?BQ ...[ CHAIN THE ORDER OF THE BLOCKS WAS REVERSED, SO
  15 3?BT ...[ BY CHAINING EACH AT FRONT OF JOURNAL CHAIN THE
   9 3?BY ...[ ORDERING IS CORRECTED)
   4 3?C3 ...[   
   8 3?C6 ...      LDN   7  GZERO
  19 3?C9 ...      LDN   6  0                   [ OVERWRITE SWAPFILE BLOCK 0 
  16 3?C# ...      BACKWRIT BSET,BSBS,NORECOVRY,AWORK1(2),EX6,EX7
   6 3?CC ...NORECOVRY   
  20 3?CD ...#UNS FSHTEST                       [ OMIT IF IN TESTING ENVIR. AS   
  21 3?CF ...#SKI                               [ THIS WILL HAVE BEEN DONE EARLIER   
  18 3?CG ...      DOWN     SETLIST,2           [ TO FREEBAX SWAPFILES   
   5 3?CK ...NXTGJRNL
  19 3?CN ...      HUNT     2,GMON,JRNAL        [ FIND NEXT GMON/JRNAL BLOCK 
  16 3?CR ...      BNG   2  XLASTGJRNL          [ J. IF NONE LEFT
  21 3?CW ...      STOZ     JWAITER(2)          [ SO IF NON-AUTO MESS.JRNL ACT WONT  
  21 3?C_ ...                                   [ TRY TO WAKE IT UP - 'COS ITS GONE !
  18 3?D4 ...      CHAIND   2,BSJC              [ CHAIN IN JOURNAL CHAIN 
   9 3?D7 ...      BRN      NXTGJRNL 
   6 3?D= ...XLASTGJRNL  
   4 3?D* ...[   
  19 3?DD ...[**************************************************  XLASTGJRNL 
  17 3?DH ...[ ALL GMON/JRNL BLOCKS SHOULD NOW BE IN JOURNAL CHAIN   
  13 3?DL ...[ ANY OTHER BLOCKS LEFT ARE FILE BLOCKS 
   4 3?DP ...[   
  11 3?DS ...      SETNCORE 6,3,FILE,FLOCNB  
   7 3?DX ...      PROPTEST  
  10 3?G6          LDN   0  ACOMMUNE1(2) 
  10 3?_Q          LDN   1  FPROPUSER(2) 
   7 3#FB          MOVE  0  3
   7 3#_2          LDN   5  0
   7 3*DL          LDN   4  0
   8 3*Y=          LDX   0  GSIGN
  15 3BCW          STO   0  AWORK1(2)           [1ST SEARCH  
  14 3BXG          STO   0  AWORK2(2)           [NO FAILS
   5 3CC6    SCRUB   
   6 3CWQ          STEP  
  16 3DBB          BZE   3  NDJRNL              [END OF :JOURNAL 
  10 3DW2          LDX   0  EDESCRIBE(3) 
  16 3F*L          BNZ   0  SCRUB               [J IF NOT NAME   
  14 3FT=          TESTNAMX 3,NLOC(1),ELOC1N(3),SCRUB,6  
  19 3G#W    #                                   JUMP IF LOCAL NAME DIFFERENT
   9 3GSG          LDX   0  AWORK1(2)
  18 3H#6          BPZ   0  SJCLOSE             [J IF SECOND TIME ROUND  
  10 3HRQ          LDX   0  ELAN-A1(3)   
  16 3J?B          BXU   0  NLAN(1),QDELETE     [WRONG LANGUAGE  
  19 3JR2          JBC      QDELETE,3,BNSERIAL  [J IF NOT SERIAL OR IF MULT  
  13 3K=L ...      JMBS     QDELETE,3,BNMDF,BNMULT   
  11 3KQ=          LDX   0  EVERSION-A1(3)   
  19 3L9W          BZE   0  QDELETE             [NOT CREATED BY JOURNAL ACT  
  21 3LPG          BXGE  5  EVERSIONN(3),SCRUB  [J UNLESS HIGHEST VERSION YET FOUND  
  11 3M96          LDX   5  EVERSION-A1(3)   
  10 3MNQ          LDX   4  EGEN-A1(3)   
   8 3N8B          BRN      SCRUB
   5 3NN2    QDELETE 
  16 3P7L          TRACEIF  K6BMSJRNL,99,299,EGEN-A1(3),NOTSJFIL 
  15 3PM=          TRACEIF  K6BMSJRNL,199,299,ELAN-A1(3),ELAN
  16 3Q6W          TRACEIF  K6BMSJRNL,199,299,EINF1-A1(3),EINF1  
  17 3QLG          TRACEIF  K6BMSJRNL,199,299,EVERSION-A1(3),EVERSION
   9 3R66          LDEX  7  ECOPSN(3)
   9 3RKQ          ADX   7  ENUSEN(3)
  15 3S5B          CALL  6  RDELETE             [DELETE FILE 
   8 3SK2          BRN      SCRUB
  21 3T4L    SJCLOSE                            [FNISH CLOSE IN PROGRESS BEORE BREAK 
  16 3TJ=          JBC      SCRUB,3,BNREVERE    [J UNLESS REVERED
  16 3W3W          TRACEIF  K6BMSJRNL,99,299,EGEN-A1(3),SJFCLOSE 
   8 3WHG          LDX   0  EDATE
  10 3X36          STO   0  EWRITDAYN(3) 
   9 3XGQ          STO   0  EDLAN(3) 
   9 3Y2B          LDX   0  CLEANCT  
  10 3YG2          STO   0  EWRITTIMEN(3)
   9 3Y_L          STO   0  ETLAN(3) 
  12 3_F=          MBC      3,BNREVERE,BNBMUWB   
   9 3_LD ...      LDN   0  FILESIZE 
   9 3_RL ...      DEX   0  EINF3N(3)
   8 3_SY ...      LDX   1  BFILE
  18 3_W= ...      LDX   0  FGMTBS(1)          [ G.M.T. FROM MASTER'S FCB
  18 3_XJ ...      STO   0  EGMTLAN(3)         [ UPDATE DIRECTORY ENTRY. 
   8 3_YW          STEPREWRITE   
   8 42DG          BRN      SCRUB
   5 42Y6    NDJRNL  
   9 43CQ          LDX   0  AWORK1(2)
  18 43XB          BPZ   0  SCLOSE              [J IF SECOND TIME THRU   
  14 44C2          TRACEIF  K6BMSJRNL,99,299,5,MAXEVERS  
  14 44WL          TRACEIF  K6BMSJRNL,99,299,4,MAXSJGEN  
  18 45B=          BZE   5  SCLOSE              [J IF NO SJFILES FOUND   
   9 45TW          STOZ     AWORK1(2)
  10 46*G          MHUNTW   2,FILE,FLOCNB
   8 46T6          ADN   1  NLOC 
   8 47#Q          ADN   2  A1   
   7 47SB          MOVE  1  6
   5 48#2    SEARCH  
  14 48RL          TRACEIF  K6BMSJRNL,99,299,5,NEXEVERS  
  10 49?=          MHUNTW   1,FILE,FLOCNB
  17 49QW          STO   4  A1+4(1)             [GENERATION REQUIRED 
   7 4==G          GETDIR   1
  16 4=Q6          TRACEIF  K6BMSJRNL,99,299,ACOMMUNE9(2),GDE REP
  17 4?9Q          TESTRPN2 OK,NOFILE           [CANT HAPPEN 1ST TIME
  10 4?PB          MHUNTW   1,FILE,ENT   
  16 4#92          JBC      NDFOUND,1,BEREVERE  [J UNLESS REVERED
   9 4#NL          LDEX  0  ECOPS(1) 
  17 4*8=          BZE   0  NDFOUND             [OFFLINE SO IGNORE   
   7 4*MW          BMCHECKRF 
  10 4B7G          MHUNTW   1,FILE,ENT   
  17 4BM6          TESTREP2 FULL,SFULL,SHORT,NDFOUND,CORRUPT,REVFAIL 
   5 4C6Q    SDELETE 
  16 4CLB          TRACEIF  K6BMSJRNL,99,299,EVERSION(1),SJF DEL 
   9 4D62          LDEX  7  ECOPS(1) 
   9 4DKL          ADX   7  ENUSE(1) 
   7 4F5=          FREECORE 1
   7 4FJW          BACKSPACE 
  21 4G4G          CALL  6  RDELETE             [DELETE FILE IF EMPTY OR FAILED 1ST B
   5 4GJ6    NOFILE  
   7 4H3Q          SBN   5  1
  17 4HHB          BZE   5  SCLOSE              [NO MORE - SO GIVE UP
  18 4J32          SBN   4  1                   [REDUCE GEN TO CORRESPOND
  18 4JGL          BNZ   4  SEARCH              [IF ZERO CORRECT TO 4095 
   8 4K2=          LDN   4  4095 
   9 4KFW          BRN      SEARCH   
   5 4K_G    REVFAIL 
  10 4LF6          LDX   0  ACOMMUNE1(2) 
  21 4LYQ          BNZ   0  SLOSTDATA           [J IF NOT FAILED ON 1ST BLOCK FF FILE
  19 4MDB          STOZ     AWORK2(2)           [TO MARK FAILED ON 1ST BLOCK 
   9 4MY2          BRN      SDELETE  
   6 4NCL    SLOSTDATA   
   7 4NX=          FREECORE 1
   9 4PBW          MONOUT   JSJTRUNC 
   8 4PWG          BRN      SETUP
   5 4QB6    NDFOUND 
   7 4QTQ          FREECORE 1
   5 4R*B    SETUP   
   9 4RT2          LDX   2  JSJPTR   
   9 4S#L          STO   4  JGEN(2)  
  10 4SS=          STO   5  JVERSION(2)  
   7 4T?W          FASTREWI 3
  21 4TRG          BRN      SCRUB               [TIDY UP ANY REMAINING REVERED FILES 
   5 4W?6    SCLOSE  
   7 4WGX ...      TOPFCB   3
  12 4WHH ...      JBC      NONLOST,3,BFPARTRES  
   9 4WJ7 ...      MONOUT  JSJTRUNC  
   5 4WJR ...NONLOST 
  13 4WKG ...      MBS      3,BFALLINCS,BFDIRUPDATE  
   7 4WQQ          CLOSETOP  
   9 4X=B          BRN      STARTACT 
   5 4XQ2    SFULL   
   9 4Y9L          LDX   0  AWORK2(2)
  21 4YP=          BNG   0  NDFOUND             [OK IF PREVIOUS FILE DIDNT FAIL ON 1S
  19 4_8W          BRN      SLOSTDATA           [ELSE DATA MAY HAVE BEEN LOST
   5 4_NG    #PAGE   
   4 5286    [   
  14 52MQ    ZBMCHECKRF     [ENTRY FOR BMCHECKRF MACRO   
   4 537B    [   
  16 53M2          READ                         [BLOCKS RECORD   
  10 546L          MHUNTW   1,FILE,FRB   
   8 54L=          LDEX  7  A1(1)
   7 555W          SBN   7  2
  18 55KG          BZE   7  MPTYFILE            [J IF FILE HAS NO BLOCKS 
  15 5656          LDN   5  0                   [BLOCK MOD   
   6 56JQ    READBLOCK   
   4 56K3 ...[   
  19 56K# ...[***************************************************  READBLOCK 
  16 56KK ...[ FIRST FIND THE FCB FOR THIS SJFILE IF RECOVERED   
   4 56KW ...[   
   8 56L7 ...      LDX   1  FX1  
   8 56LD ...      LDX   2  FX2  
  19 56LP ...      STOZ     AWORK4(2)           [ SHOW USING ORIGINAL BLOCK  
  17 56M2 ...      MHUNTW   3,FILE,ENT          [ PRESENT ON ENTRY   
   9 56M? ...      LDX   0  EGEN(3)  
  21 56MJ ...      STO   0  AWORK3(2)           [ REMEMBER GENERATION BEING CHECKED  
   7 56MT ...      LDX   3  2
  20 56N6 ...NXTFCB                             [ FIND NEXT FCB IF THERE IS ONE  
  18 56NC ...      STO   3  2                   [ REMEMBER LAST FCB FOUND
  21 56NN ...      HUNT2J   3,FILE,FTAB,,NOTRECOVRD   [ N.B. ITS CALLED A'FTAB' !!   
   9 56N_ ...      LDX   0  FGEN(3)  
  16 56P= ...      SMO      FX2                 [ X2 NOT = FX2 !!
  18 56PH ...      BXU   0  AWORK3,NXTFCB       [ J. IF WRONG GENERATION 
  15 56PS ...      TESTNAMX 3,NJOURNAL+1(1),FUSE1(3),NXTFCB,4
  14 56Q5 ...      TESTNAMX 3,NLOC(1),FLOC1(3),NXTFCB,4  
  16 56QB ...      LDX   0  FLAN(3)             [ OR IF NAME OR  
  16 56QM ...      BXU   0  NLAN(1),NXTFCB      [ LANGUAGE WRONG 
   4 56QY ...[   
  17 56R9 ...[****************************************************** 
  17 56RG ...[  WE HAVE THE FCB - IF RECOVERED, THE REQUIRED BLOCK   
  16 56RR ...[  MUST BE BETWEEN THIS FCB AND THE LAST ONE (OR THE
  16 56S4 ...[  CURRENT ACTIVITY) SO HUNT BACKWARDS AND COMPARE  
  17 56S* ...[  BACK1/2 . FIRST FIND RES. NO. AND BLOCK NO. REQUIRED 
   4 56SL ...[   
  10 56SX ...      MHUNTW   1,FILE,FRB   
   7 56T8 ...      SMO      5
   9 56TF ...      LDX   4  A1+2(1)  
   5 56TQ ...NEXTBACK
  19 56W3 ...      LDX   3  BPTR(3)             [ POINTER TO PREVIOUS BLOCK  
  20 56W# ...      BXE   3  2,NOTRECOVRD        [ J. IF NOT FOUND BEFORE => FX2  
   9 56WK ...      LDX   0  BACK1(3) 
  19 56WW ...      BXU   0  A1+1(1),NEXTBACK    [ J. IF RESIDENCE NO.WRONG   
   9 56X7 ...      LDX   0  BACK2(3) 
  18 56XD ...      BXU   0  4,NEXTBACK          [ J. IF WRONG BLOCK NO.  
  10 56XP ...      LDX   0  ALOGLEN(3)   
  19 56Y2 ...      SBN   0  512                 [ CHECK IF BLOCK LENGTH RIGHT
  21 56Y? ...      BNZ   0  NOTRECOVRD          [ J. IF RIGHT BLOCK BUT WRONG LENGTH 
   8 56YJ ...      LDX   2  FX2  
  21 56YT ...      CHAIN    3,2                 [ RECHAIN BLOCK IN FRONT OF ACTIVITY 
  17 56_6 ...      LDX   1  FPTR(2)             [ AND GET PTR. TO IT 
   7 56_C ...      NGN   0  1
  19 56_N ...      STO   0  AWORK4(2)           [ SHOW USING RECOVERED BLOCK 
  20 56__ ...      NAME     1,BSTB,BREAD        [ NAME RECOVERED BLOCK A BREAD   
  19 572= ...      BRN      USERECBLOK          [ J. TO USE RECOVERED BLOCK  
   6 572H ...NOTRECOVRD  
   4 572S ...[   
  18 5735 ...[********************************************************** 
  17 573B ...[  BLOCK NOT SUCCESSFULLY RECOVERED SO READ OLD VERSION 
  12 573M ...[  FROM BACKING STORE AND USE THAT  
   4 573Y ...[   
  21 574B          LDN   6  2                   [NUMBER OF ATTEMPTS TO READ FAILING B
   6 57J2    READAGAIN   
  10 583L          MHUNTW   1,FILE,FRB   
   7 58H=          SMO      5
  17 592W          LDX   2  A1+2(1)             [NEXT BLOCK NUMBER   
  14 59GG          BACKREAD BUSE,BSBS,TRYAGAIN,A1+1(1),2 
  21 5=26                                       [USE BFREEW : J TO TRANFAIL IF FAIL  
  10 5=FQ          MHUNTW   1,BSTB,BREAD 
   6 5=PJ ...USERECBLOK  
   8 5=_B          LDX   0  A1(1)
  21 5?F2          BZE   0  MPTYBLOCK           [J IF BLOCK HAS NEVER BEEN WRITTEN TO
   8 5?YL          LDN   3  A1(1)
   9 5#D=          LDN   6  GSBS(3)  
   5 5#XW    NEXTREC 
   9 5*CG          LDXC  0  FRH(3)   
  18 5*X6          BCC      NOTDUMMY            [J UNLESS DUMMY RECORD   
   9 5BBQ          ANDX  0  BRHMASK  
  19 5BWB          BNZ   0  SCORRUPT            [J IF RECORD HEADER CORRUPT  
   9 5CB2          LDEX  0  FRH(3)   
  19 5CTL          BZE   0  SCORRUPT            [BLOCK CANT END WITH DUMMY   
  15 5D*=          ADX   3  0                   [UPDATE PTR  
  16 5DSW          BXGE  3  6,SCORRUPTND        [OFF END OF BLOCK
   9 5F#G          BRN      NEXTREC  
   5 5FS6    NOTDUMMY
   9 5G?Q          ANDX  0  BRHMASK  
   9 5GRB          BNZ   0  SCORRUPT 
   9 5H?2          LDEX  0  FRH(3)   
  17 5HQL          BZE   0  NDBLOCK             [J IF END OF BLOCK   
   7 5J==          ADX   0  3
  10 5JPW          BXGE  0  6,SCORRUPT   
  18 5K9G          SBN   0  2(3)                [NO OF WORDS OF USER DATA
  15 5KP6          ADN   3  2                   [->USER DATA 
   7 5L8Q          SMO      0
   7 5LNB          SUM   2  0
  16 5M82          BNZ   2  SCORRUPTCH          [CHECKSUM FAIL   
  16 5MML          ADX   3  0                   [UPDATE POINTER  
   9 5N7=          BRN      NEXTREC  
   5 5NLW    NDBLOCK 
   8 5NP5 ...      LDX   2  FX2  
  20 5NR# ...      LDX   0  AWORK4(2)           [ CHECK IF USING RECOVERED BLOCK 
  15 5NTH ...      BPZ   0  USINGOLD            [ J. IF NOT  
  19 5NXQ ...      BACKWRIT ,BSBS,WFAIL         [ WRITE RECOVERED BLOCK AWAY 
  20 5N__ ...      LDX   1  FPTR(2)             [ RECOVER POINTER TO WRITE BLOCK 
   5 5P48 ...USINGOLD
  10 5P6G          NAME     1,BSTB,BFREEW
   7 5PL6          ADN   5  1
   9 5Q5Q          BCT   7  READBLOCK
   7 5QKB          FREECORE 1
   9 5R52          MFREEW   FILE,FRB 
  16 5RJL          SETREP2  FULL                [ALL BLOCKS USED 
   5 5S4=          UP
   5 5S7B ...WFAIL   
  19 5S=G ...      MONOUT   JSJTRUNC            [ INFORM OF POSS. LOST DATA  
  19 5S*L ...      STOZ     AWORK4(2)           [ INDICATE USING OLD BLOCK   
  10 5SDQ ...      BRN      NOTRECOVRD   
   6 5SHW    MPTYBLOCK   
   7 5T3G          FREECORE 1
  17 5TH6          KEEPREP  4,SHORT             [SOME UNUSED BLOCKS  
   8 5W2Q          BNZ   5  SHORT
  16 5WGB          KEEPREP  4,EMPTY             [NO USED BLOCKS  
   5 5X22    SHORT   
  21 5XFL          BZE   7  USEDALL             [TRUNCATED IN LAST BLOCK - UPDATE UNN
  16 5X_=          BACKSPACE                    [TO NAME RECORD  
   9 5YDW          CALL  6  STEPWRITE
   7 5YYG          SMO      5
   7 5_D6          LDCT  0  0
   7 5_XQ          ORN   0  1
  15 62CB          STO   0  ECOPS-A1(3)         [UPDATE ECOPS
  17 62X2          READ                         [BLOCKS RECORD AGAIN 
  10 63BL          MHUNTW   1,FILE,FRB   
  17 63W=          NAME     1,FILE,FWB          [FOR REPLACE MACRO   
  21 64*W          ADN   5  2                   [RECORD HEADER FOR SHORTENED BLOCKS R
   8 64TG          STO   5  A1(1)
  16 65*6          REPLACE                      [BLOCKS RECORD   
   9 65SQ          MFREEW   FILE,FWB 
  16 66#B          MHUNTW   1,FILE,FRB          [FROM 1ST READ   
  10 66S2          NAME     1,BSTB,FULLB 
   7 67?L          LDX   3  1
   7 67R=          SBN   5  2
  19 68=W          BZE   5  SFREE               [J IF ALL BLOCKS TO BE FREED 
  21 68QG          SBS   5  A1(1)               [SHORTEN HEADER BY NO OF USED BLOCKS 
   8 69=6          ADN   1  A1+2 
   7 69PQ          LDX   2  1
   7 6=9B          ADX   1  5
   7 6=P2          SMO      7
  18 6?8L          MOVE  1  0                   [MOVE BLOCKS DOWN FULLB  
   5 6?N=    SFREE   
   8 6#7W          FULLBPAS 3,2  
   8 6#MG          LDX   2  FX2  
   5 6*76    USEDALL 
  14 6*LQ          STO   5  ACOMMUNE1(2)        [REPLY   
   5 6B6B    PUTREP  
  14 6BL2          TRACEIF  K6BMSJRNL,99,299,4,CHRFREP   
   7 6C5L          PUTREP2  4
   5 6CK=          UP
   6 6D4W    SCORRUPTND  
  18 6DJG          SBX   3  0                   [RECOVER RECORD POINTER  
   9 6F46          BRN      SCORRUPT 
   6 6FHQ    SCORRUPTCH  
  18 6G3B          SBN   3  2                   [RECOVER RECORD POINTER  
   5 6GH2    SCORRUPT
   8 6GJY ...      LDX   2  FX2  
   9 6GLW ...      LDX   0  AWORK4(2)
  19 6GNS ...      BPZ   0  USEDOLD             [ J. IF USING OLD BLOCK ELSE 
  19 6GQQ ...      MONOUT   JSJTRUNC            [ INFORM OF POSS. LOST DATA  
  16 6GSN ...      STOZ     AWORK4(2)           [ SHOW USING OLD 
  17 6GWL ...      BRN      NOTRECOVRD          [ TO TRY OLD BLOCK   
   5 6GYJ ...USEDOLD 
  21 6H2L          STOZ     FRH(3)              [TRUNCATE BLOCK BEFORE CORRUPT RECORD
   8 6HG=          SBN   3  A1(1)
  21 6H_W          BZE   3  RECORDONE           [J IF 1ST RECORD IN BLOCK IS CORRUPT 
  21 6JFG          BACKWRIT FREE,BSBS,RECORDONE [REWRITE BLOCK - IF FAIL LOSE WHOLE B
  18 6J_6          ADN   5  1                   [TO INCLUDE THIS BLOCK   
   7 6KDQ          SBN   7  1
   9 6KYB          BRN      TRANFAIL 
   6 6LD2    RECORDONE   
   7 6LXL          FREECORE 1
   5 6MC=    TRANFAIL
   9 6MWW          KEEPREP  4,CORRUPT
   8 6NBG          BRN      SHORT
   5 6NW6    MPTYFILE
   7 6P*Q          FREECORE 1
   9 6PTB          KEEPREP  4,EMPTY  
   9 6Q*2          BRN      PUTREP   
   5 6QSL    TRYAGAIN
   9 6R#=          BCT   6  READAGAIN
   9 6RRW          BRN      TRANFAIL 
   4 6S?G    XBR 
   9 6SR6          GEOERR   1,BMSJRNL
   4 6T=Q    [   
  12 6TQB          MENDAREA BMAPGAP,K99BMSJRNL   
   4 6W=2    #END
   8 ____ ...12756607000200000000
