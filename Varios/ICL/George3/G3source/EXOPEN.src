   7 22FL    #SEG EXOPEN867  
   4 22_=    #   
  15 23DW    #   COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982
   4 23YG    #   
   4 24D6    #   
   7 24XQ    #LIS  K0EXOPEN  
   7 25CB          8HEXOPEN  
  16 25X2    # SEGMENT TO FIND FILE INDEX CELL FOR SPECIFIED FILE
  17 26BL    # ACTION  DETERMINED BY MARKERS IN EXMARK OF EXOF BLOCK 
  18 26W=    #     ENTERED VIA DOWN FROM VARIOUS SEGMENTS TO REFIND FIP  
  17 27*W    #     OR ACROSS FROM SOUP TO LOCATE FIP FOR OPEN MODE   
  20 27TG    # OUTPUT IS REPLY OK OR EXFAIL ,IF EXFAIL MARKERS FOR REASON SET IN 
  12 28*6    # EMARK OF GEXD BLOCK AS FOLLOWS:   
  18 28SQ    #     ABREAK = BREAK-IN WHILST WAITING FOR SU TO COME ONLINE
  12 29#B    #              OR WAITING FOR SCALO 
  17 29S2    #     AFAIL  = TRANSFER FAIL WHILST READING FIP CELLS   
  11 2=?L    #              OR SCA CORRUPT   
  15 2=R=    #     APLINKF= IPB FAILURE - SCALO NOT AVAILABLE
  19 2?=W    #     ANONL  = UNIT NOT ONLINE (PLEASE LOAD REQUQEST SUPRESSED) 
  15 2?QG    #     ARITE  = CD REPLY TO PLEASE LOAD REQUEST  
  11 2#=6    #     ANFOUND= FILE NOT FOUND   
   4 2#PQ    #   
   4 2*9B    #   
  15 2*P2          SEGENTRY K1EXOPEN,QK1EXOPEN  [REFIND FIP  
  17 2B8L          SEGENTRY K2EXOPEN,QK2EXOPEN  [LOCATE FIP FOR OPEN 
   4 2BN=    #   
   9 2C7W    XEWS           +EWSCA   
   9 2CMG    XEWN           +EWNOL   
   4 2D76    #   
   9 2DLQ    NX4            #777777  
   9 2F6B    NX5            #37777777
   4 2FL2    #   
   4 2G5L    #   
   9 2GK=    XER1  GEOERR   1,EXOTRAN
   4 2H4W    #   
  14 2HJG    #     S/R TO SET X2 POINTING TO EWDAS/EXOF  
  14 2J46    #            AND X3 POINTING TO EWDAS/GEXD  
   4 2JHQ    #   
  10 2K3B    XHUNT MHUNTW   2,EWDAS,EXOF 
  10 2KH2          MHUNTW   3,EWDAS,GEXD 
   7 2L2L          EXIT  5  0
   4 2LG=    #   
   4 2L_W    #   
  14 2MFG    #              S/R TO FIND AND GET NEXT S.U.
   9 2M_6    #              LINK X7  
  20 2NDQ    #              ON  ENTRY AND EXIT   X1  POINTS TO CURRENT SU  ASTUC 
  16 2NYB    #                                   X2  EXOF BLOCK  
  16 2PD2    #                                   X3  GEXD BLOCK  
   4 2PXL    #   
  10 2QC=    #              USES X4,X5   
   4 2QWW    #   
   5 2RBG    NEXTSU  
   8 2RW6          SBX   7  FX1  
   7 2S*Q          LDN   4  0
   9 2STB    NX2   LDX   1  FPTR(1)  
   8 2T*2          TXU   1  CXCC 
  17 2TSL          BCC      NX1                 [JUMP IF END OF CHAIN
   9 2W#=          LDX   0  BACK1(1) 
  18 2WRW          BZE   0  NX1                 [JUMP IF NO MORE ASTUCS  
  17 2X?G          BNG   0  NX2                 [JUMP IF DEAD ASTUC  
   8 2XR6          LDCT  0  #200 
   9 2Y=Q          ANDX  0  ASMARK(1)
  16 2YQB          BNZ   0  NX2                 [OR DISENGAGED   
  16 2_=2          LDX   5  BACK1(1)            [SAVE SERIAL NO  
  21 2_PL          USESTX   5,NX9,,TBREAK,NX9   [ADD ONE TO COUNT OF USERS AND GET SC
  18 329=          ADN   7  1                   [UPDATE LINK TO EXIT OK  
  10 32NW          MHUNTW   3,EWDAS,GEXD 
  20 338G          STO   5  ESERL(3)            [SAVE SERIAL NUMBER OF THIS UNIT 
  18 33N6    NX1   BZE   4  NX10                [JUMP IF NO UNIT SAVED   
  17 347Q          RELST    4                   [OTHERWISE RELEASE IT
   8 34MB    NX10  CALL  5  XHUNT
   8 3572          ADX   7  FX1  
   7 35LL          EXIT  7  0
   4 366=    #   
  15 36KW    #     SCALO NOT AVAILABLE OR IN USE BY S-TRUSTED
   4 375G    #   
  18 37K6    NX9   BZE   4  NX11                [JUMP IF NO UNIT SAVED   
   7 384Q          RELST    4
  18 38JB    NX11  LDX   4  5                   [SAVE NEW UNIT SERIAL NO.
  15 3942          MFINDSU  1,4                 [REFIND ASTUC
   8 39HL          CALL  5  XHUNT
  18 3=3=          BRN      NX2                 [JUMP TO GET NEXT UNIT   
   4 3=GW    #   
  19 3?2G    #     BREAKIN WHILE WAITING FOR LOCK-OUT FROM OTHER PROCESSOR   
   4 3?G6    #   
   5 3?_Q    TBREAK  
   8 3#FB          CALL  5  XHUNT
   9 3#_2          LDN   0  ABREAK   
  17 3*DL          ORS   0  EMARK(3)            [SET BREAK IN MARKER 
   8 3*Y=          BRN      NX1  
   4 3BCW    #   
   4 3BXG    #   
   4 3CC6    #   
  12 3CWQ    #     READ  S/R LINK X5 ADDRESS X1  
   4 3DBB    #   
   5 3DW2    XREAD   
  14 3F*L          SBX   5  FX1                 [READ S/R
   9 3FT=          READST   1,XRD1   
  10 3G#W          TESTREPNOT   OK,XRD2  
   9 3GSG          LDX   1  ECOR(3)  
  10 3H#6    XRD1  MHUNTW   2,EWDAS,EXOF 
   8 3HRQ          ADX   5  FX1  
   7 3J?B          EXIT  5  1
  11 3JR2    XRD2  TESTREPNOT   EXFAIL,XER1  
   8 3K=L          ADX   5  FX1  
   7 3KQ=          EXIT  5  0
   4 3L9W    #   
   4 3LPG    #   
  16 3M96    #     UNLOCK UNIT S/R   LINK X7  SERIAL NUMBER X1   
   4 3MNQ    #   
   5 3N8B    XUNLOCK 
   8 3NN2          SBX   7  FX1  
   7 3P7L          UNLOCKSU 1
   8 3PM=          CALL  5  XHUNT
   8 3Q6W          ADX   7  FX1  
   7 3QLG          EXIT  7  0
   4 3R66    #   
  16 3RKQ    #     OPEN  UNIT  S/R   LINK X7 SERIAL NO. ESERL(3) 
   4 3S5B    #   
   5 3SK2    XOPEN   
   6 3T4L          OPENST
   7 3TJ=          EXIT  7  0
   4 3W3W    #   
   4 3WHG    #   
   7 3X36    #     START HERE
  13 3XGQ    #     THROUGHOUT X2 POINTS TO EXOF BLOCK
  13 3Y2B    #                X3 POINTS TO GEXD BLOCK
   4 3YG2    #   
   6 3Y_L    QK2EXOPEN   
   7 3_F=          NGNC  0  1
   6 3_YW    QK1EXOPEN   
   7 42DG          LDN   0  0
   9 42Y6          STO   0  AWORK1(2)
   8 43CQ          CALL  5  XHUNT
  18 43XB          STOZ     EMARK(3)            [ZEROISE RESULT MARKER   
  11 44C2 ...      TRACE    EXMARK(2),EXOPEN 
   9 44WL ...      TRACE    ASUSN(2),
   4 45B=    #   
  11 45TW    #     CHECK VALID ENTRY PARAMS  
   4 46*G    #   
   9 46T6          LDX   0  EXMARK(2)
  17 47#Q          ANDN  0  BOPEN               [BOPEN MUST BE SET   
   8 47SB          BNZ   0  XOPOK
  16 48#2    XMARK GEOERR   1,MARKERR           [OTHERWISE ERROR 
   9 48RL    XOPOK LDX   0  EXMARK(2)
  10 49?=          ANDN  0  BCREATE+BRITE
   8 49QW          BNZ   0  XMARK
   4 4==G    #   
   9 4=Q6          LDX   0  EXMARK(2)
   8 4?9Q          ANDN  0  BLOCK
  19 4?PB          BNZ   0  PS1                 [JUMP IF UNIT ALREADY LOCKED 
   9 4#92          LDX   0  EXMARK(2)
   8 4#NL          ANDN  0  BSERL
  21 4*8=          BZE   0  SERCH               [JUMP IF SERIAL NUMBER NOT SPECIFIED 
   4 4*MW    #   
  11 4B7G    #     SERIAL NUMBER SPECIFIED   
  12 4BM6    #     GET UNIT ONLINE IF NECESSARY  
   4 4C6Q    #   
  12 4CLB          USEST    ASUSN(2),P2A,,P2F,P2H
   4 4D62    #   
  18 4DKL    #     UNIT NOW MARKED ^TO BE KEPT ONLINE^ + SCALO CLAIMED   
   4 4F5=    #   
   8 4FJW    P2G   CALL  5  XHUNT
   9 4G4G          LDX   0  ASUSN(2) 
  19 4GJ6          STO   0  ESERL(3)            [PUT SERIAL NUMBER IN GEXD   
  18 4H3Q          BRN      PS1                 [GO TO SEARCH FIP CELLS  
   4 4HHB    #   
  14 4J32    #     UNIT NOT ONLINE OR IN USE BY S-TRUSTED
   4 4JGL    #   
   8 4K2=    P2A   CALL  5  XHUNT
   9 4KFW          LDX   0  EXMARK(2)
  21 4K_G          ANDN  0  BSUPP               [CHECK IF TO SUPPRESS LOAD MESSAGE   
  15 4LF6          BZE   0  P2C                 [JUMP IF NOT 
  16 4LYQ          KEEPMESS XEWN(1),ASUSN(2)    [UNIT NOT ONLINE 
   8 4MDB          LDN   4  ANONL
   8 4MY2          BRN      P2D  
   4 4NCL    #   
  13 4NX=    #     SCALO NOT AVAILABLE - IPB FAILURE 
   4 4PBW    #   
  14 4PWG    P2H   LDN   4  APLINKF             [IPB FAIL
   8 4QB6          BRN      P2D  
   4 4QTQ    #   
  15 4R*B    #     OUTPUT REQUEST FOR UNIT TO BE PUT ONLINE  
   4 4RT2    #   
  12 4S#L    P2C   MUSEST   ASUSN(2),P2E,P2F,,P2H
   8 4SS=          BRN      P2G  
   4 4T?W    #   
   9 4TRG    #     CANTDO TO REQUEST 
   4 4W?6    #   
   8 4WQQ    P2E   LDN   4  ARITE
   8 4X=B          BRN      P2D  
   4 4XQ2    #   
  17 4Y9L    #     BREAK-IN WHILST WAITING FOR UNIT TO BE PUT ONLINE 
   4 4YP=    #   
  14 4_8W    P2F   LDN   4  ABREAK              [BREAKIN 
   8 4_NG    P2D   CALL  5  XHUNT
   9 5286          ORS   4  EMARK(3) 
   8 52MQ          BRN      XERR 
   4 537B    #   
  18 53M2    #     BSERL NOT SPECIFIED - START SEARCH OF ALL ONLINE UNITS
   4 546L    #   
  18 54L=    SERCH STOZ     ASUSN(2)            [ SEARCH ALL ONLINE UNITS
   8 555W          LDN   1  BCCB 
  17 55KG          CALL  7  NEXTSU              [GET NEXT UNIT ONLINE
  18 5656          BRN      TF4                 [NO SUITABLE UNIT FOUND  
   4 56JQ    #   
  16 574B    #     SEARCH FIP CELLS OF SPECIFIED OR CURRENT UNIT 
   4 57J2    #   
  15 583L    PS1   CALL  7  XOPEN               [OPEN UNIT   
   8 58H=    PS1A  LDN   0  147  
  17 592W          STO   0  HOME(2)             [START OF FIP CELLS  
   9 59GG    PS1B  LDX   1  HOME(2)  
  16 5=26          CALL  5  XREAD               [READ NEXT CELL  
  16 5=FQ          BRN      TF1                 [TRANSFER ERROR  
   8 5=_B    PS1C  LDX   0  0(1) 
  17 5?F2          BNG   0  US1                 [CHECK IF CELL UNUSED
   8 5?YL          LDX   5  2(1) 
   8 5#D=          LDX   6  3(1) 
  16 5#XW          LDX   7  4(1)                [CHECK FILENAMES 
   9 5*CG          TXU   5  FIP(2)   
   9 5*X6          TXU   6  FIP+1(2) 
   9 5BBQ          TXU   7  FIP+2(2) 
  17 5BWB          BCS      P5                  [JUMP IF DIFFERENT   
   9 5CB2          LDX   0  EXMARK(2)
   8 5CTL          ANDN  0  BSPEC
  20 5D*=          BZE   0  P6                  [JUMP IF GEN NUMBER NOT SPECIFIED
  16 5DSW          LDX   0  6(1)                [OTHERWISE CHECK 
   8 5F#G          SRL   0  12   
   9 5FS6          LDX   4  FIP+4(2) 
   8 5G?Q          SRL   4  12   
   7 5GRB          TXU   0  4
  17 5H?2          BCS      P5                  [JUMP IF DIFFERENT   
   9 5HQL    PS1D  LDX   0  ESERL(3) 
  17 5J==          STO   0  ASUSN(2)            [SAVE SERIAL NUMBER  
   8 5JPW          ADN   2  FIP  
   7 5K9G          ADN   1  2
   8 5KP6          MOVE  1  15   
   8 5L8Q          SBN   2  FIP  
   7 5LNB          SBN   1  2
  17 5M82          LDX   6  1(1)                [POINTER TO FDCELL   
   7 5MML    PS1E  LDN   0  1
   9 5N7=          STO   0  FBUK(2)  
   9 5NLW          LDEX  7  FIP+5(2) 
   7 5P6G          LDX   4  7
   7 5PL6          SLL   4  1
   7 5Q5Q          ADX   4  7
  20 5QKB          SLL   4  1                   [X6 =WORDS REQUIRED FOR FDCELLS  
  10 5R52          ADN   4  FDCELLS-A1   
  18 5RJL          TXU   4  ALOGLEN(2)          [JUMP IF BLOCK ALREADY   
  17 5S4=          BCC      PS1F                [THE CORRECT LENGTH  
   7 5SHW          LDX   3  2
  17 5T3G          ALTLEN   3,4                 [ALTER LENGTH OF EXOF
   8 5TH6          CALL  5  XHUNT
  16 5W2Q    PS1F  BZE   7  P8                  [JUMP IF NO CELLS
   8 5WGB          ADN   4  A1-6 
  17 5X22          STO   4  EFDC(3)             [POINTER TO LAST CELL
   7 5XFL    PS1G  LDX   1  6
   8 5X_=          CALL  5  XREAD
  16 5YDW          BRN      TF2                 [TRANSFER FAIL   
   9 5YYG          LDX   6  FIP+8(2) 
  15 5_D6          ANDN  6  #77                 [BUCKET SIZE 
  19 5_XQ          ADX   2  EFDC(3)             [REORGANIZE CELL INTO EXOF   
  16 62CB          LDX   0  3(1)                [SERIAL NUMBER   
   8 62X2          SMO      FX1  
   8 63BL          ANDX  0  NX4  
   8 63W=          STO   0  0(2) 
  21 64*W          LDX   5  1(1)                [GET STARTING CYLINDER,STARTING BLOCK
   8 64TG          SMO      FX1  
   8 65*6          ANDX  5  NX5  
  16 65SQ          DVS   4  4(1)                [   IN X5 AND X4 
  12 66#B          STO   5  1(2)                [
   8 66S2          STO   4  2(2) 
   7 67?L          SBN   4  1
   7 67R=          SBN   5  1
   8 68=W          NGS   4  4(2) 
   8 68QG          NGS   5  3(2) 
  16 69=6          LDX   5  2(1)                [CALCULATE EB,EC 
   8 69PQ          SMO      FX1  
   8 6=9B          ANDX  5  NX5  
   7 6=P2          SBN   5  1
   8 6?8L          DVS   4  4(1) 
  19 6?N=          ADS   5  3(2)                [NUMBER OF CYLINDERS IN AREA 
  19 6#7W          ADS   4  4(2)                [NUMBER OF SEGMENTS/CYLINDER 
  21 6#MG          LDX   4  3(2)                [CALCULATE NUMBER OF BUCKETS IN AREA 
  16 6*76          MPY   4  4(2)                [X4= TOTAL BLOCKS
   7 6*LQ          DVD   4  6
   8 6B6B          BZE   4  PS1H 
   9 6BL2          GEOERR   1,EXBUCK 
  19 6C5L    PS1H  STO   5  5(2)                [NUMBER OF LB'S IN THIS AREA 
   8 6CK=          LDCT  4  #400 
   8 6D4W          ANDX  4  2(1) 
  18 6DJG          ORS   4  3(2)                [SAVE FULL DEPTH MARKER  
   7 6F46          LDN   0  6
  17 6FHQ          SBS   0  EFDC(3)             [UPDATE  CELL POINTER
   7 6G3B          SBN   7  1
  17 6GH2          LDX   6  0(1)                [ADDRESS NEXT CELL   
  18 6H2L          BZE   7  P8                  [ JUMP IF NO MORE CELLS  
  17 6HG=          BNZ   6  PS1G                [BACK FOR NEXT CELL  
   8 6H_W          BRN      TF3  
   4 6JFG    #   
   9 6J_6    #     NO MORE FILE AREAS
   4 6KDQ    #   
   9 6KYB    P8    STOZ     EMARK(3) 
  21 6LD2          CALL  5  XHUNT               [DO CUMULATIVE TOTALS FOR HIGHEST LBN
   9 6LXL          LDEX  4  FIP+5(2) 
  15 6MC=          BZE   4  P8B                 [JUMP IF NONE
   9 6MWW          ADN   2  FDCELLS-1
  18 6NBG    P8A   LDX   0  0(2)                [HIGHEST LBN IN THIS AREA
  21 6NW6          ADS   0  6(2)                [ADD TO NUMBER IN NEXT AREA GIVING   
  18 6P*Q                                       [HIGHEST LBN IN THAT AREA
   7 6PTB          ADN   2  6
  19 6Q*2          BCT   4  P8A                 [BACK FOR NEXT AREA IF ANY   
   8 6QSL    P8B   CLOSEST  XEND 
  10 6R#=          TESTREPNOT   OK,XER1  
   8 6RRW          BRN      XEND 
   4 6S?G    #   
  11 6SR6    #     FILENAME OR GEN DIFFERENT 
   9 6T=Q    #     GET NEXT FIP CELL 
   4 6TQB    #   
  17 6W=2    P5    LDX   0  0(1)                [POINTER TO NEXT CELL
  20 6WPL          BNZ   0  P5A                 [JUMPIF NEXT CELL NOT CONSECUTIVE
   8 6X9=          LDN   1  17   
  17 6XNW          ADS   1  ECOR(3)             [UPDATE CORE POINTER 
  16 6Y8G          ADS   1  HOME(2)             [ UPDATE B.S HOME
   9 6YN6          LDX   1  ECOR(3)  
   8 6_7Q          BRN      PS1C 
   8 6_MB    P5A   SMO      FX1  
  18 7272          TXU   0  NX5                 [CHECK IF END OF CHAIN   
  16 72LL          BCC      US1                 [JUMP IF IT IS   
   9 736=          STO   0  HOME(2)  
   8 73KW          BRN      PS1B 
   4 745G    #   
   9 74K6    #     NO MORE USED CELLS
   4 754Q    #   
   9 75JB    US1   LDN   0  ANFOUND  
   9 7642    US1A  ORS   0  EMARK(3) 
   8 76HL          CLOSEST  US1B 
  10 773=          TESTREPNOT   OK,XER1  
   8 77GW          CALL  5  XHUNT
   9 782G    US1B  LDX   0  EXMARK(2)
   8 78G6          ANDN  0  BSERL
  20 78_Q          BNZ   0  S3                  [JUMP IF SERIAL NUMBER SPECIFIED 
   9 79FB    US1C  LDX   6  ESERL(3) 
   8 79_2          SMO      FX2  
  15 7=DL          STO   6  AWORK4              [PRESERVE X6 
   8 7=Y=          MFINDSU  1,6  
  17 7?CW          CALL  7  NEXTSU              [GET NEXT S.U,IF ANY 
  16 7?XG          BRN      S3                  [JUMP IF NO MORE 
   8 7#C6          SMO      FX2  
   9 7#WQ          LDX   6  AWORK4   
   8 7*BB          LDCT  0  #400 
   9 7*W2          ANDX  0  EMARK(3) 
  14 7B*L          ERS   0  EMARK(3)            [UNSET   
  20 7BT=          BNZ   0  PS1                 [J IF NOT TO UNLOCK PREVIOUS UNIT
   7 7C#W          LDX   1  6
  15 7CSG          CALL  7  XUNLOCK             [UNLOCK UNIT 
   7 7D#6          RELST    6
   8 7DRQ          CALL  5  XHUNT
  18 7F?B          BRN      PS1                 [BACK TO SEARCH NEXT UNIT
   4 7FR2    #   
  11 7G=L    #     NO MORE UNITS TO SEARCH   
   4 7GQ=    #   
   9 7H9W    S3    LDX   0  EXMARK(2)
   8 7HPG          ANDN  0  BHIGH
  17 7J96          BZE   0  Q4                  [J IF NOT HIGHEST GEN
   4 7JNQ    #   
  14 7K8B    #     HIGHEST GEN SEARCH-CHECK IF FILE FOUND
   4 7KN2    #   
   8 7L7L          LDCT  0  #200 
   9 7LM=          ANDX  0  EMARK(3) 
  18 7M6W          BZE   0  Q3                  [JUMP IF FILE NOT FOUND  
   9 7MLG          LDX   0  ASUSN(2) 
  20 7N66          TXU   0  ESERL(3)            [CHECK IF FOUND ON CURRENT UNIT  
  16 7NKQ          BCC      S3A                 [JUMP IF SAME SU 
   9 7P5B          LDX   1  ESERL(3) 
  15 7PK2          CALL  7  XUNLOCK             [UNLOCK UNIT 
  18 7Q4L          RELST    ESERL(3)            [AND RELEASE CURRENT UNIT
   8 7QJ=          CALL  5  XHUNT
   9 7R3W          LDX   0  ASUSN(2) 
   9 7RHG          STO   0  ESERL(3) 
  15 7S36          CALL  7  XOPEN               [OPEN UNIT   
  16 7SGQ    S3A   LDX   6  EFDC(3)             [START OF FDCELLS
   9 7T2B          LDX   0  EHOME(3) 
  16 7TG2          STO   0  HOME(2)             [GET HOME OF FIP 
   8 7T_L          BRN      PS1E 
   4 7WF=    #   
  15 7WYW    #     HIGHEST OR ANY GENERATION NUMBER SEARCH   
   4 7XDG    #   
   9 7XY6    P6    LDX   0  EXMARK(2)
   8 7YCQ          ANDN  0  BANY 
  18 7YXB          BNZ   0  PS1D                [J0MP IF ANY GEN NUMBER  
   9 7_C2          LDX   0  EXMARK(2)
   8 7_WL          ANDN  0  BHIGH
   8 82B=          BNZ   0  P6A  
  19 82TW          GEOERR   1,WRNGEN            [ERROR IN GEN SPECIFICATION  
   8 83*G    P6A   LDCT  0  #200 
   9 83T6          ANDX  0  EMARK(3) 
  19 84#Q          BZE   0  P6B                 [JUMP IF NO FILE FOUND YET   
   9 84SB          LDX   0  FIP+4(2) 
   8 85#2          SRL   0  12   
   8 85RL          LDX   4  6(1) 
   8 86?=          SRL   4  12   
   7 86QW          TXL   0  4
  20 87=G          BCC      P5                  [JUMP IF THIS GEN LESS THAN ONE  
  14 87Q6    P6B   ADN   1  2                   [SAVED   
   8 889Q          ADN   2  FIP  
   8 88PB          MOVE  1  15   
   7 8992          SBN   1  2
   8 89NL          SBN   2  FIP  
   8 8=8=          LDX   0  1(1) 
  17 8=MW          STO   0  EFDC(3)             [POINTER TO FDCELLS  
   9 8?7G          LDX   0  HOME(2)  
  16 8?M6          STO   0  EHOME(3)            [SAVE B S HOME   
   9 8#6Q          LDX   4  ASUSN(2) 
  13 8#LB          LDX   0  ESERL(3)            [SAME
  21 8*62          STO   0  ASUSN(2)            [OTHERWISE SAVE SERIAL NUMBER IN EXOF
   8 8*KL          LDCT  0  #200 
   9 8B5=          ANDX  0  EMARK(3) 
   8 8BJW          BZE   0  P6C  
  18 8C4G          TXU   4  ESERL(3)            [JUMP IF PREVIOUSLY SAVED
  17 8CJ6          BCC      P6C                 [UNIT IS CURRENT UNIT
   7 8D3Q          LDX   1  4
   9 8DHB          CALL  7  XUNLOCK  
  19 8F32          RELST    4                   [UNLOCK AND RELEASE OLD UNIT 
   8 8FGL          CALL  5  XHUNT
   8 8G2=    P6C   LDCT  0  #600 
  19 8GFW          ORS   0  EMARK(3)            [SET FILE FOUND AND CURRENT  
  19 8G_G                                       [UNIT NOT TO BE RELEASED BITS
   9 8HF6          LDX   1  ECOR(3)  
   8 8HYQ          BRN      P5   
   4 8JDB    #   
   8 8JY2    Q4    LDN   0  BLOCK
   9 8KCL          ANDX  0  EXMARK(2)
  19 8KX=          BNZ   0  Q3A                 [JUMP IF UNIT LOCKED ON ENTRY
   9 8LBW    Q3    LDX   1  ESERL(3) 
   9 8LWG          CALL  7  XUNLOCK  
  18 8MB6          RELST    ESERL(3)            [UNLOCK AND RELEASE UNIT 
   8 8MTQ          CALL  5  XHUNT
   9 8N*B    Q3A   LDX   0  EMARK(3) 
  18 8NT2          BNZ   0  XERR                [JUMP IF ERROR OCCURRED  
   4 8P#L    #   
   8 8PS=    XEND  SETREP   OK   
   8 8Q?W          SMO      FX2  
   9 8QRG          LDX   0  AWORK1   
   8 8R?6          BZE   0  XEND1
   9 8RQQ          ACROSS   CHINTEG,1
   5 8S=B    XEND1 UP
   4 8SQ2    #   
   8 8T9L    #     FAILURE CASES 
   4 8TP=    #   
   4 8W8W    #   
  14 8WNG    #     TRANSFER FAIL WHILE READING FIP CELLS 
   4 8X86    #   
   8 8XMQ    TF1   CALL  5  XHUNT
   8 8Y7B          LDN   0  AFAIL
  15 8YM2          ORS   0  EMARK(3)            [SET FAIL BIT
   9 8_6L          LDX   0  EXMARK(2)
   8 8_L=          ANDN  0  BSERL
  21 925W          BZE   0  US1C                [JUMP IF SERIAL NUMBER NOT SPECIFIED 
   8 92KG          BRN      S3   
   4 9356    #   
   4 93JQ    #   
  13 944B    #     TRANSFER FAIL WHILST READING FACS 
   4 94J2    #   
   8 953L    TF2   CALL  5  XHUNT
   8 95H=          LDN   0  AFAIL
   9 962W          ORS   0  EMARK(3) 
   8 96GG          BRN      Q4   
   4 9726    #   
   8 97FQ    #     SCA CORRUPT   
   4 97_B    #   
   8 98F2    TF3   LDX   1  FX1  
  11 98YL          KEEPMESS XEWS(1),ESERL(3) 
   8 99D=          CALL  5  XHUNT
   8 99XW          LDN   0  AFAIL
   9 9=CG          ORS   0  EMARK(3) 
   8 9=X6          CLOSEST  TF3A 
   9 9?BQ          TESTREPNOT OK,XER1
   8 9?WB    TF3A  CALL  5  XHUNT
   8 9#B2          BRN      Q3   
   4 9#TL    #   
   4 9**=    #   
  10 9*SW    #     NO SUITABLE SU FOUND  
   4 9B#G    #   
   8 9BS6    TF4   CALL  5  XHUNT
   9 9C?Q          LDX   0  EMARK(3) 
   9 9CRB          ANDN  0  ABREAK   
  16 9D?2          BNZ   0  XERR                [J IF BREAK IN   
   9 9DQL          LDN   0  ANFOUND  
   9 9F==          ORS   0  EMARK(3) 
   4 9FPW    #   
   9 9G9G    XERR  SETREP   EXFAIL   
   8 9GP6          SMO      FX2  
   9 9H8Q          LDX   0  AWORK1   
   8 9HNB          BZE   0  XEND1
   9 9J82          ACROSS   CHINERR,1
   4 9JML    #   
   4 9K7=    #   
   4 9KLW    #   
  10 9L6G          MENDAREA 30,K99EXOPEN 
   4 9LL6    #END
   6 ____ ...376130770003
