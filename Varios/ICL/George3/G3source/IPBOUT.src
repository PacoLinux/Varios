  14 22FL    #SEG  IPBOUT8                      [R.WEYAND
  16 22PD ...[  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD. 1982 
  16 22_=    #LIST          K0IPBOUT>K0IPB>K0ALLGEO>K0GREATGEO   
   9 23DW                   8HIPBOUT 
   4 23YG    #   
  12 24D6          SEGENTRY K1IPBOUT,SK1IPBOUT   
  20 24XQ    #     ISSUES ALL THE WRITE ORDERS TO THE IPB ; AMOP/IPBOUT ACTIVITY 
  12 25CB          SEGENTRY K4IPBOUT,SK4IPBOUT   
  21 25X2    #     SEND SUPERVISORY SEGMENT TO AM6P/IPBOUT ; USUALLY CPAT ACTIVITY   
  12 26BL          SEGENTRY K5IPBOUT,SK5IPBOUT   
  17 26W=    #     SEND TIME+DATE TO IPB CONSOLES ; ACTCDT ACTIVITY  
   4 26YD    #   
  20 26YQ    #     SUBROUTINE TO SEARCH FOR AND SEND ANY CRASH CLOSE SUPERVISORY 
  20 26_4    #     SEGMENTS IN THE OUTPUT Q. BEFORE THE SEG IS SENT ALL PRECEDING
  18 26_B    #     SEGMENTS WITH THE SAME IDENTIFIER NO. ARE DISCARDED.  
   4 26_N    #   
   6 2722    OCRASHCLOSE 
  21 272#          STOZ     4                   [INDICATE SEARCH FOR CRASH CLOSE SEG 
  21 272L    OCRAS BCHX  3  /                   [PICK UP POSSIBLE SUPERVISORY CODE   
   9 272Y          LDCH  0  IPBIDE(3)
   9 273=          ANDX  3  BITS22LS 
   8 273J          SBN   0  23   
  17 273W          BZE   0  OGOOD               [CRASH CLOSE SEGMENT 
  18 2748    OLOOKA                             [LOOK AT NEXT IOUT BLOCK 
  11 274G          HUNT2J   3,AMXOR,IOUT,,(7)
  21 274S          BZE   4  OCRAS               [STILL LOOKING FOR CRASH CLOSE SEG   
   8 2756          TXU   3  GEN0 
  21 275D          BCC      TSEND               [END OF SEARCH : SEND CRASH CLOSE SEG
   9 275Q          LDX   0  IPBIDE(3)
   8 2764          ANDN  0  #7777
   7 276B          SBX   0  4
  21 276N          BNZ   0  OLOOKA              [NOT MATCHING IDEN NO:CONTINUE SEARCH
  21 2772          LDX   0  FPTR(3)             [PICK UP NEXT IOUT PTR AND PRESERVE  
  13 277#          STO   0  GEN2                [IT  
  18 277L          FREECORE 3                   [DISCARD THIS IOUT BLOCK 
  20 277Y          SMO      GEN2                [CAUSE SEARCH TO FIND NEXT IOUT  
   8 278=          LDX   3  BPTR 
   9 278J          BRN      OLOOKA   
  21 278W    OGOOD STO   3  GEN0                [PRESERVE PTR TO CRASH CLOSE IOUT BLK
   9 2798          LDX   4  IPBIDE(3)
  21 279G          ANDN  4  #7777               [SET X4 +VE WITH IDENTIFIER NUMBER   
  19 279S          LDX   3  FX2                 [NOW SEARCH FOR AND DISCARD  
  20 27=6          BRN      OLOOKA              [RELEVANT PRECEDING IOUT BLOCKS  
   4 27=D    #   
  21 27=Q    #     SUBROUTINE TO CHECK FOR RESET/REOPEN SEGMENTS(BATCHED). IF TRUE,  
  15 27?4    #     JUMPS TO ^TWAL^ TO KICK IPBIN, ELSE EXITS.
   4 27?B    #   
   7 27?N    TREOP BCHX  3  /
  16 27#2          LDCH  0  IPBIDE(3)           [SUPERVISORY CODE
   9 27##          ANDX  3  BITS22LS 
   8 27#L          SBN   0  11   
  18 27#Y          BNZ   0  (7)                 [NOT RESET SEGMENT : EXIT
  21 27*=          FREECORE 3                   [FINISHED WITH ^REOPEN^ SUPER BLOCK  
  21 27*J          BRN      TWAL    [WAIT FOR IPBIN TO WAKE IPBOUT WHEN READ FINISHES
   4 27*W    #   
  19 27TG    #     SUBROUTINE TO CHECK FOR CLOSE DOWN SEGMENT ; CALL ON X7   
   4 28*6    #   
   7 28SQ    TCLOS BCHX  3  /
  16 29#B          LDCH  0  IPBIDE(3)           [SUPERVISORY CODE
   7 29S2          SBN   0  9
  16 2=?L          BNZ   0  TEX                 [NOT CLOSE DOWN  
  10 2=R=          LDX   3  IPBRING(2)   
   8 2?=W          LDCT  0  #40  
   9 2?QG          SBN   3  IPBRING  
  16 2#=6          ORS   0  IPBSW(3)            [INHIBIT READS   
  21 2#FY          LONGON1  CCREOPEN            [KICK IPBIN IF WAITING IN ^CCREOPEN^ 
   8 2#PQ          LDCT  0  #100 
  21 2*9B          ANDX  0  IPBSW(3)            [NO READ IN PROGRESS IF IPBIN ASLEEP 
   8 2*P2          BZE   0  (7)  
   9 2B8L          ERS   0  IPBSW(3) 
   7 2BN=          LDX   2  3
  17 2C7W          FPUT                         [WAKE IPBIN IF ASLEEP
  14 2CMG          BRN      TSUI                [SUICIDE 
   9 2D76    TEX   ANDX  3  BITS22LS 
   7 2DLQ          EXIT  7  1
   4 2F6B    #   
  21 2FL2    #     SUBROUTINES FOR USE BY LOCKC MACROS TO REFIND BLOCKS BEING LOCKED 
   4 2G5L    #   
  10 2GK=    TFI   MHUNTW   2,AMXOR,IOUT 
   7 2H4W          EXIT  1  0
   8 2HJG    TIF   LDX   2  FX2  
   7 2J46          EXIT  1  0
   4 2JHQ    #   
  19 2K3B    #     SUBROUTINE TO TEST WHETHER IPB IS MOPPED OFF OR INOPERABLE
   4 2KH2    #   
  10 2L2L    TOFF  LDN   0  IPBRING(3)   
  10 2LG=          TXU   0  IPBRING(3)   
  17 2L_W          BCC      TEXT                [EQUAL : MOPPED OFF  
   8 2MFG          LDCT  0  #420 
   9 2M_6          ANDX  0  IPBSW(3) 
  17 2NDQ          BNG   0  TNOP                [B0 SET : INOPERABLE 
  17 2NYB          BNZ   0  TEXT                [B4 SET : MOPPING OFF
   7 2PD2          EXIT  7  0
   4 2PXL    #   
  21 2QC=    #     SUBROUTINE TO CHAIN IOUT BEHIND IPBOUT ACTIVITY AND FPUT IF NEEDED
   4 2QWW    #   
  18 2RBG    TWA   LDX   2  IPBRING(3)          [AMOP/IPBIN ADDRESS IN X3
   9 2RW6          SBN   2  IPBRING  
  10 2S*Q          LDX   2  ACTRING(2)   
   9 2STB          SBN   2  ACTRING  
  18 2T*2          CHAIN    1,BPTR(2)           [AMXOR/IOUT ADDRESS IN X1
  10 2TSL          LDX   2  IPBRING(3)   
   9 2W#=          SBN   2  IPBRING  
   8 2WRW          LDCT  0  #100 
   9 2X?G          ANDX  0  IPBSW(2) 
  17 2XR6          BZE   0  (7)                 [IPBOUT ALREADY AWAKE
   9 2Y=Q          ERS   0  IPBSW(2) 
   6 2YQB          FPUT  
   7 2_=2          EXIT  7  0
   6 2_=G ...#UNS CCERROR
   4 2_?2 ...(   
   4 2_?G ...[   
   8 2_#2 ...XOUT        4HOUT   
   4 2_#G ...[   
  15 2_*2 ...[     SUBROUTINE TO OUTPUT AN AMXOR AJOURN BLOCK
   4 2_*G ...[   
   6 2_B2 ...XSUBJOURN   
   8 2_BG ...      SBX   7  FX1  
  12 2_C2 ...      SETNCORE   7,2,AMXOR,AJOURN   
   9 2_CG ...      LDX   0  XOUT(1)  
   8 2_D2 ...      STO   0  A1(2)
   8 2_DG ...      LDX   0  FTIME
   9 2_F2 ...      STO   0  A1+1(2)  
   9 2_FG ...      LDX   0  FTIME+1  
   9 2_G2 ...      STO   0  A1+2(2)  
   8 2_GG ...      SMO      FX2  
   8 2_H2 ...      LDX   1  CPPTR
   8 2_HG ...      LGEOG     1,6 
   9 2_J2 ...      STO   6  A1+3(2)  
   9 2_JG ...      LDN   4  IBSW(3)  
   9 2_K2 ...      LDN   5  A1+4(2)  
   7 2_KG ...      MOVE  4  3
  18 2_L2 ...      CHAINJRN            [MACRO TO CHAIN AMXOR AJOURN BLOCK
   8 2_LG ...      LDX   1  FX1  
   8 2_M2 ...      LDX   2  FX2  
   8 2_MG ...      ADX   7  FX1  
   7 2_N2 ...      EXIT  7  0
   4 2_NG ...)   
   4 2_PL    #   
   4 329=    #   
   6 32=M    VIRTUAL7900 
  18 32?6    #     THIS IS A MESSAGE BUFFERING COMMUNICATIONS SUBSYSTEM  
  14 32?K    #     OUTPUT ACTIVITY FOR A VIRTUAL 7900 .  
  20 32#4    #     IF THE VIRTUAL 7900 DOES NOT HAVE A WMC READ TRANSFER STORED  
   9 32#H    #     THEN GO TO SLEEP  
  19 32*2    #     IF THE VIRTUAL 7900 DOES HAVE A WMC READ TRANSFER STORED  
   6 32*F    #     THEN  
  17 32*Y    #     1. LOCK THE OUTPUT BLOCK AND REENTER THE MAIN PATH
  20 32BC    #     2. SKIP CODE WHICH ACCESSES THE DEVICE LIST (SKIP IS X7 -VE)  
  21 32BW    #        NOTE THE REPLY WORD (CPRW1) IS PRESET TO TRANSFER SUCCESSFUL   
  21 32C*    #     3. AT LABEL TWAK INSTEAD OF COOR1 GO DOWN TO K5VIPBPERI TO PERFORM
  17 32CS    #        THE TRANSFER OF THE ^IPB BLOCK^ TO THE PROGRAM 
  20 32D?    #        IF THE RETURN IS UP, IE WMC READS WERE NO LONGER STORED ,  
  21 32DQ    #        RECHECK FOR EXCEPTION FLAGS AND STORED READS BEFORE SLEEPING   
  20 32F9    #        IF THE RETURN IS UP +1, IE WMC READ TRANSFER PERFORMED ,   
  21 32FN    #        CONTINUE ON THE MAIN PATH (^END OF TRANSFER^ CODE,INC MOPTRACE)
  19 32G7          SMO      CPPTR(2)            [-> VIRTUAL 7900 DEVICE LIST 
   9 32GL          LDX   0  AVCREADS 
  21 32H5          BZE   0  TNOUT               [NO WMC READ TRANSFER STORED: SLEEP  
  17 32HJ          LOCKC    3,TFI               [LOCK THE IOUT BLOCK 
  16 32J3          MHUNTW   3,AMXOR,IOUT        [MAY HAVE MOVED  
  18 32JG          BRN      TESTIMOPTY          [REENTER THE MAIN PATH   
   6 32J_    V7900PERI   
  10 32KD          DOWN     VIPBPERI,5   
  20 32KX          BRN      SK1IPBOUT           [WMC READS WERE NO LONGER STORED 
  21 32LB                                       [RECHECK FOR FLAGS AND STORED READS  
  19 32LT          BRN      TREPL               [WMC READ TRANSFER PERFORMED 
  18 32M#                                       [CONTINUE ON MAIN PATH   
   4 32MR    #   
   8 32NW    TOLIN CALL  7  TCLOS
  14 338G          BRN      TSUI                [SUICIDE 
   8 33D#          LDCT  0  #420 
   9 33N6          ANDX  0  IPBSW(2) 
  21 33XY          BZE   0  OVERLOAD            [NOT INOP/MP OFF,PM:DEAL W/ OVERLOAD 
  20 347Q    TNOUT LDCT  0  #100                [NO IOUT OR IPB INOP OR SYS O/L  
  17 34MB    TASL  ORS   0  IPBSW(2)            [MARK IPBOUT ASLEEP  
  19 3572          UNLOCK   2                [UNLOCK IPBOUT ACTIVITY BLOCK   
   6 35LL          COOR1 
   4 366=    #   
   6 36KW    SK1IPBOUT   
  10 375G          HUNTW    3,AMXOR,IOUT 
  19 37K6          BNG   3  TNOUT               [NO IOUT BLOCK : GO TO SLEEP 
  16 384Q          LDCT  0  #620                [B4 : MOP OFF,PM 
   9 38JB          ANDX  0  IPBSW(2) 
  16 3942          BNZ   0  TOLIN               [OVERLOAD OR INOP
   4 39HL    #   
   4 3=3=    #   
  21 3=GW    #     SET UP WRITE CONTROL AREA ; FIND SOURCE ACTIVITY IF NECESSARY , TO
  19 3?2G    #     WAKE AFTER OUTPUT , OR ADJUST CONCEPTUAL OVERLOAD COUNT   
   4 3?G6    #   
   5 3?SH    SENDBLK 
   9 3?YB          LDX   7  IPBSW(2) 
   7 3?_M          SLC   7  9
  20 3#2Y          BNG   7  VIRTUAL7900         [IPBOUT ACT IS COMMUNICATING WITH
  16 3#49                                       [A VIRTUAL 7900  
  16 3#6Y          LOCKC    3,TFI               [AMXOR/IOUT BLOCK
  17 3#FB          LOCKC    2,TIF               [AMOP/IPBOUT BLOCK   
  19 3#_2          JBUSY    ,TGE                [CONTROL AREA BUSY : GEOERR  
  16 3*DL          LDN   0  #11                 [END WRITE MODE  
  10 3*Y=          DCA      ,STO,0,MODE  
   9 3BCW          STOZ     CPRW1(2) 
  18 3BXG          MHUNTW   3,AMXOR,IOUT        [MAY HAVE MOVED IN LOCKC 
  10 3CC6          LDX   0  ALOGLEN(3)   
  19 3CWQ          SBN   0  IPBIDE-A1-1         [SIZE OF DATA SEG +1 FOR BSW 
  10 3DBB          DCA      ,STO,0,COUNT 
  19 3DW2          LDN   0  IBSW(3)             [START OF BLOCK TO BE OUTPUT 
  10 3F*L          DCA      ,STO,0,CTSA  
   6 3FBX ...#UNS CCERROR
   9 3FD8 ...      CALL  7  XSUBJOURN
   6 3FFF    TESTIMOPTY  
   9 3FGQ          LDX   0  IMOPTY(3)
   7 3FJ3          ANDN  0  1
   9 3FK#          STOZ     AWORK2(2)
  20 3FLK          BZE   0  TPERI               [BRANCH IF ACT NOT TO BE WOKEN   
   4 3HRQ    #   
  17 3J?B          LDX   2  IPBRING(2)          [VIA RING TO IPBIN   
   9 3JR2          SBN   2  IPBRING  
  20 3K=L          HUNT2J  2,AMXOR,ADEVS,,  TPERI [FROM THERE TO ROUTING BLOCK   
   9 3KQ=          LDX   4  IPBIDE(3)
   8 3L9W          ANDN  4  #7777
  11 3LPG          SBX   4  A1+FOURTHWD(2)   
  16 3M96          SLL   4  2                   [MULT IDE BY 4   
   7 3MNQ          SMO      4
  18 3N8B          LDX   2  A1+BPTR(2)          [FROM ADEVS ENTRY TO DCB 
  20 3NN2          BZE   2  TPERI               [NO  DCB : ACTY SUICIDED ALREADY 
   9 3P7L          SMO      FPTR(2)  
   9 3PM=          LDX   5  FOURTHWD 
  20 3Q6W          SLL   5  5                   [B5 SET IF ONLINE TO CONCEPTUAL  
   8 3QLG          BPZ   5  TNOOL
  18 3R66          ADX   2  BSTART(2)           [POINT TO START OF CONDCB
   9 3R?#          LDXC  0  IMOPTY(3)
  18 3RDG          BCC      TH1                 [J IF NOT TO BE REFLECTED
  16 3RH5          LDX   5  2                   [SAVE CONDCB ADDR
  11 3RKN          NAME     3,AMXOR,ICONIN   
  18 3RQW          CHAIN    3,BPTR(2)           [GIVE IT BACK TO IPBCCA  
  15 3RRK          HUNTACT  AMOP,3,5           [FIND IPBCCA  
   9 3RS#          LDEX  0  ATYPE(3) 
  18 3RT3          BZE   0  SK1IPBOUT           [J IF IPBCCA NOT ON COOR3
   7 3RTQ          LDX   2  3
  15 3RWF          UNQUEUE                      [WAKE IPBCCA 
   6 3RX8          FPUT  
  17 3RY4          BRN      SK1IPBOUT           [GO ON TO NEXT BLOCK 
  18 3S5B    TNOOL SBN   2  A1+FPTR             [POINT TO START OF ADCB  
   9 3SK2          LDX   0  IWAIT(2) 
  16 3T4L          BPZ   0  TPERI               [TO WAKE ACTIVITY
  18 3TJ=          LDX   5  2                   [KEEP COPY OF DCB ADDS   
   9 3W3W    TH1   LDX   2  BPTR(2)  
   9 3WHG          LDX   0  ATYPE(2) 
  17 3X36          TXL   0  CACT                [POINT TO ACTIVITY   
   8 3XGQ          BCS      TH1  
  20 3Y2B          BPZ   5  TORD                [ORDINARY OUTPUT : NOT CONCEPTUAL
   8 3YG2          ORX   2  GSIGN
   8 3Y_L    TORD  SMO      FX2  
   9 3_F=          STO   2  AWORK2   
   4 3_YW    #   
   8 42DG    TPERI LDX   2  FX2  
  19 42QW          BNG   7  TRESP               [VIRTUAL 7900 : SKIP GPERI   
   8 42Y6          GPERI    ,2   
  15 436B    TRESP LDX   6  CPRW1(2)            [REPLY WORD  
  18 43XB          BPZ   6  TACC                [ACTION ALREADY COMPLETE 
   7 44C2          SLC   6  5
  19 44WL          BNG   6  TINOP               [BR IF REJECTED INOPERABLE   
   7 45B=          SRC   6  1
  17 45TW          BNG   6  TEND                [REJECTED OPERABLE   
   4 46*G    #   
  16 46T6    TACC  STOZ     IMOPTY(3)           [WRITE ACCEPTED  
   9 47#Q          LDX   2  AWORK2(2)
  16 47SB          BZE   2  TWAK                [NO FPUT REQUIRED
  15 48#2          BPZ   2  TJUW                [NOT ONLINE  
  10 48RL          LDX   0  ALOGLEN(3)   
  20 49?=          SBS   0  IOUTLOAD(2)         [IPBCCA'S OUTPUT OVERLOAD COUNT  
   8 49QW          BRN      TWAK 
  21 4==G    TJUW  FPUT                         [WAKE ACTIVITY CONTROLLING IDENTIFIER
   8 4=Q6          LDCT  0  #400 
   7 4?9Q          SMO      5
   8 4?PB          ERS   0  IWAIT
   4 4#92    #   
  21 4#*N    TWAK  BNG   7  V7900PERI           [VIRTUAL 7900:PERFORM XFER TO PROG   
  21 4#C=          COOR1                        [WAIT FOR END OF TRANSFER CHAINING   
  15 4#DS    TREPL LDX   0  CPRW1(2)            [REPLY WORD  
   7 4*MW          SLC   0  5
  16 4B7G          BPZ   0  TINOP               [BRANCH IF INOP  
   7 4BM6          SRC   0  2
  16 4C6Q          BNG   0  TERR                [BRANCH IF ERROR 
   8 4CLB          LDCT  0  #220 
   9 4D62          ANDX  0  CPRW1(2) 
  16 4DKL          BNZ   0  TEND                [J IF NOT IMPL   
   4 4F5=    #   
   9 4FJW          LDX   0  CPRW1(2) 
   8 4G4G          ANDN  0  #7777
  21 4GJ6          BNZ   0  TEND                [ENDED : ONLY ONE WORD READ BY DCP   
   8 4H3Q    #SKI K6IPB>100-100  
   4 4HHB    (   
  20 4J32    [TRACES (ONLINE) SUCCESSFUL CC TRANSFERS IF ^MOP TRACE^ SWITCHED ON 
   9 4JGL          LDX   0  ASWITCH1 
   7 4K2=          SLL   0  7
   9 4KFW          BPZ   0  TRACEX   
  20 4KSS          LDX   0  IPBSW(2)            [TRACE IPBSW IN PLACE OF THE BSW 
  19 4L7Q          ORS   0  A1+1(3)             [7903 DOES NOT CHECK THE BSW 
   7 4LGN          LDN   0  1
   7 4LTL    TALL  SMO      0
   8 4M8J          '177  0  A1(3)
   7 4MHG          8HIPBOUT  
   7 4MWD          ADN   0  1
  10 4N9B          TXU   0  ALOGLEN(3)   
   8 4NJ#          BCS      TALL 
   5 4NX=    TRACEX  
   5 4PBW    [TRACEE 
   4 4PWG    )   
  19 4QB6          CALL  7  TCLOS               [CHECK WHETHER 'CLOSE DOWN'  
  18 4QTQ          BRN      TIS                 [YES : COMPLETE CLOSURE  
  21 4R5J          CALL  7  TREOP             [IF ^REOPEN^,WAKEUP IPBIN & J TO TWAL  
   9 4R*B          SEGENTRY K33IPBOUT
   7 4RT2          FREECORE 3
   9 4S#L          BRN      K1IPBOUT 
   4 4SS=    #   
  19 4T?W    #     WRITE REJECTED OPERABLE OR ENDED : WAIT FOR READ TO FINISH
   4 4TRG    #   
   7 4W?6    TEND  UNLOCK   3
  10 4WQQ          LDX   1  IPBRING(2)   
   8 4X=B          LDCT  0  #100 
  16 4XQ2          SBN   1  IPBRING             [POINT TO IPBIN  
   9 4Y9L          ANDX  0  IPBSW(1) 
  18 4YP=          BZE   0  TWAL                [READ ALREADY IN PROGRESS
   9 4_8W          ERS   0  IPBSW(1) 
  18 4_NG          LDX   2  1                   [WAKE IPBIN TO ISSUE READ
   6 5286          FPUT  
  21 52GH    TWAL  LONGON1  CCREOPEN            [KICK IPBIN IF WAITING IN ^CCREOPEN^ 
  20 52SY          LDCT  0  1                   [SET B8 FOR IPBIN TO WAKE IPBOUT 
  17 537B          BRN      TASL                [WHEN READ FINISHES  
   4 53M2    #   
  18 546L    #              [NOW SET UP & ISSUE DUMMY WRITE CANCEL READ  
   4 54L=    #   
  21 54RC    TIS   JV7900   ,TSUI               [VIRTUAL 7900: SKIP WRITE CANCEL READ
   8 54T6          LDCT  0  #400 
  13 54WT          STO   0  AWORK3(2)           [BSW 
  16 55KG          LDN   0  #117                [CODE #17 = NULL 
  15 5656          SLL   0  12                  [SET UP SS   
   9 56JQ          STO   0  AWORK4(2)
  19 574B          JBUSY    ,TGE                [CONTROL AREA BUSY : GEOERR  
   7 57J2          LDN   0  4
  21 583L          DCA      ,ORS,0,MODE         [CHANGE WRITE TO WRITE CANCEL READ   
   7 58H=          LDN   0  2
  16 592W          DCA      ,STO,0,COUNT        [WD COUNT OF 2   
   9 59GG          LDN   0  AWORK3(2)
  10 5=26          DCA      ,STO,0,CTSA  
   8 5=FQ          GPERI    ,2   
   9 5=_B          LDX   6  CPRW1(2) 
  18 5?F2          BPZ   6  TAC15               [ACCEPTED AND COMPLETE   
   7 5?YL          SLC   6  5
  17 5#D=          BNG   6  TSUI                [REJECTED INOPERABLE 
   7 5#XW          SRC   6  1
  17 5*CG          BNG   6  TSUI                [REJECTED OPERABLE   
   6 5*X6    TAC15 COOR1 
  20 5BBQ    TSUI  DERINGD  IPBRING(2)          [IN CASE SUICIDE IS NOT IN CORE  
   7 5BWB          SUICIDE   
   4 5BWH    #   
  21 5BWN    #     DCP IN OVERLOAD : FILTER OUTPUT QUEUE ACCORDING TO WARNING LEVEL  
  16 5BWT    #     FIRST WARNING LEVEL = NO SUSPENSION OF OUTPUT.
  17 5BX2    #     SECOND WARNING LEVEL = SUSPENSION OF BULK OUTPUT. 
  21 5BX7    #     FIFTH WARNING LEVEL = SUSPENSION OF BULK AND INTERACTIVE OUTPUT   
  20 5BX#    #              EXCEPT BIACKS(AND BINAK)SEGMENTS FOR NON-CONCEPTUAL  
  21 5BXF    #              IDENTIFIERS WILL BE OUTPUT AFTER ^SIEVING^ ANY ASSOCIATED
  16 5BXL    #              DATA SEGMENTS IN FRONT OF THE BIACKS.
  21 5BXR    #     EXCEPTION:BEYOND FIRST W.L. ALL CRASH CLOSE SEGS IN Q ARE OUTPUT. 
   4 5BXY    #   
   5 5BY5    OVERLOAD
   7 5BY6          BCHX  3  /
   9 5BY7          LDCH  0  IPBIDE(3)
  16 5BY8          ANDX  3  BITS22LS            [LD. SUPERVISORY 
   8 5BY9          SBN   0  11   
   2 5BY=
   2 5BY?
  16 5BY#          BZE   0  SENDBLK         [ J. IF RESET REOPEN 
  21 5BY*          ORX   2  GSIGN               [GET OVERLOAD CODE:STORED BY IPBIN IN
  16 5BYC          LDCH  5  IPBSW(2)            [IPBOUT'S IPBSW  
   8 5BYJ          LDX   2  FX2  
   7 5BYP          SBN   5  2
  21 5BYW          BNG   5  TSEND          [CODE 1=PR 0=1ST WARN LEVEL : SEND BLOCK  
  20 5B_3          CALL  7  OCRASHCLOSE         [SEND ALL CRASH CLOSE SEGS IN Q  
  21 5B_8          LDX   3  FPTR(2)             [RESET X3 POINTING TO FIRST IOUT IN Q
  21 5B_*          LDX   1  IPBRING(2)          [FIND ADEVS ENTRY OF THE IDENTIFIER  
  18 5B_G          SBN   1  IPBRING             [OWNING THIS IOUT BLOCK  
  12 5B_M          HUNT2J   1,AMXOR,ADEVS,,TNOUT 
  17 5B_S          STO   1  GEN0                [PRESERVE ADEVS PTR  
   9 5B__    OLOOK LDX   4  IPBIDE(3)
   8 5C26          ANDN  4  #7777
  21 5C2?          BZE   4  TSEND               [BLK CONTAINS SYSTEM SUPER SEG : SEND
  11 5C2D          SBX   4  A1+FOURTHWD(1)   
   7 5C2K          SLL   4  2
   7 5C2Q          ADX   1  4
  21 5C2X          LDEX  0  A1+FOURTHWD(1)      [CHECK FOR INTERACTIVE DEVICE TYPE   
  19 5C34          BZE   0  ONOTBULK            [INTERACTIVE DEVICE TYPE 7071
   7 5C39          SBN   0  5
  19 5C3B          BZE   0  ONOTBULK            [INTERACTIVE DEVICE TYPE 7023
   7 5C3H          SBN   0  5
  21 5C3N          BNZ   0  ONEXT               [BULK IDENTIFIER IOUT BLOCK : IGNORE 
  19 5C3T                                       [INTERACTIVE DEVICE TYPE 7181
  19 5C42    #              IOUT BLOCK BELONGS TO AN INTERACTIVE IDENTIFIER. 
   5 5C47    ONOTBULK
  21 5C4#          BZE   5  TSEND          [CODE 2=PR 10=2ND WARN LEVEL : SEND BLOCK 
  21 5C4F    #              CODE 3=PR 20=5TH WARN LEVEL & INTERACTIVE IDENT IOUT BLK 
  11 5C4L          LDX   0  A1+FOURTHWD(1)   
   7 5C4R          SLL   0  5
  21 5C4Y          BNG   0  ONEXT               [ID ONLINE TO CONCEPTUAL:IGNORE BLOCK
   9 5C55          LDCH  0  IPBIDE(3)
  21 5C5=          BNZ   0  ONEXT               [IDENTIFIER SUPERVISORY SEG : IGNORE 
  10 5C5C          LDX   0  IPBIDE+1(3)  
   7 5C5J          SLL   0  6
  21 5C5P          BNG   0  TSEND               [BINAK /STOP SIEVING DATA SEG : SEND 
   7 5C5W          SLL   0  1
  18 5C63          BNG   0  OBIAC               [BIACK DATA SEG : SEND   
  18 5C68    ONEXT HUNT2J   3,AMXOR,IOUT,,TNOUT [LOOK AT NEXT IOUT BLOCK 
  17 5C6*          LDX   1  GEN0                [RESTORE ADEVS PTR   
   8 5C6G          BRN      OLOOK
   4 5C6M    #   
  21 5C6S    #              BIACK DATA SEGMENT : SIEVE ALL DATA SEGMENTS OF THE SAME 
  20 5C6_    #              IDENTIFIER NUMBER WHICH PRECEDE THE BIACK USING THE  
  21 5C76    #              CONTROL DISCARD MASK IN ISTATUS OF THE RELEVANT DCB BLOCK
  15 5C7?    #              THEN SEND THE BIACK DATA SEGMENT.
   4 5C7D    #   
  10 5C7K    OBIAC LDX   2  A1+BPTR(1)   
  21 5C7Q          BZE   2  TSEND               [NO ADCB BLOCK?: SEND BIACK DATA SEG 
  20 5C7X          SBN   2  A1+FPTR             [POINT TO IDENTIFIER'S ADCB BLOCK
  17 5C84          STO   2  GEN1                [PRESERVE ADCB PTR   
   8 5C89          LDX   1  FX2  
  21 5C8B    ODSEG HUNT2J   1,AMXOR,IOUT,,ODSEG [SEARCH FOR PRECEDING DATA SEGMENTS  
   7 5C8H          TXU   1  3
  21 5C8N          BCC      TSEND               [END OF SEARCH : SEND BIACK DATA SEG 
   9 5C8T          LDX   0  IPBIDE(1)
   9 5C92          TXU   0  IPBIDE(3)
  21 5C97          BCS      ODSEG               [DATA SEG'S IDEN NOT = BIACK'S:IGNORE
  14 5C9#    #                  SIEVE THIS DATA SEGMENT  
  21 5C9F          LDX   0  ISTATUS(2)          [EXTRACT CURRENT CONTROL DISCARD MASK
  21 5C9L          ANDX  0  IPBIDE+1(1)         [TEST WITH DATA SEG'S DISCARD MASK   
   9 5C9R          ANDX  0  HALFTOP  
  20 5C9Y          SLL   0  8                   [ONLY BITS 8 TO 11 ARE RELEVANT  
  21 5C=5          BZE   0  ODSEG               [DATA SEG CAN NOT BE DISCARDED:IGNORE
  18 5C==          LDX   0  FPTR(1)             [PICK UP NEXT IOUT PTR   
  16 5C=C          STO   0  GEN2                [AND PRESERVE IT 
   9 5C=J          LDX   4  IMOPTY(1)
  16 5C=P          FREECORE 1                   [DISCARD DATA SEG
  21 5C=W    #                  WAKE IDENTIFIER'S CONTROLLING ACTIVITY IF REQUESTED  
   7 5C?3          ANDN  4  1
  15 5C?8          BZE   4  OCUS                [NO REQUEST  
   9 5C?*          LDXC  0  IWAIT(2) 
  21 5C?G          BCC      OCUS                [CONTROLLING ACTIVITY ALREADY AWAKE  
  17 5C?M          STO   0  IWAIT(2)            [CLEAR ASLEEP FLAG   
  21 5C?S    OACT  LDX   2  BPTR(2)             [HUNT FOR ACTIVITY BLOCK & KICK IT   
   9 5C?_          LDX   0  ATYPE(2) 
   8 5C#6          TXL   0  CACT 
   8 5C#?          BCS      OACT 
   6 5C#D          FPUT  
  20 5C#K    OCUS  SMO      GEN2                [CAUSE SEARCH TO FIND NEXT IOUT  
   8 5C#Q          LDX   1  BPTR 
  16 5C#X          LDX   2  GEN1                [RESTORE ADCB PTR
  16 5C*4          BRN      ODSEG               [CONTINUE SEARCH 
   4 5C*9    #   
  12 5C*B    #              SEND THIS IOUT BLOCK 
  21 5C*H    TSEND CHAIN    3,FX2               [ENSURE THIS IOUT BLOCK IS FIRST IN Q
   9 5C*N          BRN      SENDBLK  
   4 5CB2    #   
  20 5CJ=    #     IPB INOPERABLE : LET IPBIN MARK AND REPORT THE INOPERABILITY  
  19 5CQG    #     IF RESET/REOPEN BLOCK TRIED, REPORT REOPEN FAILED TO IPBIN
   4 5CYQ    #   
  16 5D72    #     IPB ERROR : SEND MESSAGE TO CENTRAL CONSOLE   
   4 5D*=    #   
   9 5DSW    TMESS1         +JIPBINOP
   6 5F24    #UNS CCERROR
   4 5F7=    #SKI
   8 5F#G    TMESS2      +JIPBERR
   4 5FS6    #   
   4 5FW*    #   
   5 5FYJ    TINOP   
   7 5F_M          UNLOCK   3
   7 5G2R          BCHX  3  /
   9 5G52          LDCH  0  IPBIDE(3)
   9 5G79          ANDX  3  BITS22LS 
   8 5G9D          SBN   0  11   
  18 5G?M          BNZ   0  TCL                 [NOT RESET/REOPEN BLOCK  
  21 5G*W          LDX   1  IPBRING(2)          [REPORT ^REOPEN^ FAILED TO AMOP/IPBIN
   9 5GD5          SBN   1  IPBRING  
  20 5GG#          LDCT  4  #200                [BY SETTING REOPEN FAILED FLAG   
  18 5GJH          ORS   4  IPBSW(1)            [(B1 OF IPBIN'S IPBSW)   
   9 5GLQ          LONGON1  CCREOPEN 
  21 5GN_          CHAIN    3,2                 [PUT RESET/REOPEN BLOCK AT HEAD OF Q 
   7 5GR8          LDX   2  3
  21 5GTC    TCL   HUNT2J   3,AMXOR,IOUT,2,TSL  [CHECK FOR PRESENCE OF CLOSE DOWN SEG
  17 5GWG          LDX   2  FX2                 [RESET X2 FOR TCLOSE 
   8 5GXL          CALL  7  TCLOS
  20 5G_T          BRN      TSUI                [SUICIDE IF CLOSE DOWN SEGMENT   
   8 5H44    TSL   LDX   2  FX2  
  21 5H6?          BRN      TNOUT               [IF NOT,GO TO SLEEP IGNORING OVERLOAD
   4 5H8G    #   
   6 5H8Q    #UNS CCERROR
   4 5H92    (   
   7 5H97    TERR  UNLOCK   3
  21 5H9#          CCERROR  CCTRANSFER,CPRW1(2) [REPORT TRANSFER ERROR + (REPLY WD)  
  21 5H9G                                       [AND SUSPEND TIL MOP OFF INITIATED   
   4 5H9Q    )   
   6 5H=2    #UNS CCERROR
   4 5H==    #SKI
   4 5H=G    (   
  15 5H?2    TERR  LDX   6  TMESS2(1)           [IPB ERROR   
   9 5HQL    TMESS LDX   0  IPBSW(2) 
  20 5J==          BNG   0  K1IPBOUT            [ALREADY MARKED INOPERABLE : COOR
  10 5JPW          LDX   3  IPBRING(2)   
   8 5K9G          LDCT  0  #400 
  18 5KP6          ORS   0  IPBSW-IPBRING(3)    [MARK IPBIN INOPERABLE   
   9 5L8Q          ORS   0  IPBSW(2) 
   8 5LNB          LGEOG    ,5   
  16 5M82          CALL  7  TNOU                [OUTPUT MESSAGE  
   4 5MCS    )   
   9 5MML          BRN      K1IPBOUT 
   4 5N7=    #   
   8 5NLW    TNOU  SBX   7  FX1  
   8 5P6G          OUTNUM   5,0  
   8 5PL6          INFORMX  ,6,1 
   8 5Q5Q          ADX   7  FX1  
   7 5QKB          EXIT  7  0
   4 5R52    #   
  19 5RJL    #     ENTRY VIA CCOUT MACRO TO OUTPUT SUPERVISORY SEGMENT TO IPB
   4 5S4=    #   
   6 5SHW    SK4IPBOUT   
  13 5T3G          LDX   6  ACOMMUNE1(2)        [CODE
  15 5TH6          LDX   7  ACOMMUNE2(2)        [IDENTIFIER  
  15 5W2Q          LDX   3  ACOMMUNE3(2)        [ADDS IPBIN  
  10 5W4H          LDX   5  ACOMMUNE1(2) 
   8 5W56          ANDN  5  #7700
  21 5W5P          SRL   5  6                   [NO. OF WORDS IN SUPERVISORY SEGMENT 
  19 5W6#          ADN   5  IPBIDE-IMOPTY       [ACCOUNT FOR IMOPTY AND IBSW 
  11 5W6X          SETUPCORE 5,1,AMXOR,IOUT,1
  20 5W7G          STOZ     IMOPTY(1)           [ACT NOT TO BE WOKEN AFTER OUTPUT
  18 5W85          LDCT  4  #400                [SET NO-DATA-SEGMENTS BIT
  13 5W8N          LDCH  0  ACOMMUNE1(2)        [CODE
   7 5W9?          SBN   0  5
  18 5W9W          BNZ   0  SBSW                [NOT ^ILLEGAL^ SUPER CODE
  19 5W=F          ORX   4  CACT                [SET VITAL-SUPER-SEGMENT BIT 
  21 5W?4    SBSW  STO   4  IBSW(1)             [SET RELEVANT BITS IN BLK STATUS WORD
   8 5W?M          SLL   6  12   
   7 5W#=          DSA   7  6
   9 5W#T          STO   6  IPBIDE(1)
  11 5W*D          SBN   5  IPBIDE-IMOPTY+2  
  19 5WB3          BNZ   5  OUTSS               [NOT A 2 WORD SUPER SEGMENT  
  18 5WBL          LDX   0  ACOMMUNE4(2)        [SECOND WORD OF SEGMENT  
  10 5WC9          STO   0  IPBIDE+1(1)  
  17 5WCS    OUTSS CALL  7  TWA                 [CHAIN BEHIND IPBOUT 
   5 62CB          UP
   4 62X2    #   
  20 63BL    #     ENTRY FROM CHARGESC TO OUTPUT TIME AND DATE TO IPB CONSOLES   
  21 63W=    #     REMIND OPERATORS OF INOPERABLE , MOPPED-ON IPBS ; ACTCDT ACTIVITY 
   4 64*W    #   
   6 64TG    SK5IPBOUT   
  19 652N          LDN   3  IPBADDR-IPBL        [SET X3 -> FIRST AMXOR/IPBIN 
   5 657W    SETACOMS
  10 65*6          STOZ     ACOMMUNE1(2) 
   8 65SQ          LDX   0  ACES 
   8 662J          LDCT  6  #400 
  10 664G          STO   6  ACOMMUNE2(2) 
   8 66S2          LDCT  6  #707 
  19 67?L          STO   0  ACOMMUNE8(2)        [SPACE-FILL IN CASE DATE ONLY
   8 67R=          LDN   7  20   
  10 68=W          STO   0  ACOMMUNE9(2) 
   7 68QG          SRL   6  3
  17 69=6          STO   7  ACOMMUNE4(2)        [MESSAGE CHAR COUNT  
  21 69PQ          FINDRET  1                   [POINT TO BLOCK CONTAINING TIME+DATE 
  20 6=9B          STO   6  ACOMMUNE3(2)        [WORD COUNT + T/W DISPLAY CODE   
  17 6=P2          OUTLEN   1,6                 [GET CHAR COUNT IN X6
  20 6?8L          SRL   6  2                   [5 WORDS IF TIME+DATE ; 3 IF DATE
  10 6?N=          LDN   5  ACOMMUNE5(2) 
  20 6#MG          LDN   4  A1+2(1)             [ADDRESS OF TIME + DATE MESSAGE  
  20 6*76          SMO      6                 [ONLY DATE AFTER 'IP DATE' COMMAND 
   7 6*LQ          MOVE  4  0
  19 6BL2    TEXT  LDX   3  IPBL(3)             [ADDRESS OF NEXT AMOP/IPBIN  
  17 6C5L          BZE   3  TOUT                [FINISHED : SUICIDE  
  20 6CK=          CALL  7  TOFF                [TEST FOR MOP OFF OR INOPERABLE  
  10 6D4W          HUNTW    1,AMXOR,IOUT 
  21 6DJG          BPZ   1  TNAL                [PRESENT IF PREV. IPB JUST MOPPED OFF
  14 6F46          SETNCORE 9+IMOPTY-A1,1,AMXOR,IOUT,1   
  10 6FHQ          LDN   4  ACOMMUNE1(2) 
   9 6G3B          LDN   5  IMOPTY(1)
   7 6GH2          MOVE  4  9
  20 6H2L          CALL  7  TOFF                [MAYBE MOPPED OFF DURING SETNCORE
  17 6HG=    TNAL  CALL  7  TWA                 [CHAIN BEHIND IPBOUT 
   8 6HQ4          LDX   2  FX2  
  18 6H_W          BRN      TEXT                [ACOMMUNE WORDS STILL OK 
   6 6J3?    #UNS CCERROR
   4 6J4N    (   
  21 6J65    TNOP  LDCT  0  2                   [TEST DCP ERROR(IPBOUT'S IPBSW'S B7) 
  10 6J7G          SMO      IPBRING(3)   
  10 6J8X          ANDX  0  IPBSW-IPBRING
  21 6J=#          BNZ   0  SETACOMS            [DCP ERROR : DON'T REPORT INOPERABLE 
  10 6J?P          LGEOG    CPPTR(3),5   
   4 6J*6    )   
   6 6JBH    #UNS CCERROR
   4 6JCY    #SKI
  10 6JFG    TNOP  LGEOG    CPPTR(3),5   
   8 6J_6          SMO      FX1  
   9 6KDQ          LDX   6  TMESS1   
   8 6KYB          CALL  7  TNOU 
  20 6L9Q          BRN      SETACOMS            [ACOMMUNE WORDS NEED TO BE RESET 
  17 6LH6    TOUT  FINDRET  1                   [FREE RETAINED BLOCK 
   7 6LSG          FREECORE 1
   7 6M5W          SUICIDE   
   4 6MC=    #   
   9 6MWW    TGE   GEOERR   1,CABUSY 
  10 6NBG          MENDAREA 40,K100IPBOUT
   4 6NW6    #END
   8 ____ ...27337472000100000000
