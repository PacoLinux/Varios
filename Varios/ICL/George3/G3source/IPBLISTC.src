  14 22FL    #SEG  IPBLISTC8                    [R.WEYAND
  16 22PD ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982.   
  11 22_=    #LIS  K0IPBLISTC>K0IPB>K0ALLGEO 
  10 23DW                   8HIPBLISTC   
   4 23YG    #   
  13 24D6          SEGENTRY K1IPBLISTC,SK1IPBLISTC   
  13 24XQ          SEGENTRY K2IPBLISTC,SK2IPBLISTC   
  13 2562          SEGENTRY K3IPBLISTC,SK3IPBLISTC   
  13 25#=          SEGENTRY K4IPBLISTC,SK4IPBLISTC   
  13 25*S          SEGENTRY K5IPBLISTC,SK5IPBLISTC   
  13 25BK          SEGENTRY K7IPBLISTC,SK7IPBLISTC   
   4 25CB    #   
   9 25X2    XCLOS          #01260000
   9 26BL    XCRAS          #01270000
   9 26W=    XOPEN          #01200000
   9 27*W    ZLIST          #76200000
   8 27TG    ZJKEN          +JKEN
   9 28*6    ZJMARY         +JMARY   
   9 28MG    ZJCO           +JCORRUPT
   9 28SQ    ZJRESET        +JRESET  
   9 28Y8    ZLISTA         #76350000
   9 293L    XMSK           #77700037
   9 2976    XPAGI          +JPAGES  
   4 29#B    #   
  20 29S2    #     FREE REDUNDANT OUTPUT BLOCKS FOR RESTART OR IPB INOPERABILITY 
   4 2=?L    #   
  21 2=FF    REFRE VFREEW   AMXOR,#77           [FREE ANY UNPACKED NPU(IOUT) PRESENT 
  21 2=M#          LDX   1  FX1                 [RESET X1 IN CASE IT WAS DESTROYED   
  19 2=T7          LDEX  5  AWORK1(2)           [COUNT OF IOUT BLOCKS WAITING
  15 2?34          BZE   5  (7)                 [NONE : EXIT 
  10 2?=W    TEKRA MFREEW   AMXOR,IOUT   
   8 2?QG          BCT   5  TEKRA
  15 2#=6          DEX   5  AWORK1(2)           [CLEAR COUNT 
   7 2#PQ          EXIT  7  0
   4 2*9B    #   
  20 2*P2    #     FREE REDUNDANT BLOCKS LEFT BY DEMAND IF NOT LIKELY TO RE-ENTER
   4 2B8L    #   
  10 2BN=    RIDEM MFREEW   ADATA,ADEMAND
  21 2C7W          VFREEW   GMON,AOUT           [ABSENT IF DEMANDY DELAY NOT ENDED   
  10 2CMG          MHUNTW   3,AMXOR,ADCB 
   9 2D76          SMO      IADIN(3) 
   8 2DLQ          LDX   0  IPBSW
  16 2F6B          BNG   0  (4)                 [IPB INOPERABLE  
  15 2FL2          EXIT  4  1                   [IPB OPERABLE
   4 2FLX    #   
  20 2FMS    #     IF IPB INOPERABLE,MOPPING OFF,OR FINISH NOW,EXIT+0 ELSE EXIT+1
   4 2FNP    #   
   6 2FPL    TESTIPBSTAT 
  10 2FQH          MHUNTW   3,AMXOR,ADCB 
  10 2FRD          SMO      A1+FPTR(3)   
   9 2FS*          LDX   0  FOURTHWD 
   9 2FT=          ORX   0  FINISH   
   7 2FW7          SLL   0  2
   9 2FX4          SMO      IADIN(3) 
   8 2FX_          ORX   0  IPBSW
   8 2FYW          BPZ   0  TISX 
  18 2F_R          LONGOFF                      [IN CASE LONGSET ISSUED  
  18 2G2N          VFREEW   GMON,ASET           [IN CASE OUTPER ISSUED   
   8 2G2X          LDCT  0  #100 
   9 2G36          ORS   0  CLONG1(2)
  20 2G3*          ERS   0  CLONG1(2)           [CLEAR ^GMON/ASET PRESENT^ FLAG  
   7 2G3K          EXIT  4  0
   7 2G4G    TISX  EXIT  4  1
   4 2G5L    #   
  21 2GK=    #     RING AMXOR/IOUT BLOCK(S) BEHIND IPBOUT ACTIVITY , WHICH IS THEN   
   8 2H4W    #     PUT ON LIST   
   4 2HJG    #   
   8 2J46    RINGO SBX   7  FX1  
  10 2JHQ    REF   MHUNTW   3,AMXOR,ADCB 
   9 2JWX          SMO      IADIN(3) 
   9 2K=4          LDX   2  IPBRING  
   8 2KK9          LDCT  0  #420 
  11 2KYB          ANDX  0  IPBSW-IPBRING(2) 
   8 2L?H          LDX   2  FX2  
  20 2LLN          BNZ   0  RGO                 [IPB INOP OR MOP OFF,PM : EXIT   
  20 2LNX    # A CHECK TO SEE IF MOP OFF IS BEING DONE IS NOT TESTED FOR AT THIS 
  21 2LR6    # STAGE AS WE ARE RIGHT AT THE END OF A LISTING AND MIGHT AS WELL LET   
  21 2LT*    # IT FINISH.  MOP OFF WILL COMPLETE OK BECAUSE OF A RESPONSE FROM THE DE
  18 2LWD    # EXCEPT MUST TEST MOP OFF IF WAITING ON WARNING CONDITION  
   4 2LXJ    #   
   8 2L_W          LDX   0  GSIGN
   8 2MFG          LDN   5  #100 
  19 2M_6          ANDX  5  IWAIT(3)         [B17 SET IF SENDING CRASH CLOSE 
   9 2NDQ          ERS   5  IWAIT(3) 
  21 2NYB          BNZ   5  NOWAR               [SUPERVISORY SEGMENT : IGNORE ISTATUS
  10 2PD2          LDX   4  ISTATUS(3)   
  21 2PXL          BNG   4  TINOP               [BECAME INOPERABLE WHILE WARNING SET 
   7 2Q3H          SLL   4  4
   8 2Q7D          BNG   4  TINOP
  10 2Q?*          LDX   4  ISTATUS(3)   
   7 2QC=          SLL   4  1
  17 2QWW          BPZ   4  NOWAR               [WARNING BIT NOT SET 
  10 2R44          CALL  4  TESTIPBSTAT  
   8 2R9=          BRN      NOID 
   8 2R?W          LDX   0  GSIGN
   8 2RBG          ADN   0  #2000
  21 2RW6          ORS   0  IWAIT(3)            [INDICATE WARNING NOTICED TO IPBIN   
  21 2S*Q          COOR1                        [WAIT UNTIL WARNING CONDITION ENDS   
   8 2STB          BRN      REF  
   5 2T*2    NOWAR   
  10 2TMC          HUNTW    1,AMXOR,IOUT 
  21 2T_S          BNG   1  SREPLY              [ALREADY SENT OUTPUT:CHECK FOR REPLY 
   7 2W#=          LDN   0  1
  21 2WRW          ERS   0  IMOPTY(1)           [WAKE IF DATA ; DONT WAKE IF SUP SEG 
   9 2X?G          ERX   0  IMOPTY(1)
   7 2XR6          SRC   0  9
  21 2Y=Q          ORS   0  IWAIT(3)            [SET B8 IF SUP SEG TO CHAIN REPLIES  
  19 2YQB          LDX   3  IADIN(3)            [ADDRESS OF IPBIN ACTIVITY   
  10 2_=2          LDX   3  IPBRING(3)   
  19 2_PL          SBN   3  IPBRING             [ADDRESS OF IPBOUT ACTIVITY  
  10 329=          LDX   2  ACTRING(3)   
  19 32NW          SBN   2  ACTRING             [FIND ACTIVITY AFTER IPBOUT  
   9 338G          LDX   4  BPTR(2)  
   8 33N6          SMO      FX2  
   9 347Q          LDEX  5  AWORK1   
   8 34MB          BRN      NOWCH
  18 3572    NEBO  MHUNTW   1,AMXOR,IOUT        [FIND NEXT OUTPUT BLOCK  
   8 35LL    NOWCH CHAIN    1,4  
   8 366=          BCT   5  NEBO 
  15 36KW          NGN   0  #1000               [#77777000   
  19 375G          ANDS  0  AWORK1(2)           [CLEAR COUNT OF OUTPUT BLOCKS
   8 37K6          LDCT  0  #100 
  18 384Q          ANDX  0  IPBSW(3)            [B2 SET IF IPBOUT ASLEEP 
  16 38JB          BZE   0  RGO                 [ALREADY ON LIST 
  16 3942          ERS   0  IPBSW(3)            [INDICATE ON LIST
   7 39HL          LDX   2  3
   6 3=3=          FPUT  
   8 3?2G    RGO   ADX   7  FX1  
   7 3?G6          EXIT  7  0
   4 3?_Q    #   
  20 3#FB    #     GET AMXOR/IOUT BLOCK FOR OUTPUT ; WORD COUNT IN X3 ON ENTRY   
   4 3#_2    #   
   8 3*DL    SETU  SBX   7  FX1  
  14 3*Y=          SETNCORE IDATA-A1(3),2,AMXOR,IOUT,1   
   8 3BCW          SMO      FX2  
  19 3BXG          STO   2  ACOMMUNE2           [PRESERVE AMXOR/IOUT ADDRESS 
   7 3CC6          LDN   0  1
   8 3CWQ          SMO      FX2  
  19 3DBB          ADS   0  AWORK1              [COUNT OF BLOCKS FOR OUTPUT  
  20 3DW2          STOZ     IMOPTY(2)           [MADE NON-ZERO IF OUTPUT ALONE   
  17 3F*L          STOZ     IBSW(2)             [BLOCK STATUS WORD   
   8 3FT=          LDCT  0  #400 
  10 3G#W          STO   0  IPBIDE+1(2)  
   8 3GSG          SMO      FX2  
   9 3H#6          LDX   0  JSOURCE1 
   8 3HRQ          ANDN  0  #7777
  17 3J?B          STO   0  IPBIDE(2)           [IDENTIFIER NUMBER   
   8 3JR2          ADX   7  FX1  
   7 3K=L          EXIT  7  0
   4 3KQ=    #   
  11 3L9W    #     SEND SUPERVISORY MESSAGE  
   4 3LPG    #   
   8 3M96    SUPER SBX   6  FX1  
   8 3MB#          SMO      FX2  
  19 3MHG          DSA   6  IWORK20             [PRESERVE RELATIVISED LINK   
  10 3MNQ          LDX   3  IDENTNO(3)   
  11 3N8B          SETNCORE 3,2,AMXOR,IOUT,1 
   7 3NN2          ADX   4  3
   9 3P7L          STO   4  IPBIDE(2)
   8 3PM=          LDX   0  GSIGN
  20 3Q6W          STO   0  IBSW(2)             [NO DATA SEGMENT IN THIS BLOCK   
   7 3QLG          LDN   0  1
   8 3R66          SMO      FX2  
  19 3RKQ          ADS   0  AWORK1              [COUNT OF BLOCKS TO BE OUTPUT
  17 3S5B          STO   0  IMOPTY(2)           [INVERTED IN RINGO   
  21 3S9?          LDX   2  FX2                 [PRESERVE SUPERVISORY CODE BEING SENT
   8 3S*8          SLC   4  12   
  17 3SF5          DCH   4  IWORK20(2)          [IN B0-5 OF IWORK20  
  19 3SFW          ADN   4  32                  [DERIVE EXPECTED REPLY CODE  
  20 3SGM          ANDN  4  #76                 [IGNORE B23 FOR CRASH CLOSE CODE 
  20 3SHD          BCHX  2  /                   [AND PRESERVE IN B6-11 OF IWORK20
  10 3SJ9          DCH   4  IWORK20(2)   
   8 3SK2          CALL  7  RINGO
   4 3SL=    #   
  11 3SMG    #     AWAIT SUPERVISORY REPLY   
   4 3SNQ    #   
   5 3SQ#    WTREP   
  10 3SRW          MHUNTW   3,AMXOR,ADCB 
   5 3SX2    SLOOP   
   9 3SX5          SMO      IADIN(3) 
   9 3SX8          LDX   2  IPBRING  
   8 3SX?          LDCT  0  #420 
  11 3SXB          ANDX  0  IPBSW-IPBRING(2) 
  19 3SXF          BNZ   0  SEX                 [IPB INOP OR MOPOFF,PM : EXIT
   8 3SXJ          LDCT  0  #100 
  10 3SXM          SMO      A1+FPTR(3)   
   9 3SXQ          ANDX  0  FOURTHWD 
   7 3SXT          SLL   0  1
  11 3SXY          ANDX  0  IPBSW-IPBRING(2) 
  21 3SY3          BNZ   0  SEX                 [MOPPING OFF DURING SYS OVERLOAD:EXIT
   8 3SY=          LDCT  0  #401 
  19 3S_G          ORN   0  #2000[MARK TO BE WOKEN IF REP IS FAIL(/UNAV/CONT)
  21 3T2Q          ORS   0  IWAIT(3)            [MARK ACTLF ASLEEP AWAITING SUPER SEG
   6 3T42          COOR1 
  10 3T5=          MHUNTW   3,AMXOR,ADCB 
   5 3T=W    SREPLY  
  11 3T?G          HUNTW    1,AMXOR,IDATSG   
  18 3T#Q          BNG   1  SFLAG               [NO SUPERVISORY SEGMENT  
  19 3T*S          LDX   5  A1+2(1)             [COMPARE REPLY CODE RECEIVED 
  18 3TBW                                       [WITH EXPECTED REPLY CODE
  18 3TCY          FREECORE 1                   [FREE SUPERVISORY INPUT  
  10 3TF2          ERX   5  IWORK20(2)   
   8 3TG4          SRL   5  12   
   8 3TH6          ANDN  5  #77  
  17 3TJ#          BNZ   5  SREPLY              [NOT EXPECTED REPLY  
  10 3TKG          LDCH  0  IWORK20(2)   
   8 3TLQ          SBN   0  16   
  19 3TN2          BZE   0  SOPENED             [SUPERVISORY CODE- OPEN SENT 
  15 3TNC    SEX   LDX   2  FX2                 [RESTORE LINK
   7 3TNF          LDCT  0  1
   8 3TNH          ADN   0  #2000
   9 3TNK          ORS   0  IWAIT(3) 
  21 3TNM          ERS   0  IWAIT(3)            [ENSURE BITS 8 & 13 ARE LEFT UNSET   
  10 3TNS          LDX   6  IWORK20(2)   
   8 3TP=          ANDN  6  #7777
   8 3TPW          ADX   6  FX1  
   7 3TQG          EXIT  6  0
   8 3TRQ    SFLAG LDX   1  FX1  
  10 3TT2          LDCH  0  IWORK20(2)   
   8 3TW=          SBN   0  22   
   9 3TWL          BZE   0  SCLOSENT 
   7 3TWQ          SBN   0  6
  20 3TWW          BNZ   0  SLOOP               [J UNLESS WAITING FOR OUTPUT SENT
   5 3TX2    SCLOSENT
  10 3W3J          LDX   7  ISTATUS(3)   
  19 3W7L          BNG   7  TINOP               [UNAVAILABLE OR FAIL REPLY   
   7 3W8_          SLL   7  4
   8 3W=#          BNG   7  TINOP
   8 3W?N          BRN      SLOOP
  21 3WCQ    SOPENED                            [CHANGE MODE/NPU SIZE IF NECESSARY   
   8 3WF2          BRN      SEX  
   4 3WHG    #   
  20 3X36    #     FREE PAGE RESTART BLOCK IF PRESENT BEFORE RETURN TO LISTFILE  
   4 3XGQ    #   
  10 3Y2B    VF    VFREEW   ADATA,IPTEMP 
  10 3YG2    VFA   VFREEW   ADATA,ACONV  
   7 3Y_L          EXIT  7  0
   4 3_2H    #   
   4 3_F=    #   
   4 3_F*    [   
   5 3_FL    ZDEAL   
   8 3_FP          SBX   5  FX1  
  21 3_FS          BNZ   7  WTOUS               [J IF SPOOLING TO WAIT FOR OUTPUT SEN
   9 3_FX          LONGSET  #36,SET,6
   5 3_G2    WTUNAV  
  17 3_G5          CALL  4  TESTIPBSTAT         [SEE IF MOP OFF ETC  
  14 3_G8          BRN      NOID                [J IF SO 
  10 3_G?          LDX   0  ISTATUS(3)   
  16 3_GB          BNG   0  ZUNAV               [J IF UNAVAILABLE
   8 3_GF          LDCT  4  #400 
   8 3_GJ          ADN   4  #2000
   9 3_GM          ORS   4  IWAIT(3) 
  17 3_GQ          COOR1                        [WAIT FOR UNAVAILABLE
   9 3_GT          BRN      WTUNAV   
   8 3_GY    ZUNAV ADX   5  FX1  
   7 3_H3          EXIT  5  0
   4 3_H6    [   
   8 3_H9    WTOUS LDX   2  FX2  
  10 3_H#          DSA   5  IWORK20(2)   
  17 3_HC    [                                   SAVE LINK ADDRESS   
  19 3_HG          LDN   4  28                  [SHOW WAITING FOR OUTPUT SENT
  18 3_HK          DCH   4  IWORK20(2)          [(THERE IS NO CODE 28)   
   8 3_HN          ADN   4  32   
   7 3_HR          BCHX  2  /
  10 3_HW          DCH   4  IWORK20(2)   
   8 3_H_          BRN      WTREP
   4 3_J4    [   
   4 3_J7    [   
   6 3_J=    SK3IPBLISTC 
   8 3_J*          CALL  7  RINGO
   8 3_JB          LDCT  7  #40  
  19 3_JC          CALL  5  ZDEAL             [WAIT FOR OUTPUT SENT REPORT...
  21 3_JD    [                                 ...WHICH SHOWS ALL PREV RECORDS OUTPUT
   7 3_K5          LDN   3  1
  17 3_K8          CALL  7  SETU                [SET IP A DATA BLOCK 
  16 3_K?          LDX   6  0                   [SAVE IDENT NO.  
   9 3_KB          LDX   0  ZLIST(1) 
   9 3_KF          STO   0  IDATA(2) 
  18 3_KJ          LDCT  0  #500                [SEND END OF DATA STREAM 
   7 3_KM          ADN   0  2
   8 3_KQ          LDCT  7  #40  
   8 3_KT          SMO      FX2  
  18 3_KY          ANDX  7  AWORK4              [BIT 3 SET IF SPOOLING   
  21 3_L3          ORX   0  7                   [BI 3 HERE MEANS ACKNOWLEDGE OUTPUT  
  10 3_L6          STO   0  IPBIDE+1(2)  
  15 3_L9          CALL  7  RINGO               [SEND OUTPUT 
   8 3_L#          LDCT  7  #40  
   9 3_LC          ANDX  7  AWORK4(2)
  17 3_LG          CALL  5  ZDEAL               [WAIT TILL DC4 SENT  
   5 3_LK    REPASK  
   6 3_LN          OUTPER
  17 3_LR          BZE   7  NSPLI               [J IF NOT SPOOLING   
   5 3_LW    TELCO   
   7 3_L_          LONGOFF   
  20 3_M4          INFORM   0,JCORRUPT,1        [JUST SAY CORRUPT AND CONTINUE   
   8 3_M7          BRN      RETLI
   5 3_M=    NSPLI   
  10 3_M*          CALL  4  TESTIPBSTAT  
  17 3_MD          BRN      NOID                [J IF MOP OFF ETC.   
  10 3_MH          LDXC  0  ISTATUS(3)   
  21 3_ML          BCC      TELCO               [IF ALREADY ENGAGED JUST TELL THEM   
   7 3_MP          LDN   0  0
   9 3_MS          DSA   0  AWORK4(2)
  16 3_MX          STOZ     AWORK3(2)           [PRESET CD REPLY 
  14 3_N2          DEMAND   0,JCORRUPT,REPCD,TERM,REDEM,2
   8 3_N5          CALL  4  RIDEM
   8 3_N8          BRN      RECL 
   5 3_N?    RETLI   
   9 3_N#          LDX   0  AWORK1(2)
  15 3_N*          BPZ   0  NOTP                [J. NOT TP   
  10 3_NB          ACROSS   IPBLISTE,4   
   4 3_NC    NOTP
  10 3_ND          ACROSS   IPBLISTB,4   
   4 3_NF    [   
   6 3_NJ    SK4IPBLISTC 
   8 3_NM          BPZ   7  RCRAL
   7 3_NQ          LDN   7  0
  10 3_NT    REPCO CALL  4  TESTIPBSTAT  
  17 3_NY          BRN      RNWCD               [J IF MOP OFF ETC.   
  10 3_P3          LDXC  7  ISTATUS(3)   
  16 3_P6          BCS      RCREQ                [J IF DEV UNAV  
   8 3_P9          LDCT  7  #401 
   8 3_P#          ORN   7  #2000
   9 3_PC          ORS   7  IWAIT(3) 
  18 3_PG          COOR1                        [WAIT TILL UNAVAILABLE   
  10 3_PK          MHUNTW   3,AMXOR,ADCB 
   7 3_PN          LDXC  7  7
  17 3_PR          ORS   7  IWAIT(3)            [UNSET BITS 8 AND 13 
   9 3_PW          ERS   7  IWAIT(3) 
   8 3_P_          BRN      REPCO
  10 3_Q4    RCRAL MHUNTW   3,AMXOR,ADCB 
   5 3_Q7    RCREQ   
   7 3_Q=          SLL   7  4
   8 3_Q*          BNG   7  RNWER
   8 3_QD          SMO      FX1  
   8 3_QH          LDX   5  ZJCO 
   7 3_QL    ZDEM  LDN   4  0
   6 3_QP          OUTPER
   5 3_QS    ZDECO   
  13 3_QX          DEMANDX  4,5,RNWCD,RNWTE,REPIN,2  
   8 3_R2          CALL  4  RIDEM
   8 3_R5 ...      BNG   7  TRYV 
  18 3_R8 ...      BNG   7  TRYV                [J IF DEV ERR ON HEADINGS
   8 3_R?          BRN      NDRQ 
   4 3_RB    [   
   5 3_RF    RNWCD   
  10 3_RJ          MHUNTW   3,AMXOR,ADCB 
   8 3_RM          LDN   0  #1000
   9 3_RQ          ORS   0  IWAIT(3) 
   7 3_RT          NGNC  0  1
   5 3_RY    RNWTE   
   7 3_S3          LDN   0  0
  17 3_S6          STO   0  AWORK3(2)           [INDICATE CD OR TE   
   8 3_S# ...      BNG   7  TRYV 
   8 3_SG    RNWER LDCT  0  #400 
   8 3_SK          SMO      FX2  
   9 3_SN          STO   0  AWORK3   
  17 3_SR    [                                   INDICATE DEV ERROR  
   8 3_SW          BRN      TOCRC
   4 3_S_    NDRQ
  10 3_T4 ...      ACROSS   IPBLISTD,4   
   5 3_T7    TOCRC   
  18 3_T=          CALL  7  REFRE               [FREE ANY REMAINING IOUTS
  10 3_T*          MHUNTW   3,AMXOR,ADCB 
   8 3_TD          LDN   0  #100 
  15 3_TH          LDX   4  XCRAS(1)            [CRASH CLOSE 
   9 3_TL          ORS   0  IWAIT(3) 
   8 3_TP          CALL  6  SUPER
   8 3_TS          LDN   6  #40  
   9 3_TX          ANDX  6  IWAIT(3) 
   8 3_W2          BNZ   6  TZECL
   8 3_W5          LDN   0  #100 
  15 3_W8          LDX   4  XOPEN(1)            [OPEN AGAIN  
   9 3_W?          ORS   0  IWAIT(3) 
   8 3_WB          CALL  6  SUPER
   5 3_WF    TZECL   
  20 3_WJ          CALL  7  VF                  [GET RID OF ANY UNWANTED BLOCKS  
   9 3_WM          LDX   7  AWORK3(2)
  18 3_WQ          BNG   7  TRESQ               [J IF ERROR ON HEADINGS  
   8 3_WT          BNZ   7  TATE 
  16 3_WY          BRN      TLFTE               [J FOR TERMINATE 
   5 3_X3    TRESQ   
   8 3_X6          SMO      FX1  
   9 3_X9          LDX   5  ZJRESET  
   8 3_X#          BRN      ZDEM 
   4 3_Y5    #   
  19 3_YW    #     ENTRY FROM IPBLISTB WHEN OUTPUTTING TO A 7020 VIA AN IPB  
  19 42DG    #     OR WHEN MOP OFF IN PROGRESS : CLOSE IDENTIFIER TO ENSURE  
  21 42Y6    #     ALL OUTPUT HAS BEEN LISTED ; IF FAIL REPLY CRASH CLOSE IDENTIFIER 
  15 43CQ    #     AND TAKE CAN'T DO ACTION TO RESTART LATER 
   4 43XB    #   
   6 44C2    SK5IPBLISTC 
  16 44WL          LDX   4  XCLOS(1)            [CODE FOR 'CLOSE'
  10 45B=          MHUNTW   3,AMXOR,ADCB 
   8 49QW          CALL  6  SUPER
   4 4==G    NOID
  20 4FJW          LDCH  6  IWORK9(2)           [CURRENT RESTART SECTION NUMBER  
   7 4GJ6          LDN   0  1
  20 4HHB          STOZ     AWORK3(2)           [SIMULATE CD FOR JUMP TO REBIG   
  20 4K2=          STO   0  ACOMMUNE6(2)        [FOR PAGE RESTART AFTER MOP OFF  
  10 4K#M          CALL  4  TESTIPBSTAT  
  21 4KM4          BRN      REBIG               [IPB INOP,MOPPING OFF,OR FINISH NOW  
  19 4KML          BRN      REBIG               [ SHOULD ALWAYS BE THE ABOVE 
   4 4KN8    #   
  19 4KNQ    #     ENTRY FROM IPBLISTB WHEN OUTPUTTING TO A 7020 VIA AN IPB  
  18 4KP#    #     WHEN LISTING HAS FINISHED : WAIT FOR OUTPUT SENT REPLY
  14 4KPW    #     IF FAIL REPLY CRASH CLOSE IDENTIFIER  
  15 4KQD    #     AND TAKE CAN'T DO ACTION TO RESTART LATER 
   4 4KR2    #   
   6 4KRJ    SK1IPBLISTC 
  17 4KS6          CALL  7  RINGO               [ SEND OUTPUT WITH   
  18 4KSN          LDCT  7  #40                 [ ^REPORT OUTPUT SENT^   
  16 4KT=          CALL  5  ZDEAL               [ WAIT FOR REPLY 
  20 4KTS          LDCH  6  IWORK9(2)           [CURRENT RESTART SECTION NUMBER  
   7 4KWB          LDN   0  1
  20 4KWY          STOZ     AWORK3(2)           [SIMULATE CD FOR JUMP TO REBIG   
  20 4KXG          STO   0  ACOMMUNE6(2)        [FOR PAGE RESTART AFTER MOP OFF  
  10 4KY4          CALL  4  TESTIPBSTAT  
  21 4KYL          BRN      REBIG               [IPB INOP,MOPPING OFF,OR FINISH NOW  
   4 4K_G    #   
   9 4NCL          LDX   5  AWORK4(2)
  21 4NX=          BNG   5  SPECS               [HAVE JUST LISTED TESTPATTERN FILE   
   8 4PBW          CALL  7  VF   
  17 4PWG ...      ACROSS   IPBLISTD,3          [OUTPUT TERMINATORS  
   4 4QB6    #   
   7 4QTQ    SPECS LDN   3  1
   8 4R*B          CALL  7  SETU 
  17 4RT2          LDX   6  0                   [IDENTIFIER NUMBER   
  10 4RYF          MHUNTW   3,AMXOR,ADCB 
  12 4S3Y          JBC      SPECA,3,ADCBTSPOOL   
  12 4S7C          JBC      SPECA,3,ADCB7502LP   
   9 4S=W          LDX   0  ZLISTA(1)
   8 4SB*          BRN      SPECB
   5 4SFS    SPECA   
   9 4SK?          LDX   0  ZLIST(1) 
   5 4SNQ    SPECB   
  18 4SS=          STO   0  IDATA(2)            [FF AND DISENGAGE PRINTER
  19 4T?W          LDCT  0  #500                [B2 FOR END OF DATA STREAM   
   7 4TRG          ADN   0  2
  10 4W?6          STO   0  IPBIDE+1(2)  
   8 4WQQ          CALL  7  RINGO
   8 4W_C          LDCT  7  #40  
   9 4X3L          ANDX  7  AWORK4(2)
  16 4X5T          BNZ   7  NAVAL               [J IF SPOOLING   
  19 4X=B          LONGSET  #36,SET,6           [PUT IDENTIFIER NO. IN CLONG2
  10 4XQ2    TEAL  CALL  4  TESTIPBSTAT  
  21 4Y9L          BRN      NOID                [IPB INOP,MOPPING OFF,OR FINISH NOW  
  10 52MQ          LDX   0  ISTATUS(3)   
  17 537B          BNG   0  NAVAL               [UNAVAILABLE ALREADY 
   8 53M2          LDCT  4  #400 
   8 546L          ADN   4  #2000
   9 54L=          ORS   4  IWAIT(3) 
  19 555W          COOR1                        [WAIT FOR UNAVAILABLE REPORT 
  19 55KG          BRN      TEAL                [TEST WHETHER UNAVAILABLE YET
   8 5656    NAVAL LDCT  6  #200 
  19 56JQ          OUTPER                       [OUTPUT 'IDENTIFIER' + NUMBER
  20 574B          ANDX  6  AWORK4(2)           [B1 SET IF FIRST TIME SETUP FILE 
  16 57J2          ERS   6  AWORK4(2)           [HAS BEEN LISTED 
  19 57MF          NGN   5  1                   [IN CASE PLSE ENGAGE OBEYED  
  17 57_W          BZE   7  XNSPL                [J IF NOT SPOOLING  
  18 585*          INFORM   0,JKEN,1            [OUTPUT PLESE LINE UP... 
  15 588S          BRN      TERMA               [...ONLY ONCE
   5 58#?    XNSPL   
   8 58H=          ADN   6  #2000
  21 59GG          SLC   6  2                   [SET B11 FOR INTERVAL ; SET B23 IF   
  18 5=26          ERN   6  1                   [MINUTE DELAY REQUIRED   
  10 5=63          CALL  4  TESTIPBSTAT  
  21 5=9Y ...      BRN      TRYV                [IPB INOP,MOPPING OFF,OR FINISH NOW  
  17 5=*T          LDN   0  0                   [ASK CLUSTER CONSOLE 
   9 5=FQ          LDX   7  ZJKEN(1) 
  17 5=_B          DEMANDY  0,7,REPCD,TERMA,YMOC,6  ['PLEASE LINE UP'
  17 5?F2          CALL  4  RIDEM               [FREE DEMAND'S BLOCKS
  18 5?YL ...      BRN      TRYV                [IPB INOP : REPEAT LATER 
  19 5#D=          REWIND                       [REPEAT SETUP AFTER LINING UP
   6 5#N4          STEP  
   9 5#XW          LDN   4  IWORK5(2)
  21 5*CG    TMAIN LDN   5  IWORK10(2)          [ENTRY TO START MAINFILE AFTER SETUP 
   7 5*X6          MOVE  4  4
  21 5BBQ    TREST STOZ     IWORK9(2)           [ENTRY TO RESTART MAINFILE AFTER FAIL
  18 5BWB          STOZ     IWORK17(2)          [CLEAR PAGE LINE COUNT   
   8 5C34          LDCT  0  #40  
   9 5C4L          ANDX  0  AWORK4(2)
  17 5C68          BZE   0  XYAB                [J IF NOT SPOOLING   
   8 5C7Q          LDCT  0  #100 
  16 5C9#          ORS   0  AWORK4(2)           [OUTPUT PAGE NO  
   4 5C=W    XYAB
   9 5C?G          LDX   0  AWORK1(2)
  14 5C#6          BPZ   0  NOTP1               [J NOT TP
  20 5C#Q          ACROSS   IPBLISTE,1          [SHOULD NEVER GET HERE I THINK   
   5 5C*B    NOTP1   
  10 5CB2          ACROSS   IPBLISTB,1   
   4 5CTL    #   
  21 5D*=    #     IDENTIFIER INOPERABLE : SEND 'PLEASE ENGAGE' MESSAGE TO CONSOLE   
  19 5DSW    #     RESTART,POSTPONE RESTART OR DELETE FILE ACCORDING TO REPLY
   4 5F#G    #   
   8 5FS6    TINOP LDX   2  FX2  
  20 5HQL          LDCH  0  ISTATUS(3)          [B4 SET IF FAIL RATHER THAN UNAV 
  20 5J==          ANDN  0  2                   [B4 SET IF FAIL RATHER THAN UNAV 
  21 5JPW          DEX   0  AWORK4(2)           [0 IF HOLD BUTTON ; 2 IF STOP BUTTON 
   8 5JTR          LDCT  0  #030 
  10 5J_N          ORS   0  ISTATUS(3)   
  10 5K5K          ERS   0  ISTATUS(3)   
   6 5K9G    SK2IPBLISTC 
  10 5KP6          MHUNTW   3,AMXOR,ADCB 
   9 5L8Q          LDEX  0  AWORK4(2)
   8 5LNB          BZE   0  TUNA 
  19 5M82          CALL  7  REFRE               [FREE REDUNDANT IOUT BLOCKS  
   8 5MML    NOTES LDN   0  #100 
  19 5N7=          ORS   0  IWAIT(3)         [IGNORE INOPERABILITY IN RINGO  
  17 5NLW          LDX   4  XCRAS(1)         [CODE FOR 'CRASH CLOSE' 
   8 5PL6          CALL  6  SUPER
   8 5Q5Q          LDN   0  #40  
   9 5QKB          ANDX  0  IWAIT(3) 
   8 5R52          BNZ   0  REQC 
   8 5S4=          LDN   0  #100 
  19 5SHW          ORS   0  IWAIT(3)            [IGNORE INOPERABLE IN RINGO  
   9 5T3G          LDX   4  XOPEN(1) 
   8 5TH6          CALL  6  SUPER
   9 5TPR          LDX   0  IWAIT(3) 
   7 5TS2          ANDN  0  4
  16 5TW9          BNZ   0  TERMA               [J IF SPOOLING   
  10 5W2Q    REQC  LDX   7  IDENTNO(3)   
  10 5WGB          LDXC  5  IWORK11(2)   
  16 5X22          BCC      TERMA               [NOT PAGE RESTART
  11 5X9S          HUNTW    3,ADATA,IPTEMP   
   8 5XFL          BNG   3  TERF 
  18 5XPD          LDX   0  A1+1(3)             [PAGE NO FOR CURRENT FILE
  20 5YDW          BZE   0  TERF                [STILL ON FIRST PAGE : REPEAT IT 
  10 5YYG          LDX   6  ALOGLEN(3)   
   7 5_=W          SBN   6  1
  21 5_D6          SBN   6  10                  [VALUE OF IPAGE AT START OF LISTING  
   8 5_XQ          BZE   6  TERMA
  10 62CB          LONGSET  #104,SET,7   
  10 62X2          STO   6  ACOMMUNE6(2) 
   6 63BL          OUTPER
  10 63CX          CALL  4  TESTIPBSTAT  
  21 63F9          BRN      NOID                [IPB INOP,MOPPING OFF,OR FINISH NOW  
   9 63K5          LDX   6  IWAIT(3) 
  17 63LB          ANDN  6  2                   [SET IF UNATTENDED   
  14 63MM          DEMANDX  6,XPAGI(1),TERF,NOTE,YBAC,2  
  20 64*W          CALL  4  RIDEM               [FREE DEMAND'S REDUNDANT BLOCKS  
  17 64TG          BRN      TERF                [IPB INOP : CD ACTION
   8 655#          BRN      TERMK
   4 65SQ    #   
   7 66#B    TERF  LDN   0  1
  21 66S2          STO   0  ACOMMUNE6(2)        [CANT DONE : SIMULATE 1 PAGE RESTART 
   8 66XT    TERMK LDCT  0  #40  
   9 66_6          ANDX  0  AWORK4(2)
  17 672C          BZE   0  TERMA               [J IF NOT SPOOLING   
   9 673N          LDEX  0  AWORK4(2)
  16 674_          BZE   0  TERMA               [J IF NOT FAIL   
  10 676=          MHUNTW   3,AMXOR,ADCB 
  10 677H          LDXC  6  ISTATUS(3)   
   8 678S          BRN      REBIG
   6 67?L    TERMA REWIND
  21 67HD          BNG   5  NOWMA               [SETUP FILE LISTED:NOW LIST MAINFIL  
   9 67R=          LDX   0  AWORK4(2)
  15 68=W          BNG   0  TUNA                [NOT MAINFILE
  10 68QG          LDXC  0  IWORK11(2)   
  16 69=6          BCC      TUNA                [NOT PAGE RESTART
  15 69PQ          LDEX  0  AWORK4(2)           [ZERO IF HOLD
  19 6=9B          BZE   0  RHEL                [PGP AFTER CD TO PE WHEN HELD
  18 6=P2    TUNA  MHUNTW   3,AMXOR,ADCB        [ENTRY FOR HOLD BUTTON   
  10 6?8L          LDX   0  ISTATUS(3)   
  17 6?N=          BPZ   0  NOWAV               [ALREADY RE-ENGAGED  
   9 6?Y3          LDX   6  IWAIT(3) 
  17 6#2L          ANDN  6  2                   [SET IF UNATTENDED   
  12 6#7W          LONGSET  #36,SET,IDENTNO(3)   
   6 6#MG          OUTPER
  17 6*76          STOZ     AWORK3(2)           ['CAN'T DO' MARKER   
  10 6*##          CALL  4  TESTIPBSTAT  
  21 6*FH          BRN      NOID                [IPB INOP,MOPPING OFF,OR FINISH NOW  
   9 6*LQ          LDEX  0  AWORK4(2)
  16 6B6B          BZE   0  YPE                 [J IF HOLD BUTTON
  21 6BL2          LDX   4  ZJRESET(1)          [^PLEASE ENGAGE FOR ERROR RESTART^   
  15 6C5L          BRN      YDEM                [STOP BUTTON 
  16 6CK=    YPE   LDX   4  ZJMARY(1)           [^PLEASE ENGAGE^ 
   4 6CRG    YDEM
  12 6DY9          DEMANDX  6,4,REPCD,TERM,YTER,2
  17 6F46          CALL  4  RIDEM               [FREE DEMAND'S BLOCKS
  14 6FHQ          BRN      NOID                [IPB INOP
   5 6G3B    NOWAV   
   9 6GH2          LDEX  0  AWORK4(2)
  19 6H2L          BZE   0  REO                 [HELD : BLOCKS STILL WAITING 
  20 6HG=          STO   3  AWORK3(2)           [ENGAGED RATHER THAN CAN'T DONE  
  10 6H_W          LDX   6  ISTATUS(3)   
  16 6JFG          BNG   6  NOTES               [STILL INOPERABLE
   9 6J_6          LDX   5  AWORK4(2)
  16 6KDQ          BPZ   5  REBIG               [NOT SETUP FILE  
   9 6KMC          LDX   0  IWAIT(3) 
   7 6KPL          ANDN  0  4
   8 6KRT          BNZ   0  REBIG
  21 6KYB          LDN   5  IWORK1(2)           [REPEAT TESTPATTERN FILE FROM START  
  21 6LD2          LDN   6  IWORK10(2)          [COUNTERACT MOVE 5 4 IN K2IPBLISTB   
   7 6LXL          MOVE  5  4
  19 6MC=          ACROSS   IPBLISTB,2          [RESTART LINING UP PROCEDURE 
  20 6MWW    REO   BNZ   7  REF                 [HELD WHILE SENDING CLOSE OR FF  
   9 6N22          LDX   0  AWORK1(2)
  15 6N56          BPZ   0  NOTP2               [J. NOT TP   
  10 6N8=          ACROSS   IPBLISTE,3   
   5 6N?B    NOTP2   
  20 6NBG          ACROSS   IPBLISTB,3          [HELD DURING LISTING : CONTINUE  
   4 6NW6    #   
  19 6P*Q    TERM  REWIND                       [SET FILE(SETUP) TO BEGINNING
  10 6PTB          MHUNTW   3,AMXOR,ADCB 
  18 6Q*2          NGS   3  AWORK3(2)           [INDICATE 'TERMINATED'   
  18 6QSL          BRN      UPRE                [GET RESTART LINE NUMBER 
   9 6QXX    REBIS LDX   0  IWAIT(3) 
   7 6Q_2          ANDN  0  4
  17 6R25          BZE   0  REBIG               [J IF NOT SPOOLING   
  10 6R38          LDXC  0  IWORK11(2)   
  15 6R4?          BCC      REBIG               [NOT PAGES   
   9 6R5B          LDX   0  AWORK3(2)
  15 6R6F          BNZ   0  REBIG               [NOT CANTDO  
   7 6R7J          LDN   0  1
  10 6R8M          STO   0  ACOMMUNE6(2) 
  16 6R9Q          BRN      REQC                [ASK FOR PAGES   
   4 6R#=    #   
  10 6RRW    REPCD MHUNTW   3,AMXOR,ADCB 
   8 6S?G          LDN   0  #1000
  19 6SR6          ORS   0  IWAIT(3)         [MARK 'WAITING TO BE WRONGED'   
   9 6T=Q          LDX   5  AWORK4(2)
  19 6TQB ...      BNG   5  TRYV                [SPEC. CASE : TRY AGAIN LATER
  19 6W=2    UPRE  LDX   6  ISTATUS(3)          [FAIL RESTART SECTION NUMBER 
   9 6WPL          LDEX  0  AWORK4(2)
   8 6W_D          BNZ   0  REBIS
  19 6XNW          CALL  7  REFRE               [FREE REDUNDANT IOUT BLOCKS  
   8 6Y8G          LDN   0  #100 
  18 6YN6          LDX   4  XCRAS(1)            [CODE FOR 'CRASH CLOSE'  
  20 6_MB          ORS   0  IWAIT(3)            [IGNORE INOPERABILITY IN RINGO   
   8 7272          CALL  6  SUPER
   8 72LL          LDN   0  #40  
   9 736=          ANDX  0  IWAIT(3) 
   8 73KW          BNZ   0  RECL 
   8 74K6          LDN   0  #100 
   9 754Q          LDX   4  XOPEN(1) 
  20 75JB          ORS   0  IWAIT(3)            [IGNORE INOPERABILITY IN RINGO   
   8 7642          CALL  6  SUPER
   7 76HL    RECL  LDN   0  1
  20 773=          STO   0  ACOMMUNE6(2)        [GO BACK 1 PAGE IF PAGE RESTART  
  10 77GW          LDXC  0  IWORK11(2)   
  16 782G          BCC      RHEL                [NOT PAGE RESTART
   9 78G6          LDX   0  AWORK3(2)
  21 78_Q          BZE   0  REQC                [CANT DONE : ASK FOR PAGES IF NEC.   
  20 79FB    RHEL  LDCH  6  IWORK9(2)           [RETURN AFTER PLEASE GIVE PAGES  
   9 79LJ          LDX   0  AWORK1(2)
  13 79RQ          BNG   0  TBUFF               [TP  
   9 79YY          LDX   0  AWORK3(2)
  20 7=66          BNG   0  REBIG               [LP ^TERMINATED^ : SKIP ^TBUFF^  
  21 7=?#    TBUFF SBN   6  1                   [SIMULATE AN EXTRA RESTART SECT AGO  
   4 7=DL    #   
  17 7=Y=    REBIG ANDN  6  #37                 [ENTRY IF MOPPING OFF
   9 7?CW          LDX   0  AWORK3(2)
   8 7?MM          LDCT  7  #40  
  14 7?Q=          ANDX  7  AWORK4(2)           [SPL BIT 
  17 7?XG          BNG   0  PLRES               [J IF ^TERMINATED^   
  15 7#67          BZE   0  XS1                 [J IF CANTDO 
  16 7#8B          BNZ   7  PLRES               [...IF SPOOLING  
   4 7#=K    XS1 
  10 7#C6          LDX   0  IWORK11(2)   
   7 7#WQ          SLC   0  1
  18 7*BB ...      BNG   0  TRYV                [WHOLE RESTART REQUIRED  
   4 7*W2    #   
  21 7B*L    #     LINE OR PAGE RESTART REQUIRED : CALCULATE RESTART RECORD NUMBER   
  21 7BT=    #     FROM CURRENT AND FAILED RESTART SECTION NUMBERS AND CURRENT RECORD
  20 7C#W    #     NUMBER ; IF PAGE , ACOMMUNE6 CONTAINS NO. OF PAGES TO GO BACK 
   4 7CSG    #   
   9 7D#6    PLRES LDCH  5  IWORK9(2)
   8 7DRQ          ORN   5  #40  
  21 7F?B          SBX   5  6                   [GET DIFFERENCE BETWEEN PRESENT AND  
  20 7FR2          ANDN  5  #37                 [FAILED RESTART SECTION NUMBERS  
   9 7FY8          LDX   0  AWORK1(2)
  13 7G5B          BPZ   0  PRES                [LP  
   7 7G=L          SLL   5  1
  21 7GQ=          LDXC  0  IWORK9(2)           [B0 SET DURING SECOND REC. OF SECTION
   7 7H9W          ADN   5  5
  18 7HPG          LDX   6  IWORK10(2)          [CURRENT RECORD NUMBER   
   8 7J96          BRN      RETOG
   4 7K8B    #   
   4 7K8L    PRES
   9 7K9B          LDX   0  AWORK3(2)
  16 7K9L          BNG   0  XS23                [J IF TERMINATED 
  15 7K9W          BZE   0  XS2                 [J IF CANTDO 
  17 7K=6          BZE   7  XS2                 [J IF NOT SPOOLING   
  17 7K=B          STO   5  ACOMMUNE6(2)        [BACK TO FAILED PAGE 
   7 7K=L          LDN   0  1
  10 7K=W          ADS   0  ACOMMUNE6(2) 
   8 7K?6          BRN      SPREC
  16 7K?B    XS23  BNZ   7  XS24                [J IF SPOOLING   
   4 7K?L    XS2 
  10 7K#6          LDXC  7  IWORK11(2)   
  15 7K#S          BCS      SPREC               [PAGE RESTART
   4 7KB=    XS24
   7 7KC3          LDN   0  1
  19 7KF=          STO   0  ACOMMUNE6(2)        [LINE RESTART:GO BACK 1 PAGE 
   5 7KHF    SPREC   
  21 7KKN          LDX   6  5                   [NO OF PAGES FROM CURRENT TO FAILED  
  21 7KM8          HUNTW    3,ADATA,IPTEMP      [CONTAINS FIRST RECORD NOS PER PAGE  
   9 7KNN          BNG   3  XEREND   
   8 7KQ9          LDX   1  A1(3)
   7 7KSD          SBX   1  6
  20 7KWM          BPZ   1  SPOK                [PAGES PRINTED > PAGES TO GO BACK
  21 7KYW          LDX   6  A1(3)               [IF NOT SET DIFFERENCE = PAGES SO FAR
   4 7L35    SPOK
  14 7L5#          SBS   6  A1(3)               [UPDATE  
  10 7L5Y          LDX   0  IWORK11(2)   
   7 7L6J          SBX   0  6
   8 7L78          ANDN  0  #37  
   8 7L7S          LDCT  5  #777 
  10 7L8D          ANDS  5  IWORK11(2)   
  10 7L94          ADS   0  IWORK11(2)   
   9 7L9Q          LDX   5  A1+1(3)  
  10 7M6W          LDX   0  ALOGLEN(3)   
   7 7MLG          LDX   1  5
   7 7MW#          SBN   0  2
   7 7NKQ          DVS   4  0
   7 7P5B          LDX   5  3
  21 7PK2          ADX   3  4                   [POSITION OF CURRENT PAGE RECORD NO. 
   7 7Q4L          LDX   4  0
  20 7QJ=    TEREC BZE   6  PAGEF               [FOUND POS OF FAILED PAGE REC NO 
  19 7R3W          SBN   6  1                   [STEP BACK (X6) PAGES(RSNS)  
  17 7RHG          SBN   3  1                   [TRY PREVIOUS PAGE   
   7 7S36          SBN   1  1
   7 7SGQ          TXL   3  5
  20 7T2B          BCC      PAGES               [NOT YET AT BEGINNING OF BLOCK   
  19 7TG2          ADX   3  0                   [BACK TO LAST WORD IN BLOCK  
   8 7T_L    PAGES BCT   4  TEREC
  21 7WF= ...      BRN      TRYV           [UNREMEMBERED PAGE NO : TAKE CANTDO ACTION
  10 7WYW    PAGEF TXL   1  ACOMMUNE6(2) 
  20 7XDG          BCS      NUPR                [PAGES PRINTED < PAGES TO GO BACK
  10 7XY6          LDX   1  ACOMMUNE6(2) 
   7 7YCQ          SBN   1  1
  19 7YXB    NUPR  SBX   3  1                   [GO BACK NO. OF PAGES IN X1  
   7 7_C2          TXL   3  5
  18 7_WL          BCC      TOZB                [NOT PAST START OF BLOCK 
   7 82B=          ADX   3  0
   4 82F4    TOZB
   7 82PG          SMO      5
  18 82S#          SBS   1  A1                  [UPDATE TOTAL NO OF PAGES
  10 82T5          LDX   0  IWORK11(2)   
   7 82TW          SBX   0  1
   8 82WM          ANDN  0  #37  
   8 82XD          LDCT  5  #777 
  10 82Y9          ANDS  5  IWORK11(2)   
  10 82_2          ADS   0  IWORK11(2)   
  18 82_Y          LDX   5  IWORK10(2)          [CURRENT RECORD NUMBER   
   9 837J          SBX   5  A1+2(3)  
  11 83T6          MHUNTW   1,ADATA,IPTEMP   
   7 84#Q          SBX   3  1
   9 84JJ          STO   3  A1+1(1)  
   4 85#2    #   
  10 85RL    RETOG LDX   0  IWORK12(2)   
  19 86?=          SBS   5  IWORK10(2)          [UPDATE START RECORD NUMBER  
  16 86QW          BNG   0  REW                 [LIST ALL RECORDS
  21 87=G          ADS   5  IWORK12(2)          [UPDATE COUNT OF RECS TO BE LISTED   
  18 87Q6    REW   LDX   0  IWORK10(2)          [RESTART RECORD NUMBER   
  18 889Q          SBX   0  IWORK1(2)           [INITIAL RECORD NUMBER   
   9 88D2          LDX   7  AWORK1(2)
  13 88L=          BNG   7  TPF                 [TP  
  20 88SG          BZE   0  TRYV                [FAIL DURING FIRST PAGE:REPEAT IT
  21 892Q    TPF   BNG   0  TRYV                [FAIL DURING TP HEADINGS:REPEAT THEM 
   9 8992          LDX   0  AWORK3(2)
  21 89NL          BZE   0  TRYL                [RECEIVED CAN'T DO TO PLEASE ENGAGE  
  15 8=8=          BNG   0  TERMB               [TERMINATED  
   8 8=BX          LDCT  5  #40  
  19 8=F6          ANDX  5  AWORK4(2)           [DONOT BOTHER WITH SETUP...  
  16 8=JG          BNZ   5  POSITION            [...IF SPOOLING  
   8 8=MW          LDCT  5  #100 
  20 8?7G          ANDX  5  AWORK2(2)           [B2 SET IF TESTPATTERN FILE FIRST
  18 8?M6          BZE   5  POSITION            [NO SPECIAL STATIONERY   
   9 8#6Q          LDX   5  AWORK4(2)
   4 8#LB    #   
  21 8*62    NOWMA FILEMOVE 1,0                 [INTERCHANGE MAINFILE AND TESTPATTERN
  20 8*KL          CALL  7  VFA                 [FREE ADATA/ACONV BLOCK , IF ANY 
  11 8*RD          MHUNTW   3,ADATA,IPTEMP   
   8 8*SC          STOZ     A1(3)
  20 8*TB          STOZ     A1+1(3)             [NO OF CURRENT FILE PAGES PRINTED
  17 8*X#          LDX   0  IWORK1(2)           [START OF MAINFILE   
   9 8*_=          STO   0  A1+2(3)  
   8 8B5=          LDCT  0  #400 
  18 8BJW          ERS   0  AWORK4(2)           [INVERT FILE INDICATOR   
  17 8C4G          BNG   5  POSITION1           [J TO LIST MAINFILE  
   9 8CT2          LDX   0  IWORK5(2)
  17 8CX9          STO   0  A1+2(3)             [START OF SETUP FILE 
  20 8D3Q          ACROSS   IPBLISTB,2          [REPEAT SETUP AFTER MAINFILE FAIL
   4 8D4K    [   
   6 8D5D    POSITION1   
   9 8D6?          LDN   4  IWORK1(2)
  10 8D76          LDN   5  IWORK10(2)   
   7 8D7_          MOVE  4  4
  10 8D89          LDX   0  IWORK11(2)   
   7 8D8F          ADN   0  1
   8 8D8P          SMO      FX1  
   8 8D8_          ANDX  0  XMSK 
  10 8D99          STO   0  IWORK11(2)   
   6 8D9F          REWIND
  10 8D9P          LDX   7  IWORK10(2)   
   7 8D9_          ADN   7  1
   8 8D=9          BRN      STEPS
   4 8D=G    [   
  19 8D?*    POSITION                           [ MAINFILE AT RESTART POINT  
   4 8D#8    [   
   6 8D*3          REWIND
  10 8D*W          LDX   7  IWORK10(2)   
  17 8DBP          ADN   7  1                   [ RESTART RECORD NO. 
  10 8DBT          MHUNTW   3,AMXOR,ADCB 
  12 8DB_          JBC      STEPS,3,ADCBTSPOOL   
  12 8DC5          JBC      STEPS,3,ADCB7502LP   
  10 8DC9          STO   7  IWORK10(2)   
   7 8DC*          ADN   7  1
   5 8DCJ    STEPS   
   6 8DDC          STEP  
   8 8DF=          BCT   7  STEPS
  17 8DG5          BRN      TREST               [ RETURN TO IPBLISTB 
   4 8DG6    [   
   6 8DG7    SK7IPBLISTC 
   4 8DG8    [   
  18 8DG9    [ ENTRY FROM IPBLISTA TO SEND CRASH CLOSE & OPEN AFTER FAIL 
   4 8DG=    [   
  10 8DG?          MHUNTW   3,AMXOR,ADCB 
   8 8DG#          LDN   0  #100 
   9 8DG*          ORS   0  IWAIT(3) 
   9 8DGB          LDX   4  XCRAS(1) 
   8 8DGC          CALL  6  SUPER
   8 8DGD          LDN   0  #40  
   9 8DGF          ANDX  0  IWAIT(3) 
   8 8DGG          BNZ   0  SLEND
   8 8DGH          LDN   0  #100 
   9 8DGJ          ORS   0  IWAIT(3) 
   9 8DGK          LDX   4  XOPEN(1) 
   8 8DGL          CALL  6  SUPER
   5 8DGM    SLEND   
  10 8DGN          ACROSS   IPBLISTA,1   
   4 8DGP    [   
   4 8DGQ    [   
   4 8DHB    #   
   8 8F32    TRYL  CALL  7  VF   
  19 8FGL          LFEND    CD,IWORK10(2)       [RESTART LINE NO. IN IWORK10 
   8 8G2=    TRYV  CALL  7  VF   
   4 8G64    TATE
   8 8GFG          LFEND    CD   
   5 8GK*    TERMB   
   8 8GXQ          LDCT  0  #40  
   9 8H39          ANDX  0  AWORK4(2)
   8 8H6N          BNZ   0  XS25 
  20 8H*L          LDX   5  AWORK4(2)  [IF ^TE^ DURING SETUP FILE,OUTPUT MAINFILE
   8 8HF6          BNG   5  NOWMA
   4 8HRG    XS25
   8 8HYQ          CALL  7  VF   
  10 8JDB          LDX   0  IWORK10(2)   
  17 8JY2          SBX   0  IWORK1(2)           [TAKE CD ACTION IF   
  19 8KCL          BNG   0  TATE                [TERMINATED DURING HEADINGS  
   5 8KG9    TLFTE   
   8 8KX=          LFEND    TE   
   5 8KYM    XEREND  
   9 8L24          LDX   0  AWORK1(2)
  14 8L3F          BNG   0  XGTP                [J IF TP 
   9 8L4W          LDEX  0  AWORK4(2)
  16 8L6?          BNZ   0  TATE                [J IF NOT ON HOLD
   9 8L7N          LDX   0  AWORK3(2)
  16 8L95          BNG   0  TLFTE               [J IF HELD + TE  
   8 8L=G          BRN      TATE 
   4 8L?X    [   
   9 8L*#    XGTP  GEOERR   1,LFNOTLP
   4 8LBW    #   
  20 8LWG    NOTE  GEOERR   1,TERNOTAL          [PAGES DEMAND CANT BE TERMINATED 
  17 8MB6    SET   GEOERR   1,ALRDYSET          [LONGSET GIVEN TWICE 
   4 8N*B    #END
   8 ____ ...32160716000300000000
