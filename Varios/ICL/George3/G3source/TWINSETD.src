  16 22FL    #SEG  TWINSETD70                   [BOB NICHOLSON   
  10 22_=    #LIS  K0TWINSET>K0TWINSETD  
   7 23DW          8HTWINSETD
   4 23YG    #   
  20 24D6    #     THIS SEGMENT RECEIVES K1,AND SENDS K2,MESSAGE CONCERNED WITH  
   7 24XQ    #     TWINNING  
   4 25CB    #   
  11 25X2          SEGENTRY K1TWINSETD,QK1   
  11 26BL          SEGENTRY K2TWINSETD,QK2   
  11 26W=          SEGENTRY K3TWINSETD,QK3   
  11 27*W          SEGENTRY K4TWINSETD,QK4   
  19 27J6 ...      SEGENTRY K5TWINSETD,QK5      [SETS NOT INITIALISED REPLIES
  10 27M= ...      SEGENTRY K7TWINSETD   
  16 27QB ...               +0                  [=-1 IF TRACING  
   4 27TG    #   
   9 28*6    #     IPCREAD SUBROUTINE
   4 28SQ    #   
   6 29#B    QK1   [*****
   9 29S2    #SKI  K6TWINSETC>99-99  
  10 2=?L          TRACE     2,TWREADC   
   9 2=R=          LDN   3  ETBUFS(2)
  16 2?=W          IPCREAD  ABS,3,ETNBLEN,ETIOUT [DO ACTUAL READ 
  18 2?QG ...      LDN   0   JROUTS             [SET READ OUTSTANDING MKR
   8 2#=6          BRN       XIT 
   4 2#PQ    #   
  10 2*9B    #     IPCWRITE SUBROUTINE   
   4 2*P2    #   
   6 2B8L    QK2   [*****
  18 2BN=          MHUNTW   3,EWDAS,ESEND       [SEARCH FOR DATA BLOCK   
  21 2C2L ...      NAME     3,EWDAS,ESENDT      [IN CASE OTHER ESENDS ARE CHND BEFORE
   9 2C7W    #SKI  K6TWINSETC>99-99  
  12 2CMG          TRACE     ENMARK(3),TWRITEC   
  10 2D76          LDX   2  ALOGLEN(3)   
   7 2DLQ          SBN   2  2
   9 2F6B          LDX   5  XTYPE(1) 
  15 2FL2          IPCWRITE BLK,5,2,ETIOUT      [SEND MESSAGE
   8 2FRG ...      SMO      FX1  
  10 2FRS ...      LDX   0  K7TWINSETD   
  16 2FS6 ...      BZE   0  NTRAC1              [J IF NO TRACING 
  11 2FSQ ...      MHUNTW   3,EWDAS,ESENDT   
   9 2FYB ...      LDN   3  ENMARK(3)
   5 2F_M ...NTRAC1  
   9 2G5L          LDN   0   JIOUTS  
   9 2GK=    XIT   ORS   0   ETWINMK 
   8 2GMN ...      SMO      FX1  
  10 2GMY ...      LDX   5  K7TWINSETD   
  16 2GN8 ...      BZE   5  NTRAC2              [J IF NOT TRACING
  13 2GNH ...      LDN   5  #6463               [TS  
   8 2GPL ...      SLL   5  12   
  10 2GQP ...      ANDN  0  JIOUTS+JROUTS
   7 2GRS ...      ORX   5  0
   9 2GSX ...      LDX   0  ETWINMK  
  10 2GW2 ...      STO   0  ACOMMUNE2(2) 
  10 2GX5 ...      STO   5  ACOMMUNE1(2) 
  19 2GXX ...      STO   7  ACOMMUNE3(2)        [USUALLY LINK IN CALLING SEG 
  10 2GYP ...      LDN   4  ACOMMUNE4(2) 
   7 2G_H ...      MOVE  3  2
   7 2H2B ...      TWTRACE   
   5 2H2X ...NTRAC2  
  11 2H4W          TRACE    ETWINMK,ETWINMK  
   5 2HJG          UP
   4 2J46    #   
  17 2JHQ    [     SUBROUTINE TO SEARCH FOR ESEND WITH REQUEST TYPE  
  11 2K3B    [     IN X6 AND NUMBER IN X5.   
  21 2KH2    [     X7 LINK - EXITS 0 IF FOUND,1 IF NOT FOUND:X3 PTS TO ENLINK IF 0   
   4 2L2L    [   
   8 2LG=    SEAR  LDX   3  FX2  
  12 2L_W    TA    HUNT2J   3,EWDAS,ESEND,,NFOUND
   9 2MFG          LDEX  0  ENMARK(3)
   7 2M_6          SBX   0  6
  18 2NDQ          BNZ   0  TA              [J IF REQUEST TYPE DIFFERS   
  10 2NYB          LDX   0  ENSTART(3)   
   7 2PD2          SBX   0  5
  16 2PXL          BNZ   0  TA              [J IF NUMBER DIFFERS 
   9 2QC=          TRACE    6,TFOUND 
  13 2QWW          EXIT  7  0               [FOUND   
   5 2RBG    NFOUND  
  14 2RW6          EXIT  7  1               [NOT FOUND   
   4 2S*Q    [   
   4 2STB    #   
   7 2T*2    #     CONSTANTS 
   4 2TSL    #   
   5 2W#=    XTYPE   
  10 2WGG ...#HAL  EWDAS+ESENDT,+ENMARK  
  19 2X?G    XM6            #76007777           [CLEARS BITS 5,6,7,8,9,10,11 
   4 2XR6    #   
   4 2Y=Q    #   
   4 2YQB    #   
  14 2_=2    #     SUBROUTINE  TO ASSEMBLE HELLO MESSAGE 
   4 2_PL    #   
   9 329=    #     FORMAT OF WORD IS 
   4 32NW    #   
   9 338G    #     B0-B2    NOT USED 
  15 33N6    #     B3       =1 IF WE HAVE RIGHT TO DISENGAGE 
  17 347Q    #     B5       IS THE LOCK-OUT BIT (=0 IF WE HAVE IT)   
  18 34MB    #     B7       =1 IF THIS PROCESSOR GOING TO SEND LOCK-OUT  
  14 3572    #     B9-B10   IS THE STATUS AS FOR BSUNIT  
   9 35LL    #     B15-23   GEOG. NO.
   4 366=    #   
   6 36KW    QK3   [*****
   9 375G          LDX   6  ETNBLEN  
   7 37K6          ADN   6  2
  15 3826 ...[     SET UP ENLINK TO CONTAIN REPLY. WILL BE   
  14 38C6 ...[     RENAMED TO ESEND BY CALLING SEGMENT   
  17 38S6 ...[     AS IPC MAY OTHERWISE PICK IT UP AS TRANSFER BUFFER
  13 3996 ...[     IF A WRITE HAS ALREADY BEEN ISSUED
  12 39L6 ...      SETUPCORE 6,1,EWDAS,ENLINK,,1 
   9 3=3=          LDN   0  ENTRGA(1)
   9 3=GW          STO   0  ENTRGA(1)
  16 3?2G          STO   0  ENTRGA+1(1)         [MAKE RING EMPTY 
  10 3?G6          LDN   2  ENSTART(1)   
  16 3?_Q          CALL  7  X6                  [GET PTR TO FIRST
   9 3#FB          BRN      NOTWINS  
  16 3#_2    R22   LADDP    3,4                 [X4 PTS TO APIA  
   8 3*DL          LGEOG    3,0  
  16 3*Y=          STO   0  0(2)                [STORE GEOG. NO  
   7 3BCW          SMO      4
   9 3BXG          LDX   0  BSUNIT3  
   7 3CC6          ANDN  0  4
  17 3CWQ          DCH   0  0(2)                [DISENGAGE BIT TO B3 
   8 3DBB          SMO      FX1  
   9 3DW2          LDX   0  XBIT5910 
   7 3F*L          SMO      4
   9 3FT=          ANDX  0  BSUNIT   
  17 3G#W          ORS   0  0(2)                [STATUS AND LOCK-OUT 
  15 3R66          ADN   2  1                   [UPDATE MOD  
   8 3RKQ          CALL  7  X3   
  14 3S5B          BRN      R21                 [NO MORE 
  15 3SK2          BRN      R22                 [J IF DO NEXT
   7 3T4L    R21   NGN   0  1
  16 3TJ=          STO   0  0(2)                [-1 TO TERMINATE 
   5 3W3W          UP
   4 3WHG    [   
   4 3X36    #   
  20 3XGQ    #     SUBROUTINE TO GO ROUND JOBRING WAKING UP WAITING ACTIVITIES   
  20 3Y2B    #     WHOSE  NUMBER (X5) AND REQUEST TYPE (X6) AGREE : REPLY IN X4  
   4 3YG2    #   
   8 3Y_L    R7    LDX   2  FX2  
  10 3_F=          LDX   3  ETFRING(2)   
  10 3_YW    R7A   LDN   0  ETFRING(2)   
   7 42DG          TXU   0  3
  15 42Y6          BCC      (7)                 [J IF NO MORE
  17 43CQ          SBN   3  ENLA                [PTR TO NEXT ENLINK  
   9 43XB    #SKI  K6TWINSET>99-99   
   4 44C2    (   
  16 44WL          LDX   0  ATYPE(3)            [CHECK IF ENLINK 
   8 45B=          SRL   0  12   
  10 45TW          SBN   0  EWDAS+ENLINK 
   8 46*G          BZE   0  R8   
  10 46T6          GEOERR   1,NOT ENLK   
   4 47#Q    R8  
   4 47SB    )   
  19 48#2          BNG   5  R9                  [J IF NOT TO CHECK DETAILS   
   9 48RL          TXU   6  ENRQ(3)  
   9 49?=          TXU   5  ENBR(3)  
  15 49QW          BCC      R9                  [J IF MATCH  
  14 4==G    R8F   LDX   3  ENLA(3)             [GET NEXT
   8 4=Q6          LDX   2  FX2  
   8 4?9Q          BRN      R7A  
  15 4?PB    R9    LDX   0  ENRQ(3)             [REQUEST TYPE
   8 4#92          SBN   0  CLIP 
  16 4#NL          BZE   0  R8F                 [IGNORE IF CLOSE 
   9 4*8=          LDX   0  ENRQ(3)  
   8 4*MW          SBN   0  INIT 
  16 4B7G          BZE   0  R8F                 [OR IF INITIALISE
  15 4BM6          STO   4  ENRP(3)             [STORE REPLY 
   7 4C6Q          LDX   2  3
  16 4CLB          LDX   1  ENWTS(3)            [WAITING STYLE   
  19 4D62    R10   LDX   2  BPTR(2)             [SEARCH FOR WAITING ACTIVITY 
   9 4DKL          LDX   0  ATYPE(2) 
   8 4F5=          TXL   0  CACT 
  17 4FJW          BCS      R10                 [J IF NOT ACTIVITY   
  17 4G4G          LONGCLEAR 2,0(1),R11         [WAKE UP IF POSSIBLE 
   7 4GJ6    R11   LDX   2  3
  15 4H3Q          LDX   3  ENLA(3)             [NEXT ON RING
  14 4HHB          DERINGD  ENLA(2)             [DECHAIN 
   8 4J32          BRN      R7A  
   4 4JGL    #   
   4 4RWW ...#   
  12 4RX* ...#     SUBROUTINE TO DO THE FOLLOWING
   4 4RXS ...#   
  14 4RY? ...#     X6=0     CHAIN ENLINK ON TO JOBRING   
   4 4RYQ ...#   
  21 4R_9 ...#     X6 NON ZERO  DECHAIN ENLINK FROM ESEND,LONGCLEAR ACTIVITY (IF ANY)
  11 4R_N ...#              AND PUT REPLY IN 
   4 4S27 ...#   
  10 4S2L ...R1A   MHUNTW   3,EWDAS,ESEND
   9 4S35 ...R1    LDN   0  ENTRGA(3)
  10 4S3J ...      TXU   0  ENTRGA+1(3)  
  17 4S43 ...      BCC      R2                  [J IF NOTHING CHAINED
   9 4S4G ...      LDX   4  ENTRGA(3)
  15 4S4_ ...      DERING   ENTRGA(3)           [ELSE DECHAIN
  18 4S5D ...      SBN   4  ENLA                [PTR TO START OF ENLINK  
   7 4S5X ...      LDX   1  4
  15 4S6B ...      STO   6  ENRP(1)             [STORE REPLY 
  20 4S6T ...      BZE   6  R3                  [J IF ACTIVITY NOT TO BE WOKEN   
  18 4S7# ...R4    LDX   1  BPTR(1)             [LOOK BACK FOR ACTIVITY  
   9 4S7R ...      LDX   0  ATYPE(1) 
   8 4S8= ...      TXL   0  CACT 
  16 4S8P ...      BCS      R4                  [J NOT ACTIVITY  
   7 4S98 ...      SMO      4
  17 4S9M ...      LDX   3  ENWTS               [GET WAITING STYLE   
  18 4S=6 ...      LONGCLEAR 1,0(3),R3A         [AND WAKE UP IF WAITING  
   7 4S=K ...R3A   EXIT  7  0
  18 4S?4 ...R3    ENRING   1,ETBRING(2)        [PUT ON END OF JOBRING   
   7 4S?H ...R2    EXIT  7  0
   4 4S#L    [   
  18 4SS=    [     SUBROUTINE TO RING ESEND AND ENLINK TOGETHER AND CHAIN
  21 4T?W    [     ESEND AS LAST BLOCK IN TWINNING ACTIVITY .X1-> ESEND X3-> ENLINK  
   4 4TRG    [   
   5 4W?6    SETOFF  
  17 4WQQ          RINGTWO  ENTRGA(1),ENLA(3)   [RING THEM TO GETHER 
   8 4X=B          LDX   2  FX2  
  10 4XQ2          LDX   2  ACTRING(2)   
   9 4Y9L          SBN   2  ACTRING  
  16 4YP=          LDX   2  BPTR(2)             [PTR TO LAST BLK 
  16 4_8W          CHAIN    1,2                 [PUT ESEND ON END
   7 4_NG          EXIT  7  0
   4 5=FQ    #   
  21 5=_B    #     SUBROUTINE TO GO THOUGH ALL UDAS DEVICES LOOKING FOR TWINNED ONES 
   4 5?F2    #   
   8 5?YL    X6    LDX   1  FX1  
   7 5#D=    X4    ADN   1  1
  15 5#XW          LDX   0  TB-1(1)             [NEXT DEVICE 
   8 5*CG          BNG   0  (7)  
  18 5*X6 ...      FINDPEREC 3,APFOTY,TB-1(1),X3,,XCME,XCME    [GET PTR  
  19 5BBQ          BNG   3  X4                  [J IF NO MORE OF THIS TYPE   
  15 5BWB          JTWIN    3,X5                [J IF TWINNED
  16 5CB2          BRN      X3                  [LOOK FOR NEXT   
   7 5CTL    X5    EXIT  7  1
   4 5D*=    #   
   5 5DSW    NOTWINS 
   9 5F#G          GEOERR   1,NOTWINS
   4 5FS6    #   
   8 5G?Q    TB             +6   
   8 5GRB                   +13  
   8 5H?2                   +26  
   8 5HQL                   -1   
   4 5J==    #   
   4 5JPW    #   
   9 5K9G    XBIT5910       #01060000
   4 5KP6    #   
  15 5L8Q    #     SUBROUTINE TO CHECK REPLY FROM GHL OR GHLR
   4 5LNB    #   
   6 5M82    QK4   [*****
  16 5MML          LDN   1  ETBUFS+1(2)         [START OF BUFFER 
   8 5N7=    V8    LDX   4  0(1) 
  17 5N8S ...      STO   4  ACOMMUNE4(2)           [STORE FOR TRACING
  15 5N*G ...      BNG   4  V1                  [J IF NO MORE
   8 5P6G          LDEX  4  0(1) 
  17 5PL6 ...      FINDPEREC 3,APGEOG,4,,,XCME         [GET DL PTR   
  17 5Q5Q ...      BNG   3  V2                  [ERROR IF NOT THERE  
  15 5QKB          JTWIN    3,V3                [J IF TWINNED
  15 5R52 ...      BRN      V2                  [ELSE ERROR  
   8 5RJL    V3    LADDP    3,3  
   9 5S4=          LDX   5  BSUNIT(3)
  18 5S#4 ...      STO   5  ACOMMUNE5(2)            [STORE FOR TRACING   
  17 5SHW          ANDX  5  BITS910             [STATE IN OUR LISTS  
   8 5T3G          LDX   6  0(1) 
  16 5TH6          ANDX  6  BITS910             [STATE IN THEIRS 
  20 5W2Q          BZE   6  V4                  [J IF DEDICATED IN THEIR LISTS   
   9 5WGB          ERX   6  BITS910  
  20 5X22          BZE   6  V5                  [J IF UNDEDICATED IN THEIR LISTS 
  20 5XFL          BZE   5  V6                  [J IF DEDICATED IN OURS - ERROR  
   9 5X_=          ERX   5  BITS910  
  20 5YDW          BNZ   5  V                   [J IF SHARED IN BOTH PROCESSORS  
  17 5YL4 ...      BRN      V6                  [SHARED-UNDEDICATED  
  15 66S2    V9    ADN   1  1                   [UPDATE MOD  
  15 67?L          BRN      V8                  [GO FOR NEXT 
   4 67R=    #   
  11 68=W    #     SHARED IN BOTH PROCESSORS 
   4 68QG    #   
  13 68RN ...[     CHECK IF BISENGAGE RIGHTS DIFFER  
   4 68SW ...[   
  18 68W4 ...V     LDX   5  BSUNIT3(3)          [DR FROM THIS PROCESSOR  
   7 68X= ...      ANDN  5  4
   8 68YD ...      LDCH  6  0(1) 
  18 68_L ...      ANDN  6  4                   [DR FROM OTHER PROCESSOR 
   7 692S ...      ERX   5  6
  15 6942 ...      BNZ   5  XDIF                [DRS DIFFER  
   9 6958 ...      LDX   0  AFTERU   
  21 696B ...      BNG   0  XDIF                [J IF THIS PROCESSOR STAYS AS IT IS  
   7 697J ...      LDN   0  4
  15 698Q ...      ERS   0  BSUNIT3(3)          [REVERSE DR  
  14 699Y ...[     NOW CLEAR ANY GHOLD MESSAGES WAITING  
   8 69?6 ...      LDX   2  FX2  
  16 69## ...      STO   1  AWORK4(2)           [SAVE X1 AND X3  
   9 69*G ...      STO   3  AWORK3(2)
  15 69BN ...      LDN   6  GHOLD               [REQUEST TYPE
  14 69CW ...      LDEX  5  0(1)                [GEOG NO 
  17 69F4 ...      CALL  7  SEAR                [FIND ESEND IF THERE 
  15 69G= ...      BRN      XHLD                [J IF FOUND  
  16 69HD ...      BRN      RST                 [J IF NOT FOUND  
   7 69JL ...XHLD  FREECORE 3
  17 69KS ...RST   LDX   3  AWORK3(2)           [RESTORE X1 AND X3   
   9 69M2 ...      LDX   1  AWORK4(2)
   4 69M5 ...XDIF
   4 69M6 ...[   
  21 69M7 ...[     WE GET ALL ACTIVITIES TO REISSUE TRANSFERS BY SETTING NOT INITIAL-
  21 69M8 ...[     ISED REPLY. THIS IS BECAUSE THE OTHER PROCESSOR MAY HAVE FORGOTTEN
  15 69M9 ...[     ABOUT ANY REPLIES IT WAS SENDING TO US.   
   4 69M= ...[   
   8 69M? ...      LDX   2  FX2  
   9 69M# ...      LDX   5  ETWINMK  
  17 69M* ...      STO   5  ACOMMUNE1(2)        [REMEMBER OLD SETTING
  14 69MB ...      STO   1  AWORK4(2)           [AND MOD 
  14 69MC ...      STO   3  AWORK3(2)           [AND APIA
  18 69MD ...      LDCT  0  #400                [STOPS USE OF TWINNING   
  19 69MF ...      ORN   0  INITCOM             [WHILE BSUNIT BEING CHANGED  
   9 69MG ...      STO   0  ETWINMK  
   7 69MH ...      LDX   0  5
   9 69MJ ...      ANDN  0  JIOUTS   
   9 69MK ...      TRACE    5,WRITEOS
  19 69ML ...      BZE   0  NWOS                [J IF NO WRITE OUTSTANDING   
  17 69MM ...XTY   IPCREP   W,3                 [SEE HOW ITS GOING   
  16 69MN ...      BPZ   3  XFIN                [J IF FINISHED   
  15 69MP ...      SUSPEND                      [ELSE WAIT   
   8 69MQ ...      BRN      XTY  
  11 69MR ...XFIN  MHUNT    3,EWDAS,ESENDT   
  10 69MS ...      NAME     3,EWDAS,ESEND
  11 69MT ...      TRACE    ENMARK(3),WAITED 
   9 69MW ...      LDN   0  JIOUTS   
  19 69MX ...      ERS   0  ACOMMUNE1(2)        [CLEAR WRITE OUTSTANDING MKR 
  18 69MY ...NWOS  LDN   6  7                   [NOT INITIALISED REPLY   
  10 69M_ ...      STO   6  ACOMMUNE2(2) 
  17 69N2 ...      DOWN     TWINSETD,5          [CLEAR OUT EVERYTHING
  14 69N3 ...      LDX   3  AWORK3(2)           [APIA PTR
  10 69N4 ...      SMO      BSUNIT4(3)   
   7 69N5 ...      LDN   0  0
  18 69N6 ...      BZE   0  NOUSERS             [J IF NO ONE HAS LOCK-OUT
  18 69N7 ...      COOR3    #41                 [WAIT FOR SPRING CLEAN   
  16 69N8 ...      BRN      NWOS                [AND TRY AGAIN   
   5 69N9 ...NOUSERS 
  14 69N= ...      LDCT  7  #10                 [LO BIT  
   9 69N? ...      LDX   5  BSUNIT(3)
   9 69N# ...      LDX   1  AWORK4(2)
   8 69N* ...      LDX   6  0(1) 
   9 69NB ...      TRACE    5,OURS   
   9 69NC ...      TRACE    6,THEIRS 
   7 69ND ...      ANDX  5  7
   7 69NF ...      ANDX  6  7
   7 69NG ...      ERX   5  6
  18 69NH ...      BNZ   5  V9B                 [J IF LOCK-OUTS DIFFER   
   9 69NJ ...      LDX   0  AFTERU   
  18 69NK ...      BNG   0  V9B                 [J IF WE STAY AS WE ARE  
  17 69NL ...      ERS   7  BSUNIT(3)           [OTHERWISE REVERSE   
  11 69NM ...      TRACE    BSUNIT(3),CHANGED
   8 69NN ...V9B   SMO      FX1  
   8 69NP ...      LDX   0  XM7  
  17 69NQ ...      ANDS  0  BSUNIT(3)           [CLEAR BITS 6 AND 7  
  10 69NR ...      LDX   0  ACOMMUNE1(2) 
  16 69NS ...      STO   0  ETWINMK             [RESTORE ETWINMK 
  15 69NT ...      BRN      V9                  [GO FOR NEXT 
  18 7CSG    V4    BZE   5  V6                  [DEDICATED IN BOTH LISTS 
   9 7D#6          ERX   5  BITS910  
  21 7DRQ          BZE   5  V9                  [DEDICATED TO THEM-UNDEDICATED TO US 
  19 7F?B    V6    LDX   4  1                   [COMPILE MESSAGE FOR GEOSTOP 
  10 7FR2          HUNTW    3,EWDAS,EGOT 
  17 7G=L          BPZ   3  V10                 [J IF ALREADY EXISTS 
  16 7GQ=          SETNCORE 2,3,EWDAS,EGOT      [ELSE SET ONE UP 
   7 7H9W          LDN   0  0
   9 7HPG          STO   0  ENTRGA(3)
   8 7J96          BRN      V11  
  10 7JNQ    V10   LDX   5  ALOGLEN(3)   
   7 7K8B          ADN   5  1
  15 7KN2          ALTLEN   3,5                 [EXTEND IT   
  10 7L7L          MHUNTW   3,EWDAS,EGOT 
   7 7LM=    V11   SMO      4
   7 7M6W          LDEX  1  0
   9 7M?5 ...      SMO      ENTRGA(3)
  18 7M#G ...      STO   1  ENTRGA+1(3)         [STO UNIT NO OF BAD UNIT 
   7 7M*X ...      LDN   0  1
  15 7MC# ...      ADS   0  ENTRGA(3)           [UPDATE MOD  
   7 7WF=    VE    LDX   1  4
  15 7WYW          BRN      V9                  [GO FOR NEXT 
   4 7XDG    #   
  15 7XY6    #     DEVICE IS UNDEDICATED IN OTHER PROCESSOR  
   4 7YCQ    #   
  17 7YXB    V5    BZE   5  V9                  [J IF DEDICATED TO US
  20 7_4J ...      BRN      V6                  [ERROR IF UNDEDICATED OR SHARED  
   9 8?M6    V2    GEOERR   1,NO DEV 
   4 8#6Q    #   
   4 8SQ2    [   
  10 8STT ...V1    HUNTW    2,EWDAS,ITCH 
   8 8SX6 ...      BNG   2  NTH  
  16 8SYC ...      FREECORE 2                   [FREE ALL ITCHES 
   8 8S_N ...      BRN      V1   
   4 8T2_ ...NTH 
  10 8T4= ...      HUNTW    2,EWDAS,EGOT 
  16 8TP=          BNG   2  V1A                 [J IF NO ERRORS  
   7 8TS7 ...      LDN   0  1
  18 8TT6 ...      ADS   0  ENTRGA(2)           [ONE FOR RECORD HEADER   
   6 8TW5 ...      TCLOSE
  13 8TX4 ...      LDN   0  IPBDOWN+GEODOWN+ETMFAIL  
   9 8TY3 ...      ANDX  0  ETWINMK  
   9 8T_2 ...      ERS   0  ETWINMK  
   5 8T__ ...      UP
   8 8W2Y ...V1A   LONGON   IPBDE
   7 8W3X ...      UPPLUS   1
   4 98F2    #   
   4 98YL    #   
   9 99D=    XM7            #77177777
   4 99XW    #   
   4 99YL ...[   
   4 99YW ...QK5 
   4 99_6 ...[   
  15 99_B ...[     PUT IN REPLY IN ACOMMUNE2 TO EVERYTHING   
   4 99_L ...[   
  10 99_W ...      LDX   6  ACOMMUNE2(2) 
   9 9=26 ...      STO   6  AWORK1(2)
  20 9=2B ...N2B   LDX   3  FX2                 [SEARCH ESENDS ON ACTIVITY CHAIN 
  12 9=2L ...N2A   HUNT2J   3,EWDAS,ESEND,,NES   
   9 9=2W ...      LDX   0  ENMARK(3)
   8 9=36 ...      SBN   0  INIT 
  17 9=3B ...      BZE   0  N2A                 [BUT NOT INITIALISE  
   9 9=3L ...      SBN   0  CLIP-INIT
  14 9=3W ...      BZE   0  N2A                 [OR CLOSE
  16 9=46 ...      LDX   5  3                   [REMEMBER ESEND  
  17 9=4B ...      CALL  7  R1                  [PUT REPLY IN ENLINK 
  15 9=4L ...      FREECORE 5                   [FREE ESEND  
  15 9=4W ...      BRN      N2B                 [GO FOR NEXT 
   4 9=56 ...[   
  15 9=5B ...[     NO MORE ON ACTIVITY CHAIN-SEARCH JOBRING  
   4 9=5L ...[   
   8 9=5W ...NES   LDX   2  FX2  
  11 9=66 ...      TRACE    AWORK1(2),K5TD   
   8 9=6W ...      SMO      FX1  
  10 9=76 ...      LDX   0  K7TWINSETD   
  16 9=7B ...      BZE   0  NTRAC3              [J IF NOT TRACING
  13 9=7L ...      LDN   0  #5305               [K5  
  10 9=7W ...      STO   0  ACOMMUNE3(2) 
   7 9=86 ...      TWTRACE   
   5 9=8B ...NTRAC3  
  14 9=8W ...      LDX   4  AWORK1(2)           [REPLY   
  17 9=96 ...      NGN   5  1                   [DON'T CHECK DETAILS 
   8 9=9B ...      CALL  7  R7   
   4 9=9L ...[   
  10 9=9W ...[     FREE EWDAS/ITCH BLOCKS
   4 9==6 ...[   
  10 9==B ...NCH   HUNTW    1,EWDAS,ITCH 
  15 9==L ...      BNG   1  YCH                 [NO MORE LEFT
   7 9==W ...      FREECORE 1
  15 9=?6 ...      BRN      NCH                 [FREE NEXT   
   4 9=?B ...[   
  14 9=?L ...[     NOW ADJUST BSUNIT FOR ALL SHARED DISCS
   4 9=?W ...[   
  21 9=#6 ...YCH   CALL  7  X6                  [GET PTR (X3) TO FIRST TWINNED DEVICE
  16 9=#B ...      BRN      NOTWINS             [GEOERR IF NONE  
  16 9=#L ...XGN   LADDP    3,2                 [X2 PTR TO APIA  
  16 9=#W ...      JSHARED  2,NSH               [TO NSH IF SHARED
  17 9=*6 ...      BRN      XNT                 [ELSE LOOK FOR NEXT  
   8 9=*B ...NSH   SMO      FX1  
   8 9=*L ...      LDX   0  XM7  
  16 9=*W ...      ANDS  0  BSUNIT(2)           [CLEAR B6 AND B7 
  18 9=B6 ...XNT   CALL  7  X3                  [GET NEXT TWINNED DEVICE 
  14 9=BB ...      BRN      XALL                [NO MORE 
   8 9=BL ...      BRN      XGN  
   5 9=BW ...XALL  UP
   9 9=C8 ...XCME  GEOERR   1,NOTCME 
  11 9=CG          MENDAREA 50,K99TWINSETD   
   4 9=X6    #   
   4 9?BQ    #END
   8 ____ ...33365745000100000000
