   8 22FL    #OPT  K0PMENDITS=0  
  11 22_=    #LIS  K0PMENDITS>K0POST>K0ALLGEO
  16 23DW    #SEG  PMENDITS85                     [M.C.D. MANN   
  16 23NN ...#     (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23YG    #   
  16 24D6    #     THIS SEGMENT GIVES A LIST OF MENDITED SEGMENTS
  13 24XQ    #     DURING THE CURRENT RUN OF GEORGE  
   4 25CB    #   
  10 25X2                   8HPMENDITS   
   4 26BL    #   
   9 26W=    #     ENTRY POINT TABLE 
   4 27*W    #   
  13 27TG          SEGENTRY K1PMENDITS,XK1PMENDITS   
   4 28*6    #   
  17 28SQ    XHEAD          40HMENDITS INCLUDED IN THIS RUN OF GEORGE
  18 29#B    XCOLHEAD       44H    NAME             GEN        CHECKSUM  
  10 29S2 ...               10H    BS/128
   9 2=?L    MPATCH         7HPATCHED
  11 2=R=    MFAIL          13HCHECKSUM FAIL 
  10 2?=W    MNOKEYS        9HNOT KEYED  
  12 2?QG ...MNOTSUM        16HCHECKSUMMING OFF  
  10 2#=6    MZEROS         9HZERO KEYS  
   9 2#PQ    MNONE          4HNONE   
   4 2*9B    #   
  18 2*P2    XDMODS         +DMODSA             [TABLE OF BACKING STORE  
   9 2B8L                   +LMODSA  
  18 2BN=                   +DMODSB             [ADDRESSES AND LENGTHS   
   9 2C7W                   +LMODSB  
  19 2CMG ...               +DMODSC             [OF THE SEVEN ^MODS^ SEGMENTS
   9 2D76                   +LMODSC  
   9 2DLQ                   +DMODSD  
   9 2F6B                   +LMODSD  
   9 2FL2                   +DMODSE  
   9 2G5L                   +LMODSE  
   9 2GK=                   +DMODSF  
   9 2H4W                   +LMODSF  
   9 2H=4 ...               +DMODSG  
   9 2HC= ...               +LMODSG  
  15 2HJG    TABEND         #40000000           [END OF TABLE
   4 2J46    #   
  16 2JHQ    XEND           +ZEND               [END OF SEGMENT  
  21 2K3B    MITNUM         0                   [NO. OF MENDIT ENTRIES PER SEGMENT   
  19 2KH2    SEGPTR         +XDMODS-2           [POINTER TO THE ^MODS^ TABLE 
  20 2L2L    XLEN           6                   [LENGTH OF A ^MODS^ MENDIT RECORD
  20 2LG=    PRINTED        -1                  [SET ZERO WHEN A MENDIT IS FOUND 
   4 2L_W    #   
  15 2MFG    #     ROUTINE TO CONVERT THE BINARY NUMBER IN   
  14 2M_6    #     BITS 0 TO 15 OF X5 TO OCTAL CHARACTERS
   4 2NDQ    #   
   9 2NYB    #     ENTRY    X7 : LINK
  17 2PD2    #              X5 : BITS 0-15 CONTAIN THE BINARY NUMBER 
  16 2PXL    #              JPMOD : PRINT LINE CHARACTER MODIFIER
   4 2QC=    #   
  12 2QWW    #     EXIT     X3 AND JPMOD UPDATED 
  14 2RBG    #              X0, X5 AND X6 ARE UNDEFINED  
   4 2RW6    #   
   4 2S*Q    XOCT
  17 2STB          LDX   3  JPMOD               [LOAD OUTPUT MODIFIER
  18 2T*2          SRL   5  2                   [LOAD NUMBER IN BITS 0-17
  16 2TSL          LDN   0  7                   [CHARACTER COUNT 
  17 2W#=          LDN   6  #23                 [LOAD A ^#^ CHARACTER
  19 2WRW    XOCT1 DCH   6  0(3)                [DEPOSIT CHARACTER IN BUFFER 
   7 2X?G          BCHX  3  /
  14 2XR6          LDN   6  0                   [STOZ X6 
  18 2Y=Q          SLC   56 3                   [GET NEXT OCTAL CHARACTER
  20 2YQB          BCT   0  XOCT1               [J UNTIL ALL CHARACTERS DEPOSITED
  18 2_=2          STO   3  JPMOD               [UPDATE OUTPUT MODIFIER  
   7 2_PL          EXIT  7  0
   4 329=    #   
  14 32NW    #     THIS IS THE ONE AND ONLY ENTRY POINT  
   4 338G    #   
   6 33N6    XK1PMENDITS 
   8 347Q          LDX   1  FXPM1
  20 34MB          ADS   1  XEND(1)             [DATUMISE VARIOUS PRESET POINTERS
   9 3572          ADS   1  SEGPTR(1)
  21 35LL          PMESSAGE XHEAD(1),10         [PRINT THE ^MENDITS INCLUDED^ HEADING
  10 366=          LDN   2  XCOLHEAD(1)  
   9 36KW          LDX   3  JLPMOD   
   8 375G          MOVE  2  14   
  18 37K6          CALL  7  (JPRINT2)           [PRINT THE COLUMN HEADING
  16 384Q          CALL  7  (JPRINT1)           [AND A BLANK LINE
   5 38JB    NEXTSEG 
  20 3942          LDX   3  SEGPTR(1)           [LOAD THE SEGMENT TABLE POINTER  
  17 39HL          ADN   3  2                   [AND INCREASE IT BY 2
   8 3=3=          LDX   6  1(3) 
   9 3=GW          DVS   5  XLEN(1)  
  19 3?2G          STO   6  MITNUM(1)           [STO MAX MENDITS FOR THIS SEG
  21 3?G6          LDX   6  1(3)                [LOAD LENGTH OF THE ^MODS^ SEGMENT   
  20 3?_Q          LDXC  5  0(3)                [LOAD B/S ADDRESS OF NEXT SEGMENT
  18 3#FB          BCS      THATSALL            [J IF NO MORE SEGMENTS   
  19 3#_2          BZE   5  NEXTSEG             [J IF SEGMENT DOESN'T EXIST  
  19 3*DL          STO   3  SEGPTR(1)           [REMEMBER WHERE WE'VE REACHED
  19 3*Y=          PMBSADD  GEO,0,6,XEND(1),5   [READ THE NEXT ^MODS^ SEGMENT
   8 3BCW          LDX   1  FXPM1
  19 3BXG          LDX   4  MITNUM(1)           [SET COUNT OF MAXIMUM MENDITS
  20 3CC6          LDX   2  XEND(1)             [LOAD CORE ADDRESS OF THE SEGMENT
   6 3CWQ    NEXTRECORD  
  19 3DBB          LDX   3  0(2)                [PICK UP FIRST WORD OF RECORD
  16 3DW2          BZE   3  TISEMPTY            [J IF IT'S EMPTY 
  21 3F*L          STOZ     PRINTED(1)          [REMEMBER AT LEAST ONE MENDIT FOUND  
   9 3FT=          LDX   3  JLPMOD   
   7 3G#W          ADN   3  1
  21 3GSG          MOVE  2  3                   [STORE SUBFILE NAME & VERSION NUMBER 
  18 3H#6          CALL  7  (JUP)               [UPDATE BUFFER ADDRESS   
   8 3HRQ                   18   
  19 3J?B          LDX   5  3(2)                [LOAD THE GENERATION NUMBER  
  19 3JR2          ANDN  5  #7777               [FROM BITS 12 - 23 OF WORD 3 
  19 3K=L          CALL  7  (JDECN)             [CONVERT TO DECIMAL AND STORE
   8 3KQ=          CALL  7  (JUP)
   7 3L9W                   7
  18 3LPG          LDX   5  5(2)                [LOAD THE BINARY CHECKSUM
  19 3M96          CALL  7  (JOCT)              [CONVERT AND STORE AS OCTAL  
   8 3MNQ          CALL  7  (JUP)
   7 3N8B                   7
  20 3NN2          LDX   5  4(2)                [LOAD B/S ADDR  (DIVIDED BY 128) 
  19 3P7L          CALL  7  XOCT                [CONVERT INTO OCTAL & STORE  
   8 3PM=          CALL  7  (JUP)
   7 3Q6W                   7
  17 3QLG          LDX   5  3(2)                [LOAD FLAG BITS WORD 
  16 3R66          BPZ   5  NOTPATCHED          [J IF NOT PATCHED
   8 3RKQ          LDX   7  JPMOD
  15 3S5B          LDN   6  MPATCH(1)           [^PATCHED^   
   7 3SK2          MVCH  6  7
   8 3T4L          CALL  7  (JUP)
   8 3TJ=                   11   
   6 3W3W    NOTPATCHED  
  16 3WHG          SLL   5  1                   [LOOK AT BIT 1   
  18 3X36          BPZ   5  NOFAIL              [J IF NO CHECKSUM FAIL   
   8 3XGQ          LDX   7  JPMOD
  16 3Y2B          LDN   6  MFAIL(1)            [^CHECKSUM FAIL^ 
   8 3YG2          MVCH  6  13   
   8 3Y_L          CALL  7  (JUP)
   8 3_F=                   18   
  20 3_YW          SLL   5  2                   [MUST BE KEYED AND CHECKSUMMED   
  19 42DG          BRN      NOTCHOFF            [SO CHECK ONLY FOR ZERO KEYS 
   5 42Y6    NOFAIL  
  16 43CQ          SLL   5  1                   [LOOK AT BIT 2   
  18 43XB          BPZ   5  NOTKEYOFF           [J IF KEYING SWITCHED ON 
   8 44C2          LDX   7  JPMOD
  15 44WL          LDN   6  MNOKEYS(1)          [^NOT KEYED^ 
   7 45B=          MVCH  6  9
  18 45TW          BRN      PRINTLINE           [IT CAN'T BE CHECKSUMMED 
   6 46*G    NOTKEYOFF   
  16 46T6          SLL   5  1                   [LOOK AT BIT 3   
  20 47#Q          BPZ   5  NOTCHOFF            [J IF CHECKSUMMING SWITCHED ON   
   8 47SB          LDX   7  JPMOD
  17 48#2 ...      LDN   6  MNOTSUM(1)          [^CHECKSUMMING OFF^  
   8 48RL ...      MVCH  6  16   
   8 49?=          CALL  7  (JUP)
   8 49QW                   18   
   5 4==G    NOTCHOFF
  16 4=Q6          SLL   5  1                   [LOOK AT BIT 4   
  17 4?9Q          BPZ   5  PRINTLINE           [J IF NO ZERO KEYS   
   8 4?PB          LDX   7  JPMOD
  15 4#92          LDN   6  MZEROS(1)           [^ZERO KEYS^ 
   7 4#NL          MVCH  6  9
   6 4*8=    PRINTLINE   
  16 4*MW          CALL  7  (JPRINT1)           [PRINT THE LINE  
   5 4B7G    TISEMPTY
  19 4BM6          ADX   2  XLEN(1)             [UPDATE ^MODS^ RECORD ADDRESS
  19 4C6Q          BCT   4  NEXTRECORD          [REDUCE COUNT BY 1 AND PRINT 
  19 4CLB          BRN      NEXTSEG             [READ THE NEXT ^MODS^ SEGMENT
   5 4D62    THATSALL
  20 4DKL          LDX   7  PRINTED(1)          [HAVE ANY MENDITS BEEN PRINTED?  
  14 4F5=          BZE   7  SOME                [J IF SO 
  15 4FJW          PMESSAGE MNONE(1),1          [PRINT ^NONE^
   4 4G4G    SOME
  18 4GJ6          CALL  7  (JPRINT1)           [PRINT A FINAL BLANK LINE
  17 4H3Q          BRN      (JPMSEG)            [AND RETURN TO PMDR  
   4 4HHB    #   
  21 4J32    ZEND                               [CORE BUFFER FOR EACH ^MODS^ SEGMENT 
   4 4JGL    #END
   6 ____ ...044067310001
