   4 22FL    #   
   8 22_=    #OPT  K0PMWHATA=0   
  11 23DW    #LIS  K0PMWHATA>K0POST>K0ALLGEO 
   4 23YG    #   
  15 24D6    #SEG  PMWHATA85                    [M.C.D. MANN 
   4 24XQ    #   
  19 25CB    #     THIS SEGMENT ALWAYS ENTERED EXCEPT WHEN BREAK ON GEOSTOP. 
  19 25X2    #     IT ASKS THE OPERATOR WHAT KIND OF PM IS REQ'D AND DECIDES 
   9 26BL    #     WHICH LP TO USE.  
   4 26W=    #   
   9 27*W                   8HPMWHATA
   4 27TG    #   
   9 28*6    #     ENTRY POINT TABLE 
   4 28SQ    #   
  11 29#B     SEGENTRY K1PMWHATA,XK1PMWHATA  
   4 29S2    #   
  12 2=?L    #              CHECKSUM ERROR ACTION
   4 2=R=    #   
  17 2?=W    XSUMMESS       +34,34H_*MEND CHECKSUM ERROR IN RESTORE_*
  17 2?QG    X1             +1                  [NO WORDS TRANSFERRED
  16 2#=6    PMWHATAEND     +ZPMWHATA+1         [ADDRESS IN CORE 
  21 2#PQ    XSUM           -2                  [TO HOLD VALUE +DPMENDNOS+LPMENDNOS-2
   4 2*9B    #   
   4 2*P2    #   
  15 2B8L    MESSA          +24,24H_*POSTMORTEM REQUIRED?_*  
  10 2BN=    MESSB          +6,6H  OK_*  
  14 2C7W    MESSC          +19,19HUNIT     NOT A LP_*   
  15 2CMG    MESSD          +23,23H_*TYPE OF POSTMORTEM?_*   
   9 2D76    MESSE          +2,2H_*  
  14 2DLQ    MESSF          +19,19H_*NO LP AVAILABLE_*   
  15 2F6B    MESSG          +25,25H  _*UNIT     IS WRONGED_* 
  14 2FL2    MESSH          +21,21H  _*UNIT     FAILED_* 
  17 2G5L    MESSI          +34,34H  _*UNIT     BUSY, UNABLE TO USE_*
  17 2GK=    MESSJ           +32,32H_*PARAMETER ERROR:          _*   
  17 2GP7 ...MESSK          +31,31H  _*UNIT     IS NOT AVAILABLE_*   
  17 2GT4 ...MESSL          +34,34H  _*UNIT     ALREADY SWITCHED IN_*
  14 2GY_ ...MESSM          +19,19H  _*SW   REJECTED_*   
  17 2H4W    XJFCS          +JFCS               [END OF FIXED CORE   
   7 2HJG    XCHARSNO       0
   8 2J46    XCOMMA         #34  
   8 2JHQ    XLINK2         +0   
   8 2K3B    XLINK3         +0   
   8 2KH2    XLINK4         +0   
   8 2L2L    XLINK5         +0   
   8 2LG=    SPACE          #20  
   8 2L_W    SPACEFUL       4H   
  15 2MFG    XLINKA         +0                  [LINK ADDRESS
  17 2M_6    XALLPOST       +0                  [WORKING CONTEXT BITS
  18 2NDQ    XPARNUM        +1                  [CURRENT PARAMETER NUMBER
   8 2NYB    TWO            +2   
   8 2PD2    UPDATE         +0   
  18 2PXL    XEND           +0                  [END OF TABLE INDICATOR  
  16 2QC=    XLP            +0                  [LP DEV. REC. PTR
   9 2QWW    XRITE          4H00RI   
   9 2R6N ...XSW            4H00SW   
  19 2RBG    NUM            4H                  [HOLDS GEOGRAPHICAL NUMBER   
  19 2RW6    WHATP          +0                  [START ADDRESS OF REPLY TABLE
   4 2S*Q    #   
  19 2STB    #     TABLE OF VALID REPLIES TO 'POSTMORTEM REQUIRED?' MESSAGE  
   4 2T*2    #   
   4 2TSL    WHAT
   8 2W#=                   3HYES
   6 2WRW          NULL  
   8 2X?G          BRN      REP5 
   9 2XR6                   4HLIST   
   9 2Y=Q          LDN   3  1+2+16   
   8 2YQB          BRN      REP3 
   9 2_=2                   4HMINI   
   8 2_PL          LDN   3  1+16 
   8 329=          BRN      REP3 
   8 32NW                   2HLD 
   9 338G          LDN   3  1+2+8+16 
   8 33N6          BRN      REP3 
   8 347Q                   2HMD 
   9 34MB          LDN   3  1+8+16   
   8 3572          BRN      REP3 
   8 35LL                   2HLL 
   9 366=          LDN   3  1+2+16   
   8 36KW          BRN      REP2 
   8 375G                   2HLS 
   9 37K6          LDN   3  1+2+16   
   8 384Q          BRN      REP3 
   8 38JB                   2HML 
   8 3942          LDN   3  1+16 
   8 39HL          BRN      REP2 
   8 3=3=                   2HMS 
   8 3=GW          LDN   3  1+16 
   8 3?2G          BRN      REP3 
   8 3?G6                  3HLSD 
   9 3?_Q          LDN   3  1+2+8+16 
   8 3#FB          BRN      REP3 
   8 3#_2                  3HMSD 
   9 3*DL          LDN   3  1+8+16   
   8 3*Y=          BRN      REP3 
   9 3BCW                    3HLLD   
   9 3BXG          LDN   3  1+2+8+16 
   8 3CC6          BRN      REP2 
   8 3CWQ                   3HMLD
   9 3DBB          LDN   3  1+8+16   
   8 3DW2          BRN      REP2 
   5 3F*L    WHAT1   
   9 3FT=                   4HDUMP   
   8 3G#W          LDN   3  1+8  
   8 3GSG          BRN      REP4 
   8 3H#6                    2HDL
   8 3HRQ          LDN   3  1+8  
   8 3J?B          BRN      REP6 
   8 3JR2                   2HNO 
   7 3K=L          LDN   3  1
   8 3KQ=          BRN      REP4 
   9 3L9W    WHATA          +WHATA   
   8 3LPG    WNXT         2HMT   
   8 3M96          BRN      XMT  
   8 3MNQ                   2HDA 
   8 3N8B          BRN      XDA  
   5 3NN2    WNXTA   
   4 3P7L    #   
  15 3PM=    #      ROUTINE TO EXTRACT A SPECIFIED PARAMETER 
  18 3Q6W    #      FROM A GIVEN FIELD OF PARAMETERS ADDRESSED BY JCCBUF 
   4 3QLG    #   
  15 3R66    #      ON ENTRY  X5 CONTAINS NUMBER OF PARAMETER
  18 3RKQ    #                X6 CONTAINS TOTAL NUMBER OF CHARS IN FIELD 
   4 3S5B    #   
  20 3SK2    #      ON EXIT   X3 CONTAINS MODIFIER POINTING TO FIRST CHARACTER   
  14 3T4L    #                   OF REQUIRED PARAMETER   
  19 3TJ=    #                X7 CONTAINS NUMBER OF CHARACTERS IN PARAMETER  
  14 3W3W    #                   IF ZERO  NULL PARAMETER 
   4 3WHG    #   
   7 3X36    #      LINK X0  
   4 3XGQ    #   
  11 3Y2B    #      ACCS  1,2,4,5 UNCHANGED  
   4 3YG2    #   
  19 3Y_L    PMPAR LDN   3  JCCBUF              [ POINTER TO CHARACTER FIELD 
  17 3_F=          STO   5  XLINK4(1)           [ PARAMETER NUMBER   
  16 3_YW          STO   6  XLINK2(1)           [ CHARS IN FIELD 
   9 42DG          STOZ     XLINK5(1)
   7 42Y6          LDN   7  1
  16 43CQ    PAR6  LDCH  6  0(3)                [ GET NEXT CHAR  
   9 43XB          TXU   6  SPACE(1) 
  15 44C2          BCC      PAR                 [ J IF SPACE 
   9 44WL          STO   3  XLINK3(1)
   9 45B=    PAR3  TXU   6  XCOMMA(1)
  15 45TW          BCC      PAR1                [ J IF COMMA 
  18 46*G          ADS   7  XLINK5(1)           [ INCREMENT CHAR. COUNT  
   7 46T6          BCHX  3  /
   9 47#Q    PAR5  SBS   7  XLINK2(1)
   9 47SB          LDX   6  XLINK2(1)
  20 48#2          BZE   6  PAR2                [ ALL FIELD EXAMINED ? J IF YES  
  16 48RL          LDCH  6  0(3)                [ LOAD NEXT CHAR 
  16 49?=          BRN      PAR3                [ LOAD NEXT CHAR 
   9 49QW    PAR1  SBS   7  XLINK4(1)
   9 4==G          LDX   6  XLINK4(1)
  18 4=Q6          BZE   6  PAR4                [ J IF PARAMETER WANTED  
  21 4?9Q          STOZ     XLINK5(1)           [ INITIALISE CHARS. IN THIS PARAMETER
   7 4?PB          BCHX  3  /
   9 4#92          STO   3  XLINK3(1)
  20 4#NL          BRN      PAR5                [ COUNT CHARS. IN THIS PARAMETER 
   7 4*8=    PAR   BCHX  3  /
   9 4*MW          SBS   7  XLINK2(1)
   9 4B7G          LDX   6  XLINK2(1)
  19 4BM6          BNZ   6  PAR6                [ J IF MORE CHARS TO CHECK   
   9 4C6Q    PAR2  SBS   7  XLINK4(1)
   9 4CLB          LDX   6  XLINK4(1)
  18 4D62          BZE   6  PAR4                [ J IF PARAMETER WANTED  
   9 4DKL          STOZ     XLINK3(1)
   9 4F5=          STOZ     XLINK5(1)
  18 4FJW    PAR4  LDX   7  XLINK5(1)           [ SET UP EXIT CONDITIONS 
   9 4G4G          LDX   3  XLINK3(1)
  15 4GJ6          EXIT  0  0                   [ OUT AGAIN. 
   4 4H3Q    #   
   4 4HHB    #   
   4 4J32    #   
   4 4JGL    #   
   4 4K2=    #   
   4 4KFW    #   
  19 4K_G    #     ROUTINE TO EITHER GET THE LOWEST LINE PRINTER AVAILABLE   
  18 4LF6    #     OR THE UNIT NUMBER OF A LINE PRINTER WITH A SPECIFIC  
  10 4LYQ    #     GEOGRAPHICAL NUMBER   
   9 4MDB    #     ENTRY      X7=LINK
  15 4MY2    #              X5=NEG, GET LOWEST LP AVAILABLE  
  15 4NCL    #                X5=N  GET LP WITH GEOG. NO.=N  
  16 4NX=    #     EXIT  7  0                   [NO LP AVAILABLE 
  19 4PBW    #     EXIT  7  1                   [X5=GEOG NO., X3=DEV REC PTR 
   4 4PWG    #   
   5 4QB6    PRINTER 
  15 4QTQ          LDN   6  2                   [LP IS TYPE 2
  17 4R*B          FINDPEREC 3,APFOTY,6,XYZ     [GET DEVICE RECORD   
  17 4RT2          BNG   3  P                   [J IF NO MORE LP'S   
  17 4S#L          LGEOG 3,4                    [ELSE GET GEOG NO.   
  17 4SS=          BNG   5  P2                  [J IF NOT CHECKING IT
  15 4T?W          TXU   4  5                   [ELSE TEST   
  19 4TRG          BCS   XYZ                    [J NOT RIGHT ONE TO TRY AGAIN
  17 4W?6    P1    LDX   5  4                   [SET GEOG NO IN X5   
  15 4WQQ          EXIT  7  1                   [EXIT FOUND. 
  17 4X=B    P2    JWRONG 3,XYZ                 [TRY AGAIN IF WRONGED
  20 4XQ2          JFAIL 3,XYZ                  [TRY AGAIN IF FAILED LAST PERI   
  21 4Y9L          JBUSY 3,XYZ                  [TRY AGAIN IF LEFT BUSY FROM GEORGE  
   7 4Y?J ...#UNS  G3SIGNET  
   4 4Y*G ...(   
  10 4YCD ...      JENVNOT NOTMEEP,MEEP  
  10 4YFB ...      TXU   3  ASIGNETPTR   
  17 4YH# ...      BCC      XYZ                 [J IF SIGNET PRINTER 
   5 4YK= ...NOTMEEP 
   4 4YM8 ...)   
  14 4YP=          BRN   P1                     [ELSE OK 
  18 4_8W    P     EXIT  7  0                   [EXIT NO LP AVAILABLE.   
   4 4_NG    #   
   4 5286    #   
  18 52MQ    #     ROUTINE TO CONVERT GEOGRAPHICAL NUMBER FROM DECIMAL TO
  18 537B    #     BINARY PRESERVING THE NUMBER IN DECIMAL FORMAT AS WELL
  19 53M2    #     IF ANY OF THE ORIGINAL CHARACTERS ARE NOT NUMERICAL THE   
  15 546L    #     ROUTINE BRANCHES OUT TO OUTPUT A MESSAGE  
  18 54L=    #     ENTRY      X3=ADDRESS OF FIRST CHARACTER OF GEOG. NO. 
   9 555W    #                X0=LINK
  16 55KG    #                X7=COUNT OF CHARACTERS IN NUMBER   
  13 5656    #     EXIT    X4-X5=D/L BINARY GEOG. NO.
  17 56JQ    #               NUM=RIGHT JUSTIFIED DECIMAL GEOG. NO.   
   9 574B    #     X1,X2=UNCHANGED   
   5 57J2    XDECBIN 
  17 583L          STO   0  XLINKA(1)           [STORE LINK ADDRESS  
   7 58H=          STOZ     4
   7 592W          STOZ     5
  10 59GG          LDX   0  SPACEFUL(1)  
   7 5=26    XDB   SLC   0  6
  18 5=FQ          LDCH  6  0(3)                [STORE GEOG. NO. RIGHT   
  15 5=_B          DCH   6  0                   [JUSTIFIED IN
  16 5?F2          STO   0  NUM(1)              [DECIMAL FORMAT  
  17 5?YL          CDB   4  0(3)                [CONVERT GEOG. NO.   
  18 5#D=          BCS      XHA                 [J IF ERROR IN GEOG. NO. 
   7 5#XW          BCHX  3  /
  18 5*CG          BCT   7  XDB                 [FALL THROUGH IF FINISHED
   9 5*X6          LDX   0  XLINKA(1)
   7 5BBQ          EXIT  0  0
   4 5BWB    #   
  16 5CB2    #     ROUTINE TO DETERMINE WHETHER A GIVEN PARAMETER
  16 5CTL    #     IS IN A GIVEN TABLE.SPACES NOT SIGNIFICANT.   
  15 5D*=    #     HANDLES UP TO 4 SIGNIFICANT CHARACTERS.   
   8 5DSW    #    ENTRY  X0=LINK 
  16 5F#G    #           X3=ADDRESS OF FIRST CHARACTER OF PAR.   
  12 5FS6    #           X2=START ADDR. OF TABLE 
  13 5G?Q    #           X7=NO. OF CHARS IN PARAMETER
  14 5GRB    #           UPDATE=GAP BETWEEN TABLE ENTRIES
  13 5H?2    #           XEND=ADDR. OF END OF TABLE  
  13 5HQL    #    EXIT   X4=NO. OF SIGN. CHARS IN PAR
  17 5J==    #           X5=THE PARAMETER,LEFT JUSTIFIED,ZERO-FILLED 
  15 5JPW    #           X2=ADDR OF TABLE ENTRY FOR THIS PAR.
   9 5K9G    #       EX 0 - NOT FOUND
   8 5KP6    #       EX 1 - FOUND
  10 5L8Q    XCOMPR STO   0  XLINKA(1)   
   8 5LNB          LDN   0  24   
   7 5M82          LDN   4  4
  18 5MML    XD    LDCH  6  0(3)                [GET THE NEXT CHARACTER  
   9 5N7=          TXU   6  SPACE(1) 
  16 5NLW          BCS      XA                  [J IF NOT SPACE  
  15 5P6G          BCHX  3  /                   [IGNORE SPACE
  16 5PL6          BCT   7  XD                  [GO FOR NEXT ONE 
  14 5Q5Q          BRN      XA1                 [NO MORE 
   7 5QKB    XA    SLL   5  6
   7 5R52          DCH   6  5
   7 5RJL          SBN   0  6
   8 5S4=          BCT   7  XC   
  14 5SHW    XA1   BZE   0  XB                  [4 CHARS?
  14 5T3G          SMO      0                   [IF NOT, 
  15 5TH6          SLL   5  0                   [LEFT JUSTIFY
   8 5W2Q          BRN      XB   
   7 5WGB    XC    BCHX  3  /
  16 5X22          BCT   4  XD                  [GO FOR NEXT ONE 
   7 5XFL    XB    NGX   4  4
   7 5X_=          ADN   4  5
   9 5YDW          LDX   0  XLINKA(1)
   7 5YYG          EXIT  0  0
  13 5_D6    #           NOW SEARCH THROUGH THE TABLE
  14 5_XQ    #      FOR ENTRY & EXIT COND'S SEE XCOMPR   
   4 62CB    #   
  10 62X2    NXPAR  STO   0  XLINKA(1)   
   9 63BL          STO   4  XLINK4(1)
   9 63W=          STO   5  XLINK5(1)
   7 64*W    XB1   LDN   4  0
   7 64TG          LDN   6  0
   9 65*6          LDX   0  XLINK4(1)
   9 65SQ          LDX   5  XLINK5(1)
   8 66#B          LDX   7  0(2) 
   8 66S2    XA3   SLL   67  6   
   7 67?L          SLL   45 6
   7 67R=          TXU   4  6
  19 68=W          BCS      XA2                 [J IF PARAMETER NOT YET FOUND
   8 68QG          BCT   0  XA3  
   9 69=6          LDX   4  XLINK4(1)
   9 69PQ          LDX   0  XLINKA(1)
   9 6=9B          LDX   5  XLINK5(1)
  18 6=P2          EXIT  0  1                   [EXIT:  PARAMETER FOUND  
  14 6?8L    XA2   ADX   2  UPDATE(1)           [STEP ON 
   9 6?N=          TXU   2  XEND(1)  
  18 6#7W          BCS      XB1                 [J IF NOT END OF TABLE   
   9 6#MG          LDX   4  XLINK4(1)
   9 6*76          LDX   5  XLINK5(1)
   9 6*LQ          LDX   0  XLINKA(1)
  19 6B6B          EXIT  0  0                   [EXIT:  PARAMETER NOT FOUND  
   4 6BL2    #   
   4 6C5L    #   
   6 6CK=    XK1PMWHATA  
   4 6D4W    #   
   4 6DJG    #   
  20 6F46    #     THE FOLLOWING CODING ASKS THE OPERATOR WHAT SORT OF ACTION HE 
  21 6FHQ    #     WANTS POSTMORTEM TO TAKE,ANALYSES HIS REPLY,AND IF VALID SETS A   
  15 6G3B    #     BIT PATTERN IN ALLPOST(WORD IN SEG PMDR)  
   4 6GH2    #   
   8 6H2L          LDX   1  FXPM1
  21 6HG=          LDX   0  BDTP                 [ CHECK ENDFIX CONTAINS SENSIBLE VAL
   9 6H_W          LDX   4  ENDFIX   
   7 6JFG          TXL   0  4
  17 6J_6          BCC      OKENDFIX             [ IF NOT USE +JFCS  
   9 6KDQ          LDX   4  XJFCS(1) 
   5 6KYB    OKENDFIX
   9 6LD2          SMO      FXPMDR   
   9 6LXL          STO   4  JPMLEV+1 
  13 6MC=    #     CHECK FOR A MEND CHECKSUM ERROR   
  10 6MWW          PMDLGET  PMENDNOS,3,4 
   7 6NBG          ADX   3  4
  21 6NW6          ADS   3  XSUM(1)              [ SET UP LOCATION ON BACKING STORE O
  10 6P*Q          ADS   1  PMWHATAEND(1)
  21 6PTB          PMBSADD GEO,0,X1(1),PMWHATAEND(1),XSUM(1)  [GET GIN'S SWITCH WORD 
   8 6Q*2          LDX   1  FXPM1
  10 6QSL          SMO      PMWHATAEND(1)
   7 6R#=          LDX   2  0
  20 6RRW          BPZ   2  Y4                  [ J IF NO CHECKSUM ERROR FOUND   
   9 6S?G          SMO      FXPMDR   
   9 6SR6          STO   1  JBADMEND 
  10 6T=Q          LDN   3  XSUMMESS(1)  
   9 6TQB          CALL  7  (JCONOUT)
   9 6W=2          CALL  7  (JCONOUT)
   4 6WPL    #   
  16 6X9=    #              END OF MEND CHECKSUM ERROR ROUTINE   
   4 6XNW    #   
  18 6Y8G    Y4    LDN   2  WHAT(1)             [START ADDRESS OF TABLE  
   9 6YN6    Y4B   STO   2  WHATP(1) 
   9 6_7Q    Y4A   LDN   3  MESSA(1) 
  20 6_MB          STOZ     XLP(1)              [PRESET LP DEVICE LIST POINTER   
   7 7272          LDN   7  1
  20 72LL          STO   7  XPARNUM(1)          [AND THE CURRENT PARAMETER NUMBER
  16 736=          CALL  7  (JCONOUT)           [CONSOLE OUTPUT  
  17 73KW    X6    CALL  7  (JCTWIN)            [PERI ON T/W INPUT   
  21 745G          BRN   X6                     [A NOT OK READ BACAUSE OF 'CANCEL'   
  18 74K6    #                                  [SO SEARCH FOR I/P AGAIN 
  16 754Q          LDX   1  FXPM1               [RESET MODIFIER  
  21 75JB          ANDN  6  #777                [GET REMAINDER COUNT FROM REPLY WORD 
  17 7642          NGX   6  6                   [AND CALCULATE NUMBER
  16 76HL          ADN   6  64                  [OF WORDS READ   
  18 773=          BZE   6  Y4A                 [J IF NO CHARACTERS READ 
  17 77GW          STO   6  XCHARSNO(1)         [ELSE STORE NO READ  
   9 782G          LDX   2  WHATP(1) 
  17 78G6          LDN   5  1                   [SET PARAM NO TO 1   
  10 78_Q          LDX   6  XCHARSNO(1)  
   8 79FB          CALL  0 PMPAR 
  18 79_2          BZE   7  Y4A                 [J IF NO FIRST PARAMETER 
   7 7=DL          LDN   0  3
  16 7=Y=          STO   0  UPDATE(1)           [PREPARE YOURSELF
  15 7?CW          LDN   0  WHATA(1)            [FOR NXPAR   
   9 7?XG          STO   0  XEND(1)  
  17 7#C6          CALL  0  XCOMPR              [GET RID OF SPACES   
  17 7#WQ          CALL  0  NXPAR               [VALIDATE PARAMETER  
   8 7*BB          BRN      XRI  
   7 7*W2          EXIT  2  1
  18 7B*L    XRI   BZE   4  Y4A                 [NO CHARS TO SPEAK ABOUT 
   7 7BT=          LDX   7  4
  15 7C2D ...      LDX   4  SPACEFUL(1)         [CLEAR NUM   
   9 7C7L ...      STO   4  NUM(1)   
   7 7C#W          LDN   4  0
  17 7CSG          SLL   45 12                  [ISOLATE TOP 2 CHARS 
  15 7D#6          TXU   4  XRITE(1)            [IS IT 'RI'? 
  13 7DRQ ...      BCS      XSW1                [NO  
   7 7F?B          SBN   7  2
  16 7FR2          BZE   7  XG                  [J IF NO GEOG. NO
   7 7G=L          LDX   2  5
   7 7GQ=          LDN   3  2
  19 7H9W          CALL  0  XDECBIN             [CONVERT GEOG. NO. TO BINARY 
   9 7HPG          CALL  7  PRINTER  
  16 7J96          BRN      XG                  [J IF NO PRINTER 
  17 7JNQ          JRIGHT 3,XI                  [J IF DEVICE RIGHT   
  16 7K8B ...XRIT  RIGHT 3                      [ELSE RIGHT IT   
   9 7KN2          SMO      FXPMDR   
  17 7L7L          STOZ     JUNITNO             [SET JUNITNO NON-NEG 
   9 7LM=    XI    LDN  3  MESSB(1)  
  16 7M6W          CALL  7  (JCONOUT)           [CONSOLE OUTPUT  
  18 7MLG          LDN   2  WHAT(1)             [FULL TABLE OF REPLIES   
   9 7N66          STO   2  WHATP(1) 
   8 7NKQ          BRN      X6   
   8 7P5B    XG    LDN   5  X6(1)
   8 7PK2          BRN      XGC  
   9 7Q4L    XGA   LDN   5  Y4A(1)   
   9 7QJ=    XGC   LDX   6  NUM(1)   
  19 7R3W          STO   6  MESSC+2(1)          [LOAD GEOG. NO. INTO MESSAGE 
   9 7RHG          LDN   3  MESSE(1) 
   9 7S36          CALL  7  (JCONOUT)
   9 7SGQ          LDN   3  MESSC(1) 
   9 7T2B          CALL  7  (JCONOUT)
   8 7TG2          BRN      (5)  
  19 7T_L    XGB   LDN   2  WHAT1(1)            [LIMITED NO. OF PM DIRECTIVES
   9 7WF=          LDN   3  MESSF(1) 
   9 7WYW          CALL  7  (JCONOUT)
   8 7XDG          BRN      Y4B  
   9 7XY6    XHA   LDX   6  NUM(1)   
  10 7YCQ          STO   6  MESSJ+6(1)   
   9 7YXB          LDN   3  MESSJ(1) 
   9 7_C2 ...      CALL  7  (JCONOUT)
   8 7_WL          BRN      Y4A  
   4 7_X2 ...#   
  16 7_XB ...# THE FOLLOWING CODE IS TO SWITCH IN A LP ON REQUEST
  10 7_XQ ...# UNDER CME ENVIRONMENT ONLY
   4 7_Y6 ...#   
   4 7_YG ...XSW1
   9 7_YW ...      JENVNOT  Y4A,CME  
  17 7__= ...      TXU   4  XSW(1)              [ARE TOP 2 CHARS SW  
  13 7__L ...      BCS      Y4A                 [NO  
  13 8222 ...      SBN   7  2                   [YES-
  16 822B ...      BZE   7  XG                  [J IF NO GEOG NO 
   7 822Q ...      LDX   2  5
   7 8236 ...      LDN   3  2
  19 823G ...      CALL  0  XDECBIN             [CONVERT GEOG. NO TO BINARY  
  17 823W ...      FINDPEREC 3,APGEOG,5,,,XSW4  [SEARCH FOR DEVICE   
  17 824= ...      BPZ   3  XSW3                [ALREADY SWITCHED IN 
  15 824L ...      LDN   3  MESSK(1)            [NOT FOUND   
   9 8252 ...XSW2  LDX   6  NUM(1)   
   8 825B ...      STO   6  3(3) 
   9 825Q ...      CALL  7  (JCONOUT)
   8 8266 ...      BRN      Y4A  
   9 826G ...XSW3  LDN   3  MESSL(1) 
   8 826W ...      BRN      XSW2 
  15 827= ...XSW4  LTYPE    3,6                 [IS IT A LP  
   9 827L ...      TXU   6  TWO(1)   
  14 8282 ...      BCS      XSW5                [NO ERROR
  17 828B ...      SWIN     3,,XSW6,PM          [YES - SO SWITCH IN  
  15 828Q ...      BRN      XRIT                [SUCCESSFUL  
  17 8296 ...XSW6  LDN   3  MESSM(1)            [OUTPUT SW REJECTED  
   9 829G ...      LDX   6  NUM(1)   
   8 829W ...      DSA   6  1(3) 
   9 82== ...      CALL  7  (JCONOUT)
   8 82=L ...      BRN      Y4A  
  17 82?2 ...XSW5  LDN   3  MESSC(1)            [OUTPUT UNIT NOT A LP
   9 82?B ...      LDX   6  NUM(1)   
   8 82?Q ...      STO   6  1(3) 
   9 82#6 ...      CALL  7  (JCONOUT)
   8 82#G ...      BRN      Y4A  
   4 82#W ...#   
   4 82*= ...#   
  17 82B=    RA    LDX   5  XPARNUM(1)          [NO. OF CHECKED PARS 
   7 82TW          LDX   6  5
   7 83*G          SBN   6  3
  17 83T6          BZE   6  RALL                [J IF 3 PARS CHECKED 
   7 84#Q          ADN   5  1
  10 84SB          STO   5  XPARNUM(1)   
  10 85#2          LDX   6  XCHARSNO(1)  
   8 85RL          CALL  0  PMPAR
  15 86?=          BZE   7  RA                  [J IF NULL   
   7 86QW          LDN   0  2
   9 87=G          STO   0  UPDATE(1)
   9 87Q6          LDN   0  WNXTA(1) 
   9 889Q          STO   0  XEND(1)  
   9 88PB          LDN   2  WNXT(1)  
   9 8992          CALL  0  XCOMPR   
   9 89NL          TXU   4  TWO(1)   
   8 8=8=          BCS      WNOW 
  17 8=MW          CALL  0  NXPAR               [FIND THE TABLE ENTRY
  15 8?7G          BRN      WNOW                [NOT FOUND   
  18 8?M6 ...      EXIT  2  1                   [JUMP TO XDA OR XMT AS   
   9 8#6Q    #              MT GIVEN 
  10 8#LB    XMT   LDX   3  XALLPOST(1)  
   7 8*62          SRC   3  4
  16 8*KL          BPZ   3  Y4                  [J IF NOT DUMPING
   7 8B5=          SRC   3  3
  16 8BJW          BNG   3  Y4                  [J IF DA GIVEN   
   8 8C4G          LDN   4  128  
  16 8CJ6    XMT1  ORS   4  XALLPOST(1)         [SET APPR. BIT   
   8 8D3Q          BRN      RC1  
  10 8DHB    #              DA SPECIFIED 
  10 8F32    XDA   LDX   3  XALLPOST(1)  
   7 8FGL          SRC   3  4
  16 8G2=          BPZ   3  Y4                  [J IF NOT DUMPING
   7 8GFW          SRC   3  4
  17 8G_G          BNG   3  Y4                  [J IF MT SPECIFIED   
   8 8HF6          LDN   4  64   
   8 8HYQ          BRN      XMT1 
  10 8JDB    WNOW  LDX   3  XALLPOST(1)  
   7 8JY2          SRC   3  5
  16 8KCL          BPZ   3  Y4                  [J IF NOT LISTING
  19 8KX=          LDX   7  4                   [NO.OF CHARS IN GEOG.NO OF LP
   7 8LBW          LDN   3  2
   7 8LWG          LDX   2  5
  19 8MB6          CALL  0  XDECBIN             [CONVERT GEOG. NO. TO BINARY 
   9 8MTQ          CALL  7  PRINTER  
  15 8N*B          BRN      XGA                 [J IF NO LP  
   9 8NT2          LDN   5  MESSG(1) 
  16 8P#L          JWRONG 3,RD                  [J IF LP IS WRONG
   9 8PS=          LDN   5  MESSH(1) 
  18 8Q?W          JFAIL 3,RD                   [J IF LAST PERI FAILED   
   9 8QRG          LDN   5  MESSI(1) 
  15 8R?6          JUNBUSY 3,RC                 [J IF LP OK  
   9 8RQQ    RD    LDX   4  NUM(1)   
   7 8S=B          LDX   3  5
  19 8SQ2          STO   4  3(3)                [STORE GEOG. NO. IN MESSAGE  
   9 8T9L          CALL  7  (JCONOUT)
   8 8TP=          BRN      X6   
  10 8W8W    RALL  LDX   3  XALLPOST(1)  
   7 8WNG          SRC   3  4
  16 8X86          BPZ   3  RLS                 [J IF NOT DUMPING
   7 8XMQ          SRC   3  3
  15 8Y7B          BNG   3  RLS                 [J IF DA DUMP
   8 8YM2          LDN   4  128  
  16 8_6L          ORS   4  XALLPOST(1)         [SET MT DUMP BIT 
   9 8_L=    RLS   LDX   3  XLP(1)   
  17 925W          BNZ   3  RC3                 [J IF LP SPECIFIED   
   9 929R ...      SMO      FXPMDR   
  18 92*N ...      LDX   3  JLPPTR              [WAS THERE PREVIOUS LP   
  16 92FK ...      BNZ   3  RC                  [YES -SO USE IT  
  18 92KG    RB    NGN   5  1                   [MEANS GET ANY PRINTER   
  18 9356          CALL  7  PRINTER             [GET LOWEST AVAILABLE LP 
  16 93JQ          BRN      NOLP                [NO LP AVAILABLE 
   9 944B    RC    STO   3  XLP(1)   
  10 94J2    RC1   LDX   6  XPARNUM(1)   
   7 953L          SBN   6  3
  18 95H=          BNZ   6  RA                  [J IF NOT 3 PARS CHECKED 
   9 962W          LDX   3  XLP(1)   
  17 96GG          BZE   3  RLS                 [J IF NO LP SPECIFIED
   9 9726    RC3   LDX   1  FXPMDR   
  18 97FQ          LDX   6  JLPPTR(1)           [WAS THERE PREVIOUS LP?  
  16 97_B          BZE   6  REX                 [J IF NOT, ELSE  
  20 98F2          TXU   3  JLPPTR(1)           [IF 'JOBS' OUTPUT ON THIS PRINTER
  14 98YL          BCC      RC2                 [J TO RC2
   8 99D=          SMO      FXPM1
   8 99XW          LDX   5  NUM  
  10 9=CG          STO   5  JLPGEOG(1)   
  20 9=X6          LDN   5  JLPREC              ['UNDUMP' STORED DEVICE RECORD   
   9 9?BQ          MOVE  5  K53-K50  
  16 9?WB    REX   STO   3  JLPPTR(1)           [STORE NEW PTR   
  19 9#B2          LDX   6  JLPPTR(1)           [AND DUMP NEW DEVICE RECORD  
   9 9#TL          LDN   7  JLPREC   
   9 9**=          MOVE  6  K53-K50  
   5 9*GD ...#UNS AR1
  17 9*ML ...      CLHLFDON 3                   [CLEAR HALFDONEBIT   
   8 9*SW          SMO      FXPM1
   9 9B#G          LDX   6  XALLPOST 
  10 9BS6          STO   6  ALLPOST(1)   
  19 9C?Q          LDN   6  121                 [SET CONSTANT PARTS OF DEVICE
  15 9CRB          DCA   3,STO,6,COUNT          [CONTROL AREA
  16 9D?2          LDN   6  0                   [I.E. COUNT AND  
  13 9DQL          DCA   3,DLA,6,MODE           [MODE
  12 9F==          PMOVE      PMJOBS,FXPM1,1,1   
   4 9FPW    NOLP
   8 9G9G          SMO      FXPM1
  18 9GP6          LDX   7  XALLPOST            [ARE WE LISTING THE PM?  
   7 9H8Q          SRC   7  5
  19 9HNB          BNG   7  XGB                 [J IF SO: WE NEED A PRINTER! 
  18 9J82    RC2   SMO      FXPM1               [OTHERWISE: JUST CARRY ON
   9 9JML          LDX   6  XALLPOST 
   9 9K7=          SMO      FXPMDR   
   9 9KLW          STO   6  ALLPOST  
   9 9L6G          BRN      (JPMSEG) 
   4 9LL6    #   
  20 9M5Q    #     CODING TO SET 'ALLPOST' AFTER OPERATOR CONSOLE T/W DIRECTIVE  
   9 9MKB    #     FOUND TO BE VALID 
   4 9N52    #   
  20 9NJL    REP3                               [TO GIVE SHORT FIXED CORE LISTING
   9 9P4=          SMO      FXPMDR   
   9 9PHW          LDX   0  JPMLEV   
   9 9Q3G          SMO      FXPMDR   
   9 9QH6          STO   0  JPMLEV+1 
  20 9R2Q    REP2                               [TO GIVE LONG FIXED CORE LISTING 
   9 9RGB          SMO      FXPMDR   
   9 9S22          LDX   0  JPMLEV+1 
   9 9SFL          SMO      FXPMDR   
   9 9S_=          STO   0  JPMLEV   
  10 9TDW          STO   3  XALLPOST(1)  
   9 9TYG          LDN   3  MESSE(1) 
  17 9WD6          CALL  7  (JCONOUT)           [NEW LINE ON CONSOLE 
   8 9WXQ          BRN      RA   
   9 9XCB    REP5  LDN   3  MESSD(1) 
  21 9XX2          CALL  7  (JCONOUT)           [CONSOLE O/P 'TYPE OF POSTMORTEM?'   
   8 9YBL          BRN      X6   
   9 9YW=    REP6  SMO      FXPMDR   
   9 9_*W          LDX   0  JPMLEV+1 
   9 9_TG          SMO      FXPMDR   
   9 =2*6          STO   0  JPMLEV   
  15 =2SQ    REP4  STO   3  XALLPOST(1)          [DUMP OR NO 
   9 =3#B          SMO      FXPMDR   
   9 =3S2          LDX   0  JPMLEV   
   9 =4?L          SMO      FXPMDR   
   9 =4R=          STO   0  JPMLEV+1 
   9 =5=W          LDN   3  MESSE(1) 
   9 =5QG          CALL  7  (JCONOUT)
   8 =6=6          BRN      RA   
   4 =6PQ    #   
   4 =79B    #   
   5 =7P2    ZPMWHATA
   4 =88L    #   
   4 =8N=    #END
   6 ____ ...455677170002
