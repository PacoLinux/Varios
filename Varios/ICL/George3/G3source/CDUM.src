  16 22FL    #SEG  CDUM70                       [BOB NICHOLSON   
   6 22_=    #LIS  K0CDUM
   6 23DW          8HCDUM
   4 23YG    #   
  10 24D6          SEGENTRY K1CDUM,QK1TW 
  10 24XQ          SEGENTRY K2CDUM,QK2TW 
   9 252W ...      SEGENTRY K3CDUM   
  19 2562 ...               -1                  [=0 IF ALL TO BE DEDICATED   
   9 2596 ...      SEGENTRY K4CDUM   
  16 25#= ...               +0                  [=-1 IF TRACING  
  15 25CB    Q24            +24                 [FOR BUTTONUP
   4 25X2    #   
   4 26BL    #   
  20 26W=    #     INITIAL ENTRY POINT IN EMS TO CHECK DEDICATE/UNDEDICATE MACROS
   4 27*W    #   
   4 27TG    TYPE
  10 28*6                   +BSA+ENTWIN  
  10 28SQ    XLINK PAIR     TWINSETA,1   
   4 29#B    #   
   9 29S2    XM1            #77777777
   9 2=?L    XM2            +ETBUFFLE
   9 2=R=    XBITS510       #01020000
   9 2?=W    XBIT5910       #01060000
   9 2?GN ...XDUALCME       +EDUALCME
   4 2?QG    #   
  20 2#=6    #     SUBROUTINE TO COUNT NUMBER OF ENTRIES IN ARES AND AUNRES TABLE
   4 2#PQ    #   
  21 2*9B    #     LINK  X7  X3 PTR TO START OF AREA : COUNT IN X6 NOT CLEARED ON ENT
   4 2*P2    #   
  16 2B8L    X1    LDX   0  0(3)                [GET NEXT ENTRY  
   8 2BN=          SMO      FX1  
  15 2C7W          TXU   0  XM1                 [TEST IF END 
  14 2CMG          BCC      (7)                 [J IF SO 
  15 2D76          ADN   6  1                   [UPDATE COUNT
  14 2DLQ          ADN   3  1                   [AND PTR 
   8 2F6B          BRN      X1   
   4 2FL2    #   
   4 2G5L    #   
  21 2GK=    #     SUBROUTINE TO GO THOUGH ALL UDAS DEVICES LOOKING FOR TWINNED ONES.
   4 2H4W    #   
  19 2HJG    #     EXITS  0  IF NO MORE; EXITS+1 WITH DEVICE LIST PTR IN X3  
   4 2J46    #   
  21 2JHQ    #     CAN BE RE-ENTERED BY CALL 7 X3 TO GET NEXT DEVICE THAT IS TWINNED 
   4 2K3B    #   
   8 2KH2    X6    LDX   1  FX1  
   7 2L2L    X4    ADN   1  1
  16 2LG=          LDX   0  TAB-1(1)            [NEXT DEVICE TYPE
  15 2L_W          BNG   0  (7)                 [J IF NO MORE
  18 2MFG ...      FINDPEREC 3,APFOTY,TAB-1(1),X3 [GET PTR TO DEVICE LIST
  19 2M_6          BNG   3  X4                  [J IF NO MORE OF THIS TYPE   
  15 2NDQ          JTWIN    3,X5                [J IF TWINNED
  17 2NYB          BRN      X3                  [ELSE LOOK FOR NEXT  
   7 2PD2    X5    EXIT  7  1
   4 2PXL    #   
   4 2QC=    #   
   8 2QWW    TAB            +6   
   8 2RBG                   +13  
   8 2RW6                   +26  
   8 2S*Q                   -1   
   4 2STB    #   
  11 2T*2    #     TABLE FOR DEDICATE MACRO  
   4 2TSL    #   
   8 2W#= ...      SEGENTRY ARES 
   8 2WRW                   -1   
   5 2X?G    #REP  25
   8 2XR6                   +0   
   4 2Y=Q    #   
  11 2YQB    #     TABLE FOR UNDEDICATE MACRO
   4 2_=2    #   
   9 2_PL ...      SEGENTRY AUNRES   
   8 329=                   -1   
   5 32NW    #REP  25
   8 338G                   +0   
   4 33N6    #   
   4 347Q    #   
   8 34MB    QK1TW       [*****  
  19 3572          LDN   6  0                   [INITIALISE COUNT OF ENTRIES 
   9 35#8 ...      LDX   0  K3CDUM(1)
  20 35FB ...      BZE   0  X7                  [DONT DO ANYTHING IF TWINNING OFF
  18 35LL          LDN   3  ARES(1)             [PTR TO DEDICATE TABLE   
  19 366=          CALL  7  X1                  [GET COUNT OF DEDICATES IN X6
  15 36KW          LDX   5  6                   [AND REMEMBER
  18 375G          LDN   3  AUNRES(1)           [PTR TO UNDEDICATE TABLE 
  16 37K6          CALL  7  X1                  [AND GET COUNT   
  17 384Q          BNZ   6  X2                  [J IF SOME ENTRIES   
  19 38JB          CALL  7  X4                  [SEE IF ANY TWINNED DEVICES  
  14 3942          BRN      X7                  [J IF NOT
  21 39HL          LDN   6  0                   [INITIALISE COUNT OF TWINNED SPINDLES
  16 3=3=    X8    LADDP    3,2                 [GET PTR TO APIA 
  15 3=GW          ADN   6  1                   [UPDATE COUNT
   7 3=N4 ...      FSHSKIP   
   4 3=T= ...(   
   8 3?2G          SMO      FX1  
   9 3?G6          LDX   0  XBITS510 
   4 3?L3 ...)   
   8 3?PY ...      FSHCODE  AORB 
   8 3?TT ...      LDX   0  BIT10
  20 3?_Q          ORS   0  BSUNIT(2)           [SET LOCKED-OUT AND SHARED BITS  
  18 3#FB          CALL  7  X3                  [GET NEXT TWINNED DEVICE 
  14 3#_2          BRN      X9                  [NO MORE 
  17 3*DL          BRN      X8                  [GO TO CHECK THIS ONE
   4 3*Y=    #   
  15 3BCW    X7    UP                           [NO NOTHINGS 
   4 3BXG    #   
   4 3CC6    #   
   4 3CWQ    #   
  20 3DBB    #     GETS HERE IF EITHER DEDICATE OR UNDEDICATE HAVE BEEN ISSUED   
   4 3DW2    #   
  16 3F*L    X2    ADN   6  1                   [AND ONE FOR -1  
  20 3FT=          SETUPCORE 6,3,EWDAS,ESEND    [SET UP BLOCK TO HOLD UNIT NOS   
  16 3G#W          STOZ     ENTRGA(3)           [CLEAR OUT BLOCK 
   9 3GSG          LDN   4  ENTRGA(3)
  10 3H#6          LDN   5  ENTRGA+1(3)  
   7 3HRQ          SBN   6  1
   7 3J?B          SMO      6
   7 3JR2          MOVE  4  0
   9 3K=L          LDX   0  XM1(1)   
  16 3KQ=          STO   0  ENTRGA(3)           [-1 INDICATES END
  17 3L9W          LDN   3  ENTRGA(3)           [PTR TO START OF DATA
  16 3LPG          LDN   6  0                   [DEDICATED MARKER
  20 3M96          LDN   1  ARES(1)             [PTR TO START OF DEDICATED LIST  
  21 3MNQ          STOZ     AWORK1(2)           [COUNTER OF NUMBER OF NON-TWINNED DEV
  20 3N8B          STOZ     AWORK2(2)           [COUNTER OF NUMBER OF DUPLICATES 
  18 3NN2          CALL  7  X12                 [SORT OUT DEDICATED LIST 
   8 3P7L          LDX   2  FX1  
  18 3PM=          LDN   1  AUNRES(2)           [PTR TO UNDEDICATED LIST 
  21 3Q6W          LDX   6  XBIT5910(2)         [MKR FOR UNDEDICATED DEVICES IN APIA 
  17 3QLG          CALL  7  X12                 [AND SORT THEM OUT   
   8 3R66          LDX   2  FX2  
   9 3RKQ          LDX   0  AWORK1(2)
   9 3S5B          ADX   0  AWORK2(2)
  16 3SK2          BNZ   0  X19                 [J IF ANY ERRORS 
  10 3T4L          MHUNTW   2,EWDAS,ESEND
  16 3TJ=          LDN   6  0                   [INITIALISE COUNT
  20 3W3W          CALL  7  X6                  [GET PTR TO FIRST TWINNED DEVICE 
  16 3WHG          BRN      XMUCK               [SHOULD BE SOME  
  19 3X36    X24   LGEOG    3,7                 [X7 CONTAIN GEOGRAPHICAL NO. 
  15 3XGQ          ADN   6  1                   [UPDATE COUNT
  16 3Y2B          LDN   4  ENTRGA(2)           [START OF NUMBERS
   7 3YG2    X26   SMO      4
   7 3Y_L          LDX   0  0
   8 3_F=          SMO      FX1  
   8 3_YW          TXU   0  XM1  
  16 42DG          BCC      X23                 [J IF END OF LIST
   9 42Y6    #SKI  K6TWINSET>99-99   
   4 43CQ    (   
  17 43XB          LDCT  5  #600                [CHECK NO ERROR MARK 
   7 44C2          ANDX  5  0
   8 44WL          BZE   5  X22  
   9 45B=          GEOERR   1,HOOT!  
   4 45TW    X22 
   4 46*G    )   
   7 46T6          TXU   7  0
  20 47#Q          BCC      X25                 [J IF IN LIST - ALREADY DELT WITH
  15 47SB          ADN   4  1                   [UPDATE MOD  
  15 48#2          BRN      X26                 [GO FOR NEXT 
  20 48RL    X23   LADDP    3,4                 [NOT IN LIST SO MARK AS SHARED   
   7 48YS ...      FSHSKIP   
   4 4962 ...(   
   8 49?=          SMO      FX1  
   9 49QW          LDX   0  XBITS510 
   4 49WR ...)   
   8 4=2N ...      FSHCODE  AORB 
   8 4=6K ...      LDX   0  BIT10
   7 4==G          SMO      4
   9 4=Q6          ORS   0  BSUNIT   
  21 4?9Q    X25   CALL  7  X3                  [GET DL PTR TO NEXT TWINNED DEVICE   
  21 4?PB          BRN      X9A                 [NO-MORE - GO TO SET UP BSA/ENTWIN   
  17 4#92          BRN      X24                 [GO AND SORT OUT NEXT
   4 4#NL    #   
  21 4*8=    #     THIS BORING SUBROUTINE CHECKS OUT THE PARAMETERS TO THE DEDICATE  
  10 4*MW    #     AND UNDEDICATE MACROS.
   4 4B7G    #   
   7 4BM6    #     ON ENTRY:-
   4 4C6Q    #   
   9 4CLB    #     X0    WHO CARES!  
  21 4D62    #     X1    POINTER TO START OF DEDICATE OR UNDEDICATED LIST (ARES OR AU
   9 4DKL    #     X2    WHO CARES!  
  16 4F5=    #     X3    NEXT FREE WORD IN EWDAS/ESEND  (PTR TO) 
   8 4FJW    #     X4    SEE X0! 
  11 4G4G    #     X5    NOT USED (A RARITY) 
  20 4GJ6    #     X6    MARKER =0 IF DEDICATE LIST;=#01060000 IF UNDEDICATE LIST
   9 4H3Q    #     X7    ACTUAL LINK 
   4 4HHB    #   
  21 4J32    #     AWORK1 AND AWORK2 CONTAIN ERROR COUNTS AND ARE NOT CLEARED ON ENTR
   4 4JGL    #   
   7 4K2=    #     ON EXIT:- 
   4 4KFW    #   
  13 4K_G    #     X0,X1,X2,X4 ARE ABSOLUTLY USELESS 
  21 4LF6    #     X3    NEXT FREE ENTRY IN EWDAS/ESEND (NOT TO BE MUCKED UP FOR RE-E
   8 4LYQ    #     X5    NOT USED
  10 4MDB    #     X6    AS SET ON ENTRY 
   7 4MY2    #     X7    LINK
   4 4NCL    #   
  21 4NX=    #     ENTRIES IN THE LIST SPECIFIED IN X1 ARE MOVED TO THE EWDAS/ESEND  
  14 4PBW    #     BLOCK AND CHECKS ARE MADE AS FOLLOWS:-
   4 4PWG    #   
  19 4QB6    #     1.    THAT THE UNIT NO. HAS NOT ALREADY BEEN SPECIFIED.   
   4 4QTQ    #   
  19 4R*B    #           IF IT HAS,AWORK2 IS UPDATED AND THE ENTRY HAS B1 SET
   4 4RT2    #   
  21 4S#L    #     2.    THAT THE UNIT IS A TWINNED UDAS DEVICE:IF NOT B0 SET IN ENTR
  12 4SS=    #           AND AWORK1 IS UPDATED.  
   4 4T?W    #   
  17 4TRG    X12   LDX   0  0(1)                [GET FIRST/NEXT ENTRY
  15 4W?6          SMO      FX1                 [CHECK IF END
   8 4WQQ          TXU   0  XM1  
  15 4X=B          BCC      (7)                 [J IF NO MORE
  14 4XQ2          LDX   4  0(1)                [UNIT NO.
  10 4Y9L          MHUNTW   2,EWDAS,ESEND
  16 4YP=          LDN   2  ENTRGA(2)           [START OF DATA   
  21 4_8W    X18   LDX   0  0(2)                [CHECK NUMBER NOT ALREADY SPECIFIED  
   8 4_NG          SMO      FX1  
   8 5286          TXU   0  XM1  
  16 52MQ          BCC      X16                 [J IF END- O.K   
  21 537B          ANDX  0  BITS22LS            [GET RID OF ERROR BITS IF ANY FOR TES
   7 53M2          TXU   4  0
  18 546L          BCC      X17                 [J IF SPECIFIED ALREADY  
  15 54L=          ADN   2  1                   [UPDATE PTR  
  16 555W          BRN      X18                 [GET NEXT ENTRY  
   8 55KG    X17   LDCT  0  #200 
   8 5656          ANDX  0  0(2) 
  19 56JQ          BNZ   0  X15                 [J IF THIRD OR MORE REPEAT   
  13 574B          LDCT  0  #200                [.   
  16 57J2          ORX   4  0                   [SET ERROR FLAG  
   7 583L          LDN   0  1
   8 58H=          SMO      FX2  
  17 592W          ADS   0  AWORK2              [UPDATE ERROR COUNT  
  18 59GG    X16   LDX   0  0(3)                [MOVE END MARKED DOWN ONE
   8 5=26          STO   0  1(3) 
  16 5=FQ          STO   4  0(3)                [STORE NEW ENTRY 
   9 5=_B          ANDX  4  BITS22LS 
  20 5?F2 ...      FINDPEREC 2,APGEOG,4,,,XCME          [GET PTR TO DEVICE LIST  
  17 5?YL          BNG   2  X14                 [J IF NON-EXISTANT   
  20 5#D=          LTYPE    2,0                 [ELSE CHECK IF ITS A UDAS TYPE   
   8 5#XW          ANDN  0  #77  
   7 5*CG          SBN   0  6
  14 5*X6          BZE   0  X20                 [J IF EDS
   7 5BBQ          SBN   0  7
  14 5BWB          BZE   0  X20                 [OR FDS  
   8 5CB2          SBN   0  13   
  18 5CTL          BNZ   0  X14                 [J IF NOT EDS30 OR EDS 60
  15 5D*=    X20   JTWIN    2,X13               [J IF TWINNED
  17 5DSW    X14   LDCT  0  #400                [ELSE SET ERROR FLAG 
   8 5F#G          ORS   0  0(3) 
   7 5FS6          LDN   0  1
   8 5G?Q          SMO      FX2  
  17 5GRB          ADS   0  AWORK1              [UPDATE ERROR COUNT  
   8 5H?2          BRN      X15  
  16 5HQL    X13   LADDP    2,2                 [GET PTR TO APIA 
  16 5J==          ORS   6  BSUNIT(2)           [AND SET MARKERS 
  17 5JPW    X15   ADN   3  1                   [UPDATE MOD IN ESEND 
  14 5K9G          ADN   1  1                   [AND LIST
  16 5KP6          BRN      X12                 [SEE IF ANY MORE 
   4 5L8Q    #   
   4 5LNB    #   
  10 5M82    XMUCK GEOERR   1,MUCK UP!   
   4 5MML    #   
   4 5N7=    #   
   4 5NLW    #   
  18 5P6G    #     THIS ROUTINE OUTPUTS ERROR MESSAGES TO THE OPS CONSOLE
   4 5PL6    #   
   8 5Q5Q    #     LINK     X7   
   4 5QKB    #   
   4 5R52    #   
   7 5RJL    #     ON ENTRY  
  15 5S4=    #     X4       NO. OF UNIT NUMBERS TO BE OUTPUT 
  16 5SHW    #     X5       MASK TO GET APPROPRIATE UNIT NUMBER  
  14 5T3G    #     X6       INDENTIFIER OF SUB-MESSAGE   
   4 5TH6    #   
  16 5W2Q    RX25  SBX   7  FX1                 [RELITAVISE LINK 
  18 5WGB          ADN   4  1                   [COUNT OF UNIT NOS PLUS 1
  15 5X22          SETUPCORE 4,3,EWDAS,EGOT     [SET UP BLOCK
  18 5XFL          STO   4  ENTRGA(3)           [STORE COUNT FOR OUTPACK 
  16 5X_=          LDN   3  ENTRGA+1(3)         [NEXT VACANT HOLE
  12 5YDW          MHUNTW   1,EWDAS,ESEND       [
  15 5YYG          LDN   2  ENTRGA(1)           [FIRST ENTRY 
  15 5_D6    RX24  LDX   0  0(2)                [NEXT ENTRY  
   8 5_XQ          SMO      FX1  
   8 62CB          TXU   0  XM1  
  17 62X2          BCC      RX22                [J IF NO MORE ENTRIES
   7 63BL          ANDX  0  5
  19 63W=          BZE   0  RX23                [J IF THIS ENTRY NOT IN ERROR
  20 64*W          LDX   0  0(2)                [MOVE UNIT NO. TO PARAMETER BLOCK
   9 64TG          ANDX  0  BITS22LS 
   8 65*6          STO   0  0(3) 
  15 65SQ          ADN   3  1                   [UPDATE MOD  
  15 66#B    RX23  ADN   2  1                   [AND AGAIN   
  15 66S2          BRN      RX24                [GO FOR NEXT 
  10 67?L    RX22  MHUNTW   3,EWDAS,EGOT 
  18 67R=          OUTPACK  ENTRGA(3),V,VARDEC,RX22  [OUTPUT PARAMETER   
  15 68=W          MFREEW   EWDAS,EGOT          [FREE EGOT   
  15 68QG          OUTMESSX 6                   [OUTPUT %B   
  17 69=6          INFORM   1,ENTIN1,1          [AND ACTUALLY OUTPUT 
   8 69PQ          ADX   7  FX1  
   7 6=9B          EXIT  7  0
   4 6=P2    #   
   4 6?8L    #   
   4 6?N=    #   
  10 6#7W    #     AN ERROR HAS OCCURRED 
   4 6#MG    #   
  16 6*76    #     THE MAIN MESSAGE IN THE MONITORING FILE IS :- 
   4 6*LQ    #   
  15 6B6B    #      DEDICATE/UNDEDICATE MACROS: UNIT(S) %A %B
   4 6BL2    #   
  18 6C5L    #      %A IS A SERIES OF UNIT NUMBERS CONVERTED BY OUTPACK  
   4 6CK=    #   
  11 6D4W    #      %B IS A SUB-MESSAGE :-   
   4 6DJG    #   
  21 6F46    XMESS1         +ENTIN2             [DOES/DO NOT REFER TO TWINNED SPINDLE
  21 6FHQ    XMESS2         +ENTIN3             [HAS/HAVE BEEN SPECIFIED MORE THAN ON
   4 6G3B    #   
  19 6GH2    #      THE FORMER IS OUTPUT IF B0=1 IN ENTRIES IN EWDAS/ESEND   
  18 6H2L    #      THE LATER IS OUTPUT IF B1=1 IN ENTRIES IN EWDAS/ESEND
   4 6HG=    #   
  21 6H_W    X19   LDX   4  AWORK1(2)           [LOAD COUNT OF NON-TWINNED SPINDLES  
  15 6JFG          BZE   4  X21                 [J IF NONE   
  15 6J_6          LDCT  5  #400                [LOAD MASK   
   8 6KDQ          LDX   1  FX1  
  18 6KYB          LDX   6  XMESS1(1)           [SUB-MESSAGE IDENTIFIER  
  16 6LD2          CALL  7  RX25                [OUTPUT MESSAGE  
  16 6LXL    X21   LDX   4  AWORK2(2)           [COUNT OF REPEATS
  15 6MC=          BZE   4  XEND                [J IF NONE   
  15 6MWW          LDCT  5  #200                [LOAD MASK   
   8 6NBG          LDX   1  FX1  
  18 6NW6          LDX   6  XMESS2(1)           [SUB-MESSAGE IDENTIFIER  
  16 6P*Q          CALL  7  RX25                [OUTPUT MESSAGE  
  15 6PTB    XEND  GEOSTOP  XMESS3(1)           [AND FINISH  
   4 6Q*2    #   
   4 6QSL    #   
   8 6R#=    XMESS3         +45  
  19 6RRW                   45H PLEASE CORRECT UNDEDICATE OR DEDICATE MACROS 
   4 6S?G    #   
   4 6SR6    #   
  20 6T=Q    #     SET UP DETAILS FOR SETUP TO CREATE THE BSA/ENTWINE ACTIVITY   
   4 6TQB    #   
  10 6W=2    #     ACOMMUNE1  BLOCK TYPE 
   9 6WPL    #     ACOMMUNE2    LINK 
   9 6X9=    #     ACOMMUNE3    PAIR 
   9 6XNW    #     ACOMMUNE4  LENGTH 
   4 6Y8G    #   
  10 6YN6    X9A   MFREE    EWDAS,ESEND  
   8 6_7Q    X9    LDX   1  FX1  
   8 6_MB          LDX   2  FX2  
  21 7272          ADN   6  2                   [PLUS TWO FOR REQUEST TYPE AND TERM  
   8 72LL          LDCT  0  #400 
  10 736=          ORN   0  JNOCHAT+JOFR 
  20 73KW          STO   0  ETWINMK             [SET TWINNING NOT INITIALISED BIT
   9 745G          LDN   4  TYPE(1)  
  10 74K6          LDN   5  ACOMMUNE1(2) 
  18 754Q          MOVE  4  3                   [BLOCK TYPE AND LINK PAIR
  18 75JB          TXL   6  XM2(1)              [WORK OUT BUFFER LENGTH  
   8 7642          BCC      X10  
   9 76HL          LDN   6  ETBUFFLE 
   9 773=    X10   STO   6  ETNBLEN  
   8 77GW          ADX   6  BSABL
  10 782G          STO   6  ACOMMUNE4(2) 
  17 787N ...      JENVNOT  NCME,CME            [_ IF NOT CME ENVT   
   8 78#W ...      LDX   1  FX1  
  10 78G4 ...      MONOUTX  XDUALCME(1)  
   9 78M= ...XCME  GEOERR   1,NOTCME 
   7 78SD ...NCME  UPPLUS   1
   4 78_Q    #   
  15 79FB    #     SETS ENGAGE BUTTON FOR ALL SHARED UNITS   
   4 79_2    #   
   8 7=DL    QK2TW       [*****  
  15 7=Y=          CALL  7  X6                  [PTR TO DL   
  15 7?CW          BRN      X7                  [J IF NO MORE
   8 7?XG    XAA   LADDP    3,2  
  15 7#C6          JSHARED  2,XAB               [J IF SHARED 
   8 7#WQ    XAC   CALL  7  X3   
   8 7*MQ ...      BRN      X7TR 
   8 7*W2          BRN      XAA  
   4 7*_F ...XAB 
   8 7B4Y ...      FSHCODE  AORB 
   4 7B8C ...(   
   8 7B?W ...      SMO      FX1  
   9 7BC* ...      LDX   0  XBITS510 
   9 7BGS ...      ORS   0  BSUNIT(2)
   4 7BL? ...)   
  15 7BPQ ...      LINT     3,0           [GET INTERNAL NO   
   8 7BT=          SMO      FX1  
   8 7C#W          DVS   7  Q24  
   8 7CSG          LDCT  5  #400 
   7 7D#6          SMO      7
   7 7DRQ          SRL   5  0
   7 7F?B          LDX   7  0
  18 7FR2          BUTTONUP 5,7                 [SET ENGAGE BUTTON BIT   
   8 7G=L          BRN      XAC  
   4 7G#* ...#   
   8 7GDF ...X7TR  LDX   1  FX1  
  19 7GF7 ...      LDX   0  K4CDUM(1)           [DON'T SET UP TRACE BUFFER.. 
  16 7GFT ...      BZE   0  X7                  [..IF NO TRACING 
  17 7GGH ...      SETNCORE 126,3,EWDAS,GEXD    [SET UP TRACE BUFFER 
   8 7GH9 ...      STOZ     A1(3)
  10 7GJY ...      CHAIN    3,ENTWINPTR  
   8 7GLM ...      BRN      X7   
   4 7GQ=    #   
   4 7H9W    #   
  10 7HPG          MENDAREA 100,K99CDUM  
   4 7J96    #   
   4 7JNQ    #END
   6 ____ ...552367020002
