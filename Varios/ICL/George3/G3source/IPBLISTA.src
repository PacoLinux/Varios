  12 22*P ...   SEG      IPBLISTA,,,,G170,G269   
  15 22PD    # (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   7 22QH ...#OPT G170 = 0   
   6 22RL ...#SKI G170&1 
  16 22SP ...# WITH UGUG EDIT M170 (VARIABLE REMOTE *LP WIDTHS)  
   7 22TS ...#OPT G269 = 0   
   6 22WX ...#SKI G269&1 
  17 22Y2 ...# WITH UGUG EDIT M269 (REMOTE *LP LISTFILE DEBATCHERS)  
  13 22_=    #LIS           K0IPBLISTA>K0IPB>K0ALLGEO
   4 23DW    #   
  12 23YG       SEGENTRY K1IPBLISTA,SK1IPBLISTA  
   4 26BL    [   
  19 26W=    [************************************************************   
   4 27*W    [   
   7 27TG    [ PRESET DATA   
   4 28*6    [   
  19 28SQ    [************************************************************   
   4 29#B    [   
   4 29S2    [   
   9 2=?L    PJMARY         +JMARY   
  12 2=R=    STARSOLF       #76347666,#32323232  
   9 2?=W    NEWLINES       #76327632
   9 2#=6    STAREND        #32327666
  12 2*P2    PAGESEQ        #76007631,#74607635  
  11 2*YS    PSEQ           #76007631,#763500
  12 2BN=    TAPE           #76327620,#76207620  
  13 2C7W    THEAD          24HPRODUCED ON         AT
  14 2CMG    SHEAD          28H#OUTPUT BY LISTFILE IN ': 
   9 2D76    ONAT           8H' ON AT
  10 2DLQ    SDOC           12HDOCUMENT  
   9 2F6B    ZUSING         8H USING 
  10 2FL2    VFROM          12H FROM JOB 
   9 2G5L    TPUNCH         #40100000
   9 2GK=    ONZE           #60000002
   8 2H#N    SDEV           #4000
   5 2HJG    TABLE   
  13 2J46          #07566161,#56404040          [0   
  13 2JHQ          #05766740,#40404040          [1   
  13 2K3B          #07536165,#51404040          [2   
  13 2KH2          #07526165,#56404040          [3   
  13 2L2L          #10465262,#47424040          [4   
  13 2LG=          #07727161,#66404040          [5   
  13 2L_W          #07566565,#42404040          [6   
  13 2MFG          #07606164,#70404040          [7   
  13 2M_6          #07526565,#52404040          [8   
  13 2NDQ          #07516565,#56404040          [9   
  13 2NYB          #04454040,#40404040          [:   
  13 2PD2          #05414640,#40404040          [;   
  13 2PXL          #06445261,#40404040          [<   
  13 2QC=          #07464646,#46404040          [=   
  13 2QWW          #06615244,#40404040          [>   
  13 2RBG          #07506364,#50404040          [?   
  14 2RW6          #10404040,#40404040          [SPACE   
  13 2S*Q          #05766540,#40404040          [!   
  13 2STB          #05707040,#40404040          [^   
  13 2T*2          #12527667,#52766752          [#   
  13 2TSL          #07455765,#61404040          [/   
  13 2W#=          #11416244,#40516040          [%   
  13 2WRW          #07536562,#45404040          [&   
  13 2X?G          #04704040,#40404040          ['   
  13 2XR6          #05566140,#40404040          [(   
  13 2Y=Q          #05615640,#40404040          [)   
  13 2YQB          #06474747,#40404040          [*   
  13 2_=2          #10444457,#44444040          [+   
  13 2_PL          #05414240,#40404040          [,   
  13 329=          #07444444,#44404040          [-   
  13 32NW          #04414040,#40404040          [.   
  13 338G          #11414244,#40506040          [/   
  13 33N6          #11506265,#67615640          [@   
  13 347Q          #10576464,#64574040          [A   
  13 34MB          #12766765,#65655442          [B   
  13 3572          #10566161,#61614040          [C   
  13 35LL          #11766761,#61615640          [D   
  13 366=          #10766765,#65614040          [E   
  13 36KW          #10766764,#64604040          [F   
  13 375G          #11566161,#63634240          [G   
  13 37K6          #12766744,#44447667          [H   
  13 384Q          #07617667,#61404040          [I   
  13 38JB          #12426161,#76666060          [J   
  13 3942          #10766744,#52614040          [K   
  13 39HL          #10766741,#41414040          [L   
  13 3=3=          #12766750,#44507667          [M   
  13 3=GW          #12766750,#44427667          [N   
  13 3?2G          #10566565,#65564040          [O   
  13 3?G6          #10766764,#64504040          [P   
  13 3?_Q          #11566161,#65564140          [Q   
  13 3#FB          #11766764,#64524140          [R   
  13 3#_2          #10425165,#65424040          [S   
  13 3*DL          #11606076,#67606040          [T   
  13 3*Y=          #12766641,#41417666          [U   
  13 3BCW          #10704441,#44704040          [V   
  13 3BXG          #12766742,#44427667          [W   
  13 3CC6          #11615244,#44526140          [X   
  13 3CWQ          #10605047,#50604040          [Y   
  13 3DBB          #11416365,#61716140          [Z   
  13 3DW2          #07766761,#61404040          [[   
  13 3F*L          #11425176,#67654240          [$   
  13 3FT=          #07616176,#67404040          []   
  13 3G#W          #07507667,#50404040          [_   
  13 3GSG          #10424742,#42424040          [_   
   9 3H#6    VNULL          #40404040
  15 3HRQ    PBINARYMODE    #00700000           [BINARY MODE 
   4 4#NL    [   
  19 4*8=    [************************************************************   
   4 4*MW    [   
   4 4B7G    SETU
   4 4BM6    [   
  19 4C6Q    [************************************************************   
   4 4CLB    [   
   4 4D62    #   
  20 4DKL    # GET AMXOR/IOUT BLOCK FOR OUTPUT ; ON ENTRY MESSAGE CHAR CT. IN X4 
   4 4F5=    #   
   7 4FJW       SMO      FX2 
  16 4G4G       LDX   4  AWORK3              [NPU SIZE IN CHARS  
   4 4GJ6    [   
  19 4H3Q    [************************************************************   
   4 4HHB    [   
   4 4J32    PCOR
   4 4JGL    [   
  19 4K2=    [************************************************************   
   4 4KFW    [   
   7 4K_G       SBX   7  FX1 
   7 4LF6       LDX   5  4   
  14 4LYQ       ADN   5  IDATA-A1*4+3        [ROUND UP   
  17 4MDB       SRL   5  2                   [NO. OF WORDS IN OUTPUT 
  11 4MY2       SETUPCORE 5,2,AMXOR,IOUT,1   
  19 4NCL       STOZ     IMOPTY(2)           [DON'T WAKE UNLESS LAST BLOCK   
   7 4NX=       LDN   0  1   
   7 4PBW       SMO      FX2 
  18 4PWG       ADS   0  AWORK1              [COUNT OF BLOCKS FOR OUTPUT 
   9 4QB6       MHUNTW   1,AMXOR,ADCB
   9 4QTQ       LDX   5  IDENTNO(1)  
  16 4R*B       STO   5  IPBIDE(2)           [IDENTIFIER NUMBER  
   7 4RT2       LDX   1  FX1 
   8 4S#L       ORX   4  GSIGN   
  17 4SS=       STO   4  IPBIDE+1(2)         [OUTPUT CHARACTER COUNT 
  17 4T?W       LDN   5  IDATA(2)            [START ADDRESS OF OUTPUT
   7 4TRG       ADX   7  FX1 
  16 4W?6       STOZ     IBSW(2)             [BLOCK STATUS WORD  
   7 4WQQ       EXIT  7  0   
   4 4X=B    [   
  19 4XQ2    [************************************************************   
   4 4Y9L    [   
   5 4YP=    UFLEN   
   4 4_8W    [   
  19 4_NG    [************************************************************   
   4 5286    [   
   4 52MQ    #   
  19 537B    # STORE USER NAME AND JOB NAME , OMITTING NON-SIGNIFICANT SPACES
   4 53M2    #   
   9 546L       MHUNTW   1,GOUT,INFO 
  17 54L=       FLENME1  GUSER,1,3           [X2,X4,X5 NOT CHANGED   
   7 555W       EXIT  4  0   
   4 55KG    [   
  19 5656    [************************************************************   
   4 56JQ    [   
   5 574B    VISIB   
   4 57J2    [   
  19 583L    [************************************************************   
   4 58H=    [   
   4 592W    #   
  20 59GG    # CONVERT TAPE HEADINGS INTO VISIBLE FORM ; CHAR COUNT IN X6 ; CHARS
  18 5=26    # TO BE CONVERTED START AT IWORK1 OF ACTIVITY ; CALL ON X7  
  17 5=FQ    # ON ENTRY X5 CONTAINS ROOM LEFT IN AMXOR/IOUT IN CHARS 
   4 5=_B    #   
   7 5?F2       SBX   7  FX1 
   7 5?YL       LDX   2  FX2 
  15 5#D=       STO   7  ACOMMUNE5(2)        [PRESERVE LINK  
   7 5#XW       SMO      FX1 
  14 5*CG       LDX   0  VNULL               [#40404040  
   9 5*X6       STO   0  ACOMMUNE4(2)
   8 5BBQ       ADN   2  IWORK1  
   5 5BWB    VLOOP   
  17 5CB2       LDCH  3  0(2)                [CHAR TO BE CONVERTED   
   8 5CTL       ADN   3  TABLE(3)
   7 5D*=       ADX   3  FX1 
   7 5DSW       LDX   1  FX2 
   9 5F#G       LDN   4  ACOMMUNE2(1)
  20 5FS6       MOVE  3  2                   [STORE REPRESENTATION FOR LATER MVCH
   7 5G?Q       LDX   3  4   
  18 5GRB       LDX   4  ACOMMUNE1(1)        [DCH POINTER IN AMXOR/IOUT  
  19 5H?2       LDCH  1  0(3)                [NO. OF CHARS IN VISIBLE FORM   
   7 5HQL       BCHX  3  /   
   7 5J==       TXL   5  1   
  18 5JPW       BCC      VROOM               [ENOUGH ROOM IN THIS BLOCK  
  18 5K9G       BZE   5  VFULL               [NOT EVEN ROOM FOR 1 CHAR   
   7 5KP6       SMO      5   
  15 5L8Q       MVCH  3  0                   [FILL UP BLOCK  
   5 5LNB    VFULL   
   7 5M82       SBX   2  FX2 
   7 5MML       SMO      FX2 
  17 5N7=       STO   2  ACOMMUNE8           [PRESERVE CHAR POINTER  
   7 5NLW       LDX   2  FX2 
   7 5P6G       SBX   3  FX2 
  21 5PL6       STO   3  ACOMMUNE7(2)        [PRESERVE START ADDRESS FOR NEXT MVCH   
   7 5Q5Q       SBX   1  5   
  19 5QKB       STO   1  ACOMMUNE6(2)        [PRESERVE COUNT FOR NEXT MVCH   
  15 5R52       CALL  7  SETU                [GET NEW BLOCK  
  19 5RJL       LDXC  5  4                   [CHAR COUNT : THIS WILL SET C   
  19 5S4=       LDN   4  IDATA-1(2)          [START ADDRESS FOR DEPOSITING   
   7 5SHW       LDX   2  FX2 
  16 5T3G       LDX   1  ACOMMUNE6(2)        [COUNT FOR NEXT MVCH
   9 5TH6       LDX   3  ACOMMUNE7(2)
  17 5W2Q       ADX   3  FX2                 [START ADDRESS FOR MVCH 
   9 5WGB       LDX   2  ACOMMUNE8(2)
  19 5X22       ADX   2  FX2                 [POINTER TO CHAR BEING CONVERTED
   5 5XFL    VROOM   
   7 5X_=       MVCH  3  0(1)
   7 5YDW       SBX   5  1   
   7 5YYG       SMO      FX2 
   9 5_D6       STO   4  ACOMMUNE1   
   7 5_XQ       BCHX  2  /   
   8 62CB       BCT   6  VLOOP   
  16 62X2       LDX   2  FX2                 [END OF CONVERSION  
   9 63BL       LDX   7  ACOMMUNE5(2)
   7 63W=       ADX   7  FX1 
   7 64*W       EXIT  7  0   
   4 64TG    [   
  19 65*6    [************************************************************   
   4 65SQ    [   
   5 66#B    YETFL   
   4 66S2    [   
  19 67?L    [************************************************************   
   4 67R=    [   
   4 68=W    #   
  20 68QG    # GET NEW AMXOR/IOUT BLOCK IF NOT ENOUGH ROOM FOR NEXT MOVE OR MVCH 
   4 69=6    #   
   9 69PQ       MHUNTW   2,AMXOR,IOUT
   9 6=9B       ADX   2  ALOGLEN(2)  
  18 6=P2       ADN   2  A1                  [POINT TO WORD AFTER BLOCK  
  16 6?8L       SBX   2  5                   [DCH POINTER IN X5  
  17 6?N=       SLC   2  2                   [GET AMOUNT OF ROOM LEFT
  21 6#7W       TXL   7  2                   [X7 CONTAINS COUNT OF CHARS TO INSERT   
  16 6#MG       BCS      (6)                 [ENOUGH ROOM : EXIT 
   4 6*76    [   
  19 6*LQ    [************************************************************   
   4 6B6B    [   
   5 6BL2    UZNEWIO 
   4 6C5L    [   
  19 6CK=    [************************************************************   
   4 6D4W    [   
   7 6DJG       SBX   6  FX1 
   9 6F46       MHUNTW   2,AMXOR,IOUT
   8 6FHQ       SBN   5  IDATA(2)
   7 6G3B       SLC   5  2   
  16 6GH2       DSA   5  IPBIDE+1(2)         [OUTPUT CHAR COUNT  
   9 6H2L       ADN   5  IDATA-A1*4+3
   7 6HG=       SRL   5  2   
  19 6H_W       ALTLEN   2,5                 [SHORTEN AMXOR/IOUT FOR IPBOUT  
  16 6JFG       CALL  7  SETU                [GET NEW AMXOR/IOUT 
   7 6J_6       ADX   6  FX1 
  20 6KDQ       LDX   3  2                   [AMXOR/IOUT ADDRESS FOR MAIN PATH   
   7 6KYB       EXIT  6  0   
   4 6LD2    [   
  19 6LXL    [************************************************************   
   4 6MC=    [   
   5 6MWW    ZLINE   
   4 6NBG    [   
  19 6NW6    [************************************************************   
   4 6P*Q    [   
   4 6PTB    #   
  20 6Q*2    # END OF LINE IN HEADINGS : NL + RUNOUT FOR TAPE , NL + NL FOR PRINT
   4 6QSL    #   
   7 6R#=       LDN   4  ACES
  20 6RRW       MVCH  4  3                   [SPACE FILL REMAINDER OF LAST WORD  
  14 6S?G       ANDX  5  BITS22LS            [#17777777  
   7 6SR6       SMO      FX2 
  16 6T=Q       LDX   6  AWORK1              [>0 IF LP ; <0 IF TP
   8 6TQB       BNG   6  TLINE   
  14 6W=2       LDN   4  NEWLINES(1)         [#76327632  
   7 6WPL       LDN   2  1   
   8 6X9=       BRN      ZMOVE   
   5 6XNW    TLINE   
  16 6Y8G       LDN   4  TAPE(1)             [#76327620,#76207620
   7 6YN6       LDN   2  2   
   5 6_7Q    ZMOVE   
  17 6_MB       MOVE  4  0(2)                [INSERT LINE TERMINATOR 
   7 7272       ADX   5  2   
   7 72LL       EXIT  7  0   
   4 72MP    [   
  19 72NS    [************************************************************   
   4 72PX    [   
   6 72R2    TESTIPBSTAT 
   4 72S5    [   
  19 72T8    [************************************************************   
   4 72W?    [   
  10 72XB          MHUNTW   3,AMXOR,ADCB 
  10 72YF          SMO      A1+FPTR(3)   
   9 72_J          LDX   0  FOURTHWD 
   7 732M          SLL   0  2
   8 733Q          BNG  0  (6)   
   7 734T          EXIT  6  1
   4 736=    [   
  19 73KW    [************************************************************   
   4 745G    [   
   5 74K6    SENGAGE 
   4 754Q    [   
  19 75JB    [************************************************************   
   4 7642    [   
   7 76HL       SBX   7  FX1 
  10 76NS          CALL  6  TESTIPBSTAT  
   9 76W2          BRN      SCANTDO  
   7 773=       WHILE    TRUE
  10 77GW          MHUNT    3,AMXOR,ADCB 
  16 782G       AND      BS,3,ADCBINOP       [ DEVICE INOPERABLE 
   5 78G6       DO   
  17 78_Q          IF       BS,3,ADCBTUNATT     [ UNATTENDED SPOOLING
   6 79FB          THEN  
  19 79_2             LDN   4  2                   [ OUTPUT TO BOTH CONSOLES 
   6 7=DL          ELSE  
  19 7=Y=             LDN   4  0                   [ TO CLUSTER CONSOLE ONLY 
   5 7?CW          FI
  13 7?XG          LONGSET  #36,(GEOERR),IDENTNO(3)  
   6 7#C6          OUTPER
  14 7#WQ          DEMANDX  4,PJMARY(1),SCANTDO,STERM,,2 
   9 7*BB          VFREEW   GMON,AOUT
  10 7*W2          VFREEW   ADATA,ADEMAND
   6 7B*L       REPEAT   
  15 7BCT ...      MHUNTW   3,AMXOR,ADCB        [FIND ADEVS  
   3 7BG4 ...
  10 7BJ? ...      SMO      A1+FPTR(3)   
  16 7BLG ...      LDX   0  FOURTHWD             [LOOK IF ON TEST
   8 7BNP ...      SLL   0  11   
  20 7BQY ...      BNG   0  SCANTDO              [IF SO, TAKE CANT DO ENGAGE ACTN
   7 7BT=       ADX   7  FX1 
   7 7C#W       LDN   0  0   
   7 7CSG       EXIT  7  0   
   4 7D#6    [   
  12 7DRQ    SCANTDO [ CANTDO TO PLEASE ENGAGE   
   4 7F?B    [   
   9 7FR2       MHUNTW   3,AMXOR,ADCB
  17 7G=L       BS       3,ADCBWAITWR        [ WAITING TO BE WRONGED 
   4 7GQ=    [   
  12 7H9W    STERM [ TERMINATE TO PLEASE ENGAGE  
   4 7HPG    [   
   7 7J96       ADX   7  FX1 
   7 7JNQ       LDN   0  1   
   7 7K8B       EXIT  7  0   
   4 7KN2    [   
  19 7L7L    [************************************************************   
   4 7LM=    [   
   6 7M6W    SCHANMODE   
   4 7MLG    [   
  19 7N66    [************************************************************   
   4 7NKQ    [   
  11 7P5B    [ CHANGE MODE FOR BINARY OUTPUT 
   4 7PK2    [   
   7 7Q4L       SBX   7  FX1 
   7 84#Q       LDN   0  1   
   7 84SB       ADX   7  FX1 
   7 85#2       EXIT  7  0   
   4 85RL    [   
  19 86?=    [************************************************************   
   4 86QW    [   
   5 87=G    SFORM   
   4 87Q6    [   
  19 889Q    [************************************************************   
   4 88PB    [   
  19 8992    [ OUTPUT PAGE SEQUENCE NO. AND FORMAT LOOP DATA TO /IOUT BLOCK  
   4 89NL    [   
  10 8*62       MHUNTW   1,ADATA,FORMLOOP
   9 8*KL       LDN   4  FORMDATA(1) 
   9 8B5=       SMO      FORMCHAR(1) 
  16 8BJW       MVCH  4  0                   [ FORMAT LOOP DATAS 
   7 8C4G       EXIT  7  0   
   4 8C5X    [   
   5 8C6B    SPSEQ   
   4 8C6T    [   
   9 8C7#       MHUNTW   3,AMXOR,ADCB
  10 8C7R       IF       BS,3,ADCBTSPOOL 
   5 8C8=       THEN 
  10 8C8P       IF       BS,3,ADCB7502LP 
   5 8C98       THEN 
   8 8C9M          SMO      FX2  
   9 8C=6          LDX   0  IWORK11  
   7 8C=Q          ADN   0  1
   8 8C?B          ANDN  0  #37  
   8 8C#2          SMO      FX2  
   9 8C#F          STO   0  IWORK11  
   7 8C#Y          SRC   0  6
   9 8C*C          LDN   4  PSEQ(1)  
   7 8C*W          MVCH  4  4
   7 8CB*          SMO      4
   7 8CBS          ADX   0  0
   7 8CC?          LDN   4  0
   7 8CCQ          MVCH  4  3
   5 8CD9       ELSE 
  10 8CDN          LDN   4  PAGESEQ(1)   
   7 8CF7          MVCH  4  8
   5 8CFL       FI   
   5 8CG5       FI   
   7 8CGJ       EXIT  7  0   
   4 8CJ6    [   
  19 8D3Q    [************************************************************   
   4 8DHB    [   
   5 8F32    SRUNOUT 
   4 8FGL    [   
  19 8G2=    [************************************************************   
   4 8GFW    [   
   9 8G_G    [ RUNOUT FOR TAPE PUNCH 
   4 8HF6    [   
  10 8HYQ    [ X6 = NO OF RUNOUT CHARS   
   4 8JDB    [   
   7 8JY2       SBX   3  FX1 
   7 8KCL       WHILE    TRUE
   8 8KX=          SMO      FX2  
  17 8LBW          LDX   4  AWORK3              [ NPU SIZE IN CHARS  
   7 8LWG          SBX   6  4
  18 8MB6       AND      EITHER,6,PZ         [ MUCH MORE RUNOUT REQUIRED 
   7 8MTQ          ADX   4  6
  19 8N*B       OR       4,PZ                [ LITTLE MORE RUNOUT REQUIRED   
   5 8NT2       DO   
  17 8P#L          CALL  7  PCOR                [ SETUP AMXOR/IOUT   
   9 8PS=          LDX   0  TAPE+1(1)
   9 8Q?W          STO   0  IDATA(2) 
  10 8QRG          LDX   1  ALOGLEN(2)   
  10 8R?6          SBN   1  IDATA-A1+1   
   7 8RQQ          LDX   4  5
   7 8S=B          ADN   5  1
  17 8SQ2          MOVE  4  0(1)                [ MOVE IN NULL CHARS 
   6 8T9L       REPEAT   
   7 8TP=       ADX   3  FX1 
   7 8W8W       EXIT  3  0   
   4 8WL_    [   
  19 8WM2    [************************************************************   
   4 8WM3    [   
   5 8WM4    SFAIL   
   4 8WM5    [   
  19 8WM6    [************************************************************   
   4 8WM7    [   
  10 8WM8    [   FAIL BETWEEN LISTFILES  
   4 8WM9    [   
   9 8WM=       SMO      A1+FPTR(3)  
   8 8WM?       LDX   0  FOURTHWD
   7 8WM#       SLL   0  12  
   7 8WM*       BNG   0  SFL2
   9 8WMB       LDX   0  ISTATUS(3)  
   7 8WMC       SLL   0  4   
   7 8WMD       BNG   0  SFL1
   7 8WMF       EXIT  6  0   
   4 8WMG    SFL1
   7 8WMH       LDCT  0  #030
   9 8WMJ       ERS   0  ISTATUS(3)  
   7 8WMK       BRN      SFL3
   4 8WML    SFL2
   8 8WMM       LDX   0  SDEV(1) 
   9 8WMN       SMO      A1+FPTR(3)  
   8 8WMP       ERS   0  FOURTHWD
   4 8WMQ    SFL3
   9 8WMR       ACROSS   IPBLISTC,7  
   4 8WMS    [   
   4 8WNG    [   
   4 8X86    [   
   8 8XMQ    [ WW   WW       W   
   8 8Y7B    [ WW  WW       WW   
   8 8YM2    [ WW WW       WWW   
   8 8_6L    [ WWWW         WW   
   8 8_L=    [ WWWWW        WW   
   8 925W    [ WW  WW       WW   
   8 92KG    [ WW   WW      WW   
   8 9356    [ WW    WW     WW   
   4 93JQ    [   
   6 944B    SK1IPBLISTA 
   4 94J2    [   
   9 953L       MHUNTW   3,AMXOR,ADCB
   9 95H=       LDX   0  IMDNPU(3)   
   8 962W       ANDN  0  #7777   
   7 96GG       SLL   0  2   
  16 9726       STO   0  AWORK3(2)           [ NPU SIZE IN CHARS 
   7 97FQ       SLL   0  12  
  17 97_B       STO   0  AWORK1(2)           [ NPU SIZE IN MS 12 BITS
   7 986J       LDN   0  0   
   9 98?Q       STO   0  IWORK11(2)  
   8 98BB       CALL  6  SFAIL   
  12 98F2     [ COUNT OF AMXOR/IOUT IN LS 9 BITS 
  21 98YL       MBC      3,ADCBTSPOOL,ADCBTUNATT [ CLEAR SPOOLING SET FROM LAST LISTI
  16 99D=       IF       BS,3,ADCBPUNATT     [ STILL UNATTENDED  
   5 99XW       THEN 
  10 9=CG          BS       3,ADCBTUNATT 
   5 9=X6       FI   
  15 9?BQ       IF       BS,3,ADCBPSPOOL     [ STILL SPOOLING
   5 9?WB       THEN 
  10 9#B2          BS       3,ADCBTSPOOL 
  12 9#J8             IF       BS,3,ADCB7502LP   
   7 9#L=             THEN   
  19 9#N#                SPLCLPROPS             [ PROPERTIES REQUIRED MESSAGE
   9 9#QB                LDN   0  1  
  19 9#SD                ADS   0  AWORK1(2)     [ ADD 1 TO IOUT BLOCK COUNT  
   7 9#WG             ELSE   
  19 9#YJ                SPLPROPS XCANTDO       [ LIST PROPERTIES REQUIRED   
   6 9*2L             FI 
   5 9**=       FI   
   8 9*SW       CALL  7  SENGAGE 
  17 9B#G       IF       EITHER,0,NZ         [ CANNOT ENGAGE DEVICE  
  17 9BS6          MHUNT    1,GOUT,INFO         [ COPY OF GOUT INFO  
  14 9C?Q          IF       BS,1,BLFBINARY      [ BINARY 
   6 9CRB          THEN  
   9 9D?2             SMO      FX1   
  11 9DQL             LDX   0  PBINARYMODE   
  10 9F==             CALL  7  SCHANMODE 
   6 9FPW          ELSE  
   8 9G9G             LDN   0  0 
   5 9GP6          FI
  19 9H8Q       OR       0,NZ                [ CANNOT CHANGE MODE TO BINARY  
   5 9HNB       THEN 
   4 9J82    [   
  14 9JML    XCANTDO [ CANTDO LABEL FOR SPLPROPS MACRO   
   4 9K7=    [   
  11 9K98          MHUNTW   3,ADATA,CREADL   
   7 9K?6          FREECORE 3
  10 9K?Q          MHUNTW   3,FILE,ENT   
   7 9K#B          FREECORE 3
  10 9K*4          HUNTW    3,AMXOR,IOUT 
   8 9KC2          IF       3,PZ 
   6 9KDY          THEN  
   8 9KGW             FREECORE 3 
   5 9KJS          FI
  19 9KLW          LFEND    CD                  [ RETURN TO CENTRAL LISTFILE 
   7 9L6G          [-------  
   5 9LL6       FI   
   9 9M5Q       MHUNT    1,GOUT,INFO 
  16 9MKB       IF       BC,1,BLFBINARY      [ NOT BINARY OUTPUT 
   5 9N52       THEN 
  10 9NJL          LDCH  0  JSOURCE1(2)  
   7 9P4=          SBN   0  2
  16 9PHW          IF       0,ZE                [ LINE PRINTER   
   6 9Q3G          THEN  
   7 9Q8N ...#SKI G170&1$1   
   4 9Q*W ...(   
  11 9QH6             SEGENTRY K50IPBLISTA   
  21 9R2Q             LDN   4  A7021WIDTH+4        [ WIDTH OF LP + 4 FOR FORM FEED AN
   4 9R4N ...)   
   6 9R6L ...#SKI G170&1 
   4 9R8J ...(   
   9 9R=G ...#ALT K50IPBLISTA = 1022 
   9 9R#D ...            LDN   4  4  
  11 9RBB ...            ADX   4  IWIDTH(2)  
   4 9RD# ...)   
  15 9RGB             IF       BS,3,ADCB7502LP     [ 7502 LP 
  13 9S22                HUNTW    1,ADATA,FORMLOOP   
  18 9SFL             AND      1,PZ                [ FORMAT LOOP BLOCK   
   7 9S_=             THEN   
  11 9TDW                ADX   4  FORMCHAR(1)
  16 9TYG                IF       BS,3,ADCBTSPOOL     [ SPOOLING 
   7 9WD6                THEN
  10 9WLB                   ADN   4  7   
   7 9XCB                FI  
  20 9XQ#    [ THIS CODE SHOULD BE INSERTED IF IT IS DECIDED THAT FORMAT LOOP AND
  16 9Y5=    [ THE ASTERISKS LINE SHOULD BE STORED IN ONE BLOCK  
   4 9YD8    [   
  21 9YR6    [           IF       4,L,AWORK3(2)       [ TOTAL DATA LENGTH LESS THAN N
   7 9_64    [           THEN
  10 9_F2    [              CALL  7  PCOR
  11 9_N=    [              CALL  7  SPSEQ   
  11 9_RY    [              CALL  7  SFORM   
  10 =26W    [              LDX   1  FX1 
   7 =2FS    [           ELSE
   7 =2KD ...#SKI G170&1$1   
   4 =2P4 ...(   
  12 =2SQ                   SEGENTRY K51IPBLISTA 
  12 =3#B                   SBN   4  A7021WIDTH+4
   4 =3B# ...)   
   6 =3D= ...#SKI G170&1 
   4 =3G8 ...(   
   9 =3J6 ...#ALT K51IPBLISTA = 1022 
  10 =3L4 ...               SBN   4  4   
  12 =3N2 ...               SBX   4  IWIDTH(2)   
   4 =3PY ...)   
  10 =3S2                   CALL  7  PCOR
  11 =46B                   CALL  7  SPSEQ   
  11 =4?L                   CALL  7  SFORM   
   7 =4DS ...#SKI G170&1$1   
   4 =4L2 ...(   
  12 =4R=                   SEGENTRY K52IPBLISTA 
  12 =5=W                   LDN   4  A7021WIDTH+4
   4 =5?9 ...)   
   6 =5?J ...#SKI G170&1 
   4 =5?X ...(   
   9 =5#= ...#ALT K52IPBLISTA = 1022 
  10 =5#K ...               LDN   4  4   
  10 =5#Y ...               SMO      FX2 
  11 =5*? ...               ADX   4  IWIDTH  
   4 =5*L ...)   
  13 =5B2                   IF       BS,3,ADCBTSPOOL 
   8 =5F6                   THEN 
  10 =5J=                      ADN   4  7
   8 =5MB                   FI   
  10 =5QG                   CALL  7  PCOR
  11 =64W                   CALL  7  SPSEQ   
   7 =6=6    [           FI  
   7 =6PQ             ELSE   
  16 =79B                IF       BS,3,ADCBTSPOOL     [ SPOOLING 
   7 =7P2                THEN
  13 =?=Q                   IF       BS,3,ADCB7502LP 
   8 =??T                   THEN 
  10 =?#Y                      ADN   4  7
   8 =?B3                   ELSE 
  10 =?C6                      ADN   4  8
   8 =?D9                   FI   
   7 =?F#                FI  
  10 =?GC                CALL  7  PCOR   
  10 =?HG                CALL  7  SPSEQ  
   6 =?L2             FI 
  11 =#5L             LDN   4  STARSOLF(1)   
  17 =#K=             MVCH  4  8                   [ FORM FEED _**** 
   8 =*4W             LDX   4  5 
   8 =*JG             SBN   4  1 
   7 =*PN ...#SKI G170&1$1   
   4 =*WW ...(   
  11 =B46             SEGENTRY K53IPBLISTA   
  16 =BHQ             MVCH  4  A7021WIDTH-8        [ ***.....****
   4 =BKF ...)   
   6 =BM8 ...#SKI G170&1 
   4 =BNX ...(   
   9 =BQL ...#ALT K53IPBLISTA = 1022 
   9 =BS* ...            SMO      FX2
  10 =BW4 ...            LDX   3  IWIDTH 
  10 =BXR ...            MVCH  4  -8(3)  
   4 =B_G ...)   
  10 =C3B             LDN   4  STAREND(1)
  14 =CH2             MVCH  4  4                   [ **_ 
  21 =D2L             BS       2,IOUTEORESTR       [ END OF RESTART SECTION TO NOTICE
   6 =D4T ...#SKI G269&1 
   4 =D74 ...(   
  11 =D9? ...            DOWN     IPDBATCH,1 
  12 =D?G ...            LDX   0  ACOMMUNE1(2)   
  21 =D*P ...            STO   0  AWORK1(2)           [ RESET AMXOR/IOUT BLOCK COUNT 
   4 =DCY ...)   
  21 =DG=             CALL  7  SETU                [ SETUP AMXOR/IOUT FOR HEADINGS   
  11 =D_W             LDX   0  NEWLINES(1)   
  17 =FFG             STO   0  IDATA(2)            [ NEWLINE NEWLINE 
   8 =F_6             ADN   5  1 
  10 =GDQ          ELSE  [  TAPE PUNCH   
   9 =GYB             LDX   0  GSIGN 
  10 =HD2             ORS   0  AWORK1(2) 
   9 =HXL             LDN   6  511   
  10 =JC=             CALL  3  SRUNOUT   
   4 =JWW    #   
  18 =KBG    # OUTPUT VISIBLE TAPE HEADINGS WITH FILENAME AND USERNAME   
   4 =KW6    #   
  11 =L*Q             MHUNTW   1,ADATA,CREADL
  21 =LTB             LDEX  6  A1+FRH(1)           [NO. OF CHARS IN '#LISTING ETC.'  
  18 =M*2             ADN   1  CPDATA+3            [ADDRESS OF FILENAME  
   9 =MSL             LDX   2  FX2   
   9 =N#=             ADN   2  IWORK1
   8 =NRW             SBN   6  12
   8 =P?G             SMO      6 
   8 =PR6             MVCH  1  0 
  20 =Q=Q             LDN   5  0                   [INDICATE NO IOUT SET UP YET  
  17 =QQB             CALL  7  VISIB               [CONVERT FILENAME 
   9 =R=2             LDX   1  FX1   
   9 =RPL             ADN   1  VFROM 
   9 =S9=             ADN   2  IWORK1
   8 =SNW             MOVE  1  3 
   8 =T8G             LDN   6  10
  18 =TN6             CALL  7  VISIB               [CONVERT ' FROM JOB ' 
  10 =W7Q             LDN   7  IWORK1(2) 
  20 =WMB             CALL  4  UFLEN               [FLENME USER NAME & JOB NAME  
  10 =X72             SBN   7  IWORK1(2) 
  21 =XLL             SLC   7  2                   [NO. OF CHARS IN :USERNAME.JOBNAME
   8 =Y6=             LDX   6  7 
  21 =YKW             CALL  7  VISIB               [CONVERT USER NAME AND JOB NAME   
   4 =_5G    #   
  21 =_K6             ANDX  4  BITS22LS            [LAST THREE CHARS WERE #40 = NULL 
  18 ?24Q             LDX   5  4                   [DCH POINTER IN IOUT  
  17 ?2JB             LDN   7  44                  [RUNOUT REQUIRED  
  21 ?342             CALL  6  YETFL               [CHECK ENOUGH ROOM FOR 11 WORDS   
   9 ?3HL             SMO      FX1   
  15 ?43=             LDX   6  TAPE+1              [#76207620
   8 ?4GW             SMO      5 
   8 ?52G             STO   6  0 
   8 ?5G6             LDX   4  5 
   8 ?5_Q             ADN   5  1 
  15 ?6FB             MOVE  4  10                  [RUNOUT   
  21 ?6_2             LDN   7  100                 [MAX LENGTH OF '#LISTING ETC.' LIN
   8 ?7DL             ADN   5  10
   9 ?7Y=             CALL  6  YETFL 
   5 ?8CW          FI
   4 ?8XG    #   
  20 ?9C6    # IDENTIFY LISTING TO OUTPUT DEVICE WITH FILENAME , DATE AND TIME   
   4 ?9WQ    #   
   5 ?=BB    WHEAD   
  20 ?=W2          MHUNTW   3,ADATA,CREADL      [BLOCK CONTAINING #LISTING ETC.  
  18 ??*L          LDN   4  CPDATA(3)           [START ADDRESS OF DATA   
  18 ??T=          LDX   1  A1+FRH(3)           [NO. OF CHARS IN BLOCK   
  16 ?##W          MVCH  4  0(1)                [#LISTING ETC.   
   8 ?#SG          LDN   4  ACES 
  21 ?*#6          MVCH  4  4                   [SPACE-FILL REMAINDER OF LAST WORD   
   9 ?*RQ          ANDX  5  BITS22LS 
  17 ?B?B          FREECORE 3                   [ADATA/CREADL BLOCK  
   4 ?BR2    #   
   9 ?C=L          LDN   4  THEAD(1) 
  16 ?CQ=          MOVE  4  6                   [#PRODUCED ETC.  
  10 ?D9W          MHUNTW   2,AMXOR,IOUT 
   7 ?DPG          SBX   5  2
  10 ?F96          MHUNTW   3,FILE,ENT   
  19 ?FNQ          LDX   4  EWRITDAY(3)         [DATE AND TIME THIS FILE WAS 
  16 ?G8B          LDX   7  EWRITTIME(3)        [LAST WRITTEN TO 
  21 ?GN2          BZE   6  PRFIL               [X6 = 0 IF ENTERED FROM PRINTER CODE 
   8 ?H7L          LDCT  0  #777 
  17 ?HM=          ANDX  0  ETM(3)              [ISOLATE FILE TYPE   
   9 ?J6W          ERX   0  TPUNCH(1)
   8 ?JLG          SMO      FX2  
  20 ?K66          STO   0  IWORK5              [0 IF TAPE PUNCH TYPE :ADD NO NLS
   5 ?KKQ    PRFIL   
  16 ?L5B          FREECORE 3                   [FILE/ENT BLOCK  
   7 ?LK2          DATECON  4
  10 ?M4L          LDN   1  ACOMMUNE1(2) 
   9 ?MJ=          ADN   2  ACOMMUNE3
   7 ?N3W          MOVE  1  2
   7 ?NHG          TIMECON  7
  10 ?P36          MHUNTW   3,AMXOR,IOUT 
   8 ?PGQ          ADN   5  3(3) 
  10 ?Q2B          LDN   4  ACOMMUNE3(2) 
  18 ?QG2          MOVE  4  2                   [INSERT DATE BEFORE 'AT' 
  10 ?Q_L          LDN   4  ACOMMUNE1(2) 
  19 ?RF=          ADX   5  ONZE(1)             [ADD 2 WORDS AND 3 CHARACTERS
  18 ?RYW          MVCH  4  8                   [INSERT TIME AFTER 'AT'  
  17 ?SDG          CALL  7  ZLINE               [INSERT NEWLINE ETC. 
   4 ?SY6    #   
  18 ?TCQ          LDN   7  107                 [MAX LTH OF #OUTPUT LINE 
  18 ?TXB    [                                  [DOESNT MATTER IF NPU<107
  20 ?WC2          CALL  6  YETFL               [MAX. LENGTH OF '#OUTPUT ETC.'   
   9 ?WWL          LDN   4  SHEAD(1) 
  15 ?XB=          MOVE  4  6                   [#OUTPUT ETC.
   7 ?XTW          LDX   7  5
   7 ?Y*G          ADN   7  6
  18 ?YT6          LDX   5  3                   [ADDRESS OF AMXOR/IOUT   
  19 ?_#Q          CALL  4  UFLEN               [STORE USER NAME & JOB NAME  
  17 ?_SB          SBX   7  5                   [IN CASE IOUT MOVES  
   8 #2#2          DATECON  EDATE
  10 #2RL          LDN   1  ACOMMUNE1(2) 
   9 #3?=          ADN   2  ACOMMUNE3
   7 #3QW          MOVE  1  2
   9 #4=G          TIMECON  CLEANCT  
  19 #4Q6          MHUNTW   3,AMXOR,IOUT        [RELOCATE AFTER COORDINATION 
   7 #59Q          LDX   5  7
   7 #5PB          ADX   5  3
   8 #692          LDX   2  FX2  
   9 #6NL          LDN   4  ONAT(1)  
  13 #78=          MVCH  4  5                   [' ON
  10 #7MW          LDN   4  ACOMMUNE3(2) 
  15 #87G          MVCH  4  7                   [CURRENT DATE
   9 #8M6          LDN   4  ONAT+1(1)
  13 #96Q          MVCH  4  4                   [AT  
  10 #9LB          LDN   4  ACOMMUNE1(2) 
  15 #=62          MVCH  4  8                   [CURRENT TIME
  15 #=KL    [ SEE IF THERES ROOM FOR ^USING (DEVICE NAME)^  
   4 #?5=    [   
   9 #?JW          LDX   0  AWORK1(2)
  17 ##4G          BNG   0  UZTAPE              [ALWAYS ROOM ON TP   
   7 ##9N ...#SKI G170&1$1   
   4 ##BW ...(   
  10 ##J6          SEGENTRY K54IPBLISTA  
  10 #*3Q          LDN   0  A7021WIDTH   
   4 #*5_ ...)   
   6 #*88 ...#SKI G170&1 
   4 #*=C ...(   
   9 #*#L ...#ALT K54IPBLISTA = 1022 
  10 #*BT ...         LDX   0  IWIDTH(2) 
   4 #*F4 ...)   
   8 #*HB          SBN   0  90   
  19 #B32          BNG   0  UZNO                [J IF NOT ENOUGH ROOM ON LINE
   5 #BGL    UZTAPE  
   9 #C2=          LDX   0  AWORK3(2)
   8 #CFW          SBN   0  107  
   8 #C_G          BPZ   0  UZPUT
  21 #DF6                                       [J TO INSERT ^USING^ IF ROOM IN NPU  
   8 #DYQ          LDX   0  CACT 
  17 #FDB          ERS   0  IPBIDE+1(3)         [MARK IOUT ^NOT EOM^ 
  15 #FY2          CALL  6  UZNEWIO             [GET NEW IOUT
   5 #GCL    UZPUT   
  17 #GX=          SBX   5  3                   [SAVE DISP IN IOUT   
  10 #HBW          LDN   0  JSOURCE1(2)  
  10 #HWG          LDN   1  ACOMMUNE1(2) 
   7 #JB6          MOVE  0  2
  19 #JTQ    [                                  [SET UP ACOMM WDS FOR OUTPERA
   7 #K*B          OUTPERA   
  16 #KT2    [                                  [GET DEVICE NAME 
  10 #L#L          MHUNTW   3,AMXOR,IOUT 
  20 #LS=          ADX   5  3                   [RESET ADDR FOR MOVING DATA TO   
   9 #M?W          LDN   4  ZUSING(1)
  16 #MRG          MVCH  4  7                   [MOVE IN ^USING^ 
  10 #N?6          LDN   4  ACOMMUNE2(2) 
  17 #NQQ          MVCH  4  8                   [MOVE IN DEVICE NAME 
   4 #P=B    UZNO
  17 #PQ2          CALL  7  ZLINE               [INSERT NEWLINE ETC. 
   4 #Q9L    #   
  20 #QP=          LDN   7  52                  [MAX LENGTH OF 'DOCUMENT ETC.'   
   4 #R8W    PAD 
   8 #RNG          CALL  6  YETFL
   9 #S86          LDN   4  SDOC(1)  
  14 #SMQ          MOVE  4  3                   [DOCUMENT
   7 #T7B          ADN   5  3
  10 #TM2          MHUNTW   2,GOUT,INFO  
   9 #W6L          LDN   4  GHEAD(2) 
   9 #WL=          LDX   2  GSUSE(2) 
  19 #X5W          MVCH  4  0(2)                [COPY ACROSS USER'S HEADER   
   8 #XKG          CALL  7  ZLINE
   4 #Y56    PRIN
  19 #YJQ          SBN   5  IDATA(3)            [COUNT OF WORDS IN MESSAGE   
   7 #_4B          SLC   5  2
  20 #_J2          DSA   5  IPBIDE+1(3)         [STORE COUNT OF CHARS IN MESSAGE 
  10 *23L          ADN   5  IDATA-A1*4+3 
   7 *2H=          SRL   5  2
  17 *32W          ALTLEN   3,5                 [CORRECT AMXOR/IOUT  
  17 *3GG          BPZ   6  OUTHEAD             [J IF PRINTER CODE   
  15 *426    # NEED 12.8 INS OF RUNOUT AFTER DOCUMENT BLOCK  
  21 *4FQ          LDN   6  242                 [NO. OF INS OF REQUIRED RUNOUT*10-3-4
   9 *4_B          CALL  3  SRUNOUT  
   4 *5F2    #   
   5 *5YL    OUTHEAD 
   5 *7X6       FI   
   9 *8BQ       ACROSS   IPBLISTD,1  
   4 *8WB    #END
   6 ____ ...276664130007
