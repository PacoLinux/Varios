  15 22FL    #SEG  IPBIN8                       [COMM - IPB  
  13 22_=    [  (C) INTERNATIONAL COMPUTERS LTD. 1982
  15 23DW    #LIST          K0IPBIN>K0IPB>K0ALLGEO>K0GREATGEO
   9 23YG                   8HIPBIN  
   4 24D6    #   
  11 24XQ          SEGENTRY K1IPBIN,SK1IPBIN 
   4 25CB    #   
  21 25X2    #DEF VRUC=33                       [COUNT OF CHARS IN RACK UP SEQUENCE  
  21 26BL    #DEF VBIC=8                        [COUNT OF CHARS IN ^BREAK IN^ MESSAGE
   4 26W=    #   
   9 27*W    VRACKUP        #76130700
  10 27TG    SBREAK         8HBREAK IN   
   9 28*6    SMSKOP         #63770077
   9 28SQ    SMSKR          #77607777
   9 29#B    SMOP           #77717777
   9 29S2    SMSOR          #57777777
  20 2=?L    SHDR           #40240000           [EOM+BIACK+NORMAL DATA SIEVE BIT 
  14 2=R=    XMASK            #44040000           [B0,3,9
  20 2?=W    SPSDMSK        #77352040           [FOURTHWD MASK FOR PSEUDO VDUS   
   5 2?QG    SSTYP   
   8 2#=6    #HAL  AMXOR+IDATSG,0
   9 2#PQ    ZJIPBERR       +JIPBERR 
   9 2*9B    ZJIPBINOP      +JIPBINOP
   4 2*P2    #   
   4 2B8L    #   
   4 2BN=    [   
   4 2C7W    [   
   7 2CMG    #UNS  CCERROR   
   4 2D76    (   
   4 2DLQ    [   
  16 2F6B    [      SUBROUTINE TO OUTPUT AN AMXOR AJOURN BLOCK   
   4 2FL2    [   
  20 2G5L    [     MADE A SUBROUTINE SO AS NOT TO INTERRUPT FLOW FOR MAINTENANCE 
   4 2GK=    [   
   6 2H4W    XSUBJOURN   
  16 2HJG          SBX   7  FX1               [RELATIVIES LINK   
   9 2J46          DOWN   IPBINB,31  
   8 2JHQ          LDX   1  FX1  
   8 2K3B          LDX   2  FX2  
   9 2KH2          LDX   3  AWORK4(2)
   8 2L2L          ADX   7  FX1  
  13 2LG=          EXIT  7  0                   [EXIT
   4 2L_W    )   
   4 2MFG    #   
  21 2M_6    #     SUBROUTINE TO COPY AVAILABILITY FROM X0 TO FOURTHWD AND ISTATUS   
   4 2NDQ    #   
  15 2NYB    SAVA  NGNC  4  1                   [#37777777   
  14 2PD2          ANDS  4  A1+FOURTHWD(2)      [UNSET B0
  16 2PXL          ORS   0  A1+FOURTHWD(2)      [COPY TO ADEVS   
  20 2QC=          BZE   1  (6)                 [EXIT IF NOT RINGED : DEVICE IDLE
  15 2QWW          ORS   0  ISTATUS(1)          [COPY TO ADCB
  17 2RBG          BNG   0  (6)                 [EXIT IF UNAVAILABLE 
  16 2RW6          LDCT  4  #420                [DEVICE AVAILABLE
  10 2S*Q          CALL  7  TBULKINPUT   
  18 2STB          LDCT  4  #400                [NOT A BULK INPUT DEVICE 
  21 2T*2                                       [BULK INPUT DEVICE : DON'T UNSET LINE
  21 2TSL                                       [/TERMINAL FAIL EVENT FLAG(READBULK  
  21 2W#=                                       [WILL,TO ENSURE EVENT ISN'T MISSED   
   7 2WRW          LDCT  0  2
  10 2X?G          ANDX  4  ISTATUS(1)   
  11 2XR6          ANDX  0  A1+FOURTHWD(2)   
  20 2Y=Q          BZE   4  (6)                 [EXIT IF ALREADY MARKED AVAILABLE
  10 2YQB          ERS   4  ISTATUS(1)   
  16 2_=2          BNZ   0  (6)                 [EXIT IF WRONGED 
  19 2_PL          LONGON1  #36,IDENTNO(1)      [WAKE ACTLF WAITING FOR THIS 
  18 329=          CALL  7  SKADB               [LONGON HAS CHANGED X1   
   7 32NW          EXIT  6  0
   4 338G    #   
  17 33N6    #     SUBROUTINE TO TEST IF IDENTIFIER IS A MOP TERMINAL
  15 347Q    #     ON ENTRY X2 -> ADEVS ENTRY-(A1+FOURTHWD)  
  18 34MB    #     EXITS+0,WITH X0=DEVICE TYPE,IF FALSE; EXITS+1 IF TRUE 
   4 3572    #   
   6 35LL    TESTIFMOP   
  11 366=          LDEX  0  A1+FOURTHWD(2)   
  15 36KW          BZE   0  YMOP                [MOP CONSOLE 
   8 375G          SBN   0  10   
  15 37K6          BZE   0  YMOP                [MOP DISPLAY 
  18 384Q          ADN   0  10                  [LEAVE X0 = DEVICE TYPE  
   7 38JB          EXIT  7  0
   7 3942    YMOP  EXIT  7  1
   4 39HL    #   
  21 3=3=    #     SUBROUTINE TO TEST IF IDENTIFIER IS A BULK INPUT DEVICE : ON ENTRY
  21 3=GW    #     X2 -> ADEVS ENTRY-(A1+FOURTHWD); EXITS+0 IF FALSE,EXITS+1 IF TRUE 
   4 3?2G    #   
   6 3?G6    TBULKINPUT  
  11 3?_Q          LDEX  0  A1+FOURTHWD(2)   
   7 3#FB          SBN   0  4
  14 3#_2          BZE   0  YBIP                [RJE CR  
   7 3*DL          SBN   0  2
  15 3*Y=          BNZ   0  (7)                 [NOT RJE TR  
   7 3BCW    YBIP  EXIT  7  1
   4 3BXG    #   
  21 3CC6    #     SUBROUTINE TO OUTPUT SUPERVISORY SEGMENT (IN X6) ; IDENT NO IN X5 
   4 3CWQ    #   
   8 3DBB    SCCO  LDX   2  FX2  
  18 3DW2          STO   6  ACOMMUNE1(2)        [COUNT + SUPERVISORY CODE
   8 3F*L          SBX   7  FX1  
   8 3FT=          CCOUT    5,2  
   8 3G#W          ADX   7  FX1  
   7 3GSG          EXIT  7  0
   4 3H#6    #   
  20 3HRQ    #     INPUT RECEIVED FROM IDLE IDENTIFIER : NEW ACTIVITY REQUIRED   
   4 3J?B    #   
   5 3JR2    SETDB   
  21 3K=L          LDCT  0  #163               [B2:MOP OFF  B3:IDENTIFIER NOT YET SCA
  21 3KQ=                                       [B4:ACTIVITY BEING SETUP  B7:WRONGED 
  21 3L9W          ANDX  0  A1+FOURTHWD(2)      [B8 : ATTACHING OR CLOSING FOR ONLINE
  15 3LPG          BNZ   0  SNEW                [IGNORE INPUT
   8 3M96          LDCT  0  #20  
  20 3MNQ          ORS  0  A1+FOURTHWD(2)      [SET 'ACTIVITY BEING SET UP' BIT  
  19 3N8B                                      [BIT WILL BE CLEARED IN IPBINB
   8 3NN2          SBX   7  FX1  
  19 3P7L          DOWN     IPBINB,2            [SET UP NEW ACTIVITY & DCB   
   8 3PM=          ADX   7  FX1  
   7 3Q6W          EXIT  7  0
   4 3QLG    #   
  20 3R66    #     SUBROUTINE TO GET ADDRESS IN X2 OF ACTIVITY OWNING ADCB IN X1 
   4 3RKQ    #   
   7 3S5B    SKACT LDX   2  1
   9 3SK2    SFRED LDX   2  BPTR(2)  
   9 3T4L          LDX   0  ATYPE(2) 
   8 3TJ=          TXL   0  CACT 
   8 3W3W          BCS      SFRED
   7 3WHG          EXIT  7  0
   4 3X36    #   
  20 3XGQ    #     SUBROUTINE TO FIND ADDRESS IN X1 OF ADCB FOR GIVEN IDENTIFIER 
  21 3Y2B    #              IN ((X3)) AND ADDRESS IN X2 OF ADEVS ENTRY FOR THE IDEN  
   4 3YG2    #   
  10 3Y_L    SKADB MHUNTW   2,AMXOR,ADEVS
  17 3_F=          LDX   0  0(3)                [IDENTIFIER NUMBER   
   8 3_YW          ANDN  0  #7777
  21 42DG          BZE   0  TDCB                [ZERO : SYSTEM SUPERVISOR'S NUMBER   
  20 42Y6          SBX   0  A1+FOURTHWD(2)      [SUBTRACT LOWEST IDENTIFIER NO.  
   7 43CQ          SLC   0  2
  10 43XB          TXL   0  ALOGLEN(2)   
   6 44C2    #UNS CCERROR
  20 44WL          BCC      SERR1               [DCP ERROR : IDENTIFIER TOO LARGE
   6 45B=    #UNS CCERROR
   4 45TW    #SKI
  20 46*G          BCC      SERR                [DCP ERROR : IDENTIFIER TOO LARGE
   7 46T6          ADX   2  0
  10 47#Q    TDCB  LDX   1  A1+BPTR(2)   
  19 47SB          BZE   1  (7)                 [X1 ZERO IF NO RING POINTER  
   9 48#2          SBN   1  A1+FPTR  
   7 48RL          EXIT  7  0
   4 49?=    #   
  21 49QW    #     SUBROUTINE TO WAKE ACTIVITY IF NECESSARY , WITH GIVEN DCB ADDRESS 
   4 4==G    #   
   8 4=Q6    STASH LDN   0  #2000
  21 4?9Q          ANDX  0  IWAIT(1)            [B13 SET IF WARNING NOTICED BY ACT.  
   8 4?PB          BZE   0  (6)  
   9 4#92          ERS   0  IWAIT(1) 
   9 4#NL    STAKE LDXC  0  IWAIT(1) 
  17 4*8=          BCC      VCK                 [ACTIVITY NOT ASLEEP 
   9 4*MW          STO   0  IWAIT(1) 
   8 4B7G          CALL  7  SKACT
  18 4BM6          LDX   7  1                   [PRESERVE ADCB ADDRESS   
  18 4C6Q          FPUT                         [END OF WARNING CONDITION
   7 4CLB    WOKEN LDX   1  7
   7 4D62          EXIT  6  0
  20 4DKL    VCK            [WAKEUP MOP ACTIVITY IF IN WAITING STYLE: IVDUWAIT   
  10 4F5=          SMO      A1+FPTR(1)   
   9 4FJW          LDEX  0  FOURTHWD 
   8 4G4G          SBN   0  10   
  16 4GJ6          BNZ   0  NVDTW               [J IF NOT A VDU  
   8 4H3Q          LDN   0  #1000
   9 4HHB          ANDX  0  ICONTU(1)
  17 4J32          BZE   0  NVDTW               [J IF NOT SUSP OUTPUT
  20 4JGL          ERS   0  ICONTU(1)           [UNSET ^WAITING OUTPUT SUSPENDED^
   7 4K2=          LDX   7  1
  14 4KFW          FON      CONTVDUWT           [WAKE CPA
   8 4K_G          BRN      WOKEN
   5 4LF6    NVDTW   
  16 4LYQ          CALL  7  SKACT               [SET X2 -> CPAT  
  18 4MDB          LDX   7  1                   [PRESERVE ADCB ADDRESS   
  11 4MY2          LONGCLEA 2,IVDUWAIT,(6)   
   8 4NCL          BRN      WOKEN
   4 4NX=    #   
  15 4PBW    #     SUBROUTINE TO CREATE AN AMXOR/IOUT BLOCK  
  18 4PWG    #     ON ENTRY X6 = THE REQUIRED LOGICAL LENGTH OF THE BLOCK
  15 4QB6    #     ON EXIT X2 -> THE AMXOR/IOUT BLOCK CREATED
   7 4QTQ    #     LINK IN X7
   4 4R*B    #   
   6 4RT2    SETUPIOUTBK 
   8 4S#L          SBX   7  FX1  
  11 4SS=          SETUPCOR 6,2,AMXOR,IOUT,1 
   8 4T?W          ADX   7  FX1  
   7 4TRG          EXIT  7  0
   4 4W?6    #   
  17 4WQQ    #     REHUNT THE INBF BLOCK IF IT MOVES DURING A LOCKC  
   4 4X=B    #   
  10 4XQ2    STIF  MHUNTW   2,AMXOR,INBF 
   7 4Y9L          EXIT  1  0
   4 4YP=    #   
   4 4_8W    #   
  20 4_NG    #     ISSUE READ ORDERS TO THE IPB ; OBEYED FROM AMOP/IPBIN ACTIVITY
   4 5286    #   
  19 52MQ    SELECTFLAG                         [DEAL WITH EXCEPTION FLAGS IN
  15 537B                                       [STRICT ORDER
   7 53M2          SLC   0  3
  16 546L          BNG   0  SWT                 [READ INHIBITED  
   7 54L=          SRC   0  3
  17 555W          BNG   0  SERR0               [DCP ERROR REPORTED  
  20 55KG    #              THIS IS A MESSAGE BUFFERING COMMUNICATIONS SUBSYSTEM 
  16 5656    #              INPUT ACTIVITY FOR A VIRTUAL 7900 .  
  20 56JQ    #              IF THE VIRTUAL 7900 HAS A WMC WRITE TRANSFER STORED, 
  18 574B    #              GO DOWN TO K5VIPBPERI TO PERFORM THE TRANSFER
  21 57J2    #              IF THE RETURN IS UP, IE WRITE TRANSFERS WERE NO LONGER   
  21 583L    #              STORED ,RECHECK FOR FLAGS & STORED WRITES BEFORE SLEEPING
  20 58H=    #              IF THE RETURN IS UP +1, IE WRITE TRANSFER PERFORMED ,
  19 592W    #              ENTER ROUTINE SNORJ TO PROCESS THE INPUT BUFFER  
  20 59GG    #              (NOTE K5VIPBPERI SETS THE INPUT ACTIVITY'S REPLY WORD
  21 5=26    #              WITH A SUCCESSFUL 7900 READ TRANSFER REPLY TO MINIMISE   
  12 5=FQ    #              CHANGES TO K1IPBIN)  
  21 5=_B    #              THEN RECHECK FOR FLAGS AND STORED WRITES BEFORE SLEEPING 
  19 5?F2          SMO      CPPTR(2)            [-> VIRTUAL 7900 DEVICE LIST 
   9 5?YL          LDX   0  AVCWRITES
  21 5#D=          BZE   0  VSLEEP              [NO WMC WRITE TRANSFER STORED : SLEEP
   9 5#XW          SMO      CPPTR(2) 
   8 5*CG          LDX   0  JK52H
  16 5*X6          BZE   0  VSLEEP            [J IF MOPPED OFF   
  10 5BBQ          DOWN     VIPBPERI,5   
  20 5BWB          BRN      SK1IPBIN            [WMC WRITES WERE NO LONGER STORED
  21 5CB2                                       [RECHECK FOR FLAGS AND STORED WRITES 
  19 5CTL          MHUNTW   3,AMXOR,INBF        [WMC WRITE TRANSFER PERFORMED
  21 5D*=          BRN      SENDOFXFER          [PROCESS INPUT BUFFER (INC MOPTRACE) 
   5 5DSW    VSLEEP  
  21 5F#G          LDCT  0  #100                [MARK AMOP/IPBIN ACTIVITY AS ASLEEP  
   9 5FS6          ORS   0  IPBSW(2) 
  19 5G?Q          MHUNTW   3,AMXOR,INBF        [UNLOCK MBS'S INPUT BUFFER   
   7 5GRB          UNLOCK   3
  14 5H?2          COOR1                        [SLEEP   
  18 5HQL          MHUNTW   3,AMXOR,INBF        [LOCK MBS'S INPUT BUFFER 
   9 5J==          LOCKC    3,STIF   
  10 5JPW          MHUNTW   3,AMXOR,INBF 
  21 5K9G          BRN      SK1IPBIN            [RECHECK FOR FLAGS AND STORED WRITES 
   4 5KP6    #   
  21 5L8Q    SWT   LDCT  0  #040                [UNSET B3 TO SHOW READ HAS FINISHED  
   9 5LNB          ERS   0  IPBSW(2) 
  10 5M82          MHUNTW   3,AMXOR,ADEVS
  21 5MML          LDX   2  A1+BPTR(3)          [0 OR RINGED TO MOP OFF'S DUMMY ADCB 
   8 5N7=          BZE   2  SFPA 
  15 5NLW          STOZ     A1+BPTR(3)          [CLEAR ADEVS 
  15 5P6G          STOZ     BPTR(2)             [CLEAR ADCB  
   9 5PL6          SBN   2  A1+FPTR  
   8 5Q5Q          CALL  7  SFRED
  19 5QKB          FPUT                         [PUT MOPPING OFF ACT ON LIST 
  15 5R52    SFPA                             [FREEING ADEVS 
   7 5RJL          LDX   2  3
  10 5S4=          LDX   4  ALOGLEN(3)   
   7 5SHW          SRL   4  2
   7 5T3G          SBN   4  1
   5 5TH6    SFPALOOP
  10 5W2Q          LDX   1  A1+BPTR+4(3) 
  15 5WGB          BZE   1  SFALPA              [NEXT ENTRY  
  17 5X22          GEOERR   1,ADEVSRIN         [A RING STILL EXISTS  
   5 5XFL    SFALPA  
   7 5X_=          ADN   3  4
   9 5YDW          BCT   4  SFPALOOP 
  18 5YYG          LDX   3  2                   [RESTORE ADEVS POINTER   
  15 5_D6          FREECORE  3                  [FREE ADEVS  
  10 5_XQ          MFREEW   AMXOR,INBF   
   5 62CB    XTESTWR 
  18 62X2          LDX   1  IPBRING(2)          [LD. PTR. TO IPBOUT ACT. 
   9 63BL          SBN   1  IPBRING  
  15 63W=          BXE   1  2,XNOUT      [J. NO IPBOUT ACT   
  17 64*W          JBUSY    CPPTR(1),XBUSY      [J. IPBOUT ACT BUSY  
   5 64TG    XNOUT   
  18 65*6          JWTBWR   ,XWRG               [J WAITING TO BE WRONGED 
  21 65SQ    XWAIT                             [GEORGE WONT BE USING IPB UNTIL THE   
  18 66#B                                       [UNIT IS MOPPED ON AGAIN 
   9 66S2          JENVNOT  YWAIT,CME
   9 67?L          JWTBSW   ,XSWOUT  
   5 67R=    YWAIT   
  18 68=W          COOR1                        [WAIT IF READ INHIBITED  
  19 68QG          BRN      XFIN         [J. JUST IN CASE WOKEN BY MISTAKE   
   8 69=6    XBUSY LDX   0  BIT11
  20 69PQ          ORS   0  IPBSW(2)          [SET BIT 11 - WAIT WRITE TO FINISH 
  21 6=9B          COOR1                      [WAIT WRITE END READ TO FINISH WRITE   
  16 6=P2          BRN      XTESTWR           [J. WRITE FINISHED 
   9 6?8L    XJWRONG         +JWRONG 
  10 6?N=    XJUNIT            +JUNITSWO 
   9 6#7W    XJREJ           +JSWOREJ
  21 6#MG    XWRG                               [UNIT MARKED WAITING TO BE WRONGED   
  14 6*76          JV7900   ,XW7900             [J. V7900
   9 6*LQ          YWRONG XGEO1,XGEO1
  17 6B6B          DISENGPER                    [DISENGAGE PERIPHERAL
   4 6BL2    XWR 
  17 6C5L          WRONG                        [MARK DEVICE WRONG   
  19 6CK=          CLWTBWR                       [CLEAR WAITING TO BE WRONGED
   8 6D4W          LDX   1  FX1  
  10 6DJG          LDX   7  XJWRONG(1)   
   5 6F46    XINFO   
  15 6FHQ          LGEOG    ,6                  [LD. GEOG NO.
   8 6G3B          GEOPACK  4,6,C
  10 6GH2          OUTPACK  4,1,GEOPER   
   4 6H2L    XMES
   7 6HG=          LDN   5  0
  16 6H_W          INFORMX  5,7,1               [OUTPUT MESSAGE  
   8 6JFG          BRN      XWAIT
   5 6J_6    XW7900  
   8 6KDQ          LDCT  0  #400 
   9 6KYB          SMO      CPPTR(2) 
  19 6LD2          ERS   0  JK52B               [REVERSE 'DEVICE WRONG BIT   
   8 6LXL          BRN      XWR  
   5 6MC=    XSWOUT  
   7 6MWW          CLWTBSW   
   9 6NBG          SWOUT   ,XGEO,XRE 
  15 6NW6          LDX   7  XJUNIT(1)             [ACCEPTED  
   8 6P*Q          BRN      XINFO
   4 6PTB    XRE 
  15 6Q*2          LDX   7  XJREJ(1)             [REJECTED   
   8 6QSL          BRN      XINFO
   4 6R#=    XGEO
  10 6RRW          GEOERR   1,HLSBRKIN   
   5 6S?G    XGEO1   
  19 6SR6          GEOERR 1,IDFERR              [ERROR IN IDF-UNIT MISSING   
   4 6T=Q    [   
  17 6TQB    [   A DUMMY LABEL IN CASE IPBIN ACT GETS FPUT IN ERROR  
   4 6W=2    [   
   4 6WPL    XFIN
   4 6X9=    #   
   5 6XNW    [*  *  *
   5 6Y8G    [* *   *
   5 6YN6    [**    *
   5 6_7Q    [* *   *
   5 6_MB    [*  *  *
   4 7272    [   
   4 72LL    [   
   5 736=    SK1IPBIN
   8 73KW          LDX   1  FX1  
   8 745G          LDX   2  FX2  
   4 74K6    [   
  21 754Q    [     CODE TO CHECK FOR FINISH AND IF SO TO KICK OFF ANY CARD OR TAPE   
  20 75JB    [     READER ACTIVITY THAT MAY BE WAITING FOR DATA, IE THE COS ERROR
  20 7642    [     LOGGING FILE.  SO THAT THEY CAN TAKE THEIR OWN FINISH ACTION  
   4 76HL    [   
   9 773=          LDX   0  FINISH   
  17 77GW          BZE   0  SK1F                  [J. NOT FINISHING  
  15 782G          HUNT2J   3,AMXOR,ADEVS,2,SK1E  [HUNT ADEVS
  10 78G6          LDX   6  ALOGLEN(3)   
  10 78_Q          ADN   3  A1+FOURTHWD  
   4 79FB    SK1A
   7 79_2          SBN   6  4
  16 7=DL          BZE   6  SK1E                [J END OF ADEVS  
  15 7=Y=          LDX   0  FOURTHWD(3)         [LD FOURTHWD 
  17 7?CW          ANDN  0  #17                 [MASK OUT DEVICE TYPE
   7 7?XG          SBN   0  4
  15 7#C6          BZE   0  SK1B            [J CARD READER   
   7 7#WQ          SBN   0  2
  17 7*BB          BNZ   0  SK1D                [J. NOT CARD OR TAPE 
   4 7*W2    SK1B
   9 7B*L          LDX   2  BPTR(3)  
  15 7BT=          BZE   2  SK1D            [J. NOT RINGED   
   9 7C#W          LDX   0  IWAIT(2) 
  16 7CSG          BPZ   0  SK1D                [J. NOT ASLEEP   
   4 7D#6    SK1C
   9 7DRQ          LDX   2  BPTR(2)  
   9 7F?B          LDX   0  ATYPE(2) 
  17 7FR2          SBN   0  CACT                [FIND ACTIVITY BLK   
   8 7G=L          BNZ   0  SK1C 
  16 7GQ=          FPUT                         [RESTART ACTIVITY
   4 7H9W    SK1D
   7 7HPG          ADN   3  4
  18 7J96          BRN      SK1A                [J. TO LOOK AT NEXT ENTRY
   4 7JNQ    SK1E
   8 7K8B          LDX   1  FX1  
   8 7KN2          LDX   2  FX2  
   4 7L7L    SK1F
  19 7LM=    #              DON'T ISSUE  READ IF DCP ERROR REPORTED OR READ  
  21 7M6W    #              INHIBITED(BY IPBOUT) OR COMMUNICATING W/ A VIRTUAL 7900  
  15 7MLG          LDX   0  XMASK(1)              [B0/B3/B9  
   9 7N66          ANDX  0  IPBSW(2) 
  18 7NKQ          BNZ   0  SELECTFLAG          [EXCEPTION FLAG(S) SET   
   4 7P5B    #   
  16 7PK2    #     SET UP READ CONTROL AREA IF NOT ALREADY BUSY  
   4 7Q4L    #   
   9 7QJ=          JSBUSY   ,(GEOERR)
   7 7R3W          LDN   0  2
  10 7RHG          DSCA     ,STO,0,MODE  
   9 7S36          STOZ     CPRW1(2) 
  10 7SGQ          MHUNTW   3,AMXOR,INBF 
   7 7T2B          PHOTO    4
   9 7TG2          LOCKC    3,STIF   
   9 7T_L          TESTMOVE 4,SNOBL  
  10 7WF=          MHUNTW   3,AMXOR,INBF 
  10 7WYW    SNOBL LDX   0  ALOGLEN(3)   
  10 7XDG          DSCA     ,STO,0,COUNT 
   8 7XY6          LDN   0  A1(3)
  10 7YCQ          DSCA     ,STO,0,CTSA  
   4 7YXB    #   
  15 7_C2          GSPERI   ,2                  [ISSUE READ. 
  15 7_WL          LDX   0  CPRW1(2)            [CHECK REPLY 
  17 82B=          BPZ   0  SPOS                [TERMINATED ALREADY  
   7 82TW          SLC   0  5
  17 83*G          BNG   0  SINOP               [BRANCH IF INOPERABLE
   7 83T6          SRC   0  1
  14 84#Q          BNG   0  (GEOERR)            [REJECTED
   6 84SB    SPOS  COOR1 
   6 85#2    SENDOFXFER  
   8 85RL    #SKI K6IPB>100-100  
   4 86?=    (   
  21 86QW    [TRACE THE TRANSFER ONLINE,IF SUCCESSFUL AND ^MOP TRACE^ IS SWITCHED ON 
   9 87=G          DOWN     IPBINB,1 
   4 87Q6    )   
   9 889Q          LDX   0  CPRW1(2) 
   7 88PB          SLC   0  5
  17 8992          BPZ   0  SINOP               [BRANCH IF INOPERABLE
   7 89NL          SRC   0  2
   6 8=8=    #UNS CCERROR
  16 8=MW          BNG   0  SERR2               [B3 SET : ERROR  
   6 8?7G    #UNS CCERROR
   4 8?M6    #SKI
  16 8#6Q          BNG   0  SERR                [B3 SET : ERROR  
   7 8#LB          SRC   0  1
   6 8*62    #UNS CCERROR
  19 8*KL          BNG   0  SERR3               [B2 SET : UNTERMINATED READ  
   6 8B5=    #UNS CCERROR
   4 8BJW    #SKI
  19 8C4G          BNG   0  SERR                [B2 SET : UNTERMINATED READ  
  10 8CJ6          LDX   1  IPBRING(2)   
   9 8D3Q          SBN   1  IPBRING  
   7 8DHB          LDCT  0  1
   9 8F32          ANDX  0  IPBSW(1) 
  20 8FGL          BZE   0  SNORJ               [SET IF OUTPUT REJECTED OPERABLE 
  17 8G2=          ERS   0  IPBSW(1)            [IF SO UNSET MARKER  
  16 8GFW          LDX   2  1                   [AND PUT ON LIST 
   6 8G_G          FPUT  
   4 8HF6    #   
   8 8HYQ    SNORJ LDCT  0  #220 
  16 8JDB          ANDX  0  CPRW1(2)            [ISSUE READ AGAIN
   9 8JY2          BNZ   0  K1IPBIN  
   9 8KCL          LDX   0  CPRW1(2) 
   8 8KX=          ANDN  0  #7777
   7 8LBW          NGX   0  0
  10 8LWG          ADX   0  ALOGLEN(3)   
  19 8MB6          SBN   0  1                   [SUB 1 FOR BLOCK STATUS WORD 
  20 8MTQ          STO   0  AWORK3(2)           [AWORK3 - COUNT OF WORDS IN BLOCK
   9 8N*B          LDN   3  A1+1(3)  
  19 8NT2          STO   3  AWORK4(2)           [AWORK4 - POINTER FOR BLOCK  
   4 8P#L    #   
  20 8PS=    #     READ FINISHED SUCCESSFULLY : EXAMINE EACH SEGMENT OF DATA OR  
  21 8Q?W    #     SUPERVISORY INFORMATION IN AMXOR/INBF SEPARATELY ; CHAIN DATA AND 
  21 8QRG    #     SOME SUP. SEGS. BEHIND CONTROLLING ACTIVITY , OR AMOP/IPBCCA IF   
  21 8R?6    #     IDENTIFIER ONLINED TO A CONCEPTUAL ; INBF ADDRESS IN X3 (LOCKED)  
   4 8RQQ    #   
   8 8S=B    SNEW  LDX   2  FX2  
  19 8SQ2          NGX   0  AWORK3(2)           [COUNT OF WORDS IN SEGMENT   
  19 8T9L          BPZ   0  K1IPBIN             [NONE LEFT : ISSUE NEXT READ 
   9 8TP=          LDX   3  AWORK4(2)
   7 8W8W    #UNS  CCERROR   
   9 8WNG          CALL  7  XSUBJOURN
  19 8X86          LDCH  0  0(3)                [EXAMINE 1ST CHAR OF SEGMENT 
  18 8XMQ          BZE   0  SDATSEG             [BRANCH IF DATA SEGMENT  
  20 8Y7B    #                                  [OTHERWISE SUPERVISORY SEGMENT   
  15 8YM2          SBS   0  AWORK3(2)           [UPDATE COUNT
  15 8_6L          ADS   0  AWORK4(2)           [AND POINTER 
   6 8_L=    #UNS CCERROR
   4 925W    (   
  20 92KG          LDCH  2  0(3)                [LENGTH OF SUPERVISORY SEGMENT   
  17 9356          GSCAN    0(3),SUPSEG,0(2)    [SUPERVISORY SEGMENT 
   4 93JQ    )   
  19 944B          LDN   5  0                   [CONCEPTUAL / SYSTEM SWITCH  
   7 94J2          BCHX  3  /
  15 953L          LDCH  4  0(3)            [LD. SUPERVISORY 
   9 95H=          ANDX  3  BITS22LS 
   8 962W          LDX   6  0(3) 
   8 96GG          ANDN  6  #7777
   8 9726          SBN   4  32   
  21 97FQ          BNG   4  SERR4               [DCP ERROR  ILLEGAL SUPERVISORY CODE 
  16 97_B          BZE   4  XSUP                [J. SYSTEM SUP   
   7 98F2          LDX   0  4
   8 98YL          SBN   0  15   
  16 99D=          BZE   0  XSUP                [J. SYSTEM SUP   
  16 99XW          CALL  7  SKADB               [FIND ADCB BLOCK 
  21 9=CG                                  [IF NO ADCB BLOCK THIS WILL BE DEALT WITTH
   8 9=X6          BRN      XOK  
  17 9?BQ    XSUP                               [SYSTEM SUPERVISORY  
  20 9?WB          BNZ   6  SERR1               [IDENT MUST THEREFORE EQUAL ZERO 
  17 9#B2          LDN   1  0                   [DUMMY DCB BLOCK !   
   9 9#TL          BRN      SSUPSG   
  18 9**=    XOK                                [IDENTIFIER SUPERVISORIES
   8 9*SW          LDCT  0  #10  
  16 9B#G          ANDX  0  A1+FOURTHWD(2)      [B5 SET IF ONLINE
   9 9BS6          BZE   0  SSUPSG   
   4 9C?Q    #   
  17 9CRB          LDCH  7  0(3)                [LENGTH OF SUPER SEG 
  18 9D?2          LDCT  5  #400                [FOR BLOCK STATUS WORD   
  20 9DQL          DOWN     IPBINB,14           [PASS INPUT TO IPBCCA ACTIVITY   
   8 9F==          BRN      SERR 
   8 9FPW          BRN      SERR1
  17 9G9G    [     CURRENT BLOCK - BLOCK WAS LOCKED BEFORE GOING DOWN
  17 9GP6          CALL  7  SKADB               [POINT TO ADEVS ENTRY
  17 9H8Q          LDCT  5  #400                [CONCEPTUAL SWITCH   
  16 9HNB          LDN   1  0                   [SIMULATE NO DCB 
   5 9J82    SSUPSG  
  18 9JML    #                  WHEN SCOMMAND OBEYED X1 HOLDS ADDS ADCB  
  17 9K7=    #                  X3 HOLDS ADDS OF SUPERVISORY SEGMENT 
   7 9KLW          SMO      4
   9 9L6G          BRN      SCOMMAND 
   4 9LL6    #   
   5 9M5Q    SCOMMAND
  14 9MKB          BRN      SREADY              [READY   
  15 9N52          BRN      SACKN               [ACKNOWLEDGE 
  17 9NJL          BRN      SERIN               [END OF REMOTE INPUT 
  17 9P4=          BRN      SEROP               [END OF REMOTE OUTPUT
   8 9PHW          BRN      SNEW 
  14 9Q3G          BRN      SILLG               [ILLEGAL 
   8 9QH6          BRN      SNEW 
   8 9R2Q          BRN      SNEW 
  17 9RGB          BRN      SUSOP               [SUSPEND IPB OUTPUT  
  15 9S22          BRN      SLOST               [DATA LOST   
  16 9SFL          BRN      SYSOL               [SYSTEM OVERLOAD 
  16 9S_=          BRN      SYSCU               [SYSTEM CONTINUE 
  17 9TDW          BRN      SCORP               [CONFIGURATION REPORT
  15 9TYG          BRN      SLIC                [LINE CHANGED
   8 9WD6          BRN      SNEW 
   8 9WXQ          BRN      SNEW 
  17 9XCB          BRN      SIDOP               [IDENTIFIER OPENED   
  16 9XX2          BRN      SCREQ               [CLOSE IDENTIFIER
  14 9YBL          BRN      SWARN               [WARNING 
  14 9YW=          BRN      SCONT               [CONTINUE
  16 9_*W          BRN      SINCL               [INPUT CLEARED   
  13 9_TG          BRN      SFAIL               [FAIL
  17 =2*6          BRN      SCLIM               [CLOSE IMPLEMENTED   
   8 =2SQ          BRN      SNEW 
  15 =3#B          BRN      SMDCH               [MODE CHANGED
  16 =3S2          BRN      SDEVA               [DEVICE AVAILABLE
  17 =4?L          BRN      SDEVU               [DEVICE UNAVAILABLE  
  16 =4R=          BRN      SIDFR               [IDENTIFIER FREE 
  15 =5=W          BRN      SOPS                [OUTPUT SENT 
   8 =5QG          BRN      SNEW 
   8 =6=6          BRN      SNEW 
   8 =6PQ          BRN      SNEW 
   4 =79B    #   
   5 =7P2    SREADY  
   5 =88L    SACKN   
   8 =8N=          BRN      SNEW 
  17 =97W    SILLG                              [**** ILLEGAL ****   
   6 =9MG    #UNS CCERROR
   4 ==76    #SKI
  20 ==LQ          TRACE    1(3),SUPSEGW2       [SECOND WORD OF ILLEGAL SUPER SEG
   5 =?6B    SERIN   
   5 =?L2    SEROP   
   8 =#5L          BZE   1  SNEW 
  20 =#K=          SEGENTRY K37IPBIN            [USED BY THE CCILL RESTORE MACRO 
   8 =*4W          BRN      STOSS
  11 =*JG          LDEX  0  A1+FOURTHWD(2)   
   7 =B46          SBN   0  5
  18 =BHQ          BNZ   0  STOSS               [NOT A 7023 IDENTIFIER   
   8 =C3B          LDX   0  1(3) 
   8 =CH2          SRL   0  12   
   8 =D2L          SBN   0  #0120
   6 =DG=    #UNS CCERROR
  21 =D_W          BZE   0  SERR4               [ILLEGAL ^ALREADY OPENED^:DCP ERROR  
   6 =FFG    #UNS CCERROR
   4 =F_6    #SKI
  21 =GDQ          BZE   0  SERR                [ILLEGAL ^ALREADY OPENED^ : DCP ERROR
   4 =GYB    #   
   7 =HD2    STOSS LDCT  0  1
  19 =HXL          ANDX  0  IWAIT(1)            [B8 SET IF SUP SEG REQUIRED  
   8 =JC=          BZE   0  SNEW 
   7 =JWW          LDX   6  3
  17 =KBG          LDCH  3  0(3)                [LENGTH OF SUP SEG   
  14 =KW6          SETNCORE IPBIDE-A1(3),2,AMXOR,IDATSG  
   9 =L*Q          LDN   7  IPBIDE(2)
   7 =LTB          NGN   0  1
  17 =M*2          MOVE  6  0(3)                [SUPERVISORY SEGMENT 
  16 =MSL          STO   0  IBSW(2)             [INDICATE NO DATA
  19 =N#=          LDN   3  IPBIDE(2)           [-> IPBIDE OF AMXOR/IDATSG   
   8 =NRW          CALL  7  SKADB
  16 =P?G          SBN   3  IPBIDE              [-> AMXOR/IDATSG 
  16 =PR6          BZE   1  SGON                [DCB JUST FREED  
   7 =Q=Q          LDX   2  1
  15 =QQB          CHAIN    3,2                 [AFTER DCB.  
  19 =R=2          ADN   3  IPBIDE              [-> IPBIDE OF AMXOR/IDATSG   
  17 =RPL          CALL  7  SKADB               [RESET POINTER TO DCB
   9 =S9=          LDXC  0  IWAIT(1) 
  18 =SNW          BCC      SNEW                [ACTIVITY ALREADY AWAKE  
   9 =T8G          STO   0  IWAIT(1) 
   8 =TN6          CALL  7  SKACT
   6 =W7Q          FPUT  
   8 =WMB          BRN      SNEW 
  20 =X72    SGON  FREECORE 3                   [IDATSG REDUNDANT : NO DCB NOW.  
   8 =XLL          BRN      SNEW 
   4 =Y6=    #   
  18 =YKW    SUSOP LDX   4  1(3)                [PRIORITY IN SECOND WORD 
   7 =_5G          SRL   4  3
  19 =_K6          ADN   4  1                   [<8 :1 ; 7<P<16 :2 ; >15 : 3 
   8 ?24Q          SMO      FX2  
   9 ?2JB          LDX   2  IPBRING  
  18 ?342          SBN   2  IPBRING             [POINTER TO AMOP/IPBOUT  
   8 ?3HL          BRN      SOC  
   4 ?43=    #   
  21 ?4GW    SLOST BRN      SERR4               [*** DATA LOST *** TREAT AS INVALID  
   4 ?52G    #   
  16 ?5G6    SYSOL SMO      FX2                 [SYSTEM OVERLOAD 
   9 ?5_Q          LDX   2  IPBRING  
  17 ?6FB          LDN   4  1                   [FOR IPBSW & IPSTATUS
   9 ?6_2          SBN   2  IPBRING  
   8 ?7DL          LDCT  0  #200 
   9 ?7Y=          ORS   0  IPBSW(2) 
   8 ?8CW          BRN      SWAK 
   4 ?8XG    #   
  16 ?9C6    SYSCU SMO      FX2                 [SYSTEM CONTINUE 
   9 ?9WQ          LDX   2  IPBRING  
   8 ?=BB          LDCT  0  #200 
   9 ?=W2          SBN   2  IPBRING  
  17 ??*L          LDN   4  0                   [FOR IPBSW & IPSTATUS
   9 ??T=          ANDX  0  IPBSW(2) 
   9 ?##W          ERS   0  IPBSW(2) 
  21 ?#SG    SOC   ORX   2  GSIGN               [RECORD THE OVERLOAD CODE IN CHAR 2  
  21 ?*#6          DCH   4  IPBSW(2)            [OF IPBOUT'S IPBSW (CLEAR IF SYS CU) 
   9 ?*RQ          ANDX  2  BITS22LS 
   8 ?B?B    SWAK  LDCT  0  #100 
  18 ?BR2          ANDX  0  IPBSW(2)            [B2 SET IF IPBOUT ASLEEP 
   8 ?C=L          BZE   0  STEL 
   9 ?CQ=          ERS   0  IPBSW(2) 
   6 ?D9W          FPUT  
   4 ?DPG    #   
  21 ?F96    STEL  HUNTW    2,AMXOR,CROUTE      [ENTRY PER CONCEPTUAL USING THIS IPB 
   8 ?FNQ          BNG   2  SNEW 
  10 ?G8B          LDX   6  ALOGLEN(2)   
  10 ?GN2    SLO   LDX   1  A1+BPTR(2)   
   8 ?H7L          BZE   1  SNOCS
  21 ?HM=          DEX   4  IPSTATUS(1)         [RECORD OVERLOAD STATUS IN CSTATE BLK
   9 ?J6W    SNOCS ADN   2  CROUTELEN
   9 ?JLG          SBN   6  CROUTELEN
  18 ?K66          BNZ   6  SLO                 [NOT YET AT END OF CROUTE
   8 ?KKQ          BRN      SNEW 
   4 ?L5B    #   
  19 ?LK2    SCORP BRN      SNEW                [*** CONFIGURATION REPORT ***
   4 ?M4L    #   
  17 ?MJ=    SLIC  DOWN     IPBINB,7            [*** LINE CHANGED ***
   8 ?N3W          BRN      SNEW 
   4 ?NHG    #   
  19 ?P36    SIDOP LDX   0  A1+BPTR(2)          [*** IDENTIFIER OPENED ***   
  16 ?PGQ          BZE   0  SIOPZ              [J IF NULL RING   
  17 ?Q2B          LDX   0  BIT10               [ELSE DONT UNSET B10 
   8 ?QG2    SIOPZ SMO      FX1  
   8 ?Q_L          ORX   0  SMOP 
  17 ?RF=          ANDS  0  A1+FOURTHWD(2)      [UNSET BITS 9 & 10   
   8 ?RYW          LDX   0  2(3) 
   8 ?SDG          ANDX  0  GSIGN
  21 ?SY6          CALL  6  SAVA                [COPY AVAILABILITY TO ADEVS & ADCB   
   8 ?TCQ          BZE   1  SNEW 
  20 ?TXB          SMO      FX1                 [ZEROISE CHAR 2 : PRIORITY AND   
   9 ?WC2          LDX   0  SMSKOP   
  19 ?WWL          ANDS  0  ISTATUS(1)          [UNSET CLOSED & STOPPED BITS 
   8 ?XB=          LDX   0  1(3) 
  16 ?XTW          STO   0  IMDNPU(1)           [MODE,NPU SIZE   
   8 ?Y*G          LDX   0  2(3) 
   7 ?YT6          SRC   0  6
   8 ?_#Q          ANDN  0  #7700
  16 ?_SB          NGN   5  #7701               [LOADS #77770077 
  10 #2#2          ANDS  5  ISTATUS(1)   
  16 #2RL          ORS   0  ISTATUS(1)          [RECORD PRIORITY 
  11 #3?=          LDEX  0  A1+FOURTHWD(2)   
   8 #3QW          ANDN  0  #17  
   7 #4=G          SBN   0  5
  15 #4Q6          BZE   0  STOSS               [J. IF RJE   
   8 #59Q          LDN   0  #200 
  11 #5PB          ANDX  0  A1+FOURTHWD(2)   
   8 #692          BZE   0  STOSS
  11 #6NL          ERS   0  A1+FOURTHWD(2)   
   7 #78=          LDCT  0  1
   9 #7MW          ANDX  0  IWAIT(1) 
   8 #87G          BNZ   0  STOSS
   9 #8M6          BRN      SNOTIFY  
   4 #96Q    #   
  18 #9LB    SCREQ LDX   0  BIT11               [*** CLOSE IDENTIFIER ***
  20 #=62          ORS   0  A1+FOURTHWD(2)      [INHIBIT GEORGE USE (EG GETPER)  
  17 #=KL          BNG   5  SNEW                [ONLINE TO CONCEPTUAL
  17 #?5=          BZE   1  SCL                 [CRASH CLOSE IF IDLE 
  18 #?JW          LDN   0  #2040               [SET B13 FOR STASH , AND 
  21 ##4G          ORS   0  IWAIT(1)            [MARK 'CLOSE IDENTIFIER REQUESTED'   
   9 ##J6          CALL  7  TESTIFMOP
  17 #*3Q          BRN      SNMOP               [NOT A MOP TERMINAL  
   8 #*HB          LDCT  6  #40  
   8 #B32          CALL  7  SKACT
   9 #BGL          ANDX  6  IWAIT(1) 
  10 #C2=          LDCH  0  JSOURCE1(2)  
  20 #CFW          ERS   6  IWAIT(1)            [UNSET TIMED-OUT MARKER , IF SET 
   7 #C_G          SBN   0  7
  20 #DF6          BNZ   0  SINM                [NOT ALREADY TAKING INOP. ACTION 
   8 #DYQ    SCL   LDX   5  0(3) 
  15 #FDB          LDN   6  #127                [CRASH CLOSE 
  17 #FY2          ANDN  5  #7777               [IDENTIFIER NUMBER   
   8 #GCL          CALL  7  SCCO 
   8 #GX=          BRN      SNEW 
   7 #HBW    SNMOP SBN   0  5
  14 #HWG          BNZ   0  SNEW                [NOT 7023
   8 #JB6    SINM  LDCT  0  #400 
  16 #JTQ          ORS   0  ISTATUS(1)          [MARK INOPERABLE 
  19 #K*B          CALL  6  STASH               [WAKE ACTIVITY IF NECESSARY  
   8 #KT2          BRN      SNEW 
   4 #L#L    #   
  16 #LS=    SWARN BZE   1  SNEW                [*** WARNING *** 
   8 #M?W          LDCT  0  #200 
  10 #MRG          ORS   0  ISTATUS(1)   
   8 #N?6          BRN      SNEW 
  16 #NQQ    SCONT BZE   1  SNEW                [*** CONTINUE ***
   5 #P=B    SCONTA  
   8 #PQ2          LDCT  0  #200 
  10 #Q9L          ANDX  0  ISTATUS(1)   
  18 #QP=          BZE   0  SNEWCT              [J. CONT. WITH NO WARNING
   8 #R8W          SMO      FX1  
   8 #RNG          LDX   0  SMSOR
  10 #S86          ANDS  0  ISTATUS(1)   
   8 #SMQ          CALL  6  STASH
   5 #T7B    SNEWCT  
   8 #TM2          LDN   0  19   
  16 #W6L          BXE   0  4,SNEW              [J. IF CONTINUE  
  19 #WL=          BRN      SCLAA1               [J. COMMAND WAS CLOSE IMPL. 
   4 #X5W    #   
  19 #XKG    SCLIM LDX   0  BIT9                [***  CLOSE IMPLEMENTED   ***
  17 #Y56          ORS   0  A1+FOURTHWD(2)      [MARK CLOSED IN ADEVS
  10 #YJQ          LDX   0  A1+BPTR(2)   
  19 #_4B          BNZ   0  SCLAA               [J IF RING ELEMENT NOT NULL  
  17 #_J2          LDX   0  BIT10               [ELSE UNSET BIT 10   
  11 *23L          ORS   0  A1+FOURTHWD(2)   
  11 *2H=          ERS   0  A1+FOURTHWD(2)   
   5 *32W    SCLAA   
   8 *3GG          BZE   1  SNEW 
  15 *426          LDCT  0  #140                [SET B2,B3   
  10 *4FQ          ORS   0  ISTATUS(1)   
  20 *4_B          BRN      SCONTA              [J. TO SEE IF WAITING FOR CONT   
  17 *5F2    SCLAA1                              [FINISH CLOSE IMPL. 
  21 *5YL          LONGON1  #36,IDENTNO(1)      [RESTART ACT(ESP. LF) IF ON A P.E.   
  14 *6D=          CALL  7  SKADB               [RESET X1
   8 *6XW          BRN      STOSS
  15 *7CG    SOPS                               [OUTPUT SENT 
  18 *7X6    SINCL                              [*** INPUT CLEARED ***   
  15 *8BQ    SMDCH                              [MODE CHANGED
   8 *8WB          BZE   1  SNEW 
   8 *9B2          BRN      STOSS
   4 *9TL    #   
  18 *=*=    SDEVA LDN   0  0                   [*** DEVICE AVAILABLE ***
  20 *=SW          CALL  6  SAVA                [MARK AVAILABLE IN ADEVS & ADCB  
  16 *?#G          BNZ   1  SANI                [DEVICE NOT IDLE 
  17 *?S6          BNG   5  SNEW                [ONLINE TO CONCEPTUAL
  11 *#?Q          LDEX  0  A1+FOURTHWD(2)   
  19 *#RB          ANDN  0  3                   [LP : TYPE 3 ; TP : TYPE 7   
   7 **?2          ERN   0  3
  17 **QL          BNZ   0  SVDC                [J IF NOT LP OR TP   
  17 *B==    SNBOF LDN   5  ACTLF+GREMOTE       [LISTFILE ACTIVITY   
  17 *BPW          LDN   6  ACTLFLEN+GREMOTELEN [LENGTH OF ACTIVITY  
  17 *C9G          CALL  7  SETDB               [SETS UP ACTY & DCB  
   8 *CP6          BRN      SNEW 
  11 *D8Q    SVDC  LDEX  0  A1+FOURTHWD(2)   
   8 *DNB          SBN   0  10   
  15 *F82          BNZ   0  SNEW                [J IF NOT VD 
  17 *FML          LDN   5  CPAT                [START UP MOP DISPLAY
   9 *G7=          LDN   6  CPATLEN  
   8 *GLW          CALL  7  SETDB
   8 *H6G          BRN      SNEW 
  18 *HL6    SANI  LDEX  0  A1+FOURTHWD(2)      [IF A RJE CONSOLE,ENSURE 
  20 *J5Q          SBN   0  5                   [CONSA/I7023 ACTIVITY IS NOTIFIED
  21 *JKB                                       [(ESPECIALLY AFTER A LINE/TERM FAIL) 
  16 *K52          BNZ   0  SNEW                [NOT RJE CONSOLE 
   8 *KJL          CALL  6  STAKE
   8 *L4=          BRN      SNEW 
   4 *LHW    #   
  15 *M3G    SFAIL BNZ   1  SADF                [*** FAIL ***
  17 *MH6          BNG   5  SDEVU               [ONLINE TO CONCEPTUAL
  10 *N2Q          CALL  7  TBULKINPUT   
  18 *NGB          BRN      XBIP                [NOT A BULK INPUT DEVICE 
  20 *P22          LDN   5  CPAT                [SET UP ACTIVITY TO DEAL WITH THE
  21 *PFL          LDN   6  CPATLEN             [FAIL (NOTE DCP HAS STOPPED INPUT)   
   8 *P_=          CALL  7  SETDB
  16 *QDW          CALL  7  SKADB               [RESET X1 & X2   
  20 *QYG          BNZ   1  SADF                [UPDATE DCB IF ACTIVITY SET UP   
   4 *RD6    XBIP
  19 *RXQ          LDCT  0  #400                [MARK UNAVAILABLE IN ADEVS   
  11 *SCB          LDEX  7  A1+FOURTHWD(2)   
   7 *SX2          ANDN  7  3
   7 *TBL          ERN   7  3
  15 *TW=          BNZ   7  SSS                 [NOT LP OR TP
  20 *W*W          ORN   0  #4000               [SET INITIAL BULK OUTPUT FAIL BIT
   4 *WTG    SSS 
  11 *X*6          ORS   0  A1+FOURTHWD(2)   
  21 *XSQ    [              IF MOP OFF(A SYSTEM SUPERVISOR) IS CLOSING THE CC SYSTEM,
  19 *Y#B    [              CRASH CLOSE FAILED IDENTIFIERS TO AID COMPLETION 
  10 *YS2          ADX   2  A1+BSTART(2) 
  17 *_?L          LDX   1  A1+FPTR+A1+BPTR(2)  [(A1+BPTR OF ADEVS)  
  17 *_R=          BZE   1  SFID                [NO SYSTEM SUPERVISOR
  11 B2=W          LDX   0  IWAIT-A1+FPTR(1) 
   8 B2QG          ANDN  0  #400 
  21 B3=6          BZE   0  SFID                [NOT MOP OFF ACTIVITY CLOSING SYSTEM 
  16 B3PQ          BRN      SCL                 [SEND CRASH CLOSE
  21 B49B    SFID  CALL  7  SKADB               [RE-FIND FAILED IDEN'S ADEVS ENTRY   
   8 B4P2          BRN      SDEVU
  17 B58L    SADF  LDCT  0  #20                 [SET B4 TO SHOW FAIL 
   8 B5N=          LDXC  7  1(3) 
   8 B67W          BCC      SIN  
  18 B6MG          LDCT  0  #30                 [SET B5 ALSO IF LINE FAIL
  10 B776    SIN   ORS   0  ISTATUS(1)   
   7 B7LQ          SMO      1
  21 B86B          DCH   7  ISTATUS             [PUT RESTART SECTION NO INTO CHAR 3  
   4 B8L2    #   
  19 B95L    SDEVU LDCT  0  #400                [*** DEVICE UNAVAILABLE ***  
  20 B9K=          CALL  6  SAVA                [MARK UNAVAILABLE IN ADEVS & ADCB
  17 B=4W          BNZ   1  SNI                 [J IF DEVICE NOT IDLE
  17 B=JG          BNG   5  SNEW                [ONLINE TO CONCEPTUAL
  18 B?46    [              WHEN ^DEVICE UNAVAILABLE^ IS RECEIVED FOR AN 
  18 B?HQ    [              IDLE MOP IDENTIFIER,THE IDENTIFIER MUST BE   
  20 B#3B    [              CRASH CLOSED TO ^DISCONNECT^ THE LINE (WHEN DIALED)  
  11 B#H2          LDEX  0  A1+FOURTHWD(2)   
  18 B*2L          BNZ   0  SNEW                [J IF NOT A MOP CONSOLE  
   8 B*G=          LDN   5  CPAT 
   9 B*_W          LDN   6  CPATLEN  
  21 BBFG          CALL  7  SETDB[GET(EVENTUALLY)IPBMOP TO CRASH CLOSE THE IDENTIFIER
   8 BB_6          BRN      SNEW 
   4 BCDQ    [   
   4 BCYB    SNI 
  21 BDD2    [              MAJOR CHANGE-FAIL & UNAVAILABLE SUPERVISORY SEGMENTS ARE 
  16 BDXL    [              CHAINED AFTER CONSA/I7023 ACTIVITIES 
   8 BFC=          LDCT  0  #20  
   8 BFWW          ORN   0  #777 
  11 BGBG          ANDX  0  A1+FOURTHWD(2)   
   7 BGW6          SBN   0  5
  21 BH*Q          BZE   0  STOSS               [7023 IDENTIFIER WITH ACTIVITY SET UP
   4 BHTB    [   
  21 BJ*2    [              IF A MOP TERMINAL IDENTIFIER, BREAK-IN ON MOP JOB,IN CASE
  20 BJSL    [              IT IS WAITING ABOVE ACCESS LEVEL(IPBMOP), EXCEPT IF  
  20 BK#=    [              ^INOPTIME^ IS SET NON-ZERO AND THE IDENTIFIER IS ON A
  12 BKRW    [              NON-AUTO-ANSWER LINE.
   9 BL?G          CALL  7  TESTIFMOP
  17 BLR6          BRN      SNOTIFY             [NOT A MOP TERMINAL  
   8 BM=Q    #OPT  IPBINOPTIME=0 
  19 BMQB          SEGENTRY K51IPBIN            [USED BY THE INOPTIME MACRO  
  11 BN=2          LDN   0  IPBINOPTIME<4095 
  21 BNPL          BZE   0  SNBRKIN16           [INOPTIME = 0 : TAKE INOPERABILITY   
  17 BP9=                                       [ACTION IMMEDIATELY  
   8 BPNW          LDN   0  #1000
  18 BQ8G          ANDX  0  A1+FOURTHWD(2)      [B14 = AUTO-ANSWER LINE  
  21 BQN6          BZE   0  SNOTIFY             [NOT ON AN AUTO-ANSWER LINE : SKIP BI
   6 BR7Q    SNBRKIN16   
  20 BRMB          LDCT  0  4                   [SET ^TAKE DEVICE INOPERABILITY  
  19 BS72          ORS   0  ISTATUS(1)          [ACTION IMMEDIATELY^ FLAG(B6)
  21 BSLL                                       [ENSURES 'DEVICE AVAILABLE' DOESN'T  
  18 BT6=                                       [CANCEL INOPERABLE ACTION
  16 BTKW          CALL  7  SKACT               [SET X2 -> CPAT  
  19 BW5G          NBREAKIN 2,16                [BREAK-IN ON JOB IF PRESENT  
  15 BWK6          CALL  7  SKADB               [RE-FIND DCB 
  18 BX4Q          BZE   1  SNEW                [CPAT HAS JUST SUICIDED  
   4 BXJB    [   
   5 BY42    SNOTIFY 
   8 BYHL          CALL  6  STASH
   8 B_3=          BRN      SNEW 
   4 B_GW    #   
  18 C22G    SIDFR LDX   0  BIT11               [*** IDENTIFIER FREE *** 
  19 C2G6          ADX   0  GSIGN               [MASK OUT TEST AND UNAV BITS 
  11 C2_Q          ORS   0  A1+FOURTHWD(2)   
  18 C3FB          ERS   0  A1+FOURTHWD(2)      [RELEASE FOR GEORGE USE  
  19 C3_2          BNZ   1  STRI                [STILL RINGED : 7023 CONSOLE 
  17 C4DL          BNG   5  SNEW                [ONLINE TO CONCEPTUAL
   9 C4Y=          DOWN     IPBINB,5 
   8 C5CW          BRN      SNEW 
   8 C5XG    STRI  LDN   0  #40  
  11 C6C6          LDEX  5  A1+FOURTHWD(2)   
   7 C6WQ          ERN   5  5
  17 C7BB          BNZ   5  SNOT7023            [J IF NOT OPS CONSOLE
  17 C7W2          LDX   5  IDENTNO(1)          [IDENTIFIER NUMBER   
  13 C8*L          LDN   6  #120                [OPEN
  17 C8T=          ERS   0  IWAIT(1)            [CLEAR ON-TEST BIT   
   8 C9#W          CALL  7  SCCO 
  15 C9SG          LDN   6  #121                [START INPUT 
   8 C=#6          CALL  7  SCCO 
   8 C=RQ          BRN      SNEW 
   5 C??B    SNOT7023
   9 C?R2          ORS   0  IWAIT(1) 
  18 C#=L          ERS   0  IWAIT(1)            [ENSURE ON-TEST BIT CLEAR
   8 C#Q=          BRN      SNEW 
   4 C*9W    #   
  16 C*PG    #     DATA SEGMENT : SET UP ACTIVITY IF NONE ALREADY
  20 CB96    #     IF ORDINARY DATA FROM MOP OR BULK , TRANSMISSION ERROR OR MOP 
  18 CBNQ    #     TIME-OUT SEGMENT : PASS COPY TO CONTROLLING ACTIVITY  
  20 CC8B    #     IF MOP BREAK-IN OR END OF REVERSION SEGMENT : ADJUST BREAK-IN 
  17 CCN2    #     LEVEL IN ADCB AND SET MARKERS IF BREAK-IN ALLOWED 
   4 CD7L    #   
   5 CDM=    SDATSEG 
  19 CF6W          LDX   0  1(3)                [NO. CHARS OF DATA IN DATA   
  16 CFLG          ANDN  0  #7777               [SEGMENT HEADER  
  19 CG66          ADN   0  11                  [CALCULATE NO WORDS IN SEMENT
  16 CGKQ          SRL   0  2                   [2+NO WDS OF DATA
  15 CH5B          SBS   0  AWORK3(2)           [UPDATE COUNT
  15 CHK2          ADS   0  AWORK4(2)           [AND POINTER 
   8 CJ4L          LDX   0  0(3) 
   8 CJJ=          ANDN  0  #7777
   6 CK3W    #UNS CCERROR
  19 CKHG          BZE   0  SERR1               [DCP ERROR : IDENTIFIER ZERO 
   6 CL36    #UNS CCERROR
   4 CLGQ    #SKI
  19 CM2B          BZE   0  SERR                [DCP ERROR : IDENTIFIER ZERO 
   8 CMG2          CALL  7  SKADB
  16 CM_L          BZE   1  SIDLE               [ADEVS RING NULL 
   8 CNF=          LDCT  0  #10  
  16 CNYW          ANDX  0  A1+FOURTHWD(2)      [B5 SET IF ONLINE
   8 CPDG          BZE   0  SNOOL
   8 CPY6          LDX   7  1(3) 
   8 CQCQ          ANDN  7  #7777
  18 CQXB          LDN   5  0                   [FOR BLOCK STATUS WORD   
  16 CRC2          ADN   7  11                [HEADING + ROUNDING
  18 CRWL          SRL   7  2                   [NO. OF WORDS IN SEGMENT 
  20 CSB=          DOWN     IPBINB,14           [PASS INPUT TO IPBCCA ACTIVITY   
   8 CSTW          BRN      SERR 
   8 CT*G          BRN      SERR1
  18 CTT6    [      CURRENT BLOCK - BLOCK WAS LOCKED BEFORE GOING DOWN   
   8 CW#Q          BRN      SNEW 
   4 CWSB    #   
   5 CX#2    SIDLE   
  18 CXRL          LDX   4  0                   [REL PTR TO ADEVS ENTRY  
  11 CY?=          LDEX  0  A1+FOURTHWD(2)   
   8 CYQW          ERN   0  #32  
  17 C_=G          BNZ   0  NOTPSEUD            [J IF NOT PSEUDO VDU 
  11 C_Q6          LDX   0  A1+FOURTHWD(2)   
   8 D29Q          SMO      FX1  
  16 D2PB          ANDX  0  SPSDMSK             [FOURTHWD MASK   
  18 D392          BNZ   0  SNEW                [IGNORE DATA IF BIT MATCH
  17 D3NL          DOWN     IPBINC,11           [TO INITIATE TELELOAD
   8 D48=          BRN      SNEW 
   5 D4MW    NOTPSEUD
   7 D57G          LDCT  0  2
   8 D5M6          ANDX  0  1(3) 
  19 D66Q          BNZ   0  SNEW                [IGNORE IF 'END OF REVERSION'
   7 D6LB          LDN   4  0
   8 D762          LDN   5  CPAT 
   9 D7KL          LDN   6  CPATLEN  
  20 D85=          CALL  7  SETDB               [GET NEW ACTY IF NOT ATTACHING , 
  21 D8JW          CALL  7  SKADB               [ONLINING , MOPPING OFF OR WRONGED   
  18 D94G    SNOOL LDX   5  1(3)                [USE X5 IN CASE J TO SOP 
   7 D9J6          SLL   5  4
  18 D=3Q          BPZ   5  SNOBI               [NOT A BREAK-IN SEGMENT  
   7 D=HB          SLL   5  5
  21 D?32          BNZ   5  SNOBI               [NON-ZERO CHARACTER COUNT : NOT BI   
   4 D?GL    #   
  20 D#2=    #     BREAK-IN SEGMENT ; ACTIVITY NEWLY SET UP , TIMED OUT , ALREADY
  19 D#FW    #     BROKEN IN ON OR ACTIVE ; IF ACTIVE TRY TO BREAK IN ON JOB 
  19 D#_G    #     FOR MOP DISPLAYS: NO ^REVERSION^ OCCURS; REPLY IS ALWAYS  
  20 D*F6    #     BINAK TO PREVENT ANY MESSAGES BEING DISCARDED GIVING RISE TO  
  12 D*YQ    #     MIS-POSITIONING OF MESSAGES   
   4 DBDB    #   
   4 DBY2    [   
  15 DCCL    [      THIS IS NOW DEALT WITH IN SEGMENT IPBINC 
   4 DCX=    [   
   9 DDBW          DOWN     IPBINC,1 
   8 DDWG          BRN      SNEW 
  14 DFB6          BRN      SK1IPBIN             [ERROR  
   8 DFTQ          CALL  7  SKADB
  15 DG*B          BRN      SIDLE               [CPAT REMOVED
   4 DGT2    #   
  14 DH#L    #     ORDINARY DATA AND/OR END OF REVERSION 
   4 DHS=    #   
   7 DJ?W    SNOBI LDCT  0  2
  19 DJRG          ANDX  0  1(3)                [B7 SET IF END OF REVERSION  
   8 DK?6          BZE   0  SNER 
   7 DKQQ          LDN   0  1
  17 DL=B          SBS   0  IBIL(1)             [DECREASE BR IN LEVEL
  17 DLQ2          CALL  7  SKACT               [ADDS OF ACT TO X2   
   4 DM9L    #   
   9 DMP=    SNID  LDX   2  FPTR(2)  
  17 DN8W          BXE   2  1,SNODS             [BR IF ADCB REACHED  
   8 DNNG          SMO      FX1  
   8 DP86          LDX   0  SSTYP
   9 DPMQ          TXU   0  ATYPE(2) 
  18 DQ7B          BCS      SNID                [NOT YET REACHED IDATSG  
  19 DQM2          CHAIN    1,BPTR(2)           [CHAIN DCB BEFORE 1ST IDATSG 
   8 DR6L    SNODS LDX   6  1(3) 
  19 DRL=          ANDN  6  #7777               [ISOLATE NO CHARS IN MESSAGE 
  17 DS5W          CALL  7  SKADB               [ADDS OF ADCB TO X1  
  19 DSKG          BZE   6  SNIP                [NO DATA & END OF REVERSION  
   4 DT56    #   
   8 DTJQ    SNER  LDX   6  1(3) 
  17 DW4B          ANDN  6  #7777               [NO CHARS IN MESSAGE 
  19 DWJ2          ADN   6  11                  [ALLOW 2 WORDS FOR IDE,HDDR  
   7 DX3L          SRL   6  2
  16 DXH=          LDX   7  6                   [NO WDS TO MOVE  
   7 DY2W          SBN   7  2
   8 DYGG          LDCT  0  #200 
  20 D_26          LDN   5  CPDATA              [START AT CPDATA UNLESS REVERTED 
  18 D_FQ          ANDX  0  IWAIT(1)            [UNLESS IT IS REVERTED   
   8 D__B          BZE   0  SORD 
  19 F2F2          ADN   6  IDATA-A1-2          [BLOCK LONGER IF REVERTED SEG
   8 F2YL          LDN   5  IDATA
   8 F3D=          LDCT  0  #200 
  20 F3XW          ORS   0  0(3)                [SET B1 TO INDICATE REVERTED SEG 
  21 F4CG    SORD  SETUPCORE 6,2,AMXOR,IDATSG,1 [MAY BE LOCKED IF REVERTED & REOUTPUT
  18 F4X6          LDN   4  2(3)                [POINT TO START OF DATA  
   7 F5BQ          ADX   5  2
   8 F5WB          BZE   7  SUGAR
   7 F6B2          SMO      7
  20 F6TL          MOVE  4  0                   [MOVE DATA SEG TO IDATSG BLOCK   
   8 F7*=          SBN   7  513  
  17 F7SW          BNG   7  SUGAR               [DATA SEG < 513 CHARS
  17 F8#G          MOVE  4  0                   [COMPLETE THE MOVE   
   8 F8S6    SUGAR LDX   0  1(3) 
  14 F9?Q          STO   0  A1(2)               [SET HDDR
  14 F9RB          LDX   0  0(3)                [& IDE   
   9 F=?2          STO   0  A1+1(2)  
   7 F=QL          LDX   6  2
   8 F?==          CALL  7  SKADB
  21 F?PW          BZE   1  SJUG                [CPAT HAS JUST SUICIDED : TRY AGAIN  
  18 F#9G          LDX   2  BPTR(1)             [CHAIN IDATSG BEFORE ADCB
   8 F#P6          CHAIN    6,2  
  16 F*8Q          CALL  7  SKADB               [RELOCATE ADCB   
  18 F*NB    SNIP  CALL  6  STAKE               [WAKE CPA IF NECESSARY   
   8 FB82          BRN      SNEW 
   4 FBML    #   
  20 FC7=    SJUGA FREECORE 5                   [^BREAK IN^ MESSAGE IOUT BLOCK   
  21 FCLW          MHUNTW   2,AMXOR,IOUT        [ALSO FREE BIACK+STOPSIEVING IOUT BLK
   7 FD6G          LDX   6  2
  19 FDL6    SJUG  FREECORE 6                   [DATA IDATSG OR BIACK IOUT   
  16 FF5Q          CALL  7  SKADB               [RESET X1 & X2   
  19 FFKB          BRN      SIDLE               [TRY THIS DATA SEGMENT AGAIN 
   4 FG52    #   
   7 FGJL    #UNS  CCERROR   
   4 FH4=    (   
  17 FHHW    SINOP SMO      IPBRING(2)          [IPB FOUND TO BE INOP
  21 FJ3G          LDX   6  IPBSW-IPBRING        [HAVE A LOOK AT IPBSW IN OUTPUT ACT 
  19 FJH6          SLL   6  4                   [IS UNIT BEING MOPPED OFF,PM 
  16 FK2Q          BNG   6  SK1IPBIN           [YES RETURN TO K1 
  15 FKGB          NGS   2  ACOMMUNE2(2)       [FOR CHAINADD 
  10 FL22          LDX   1  ZJIPBINOP(1) 
   8 FLFL          BRN      SD   
  21 FL_=    SERR0 LDN   0  0                   [INDICATE DCP ERROR HAS BEEN REPORTED
   8 FMDW    SERR  LDX   2  FX2  
  21 FMYG          STO   0  ACOMMUNE2(2)        [FOR CCERROR MACRO %A AND CHAINADD   
  20 FND6    SD    STO   1  ACOMMUNE3(2)        [FOR CCERROR MACRO %B OR INFORMX 
   9 FNXQ          DOWN     IPBINB,6 
   9 FPCB          BRN      K1IPBIN  
  20 FPX2    SERR1 LDN   0  CCINVIDEN           [REPORT INVALID IDEN + (SEG WD0) 
   8 FQBL    SERRZ LDX   1  0(3) 
   8 FQW=          BRN      SERR 
  21 FR*W    SERR2 LDN   0  CCTRANSFER          [REPORT TRANSFER ERROR + (REPLY WD)  
   9 FRTG    SERRR LDX   1  CPRW1(2) 
   8 FS*6          BRN      SERR 
  21 FSSQ    SERR3 LDN   0  CCUNTERMRD          [REPORT UNTERMINATED READ +(REPLY WD)
   8 FT#B          BRN      SERRR
  20 FTS2    SERR4 LDN   0  CCINVSUP            [REPORT INVALID SUPER + (SEG WD0)
   8 FW?L          BRN      SERRZ
   4 FWR=    )   
   6 FX=W    #UNS CCERROR
   4 FXQG    #SKI
   4 FY=6    (   
  16 FYPQ    SINOP NGS   2  ACOMMUNE2(2)          [FOR CHAINADD  
  10 F_9B          LDX   0  ZJIPBINOP(1) 
   8 F_P2          BRN      SD   
   8 G28L    SERR  LDX   2  FX2  
   8 G2N=          LDX   1  FX1  
  15 G37W          STOZ     ACOMMUNE2(2)        [FOR CHAINADD
  10 G3MG          LDX   0  ZJIPBERR(1)  
  15 G476    SD    STO   0  ACOMMUNE3(2)        [FOR INFORMX 
   9 G4LQ          DOWN     IPBINB,6 
   9 G56B          BRN      K1IPBIN  
   4 G5L2    )   
   4 G65L    #   
  10 G6K=          MENDAREA 10,K100IPBIN 
   4 G74W    #END
   6 ____ ...337406120001
