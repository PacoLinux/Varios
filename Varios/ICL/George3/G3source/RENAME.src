  15 22FL ...      SEG      RENAME,6,EDWARD  MOON ,FILESTORE 
  11 22_=          SEGENTRY K1RENAME,Z1RENAME
  12 2394 ...      FSHENTRY K2RENAME,OPDIR,,OPDIR
  21 23DW    #              THIS SEGMENT IMPLEMENTS THE RENAME COMMAND WHICH IS USED 
  20 23YG    #              TO ALTER THE GENERAL LOCAL NAME OF A TERMINAL FILE.  
   4 242P ...[   
  16 244Y ...[ FORBIDDEN LANGUAGES(WOULD BE ALLOWED BY 'FNORM'   
   9 2477 ...[ IF NOT CHECKED HERE   
   4 249B ...[   
   9 24?K ...SAVELANG       4H####   
   9 24*S ...XJOBLANG       4H****   
   4 24D6    [   
   8 24XQ    XERR  SBX   3  FX1  
  15 25CB          CLOSETOP                     [DIRECTORY   
   8 25X2          ADX   3  FX1  
   5 26BL    XCOMERR 
   8 26W=          COMERRX  0(3) 
   4 27*W    [   
  11 27TG    Z1RENAME       [COMMAND ENTRY   
   4 28*6    [   
  20 28SQ    #              CARRY OUT FORMAT CHECKSON THE 2ND PARAMETER(GENERAL  
  21 29#B    #              LOCAL NAME) AND SET UP A FILE/FLOCNB BLOCK FOR INPUT TO  
  21 29S2    #              GETDIR WHEN CHECKING THAT THE GENERAL LOCAL NAME IS NOT  
  11 2=?L    #              ALREADY IN USE.  
   7 2=R=          LDN   4  2
  21 2?=W          CALL  5  PARIN               [PUT 2ND PAPAMETER IN FABSNB BLOCK   
  11 2?QG    #SKI           K6RENAME>199-199 
   9 2#=6          TRACE    4,RENTYPE
   7 2#PQ          STO   4  7
  19 2*9B          CALL  5  SFLOC               [SET UP A FILE/FLOCNB BLOCK  
  21 2*P2    #              CARRY OUT FORMAT CHECKS ON THE FIRST PARAMETER(FILE NAME)
  21 2B8L    #              THE FILE/FABSNB BLOCK WILL BE USED AS INPUT TO OPENDIR   
  16 2BN=    #              WHEN OPENING THE FILE'S DIRECTORY.   
   7 2C7W          LDN   4  1
  21 2CMG          CALL  5  PARIN               [PUT 1ST PARAMETER IN FABSNB BLOCK   
   7 2D76          SBN   6  5
  17 2DLQ          BZE   6  RENMT               [BRANCH IF MAG TAPE  
   7 2F6B          ADN   6  5
  19 2FL2          BNZ   6  WRONG               [BRANCH IF WRONG QUALIFIER   
   8 2G5L          ANDN  4  #6600
   8 2GK=          BNZ   4  WRONG
  21 2H4W          ERN   7  #1000               [2ND PARAMETER MUST BE LOCAL NAME ONL
   8 2HJG          ANDN  7  #7600
  20 2J46          BNZ   7  NOTLOCAL            [BRANCH IF ANY FORBIDDEN DETAILS 
  10 2JHQ          MHUNT    2,FILE,FLOCNB
  18 2K3B          BBS      2,A1+4(2),ZEROGEN   [GENERATION ZERO ILLEGAL 
  14 2L2L    #     THIS SECTION RENAMES A FILESTORE FILE 
  20 2LG=    #              OPEN THE FILE'S DIRECTORY AND CHECK THAT THE USER IS 
  14 2L_W    #              ALLOWED TO RENAME THIS FILE  
   5 2MFG    OPDIR   
  10 2MGX ...      FSHCODE  B,XFSHBRENAME
   4 2MJ# ...(   
  15 2MKP ...#  RENAMING OF A FILE MUST BE DONE ON MACHINE A 
  16 2MM6 ...      TRANSBEG FSHRENAMEID,RENAME,2,CLB,,ACOMMUNE1  
  16 2MNH ...      BRN      XENDCOM             ['ENDCOM' RETURN 
  17 2MPY ...      BRN      OPBRK               ['COMBRKIN' RETURN   
   6 2MR* ...XFSHBRENAME 
   4 2MSQ ...)   
   7 2MW7 ...      FSHSKIP  B
   4 2MXJ ...(   
  14 2M_6          USEROPEN OPBRK,WRITE,DIR,LEAVE,ERASE  
   8 2NDQ          REPERR2  USCHK
   9 2NYB          BRN      XENDCOM  
   5 2PD2    USCHK   
  18 2PK8 ...      OWNERCHECK                   [THE USEROPEN WONT HAVE  
  16 2PMR ...      TESTREP2 NOALLACC,NOTALAC,NOINFACC,NOTINFAC   
  21 2PQB ...      TESTRPN2 OK,NOTOWNER         [DONE THIS - BECAUSE OF ERASE SUBMODE
  10 2PXL          MHUNTW   3,FILE,ENT   
   9 2QC=          LDX   4  EUSE1(3) 
  19 2QWW          BNZ   4  NOTTERMINAL         [BRANCH UNLESS TERMINAL FILE 
  13 2R6N ...      JMBS     NOMULTS,3,BEMDF,BEMULT   
  20 2RBG    #              RETAIN ANY DETAILS FOR WHICH NEW VALUES HAVE NOT BEEN
   9 2RW6    #              SUPPLIED 
  10 2S*Q          MHUNT    2,FILE,FLOCNB
   9 2STB          LDX   4  A1+3(2)  
   8 2T*2          BNZ   4  RET1 
   9 2TSL          LDX   4  EREEL(3) 
  16 2W#=          STO   4  A1+3(2)             [RETAIN REEL NO  
   4 2WRW    RET1
   9 2X?G          LDX   4  A1+4(2)  
   8 2XR6          BNZ   4  RET2 
   9 2Y=Q          LDX   4  EGEN(3)  
  17 2YQB          STO   4  A1+4(2)             [RETAIN GENERATION NO
   4 2_=2    RET2
   9 2_PL          LDX   4  A1+5(2)  
  18 2_WS ...      BXE   4  SAVELANG(1),WRONGLANG [J. IF ILLEGAL LANG.   
  17 3242 ...      BXE   4  XJOBLANG(1),WRONGLANG [ CODE SPECIFIED   
   8 329=          BNZ   4  RET3 
   9 32NW          LDX   4  ELAN(3)  
  16 338G          STO   4  A1+5(2)             [RETAIN LANGUAGE 
   4 33N6    RET3
  20 347Q    #              CHECK THAT THE SPECIFIED GENERAL LOCAL NAME IS NOT   
  16 34MB    #              ALREADY USED UNDER THE OPEN DIRECTORY
  20 34YF ...      GETDIR   5                   [ENSURE NO NON-ERASED ENTRANTS   
  10 359J ...      TREP2    MULTI,MULTIEX
  16 35GM ...      TESTREP2 OK,XISTS,WRGEN,WRGEN,VRYWRONG,RONGBUT
  11 35RS ...      TESTRPN2 NOFILE,(GEOERR)  
   5 35_3 ...QERASE  
  10 366=          MHUNT    3,FILE,FLOCNB
  21 36KW          LONGSET  #12,(GEOERR),A1(3)  [BEFORE COORDINATING - IN CASE WAIT R
  19 375G          GETDIR   3                   [CHECK FOR ERASED ENTRANTS   
  20 37K6          MHUNTW   3,FILE,ENT          [FROM GD IF OK ELSE FROM USEROPEN
  12 37_L ...      TESTREP2 NOFILE,NERAS,OK,NOTMT
  11 38B6 ...      TESTRPN2 VRYWRONG,(GEOERR)
   7 38QL ...      STEPAGAIN 
  20 3976 ...      LDX   4  ESERN(3)            [GET TSN OF TAPE BEING RETURNED  
  21 39HL          JMTONL   4,,SLUDGE           [J TO SLUDGE IF TAPE OPEN TO THIS JOB
   6 3=3=    WAITERASE   
  15 3=GW          CLOSETOP                     [DIRECTORY   
   9 3?2G          MFREEALL FILE,ENT 
  19 3?G6          LONGSTOP OPBRK,,FWTBER       [WAIT FOR ERASE OR RETURN ETC
  15 3?_Q          BRN      OPDIR               [TRY AGAIN   
   4 3#4* ...)   
   5 3#6Y ...OPBRK   
  10 3#9H ...      FSHCODE  A,XFSHABRKIN 
   4 3##6 ...(   
  18 3#BP ...      LDN   4  1                   [OFFSET FOR LABEL 'OPBRK'
  21 3#F# ...      TRANSFIN 4,,ALIEN            [RETURN TO M/C B IF ORIGINATED THERE 
   6 3#HX ...XFSHABRKIN  
   4 3#LG ...)   
  14 3#P5 ...      COMBRKIN                     [BREAKIN 
   7 3#RN ...      FSHSKIP  B
   4 3#W? ...(   
   5 3#_2    NOTMT   
  14 3*DL          CALL  7  SEFCB               [FIND FCB
  21 3*Y=          BRN      WAITERASE           [NONE SO WAIT FOR FILE TO BE ERASED  
  21 3BCW          BRN      TESTCLUDGE          [FOUND SO TEST FOR CLUDGE BEFORE WAIT
   9 3BXG ...      BRN      WAITERASE
   6 3CC6    TESTCLUDGE  
  18 3CWQ          CLUDGEQU 3,SLUDGE,4,5        [J TO SLUDGE IF CLUDGE   
  15 3DBB          BRN      WAITERASE           [OK TO WAIT  
   5 3DW2    NERAS   
   7 3F*L          LONGOFF   
   9 3FT=          LDX   4  ECOPS(3) 
  21 3G#W          BZE   4  NERA1               [BRANCH IF NO BLOCKS OR ONLINE COPIES
   8 3GSG          ANDN  4  #777 
  19 3H#6          BZE   4  NONLI               [BRANCH IF NO ONLINE COPIES  
   5 3HRQ    NERA1   
  10 3J?B          LDEX  4  EAUTOCOUNT(3)
  10 3JR2          ORX   4  ESAVECOUNT(3)
  18 3K=L          BNZ   4  NONLI               [BRANCH IF FILE FROZEN   
  21 3KQ=          CALL  7  SEFCB               [LOOK FOR AN FCB BLOCK FOR THIS FILE 
   8 3L9W          BRN      UPDIR
   8 3LPG          BRN      WAIT 
  10 3M96          MHUNT    2,FILE,FLOCNB
   8 3MNQ          ADN   2  A1   
   8 3N8B          ADN   3  FLOC1
  21 3NN2          MOVE  2  6                   [OVERWRITE GENERAL LOCAL NAME IN FCB 
  17 3P7L    #              UPDATE DIRECTORY ENTRY IN FILE/ENT BLOCK 
   5 3PM=    UPDIR   
   5 3Q6W    #SKI  G4
  10 3QLG          ERASESUD FX2,ACOMMUNE1
  19 3R66          CALL  5  SFLOC               [SET UP A FILE FLOCNB BLOCK  
  20 3RKQ          GETDIR   1                   [RESET POINTERS TO AFTER RECORD  
  10 3S5B          MFREE    FILE,FLOCNB  
   6 3S8G ...#UNS ILOGACC
   4 3S?L ...(   
   8 3SBQ ...      LOGACCESS  6  
   4 3SFW ...)   
  10 3SK2          MHUNT    2,FILE,FLOCNB
  10 3T4L          MHUNTW   3,FILE,ENT   
  10 3TJ=          NAME     3,FILE,FWB   
   9 3W3W          LDX   0  A1+5(2)  
   9 3WHG          STO   0  ELAN(3)  
   8 3X36          LDN   0  A1(2)
   9 3XGQ          LDN   1  ELOC1(3) 
   7 3Y2B          MOVE  0  5
  17 3YG2          BS       3,BEDUMP            [ENSURE FILE REDUMPED
  10 3Y_L          STOZ     EINCLOOK(3)  
   9 3_F=          STOZ     EINC(3)  
  21 3_YW          MBC      3,BEDIRDUMP,BEREVERE[REVERED STATUS LOST(BACKMAP CANT COP
  16 42DG          LDEX  7  ECOPS(3)            [BLOCKS RECORDS  
  16 42Y6          ADX   7  ENUSE(3)            [TRAPS RECORDS   
   9 437Y ...      LDX   5  ELOC1(3) 
  11 43CQ          JBC      NOINDEX,3,BEINDEX
   7 43XB          ADN   7  1
   5 44C2    NOINDEX 
   7 44WL          LDX   6  7
  15 45B=          ADN   7  1                   [NAME RECORD 
   8 45TW          BZE   6  NDEL 
   5 46*G    READLOOP
  17 46T6          READ                         [NEXT RECORD IN ENTRY
  10 47#Q          MHUNTW   1,FILE,FRB   
  15 47SB          NAME     1,FILE,FWB          [FOR INSERT  
  10 48#2          LDX   2  ACTRING(2)   
   9 48RL          SBN   2  ACTRING  
  21 49?=          CHAIN    1,BPTR(2)           [IN REVRSE ORDER TO ENABLE INSERTION 
  20 49QW          BCT   6  READLOOP            [             IN CORRECT ORDER   
   4 4==G    NDEL
   7 4=Q6          LDX   6  7
   5 4?9Q    NDELOOP 
   6 4?PB          DELETE
   9 4#92          BCT   6  NDELOOP  
  17 4#NL          GETDIR   3                   [POSITION TO INSERT  
  15 4*8=          MFREEW   FILE,ENT            [FROM GETDIR 
  10 4*MW          MFREE    FILE,FLOCNB  
   6 4B7G    WRITELOOP   
  17 4BM6          INSERT                       [EACH RECORD OF ENTRY
   9 4C6Q          MFREEW   FILE,FWB 
   9 4CLB          BCT   7  WRITELOOP
  19 4D62          LONGON   #10,5               [WAITERS FOR THIS COMM FILE  
   6 4D7Y ...#UNS FNSAD  
   4 4D9W ...(   
   7 4D?S ...      TOPFCB2  3
  11 4D*Q ...      JBS      NOUPDATE,3,BEDUMP
  11 4DCN ...      MBS   3,BFDUMP,BFDIRUPDATE
   5 4DFL ...NOUPDATE
   4 4DHJ ...)   
  16 4DKL          CLOSETOP                     [CLOSE DIRECTORY 
   4 4DTD ...)   
   5 4F5=    XENDCOM 
  10 4F7T ...      FSHCODE  A,XFSHAENDCOM
   4 4F=D ...(   
  21 4F*3 ...      TRANSFIN ,,ALIEN             [RETURN TO M/C B IF ORIGINATED THERE 
   6 4FCL ...XFSHAENDCOM 
   4 4FG9 ...)   
   6 4FJW          ENDCOM
   5 4G4G    RENMT   
  18 4GJ6          ACROSS   RENAMAG,1           [RENAME A MAGNETIC TAPE  
   5 4H3Q    ZEROGEN 
   9 4HHB          CALL  3  XCOMERR  
   9 4J32                   +ERWRGEN 
   7 4J4? ...      FSHSKIP  B
   4 4J5J ...(   
   5 4J6X ...WRGEN   
   8 4J=S ...      CALL  3  XERR 
   9 4JBP ...               +ERWRGEN 
   5 4JGL    NOTOWNER
   8 4K2=          CALL  3  XERR 
   9 4KFW                   +ERALT   
   5 4KG_ ...NOTALAC 
   8 4KJ4 ...      CALL  3  XERR 
  10 4KK7 ...               +ERNOALLACC  
   5 4KL= ...NOTINFAC
   8 4KM* ...      CALL 3  XERR  
  10 4KND ...               +ERNOINFACC  
   5 4KPH ...NOMULTS 
   8 4KQL ...      CALL  3  XERR 
  10 4KRP ...               +ERNOMULTS   
   5 4KSS ...MULTIEX 
   9 4KTX ...      MFREE    CPB,CUNI 
   8 4KX2 ...      CALL  3  XERR 
   9 4KY5 ...               +JMULTIEX
   4 4KYR ...)   
   5 4K_G    PARMIS  
   9 4LF6          CALL  3  XCOMERR  
   9 4LYQ                   +JPARMIS 
   5 4MDB    NOTLOCAL
   9 4MY2          MFREE    CPB,CUNI 
   9 4NCL          CALL  3  XCOMERR  
   9 4NX=                   +ERNOTLOC
   5 4PBW    WRONG   
   9 4PWG          CALL  3  XCOMERR  
   9 4QB6                   +ERWRONG 
   7 4QH# ...      FSHSKIP  B
   4 4QNG ...(   
   6 4QTQ    NOTTERMINAL 
   8 4R*B          CALL  3  XERR 
   9 4RT2                   +ERDIR   
   5 4S#L    SLUDGE  
   7 4SS=          LONGOFF   
   9 4SW8 ...      MFREE    CPB,CUNI 
   8 4SY6 ...      CALL  3  XERR 
   9 4T24 ...               +ERCLUDGE
   5 4T2H ...RONGBUT 
   7 4T32 ...      STEPAGAIN 
  11 4T3F ...      JBS      QERASE,3,BNERASE 
   5 4T42 ...VRYWRONG
   9 4T5Y ...      MFREE    CPB,CUNI 
   8 4T7W ...      CALL  3  XERR 
  10 4T9S ...               +ERVRYWRONG  
   5 4T?W ...XISTS   
   9 4TRG          MFREE    CPB,CUNI 
   8 4W?6          CALL  3  XERR 
   9 4WQQ                   +ERALREX 
   6 4WTW ...WRONGLANG   
   9 4W_2 ...      MFREE    CPB,CUNI 
   8 4X46 ...      CALL  3  XERR 
   9 4X7= ...               +ERILLANG
   4 4X=B    [   
  18 4XQ2    #     THIS SECTION PUTS A FILE ONLINE IF NOT ALREADY ONLINE 
  19 4Y9L    #     AND CAUSES THE RENAMING TO WAIT UNTIL THE FILE IS UNFROZEN
   5 4YP=    NONLI   
  13 4_8W          FREECORE 3                   [ENT 
   5 4_NG    OPENFILE
  16 5286          CLOSETOP                     [CLOSE DIRECTORY 
  20 52MQ ...      USEROPEN OPBRK,WRITE,UNTRAP  [TO GET FILE ONLINE &/OR THAWED  
   9 537B          REPERR   OPENED   
   9 53M2          BRN      XENDCOM  
   5 546L    OPENED  
   7 54L= ...      CLOSETOP  
  16 54Q7 ...      JBC      NOWAIT,,ICTSW         [J IF NOT ASF  
  19 54W4 ...      COOR3    #41                  [WAIT FOR OTHER ACTS TO RUN 
   5 54__ ...NOWAIT  
  15 555W          BRN      OPDIR               [TRY AGAIN   
  15 55KG    #     FILE IS OPEN OR BEING WAITED FOR - WAIT   
   4 5656    WAIT
  20 56JQ          BS       3,BFEMPTY           [SET WAITING TO EMPTY FILE BIT   
   9 574B          MFREEW   FILE,ENT 
   9 57J2          BRN      OPENFILE 
   4 57RS ...)   
  21 583L    #     SUBROUTINE TO INPUT THE PARAMETER WHOSE NUMBER IS IN X4, SET UP A 
  21 58H=    #     FILE/FABSNB BLOCK AN LEAVE CERTAIN SYNTACTIC FORM BITS IN X4 AND  
  18 592W    #     LEAVE A CPB/CUNI BLOCK FOR POSSIBLE ERROR MESSAGES.   
   5 59GG    PARIN   
   8 5=26          SBX   5  FX1  
  16 5=FQ          SPARANOX 4                   [PASS PARAMETER  
  10 5=_B          MHUNT    3,CPB,CUNI   
   9 5?F2          NGX   0  ANUM(3)  
   9 5?YL          BPZ   0  PARMIS   
  10 5#D=          NAMETOP  3,FILE,FNAME 
   7 5#XW          FNORM    7
  11 5*CG          TESTREP2 NAMEFORM,XENDCOM 
   7 5*X6          LDN   6  0
  10 5BBQ          TESTRPN2 ADJUNCTS,PAR1
  11 5BWB          MHUNTW   1,FILE,ADJUNCTS  
   9 5CB2 ...      LDX   6  A1+2(1)  
  15 5CTL          SRL   6  15                  [DEVICE TYPE 
   7 5D*=          SBN   4  1
   4 5DSW ...PAR1
  10 5G?Q          MHUNT    3,FILE,FNAME 
  19 5GRB          NAMETOP  3,CPB,CUNI          [NEEDED FOR ERROR MESSAGES   
   9 5GYJ ...      HUNTW 3,FILE,FTRAP
   9 5H5Q ...      BPZ   3  TRAPQUAL 
  10 5H?2          MHUNT    3,FILE,FABSNB
   9 5HQL          LDX   4  ATYPE(3) 
  20 5J==          ANDN  4  #7600               [EXTRACT SOME SYNTACTIC FORM BITS
   8 5JPW          ADX   5  FX1  
   7 5K9G          EXIT  5  0
   5 5K#5 ...TRAPQUAL
   8 5KBN ...      MONOUT   INVTG
   9 5KF? ...      SETREP2  NAMEFORM 
   7 5KHW ...      REPALLER  
   9 5KLF ...      BRN      XENDCOM  
  19 5KP6    #     SUBROUTINE TO SET UP A FILE/FLOCNB BLOCK FROM THE FIRST   
   9 5L8Q    #     FILE/FABSNB BLOCK 
   5 5LNB    SFLOC   
   8 5M82          SBX   5  FX1  
  10 5MML          MHUNT    2,FILE,FABSNB
   7 5N7=          LDN   6  6
   9 5NLW          LDX   4  ATYPE(2) 
   8 5P6G          ANDN  4  #2400
  21 5PL6          BZE   4  SFLOD               [BRANCH UNLESS MAG. TAPE DESCRIPTION 
   7 5Q5Q          ADN   6  2
   5 5QKB    SFLOD   
  11 5R52          SETUPCORE 6,3,FILE,FLOCNB 
  10 5RJL          MHUNT    2,FILE,FABSNB
   9 5S4=          LDX   4  ATYPE(2) 
   9 5SHW          DSA   4  ATYPE(3) 
   8 5T3G          ADX   2  A1(2)
   8 5TH6          ADN   2  A1-6 
   8 5W2Q          ADN   3  A1   
   7 5WGB          SMO      6
   7 5X22          MOVE  2  0
   8 5XFL          ADX   5  FX1  
   7 5X_=          EXIT  5  0
   7 5Y6D ...      FSHSKIP  B
   4 5Y?L ...(   
  21 5YDW    #     SUBROUTINE TO FIND FILE/FCB BLOCK, IF THERE IS ONE, FOR THE FILE  
  21 5YYG    #     TO BE RENAMED. X7= LINK. ON EXIT X3= ADDRESS OF FILE/FCB BLOCK.   
  13 5_D6    #     EXIT AT LINK IF BLOCK NOT FOUND   
  17 5_XQ    #     EXIT AT LINK+1 IF FILE OPEN OR BEING WAITED FOR   
  16 62CB    #     EXIT AT LINK+2 IF FILE RENAMABLE AND FCB FOUND
   9 62X2    #     NO COORDINATION   
   5 63BL    SEFCB   
   7 63W=          TOPFCB2  1
  14 64*W          LDX   2  3                   [->ENT   
   5 64TG    #SKI  G4
   4 65*6    (   
   9 65SQ          LDN   3  FME1(1)  
   8 66#B          SMO      FX2  
   9 66S2          LDN   4  ACOMMUNE1
  17 67?L          MOVE  3  3                   [USER NAME OF DIR.   
   9 67R=          LDN   3  ELOC1(2) 
   7 68=W          ADN   4  3
  16 68QG          MOVE  3  6                   [G.L.N. OF FILE  
   4 69=6    )   
  16 6CD3 ...#              X3= ADDRESS OF FOUND FILE/FCB BLOCK  
   3 6CD_ ...
  10 6CFX ...      BASEFCB 3,FILE,SEXIT,6
   9 6CGT ...      LDX   4  ELOC1(2) 
   5 6CHR ...SLOOP   
  18 6CJP ...      BXE   4  FLOC1-FCBRING(3),SLOOP2   [B IF MATCH FOUND  
   5 6CKM ...SLOOP1  
  14 6CLK ...      NEXTFCB 3,FILE,SLOOP,6   [TRY NEXT FCB
  13 6CMH ...SEXIT                  [EXIT NOT FOUND  
   7 6CNF ...      EXIT  7  0
   5 6CPC ...SLOOP3  
   9 6CQ* ...      LDX   4  ELOC1(2) 
   9 6CR? ...      BRN      SLOOP1   
   5 6CS9 ...SLOOP2  
  16 6CT7 ...      TESTNAMX 4,FLOC2-FCBRING(3),ELOC2(2),SLOOP3,4 
  18 6CW5 ...      LDX   4  ELAN(2)         [CHECK LOCAL NAME LANG CODE  
  13 6CX3 ...      BXU   4  FLAN-FCBRING(3),SLOOP3   
   4 6CX_ ...[   
  12 6CYX ...[ TEST FULL LOCAL NAME AND USERNAME 
   4 6C_T ...[   
  16 6D2R ...      TESTNAMX 3,FME1(1),FUSER1-FCBRING(3),SLOOP3,4 
  14 6D3P ...      SBN   3  FCBRING         [FCB FOUND   
   9 6D4W          LDX   4  CTOPEN(3)
  10 6DJG          ORX   4  FWAITCOUNT(3)
   9 6F46          BNZ   4  SEFEX1   
  21 6FBH ...      JMBS     SEFEX1,3,BFMCOP,BFSOLE,BFTIDYLOCK  [J IF FILE BEING COPIE
  16 6FNY ...                                   [ OR BEING CLOSED
  21 6G3B          BC       3,BFEMPTY           [CLEAR 'WAITING TO EMPTY FILE' BIT   
  20 6GH2          EXIT  7  2                   [FCB FOUND, FILE COMPLETELY FREE 
   5 6H2L    SEFEX1  
  20 6HG=          EXIT  7  1                   [FCB FOUND, OPEN OR BEING AWAITED
   4 6HQ4 ...)   
   4 6H_W    [   
  10 6JFG          MENDAREA 20,K99RENAME 
   4 6J_6    #END
   8 ____ ...46004620000200000000
