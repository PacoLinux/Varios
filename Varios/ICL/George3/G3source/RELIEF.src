  16 22FL    #SEG  RELIEF70                     [BOB NICHOLSON   
   9 22_=    #LIS  K0TWINSET>K0RELIEF
   7 23DW          8HRELIEF  
   4 23YG    #   
  21 24D6    #     THIS SEGMENT DEALS WITH HOLD ,SH, DE AND UN MESSAGES RECEIVED FROM
  21 24XQ    #     THE OTHER PROCESSOR VIA TWINSETA.ALSO SET ENGAGE BUTTONS IF STATE 
  17 25CB    #     OF EDS60 HAS CHANGED I.E.ENGAGED TO DISENGAGED.   
  19 25X2    #     1PCSTATE SUBROUTINES ARE ALSO CONTAINED IN THIS SEGMENT   
   4 26BL    #   
  21 26W=          SEGENTRY K1RELIEF,YGHOLD     [HOLD MESSAGE RECEIVED FROM OTHER SID
  17 27*W          SEGENTRY K2RELIEF,YGBOLE     [SET ENGAGE BUTTON   
  21 27TG          SEGENTRY K3RELIEF,QK3        [STATUS CHANGE ISSUED IN OTHER PROCES
  14 28*6          SEGENTRY K4RELIEF,QK4        [IPCSTATE
  20 28SQ          SEGENTRY K5RELIEF,QK5        [RECEIVES LIST OF SCRATCH AREAS  
  19 29#B          SEGENTRY K7RELIEF,QK7        [SENDS LIST OF SCRATCH AREAS 
   9 29S2          SEGENTRY K8RELIEF 
  16 2=?L                   +0                  [=-1 IF TRACING  
  21 2=R=          SEGENTRY K9RELIEF,QK9        [REPLY TO STATUS CHANGE FROM OTHER PR
   4 2?=W    #   
   7 2?QG    #     CONSTANTS 
   4 2#=6    #   
   9 2#PQ    XBIT5910       #01060000
   9 2*9B    XM5            #76027777
   9 2*P2    XM6            #76007777
  16 2B8L    Q24            +24                 [FOR BUTTON UP   
  16 2BN=    XSHARE         +ENTIN16            [UNIT N SHARED   
  17 2C7W    XDED           +ENTIN18             [UNIT N IS DEDICATED
  18 2CMG    XUNDED         +ENTIN20             [UNIT N IS UNDEDICATED  
   4 2D76    #   
   4 2DLQ    #   
  15 2F6B    #     ROUTINE TO FIND DEVICE LIST  X5=GEOG NO   
   4 2FL2    #   
   5 2G5L    XFINDL  
  15 2GK=          STO   0  EX4                 [STORE LINK  
  12 2H4W          FINDPEREC 1,APGEOG,5,,,XCME   
   8 2HJG          BRN      (EX4)
   4 2J46    #   
   4 2JHQ    [   
  17 2K3B    [     ROUTINE TO OUTPUT MESSAGE ON CONSOLE FOR A DEVICE 
  17 2KH2    [     ON ENTRY X4=UNIT NO. X5=MESSAGE IDENTIFIER X7=LINK
   4 2L2L    [   
   5 2LG=    OUTPUT  
   8 2L_W          SBX   7  FX1  
  17 2MFG          OUTPACK  4,1,VARDEC          [CONVERT TO ENGLISH  
  14 2M_6          LDN   1  1                   [CONSOLE 
  14 2NDQ          INFORMX  1,5,1               [OUTPUT  
   8 2NYB          ADX   7  FX1  
   7 2PD2          EXIT  7  0
   4 2PXL    [   
  10 2QC=    T2    ACROSS   TWINSETA,2   
   4 2QWW    #   
   4 2RBG    #   
  15 2RW6    #     SUBROUTINE TO FIND EWDAS/ENLINK ON JOBRING
   4 2S*Q    #   
  15 2STB    #     ON ENTRY X6=REQUEST TYPE X5=NUMBER X7 LINK
   4 2T*2    #   
  21 2TSL    #     ON EXIT X2=FX2 X3=PTR TO ENLINK IF EXIT1 ELSE EXIT 0 X3=RUBBISH   
   4 2W#=    #   
   8 2WRW    XLOOK LDX   2  FX2  
  17 2X?G          LDX   3  ETFRING(2)          [PTR TO FIRST ENLINK 
  18 2XR6    RA    LDN   0  ETFRING(2)          [BASE OF ENTWIN'S JOBRING
   7 2Y=Q          TXU   0  3
  17 2YQB          BCC      (7)                 [J IF END OF CHAIN   
  18 2_=2          SBN   3  ENLA                [PTR TO START OF ENLINK  
  12 2_PL          TXU   6  ENRQ(3)             [
  14 329=          TXU   5  ENBR(3)             [COMPARE 
  18 32NW          BCC      RB                  [..AND J IF ENLINK FOUND 
  17 338G          LDX   3  ENLA(3)             [ELSE GET PTR TO NEXT
   8 33N6          BRN      RA   
  15 347Q    RB    EXIT  7  1                   [EXIT FOUND  
   4 34MB    #   
  21 3572    #     SUBROUTINE TO FIND EWDAS/ITCH WITH SPECIFIED NUMBER IN ETBUFS+1   
   4 35LL    #   
  14 366=    #     X7 LINK  NO SPECIAL ENTRY CONDITIONS  
   4 36KW    #   
  18 375G    #     ON EXIT X3 PTR TO ITCH IF EXIT 1 ELSE EXIT 0 NOT FOUND
   4 37K6    #   
   8 384Q    XITCH LDX   3  FX2  
  11 38JB    NG3   HUNT2J   3,EWDAS,ITCH,,NG9
   8 3942          SMO      FX2  
  14 39HL          LDX   0  ETBUFS+1            [GET NO. 
  14 3=3=          TXU   0  ENSTART(3)          [COMPARE 
  15 3=GW          BCS      NG3                 [J IF DIFFER 
  14 3?2G          EXIT  7  1                   [FOUND   
  15 3?G6    NG9   EXIT  7  0                   [NOT FOUND   
   4 3?_Q    #   
  21 3#FB    #     SUBROUTINE TO CHAIN BLOCK POINTED TO BY X1 ON END OF BSA/ENTWIN   
   4 3#_2    #   
  18 3*DL    #     ACTIVITY.  LINK X0  ON EXIT X0=RUBBISH X1=FX1 X2=FX2  
   4 3*Y=    #   
  15 3BCW    R23   STO   0  EX4                 [SAVE LINK   
   8 3BXG          SMO      FX2  
   9 3CC6          LDX   2  ACTRING  
  17 3CWQ          SBN   2  ACTRING             [PTR TO NEXT ACTIVITY
  16 3DBB          LDX   2  BPTR(2)             [PREVIOUS BLOCK  
   8 3DW2          CHAIN    1,2  
   8 3F*L          BRN      (EX4)
   4 3FT=    #   
   4 3G#W    #   
  11 3GSG    #     HOLD BUTTON EVENT RECEIVED
   4 3H#6    #   
   5 3HRQ    YGHOLD  
  10 3J?B          LDX   5  ETBUFS+1(2)  
  16 3JR2          CALL  0  XFINDL              [GET PTR TO DL   
  14 3K=L          LADDP    1,1                 [AND APIA
  10 3KQ=          LDX   0  BSUNIT3(1)   
   7 3L9W          ANDN  0  4
  21 3LPG          BZE   0  U9                      [J IF WE HAVEN'T GOT  THE RIGHT  
   8 3M96          LDX   0  BIT11
   9 3MNQ          ORS   0  BSUNIT(1)
  17 3N8B          CALL  7  OLE                 [SET ENGAGE BUTTON   
   8 3NN2          BRN      T2   
   9 3P7L    U9    GEOERR   1,HOLD UP
   4 3PM=    #   
   4 3Q6W    #   
   8 3QLG    #     ENGAGE BUTTON 
   4 3R66    #   
   5 3RKQ    YGBOLE  
  17 3S5B          CALL  7  OLE                 [SET ENGAGE BUTTON   
   8 3SK2          BRN      T2   
   4 3T4L    #   
   9 3TJ=    #     SET ENGAGE BUTTON 
   4 3W3W    #   
  10 3WHG    OLE   LDX   5  ETBUFS+1(2)  
  16 3X36          CALL  0  XFINDL              [GET PTR TO DL   
  16 3XGQ          LINT     1,5                 [GET INTERNAL NO.
   8 3Y2B          SMO      FX1  
  18 3YG2          DVS   4  Q24                 [GET WORD AND BIT IN EBA 
   8 3Y_L          LDCT  6  #400 
   7 3_F=          SMO      4
   7 3_YW          SRL   6  0
   8 42DG          BUTTONUP 6,5  
   7 42Y6          EXIT  7  0
   4 43CQ    [   
  18 43XB    [     REPLY RECEIVED FROM OTHER MACHINE FOR STATUS CHANGE   
  21 44C2    [     IF REPLY OK THE APIA IS ALTERED AND MESSAGE OUTPUT HERE OTHERWISE 
  14 44WL    [     THE COMMAND ACTIVITY REPORTS ERROR.   
  21 45B=    [     THE COMMAND ACTIVITY USED TO ALTER APIA BUT AS THE OTHER PROCESSOR
  20 45TW    [     COULD GET IN WITH LOCK OUT REQUEST BEFORE TWINSET WAS ENTERED 
  10 46*G    [     IT IS ALTERED HERE.   
   4 46T6    [   
  14 47#Q    QK9   LDX   5  ETBUFS+4(2)         [REPLY   
  15 47SB          BPZ   5  TUP                 [J IF NOT OK 
  15 48#2          LDX   5  ETBUFS+1(2)         [GEOG NUMBER 
  16 48RL          CALL  0  XFINDL              [GET DL PTR IN X1
  16 49?=          LADDP    1,3                 [APIA PTR TO X3  
   8 49QW          LDX   1  FX1  
   9 4==G          LDX   5  BSUNIT(3)
  16 4=Q6          LDX   0  XM6(1)               [UNSET BITS 5-11
   9 4?9Q          ANDS  0  BSUNIT(3)
   9 4?PB          LDX   0  ETBUFS(2)
   8 4#92          SBN   0  GDEDR
  17 4#NL          BZE   0  XD1                  [_ IF DEDICATE REPLY
  18 4*8=          BPZ   0  XD2                  [_ IF UNDEDICATE REPLY  
  21 4*MW          ANDX  5  BIT9                 [IF REPLY TO SHARE LEAVE LOCK-OUT   
  20 4B7G          SLL   5  4                    [IN PROCESSOR IN WHICH IT WAS   
  20 4BM6          ORX   5  BIT10                [PREVIOUSLY DEDICATED AND SET   
  15 4C6Q          ORS   5  BSUNIT(3)            [SHARED BIT 
   7 4CLB    XD1   LDN   0  4
  19 4D62          ORS   0  BSUNIT3(3)           [SET DISENGAGE RIGHTS BIT   
  18 4DKL          CALL  7  OLE                  [SET ENGAGE BUTTON EVENT
   8 4F5=          LDX   1  FX1  
   9 4FJW          LDX   5  XSHARE(1)
   9 4G4G          LDX   0  ETBUFS(2)
  21 4GJ6          SBN   0  GSHARER              [SET APPROPRIATE MESSAGE IDENTIFIER 
   8 4H3Q          BZE   0  XD3  
   9 4HHB          LDX   5  XDED(1)  
   8 4J32          BRN      XD3  
   9 4JGL    XD2   CALL  6  RUNDED   
  17 4K2=    XD3   LDX   4  ETBUFS+1(2)                   [UNIT NO   
  17 4KFW          CALL  7  OUTPUT              [MESSAGE TO CONSOLE  
   8 4K_G          BRN      TUP  
   4 4LF6    #   
  19 4LYQ    #    S/R TO MAKE DRIVE UNDEDICATED. IT FREES EWDAS/ASTUC IF IT  
  16 4MDB    #    EXISTS, SETS RELEVANT BITS IN APIA AND SETS UP 
   9 4MY2    #    MESSAGE IDENTIFIER.
   4 4NCL    #   
   5 4NX=    RUNDED  
  14 4PBW          LDX   0  ETBUFS+1(2)          [UNIT NO
   8 4PWG          LDN   1  BCCB 
   9 4QB6    RF1   LDX   1  FPTR(1)  
   8 4QTQ          TXU   1  CXCC 
  17 4R*B          BCC      RF2                  [_ IF END OF CHAIN  
   9 4RT2          LDX   7  BACK1(1) 
  15 4S#L          BZE   7  RF2                  [_ IF EXOF  
   9 4SS=          TXU   0  GEOGU(1) 
  18 4T?W          BCS      RF1                  [_ IF NOT THIS UNIT NO  
  10 4TRG          LDCH  0  BSUNIT1(3)   
   7 4W?6          SBN   0  1
  17 4WQQ          DCH   0  BSUNIT1(3)           [DECREMENT CT OF SUS
  15 4X=B          FREECORE 1                    [FREE ASTUC 
   8 4XQ2    RF2   LDX   1  FX1  
  10 4Y9L          LDX   0  XBIT5910(1)  
   9 4YP=          ORS   0  BSUNIT(3)
   9 4_8W          LDX   5  XUNDED(1)
   7 4_NG          EXIT  6  0
   4 5286    #   
   4 52MQ    #   
  12 537B    #     SHARE, DEDICATE OR UNDEDICATE 
   4 53M2    #   
   4 546L    QK3 
  10 54L=          LDX   5  ETBUFS+1(2)  
  15 555W          CALL  0  XFINDL              [PTR TO DL   
  15 55KG          LADDP    1,3                 [PTR TO APIA 
   7 5656          LDN   5  1
   6 56JQ    #UNS ATBDG  
   8 574B          BRN      UD1  
  10 57J2          LDX   0  BSUNIT3(3)   
   8 583L          ANDN  0  #10  
  20 58H=          BNZ   0  UA                  [J IF STATUS CHANGE IN PROGRESS  
   7 592W    UD1   LDN   5  5
  10 59GG          LDXC  6  BSUNIT2(3)   
  19 5=26          BCS      UA                      [_ IF IN USE BY SYSTEM   
  10 5=FQ          ADX   6  BSUNIT4(3)   
  16 5=_B          BNZ   6  UA                  [ERROR IF IN USE 
  19 5?F2          LDN   1  BCCB                [SEARCH FOR POSSIBLE ASTUC   
   9 5?YL    UD    LDX   1  FPTR(1)  
   8 5#D=          TXU   1  CXCC 
  16 5#XW          BCC      UC                  [J IF END OF CCB 
  16 5*CG          LDX   0  BACK1(1)            [SERIAL NUMBER   
  17 5*X6          BZE   0  UC                  [J IF NO MORE ASTUCS 
  14 5BBQ          LDX   4  ETBUFS+1(2)         [GEOG NO 
   9 5BWB          TXU   4  GEOGU(1) 
   8 5CB2          BCS      UD   
  10 5CTL          LDX   0  BSUNIT3(3)   
   9 5D*=          ANDN  0  BCGCOM   
  17 5DSW          BNZ   0  UA                   [_ IF CG IN PROGRESS
  16 5F#G          LDX   0  BACK1(1)            [SUSN FROM ASTUC 
  17 5FS6          LDN   1  BCCB                [SEARCH FOR AS  EXOF 
   9 5G?Q    NEXF  LDX   1  BPTR(1)  
   8 5GRB          TXU   1  CXCC 
  17 5H?2          BCC      UC                  [J IF END OF CHAIN   
   9 5HQL          LDX   7  BACK2(1) 
  17 5J==          BZE   7  UC                  [ J IF NOT AN EXOF   
   9 5JPW          TXU   0  ASUSN(1) 
   8 5K9G          BCC      UA   
  16 5KP6          LDEX  7  FIP+5(1)            [CUONT OF AREAS  
   7 5L8Q          LDN   4  0
  17 5LNB    NEXS  BZE   7  NEXF                [J IF NO MORE AREAS  
   7 5M82          SBN   7  1
   7 5MML          ADN   4  6
   7 5N7=          SMO   4   
  10 5NLW          TXU   0  FDCELLS-6(1) 
  18 5P6G          BCS   NEXS                   [J IF AREA UNIT DIFFERS  
   8 5PL6          BRN      UA   
   4 5Q5Q    UC  
  21 5QKB    #UNS ATBDG                           [DONT DO ANY CHANGES IN TESTING MOD
   4 5R52    #SKI
   4 5RJL    (   
   8 5S4=          LDX   1  FX1  
   9 5SHW          LDX   5  BSUNIT(3)
   9 5T3G          LDX   0  XM6(1)   
  16 5TH6          ANDS  0  BSUNIT(3)            [UNSET BITS 5-11
   9 5W2Q          LDX   0  ETBUFS(2)
   8 5WGB          SBN   0  GDED 
  21 5X22          BZE   0  UC3                  [_ IF DEDICATE I.E. MAKE UNDEDICATED
  21 5XFL          BPZ   0  UC1                  [_ IF UNDEDICATE I.E. MAKE DEDICATED
  19 5X_=          ANDX  5  BIT9               [IF SHARE LEAVE LOCK-OUT IN   
  21 5YDW          SLL   5  4                    [PROCESSOR WHERE IT WAS PREVIOUSLY  
  17 5YYG          ORX   5  BIT10                [DEDICATED AND SET  
  15 5_D6          ORS   5  BSUNIT(3)            [SHARED BIT 
   7 5_XQ          NGN   0  5
  19 62CB          ANDS  0  BSUNIT3(3)           [UNSET DISENGAGE RIGHTS BIT 
   8 62X2          BRN      UC2  
   7 63BL    UC1   LDN   0  4
  19 63W=          ORS   0  BSUNIT3(3)           [SET DISENGAGE RIGHTS BIT   
   8 64*W ...UC2   LDX   1  FX1  
   9 65*6          LDX   5  XSHARE(1)
   9 65SQ          LDX   0  ETBUFS(2)
   9 66#B          SBN   0  GSHARE   
  21 66S2          BZE   0  UC4                   [SET APPROPRIATE MESSAGE IDENTIFIER
   9 67?L          LDX   5  XDED(1)  
   8 67R=          BRN      UC4  
  16 68=W    UC3   CALL  6  RUNDED              [MAKE UNDEDICATED
  10 68QG    UC4   LDX   4  ETBUFS+1(2)  
   9 69=6          CALL  7  OUTPUT   
   8 69FY ...      CALL  7  OLE  
   4 69PQ    )   
  15 6=9B          LDCT  5  #400                  [OKAY REPLY
   9 6=P2    UA    LDX   6  ETBUFS(2)
  19 6?8L          ADN   6  1                     [SET UP REPLY IDENTIFIER   
  10 6?N=          ACROSS   TWINSETA,5   
   4 6#7W    #   
   9 6#MG    #     IPCSTATE ROUTINE  
  17 6*76    #     ON ENTRY ACOMMUNE1 CONTAINS MODE (READ OR WRITE)  
  14 6*LQ    #     ON EXIT ACOMMUNE1 CONTAINS THE REPLY  
   4 6B6B    #   
  10 6BL2    QK4   LDX   0  ACOMMUNE1(2) 
  18 6C5L          STO   0  ACOMMUNE3(2)        [STORE MODE FOR TRACING  
  19 6CK=          BPZ   0  READ                [J IF IPCSTATE FOR READ LINK 
  15 6D4W          IPCSTATE W,3                 [ELSE WRITE  
   8 6DJG          BRN      XIT  
   8 6F46    READ  IPCSTATE R,3  
  15 6FHQ    XIT   STO   3  ACOMMUNE1(2)        [STORE REPLY 
   7 6G3B          SMO   FX1 
   9 6GH2          LDX   0  K8RELIEF 
  16 6H2L          BZE   0  NTRACE              [J IF NO TRACING 
  10 6HG=          STO   3  ACOMMUNE2(2) 
  13 6H_W          LDN   0  #6364               [ST  
  10 6JFG          STO   0  ACOMMUNE1(2) 
   9 6J_6          LDX   0  ETWINMK  
  10 6KDQ          STO   0  ACOMMUNE4(2) 
  18 6KYB          STO   7  ACOMMUNE5(2)        [USUALLY AN INTERNAL LINK
   7 6LD2          TWTRACE   
  10 6LXL          STO   3  ACOMMUNE1(2) 
   5 6MC=    NTRACE  
   5 6MWW          UP
   4 6NBG    #   
   4 6NW6    #   
   4 6P*Q    #   
   4 6PTB    QK5 
   4 6Q*2    #   
  18 6QSL    #     SERVICES INCOMING GCHAR MESSAGES FROM OTHER PROCESSOR 
   4 6R#=    #   
   8 6RRW          LDCT  7  #200 
   9 6S?G          ANDX  7  ETBUFS(2)
  18 6SR6          BNZ   7  NG2                 [J IF CONTINUATION BLOCK 
  16 6T=Q          SETNCORE ESENDLEN,3,EWDAS,ITCH [SET UP BUFFER 
   9 6TQB          LDN   1  ETBUFS(2)
   9 6W=2          LDN   2  ENMARK(3)
  18 6WPL          MOVE  1  ETBUFFLE            [MOVE READ INFO TO BUFFER
   8 6X9=    NG6   LDX   2  FX2  
   9 6XNW          LDXC  0  ETBUFS(2)
  18 6Y8G          BCS      TUP                 [J IF NOT LAST TRANSFER  
   9 6YN6          LDX   5  BITS22LS 
  20 6_7Q          ANDS  5  ENMARK(3)             [CLEAR B0+B1-NOT IN CHECKSUM   
  17 6_MB          LDN   5  ENMARK(3)           [ELSE CHECK CHECKSUM 
   9 7272          SMO      ENCT(3)  
   7 72LL          SUM   4  0
  16 736=          BZE   4  NG7                 [J IF CHECKSUM OK
  10 73KW          GEOERR   1,CHECKSUM   
   8 745G    NG7   LDN   6  GCHA 
  10 74K6          LDX   5  ENSTART(3)   
  10 754Q          TRACE    ENCT(3),SCRCT
  19 75JB          LDX   0  ENCT(3)             [IF NO SCRATCH AREAS IN USE..
  17 7642          SBN   0  ENAR-ENMARK         [..IN OTHER MACHINE..
   8 76HL          BNZ   0  NG1  
  17 773=          FREECORE 3                   [..FREE ITCH BLOCK   
   8 77GW          BRN      NG5  
  15 782G    NG1   CALL  7  XLOOK               [FIND ENLINK 
  15 78G6          BRN      NG8                 [NOT FOUND   
  16 78_Q          LDX   2  3                   [SAVE ENLINK PTR 
  16 79FB          CALL  7  XITCH               [FIND EWDAS/ITCH 
  15 79_2          BRN      NG4                 [NOT FOUND   
  18 7=DL          CHAIN    3,2                 [CHAIN ITCH AFTER ESEND  
   7 7=Y=    NG5   UPPLUS   1
   9 7?CW    NG4   GEOERR   1,LOST IT
   4 7?XG    #   
  16 7#C6    #     ENLINK NOT FOUND AS JOB HAS PROBABLY BROKEN IN
   4 7#WQ    #   
   8 7*BB    NG8   CALL  7  XITCH
   8 7*W2          BRN      TUP  
  15 7B*L          FREECORE 3                   [FREE ITCH   
   5 7BT=    TUP   UP
   4 7C#W    #   
   9 7CSG    #     CONTINUATION BLOCK
   4 7D#6    #   
  16 7DRQ    NG2   CALL  7  XITCH               [FIND EWDAS/ITCH 
  15 7F?B          BRN      TUP                 [NOT FOUND   
  10 7FR2          LDX   4  ALOGLEN(3)   
  18 7G=L          LDX   5  4                   [REMEMBER ORIGINAL LENGTH
  15 7GQ=          ADN   4  ETBUFFLE-2          [NEW LENGTH  
  15 7H9W          ALTLEN   3,4                 [EXTEND ITCH 
  15 7HPG          CALL  7  XITCH               [FIND ITCH   
   8 7J96          BRN      NG4  
  17 7JNQ          LDN   1  ETBUFS+2(2)         [START OF SCRATCH FAC
   9 7K8B          LDN   2  ENTRGA(3)
  20 7KN2          ADX   2  5                   [PTR TO NEXT FREE WORD IN ITCH   
  10 7L7L          MOVE  1  ETBUFFLE-2   
  14 7LM=          BRN      NG6                 [FINISH  
   4 7M6W    #   
  13 7MLG    #     RECEIVES GCHA FROM OTHER PROCESSOR
   4 7N66    #   
  20 7NKQ    #     SINCE THE INFO TO BE SENT MAY BE LONGER THAN THE READ BUFFER  
  20 7P5B    #     IT HAS TO BE SENT IN CHUNCKS.A NUMBER OF ESENDS MAY BE SET UP 
  21 7PK2    #     THE FIRST ALWAYS HAS STANDARD FORMAT THE REST HAVE REQUEST TYPE   
  20 7Q4L    #     IN ENMARK AND A UNIQUE NUMBER IN ENSTART. B0 AND B1 ARE SET AS
  15 7QJ=    #     FOLLOWS B0=1 MEANS ANOTHER TRANSFER COMING
  13 7R3W    #             B1=1 NON STANDARD FORMAT  
   4 7RHG    #   
  16 7S36    QK7   LDN   7  0                   [NUMBER OF ESENDS
  19 7SGQ          LDN   6  0                  [DEFAULT IN CASE SU OFFLINE   
  15 7T2B          FINDSUX  1,ETBUFS+3(2),NE3   [FIND ASTUC  
  10 7TG2          LDXC  7  CSCRATCH(1)  
  18 7T_L          BZE   7  NE1                 [J IF NO SCRATCH AREAS   
  18 7WF=          LDN   5  ETBUFFLE-2          [HOW MANY FACS/TRANSFER  
  20 7WYW          DVS   6  5                   [X7=ANSWER X6=REM X7=NO OF ESENDS
  15 7XDG          BZE   6  NE1                 [J IF NO REM 
  20 7XY6          ADN   7  1                   [OTHERWISE IT NEEDS ANOTHER ESEND
  10 7YCQ    NE1   LDXC  6  CSCRATCH(1)  
   7 7YXB          BCS      /
  19 7_C2    NE3   ADN   7  1                    [PLUS ONE FOR 1ST TRANSFER  
  18 7_WL          ADN   6  ETBUFFLE            [X6=COUNT FOR CHECKSUM   
   7 82B=          LDX   4  6
  21 82TW          ADN   4  ESENDLEN-ETBUFFLE   [ROUND UP TO MULTIPLE OF BUFFER SIZE 
  21 83*G          SETUPCORE 4,3,EWDAS,GEXD     [ASSEMBLE EVERYTHING IN HERE FIRST   
   7 83T6          LDN   0  1
   7 84#Q          TXU   0  7
  20 84SB          BCC      NE4                 [J IF NO SCRATCH AREAS OR NOT... 
  17 85#2                                       [..PREVIOUSLY ONLINE 
  11 85RL          FINDSUX  1,ETBUFS+3(2),NE2
  10 86?=          LDXC  4  CSCRATCH(1)  
   7 86QW          BCS      /
  18 87=G          BZE   4  NE4                 [J IF NO SCRATCH AREAS   
  16 87Q6          LDN   1  HSCRATCH(1)         [START OF LIST   
  16 889Q          LDN   2  ENMARK+ETBUFFLE(3)  [POSITION IN GEXD
   7 88PB          SMO      4
  19 8992          MOVE  1  0                   [SHIFT SCRATCH AREAS TO GEXD 
   9 89NL    #     NOW THE RED TAPE  
  15 8=8=    NE4   STO   6  ENCT(3)             [NO OF WORDS 
   8 8=MW          LDCT  0  #400 
  14 8?7G          STO   0  ESREP(3)            [OK REPLY
   8 8?M6          LDX   2  FX2  
  10 8#6Q          LDX   0  ETBUFS+3(2)  
  16 8#LB          STO   0  ENSUSN(3)           [SERIAL NUMBER   
  14 8*62          STOZ     ENHOME(3)           [NO HOME 
  10 8*KL          LDX   6  ETBUFS+1(2)  
  15 8B5=          STO   6  ENSTART(3)          [UNIQUE NO.  
   8 8BJW          LDN   0  GCHAR
  17 8C4G          STO   0  ENMARK(3)           [MESSAGE/REQUEST TYPE
  14 8CJ6          STOZ     ENCK(3)             [CHECKSUM
   9 8D3Q          LDN   5  ENMARK(3)
   9 8DHB          SMO      ENCT(3)  
  16 8F32          SUM   4  0                   [FORM CHECKSUM   
   9 8FGL          SBS   4  ENCK(3)  
  19 8G2=          LDX   4  7                   [REMEMBER NO OF ESENDS NEEDED
  17 8GFW          LDN   6  0                   [B0 AND B1 MARKERS   
  15 8G_G          LDN   5  0                   [MOD TO GEXD 
   9 8HF6    #     SET UP ESEND LOOP 
  13 8HYQ    NE9   SETNCORE ESENDLEN,1,EWDAS,ESEND,,1
   9 8JDB          LDN   0  ENTRGA(1)
   9 8JY2          STO   0  ENTRGA(1)
  15 8KCL          STO   0  ENTRGA+1(1)         [EMPTY RING  
  10 8KX=          MHUNTW   3,EWDAS,GEXD 
   7 8LBW          SMO      5
  16 8LWG          LDN   2  ENMARK(3)           [POSITION IN GEXD
  17 8MB6          LDN   3  ENMARK(1)           [POSITION IN ESEND   
  17 8MTQ          BNZ   5  NE10                [J IF NOT FIRST TIME 
  16 8N*B          MOVE  2  ETBUFFLE            [SHIFT RED TAPE  
   7 8NT2          SBN   4  1
  19 8P#L          BZE   4  NE8                 [J IF ONLY ONE ESEND TO SEND 
  16 8PS=          LDCT  6  #400                [SET NOT LAST BIT
  19 8Q?W          ADN   5  2                   [PTR MOVES UP 7 FIRST TIME   
   8 8QRG          BRN      NE11 
  17 8R?6    NE10  LDN   0  GCHAR               [NON STANDARD FORMAT 
   8 8RQQ          STO   0  0(3) 
   8 8S=B          SMO      FX2  
   9 8SQ2          LDX   0  ETBUFS+1 
  15 8T9L          STO   0  1(3)                [UNIQUE NO   
   7 8TP=          ADN   3  2
  18 8W8W          MOVE  2  ETBUFFLE-2          [MOVE IN SCRATCH AREAS   
   8 8WNG          LDCT  0  #200 
  17 8X86          ORS   0  6                   [SET NON STANDARD MKR
  19 8XMQ    NE11  ORS   6  ENMARK(1)           [SET B0 AND B1 AS APPROPRIATE
   7 8Y7B          LDX   3  1
  19 8YM2          CALL  0  R23                 [CHAIN ESEND ON TWINNING Q   
  16 8_6L          ADN   5  ETBUFFLE-2          [UPDATE GEXD MOD 
   8 8_L=          BCT   7  NE9  
   9 925W          LDXC  0  ENMARK(3)
   7 92KG          BCS      /
  19 9356          STO   0  ENMARK(3)           [CLEAR B0-INDICATES LAST ONE 
   8 93JQ          BRN      NE12 
  19 944B    NE8   CALL  0  R23                 [QUEUE FIRST ESEND (AND LAST)
  10 94J2    NE12  MHUNTW   1,EWDAS,GEXD 
   7 953L          FREECORE 1
   5 95H=          UP
  15 962W    NE2   FREECORE 3                   [FREE GEXD   
  15 96GG          BRN      QK7                 [START AGAIN 
   4 9726    #   
   9 97FQ    XCME  GEOERR   1,NOTCME 
   4 97_B    #   
  10 98F2          MENDAREA 100,K99RELIEF
   4 98YL    #END
   6 ____ ...403547410002
