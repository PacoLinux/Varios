   4 22FL    [   
   4 22_=    [   
  19 23DW    [THIS IS A TIDIED-UP VERSION WITH MODETEST 6 BITSETTING MACROS  
   4 23YG    [   
  14 24D6          SEG      ORELEMPT,8,JUDY BIDGOOD,OPEN 
  12 24XQ          SEGENTRY K1ORELEMPT,Z1ORELEMPT
   4 25CB    [   
  17 25X2    [THIS ROUTINE FINDS THE FCB WHOSE UNIQUE NUMBER IS IN X6
   5 26BL    XGETFCB 
   8 26W=          GFCB     6,3  
   7 27*W          EXIT  2  0
  17 27TG    [THIS ROUTINE IS TO FIND THE NEXT FCB AFTER A GIVEN FCB 
   5 28*6    NEXTFCB 
  10 28SQ          LDX   0  FILEPLUSFCB  
   9 29#B          LDX   2  BPTR(3)  
   5 29S2    XLOPE   
   9 2=?L          LDX   2  FPTR(2)  
   9 2=R=          SMO      FPTR(2)  
  10 2?=W          BXU   0  ATYPE,XLOPE  
   7 2?QGP...#SKI  IFS<1$1   
   4 2#=6P...(   
  10 2#PQ          BXU   2  CXFI,NBASE   
   9 2*9B          LDX   2  BFILE+1  
   5 2*P2    NBASE   
   4 2B8LP...)   
   7 2BN=          EXIT  1  0
  20 2C7W    [THIS ROUTINE SETS UP A FULLB FOR B.S. TO BE FREED & CHAINS IT AFTER
   7 2CMG    [RELEVANT FMAPP 
   5 2D76    XFREEBS 
   9 2DLQ          LDX   4  FUSEBL(3)
  16 2F6B ...      LDN   1  FBLKS-A1            [UPDATE FUSEBL   
   9 2FL2          STO   1  FUSEBL(3)
   9 2G5L          SBN   4  FBLKS-A1 
  18 2GK=          BZE   4  NOBK                [IF NO B.S. IN FILE EXIT 
   7 2H4W          ADN   4  2
   8 2HJG          SBX   5  FX1  
  18 2J46          SETUPCORE 4,1,BSTB,FULLB     [SET UP AFULLB TO CONTAIN
  17 2JHQ          CALL  2  XGETFCB             [S. BLOCKS FROM FCB  
   9 2K3B          LDN   0  BSPRE(3) 
   8 2KH2          STO   4  A1(1)
   8 2L2L          ADN   1  A1+1 
   7 2LG=          SMO      4
   8 2L_W          MOVE  0  511  
   4 2MFG    XCHF
  11 2M_6          TESTMODE 7,YCAR,,CAREFUL  
  20 2NDQ          TOPFCA   3                   [IF NOT LAREFUL CHAIN FULLB AFTER
  15 2NYB          PSTAC    3,3                 [DIRS FMAPP  
   5 2PD2    XCHFB   
  20 2PXL          CALL  1  NEXTFCB             [IF CAREFUL FMAPP WILL BE SET UP 
  20 2QC=          MHUNTW   1,BSTB,FULLB        [LATER. CHAIN FULLB AFTER BLOCK  
  21 2QWW          CHAIN    1,2                 [AFTER WHICH FMAPP WILL BE CHAINDE   
   8 2RBG          ADX   5  FX1  
   7 2RW6    NOBK  EXIT  5  0
   5 2S*Q    XCHFULLB
   8 2STB          SBX   5  FX1  
   8 2T*2          BRN      XCHF 
   9 2TSL    YCAR  CALL  2  XGETFCB  
   8 2W#=          BRN      XCHFB
  12 2WRW    [THIS ROUTINE CHECKS FOR ALLOCATE   
   5 2X?G    XALLOC  
  10 2XR6          MHUNTW   2,FILE,ENT   
   9 2Y=Q          LDX   4  EALLOC(2)
   8 2YQB          ANDN  4  #77  
  18 2_=2          BZE   4  NALLOC              [EXIT IF NOT ALLOCATED   
   8 2_PL          LDN   0  48   
  19 329=          BXL   4  0,NALLOC            [EXIT IF RESIDENCE NO. < 48  
  20 32NW          RESQUERY 4,NALLOC            [EXIT IF RESIDENCE DOES NOT EXIST
  16 338G          EXIT  5  1                   [ALLOCATE EXIT   
   5 33N6    NALLOC  
   7 347Q          EXIT  5  0
   5 34MB    XRWBLKR 
   8 3572          SBX   5  FX1  
  10 35LL          SETNCORE 2,1,FILE,FWB 
  21 366=          LDN   0  2                   [IF THE FILE IS NOT CAREFUL & ONLINE 
  21 36KW          STO   0  A1(1)               [& SOME OF ITS EXISTING B.S. IS FREED
  21 375G          CALL  2  XGETFCB             [THE BLOCKS RECORD MUST BE UPDATED   
  21 37K6          LDX   0  BSPRE(3)            [TO SAY THERE ARE NO BLOCKS BECAUSE  
  21 384Q          STO   0  A1+1(1)             [THE B.S. IS FREED WHEN THE DIR IS   
  21 38JB          STEP                         [CLOSED      THE BLOCKS RECORD MUST  
  19 3942          REPLACE                      [NOT THEN CONTAIN FREED B.S. 
   9 39HL          MFREEW   FILE,FWB 
   7 3=3=          BACKSPACE 
  10 3=GW          MHUNTW   1,FILE,ENT   
   7 3?2G          LDN   0  1
  21 3?G6          STO   0  ECOPS(1)            [ECOPS MUST REFLECT LENGTH OF BLOCKS 
  16 3?_Q          NAME     1,FILE,FWB          [RECORD I.E. ZERO
   7 3#FB          REWRITE   
  10 3#_2          MHUNTW   1,FILE,FWB   
  10 3*DL          NAME     1,FILE,ENT   
   8 3*Y=          ADX   5  FX1  
   7 3BCW          EXIT  5  0
   4 3BXG    [   
  15 3CC6    Z1ORELEMPT                         [ENTRY POINT 
   4 3CWQ    [   
  21 3DBB          STOZ     AWORK1(2)           [ZEROIZE ' NO BLOCKS ALTERED' MARKER 
  11 3DW2          TESTMODE 7,NEM,NOT,EMPTY  
  10 3F*L          MHUNTW   1,FILE,ENT   
   9 3FT=          LDEX  0  ECOPS(1) 
  19 3G#W          BZE   0  YOF                 [J IF FILE EMPTY & OFFLINE   
   9 3GSG          CALL  2  XGETFCB  
  14 3H#6          TESTMODE 7,NOFF,NOT,ERASING,CAREFUL   
   7 3HRQ          LDN   0  2
  21 3J?B          SMO      FX2                 [SET 'NO.OF BLOCKS ALTERED' MARKER   
   9 3JR2          ORS   0  AWORK1   
   9 3K=L    #SKI  K6ORELEMPT>100-100
   8 3KQ=          TRACE 0,ERCARE
  19 3L9W          CALL  5  XFREEBS             ['FREE' B.S. IF ANY IN FILE  
  19 3LPG          TESTMODE 7,NBSG,,ERASING     [NO BS REQUIRED IF ERASING   
  18 3M96          HUNTW    1,BSTB,FULLB        [IF NO HULLB GET NO B.S. 
   8 3MNQ          BNG   1  NBSG 
   9 3N8B          BRN      UFULLB   
   4 3NN2    NOFF
  21 3P7L          HUNTW    1,BSTB,FULLB        [IF NO FULLB GIVEN DON'T CHANGE B.S. 
   9 3PM=    #SKI  K6ORELEMPT>100-100
  10 3Q6W          TRACE    1,FULLBPTR   
  19 3QLG          BNG   1  NOF                 [ALREADY ALLOCATED TO FILE   
   7 3R66          LDN   0  2
  21 3RKQ          SMO      FX2                 [SET 'NO.OF BLOCKS ALTERED' MARKER   
   9 3S5B          ORS   0  AWORK1   
  17 3SK2          CALL  5  XALLOC              [TEST IF ALLOCATED   
   8 3T4L          BRN      NAL  
   8 3TJ=          BRN      YAL  
  17 3W3W    NAL                                [NOT ALLOCATED PATH  
   9 3WHG          CALL  5  XFREEBS  
  15 3X36          CALL  5  XRWBLKR             [UPDATE DIR  
   4 3XGQ    UFB 
  10 3Y2B          MHUNTW   1,BSTB,FULLB 
   5 3YG2    UFULLB  
   9 3Y_L    #SKI  K6ORELEMPT>100-100
  10 3_F=          TRACE    4,CARENALL   
   9 3_YW          CALL  2  XGETFCB  
   8 42DG          LDX   4  A1(1)
  20 42Y6          ADN   4  FBLKS-A1-2          [CALCULATE REQUIRED NEW LENGTH OF
   9 43CQ    #SKI  K6ORELEMPT>100-100
  10 43XB          TRACE    4,NEWFCBLN   
  17 44C2          ALTLEN   3,4                 [FCB & ALTER LENGTH  
  10 44WL          TESTMODE 7,NCC1,,EMPTY
  21 45B=          CALL  5  XALLOC              [IF MODE IS NOT EMPTY, DIR MAY NOT   
  20 45TW          BRN      NCC1                [STILL BE OPEN TO THIS ACT & THUS
  20 46*G          MHUNTW   1,BSTB,FULLB        [FILE COULD HAVE BEEN ALLOCATED, 
  21 46T6          BXU   4  A1+1(1),YCC         [WHICH CASE OUR B.S. WRONG SO GIVE UP
   8 47#Q          BRN      NCC2 
   4 47SB    NCC1
  10 48#2          MHUNTW   1,BSTB,FULLB 
   4 48RL    NCC2
   9 49?=          CALL  2  XGETFCB  
  10 49QW          LDX   4  ALOGLEN(3)   
  16 4==G          STO   4  FUSEBL(3)           [UPDATE FUSEBL   
  19 4=Q6          ADN   3  BSPRE               [COPY B.S. FROM FULLB TO FCB 
   9 4?9Q          LDN   2  A1+1(1)  
   8 4?PB          SMO      A1(1)
   8 4#92          MOVE  2  511  
  15 4#NL          MFREEW   BSTB,FULLB          [FREE FULLB  
   8 4*8=          BRN      NOF  
   4 4*MW    YCC 
   9 4B7G          CALL  2  XGETFCB  
  21 4BM6          LDX   4  FUSEBL(3)           [IF WE HAVE DECIDED TO GIVE UP, PUT  
  18 4C6Q          ALTLEN   3,4                 [FCB BACK TO OLD LENGTH  
   8 4CLB          BRN      YFCB 
  16 4D62    YAL                                [ALLOCATED PATH  
   9 4DKL    #SKI  K6ORELEMPT>100-100
  10 4F5=          TRACE    4,NCAREALL   
  21 4FJW          MHUNTW   1,BSTB,FULLB        [IF FULLB GIVEN HAS B.S. ON CORRECT  
   8 4G4G          LDX   5  A1(1)
   9 4GJ6          CALL  2  XGETFCB  
   7 4H3Q          SBN   5  2
  21 4HHB          LDX   0  FUSEBL(3)           [CALCULATE DIFFERENCE BETWEEN NO. OF 
   9 4J32    #SKI  K6ORELEMPT>100-100
   9 4JGL          TRACE    0,FCBLEN 
  21 4K2=          SBN   0  FBLKS-A1            [B.S. BLOCKS ALREADY ALLOCATED TOFILE
  21 4KFW          SBX   5  0                   [& NO. OF B.S. BLOCKS IN GIVEN FULLB 
  21 4K_G          BZE   5  YFCB                [IF NO DIFFERENCE NO CHANGE NECESSARY
  19 4LF6          BNG   5  XCESS               [J IF BLOCKS TO BE ^FREED^   
  10 4LYQ          LDX   4  ALOGLEN(3)   
   7 4MDB          ADX   4  5
   9 4MY2    #SKI  K6ORELEMPT>100-100
   9 4NCL          TRACE    4,ALLFCBL
   8 4NX=          ALTLEN   3,4  
   9 4PBW          CALL  2  XGETFCB  
   8 4PWG          LDN   2  A1(3)
  10 4QB6          ADX   2  ALOGLEN(3)   
  21 4QTQ          SBX   2  5                   [COPY NUMBER OF B.S. BLOCKS REQUIRED 
  20 4R*B          MHUNTW   1,BSTB,FULLB        [TO MAKE NUMBER IN FCG EQUAL TO  
  16 4RT2          ADN   1  A1                  [NUMBER IN FULLB 
   8 4S#L          ADX   1  0(1) 
   7 4SS=          SBX   1  5
   7 4T?W          SMO      5
  20 4TRG          MOVE  1  0                   [TAKE REQUISITE NUMBER OF BLOCKS 
  20 4W?6          CALL  2  XGETFCB             [FROM END OF FULLB TO END OF FCB 
  10 4WQQ          LDX   0  ALOGLEN(3)   
   9 4X=B          STO   0  FUSEBL(3)
  10 4XQ2          MHUNTW   1,BSTB,FULLB 
  10 4Y9L          LDX   2  ALOGLEN(3)   
  10 4YP=          SBN   2  FBLKS-A1-2   
   7 4_8W          SBX   2  5
  19 4_NG          STO   2  A1(1)               [RESET RECORD HEADER IN FULLB
  21 5286          SHORTEN  1,0(2)              [SHORTEN FULLB TO REFLECT NUMBER OF  
   7 52MQ          MFREEBAX  
   8 537B          BRN      NOF  
   4 53M2    NBSG
   9 546L          CALL  2  XGETFCB  
   9 54L=          LDN   1  FBLKS-A1 
  16 555W    XSH   STO   1  FUSEBL(3)           [UPDATE FUSEBL   
   4 55KG    XSHO
   9 5656    #SKI  K6ORELEMPT>100-100
  10 56JQ          TRACE    0(1),SHORTFCB
  15 574B          SHORTEN  3,0(1)              [SHORTEN FCB 
   4 57J2    NOF 
   7 583L          LDN   5  0
   9 58H=          CALL  2  XGETFCB  
  18 592W          JMBS     XACC,3,BFSER,BFMT   [IF *DA : FBLMOD=FUSEBL  
   9 59GG          LDX   0  FUSEBL(3)
   9 5=26          STO   0  FBLMOD(3)
   4 5=FQ    XACC
   9 5=_B          ACROSS   ORELEND,1
   5 5?F2    XCESS   
   7 5?YL          MFREEBAX  
   7 5#D=          NGX   5  5
   7 5#XW          ADN   5  2
   9 5*CG    #SKI  K6ORELEMPT>100-100
   9 5*X6          TRACE    5,XCESS  
  11 5BBQ          SETUPCORE 5,1,BSTB,FULLB  
   9 5BWB          CALL  2  XGETFCB  
   8 5CB2          STO   5  A1(1)
  21 5CTL          SBN   5  2                   [MOVE THE REQUISITE NUMBER OF B.S.   
  21 5D*=          LDX   0  BSPRE(3)            [FROM THE END OF THE FCB TO A FULLB  
   9 5DSW          STO   0  A1+1(1)  
   8 5F#G          ADN   1  A1+2 
   9 5FS6          LDX   0  FUSEBL(3)
   8 5G?Q          ADN   0  A1(3)
   7 5GRB          SBX   0  5
   7 5H?2          SMO      5
   7 5HQL          MOVE  0  0
  21 5J==          SBS   5  FUSEBL(3)           [REMOVE NUMBER OF B.S. BLOCKS FROM   
  20 5JPW          CALL  5  XCHFULLB            [FCB SO THST NUMBER LEFT EQUALS  
  15 5K9G          CALL  5  XRWBLKR             [UPDATE DIR  
  16 5KP6          CALL  2  XGETFCB             [NUMBER IN FULLB.
   9 5L8Q          LDX   1  FUSEBL(3)
   8 5LNB          BRN      XSHO 
   9 5M82    XGR   GEOERR   1,DIRBRK 
   5 5MML    TRCLU   
   8 5N7=          SETREP   NAME 
   4 5NLW    UPF 
   7 5P6G          UPPLUS   1
   4 5PL6    YOF 
  16 5Q5Q          TESTMODE 7,NOF,,ERASING      [IF ERASING+EMPTY
  21 5QKB          MHUNTW   1,BSTB,FULLB        [IF THERE IS A FULLB THE B.S. FROM   
  19 5R52          MHUNTW   2,FILE,FCB          [IT SHOULD BE PUT INTO FCB   
   8 5RJL          BNG   1  NOF  
  21 5S4=          BNG   2  UFULLB              [IF FCB IN ACT  CHAIN THEN ONE SET   
  20 5SHW          LDN   0  2                   [SET 'NO.OF BS ALTERED' MARKER   
   8 5T3G          SMO      FX2  
   9 5TH6          ORS   0  AWORK1   
  21 5W2Q          CALL  2  XGETFCB             [UP BY THIS OPEN WAS NOT USED. THUS  
  19 5WGB          LDX   4  FUSEBL(3)           [FCB IN FILE CHAIN COULD HAVE
  20 5X22          LDN   0  FBLKS-A1            [DIFFERENT B.S. IN IT. SINCE THE 
  20 5XFL          STO   0  FUSEBL(3)           [DIRENT IS MARKED AS HAVING NO   
  20 5X_=          SBN   4  FBLKS-A1            [BLOCKS THIS B.S. CAN BE FREED AS
  19 5YDW          BZE   4  UFB                 [IT IS NOT RECORDED IN DIRENT
  20 5YYG          ADN   4  2                   [AND THE B.S. IN OUR FULLB CAN BE
  13 5_D6          SETUPCORE 4,1,BSTB,FULLB     [USED
   9 5_XQ          CALL  2  XGETFCB  
   9 62CB          LDN   0  BSPRE(3) 
   8 62X2          STO   4  A1(1)
   8 63BL          ADN   1  A1+1 
   7 63W=          SMO      4
   8 64*W          MOVE  0  511  
   7 64TG          MFREEBAX  
   8 65*6          BRN      UFB  
   4 65SQ    NEM 
  21 66#B          MHUNTW   1,BSTB,FULLB        [IF THERE IS NO FULLB THEN NO B.S.   
  18 66S2          BNG   1  NOF                 [SHOULD BE ADDED TO FCB  
   7 67?L          LDN   0  2
  21 67R=          BXE   0  A1(1),NOF           [IF FULLB IS EMPTY NO B.S. SHOULD BE 
   9 68=W          CALL  5  XALLOC   
  21 68QG          BRN      NOA                 [IF MODE IS NOT EMPTY, DIR MAY NOT   
  21 69=6          MHUNTW   1,BSTB,FULLB        [BE OPEN TO THIS ACT, THUS FILE COULD
  21 69PQ          BXU   4  A1+1(1),YFCB        [HAVE BEEN ALLOCATED SINCE B.S. WAS  
  20 6=9B                                       [GOT, IN WHICH CASE WE GIVE UP   
   4 6=P2    NOA 
  15 6?8L          MHUNTW   2,FILE,FCB          [ADDED TO FCB
   8 6?N=          BPZ   2  YFCB 
  21 6#7W          CALL  2  XGETFCB             [IF THERE IS AN FCB IN ACT CHAIN, OUR
  20 6#MG          LDX   0  FUSEBL(3)           [FCB WAS NOT USED, THUS OUR B.S. 
  21 6*76          SBN   0  FBLKS-A1            [SHOULD NOT BE USED. IF FCB IN FILE  
  20 6*LQ ...      BZE   0  UFB                 [CHAIN HAS B.S. ALREADY THEN OUR 
  18 6B6B    YFCB                               [B.S. SHOULD BE FREED.   
   7 6BL2          MFREEBAX  
   8 6C5L          BRN      NOF  
   4 6CK=    [   
  12 6D4W          MENDAREA GAPOPEN,K99ORELEMPT  
   4 6DJG    #END
   8 ____ ...30624162000400000000
