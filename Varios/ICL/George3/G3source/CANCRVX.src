  13 22FL ...  SEG  CANCRVX,860,C.R.JOHNSON,SCHEDULER
   4 22_= ...[   
  18 23DW ...[     THIS SEGMENT IMPLEMENTS THE CANCEL RETRIEVE COMMAND   
   4 23YG ...[   
  18 24D6 ...[     THE FIRST PARAMETER IS A FILE DESCRIPTION; THE FILE'S 
  20 24XQ ...[     DIRECTORY IS OPENED TO OBTAIN THE FILE AND INCREMENT NUMBER   
  21 25CB ...[     AND A SEARCH IS MADE DOWN THE OFF-LINE CHAIN FOR AN APPROPRIATE   
  20 25X2 ...[     FREQ BLOCK. IF FOUND THE FREQ BLOCK IS FREED UNLESS SOMEONE   
  20 26BL ...[     ELSE REQUIRES THE FILE. IF THERE IS NO FREQ BLOCK WE DO NOT   
  20 26W= ...[     KNOW WHAT HAS HAPPENED SO A MESSAGE IS ISSUED GIVING THE STATE
  11 27*W ...[     OF THE FILE AT PRESENT.   
   4 27TG ...[   
  12 28*6          SEGENTRY K1CANCRVX,X1CANCRVX  
   4 29S2    [   
   7 29_8 ...      FSHSKIP  B
   4 2=6B ...(   
   9 2=?L    XT11           #76777777
  15 2=R=    XJNOPAR        +JNOPAR             [NO PARAMS   
   9 2?=W    XAMISS         +AMISS   
   9 2?QG    XJUSE          +JUSNA   
  10 2#=6    XERENT         +ERENTTYPE   
   9 2#PQ    XJLOC          +JLOCNA  
   4 2*9B    [   
  11 2*P2    [     MONITORING FILE MESSAGES  
   4 2B8L    [   
  18 2BN=    MON1           +ALRONL             [%A IS ALREADY ON-LINE   
  17 2C7W    MON2           +ANOTNL             [%A IS NOT ON-LINE   
   4 2CMG    [   
  21 2CX# ...[   ******************************************************************  
   4 2D76    [   
  17 2DLQ    XLOCAL                             [USER NAME MISSING   
   9 2F6B          LDN   1  XAMISS   
   8 2FL2          LDN   2  XJUSE
   8 2G5L          CALL  7  ZERR2
   9 2G*D ...      MHUNT 3,CPB,CUNI  
   9 2GK=          BRN      NULLPAR  
  18 2H4W    XUSE  LDN   1  XAMISS              [ONLY USERNAME PRESENT   
   8 2HJG          LDN   2  XJLOC
   8 2J46          CALL  7  ZERR2
   8 2JHQ          BRN      XINCV
   4 2JMM ...[   
  21 2JRJ ...[   ******************************************************************  
   4 2JXF ...[   
  12 2K3B    [     SEND TWO-PARAM ERROR MESSAGE  
   4 2KH2    [   
   8 2L2L    ZERR2 SMO      FX1  
   8 2LG=          LDX   1  0(1) 
   8 2L_W          SMO      FX1  
   8 2MFG          LDX   2  0(2) 
   8 2M_6          SBX   7  FX1  
   8 2NDQ          ERRORX   1,2  
   8 2NYB          ADX   7  FX1  
   7 2PD2          EXIT  7  0
  17 2PXL    XENTTYPE                           [ENTRANT TYPE INVALID
   9 2QC=          LDN   1  XERENT   
   4 2QJD ...[   
  21 2QPL ...[   ******************************************************************  
   4 2QWW    [   
  16 2RBG    [            SEND OUT ERROR MESSAGE WITH ONE PARAM  
   4 2RW6    [   
   8 2S*Q    XERROR SMO     FX1  
   8 2STB          LDX   1  0(1) 
   7 2T*2          ERRORX   1
  18 2TSL          BRN      XINCV               [J TO PICK UP NEXT PAR   
   4 2TWR ...UP  
  10 2TYY ...      FSHCODE  A,XFSHATRAN  
   4 2W35 ...(   
  20 2W5= ...      TRANCHKN FSHCANCRVX,EQUAL,XFSHATRAN,XFSHATRAN [J IF FROM M/C B
  17 2W7C ...      TRANSFIN                     [RETURN TO MACHINE B 
   6 2W9J ...XFSHATRAN   
   4 2W?P ...)   
   6 2W*W ...      ENDCOM
   4 2WD7 ...[   
  21 2WJ4 ...[   ******************************************************************  
   4 2WM_ ...[   
  20 2WRW    [     THIS SUBROUTINE WILL REMOVE A SPECIFIC JOB NO FROM AFREQ BLOCK
  11 2X?G    [           AND ALTER ITS LENGTH
   4 2XH# ...[   
  12 2XR6    [              ENTRY   X2=FREQ BLOCK
   4 2XW= ...[   
  19 2X_B ...[              THE SUBROUTINE MUST NOT CORRUPT ACOMMUNE1,2,OR 3.
   4 2Y4G ...[   
   4 2Y7L ...[   
   5 2Y=Q    REMJOB  
  16 2YQB          SBX   7  FX1                 [RELATIVISE LINK 
  18 2_=2          STO   2  4                   [PRESERVE POINTER TO FREQ
  16 2_PL          LDX   5  FREVCT(2)           [X5=NO OF JOBS   
  16 329=          BZE   5  XEXIT2              [J IF NO JOBNOS  
   4 32NW    RET1
  16 338G          BXE   6  FREVCT+1(2),REQ1    [LOOP ROUND FOR  
  16 33N6          ADN   2  1                   [EQUAL JOB NOS   
   8 347Q          BCT   5  RET1 
  18 34MB    XEXIT2                             [UNABLE TO FIND JOB NO   
   8 3572          ADX   7  FX1  
  18 35LL          EXIT  7  0                   [EXIT IF JOB NO NOT FOUND
   4 366=    REQ1
   7 36KW          SBN   5  1
  18 375G          BZE   5  NOMOVE              [J IF POS ON LAST JOB NO 
  10 37K6          LDN   1  FREVCT+2(2)  
   9 384Q          ADN   2  FREVCT+1 
   7 38JB          SMO      5
   7 3942          MOVE  1  0
   5 39HL    NOMOVE  
  17 3=3=          LDX   2  4                   [REPLACE THE POINTER 
   7 3=GW          LDN   1  1
   9 3?2G          SBS   1  FREVCT(2)
  17 3?G6          SBS   1  ALOGLEN(2)          [REDUCE LOG LENGTH   
  10 3?_Q          LDX   3  ALOGLEN(2)   
   7 3#FB          ALTLEN 4,3
   8 3#_2          ADX   7  FX1  
  17 3*DL          EXIT  7  1                   [EXIT IF JOB NO FOUND
   4 3*KS ...[   
  21 3*R2 ...[   ******************************************************************  
   4 3*Y=    [   
  20 3BCW    [              S.R. TO DISCOVER PRESENCE OR ABSENCE OF REQUEST BLOCK
  16 3BXG    [                                  IN OFF LINE CHAIN
   4 3CC6    [   
   5 3CWQ    VSEARCH 
  16 3DBB          LDN   3  0                   [SET 'VARIABLE'  
   8 3DW2          BRN      VS   
   4 3F5S ...[   
  21 3F*L    [     SUBROUTINE TO SEARCH OFF LINE CHAIN FOR A REQUEST BLOCK SPECIFIED 
  18 3FM2 ...[     BY AN INCREMENT NUMBER IN X4, FILE NUMBER IN X5, AND  
  11 3FYB ...[     FILESTORE NUMBER IN X6.   
  20 3G9Q ...[     THE CHAIN IS SORTED ON INCREMENT NUMBER, FILE NUMBER AND THEN 
   9 3GH6 ...[     FILESTORE NUMBER. 
  21 3GSG    [     IF THE REQUEST BLOCK IS NOT FOUND X2 POINTS TO THE NEXT BLOCK IN  
  15 3H#6    [     THE CHAIN                    EXIT  7  1   
  19 3HLH ...[     IF THE REQUEST BLCK IS FOUND X2 POINTS TO IT  EXIT  7  0  
   4 3HYY ...[   
   5 3J?B    SEARCH  
  16 3JR2          LDN   3  1                   [SET MANDATORY   
   8 3K=L    VS    LDX   2  BOLC 
   8 3KQ=          LDN   0  BOLC 
   5 3L9W    M1SFOLC 
   8 3LPG          LDX   2  0(2) 
  17 3M7B ...      BXE   2  0,XIT1               [J IF END OF CHAIN  
  19 3MK= ...      BXL   4  FDTINCNUM(2),XIT1    [J IF INCREMENT NOT THERE   
  20 3N36 ...      BXU   4  FDTINCNUM(2),M1SFOLC [J IF INCREMENT NOT YET REACHED 
  19 3NF2 ...      BXL   6  FDTFILSTR(2),XIT1    [J IF FILESTORE NOT THERE   
  20 3NWW ...      BXU   6  FDTFILSTR(2),M1SFOLC [J IF FILESTORE NOT YET REACHED 
  17 3P#Q ...      BXL   5  FDTFILNUM(2),XIT1    [J IF FILE NOT THERE
  19 3PQL ...      BXU   5  FDTFILNUM(2),M1SFOLC [J IF FILE NOT YET REACHED  
   4 3Q8G ...[   
  15 3QLG    [     X2 POINTS TO A REQUEST BLOCK FOR THIS FILE
   4 3QW# ...[   
   7 3R66          EXIT  7  0
   4 3R*Y ...[   
  19 3RKQ    [     THERE IS NO REQUEST BLOCK FOR THIS FILE IN OFF LINE CHAIN 
   4 3RTJ ...[   
  16 3S5B    XIT1  BNZ   3  XGONE               [J IF MANDATORY  
   7 3SK2          EXIT  7  1
  10 3T4L    XGONE GEOERR   1,FILEGONE   
   4 3T9S ...[   
  21 3TC2 ...[   ******************************************************************  
   4 3TJ=    [   
  18 3W3W    [     S.R. TO SET CHAIN ALTERED BITS IN DUMP TAPE ACTIVITIES
   4 3WHG    [   
   4 3X36    [   
  21 3XGQ    [     IN N.I. JUST SET BIT 8 OF FDST FOR EACH FDT ENTRY-MEANS CHAIN ALTE
   4 3Y2B    [   
   8 3YG2    SACT  LDX   2  BOLC 
  14 3Y_L          LDCT  0  1                   [BIT 8   
  17 3_F=          LDX   1  A1(2)               [COUNT OF FDT ENTRIES
   8 3_YW          BZE   1  SACT2
  14 42DG    SET8  ORS   0  FDST(2)             [SET B8  
  15 42Y6          ADN   2  FDTLEN              [NEXT ENTRY  
   8 43CQ          BCT   1  SET8 
   7 43XB    SACT2 EXIT  7  0
   4 43_# ...)   
   4 443? ...[   
  21 4478 ...[   ******************************************************************  
   4 44?5 ...[   
  14 44C2    [     ENTRY 1  FETCH THE ENTRANT DESCRIPTION
   4 44WL    [   
   6 45B=    X1CANCRVX   
  10 45CS ...      FSHCODE  B,XFSHBTRAN  
   4 45FB ...(   
  15 45GY ...#  ALL CANCEL RETRIEVES TO BE DONE IN MACHINE B 
  16 45JG ...      TRANSBEG FSHCANCRVX,CANCRVX,1,CLB,,ACOMMUNE1  
   6 45L4 ...      ENDCOM
   6 45ML ...XFSHBTRAN   
   4 45P8 ...)   
   7 45QQ ...      FSHSKIP  B
   4 45S# ...(   
   9 45TW          STOZ     AWORK1(2)
   4 46*G    [   
   5 46T6    STARTAGN
   7 47#Q          SPARAPAS  
  17 47SB          MHUNT    3,CPB,CUNI          [FETCH THE PARAMETER 
   9 48#2          LDX   0  ANUM(3)  
  18 48RL          BNG   0  NOMORE              [J IF PARAMETER MISSING  
  18 49?=          BNZ   0  NORM                [J IF +VE IE. PAR PRESENT
   5 49QW    NULLPAR 
  18 4==G          FREECORE 3                   [NULL PAR  FREE  CPB/CUNI
   9 4=Q6          BRN      STARTAGN 
   5 4?9Q    NOMORE  
   8 4?PB          SMO      FX2  
   9 4#92          LDX   0  AWORK1   
  19 4#NL          BNZ   0  ZATLONE             [J IF AT LEAST ONE PAR SPEC. 
  18 4*8=          LDN   1  XJNOPAR             [OUTPUT ERROR MESSAGES   
   4 4*BG ...[   
  17 4*JQ ...[     FOR NO PARAM CASE - SIMPLEST TO HAVE SEPARATE S/R 
   4 4*R2 ...[   
   4 4*_= ...[   
   8 4B7G          SMO      FX1  
   8 4BM6          LDX   1  0(1) 
   7 4C6Q          ERRORX   1
   8 4CLB          BRN      UP   
   5 4D62    ZATLONE 
  17 4DKL          FREECORE 3                   [FREE CPB/CUNI BLK   
  16 4F5=          BRN      UP                  [COMMAND FINISHED
   4 4FJW    [   
  20 4G4G    [     MUST FIRST DO SOME WORK FOR FNORM-IT WONT COPE WITH A RELATIVE
  11 4GJ6    [     NAME IN NO USER CONTEXT   
   4 4H3Q    [   
   4 4HHB    NORM
  16 4J32          STO   3  AWORK1(2)           [MAKE AWK1 +VE   
  16 4JGL          LDCH  0  APARA(3)            [FIRST CHARACTER 
  13 4K2=          SBN   0  #12                 [:   
  18 4KFW          BZE   0  PRESCOL             [J IF USER NAME PRESENT  
  21 4K_G          BBUS     23,CONTEXT(2),XLOCAL    [J IF RELATIVE NAME IN NO USER   
  15 4LF6                                           [CONTEXT 
   4 4LYQ    [   
  13 4MDB    [     EITHER COLON OR IN USER CONTEXT   
   4 4MY2    [   
   5 4NCL    PRESCOL 
  10 4NX=          NAMETOP  3,FILE,FNAME 
  18 4PBW          FNORM                        [CONVERT TO A FILE/FABSNB
  10 4PJ4 ...      MHUNT    3,FILE,FNAME 
  19 4PP= ...      NAMETOP  3,CPB,CUNI          [FOR USEROPEN ERROR MESSAGES 
  18 4PWG          TESTREP2  NAMEFORM,XFNO      [ERROR REPORTED BY FNORM 
  10 4QB6          MHUNT    1,FILE,FABSNB
  21 4QTQ          LDN   0  #2400               [CHECK WHETHER TSN OR REEL/RET PRESEN
   9 4R*B          ANDX  0  ATYPE(1) 
  19 4RT2          BNZ   0  XENTTYPE            [J IF ENTRANT TYPE INVALID   
  16 4S#L          BBS      16,ATYPE(1),SHRIEK  [J IF WORKFILE   
  10 4SS=          LDX   0  ALOGLEN(1)   
   7 4T?W          SBN   0  4
  17 4TRG          BZE   0  XUSE                [J IF ONLY USERNAME  
   4 4W?6    [   
  13 4WQQ    [     OPEN THE FILE'S SUPERIOR DIRECTORY
   4 4X=B    [   
  19 4XQ2 ...      USEROPEN XBR,READR,LEAVE,STREAMS,DIR,TERMDIR,UNTRAP,FROZEN
   8 4Y9L          REPERR   OK2  
  17 4YP= ...      BRN      XINCV               [ TO PICK NEXT PAR   
   9 4_8W ...OK2   STOZ     AWORK4(2)
  10 4_NG ...      TREPN    STREAM,OK3   
   8 5286 ...      LDX   0  GSIGN
  18 52MQ ...      STO   0  AWORK4(2)           [REMEMBER TO CLOSE SDF   
   4 53M2    OK3 
  10 546L          MHUNTW   3,FILE,ENT   
   7 54L=          LDN   6  0
  15 555W          BBUS     23,CONTEXT(2),NOUSERACT  [J IF IN
  17 55KG                                       [ NO USER ACTIVITY   
  16 5656          LDX   6  JOBNO(2)            [PICK UP JOB NO  
   6 56JQ    NOUSERACT   
  16 56SJ ...      STO   6  AWORK2(2)           [STORE JOBNUMBER 
  15 574B          LDX   4  EINC(3)             [X4 =INC NO  
  15 57J2          LDX   5  EFILNUM(3)          [X5=FILE NO  
  18 57P8 ...      LF       3,FEFSNO,6          [X6 => FILESTORE NUMBER. 
   4 57WB ...[   
  11 583L    [      SEARCH FOR REQUEST BLKS  
   4 58?D ...[   
  19 58H=          CALL  7  VSEARCH             [SEARCH FOR FREQ BLK;X2=FREQ 
  18 592W          BRN      XFREQP              [J IF REQUEST BLK PRESENT
   4 59*? ...[   
  21 59MN ...[     IF FREQ BLK NOT FOUND (OR JOB NO) THEN IT IS NOT POSSIBLE TO DET. 
  21 5=26    [     EXACTLY WHAT HAPPENED, SO WE MERELY INFORM THE USER OF THE CURRENT
   9 5=FQ    [     STATE OF THE FILE 
   4 5=_B    [   
   5 5?F2    SEREND  
  20 5?YL          SETNCORE 100,3,ADATA,CREADL  [BLOCK FOR ENTRANT DESCRIPTION   
  17 5#D=          STOZ     A1(3)               [NOTHING IN IT YET   
  18 5#XW          UNNORM   TWO                 [CONVERT NAME FROM FABSNB
  11 5*CG          MHUNTW   3,ADATA,CREADL   
  20 5*X6          OUTPARAM A1(3),CPDATA,ADATA,CREADL   [SET UP AS A PARAMETER   
  10 5BBQ          MFREEW   ADATA,CREADL 
  10 5BWB          MHUNTW   3,FILE,ENT   
  18 5CB2          LDX   4  MON1(1)             [%A IS ALREADY ON-LINE   
   9 5CTL          LDX   0  ECOPS(3) 
   8 5D*=          ANDN  0  #7777
  17 5DSW          BNZ   0  ONL                 [BRANCH IF ON-LINE   
  17 5F#G          LDX   4  MON2(1)             [%A IS NOT ON-LINE   
   9 5FS6    ONL   MFREEW   FILE,ENT 
   7 5G?Q ...ONLSH MONOUTX  4
   8 5GRB ...      BRN      XINCF
   5 5H?2    XFREQP  
   8 5HQL          LDX   3  FX2  
  10 5J42 ...      STO   4  ACOMMUNE1(3) 
  10 5J*B ...      STO   5  ACOMMUNE2(3) 
  10 5JLQ ...      STO   6  ACOMMUNE3(3) 
  16 5JY6 ...      LDX   6  AWORK2(3)           [X6 => JOBNUMBER 
   9 5K9G          CALL  7  REMJOB   
  16 5KP6          BRN      SEREND              [JOB NOT PRESENT 
  10 5L4T ...      LDX   4  ACOMMUNE1(2) 
  15 5LDJ ...      LDX   5  ACOMMUNE2(2)        [RESTORE ACCS
  10 5LS? ...      LDX   6  ACOMMUNE3(2) 
  15 5M82          CALL  7  SEARCH              [X2=FREQ BLK 
   9 5MML          LDX   0  FREQCT(2)
  17 5N7=          BNZ   0  XENDCC              [J IF WAITERS PRESENT
   9 5NLW          LDX   0  FREVCT(2)
  17 5P6G          BNZ   0  XENDCC              [J IF JOBS PRESENT   
  17 5PL6          FREECORE 2                   [ FREE REQUEST BLOCK 
  17 5Q5Q          CALL  7  SACT                [CHAIN ALTERED BITS  
  10 5Q9M ...      MHUNTW   3,FILE,ENT   
  20 5Q?K ...      FC    3,FEGMTRV              [CLEAR GMTNOW FIELD IN FILE ENT  
  10 5Q*H ...      NAME     3,FILE,FWB   
  17 5QCF ...      REWRITE                      [UPDATE NAME RECORD  
   9 5QFC ...      MFREEW   FILE,FWB 
   8 5QHB ...      BRN      XINCF
   5 5QKB    XENDCC  
   9 5R52          MFREEW   FILE,ENT 
   5 5RB5 ...XINCF   
  16 5RM9 ...      CLOSETOP                     [CLOSE DIRECTORY 
   9 5RPS ...      LDX   0  AWORK4(2)
  16 5RSC ...      BPZ   0  XINCV               [CHECK FOR SDF   
   8 5RX2 ...      CLOSESTREAM   
   5 5R_L ...XINCV   
  10 5S4=          MFREE    FILE,FABSNB  
   4 5SHW    XFNO
   7 5T3G ...      UNIFREE   
   9 5TH6          BRN      STARTAGN 
   4 5W2Q    [   
   8 5WGB    [     CANCEL RV !   
   4 5X22    [   
   5 5XFL    SHRIEK  
   8 5X_=          LDX   2  FX2  
  14 5YDW          LDCT  0  #210                [! CHAR  
  10 5YYG          STO   0  ACOMMUNE1(2) 
   7 5_D6          LDN   0  1
  10 5_XQ          OUTPARAM 0,ACOMMUNE1  
   9 62CB          LDX   4  MON1(1)  
   7 62PR ...      MONOUTX 4 
   8 6348 ...      BRN      XINCV
   7 63BL    XBR   COMBRKIN  
  11 63W=          MENDAREA 100,K99CANCRVX   
   4 6464 ...)   
   4 64*W    #END
   2 64TG
   8 ____ ...15400262000200000000
