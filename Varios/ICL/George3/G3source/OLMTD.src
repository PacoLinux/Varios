  11 22FL    #LIS  K0OLMTD>K0LIBRARY>K0ALLGEO
  15 22_=    #SEG           OLMTD65             [N.R.BOULT   
   9 23DW                   8HOLMTD65
  11 23YG          SEGENTRY K1OLMTD,QK1OLMTD 
  11 24D6          SEGENTRY K2OLMTD,QK2OLMTD 
  11 24XQ          SEGENTRY K4OLMTD,QK4OLMTD 
  17 25CB    [THIS SEGMENT DEALS WITH THE FIRST PERI ON AN ONLINED   
   9 25X2    [MAGNETIC TAPE CHANNEL. 
   9 26BL    TL1   PAIR     OLMTD,4  
   9 26W=    TL2   PAIR     OLMTD,2  
   4 27*W    TPRB
  11 27TG    #HAL           APERI+APERIRES,0 
   4 28*6    TOLP
   9 28SQ    #HAL           AOLPT,0  
   9 29#B    MASKMON        #77077   
   9 29S2    OWNMON         #34000   
   8 2=3S ...OWNMONA        #7777
   8 2=?L    RENAME         #500 
   8 2=R=    SCRATCH        #600 
   9 2?=W    XB             #77777000
   8 2?QG    XADR           +5   
   8 2#=6    ONE            +1   
   8 2#PQ    X9             +9   
   8 2*9B    WTM            +5   
   8 2*P2    RASECON        261:8
   4 2B8L    #   
   4 2BN=    #   
  13 2C7W    [SUBROUTINE TO SET ADDRESS OF PCA IN X7 
   5 2CMG    SPCAOLP 
   8 2D76          LDX   2  FX2  
   9 2DLQ          ADN   2  ACTRING  
   4 2F6B    SPC1
  16 2FL2          LDX   2  1(2)                [BACKWARD POINTER
   7 2G5L          LDX   7  2
   9 2GK=          SBN   7  ACTRING  
   8 2H4W          LDCT  1  #770 
   7 2HJG          SMO      7
   8 2J46          ANDX  1  ATYPE
   8 2JHQ          SMO      FX1  
  10 2K3B          BXU   1  PCATYPE,SPC1 
   8 2KH2          LDX   1  FX1  
   8 2L2L          LDX   2  FX2  
   7 2LG=          EXIT  0  0
   5 2L_W    PCATYPE 
   7 2MFG    #HAL  +APET,0   
  20 2M_6    [THIS SUBROUTINE SEARCHES THE ACTIVITY CHAIN UNTIL AN AOLPT ACTIVITY
  21 2NDQ    [BLOCK IS FOUND. IF A NON ACTIVITY BLOCK IS FOUND A GEOERR XBLK IS GIVEN
   5 2NYB    SBLOC   
   9 2PD2          LDX   3  FPTR(3)  
   9 2PXL          LDX   6  ATYPE(3) 
   9 2QC=          TXU   6  TOLP(1)  
   8 2QWW          BCS      SB1  
   8 2RBG          BCT   5  SBLOC
   7 2RW6          EXIT  0  0
   9 2S*Q    SB1   BXGE  6  CACT,OERR
   8 2STB          BRN      SBLOC
   4 2T*2    #   
   4 2TSL    #   
  21 2W#=    [THIS SUBROUTINE SEARCHES THE ACTIVITY CHAIN UNTIL A PRB (APERI/APERIRES
  21 2WRW    [BLOCK IS FOUND. IF AN ACTIVITY BLOCK IS FOUND A GEOERR XPRB IS GIVEN   
   4 2X?G    SPRB
   9 2XR6          LDX   3  FPTR(3)  
   9 2Y=Q          LDX   6  ATYPE(3) 
  10 2YQB          BXGE  6  CACT,OERR2   
   8 2_=2          SMO      FX1  
   9 2_PL          BXU   6  TPRB,SPRB
   7 329=          EXIT  0  0
   5 32NW    #PAGE   
   4 338G    #   
  20 33N6    #                                                          K1OLMTB  
   4 347Q    #   
  21 34MB    [ENTRY POINT FROM EXEC,WHEN THE FIRST PERI TO THE TAPE IS ENCOUNTERED   
  13 3572    [AFTER AN ONLINE COMMAND HAS BEEN GIVEN.
  21 35LL    [FOR AN OPEN PERI  1)THE REPLY FROM THE ONLINE COMMAND IS PASSED ACROSS 
   8 366=    [TO THE USER PROGRAM
  15 36KW    [                  2)THE PROGRAM IS UNPLUGGED   
  21 375G    [                  3)THE ONLINE BIT OF THE PRB ENTRY IS SET (I.E.B14)   
  20 37K6    [                  4)THE ALOPT ACTIVITY BLOCK AND ITS DATA BLOCK ARE
   5 384Q    [FREED  
  11 38JB    [PERI OTHER THAN AN OPEN PERI.  
  20 3942    [                  1)STEPS BACK THE PROGRAM TO REPEAT THE PERI, THEN
  13 39HL    [FOLLOWS STEPS 2),3) AND 4) OF ABOVE.   
   5 3=3=    QK1OLMTD
  18 3=GW          CALL  0  SPCAOLP             [SET X7 = ADDRESS OF PCA 
   9 3?2G          LDX   6  EVENT4(2)
  10 3?G6          ANDX  6  MASKMON(1)   
  15 3?_Q          BZE   6  PCM                 [OK IF ZERO  
  16 3#FB          TXU   6  OWNMON(1)           [OWN-MONITORING ?
   8 3#_2          BCC      PCM  
   9 3*DL          LDX   0  AWORK3(2)
  13 3*Y=          BZE   0  PCM                 [NO  
  18 3B84 ...      ANDX  6  OWNMONA(1)          [IGNORE OWN MONITORING   
  15 3BCW          TXU   6  ONE(1)              [WRITE PERI? 
   8 3BXG          BCC      RASE 
   9 3CC6          TXU   6  WTM(1)   
   8 3CWQ          BCS      PCM  
  10 3DBB    RASE  LDX   0  RASECON(1)   
  10 3DW2          DCA      ,STO,0,MODE  
   9 3F5S ...      SEGENTRY K8OLMTD  
   8 3F*L          LDN   0  #3777
  10 3FT=          DCA      ,STO,0,COUNT 
   8 3G#W          JWPR     ,WPR 
   8 3GSG          BRN      PSB  
   8 3H#6    WPR   GPERI    ,2   
   6 3HRQ          COOR1 
   8 3J?B          BRN      PSB  
   8 3JR2    PCM   LDN   6  #700 
   9 3K=L          ANDX  6  EVENT4(2)
  16 3KQ=          BZE   6  PSB                 [EXCLUDE MODE 0  
  16 3L9W          TXU   6  RENAME(1)           [AND MODE #500   
   8 3LPG          BCC      PSB  
  16 3M96          CHECKEXT EVENT2(2),X9(1),RESF,7,W,PGR,HLOCK1  
   8 3MNQ          BRN      PGR0 
  18 3N8B    PGR   CALL  0  SPCAOLP             [SET X7 = ADDRESS OF PCA 
  10 3NN2    PGR0  MHUNT    3,FLIB,GOLMT 
   9 3P7L          LDX   0  AWORK3(2)
  15 3PM=          BNZ   0  OMS                 [NONSTANDARD 
   9 3Q6W          LDX   0  EVENT4(2)
   8 3QLG          ANDX  0  GSIGN
  15 3R66          ORS   0  A1+2(3)             [REPLACER BIT
   9 3RKQ          LDN   6  A1+2(3)  
  10 3S5B          LDX   3  ALOGLEN(3)   
   7 3SK2          SBN   3  2
   9 3T4L          LDX   0  EVENT4(2)
   8 3TJ=          ANDN  0  #700 
  11 3W3W          BXU   0  SCRATCH(1),PIR   
   7 3WHG          LDN   3  2
   4 3X36    PIR 
  18 3XGQ          INMOVE   7,EVENT2(2),6,3     [INSERT REPLY INFORMATION
  18 3Y2B          LDN   6  0                   [DO UNPLUG; NOT UNPLUG 1 
   4 3YG2    OM  
   9 3Y_L          TOPCA    TL1(1)   
   6 3_F=          COOR1 
   9 3_YW          BRN      QK1OLMTD 
   4 42DG    #   
   7 42Y6    OMS   OLPFIN   7
   9 43CQ          BRN      QK1OLMTD 
  20 43XB    #                                                          K16OLMTB 
   4 44C2    #   
   5 44WL    QK4OLMTD
   8 45B=          BNZ   6  PU1  
   6 45TW          UNPLUG
   8 46*G          BRN      PU   
   7 46T6    PU1   UNPLUG   1
   4 47#Q    PU  
  17 47SB          CAPCA                        [CHANGE TO CPA AND   
   8 48#2          FPCACA   3,FX2
  17 48RL          CALL  0  SPRB                [X3=ADDRESS OF PRB   
   8 49?=          ADN   3  A1   
  15 49QW          FPERENT  4,3,2,5             [FIND ENTRY  
  16 4==G          BNG   2  OERR2               [ERROR NOT THERE 
   8 4=Q6          SBN   3  A1   
  17 4?9Q          CALL  0  SBLOC               [X3=ADDRESS OF AOLPT 
   9 4?PB          LDX   0  ACOUNT(3)
  17 4#92          SMO      CPPTR(3)            [TO BE REPLACED BY A 
  17 4#NL ...      ADS   0  JK52H               [MACRO: SETOBJPERIS  
   9 4*8=          LDX   0  CPPTR(3) 
   8 4*MW          STO   0  1(2) 
   7 4B7G          LOCK     3
  15 4BM6          MURDER   3                   [KILL AOLPT  
  14 4C6Q          RUNPROG                      [AND RUN 
  18 4CLB    PSB   STEPBAC1 ,7                  [STEP BACK OBJECT PROGRAM
  10 4D62          MHUNT    3,FLIB,GOLMT 
  15 4DKL          LDN   6  1                   [DO UNPLUG 1 
  17 4F5=          BRN      OM                  [GO TO REMOVE AOLPT  
  20 4FJW    RESF                               [REPORT IF CONTROL AREA OUT OF   
   9 4G4G          TOPCA    TL2(1)   
   6 4GJ6          COOR1 
   9 4H3Q          BRN      QK1OLMTD 
   4 4HHB    #   
  20 4J32    #                                              K12OLMTB & K14OLMTB  
   4 4JGL    #   
  14 4K2=    [ENTRY POINT FOR REPORTING ILLEGAL FROM PCA 
   5 4KFW    QK2OLMTD
   8 4K_G          ILLEGAL  NOCA 
   4 4LF6    #   
   4 4LYQ    #   
   4 4MDB    #   
   4 4MY2    #   
   7 4NCL    [GEORGE ERRORS  
   9 4NX=    OERR  GEOERR   1,XBLK   
   9 4PBW    OERR2 GEOERR   1,XPRB   
  10 4PWG          MENDAREA 50,K99OLMTD  
   4 4QB6    #END
   8 ____ ...05627620000400000000
