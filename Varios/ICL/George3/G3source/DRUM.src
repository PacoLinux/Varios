  13 22FL ...#SEG  DRUM852            [FRANK BLUNDELL
   6 22_=    #DEF  XA1=A1
   6 23DW    #LIS  K0DRUM
   6 23YG          8HDRUM
   4 24D6    #   
  11 24XQ          SEGENTRY K2DRUM,QK2DRUM   
  11 25CB          SEGENTRY K3DRUM,QK3DRUM   
  11 25X2          SEGENTRY K4DRUM,QK4DRUM   
   4 26BL    #   
   7 26W=    #     PROPERTY  
  17 27*W    #     STORED IN A CPAR/GPERCON BLOCK.PARAMETER 2 IS THE 
  18 27TG    #     ON ENTRY,PARAMETER 1 HAS BEEN ANALYSED AND THE RESULT 
  18 28*6    #     THIS SEGMENT DEALS WITH THE ONLINE COMMAND FOR DRUM.  
   9 28SQ    XER GEOERR 1,TYPWRONG   
   8 29#B    XLINK PAIR DRUM,2   
   8 29J8 ...XGHSD      4HGHSD   
   4 29S2    #   
   7 2=?L    XCOM  COMERRX  3
   7 2=R=    XBRKIN COMBRKIN 
  15 2?=W    XWRNG +ERWRONGED                   [UNIT WRONGED
  16 2?QG    XDALA +JDALLA                      [DRUM ALLOCATED  
  17 2#=6    XDOSN +JDOESN                      [DRUM DOES NOT EXIST 
  19 2#PQ    XPER1 +JPERR1                      [CONSOLE PROPERTY SPECIFIED  
  20 2*9B    XPER2 +JPERR2                      [TOO MANY PROPERTIES SPECIFIED   
  17 2*P2    XPERX +JPERMX                      [PROP NOT ATTRIBUTED 
  17 2B8L    XPROP +JPROPM                      [PROP PARAM MISSING  
  17 2BN= ...XPRNA +JPROPSNA1                   [PROPERTY UNAVAILABLE
  16 2C7W    XPRUN +JPROPUNK                    [PROPERTY UNKNOWN
  18 2CMG    XCNT  +JYCOUNT                     [ILL:COUNT OUT OF RANGE  
  18 2D76    XMODE +JYMODE                      [ILLEGAL:MODE IN ERROR   
  18 2DLQ    XNOBF +JYNOBUF                     [ILL:BUFFER OUT OF RES   
  19 2F6B    XNOCA +JYNOCA                      [ILL:C.A.OUT OF RESERVATIONS 
  20 2FL2    XRGE  +JYRANGE                     [ILL:DEVICE ADDRESS OUT OF RANGE 
   4 2G5L    #   
   5 2GK=    QK4DRUM 
   8 2H4W    #SKI  K6DRUM>99-99  
  10 2HJG          TRACE    1,DRUM ONL   
  11 2J46          MHUNTW   1,CPAR,GPERCON   
   9 2JHQ          LDX   0  XA1(1)   
   7 2K3B          STO   0  7
   8 2KH2          SRL   0  15   
  16 2L2L          SBN   0  9                   [TEST FOR DRUM   
  17 2LG=          BNZ   0  XER                 [J TO GEOERR IF NOT  
  15 2L_W          FREECORE 1                   [FREE GPERCON
   9 2MFG          LDX   3  XPROP(1) 
   9 2M_6          PROPERTY XCOM,1   
  16 2NDQ          LDCT  4  #200                [ONLINE BIT SET  
   7 2NYB          ADN   4  9
   7 2PD2          STOZ     6
   9 2PXL          STOZ     CPPTR(2) 
  10 2QC=          ISPERG   4,XBRK,NOPR  
  19 2QWW          TESTREP2 OK,OKAY,NOPER,RC,PERMEXCL,RA,NOPROP,RB,NOTAV,RD  
  17 2RBG          TESTREP2 XCPROP,XC,MAXATT,XT,XPROP,XPRO,WRNG,RE   
   7 2RW6    RA    LDN   6  1
   8 2S*Q          BRN      RC   
   7 2STB    RB    LDN   6  2
   8 2T*2          BRN      RC   
   7 2TSL    RD    LDN   6  3
   8 2W#=          BRN      RC   
   7 2WRW    RE    LDN   6  4
   8 2X?G    RC    LDCT  4  16   
   7 2XR6          ADS   4  7
   8 2Y=Q          LDCT  4  #310 
  10 2YQB          GETPER7  4,XBRK,NOPR  
  17 2_=2          TESTREP2 OK,OKAY,NOPER,RJ,PERMEXCL,RK,NOPROP,RF   
  11 2_PL          TESTREP2 NOTAV,RG,WRNG,RH 
   8 329=    RJ    BZE   6  NOPER
   7 32NW          SBN   6  1
   8 338G          BZE   6  PEXC 
   7 33N6          SBN   6  1
  21 347Q          BZE   6  NOPROP              [DEVICE OF SPECIFIED TYPE EXISTS BUT 
  20 34MB          SBN   6  1                   [DOES NOT HAVE THE REQ.PERM.PROP.
   8 3572          BZE   6  NOTAV
   8 35LL          BRN      WRNG 
   8 366=    RK    BZE   6  PEXC 
   7 36KW          SBN   6  1
   8 375G          BZE   6  PEXC 
   7 37K6          SBN   6  1
   9 384Q          BZE   6  NOPROP   
   8 38JB          BRN      PEXC 
   9 3942    RF    BRN      NOPROP   
   8 39HL    RG    BZE   6  NOTAV
   7 3=3=          SBN   6  1
   8 3=GW          BZE   6  PEXC 
   7 3?2G          SBN   6  1
   9 3?G6          BZE   6  NOPROP   
   7 3?_Q          SBN   6  1
   8 3#FB          BZE   6  NOTAV
   8 3#_2          BRN      WRNG 
   8 3*DL    RH    BZE   6  WRNG 
   7 3*Y=          SBN   6  1
   8 3BCW          BZE   6  PEXC 
   7 3BXG          SBN   6  1
   9 3CC6          BZE   6  NOPROP   
   8 3CWQ          BRN      WRNG 
  17 3DBB    NOPER LDX   3  XDOSN(1)            [DRUM DOES NOT EXIST 
   8 3DW2          BRN      XCOM 
   9 3F*L    PEXC  LDX   3  XPERX(1) 
   8 3FT=          BRN      XCOM 
   9 3G#W    XT    LDX   3  XPER2(1) 
   8 3GSG          BRN      XCOM 
   9 3H#6    XC    LDX   3  XPER1(1) 
   8 3HRQ          BRN      XCOM 
   9 3J?B    XBRK  LDX   3  CPPTR(2) 
   9 3JR2          BZE   3  XBRKIN   
   7 3K=L          FREEPER   
   9 3KQ=          BRN      XBRKIN   
   9 3L9W    NOPR  LDX   3  CPPTR(2) 
   9 3LPG          BZE   3  NOPROP   
   7 3M96          FREEPER   
   5 3MNQ    NOPROP  
   9 3N8B          LDX   3  XPRNA(1) 
   8 3NN2          BRN      XCOM 
  11 3P7L    XPRO  MHUNTW   1,APERI,APROPS   
  10 3PM=          MHUNT    3,CPB,CUNI   
  10 3Q6W          ADX   1  ACOMMUNE1(2) 
   9 3QLG          LDN   2  ANUM(3)  
  17 3R66          LDX   5  0(1)                [NO OF CHARS IN PROP 
  16 3RKQ          ADN   5  4                   [ADD 4 FOR COUNT 
   7 3S5B          SMO      5
   7 3SK2          MVCH  1  0
   8 3T4L          SMO      FX1  
   8 3TJ=          LDX   3  XPRUN
   8 3W3W          BRN      XCOM 
   9 3WHG    NOTAV LDX   3  XDALA(1) 
   8 3X36          BRN      XCOM 
   7 3XGQ    WRNG  LDN   0  0
   9 3Y2B          OUTPARAM 0,APARA  
   9 3YG2          LDX   3  XWRNG(1) 
   8 3Y_L          BRN      XCOM 
   7 3_F=    OKAY  SETALLOC  
   6 3_YW          PERON 
   8 42DG          LGEOG    ,5   
  18 42Y6          LDN   0  4                   [NUMBER OF PARAMETERS FOR
  17 43CQ          OUTBLOCK 0                   [SETTING UP GMON/ASET
   8 43XB          OUTNUM   5,0  
  10 44C2          OUTNUM   JOBNO(2),0   
  18 44WL          INFORM   1,JDONL,1           [MESSAGE:'DRUM ONLINE'   
  17 45B=          LDCT  4  AOLPT/8             [LOAD TYPE OF BLOCK  
  16 45TW          LDN   5  EXAOLPT             [LENGTH REQUIRED 
  15 46*G          GETACTX  4,5,1               [AOLPT SET UP
  15 46T6          LDX   1  BPTR(2)             [PTR TO AOLPT
   9 47#Q          LDX   6  CPPTR(2) 
  17 47SB          STO   6  CPPTR(1)            [STORE ADDR OF D.L.  
   8 48#2          LDCT  6  #400 
  16 48RL          STO   6  FRECHADD(1)         [SET UP FRECHADD 
   9 49?=          LDX   6  JOBNO(2) 
  15 49QW          STO   6  JOBNO(1)            [JOB NUMBER  
   7 4==G          LDX   6  7
  16 4=Q6          ANDN  6  #7777               [OBTAIN UNIT NO. 
   9 4?9Q          STO   6  EWCHAN(1)
   7 4?PB          LDN   6  8
   9 4#92          STO   6  ATMARK(1)
   9 4#NL          STOZ     GIVE(1)  
   9 4*8=          STOZ     AWORK1(1)
   8 4*MW          LDX   3  FX1  
  10 4B7G          LINKSET  1,XLINK(3)   
  20 4BM6          LDCT  6  #400                [INDICATES PERIPHERAL IS OFFLINE 
  20 4C6Q          PUTPERENT 7,6                [ENTRY IN PERIPHERAL RES BLOCK   
  18 4CLB          ROLPA                        [CHAIN AOLPT IN PCA CHAIN
  10 4D62          MHUNT    3,CPB,CUNI   
  15 4DKL          FREECORE 3                   [FREE CUNI   
  17 4F5=          ENDCOM                       [ACROSS TO COMPROB   
   4 4FJW    #   
  13 4G4G    #      ENTRY POINT FOR DRUM TRANSFERS   
   4 4GJ6    #   
   5 4H3Q    QK2DRUM 
   8 4HHB    #SKI  K6DRUM>199-199
  10 4J32          TRACE    1,DRUMPERI   
  10 4JGL          LDX   0  APETADDR(2)  
  18 4K2=          LDX   5  EVENT2(2)           [OBJ PROG ADDR OF C.A.   
   8 4KFW          LOCKCA   0,5  
  16 4K_G          LDN   4  5                   [LENGTH OF C.A.  
   9 4LF6          LDX   3  XNOCA(1) 
  15 4LYQ          CHECKEXT 5,4,STP,APETADDR(2),W,SA,HLOCK1  
   4 4MDB    SA  
   8 4MY2    #SKI  K6DRUM>199-199
   4 4NCL    (   
  18 4NX=          LDX   3  EVENT4(2)           [LOAD 1ST WORD OF C.A.   
   8 4PBW          SRL   3  15   
   8 4PLN ...      ANDN  3  #377 
  16 4PWG          SBN   3  9                   [TEST FOR DRUM   
   8 4QB6          BZE   3  SB   
  16 4QTQ          SBN   3  16                  [TEST FOR HS DRUM
  17 4R*B          BNZ   3  XER                 [XER=GEOERR TYPWRONG 
  17 4RT2    SB    LDX   3  EVENT3(2)           [OBTAIN CHANNEL NO   
   9 4S#L          LDX   0  EWCHAN(2)
  18 4SS=          SBX   3  0                   [CHECK CHAN NO. CORRECT  
   8 4T?W          BZE   3  SC   
  10 4TRG          GEOERR   1,CHANRONG   
   4 4W?6    SC  
   4 4WQQ    )   
  16 4X=B          LDX   0  EVENT4(2)           [LOAD TYPE/MODE  
  15 4XQ2          ANDN  0  #7777               [ISOLATE MODE
  15 4Y9L          BZE   0  MODE                [J IF READ   
   7 4YP=          SBN   0  1
  15 4_8W          BZE   0  MODE                [J IF WRITE  
   8 4_NG          SBN   0  #577 
  18 5286          BZE   0  MODE6               [J IF WORKFILE OPEN MODE 
   8 52MQ          SBN   0  #200 
  17 537B          BZE   0  MODEX               [J IF CLOSE FILE MODE
  18 53M2    SLD   LDX   3  XMODE(1)            [LOAD MESSAGE IDENTIFIER 
   9 546L    STP   TOPCA1   XLINK(1) 
   6 54L=          UNPLUG
   7 555W          ILLEGALX 3
   4 55KG    #   
  11 5656    #     ROUTINE FOR MODES 0 AND 1 
   4 56JQ    #   
  17 574B    MODE  LDCT  4  #400                [CHECK PERI OPEN HAS 
  18 57J2          ANDX  4  GIVE(2)             [ALREADY BEEN RECEIVED   
  14 583L          BZE   4  SLD                 [J IF NOT
  10 58H=          LDX   3  APETADDR(2)  
  16 592W          LDX   4  EVENT2(2)           [ADDR OF AREA TO 
  14 59GG          ADN   4  2                   [BE MOVED
  21 5=26          LDN   5  AWORK2(2)           [AREA INTO WHICH C.A. WILL BE MOVED  
  16 5=FQ          LDN   6  3                   [LENGTH OF C.A.  
  19 5=_B          FOUTMOVE 3,4,5,6,HLOCK1      [MOVE C.A. INTO AWORK WORDS  
  20 5?F2          PMODES   4,3                 [OBTAIN BRANCH AND STORE MODES   
  19 5?YL          ANDN  4  #10                 [SEE IF EXTENDED STORE MODE  
  16 5#D=          BNZ   4  TA                  [AND J IF IT IS  
  16 5#XW          LDX   5  BSP16               [MASK FOR 15AM   
   8 5*CG          BRN      TB   
  16 5*X6    TA    LDX   5  BITS22LS            [MASK FOR 22AM   
  18 5BBQ    TB    ANDX  5  AWORK3(2)           [MASK IN BUFFER ADDRESS  
  17 5BWB          LDX   3  XNOBF(1)            [MESSAGE IDENTIFIER  
  15 5CB2          CHECKA   5,AWORK2(2),STP,APETADDR(2),W,TC 
  10 5CTL    TC    LDX   3  APETADDR(2)  
   5 5D*=    #SKI  G3
  16 5DSW          FADDRESS 3,5,HLOCK2          [DATUMISE ADDRESS
  18 5F#G          DCA      ,STO,5,CTSA         [STORE BUFF ADDR IN D.L. 
  15 5FS6          LDEX  0  EVENT4(2)           [LOAD MODE   
  17 5G?Q          DCA      ,DLA,0,MODE         [STORE MODE IN D.L.  
   5 5GRB    #SKI  G4
   4 5H?2    (   
  10 5HQL          SMO      APETADDR(2)  
   9 5J==          LDX   0  ASEGTAB  
  10 5JPW          DCA      ,STO,0,SEGTAB
   8 5K9G          LDX   0  BIT9 
  10 5KP6          DCA      ,ORS,0,MODE  
   4 5L8Q    )   
  15 5LNB          LDX   0  AWORK2(2)           [LOAD COUNT  
  16 5M82          LDX   3  XCNT(1)             [MESS IDENTIFIER 
   8 5MML          BZE   0  STP  
   8 5N7=          LDX   4  BSP16
  17 5NLW          TXL   4  0                   [IS COUNT < #77777   
  14 5P6G          BCS      STP                 [J IF SO 
  17 5PL6          DCA      ,STO,0,COUNT        [STORE COUNT IN D.L. 
  17 5Q5Q          LDX   0  AWORK4(2)           [LOAD DRUM ADDRESS   
  16 5QKB          LDX   3  XRGE(1)             [MESS IDENTIFIER 
   9 5QMK ...      LDX   1  EVENT4(2)
  18 5QPS ...      SRL   1  15                    [  TYPE.  -- 9 OR 25   
  21 5QS3 ...      SBN   1  9                   [ * FOR *DR, BS ADDRESS IS IN WORDS  
  21 5QW= ...      BZE   1  TYPE9               [ * FOR HSD, BS ADDRESS IS IN BLOCKS 
  19 5QYF ...      SLL   0  7                   [ GET DRUM ADDRESS IN WORDS  
   8 5R2N ...TYPE9 LDX   1  FX1  
  16 5R52          TXL   0  AWORK1(2)           [IS ADDRESS VALID
  14 5RJL          BCC      STP                 [J IF NOT
  15 5SHW          ADX   0  AWORK2(2)           [ADD COUNT   
   7 5T3G          SBN   0  1
  16 5TH6          LDX   3  XCNT(1)             [MESS IDENTIFIER 
  16 5W2Q          TXL   0  AWORK1(2)           [IS ADDRESS VALID
  14 5WGB          BCC      STP                 [J IF NOT
  17 5WMJ ...      LDX   0  AWORK4(2)           [ DRUM  ADDRESS  ....
  18 5WSQ ...      DCA      ,STO,0,DTSA         [ ...  TO DEVICE LIST.   
  15 5X22          LDEX  0  EVENT4(2)           [LOAD MODE   
   9 5XFL          ORS   0  GIVE(2)  
  17 5X_=          GPERI    ,2                  [INITIATES TRANSFER  
   6 5YDW          COOR1 
  16 5YYG          LDX   5  CPRW1(2)            [LOAD REPLY WORD 
   5 5_D6    #SKI  G3
   4 5_XQ    (   
  10 62CB          LDX   3  APETADDR(2)  
   8 62X2          LDCT  6  #770 
  17 63BL          ANDX  6  5                   [ISOLATE ERROR BITS  
   7 63W=          SLL   5  6
   7 64*W          SRL   5  6
   8 64TG          BZE   5  TE   
   9 65*6          SBX   5  ADATUM(3)
   7 65SQ    TE    ORX   5  6
   4 66#B    )   
   8 66S2          BRN      UF   
   4 67?L    #   
  10 67R=    #     ROUTINE FOR MODE #600 
   4 68=W    #   
  18 68QG    MODE6 LDCT  0  #400                [HAS A PERI OPEN ALREADY 
  15 69=6          ANDX  0  GIVE(2)             [BEEN DONE   
  14 69PQ          BNZ   0  SLD                 [J IF SO 
   8 6=9B          LDCT  0  #400 
   9 6=P2          ERS   0  GIVE(2)  
   9 6?8L          LDX   0  EVENT4(2)
  16 6?N=          SRL   0  15                  [TEST FOR HIGH   
  16 6#7W          SBN   0  25                  [SPEED DRUM AND  
  14 6#MG          BNZ   0  UB                  [J IF NOT
  15 6*76          LDCT  5  #20                 [SET 512K BIT
  18 6*LQ          STO   5  AWORK1(2)           [REMEMBER SIZE OF DRUM   
   8 6B6B          BRN      UF   
  19 6BL2    UB    LPROP    ,3                  [LOAD PHYSICAL PROPS INTO X3 
   7 6C5L          SRL   34 3
  17 6CK=          SRL   4  21                  [DRUM TYPE 0,1 OR 2  
  17 6D4W          ANDN  3  #17                 [TOTAL NO OF DRUMS   
  16 6DJG          BNZ   4  UC                  [J IF TYPE 1 OR 2
  15 6F46          LDCT  0  1                   [SET 32K BIT 
   8 6FHQ          BRN      UE   
   7 6G3B    UC    SBN   4  1
  15 6GH2          BNZ   4  UD                  [J IF TYPE 2 
  15 6H2L          LDCT  0  4                   [SET 128K BIT
   8 6HG=          BRN      UE   
  15 6H_W    UD    LDCT  0  #20                 [SET 512K BIT
   9 6JFG    UE    ADS   0  AWORK1(2)
   8 6J_6          BCT   3  UE   
  18 6KDQ          LDX   5  AWORK1(2)           [LO SYST SIZE AS REPLY   
   8 6KYB          BRN      UF   
   7 6LD2    UG    STOZ     5
  10 6LXL    UF    LDX   3  APETADDR(2)  
  17 6MC=          LDX   1  EVENT2(2)           [OBJECT PROGRAM REPLY
  16 6MWW          ADN   1  1                   [WORD ADDRESS TO 
  21 6NBG          FADDRESS 3,1,HLOCK1          [BE CONVERTED RELATIVE TO G'S DATUM  
  15 6NW6          STO   5  0(1)                [SET REPLY   
   8 6P*Q          LDX   1  FX1  
   9 6PTB          OLPFIN1  XLINK(1) 
   4 6Q*2    #   
  12 6QSL    #     ROUTINE FOR CLOSE MODE #1000  
   4 6R#=    #   
  17 6RRW    MODEX LDCT  0  #400                [CHECK PERI OPEN HAS 
  18 6S?G          ANDX  0  GIVE(2)             [ALREADY BEEN RECEIVED   
  14 6SR6          BZE   0  SLD                 [J IF NOT
  15 6T=Q          ERS   0  GIVE(2)             [REMOVE BIT  
   9 6TQB          STOZ     AWORK1(2)
   8 6W=2          BRN      UG   
   4 6WPL    #   
   4 6X9=    #   
  10 6XNW    #     RELPERENT ENTERS HERE 
   9 6Y8G    #     X3 POINTS TO AOLPT
   4 6YN6    #   
   7 6_7Q    XDRUM #44626555 
   7 6_MB    XFREE #46624545 
   7 7272    XAREA #41624541 
   4 72LL    #   
   5 736=    QK3DRUM 
   8 73KW    #SKI  K6DRUM>99-99  
  10 745G          TRACE    3,DRUMFREE   
   9 74K6          LDX   5  CPPTR(3) 
   7 754Q          CLALLOC  5
  16 75JB          PEROFF   5                   [CLEAR ONLINE BIT
   9 7642          LDX   7  AWORK1(3)
   9 76HL          LDEX  6  GIVE(3)  
   8 773=          LGEOG    5,4  
  16 77GW          OUTNUM   4,0                 [SET UP GEOG NO. 
  15 782G          MURDER   3                   [MURDER AOLPT
   8 78G6          BNZ   6  VA   
  14 78_Q          INFORM   1,JDFREE,1          [DRUM OK 
   8 79FB          BRN      VE   
  11 79_2    VA    SETNCORE 8,3,ADATA,AWPER,1
  18 7=DL          ADN   3  XA1                 [ADJUST POINTER TO BLOCK 
   8 7=Y=          LTYPE    5,0  
  17 7?CW          SBN   0  25                  [TEST FOR H.S.DRUM   
   8 7?XG          BNZ   0  VB   
   8 7#C6          STOZ     0(3) 
   8 7#WQ          LDN   4  1(3) 
  17 7*BB          MOVE  3  7                   [ZEROISE DATA WORDS  
   9 7*W2 ...      LDX   0  XGHSD(1) 
   8 7B*L          STO   0  0(3) 
   7 7BT=          LDN   0  8
  18 7C#W          STO   0  2(3)                [STORE 8 IN THIRD WORD   
  19 7CSG          LDN   4  0                   [DEVICE ADDRESS,I.E.-BLOCK 0 
   8 7D#6          BRN      VC   
  20 7DRQ    VB    LDN   0  1033                [PNTR TO NEXT FREE WORD IN DIR   
   8 7F?B          STO   0  0(3) 
  18 7FR2          SRL   7  10                  [SYSTEM SIZE CONVERTED   
   7 7G=L          SBN   7  2
   8 7GQ=          LDN   0  #4000
  21 7H9W          SLL   0  1                   [ABS ADDR OF UNUSED AREA-BITS 2-12   
   7 7HPG          ORS   0  7
  17 7J96          STO   7  1(3)                [STO IN DATA BLOCK   
   9 7JNQ          LDX   0  XDRUM(1) 
   8 7K8B          STO   0  2(3) 
   9 7KN2          LDX   0  XFREE(1) 
   8 7L7L          STO   0  3(3) 
   9 7LM=          LDX   0  XAREA(1) 
   8 7M6W          STO   0  4(3) 
   8 7MLG          LDN   0  #2001
   8 7N66          STO   0  5(3) 
  20 7NKQ          LDN   0  6                   [NO. OF WORDS TO BE TRANSFERRED  
  16 7P5B          LDN   4  1024                [DEVICE ADDRESS  
  10 7PK2    VC    DCA      5,STO,0,COUNT
  10 7Q4L          DCA      5,STO,4,DTSA 
   7 7QJ=          LDN   0  1
  10 7R3W          DCA      5,DLA,0,MODE 
   8 7RHG          LOCKC    2,/  
   8 7S36          SBN   3  XA1  
   8 7SGQ          LOCKC    3,ZR 
  10 7T2B          MHUNTW   3,ADATA,AWPER
  14 7TG2          ADN   3  XA1                 [GET TSA 
  18 7T_L          DCA   5,STO,3,CTSA           [AND STORE IN DEVICE LIST
   8 7WF=          LDX   2  FX2  
  19 7WYW          SBN   3  XA1                 [RESTORE PTR TO ADATA AWPER  
   8 7XDG          GPERI    5,2  
   6 7XY6          COOR1 
   7 7YCQ          UNLOCK   2
   7 7YXB          UNLOCK   3
   7 7_C2          FREECORE 3
  15 7_WL          LDX   0  CPRW1(2)            [LOAD REPLY  
  16 82B=          SRL   0  18                  [TEST FOR ERROR  
   8 82TW          BNZ   0  VD   
  18 83*G          INFORM   1,JALLF,1           [FILES NEED ALLOCATING   
   8 83T6          BRN      VE   
  18 84#Q    VD    INFORM   1,JDCORR,1          [DRUM NEEDS INITIALIZING 
   5 84SB    VE    UP
  10 85#2    ZR    MHUNTW   2,ADATA,AWPER
   7 85RL          EXIT  1  0
  10 86?=          MENDAREA 50,K99DRUM   
   4 86QW    #END
   8 ____ ...54443560000300000000
