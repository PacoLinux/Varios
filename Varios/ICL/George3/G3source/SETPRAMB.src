  16 22#C ...      SEG SETPRAMB,,CENT(COMMAND PROCESSOR),,G250   
   4 22LS ...[   
  15 22_9 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  13 23?L ...[     THIS EXCLUDES CODE UNDER #SKI G250
   4 23L3 ...[   
   7 23YD ...#OPT G250 = 0   
   6 24=T ...#SKI G250&1 
  16 24K= ...# WITH UGUG EDIT M250 (ADDITIONAL NUMBER FORMATS)   
  11 24XQ          SEGENTRY K1SETPRAMB,XENT1 
  11 25CB          SEGENTRY K2SETPRAMB,XENT2 
  11 25X2          SEGENTRY K3SETPRAMB,XENT3 
  11 26BL          SEGENTRY K4SETPRAMB,XENT4 
  11 26W=          SEGENTRY K5SETPRAMB,XENT5 
  11 2764          SEGENTRY K7SETPRAMB,XENT7 
   4 27*W    #   
  14 27TG    #           OVERFLOW SEGMENT FOR SETPARAM   
  19 28*6    #           IMPLEMENTS VALUE,TEXT,INSTPARA AND LENGTH FUNCTIONS 
   4 28SQ    #   
  11 29#B    #UNS  IPEXTENSION+1*IPEXTENSION 
   7 29S2    #DEF  XIPEXT=1  
  11 2=?L    #UNS  IPEXTENSION+1*IPEXTENSION 
   4 2=R=    #SKI
   7 2?=W    #DEF  XIPEXT=0  
   4 2?QG    #   
   6 2?XN ...#SKI G250&1 
   6 2#4W ...TEN   +10   
   6 2#=6    MINUS #35   
   7 2#PQ    MAGIC +7036875  
   6 2*9B    SPACE #20   
   7 2*P2    SPEC  8H-8388608
   8 2B8L          FSHCODE   AORB
   4 2BN=    (   
   9 2C7W    ZINA            4HINA   
   9 2CMG    ZINB            4HINB   
   4 2D76    )   
  10 2DLQ    TRACE +IMOPTRACE-INSTPARAM  
  10 2F6B          +IMINTRACE-INSTPARAM  
  10 2FL2          +JOBTRACE-INSTPARAM   
  10 2G5L          +ICONTEXT-INSTPARAM   
   6 2GK=    #SKI  JNL   
  10 2H4W          +IOPTRACE-INSTPARAM   
   4 2MFG    #   
  13 2M_6    #     SECTION TO PROCESS VALUE PARAMETER
   4 2NDQ    #   
   5 2NYB    XENT1   
   9 2PD2    #SKI K6SETPRAMB>99-99   
   9 2PXL          TRACE    5,VALUE  
   7 2QC=          LDN   4  2
  19 2QWW          CALL  5  XHNT                [PARANEXT AND HUNT CPB/CUNI  
  10 2RBG          CHNUMCO1 1  ,XBREAK   
  11 2RW6          TESTREP  CHNUMERR,XERR10  
  19 2S*Q          LDX   4  ACOMMUNE1(2)        [BINARY NO. TO BE CONVERTED  
   6 2SB8 ...#SKI G250&1 
   4 2SBL ...(   
   9 2SC4 ...      BNG   7  XDECIM   
   4 2SCG ...#   
  13 2SCY ...#     SETPARAM OCT & HEX - VALUE IN X4  
   4 2SDB ...#   
  10 2SDS ...      MHUNT    3,CPB,CUNI   
  11 2SF= ...      NAME     3,COMDAT,CSETP   
  17 2SFN ...      LDX   6  7                   [ 0 - HEX,  1 - OCTAL
  11 2SG6 ...      ALTLEND  3,4,COMDAT,CSETP 
   7 2SGJ ...      SMO      7
  17 2SH2 ...      ADN   7  6                   [ 6 - HEX,  8 - OCTAL
  11 2SHD ...      MHUNT    3,COMDAT,CSETP   
  16 2SHW ...      STO   7  ANUM(3)             [ CHARACTER COUNT
  14 2SJ# ...      LDX   5  4                   [ VALUE  
   7 2SJQ ...      LDN   2  1
  17 2SK8 ...      ERX   2  6                   [ 1 - HEX,  0 - OCT  
   7 2SKL ...NXD   LDN   4  0
   8 2SL4 ...      SLL   45 3(2) 
   9 2SLG ...      TXL   4  TEN(1)   
   8 2SLY ...      BCS      OKDIG
  18 2SMB ...      ADN   4  #41-10              [ 10-15  ->  A-F FOR HEX 
   9 2SMS ...OKDIG DCH   4  A1+2(3)  
   7 2SN= ...      BCHX  3  /
   8 2SNN ...      BCT   7  NXD  
   8 2SP6 ...      LDX   2  FX2  
   9 2SPJ ...      LDX   4  AWORK4(2)
  17 2SQ2 ...      BZE   4  SPCOM               [ IF SETPARAM COMMAND
  15 2SQD ...      ACROSS   SETPARAM,3          [ IF %;..;   
   5 2SQW ...SPCOM   
   9 2SR# ...      ACROSS   SETPB,1  
   4 2SRQ ...#   
   4 2SS8 ...)   
  19 2STB    XDECIM                             [BRANCH TO HERE FROM INSTPARA
  10 2T*2          HUNT     3,CPB,CUNI   
  11 2TSL          NAME     3,COMDAT,CSETP   
   8 2W4D          BRN      XDEC2
   5 2W#=    XENT5   
  18 2WD7    #     THIS ROUTINE CONVERTS BINARY NUMBER IN X4 TO DECIMAL  
  14 2WJ4    #     PLACING IT IN APARA OF COMDAT,CSETP   
  11 2WM_          MHUNTW    3,COMDAT,CSETP  
   5 2WRW    XDEC2   
   8 2X?G          LDX   2  FX2  
  10 2XR6          LDN   2  ACOMMUNE1(2) 
  19 2Y=Q          LDN   7  1                   [COUNT OF CHARACTERS STORED  
   8 2YQB          BPZ   4  POZ  
  19 2_=2          ADN   7  1                   [NOW AT LEAST 2 CHARACTERS   
  17 2_PL          NGX   4  4                   [FOR -VE NO.,NEGATE  
  16 329=          BNG   4  SPECIAL              [SPECIAL NUMBER 
   9 32NW          LDX   6  MINUS(1) 
  19 338G          DCH   6  0(2)                [AND STICK A - SIGN IN FRONT 
   7 33N6          BCHX  2  /
  20 347Q    POZ   MPY   4  MAGIC(1)            [CONVERT TO D/L BINARY FRACTION  
  20 34MB          LDN   0  7                   [MAY. NO OF CONVERSIONS NECESSARY
   7 3572          MODE     0
   8 35LL    XCDR  CBD   4  0(2) 
   8 366=          LDCH  6  0(2) 
   8 36KW          BNZ   6  YOKO 
   8 375G          BCT   0  XCDR 
   8 37K6          BRN      WEND 
   7 384Q    ONO   BCHX  2  /
   8 38JB          CBD   4  0(2) 
   7 3942          ADN   7  1
   8 39HL    YOKO  BCT   0  ONO  
  18 3=3=    WEND  STO   7  ANUM(3)             [SET PARAMETER TYPE WORD 
  19 3=GW          ADN   7  3                   [SET X7 = NO OF WORDS REQ.   
  18 3?2G          SRL   7  2                   [TO HOLD DECIMAL VALUE   
  10 3?G6          LDX   5  ALOGLEN(3)   
   7 3?_Q          SBN   5  2
   7 3#FB          SBX   5  7
   8 3#_2          BPZ   5  WOK  
   7 3*DL          LDN   5  2
  19 3*Y=          ADX   5  7                   [X5 = REQUIRED LOGICAL LENGTH
  11 3BCW          ALTLEN   3,5,COMDAT,CSETP 
  11 3BXG          MHUNT    3,COMDAT,CSETP   
   8 3CC6    WOK   LDX  2  FX2   
  10 3CWQ          LDN   1  ACOMMUNE1(2) 
   9 3DBB          LDN   2  APARA(3) 
   7 3DW2          LDX   3  7
  19 3F*L          MOVE  1  0(3)                [MOVE VALUE INTO COMDAT?CSETP
   5 3FT=    XACROSS 
   9 3G#W          BRN      XCROSS   
   5 3GSG    SPECIAL 
   9 3H#6          LDN   1  SPEC(1)  
  20 3HRQ          MOVE  1  2                   [TREAT #40000000 AS SPECIAL CASE 
   7 3J?B          LDN   7  8
   8 3JR2          BRN      WEND 
   7 3K=L    PN2   FREECORE 3
  18 3KQ=          BRN      XERR2               [PARAMETER FORMAT ERROR  
   4 3L9W    #   
  13 3LPG    #     SECTION TO PROCESS TEXT FUNCTION  
   4 3M96    #   
   5 3MNQ    XENT2   
   9 3N8B    #SKI K6SETPRAMB>99-99   
   9 3NN2          TRACE    5,TEXT   
   7 3P7L          LDN   4  2
  19 3PM=          CALL  5  XHNT                [PARANEXT AND HUNT CPB/CUNI  
   9 3Q6W          CHNUMCON 1,XBREAK 
  11 3QLG          TESTREP  CHNUMERR,XERR10  
  10 3R66          HUNT     2,CPB,CUNI   
   7 3RKQ          FREECORE 2
  11 3S5B          HUNTW    3,CPAR,GNUMCON   
  19 3SK2          LDX   7  A1(3)               [GET NUMBER OF EXPRESSIONS   
   7 3T4L          LDX   2  3
  19 3TJ=          LDN   4  0                   [INITIALISE CHARACTER COUNT  
   7 3W3W          LDN   1  1
  15 3WHG          STO   3  6                   [PRESERVE X3 
  21 3X36    #     THE OBJECT OF THIS NEXT EXERCISE IS TO IGNORE LEADING SPACES IN   
  21 3XGQ    #     THE FINAL CHARACTER STRING AND ALSO TO IGNORE NONSIGNIFICANT ZEROS
  21 3Y2B    #     AT THE START OF EACH NUMBER.TO THIS END A THREE WAY SWITCH IS SET 
  21 3YG2    #     IN X1, WHEN ZERO SPACES ARE SIGNIFICANT. WHEN SET NEGATIVE ZEROS  
  21 3Y_L    #     ARE SIGNIFICANT (AND THE FIRST ZERO CONVERTS X1 TO ZERO).IF SET TO
  21 3_F=    #     ONE THEN SPACES ARE BEING IGNORED BUT NONE HAS SO FAR BEEN IGNORED
   8 3_YW    #     IN THIS NUMBER
  19 42DG    XOLP  LDN   5  4                   [4 CHARS IN EACH EXPRESSION  
   9 42Y6    XILP  LDCH  0  A1+1(3)  
  18 43CQ          BCHX  3  /                   [IGNORE PRECEDING ZEROES 
  17 43XB          BNZ   0  XTSP                [NON ZERO CHARACTER  
   8 44C2          BNG   1  XDCH 
   8 44WL          BCT   5  XILP 
  20 45B=    XDCH  DCH   0  A1+1(2)             [STORE NON ZERO CHARACTERS OR ONE
   7 45TW          LDN   1  0
  20 46*G          ADN   4  1                   [ZERO FOR AN ALL ZERO EXPRESSION 
   7 46T6          BCHX  2  /
   8 47#Q          BZE   5  XBCT 
   8 47SB          BCT   5  XLOAD
   8 48#2    XBCT  BCT   7  XOLP 
   8 48RL          BRN      XOUT 
  17 49?=    XTSP  BZE   1  XDCH                [SPACES SIGNIFICANT  
   8 49QW          SMO      FX1  
   8 4==G          TXU   0  SPACE
   8 4=Q6          BCS      XDCH 
   8 4?9Q          LDX   1  GSIGN
   8 4?PB          BCT   5  XILP 
   7 4#92          LDN   1  1
   8 4#NL          BRN      XBCT 
   9 4*8=    XLOAD LDCH  0  A1+1(3)  
   7 4*MW          BCHX  3  /
   8 4B7G          BRN      XDCH 
  13 4BM6    #              REMOVE TRAILING SPACES   
   8 4C6Q    XOUT  BZE   4  XOUT1
   7 4CLB          SLC   2  2
   7 4D62          SBN   2  1
   7 4DKL          SRC   2  2
   9 4F5=          LDCH  0  A1+1(2)  
   8 4FJW          SMO      FX1  
   8 4G4G          TXU   0  SPACE
   8 4GJ6          BCS      XOUT1
   7 4H3Q          SBN   4  1
   8 4HHB          BRN      XOUT 
   5 4J32    XOUT1   
   7 4JGL          LDX   3  6
  18 4K2=          STO   4  A1(3)               [STORE CHARACTER COUNT   
   8 4KFW          ADN   4  11   
  17 4K_G          SRL   4  2                   [NO OF WORDS IN BLOCK
   7 4LF6          PHOTO    5
  11 4LYQ          ALTLEN   3,4,CPAR,GNUMCON 
   9 4MDB          TESTMOVE 5,XNOMOV 
  11 4MY2          HUNTW    3,CPAR,GNUMCON   
   5 4NCL    XNOMOV  
   9 4NX=          ADN   4  A1-1(3)  
  10 4PBW          LDX   6  ALOGLEN(3)   
  17 4PWG          SBN   6  1                   [NUMBER TO BE MOVED  
   7 4QB6          LDX   5  4
   7 4QTQ    XT1   SBN   4  1
  19 4R*B          MOVE  4  1                   [MOVE NUMBERS DOWN ONE WORD  
   7 4RT2          SBN   5  1
   8 4S#L          BCT   6  XT1  
  11 4SS=          NAME     3,COMDAT,CSETP   
   8 4T?W          LDX   4  A1(3)
   9 4TRG          STO   4  A1+1(3)  
   8 4W?6          BRN      PTYPE
   4 4WQQ    #   
  14 4X=B    #     SECTION TO PROCESS INSTPARA FUNCTION  
   4 4XQ2    #   
   5 4Y9L    XENT3   
   9 4YP=    #SKI K6SETPRAMB>99-99   
  10 4_8W          TRACE    5,INSTPARA   
   7 4_NG          LDN   4  2
   8 5286          FSHCODE  AORB 
  19 52MQ          CALL  5  XHNTC               [PARANEXT AND HUNT CPB/CUNI  
   7 537B          FSHSKIP   
  19 53M2          CALL  5  XHNT                [PARANEXT AND HUNT CPB/CUNI  
   8 546L          SRL   4  12   
   8 54L=          BNZ   4  PN2  
   5 555W    #SKI  G3
  14 55KG          OPENSYS  XBROKE,PARAMINDEX,READ,QUERY 
   5 5656    #SKI  G4
  15 56JQ          OPENSYS  XBROKE,PARAMINDEXG4,READ,QUERY   
  10 58H=          TESTREPNOT   OK,XNOTOK
   6 59GG    #SKI  XIPEXT
  17 5=26          DOWN     SETUPARA,2          [SETUP FILE/FRB BLOCK
   7 5=FQ    #SKI  XIPEXT<1$1
   6 5=_B          READ  
   6 5?F2          CLOSE 
  10 5?YL          HUNT     2,FILE,FRB   
  10 5#D=          HUNT     3,CPB,CUNI   
   7 5#XW          STO   2  4
   5 5*CG    STLOOP  
  20 5*X6          LDN   1  0                   [SEARCH PARAMINDEX FOR CORRECT   
  18 5BBQ          LDX   5  ANUM(3)             [INSTALLATION PARAMETER  
   8 5BWB          ANDN  5  #7777
   5 5CB2    TSTAGN  
   7 5CTL          SMO   3   
   9 5D*=          LDCH  6  APARA(1) 
   7 5DSW          SMO   2   
   9 5F#G          LDCH  7  IPNAME(1)
   7 5FS6          TXU   6  7
   9 5G?Q          BCC      XCARON   
   9 5GRB          ADN   2  IPLINE   
   7 5H?2          LDX   6  2
   7 5HQL          SBX   6  4
   9 5J==          ADN   6  IPNAME-A1
   7 5JPW          SMO      4
   9 5K9G          TXL   6  ALOGLEN  
   7 5KP6          BCC   PN2 
   9 5L8Q          BRN      STLOOP   
   5 5LNB    XCARON  
   7 5M82          BCHX  1  /
   9 5MML          BCT   5  TSTAGN   
  21 5N7=    #     CHECK IF THE INSTALLATION PARAMETER IS A TRACE AND IF SO CONVERT  
  20 5NLW    #     TO A CHARACTER STRING IN FORMAT OF ACTION ON MONITORING FILE  
   7 5P6G          LDX   1  4
   9 5PL6          LDX   2  IPNUM(2) 
   8 5Q5Q          FSHCODE  AORB 
   4 5QKB    (   
  16 5R52          STO   2  5                   [SAVE IPNUM IN X5
  18 5RJL          PARAPASS            [PASS NEXT PARAMETER IF PRESENT   
  19 5S4=          MHUNT    2,CPB,CUNI          [PTR TO PARAMETER BLOCK IN X3
  16 5SHW          LDX   4  ANUM(2)             [LOAD REPLY WORD 
  16 5T3G          LDX   3  APARA(2)            [LOAD PARAMETER  
  17 5TH6          FREECORE 2                   [FREE CPB/CUNI BLOCK 
  21 5W2Q          BNG   4  NOCONV              [J IF NO PARAMETER - GOT REQD IPNUM  
  17 5WGB          BZE   4  XINERR              [J IF 'IN' - ERROR   
   4 5X22    XIN 
   9 5XFL          FSHCODE  A,XFSHAIN
   4 5X_=    (   
  19 5YDW          BXE   3  ZINA(1),NOCONV      [J IF 'INA' - GOT REQD IPNUM 
  21 5YYG          BXE   3  ZINB(1),XCONV       [J IF 'INB' - CONVERT FOR REQD IPNUM 
   5 5_D6    XFSHAIN 
   4 5_XQ    )   
   9 62CB          FSHCODE  B,XFSHBIN
   4 62X2    (   
  21 63BL          BXE   3  ZINA(1),XCONV       [J IF 'INA' - CONVERT FOR REQD IPNUM 
  19 63W=          BXE   3  ZINB(1),NOCONV      [J IF 'INB' - GOT REQD IPNUM 
   5 64*W    XFSHBIN 
   4 64TG    )   
   5 65*6    XINERR  
  20 65SQ          MHUNT    3,CPB,CUNI          [PTR TO CPB/CUNI BLOCK FOR ERROR 
   8 66#B          BRN      PN2  
   5 66S2    XCONV   
  17 67?L          FSHIPCON 5                   [CONVERT IPNUM IN X5 
   5 67R=    NOCONV  
  17 68=W          LDX   2  5                   [LOAD IPNUM INTO X2  
   4 68QG    )   
   6 69=6    #SKI  XIPEXT
  20 69PQ          IPUSEX   LDX   4,2          [LOAD VALUE OF IP FROM FIXED CORE 
   7 6=9B    #SKI  XIPEXT<1$1
  10 6=P2          LDX   4  INSTPARAM(2) 
   7 6?8L          LDX   5  2
   8 6?N=          FSHCODE  AORB 
  17 6#7W          MFREE    FILE,FRB            [FREE FILE/FRB BLOCK 
   7 6#MG          FSHSKIP   
   7 6*76          FREECORE 1
   7 6*LQ          LDX   2  5
   9 6B6B          TXU   2  TRACE(1) 
   8 6BL2          BCC      TST4 
  10 6C5L          TXU   2  TRACE+1(1)   
   8 6CK=          BCC      TST4 
  10 6D4W          TXU   2  TRACE+2(1)   
   8 6DJG          BCC      TST4 
  10 6F46          TXU   2  TRACE+3(1)   
   8 6FHQ          BCC      TST5 
  10 6G3B          TXU   2  TRACE+4(1)   
   8 6GH2          BCC      TST4 
   9 6H2L          BRN      XDECIM   
   4 6HG=    TST4
   8 6H_W          SYNTHMON 4,1  
   9 6JFG          LDX   4  EXEC3(2) 
   7 6J_6          LDX   5  4
  10 6KDQ    TST6  MHUNT    3,CPB,CUNI   
   8 6KYB          ADN   5  11   
   7 6LD2          SRL   5  2
  10 6LXL          TXU   5  ALOGLEN(3)   
   8 6MC=          BCC      XN   
   7 6MWW          PHOTO    6
   8 6NBG          ALTLEN   3,5  
   8 6NW6          TESTMOVE 6,XN 
  10 6P*Q          MHUNT    3,CPB,CUNI   
  11 6PTB    XN    NAME     3,COMDAT,CSETP   
   9 6Q*2          STO   4  ANUM(3)  
   9 6QSL          LDN   5  APARA(3) 
  10 6R#=          LDN   4  ACOMMUNE1(2) 
   9 6RRW          SMO      ANUM(3)  
   7 6S?G          MVCH  4  0
   8 6SR6          BRN      PTYPE
   4 6T=Q    TST5
   8 6TQB          IPCONT   4,5  
   8 6W=2          BPZ   5  XLD4 
  10 6WPL          STOZ     ACOMMUNE1(2) 
   7 6X9=          LDN   5  1
   4 6XNW    XLD4
   7 6Y8G          LDX   4  5
   8 6YN6          BRN      TST6 
   4 6_7Q    #   
  13 6_MB    #     SECTION TO PROCESS LENGTH FUNCTION
   4 7272    #   
   5 72LL    XENT4   
   9 736=    #SKI K6SETPRAMB>99-99   
   9 737H          TRACE    5,LENPARA
   7 738S          LDN   4  2
  19 73=5          SPARANXTX ,,4                [REMOVE BRACKETS,RETAINING   
  16 73?B          TESTREP  UNPAIR,XERR9        [INTERNAL SPACES 
  11 73#M          TESTREP  TOOMANY,XERR14   
  10 73*Y          MHUNT    3,CPB,CUNI   
   9 73C9          LDX   4  ANUM(3)  
  16 73DG          BNG   4  PN2                 [NULL PARAMETER  
   8 73FR          ANDN  4  #7777
  11 73H4          NAME     3,COMDAT,CSETP   
   8 73J*          BRN      XDEC2
   4 73JF    #   
  14 73JK    #     SECTION TO PROCESS PROCESSORS FUNCTION
   4 73JP    #   
   5 73JT    XENT7   
   9 73J_    #SKI K6SETPRAMB>99-99   
   9 73K5          TRACE    5,PROPARA
   9 73K9          LDCH  4  APROCNO  
  11 73K*          SETNCORE 3,3,COMDAT,CSETP 
   8 73KF          BRN      XDEC2
   4 73KW    #   
  14 745G    #     ROUTINE TO DETERMINE PARAMETER TYPE   
   4 74K6    #   
   5 754Q    PTYPE   
  14 75JB          PTYPE    COMDAT,CSETP,3,APARA,ANUM(3) 
   5 7642    XCROSS  
   8 76HL          SMO      FX2  
   9 773=          LDX   4  AWORK4   
   8 77GW          BZE   4  XC1  
   8 782G          CALL  7  UNIF 
   7 78G6          UPPLUS   1
   4 78_Q    XC1 
   9 79FB          ACROSS   SETPB,1  
   4 79_2    #   
   8 7=DL    UNIF  SBX   7  FX1  
   7 7=Y=          PARAFREE  
   7 7?CW          UNIFREE   
   8 7?XG          LDX   2  FX2  
   9 7#C6          LDX   0  AWORK3(2)
  10 7#WQ          STO   0  JOBEVENTS(2) 
   8 7*BB          ADX   7  FX1  
   7 7*W2          EXIT  7  0
   8 7B*L          FSHCODE  AORB 
   4 7BT=    (   
   8 7C#W    XHNTC SBX   5  FX1  
   8 7CSG          LDN   1  #34  
   8 7D#6          BRN      XPASS
   4 7DRQ    )   
   8 7F?B    XHNT  SBX   5  FX1  
   7 7FR2          NGN   1  1
   5 7G=L    XPASS   
   8 7GQ=          PARANXTX 1,,4 
  10 7H9W          TESTREP  UNPAIR,XERR9 
  11 7HPG          TESTREP  TOOMANY,XERR14   
   8 7J96          ADX   5  FX1  
   9 7JNQ          HUNT  3,CPB,CUNI  
   9 7K8B          LDX   4  ANUM(3)  
   8 7KN2          BNG   4  PN2  
   8 7L7L          BZE   4  PN2  
   7 7LM=          EXIT  5  0
   9 7M6W    PJMAXPAR       +JMAXPAR 
   9 7MLG    PAPFERR        +APFERR  
   9 7N66    PERUNPAIR      +ERUNPAIR
   9 7NKQ    PCHNUMERR      +CHNUMERR
  18 7P5B    XERR2 LDX   7  PAPFERR(1)          [PARAMETER FORMAT ERROR  
   8 7PK2          BRN      NGN  
  10 7Q4L    XERR9 LDX   7  PERUNPAIR(1) 
   8 7QJ=          BRN      NGN  
   5 7R3W    XERR10  
  10 7RHG          LDX   7  PCHNUMERR(1) 
   8 7S36          BRN      NGN  
   5 7SGQ    XERR14  
  10 7T2B          LDX   7  PJMAXPAR(1)  
   7 7TG2    NGN   NGN   6  1
   4 7T_L    XOR 
   8 7WF=          SMO      FX2  
   9 7WYW          LDX   4  AWORK4   
   8 7XDG          BZE   4  XOR1 
   8 7XY6          CALL  7  UNIF 
   5 7YCQ          UP
   4 7YXB    XOR1
   8 7_C2          COMERRX  7,6  
   5 7_WL    XBROKE  
  10 82B=          MFREEW   FILE,FABSNB  
   5 82TW    XBREAK  
   7 83*G          COMBRKIN  
   5 83T6    XNOTOK  
  10 84#Q          GEOERR   1,WRONGREP   
  11 84SB          MENDAREA 30,GAPSETPRAMB   
   4 85#2    #END
   6 ____ ...245364530001
