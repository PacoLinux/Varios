  14 22FL ...      SEG   INCSTART,864,  BACK  ,INCDUMP   
   4 22_=    #   
  12 23DW          SEGENTRY K1INCSTART,X1INCSTART
   4 23YG    #   
   6 24D6    #  CONSTANTS
   4 24XQ    #   
  10 25CB          SEGENTRY INCNEWDUMP   
  21 25X2                   +0                  [SETTING OF INITIAL INCREMENT NO. +1 
  21 26BL                                       [OPTIONALLY 0 (INCR.1), BUT CHANGEABL
  16 26W=                                       [BY NEWDUMP MACRO
  10 27*W    MPROCESSED     12HPROCESSED 
  10 27TG                   12HOBSOLETE  
  10 285# ...STARTGRINC     12HSTARTGRINC
   9 286W ...MASTER         12HMASTER
   8 288D ...MLANCOMP        4HC1
  11 28=2 ...NAMEINCR       12HGEORGE3DUMP   
  10 28=R ...XINCIND         12HINCINDEX 
  15 28?J ... BITDEFS       AWORK1,0,MBFINAL,MBEMS,MBPGENRES 
   4 28*6    #   
   7 28SQ    #  SUBROUTINES:-
   4 28T7 ...#   
   4 2=NW ...#   
   4 2=P? ...#   
  17 2=R=    SFINDFP            [LOCATES FINCPROC BLOCK FOR ALTLENG  
  11 2?=W          MHUNTW   2,FILE,FINCPROC  
   7 2?QG          EXIT  1  0
   4 2#=6    #   
   4 2#PQ    #   
  18 2#PY ...MADDFINCP          [ADDS INCR NO IN X5 TO LIST BEING SET UP 
  17 2#Q6 ...                   [CALLED ON X4, LEAVES X3 -> FINCPROC 
   8 2#Q# ...      SBX   4  FX1  
   7 2#QG ...      SMO    FX2
   9 2#QN ...      STO   4  AWORK4   
  11 2#QW ...      MHUNTW   3,FILE,FINCPROC  
  16 2#R4 ...     LDX   4   FINCPRH(3)          [CURRENT LENGTH  
   7 2#R= ...      ADN   4  1
  15 2#RD ...      ALTLENG  3,4,SFINDFP         [ALTER LENTH 
  11 2#RL ...      MHUNTW   3,FILE,FINCPROC  
   7 2#RS ...      SMO      4
  17 2#S2 ...      STO   5  FINCPRH-1(3)        [ADD INCR NO AT END  
  18 2#S8 ...      STO   4  FINCPRH(3)          [STORE UPDATED REC HEADER
  17 2#SB ...      LDX   4  AWORK4(2)           [RESTORE LINK + EXIT 
   8 2#SJ ...      ADX   4  FX1  
   7 2#SQ ...      EXIT  4  0
   4 2#SY ...#   
   4 2#T6 ...#   
  19 2#T# ...ZEROBATCH          [EMPTIES INCRS IN BATCH LIST. CALLED ON X4   
  11 2#TG ...      MHUNTW   3,FILE,FINCPROC  
   8 2#TN ...      SBX   4  FX1  
   8 2#TW ...      SMO      FX2  
  15 2#W4 ...      STO   4  AWORK4              [SAVE LINK   
  16 2#W= ...      LDX   4  INCBATCH(3)         [NUMBER IN LIST  
  17 2#WD ...      BZE   4  XIT                 [J IF ALREADY EMPTY  
  18 2#WL ...      SBS   4  FINCPRH(3)          [REDUCE LENGTH OF BLOCK  
  15 2#WS ...      STOZ     INCBATCH(3)         [ZERO COUNT  
  17 2#X2 ...      LDX   4  FINCPRH(3)          [GET NEW SIZE WANTED 
  16 2#X8 ...      ALTLENG  3,4,SFINDFP         [SHORTEN BLOCK   
   4 2#XB ...XIT 
   8 2#XJ ...      SMO      FX2  
   9 2#XQ ...      LDX   4  AWORK4   
   8 2#XY ...      ADX   4  FX1  
   7 2#Y6 ...      EXIT  4  0
   4 2#Y# ...#   
   4 2#YG ...#   
   4 2#YJ ...#   
  14 2#YL ...SFINDFQUAL         [LOCATES FQUAL FOR LOCKC 
  10 2#YN ...      MHUNTW   2,FILE,FQUAL 
   7 2#YQ ...      EXIT  1  0
   4 2#YS ...#   
   4 2#YW ...#   
  20 2#YY ...MARKAPIA           [CALLED ON X4. ON ENTRY X3->FINC. MARKS THE APIA 
  20 2#_2 ...                   [ENTRY FOR ALL DECKS SHARING A CONTROL WITH THE  
  20 2#_4 ...                   [TAPES FOR THIS INCR AS FORBIDDEN TO USE SKIPS.  
  21 2#_5 ...                   [IT ALSO DOES A BACKSPACE AND READ ON ALL TAPES FOR  
  20 2#_6 ...                   [THE INCREMENT. IF THEY HAVE BEEN INTERFERED WITH
  20 2#_7 ...                   [(I.E. UNLOADED AND RELOADED ) THEY WILL BE AT   
  20 2#_8 ...                   [LOAD POINT SO THE BACKSPACE WILL LEAVE THE INCR 
  21 2#_9 ...                   [MARKED AS FAILED. INCWRITE WILL SPOT THIS ON ENTRY  
  15 2#_= ...                   [ON EXIT X3-> THE SAME FINC  
   7 2#_# ...#UNS ANULLDUMP  
  17 2#_B ...      EXIT  4  0             [ NULL SR IF ANULLDUMP ON  
   8 2#_D ...      LDX   2  FX2  
   8 2#_J ...      SBX   4  FX1  
  14 2#_N ...      STO   4  AWORK1(2)        [SAVE LINK  
  17 2#_S ...      LDX   7  3                   [KEEP COPY OF POINTER
  16 2*22 ...      LDN   4  0                   [OFFSET DOWN FINC
  18 2*28 ...      LDX   5  INCCOPIES(3)        [NO OF MTS TO BE CHECKED 
   5 2*2B ...NEXTACT 
  21 2*2J ...      FINDACTFB 2,,ACTDOC,ADUMPT,,BPTR [X2-> NEXT DTA BACK ALONG ACT C  
  19 2*2Q ...      LDX   0  ADTSN(2)            [PICK UP THE TAPE IT CONTROLS
   7 2*2Y ...      SMO      4
  19 2*36 ...      BXU   0  FGETSER(3),NEXTACT  [LOOP IF NOT THE DTA WE WANT 
   9 2*37 ...      JENVNOT  XNDME,DME
  17 2*38 ...[ IF DME LOAD THE UNIT NO OF THE DECK AND THE MECH NO   
   8 2*39 ...      LDN   0  #777 
   9 2*3= ...      SMO      CPPTR(2) 
   8 2*3? ...      ANDX  0  JK52C
   8 2*3# ...      SMO      FX2  
   9 2*3* ...      STO   0  ACOMMUNE1
   9 2*3B ...      SMO      CPPTR(2) 
   8 2*3C ...      LDX   0  K56+1
   8 2*3D ...      SRL   0  18   
   8 2*3F ...      SMO      FX2  
   9 2*3G ...      STO   0  ACOMMUNE2
   8 2*3H ...      BRN      XDME 
  12 2*3J ...[ NOT DME SO LOAD THE CHANNEL NUMBER
   5 2*3K ...XNDME   
   8 2*3L ...      LDN   6  #7777
   9 2*3M ...      SMO      CPPTR(2) 
  18 2*3N ...      ANDX  6  K52                 [EXTRACT CHANNEL NUMBER  
   4 2*3R ...XDME
   9 2*3W ...      LDX   2  JPTI+5   
   9 2*44 ...      LDX   3  JPTI+6   
  19 2*4= ...      SBX   3  2                   [X3=NO OF DECKS ON MACHINE   
   5 2*4D ...RPTAPE  
  19 2*4L ...      LDX   1  0(2)                [X1-> DEVLIST ENTRY FOR DECK 
   9 2*4M ...      JENVNOT  XNDM2,DME
  12 2*4N ...[ IF DME SUBTRACT THE UNIT NUMBERS  
   8 2*4P ...      LDN   0  #777 
   9 2*4Q ...      ANDX  0  JK52C(1) 
  20 2*4R ...      BZE   0  NEXTDECK            [FROM 866,IGNORE IF UNIT NO ZERO 
   8 2*4S ...      SMO      FX2  
   9 2*4T ...      SBX   0  ACOMMUNE1
  11 2*4W ...[ IF DME SUBTRACT MECH NUMBERS  
   9 2*4X ...      LDX   6  K56+1(1) 
   8 2*4Y ...      SRL   6  18   
   8 2*4_ ...      SMO      FX2  
   9 2*52 ...      SBX   6  ACOMMUNE2
   8 2*53 ...      BRN      XDME2
   5 2*54 ...XNDM2   
   8 2*55 ...      LDN   0  #7777
  17 2*56 ...      ANDX  0  K52(1)              [PICK UP CHANNEL NO  
   5 2*57 ...XDME2   
  19 2*58 ...      BXU   0  6,NEXTDECK          [IGNORE IT IF NOT ON CHANNEL 
  20 2*5B ...      DFSET    1,DUMP              [SET THE SHARING WITH DUMPER BIT 
   5 2*5J ...NEXTDECK
   7 2*5Q ...      ADN   2  1
  18 2*5Y ...      BCT   3  RPTAPE              [J IF MORE DECKS TO CHECK
  18 2*66 ...      LDX   3  7                   [RESTORE POINTER TO FINC 
  17 2*6# ...      ADN   4  FGETLEN             [UPDATE MOD DOWN FINC
   8 2*6G ...      LDX   2  FX2  
  20 2*6N ...      BCT   5  NEXTACT             [LOOP IF MORE TAPES TO BE CHECKED
  18 2*6P ...      CHAIN    3,2                 [RECHAIN FINC NEXT TO CPA
   7 2*6Q ...      LDN   0  3
  15 2*6R ...      STO   0  ACOMMUNE1(2)        [SET UP BSP  
  14 2*6S ...      DOWN     MTDUMP,2            [DO BSP  
  15 2*6T ...      STOZ     ACOMMUNE1(2)        [SET UP READ 
  17 2*6W ...      LDN   0  20                  [LENGTH OF TRANSFER  
  10 2*6X ...      STO   0  ACOMMUNE2(2) 
  10 2*6Y ...      MHUNTW   3,FILE,FQUAL 
  15 2*6_ ...      LOCKC    3,SFINDFQUAL        [LOCK BUFFER 
  10 2*72 ...      MHUNTW   3,FILE,FQUAL 
   8 2*73 ...      LDN   0  A1(3)
  18 2*74 ...      STO   0  ACOMMUNE3(2)        [START ADDR OF TRANSFER  
  14 2*75 ...      DOWN     MTDUMP,2            [DO READ 
  15 2*76 ...      UNLOCK   3                   [UNLOCK FQUAL
  10 2*77 ...      MHUNTW   3,FILE,FINC  
   9 2*78 ...      LDX   0  INCACT(3)
  10 2*79 ...      BZE   0   NOTPEVENT   
  20 2*7= ...      MBS      3,BINCEOTS,BINCEOIS [TELL EOT CODE WHEN IT IS FINALLY
  21 2*7? ...                                   [ENTERED NOT TO WRITE END SENTS TO   
  21 2*7# ...                                   [THIS INCR. AS INCEND IS CAREFUL NOT 
  19 2*7* ...                                   [TO WRITE START SENTS EITHER 
  19 2*7B ...                                   [TAPE IS ASSURED OF NOT BEING
  15 2*7C ...                                   [CORRUPTED.  
  11 2*7D ...      OUTPACK  INCNO(3),1,INCNUM
  21 2*7F ...      MONOUT   INCTAMPER          [A TAPE FOR INCR. N HAS EITHER JUST FA
  21 2*7G ...                             [OR HAS BEEN TAPMPERED WITH SINCE IT WAS LO
  10 2*7H ...      MHUNTW   3,FILE,FINC  
   6 2*7J ...NOTPEVENT   
  15 2*7K ...      LDX   4  AWORK1(2)        [RESTORE LINK   
   8 2*7L ...      ADX   4  FX1  
   7 2*7M ...      EXIT  4  0
   4 2*7N ...#   
   4 2*7P ...#   
   4 2*7Y ...#   
   4 2*87 ...#   
  19 2*8B ...#  OUTPUTS A BLANK LINE TO THE MONFILE, CALLED ON X7, USES X4.  
   7 2*8K ...#  COORDINATES. 
   4 2*8S ...#   
   5 2*93 ...OUTBLANK
   8 2*9= ...      SBX   7  FX1  
   8 2*9F ...      LDN   4  #40  
  10 2*9N ...      OUTMOND  0,ACOMMUNE1,4
   8 2*9X ...      ADX   7  FX1  
   7 2*=6 ...      EXIT  7  0
   4 2*=* ...#   
   4 2*=J ...#   
  21 2*=R ...#  SETS UP A PARAMETER BLOCK READY FOR 'MONOUT' WITH INCR. NO. AS 1ST   
  21 2*?2 ...#  PARAMETER AND SERIAL NOS. (ON WHICH INCR. IS TO BE DUMPED) AS 2ND.   
  19 2*?9 ...#  ON ENTRY, RELEVANT FINC BLOCK IS CALLED FGET. X4 IS S/R LINK.
  18 2*?D ...#  ON EXIT, X1=FX1, X2=FX2.        USES X0, X3, X5, X6 & X7.
   6 2*?M ...SETMONOUT   
   8 2*?W ...      SBX   4  FX1  
  16 2*#5 ...      MHUNTW   3,FILE,FGET         [X3-> FGET BLOCK 
  19 2*## ...      LDX   5  INCCOPIES(3)        [X5= NO. OF MTS TO DUMP ON   
  19 2*#H ...      LDX   3  INCNO(3)            [X3= NO. OF THIS INCREMENT   
  21 2*#Q ...      OUTBLOCN 11                  [GET PARAMETER BLOCK -SHOULD AMPLY CO
  16 2*#_ ...                                   [   MOST CASES   
  19 2**8 ...      OUTPACK  3,1,INCNUM          [INC.NO. TO PARAMETER BLOCK  
   7 2**C ...      SBN   5  1
  15 2**L ...      BNZ   5  SPARAM              [J IF >1 TAPE
  16 2**T ...      OUTNULL                      [NULL PARAMETER  
   9 2*B4 ...      BRN      PARASENT 
   5 2*B? ...SPARAM  
   8 2*BG ...      LDCT  0  #630 
  10 2*BP ...      STO   0  ACOMMUNE1(2) 
  16 2*BY ...      OUTPARN  1,ACOMMUNE1         [MAKE MT PLURAL  
   5 2*C7 ...PARASENT
  16 2*CB ...      MHUNTW   3,FILE,FGET         [X3-> FGET BLOCK 
  21 2*CK ...      MHUNTW   2,FILE,FQUAL        [X2-> FQUAL BLOCK (USED SIMPLY AS A B
  18 2*CS ...                                   [                   HERE)
  17 2*D3 ...      STOZ     1                   [ZEROISE CHAR. COUNT 
  18 2*D= ...      LDX   5  INCCOPIES(3)        [NO OF MTS IN FINC(FGET) 
   5 2*DF ...NXTSN   
  18 2*DN ...      LDX   7  FGETSER(3)          [X7 = NEXT TSN IN FGET   
   7 2*DX ...      LDN   0  8
   5 2*F6 ...NXOCT   
   7 2*F* ...      LDN   6  0
   7 2*FJ ...      SLL   67 3
  17 2*FR ...      BNZ   6  NZERO               [J IF DIGIT NON-ZERO 
  20 2*G2 ...      BPZ   4  NONSIG              [J IF DIGIT NON-SIGNIFICANT ZERO 
   5 2*G9 ...NZERO   
  21 2*GD ...      ORX   4  GSIGN               [ENSURE 'SIGNIFICANT DIGITS' MARKER S
  19 2*GM ...      DCH   6  A1(2)               [PUT CHAR. INTO FQUAL BLOCK  
   7 2*GW ...      BCHX  2  /
  21 2*H5 ...      ADN   1  1                   [UPDATE COUNT OF CHARS. IN PARAMETER 
   5 2*H# ...NONSIG  
  19 2*HH ...      BCT   0  NXOCT               [J IF MORE CHARS. TO EXTRACT 
   8 2*HQ ...      ERX   4  GSIGN
   8 2*H_ ...      LDN   6  #20  
  17 2*J8 ...      DCH   6  A1(2)               [INSER3 SPACE CHA$R. 
  17 2*JC ...      ADN   1  1                   [UPDATE CHAR. COUNT  
   7 2*JL ...      BCHX  2  /
  17 2*JT ...      ADN   3  FGETLEN             [UPDATE MOD DOWN FGET
  18 2*K4 ...      BCT   5  NXTSN               [J IF MORE MTS IN FGET   
  21 2*K? ...      OUTPARAM 1,A1,FILE,FQUAL     [PUT TSNS IN OUTPUT PARAMETER BLOCK  
   8 2*KG ...      ADX   4  FX1  
   7 2*KP ...      EXIT  4  0
   4 2*KY ...#   
   4 2*L7 ...#   
   4 2*LB ...#   
   9 2*LK ...#  *********************
   4 2*LS ...#   
   9 2*M3 ...#  MAIN PATH ENTERS HERE
   4 2*M= ...#   
   9 2*MF ...#  *********************
   4 2*MN ...#   
  11 2*MX ...#  ENTERED FROM INCWAIT SEGMENT 
  17 2*P2    #  ON ENTRY, :SYSTEM.INCINDEX IS OPEN IN GENERAL MODE   
   4 2B4P ...#   
  20 2BDD ...#  EXPECTS X6 TO BE AS ON EXIT FROM INCWAIT (B0,1 POSSIBLY NONZERO  
   7 2BS7 ...#  RESTALL ZERO)
  20 2C7W    #  SETS UP AND INITIALISES THE BASIC BLOCKS USED BY DUMPER, NAMELY  
  15 2CMG ...#  FILE   FINC (ALIAS FGET) AND FINCPROC BLOCKS.
   4 2D76    #   
   6 2DLQ    X1INCSTART  
  19 2DQM ...      SETBIT  1,CLONG1(2)        [ SET BACKING STORE PRIVILEGE  
   6 2DQ_ ...#UNS  AMTGR 
   4 2DR? ...(   
  16 2DRK ...      BC       ,BMTGR            [ ENSURE BIT CLEAR 
  10 2DRX ...      SEGENTRY K90INCSTART  
   6 2DS9 ...      NULL  
   4 2DSH ...#   
  11 2DST ...#   SET BIT FOR MTGR IN INCWORD 
   4 2DT7 ...#   
  16 2DTF ...      BS       ,BMTGR            [ SET BIT FOR MTGR 
  10 2DTR ...      SEGENTRY K91INCSTART  
   4 2DW5 ...)   
  19 2DWJ ...      BC       ,BFILEINC           [ENSURE FILE INC SWITCH CLEAR
  18 2F6B ...      SETNCORE 20,3,FILE,FQUAL  [BLOCK TO HOLD TAPE SENTS   
  19 2FL2 ...      JBS      PRLLSET,,BPARALLEL  [IF HALF WAY THRU PLLL DUMP  
  16 2G5L ...                                   [ACTION IS SIMPLE
  11 2GK= ...      HUNTW   3,FILE,FINCPROC   
  21 2H4W ...      BPZ   3  YFP                        [J IF FINCPROC SET UP ALREADY 
  19 2HJG ...      SETBIT   2,6                         [SHOW FP NEWLY SET UP
  20 2J46 ...      SETNCORE INCLISTS-FINCPRH,2,FILE,FINCPROC[X2-> FINCPROC BLOCK 
  21 375G          STOZ     FINCNOBS(2)         [ZEROISE ^NO. OF STILL OBSOLETE INCRS
  11 37K6 ...      LDN   0  INCLISTS-FINCPRH 
  16 384Q          STO   0  FINCPRH(2)          [SET UP HEADER   
   9 387W ...      STOZ     INCRV(2) 
  10 38?2 ...      STOZ     FINCTRD(2)   
  10 38B6 ...      STOZ     INCUNFIN(2)  
  10 38F= ...      STOZ     INCBATCH(2)  
  20 38JB          LDX   0  AFREE               [ALLOW ENOUGH RVS TO USE AFREE/8 
  20 3942          SRL   0  8                   [WORDS OF RED TAPE (32 WD FREQS) 
  18 39HL          STO   0  FINCRVLIM(2)        [STORE IN FINCPROC BLOCK 
  17 39KJ ...      BRN      SCAN1               [J TO SCAN INCINDEX  
   4 39MG ...YFP 
   4 39PD ...#   
  18 39RB ...#  FINCPROC ALREADY SET UP. WE SIMPLY THROW AWAY THE INCS   
  15 39T# ...#  IN BATCH LIST( IT GETS INITIALISED LATER) .  
   4 39X= ...#   
   9 39_8 ...      CALL  4  ZEROBATCH
   4 3=3=    #   
  21 3=GW    #  READS THROUGH INCINDEX CHECKING RECORDS ARE IN ASCENDING INCR NO. ORD
  21 3?2G    #  SETS UP LIST OF ^PROCESSED BUT NOT OBSOLETE^ INCRS IN FILE.FINCPROC  
  21 3?G6 ...#  BLOCK (ALSO MON. FILE MESSAGE FOR EACH SUCH INCR.) UNLESS THIS LIST I
  12 3?_Q ...#  ALREADY SET UP (B2 OF X6 UNSET)  
   4 3#FB    #   
  19 3#H# ...#  ALSO WORK OUT HOW MANY END OF BATCH INCRS HAVE OCCURRED SINCE
  19 3#K= ...#  THE LAST RESTORABLE ONE. THIS IS USED TO HELP DECIDE WHETHER 
  18 3#M8 ...#  TO HELP US DECIDE WHETHER TO ATTEMPT A RESTORABLE INCR.  
  19 3#P6 ...#  SOME SITES INSIST ON A BATCH OR TWO OF NONREST INCS FIRST.   
   4 3#R4 ...#   
   5 3#S= ...SCAN1   
   8 3#TD ...      LDX   2  FX2  
  19 3#WL ...      STOZ     AWORK1(2)           [ZERO COUNT OF END BATCH INCS
   9 3#XS ...      STOZ     AWORK2(2)
   4 3#_2    STEP
  19 3*DL          STEP                         [X3-> NEXT INCREMENT RECORD  
  17 3*Y= ...      BZE   3  UNFINLIST           [J IF END OF INCINDEX
   7 3BCW    #SKI  K6INCSTART
   4 3BXG    (   
   9 3CC6          LDEX  0  FRH(3)   
   8 3CWQ          SBN   0  AMAGR
  20 3DBB          BNG   0  SHORT               [ GEOERR IF TOO SHORT A RECORD   
   9 3DW2          LDX   0  AWORK2(2)
  21 3F*L          BXGE  0  AINCNOR(3),SHORT    [GEOERR IF INCR NOS. IN WRONG ORDER  
  10 3FT=          LDX   0  AINCNOR(3)   
  19 3G#W          STO   0  AWORK2(2)           [REMEMBER CURRENT INCR NO.   
   4 3GSG    )   
  20 3H#6          JBC   TREDUMP,3,BIDUMPROCR   [J IF INCR NOT ^DUMP PROCESSED^  
  17 3HRQ          JBS   TREDUMP,3,BIOBSR       [J IF INCR OBSOLETE  
  20 3J?B ...      BBUS  2,6,TREDUMP            [J IF FINCPROC NOT NEWLY SET UP  
  18 3K=L          LDX   5  AINCNOR(3)          [X5= PROCESSED INCR NO.  
  19 3KQ= ...      CALL  4  MADDFINCP           [ADD INCR NO TO FINCPROC LIST
   7 3N8B          LDN   0  1
  21 3NN2          ADS   0  FINCNOBS(3)         [UPDATE NO. OF PROCESSED (BUT NOT OBS
  20 3P7L ...      ADS   0  FINCTRD(3)                [UPDATE COUNT OF SUCH INCS 
  19 3PM= ...      OUTPACK  5,1,INCNUM          [OUTPUT INCR NO. AS PARAMETER
  16 3Q6W ...      NGN   4  3                   [INITIALISE MOD. 
   5 3QLG ...SECOND  
  16 3R66 ...      ADN   4  3                   [UPDATE MODIFIER 
   7 3RKQ ...      SMO      4
  10 3S5B ...      ADN   1  MPROCESSED   
   9 3SK2 ...      ADN   2  ACOMMUNE1
   7 3T4L ...      MOVE  1  3
  21 3TJ= ...      OUTPARN  9,ACOMMUNE1         [OUTPUT ^PROCESSED^ OR ^OBSOLETE^ AS 
  18 3W3W ...      BZE   4  SECOND              [J IF 1ST TIME THROUGH   
  17 3WHG ...      MONOUT   INCNOTYET           [OUTPUT TO MON. FILE:
  21 3X36 ...                                   [^INCREMENT %A PROCESSED BUT NOT YET 
  21 3XGQ ...                                   [                               OBSOL
  18 3Y2B ...      STEPAGAIN                    [X3-> DIR. ENTRY RECORD  
   5 3YG2 ...TREDUMP 
  19 3Y_L ...      JMBAC STEP,3,BIENDBATR,BIGENRESR [J IF INC NOT RESTORABLE 
  16 3_F= ...                                   [OR END OF BATCH 
  17 3_YW ...      JBC   NOTGR,3,BIGENRESR      [J IF NOT RESTORABLE 
  20 42DG ...      STOZ     AWORK1(2)           [RESET COUNT OF END BATCH INCS   
   8 42Y6 ...      BRN      STEP 
   5 43CQ ...NOTGR   
   7 43XB ...      LDN   0  1
  16 44C2 ...      ADS   0  AWORK1(2)           [INCREASE COUNT  
   8 44WL ...      BRN      STEP 
   5 48#2    SHORT   
   7 48RL    #SKI  K6INCSTART
  21 49?=          GEOERR   BRIEFPM,INCWRORD    [INCINDEX RECORD TOO SHORT OR IN WRON
   4 49QW    #   
  20 4=2N ...#  IF THE FINCPROC IS NEWLY SET UP WE READ BACKWARDS TO THE LAST END
  21 4==G ...#  BATCH INCR RECORD SETTING UP A LIST OF INCOMPLETE INCREMENTS TO BE   
   6 4=G# ...#  REDUMPED.
   4 4=Q6    #   
   6 4?6F ...UNFINLIST   
  20 4?GS ...      BBUS     2,6,YFP2            [J IF FINCPROC NOT NEWLY SET UP  
   6 4?X7 ...UNFINLOOP   
  16 4#?G ...      READBACK                     [READ LAST RECORD
  20 4#MT ...      TESTREP2 FIRSTREC,YFP2       [J IF REACHED START OF INCINDEX  
  10 4*48 ...      MHUNTW   3,FILE,FRB   
  17 4*DH ...      JBS      UNFINEND,3,BIENDBAT   [J IF SCAN FINISHED
  19 4*SW ...      JBC      NOTNRV,3,BINSUITRV   [J IF NOT INCOMPLETE RECORD 
  16 4B99 ...      LDX   5  AINCNO(3)           [REMEMBER INCR NO
   9 4BKJ ...      CALL  4  MADDFINCP
   7 4B_X ...      LDN   0  1
  20 4CB= ...      ADS   0  INCUNFIN(3)         [UPDATE COUNT OF UNFINISHED INCS 
   5 4CQK ...NOTNRV  
   9 4D6Y ...      MFREEW    FILE,FRB
   9 4DH? ...      BRN      UNFINLOOP
   5 4DXL ...UNFINEND
   9 4F?_ ...      MFREEW   FILE,FRB 
   4 4FN# ...YFP2
  18 4G4M ...      LDX   7  INCNEWDUMP(1)       [DEFAULT 1ST INC NO -1   
  18 4GF2 ...                                   [IN CASE INCINDEX EMPTY  
  18 4GT* ...      WIND                         [WIND TO END OF INCINDEX 
   6 4H9N ...      STEP  
   7 4H*R ...#UNS  FPARTRES  
  21 4HFW ...      FLIPS    6,1                 [CLEAR B1 OF X6 (SEE COMMENTIN NEXT P
   8 4HL3 ...      BRN      RBACK
   4 4J32    #   
  20 4JB7 ...#  WE NOW PERFORM ANOTHER BACKWARDS READ SIMILAR TO THE ABOVE BUT   
  21 4JP# ...#  COLLECTING THE NUMBERS OF INCREMENTS IN THE CURRENT BATCH. WE ALSO   
  20 4K4F ...#  KEEP THE LAST RVABLE INCR RECORD AS A FILE FINTER BLOCK AND THE  
  16 4KCL ...#  LAST RECORD (RVABLE OR NOT) AS A FILE FINTER1.   
  19 4KQR ...#  WE KEEP IN X7 THE NUMBER OF THE HIGHEST INCREMENT FOR USE IN 
  13 4L5Y ...#  CALCULATING ANY NEW INC NOS WE WANT. 
   7 4L73 ...#UNS  FPARTRES  
   4 4L86 ...(   
  21 4L99 ...#  WE LEAVE BIT 1 OF X6 SET IF THERE IS A PREVIOUS INCR IN THEBATCH THAT
  21 4L=# ...#  WAS NOT TERMINATED BY SYSTEM BREAK OR WRITE FAIL. THIS MEANSTHAT A   
  19 4L?C ...#  NON-RESTRABLE INCR IS NECESSARY( EXCPET DURING A PLLLDUMP)   
  20 4L#G ...#  OTHERWISE BIT 1 WILL BE LEFT CLEAR AND A RESTORABLE INCR WILL BE 
  20 4L*K ...#  ATTEMPTED UNLESS WE ARE SUPPOSED TO PRODUCE SOME NONREST BATCHES 
   6 4LBN ...#  FIRST.   
   4 4LCR ...)   
   4 4LF6    #   
   5 4LYQ    RFREE   
  14 4MDB          FREECORE 1                   [FREE FRB
   5 4MY2    RBACK   
  20 4NCL          READBACK                     [READ PREVIOUS INCREMENT RECORD  
  21 4NX=          TESTREP2 OK,REPOK,FIRSTREC,NBATCH    [NO MORE SEARCHING IF ALREADY
  21 4PBW                                               [       POSITIONED AFTER 1ST 
   7 4PWG    #SKI  K6INCSTART
  20 4QB6          GEOERR   BRIEFPM,INCRBACK    [UNEXPECTED REPLY FROM READBACK  
   5 4QTQ    REPOK   
  10 4R*B          MHUNTW   1,FILE,FRB   
  20 4RT2          HUNTW    3,FILE,FINTER1      [X3-> FINTER1 BLOCK (LAST INCR.) 
  21 4S#L          BNG   3  SLAST               [J IF LAST INCR. & NO FINTER1 FOR IT 
  16 4SS=          LDX   4  AINCSTATE(1)        [X4= STATE WORD  
  19 4T?W          LDX   5  AINCNO(1)           [X5= INCR. NO. OF THIS RECORD
  21 4TRG          JBS   RFREE,1,BINSUITRV      [J IF THIS INCR. NOT SUITABLE RETRIEV
  10 4W?6          HUNTW    3,FILE,FINTER
  17 4WQQ          BNG   3  NLAST               [J IF NO FINTER BLOCK
  14 4X=B          FREECORE 1                   [FREE FRB
   9 4XQ2          BRN      NLASTCOMP
   5 4Y9L    SLAST   
  20 4YP= ...      NAME  1,FILE,FINTER1         [LAST INCR. REC. IN FINTER1 BLOCK
  17 4_8W          LDX   7  AINCNO(1)           [X7 = LAST INCR. NO. 
  18 4_NG          READAGAIN                    [RE-READ LAST INCR. REC. 
   8 5286          BRN      REPOK
   5 52MQ    NLAST   
  21 537B          NAME  1,FILE,FINTER          [LAST RETRIEVABLE INCR. IN FINTER BLO
   6 53M2    NLASTCOMP   
  10 53RW ...      BSXD     0,BIENDBAT   
   7 53YQ ...      ANDX  0  4
  18 545L ...      BNZ   0  NBATCH              [J IF END OF BATCH INCR  
  10 54J= ...      BSXD     0,BIWFBREAK  
   7 54P6 ...      ANDX  0  4
  21 54W2 ...      BNZ   0  WFBREAK             [PRESENCE OF WF'ED OR BROKEN INCRS   
  21 552W ...                                   [DOESNT INHIBIT ATTEMPTS AT RESTABLE 
  14 557Q ...                                   [INCRS.  
  20 55#L ...      SETBIT   1,6                 [OTHERWISE MUST DUMP NONREST INC 
  19 55FG ...                                   [(UNLESS PARALLEL DUMPING)   
   6 55G8 ...#UNS AMTGR  
   4 55GW ...(   
  11 55HJ ...      JBC      WFBREAK,,BMTGR   
  20 55J= ...      FLIPS    6,1                [ IF MTGR I.E. IF WE ARE PREPARED 
  18 55JY ...[     TO PRODUCE A RESTORABLE BATCH THEN CLEAR BIT 1 OF X6  
   4 55KL ...)   
   5 55LB ...WFBREAK 
  18 55Y6 ...      CALL  4  MADDFINCP           [ ADD INC NO TO FINCPROC 
   7 5656 ...      LDN   0  1
  20 56JQ ...      ADS   0  INCBATCH(3)         [UPDATE COUNT OF INCS IN BATCH   
   8 5=FQ          BRN      RBACK
   4 5=_B    #   
  18 5?F2 ...#  NOW WE HAVE TO DECIDE WHAT INCREMENTS NEED SETTING UP.   
   4 5?YL ...#   
   5 5#D= ...NBATCH  
  18 5#XW ...      LDX   0  AWORK1(2)         [COUNT OF END BATCH INCRS  
  21 5*CG ...      SEGENTRY K98INCSTART         [USED BY DUMPNODI TO MEND NEXT INST  
  20 5*X6 ...      SBN   0  0                   [SUBTRACT NO OF FILE ONLY BATCHES
  19 5BBQ ...                                   [THE INTALLATION WANTS FIRST 
  16 5BWB ...      BNG   0  NRESTBLE            [J IF NOT ENOUGH 
  18 5CB2 ...      SEGENTRY K80INCSTART         [PRLLDUMP NULLS NEXT INST
  17 5CTL ...      BRN      NPLLLDUMP           [J IF MACRO NOT IN   
   4 5D*= ...#   
  10 5DSW ...#  START OF PARALLEL DUMP   
   4 5F#G ...#   
  17 5G?Q ...      BS       ,BPARALLEL          [NOW IN PARALLEL MODE
  20 5GRB ...      SETBIT   1,6                 [INDICATE WE WANT A FILE ONLY INC
  15 5GXS ...      DOWN     INCSET,1            [SET IT UP   
   9 5H4= ...      BRN      YINCAB3  
  18 5H8N ...      ADN   7  1                   [INCREASE LATEST INC. NO.
   6 5H*9 ...#UNS  AMTGR 
  19 5HFM ...      JBS      YMTGR,,BMTGR        [ CLEAR LIST OF INCS IN BATCH
  17 5HHW ...      CALL  4  ZEROBATCH           [ ONLY IF NOT MTGR   
   5 5HL5 ...YMTGR   
   6 5HLK ...#UNS  AMTGR 
   4 5HM5 ...(   
  11 5HMK ...      JBC      XNOADD,,BPARALLEL
  17 5HN5 ...      MHUNTW   3,FILE,FINCPROC     [IF PLLDUMP & MTGR   
  17 5HNK ...      LDN   0  1                   [NO OF INCS IN BATCH 
  17 5HP5 ...      ADS   0  INCBATCH(3)         [MUST BE INCREASED   
   5 5HPK ...XNOADD  
   4 5HQ5 ...)   
  16 5HQL ...      OPENINC ,GENERAL            [REOPEN INCINDEX  
   4 5J== ...#   
  13 5JPW ...#  RESTORABLE INCR (SIMPLE OR PLLL CASE)
   4 5K9G ...#   
   6 5KP6 ...YRESTABLE   
  21 5L8Q ...      FLIPS    6,1                 [CLEAR BIT TO SHOW RSTABLE INC WANTED
  15 5LB? ...      DOWN     INCSET,1            [SET IT UP   
   9 5LHS ...      BRN      YINCAB2  
   4 5LP* ...#   
  19 5LQ# ...#  IF THE RESTORE TIME MACRO 'INCSTEND' IS INCLUDED AND WE ARE  
  21 5LR? ...#  STARTING A POTENTIALLY RESTORABLE INCREMENT WE ISSUE THE J.D. MACRO  
   7 5LS= ...#  'STARTGRINC' 
   4 5LT9 ...#   
  10 5LW8 ...      SEGENTRY K50INCSTART  
   9 5LX7 ...      BRN      NOWACROSS
   6 5LX= ...#UNS  AMTGR 
   4 5LX* ...(   
  10 5LXD ...      JBC      XMTGR2,,BMTGR
  11 5LXH ...      MHUNTW  3,FILE,FINCPROC   
  20 5LXL ...      BWNZ     INCBATCH(3),NOWACROSS    [J. NOT FIRST INCR. OF BATCH
   5 5LXP ...XMTGR2  
   4 5LXS ...)   
  14 5LY6 ...      SETNCORE CPDATA-A1+3,2,ADATA,CREADL   
   8 5L_5 ...      LDN   0  10   
   8 5M24 ...      STO   0  A1(2)
  10 5M33 ...      ADN   1  STARTGRINC   
   9 5M42 ...      ADN   2  CPDATA   
   7 5M4_ ...      MOVE  1  3
   9 5M5Y ...      ISSUCOM  NOWACROSS
   4 5M6X ...#   
   6 5M82 ...NOWACROSS   
  21 5MCS ...      DOWN     INCDTA,1            [RECHAIN DTA'S IN FRONT DUMPER'S CPAT
  10 5MML ...      MHUNTW   3,FILE,FINC  
  20 5N7= ...      CALL  4  MARKAPIA            [BEFORE ENTERING INCEND WE SET   
  21 5NLW ...      JBC      NOT2,,BPARALLEL       [THE 'DUMP' BIT IN THE APIA ENTRIES
  21 5P6G ...      HUNT2    3,FILE,FINC         [FOR ALL TAPES SHARING A CLUSTER WITH
  14 5PL6 ...      CALL  4  MARKAPIA            [DUMPER  
   4 5Q5Q ...NOT2
   9 5Q5X ...      BC       ,BFILEINC
  17 5Q64 ...      BC       2,MBEMS              [SHOW NOT X2 ENTRY  
  19 5Q69 ...      OPENINC  ,CLEAN              [OPEN INCINDEX IN CLEAN MODE 
   6 5Q6B ...      WIND  
  20 5Q6H ...YBACK                              [BACKSPACE TO LAST END OF BATCH  
  20 5Q6N ...                                   [RECORD, THEN SCAN FORWARD AND   
  20 5Q6T ...                                   [UPDATE THE NEW RECORDS, WRITING 
  18 5Q72 ...                                   [START SENTS AS WE DO SO.
   7 5Q77 ...      BACKSPACE 
  17 5Q7# ...      TESTRPN2 OK,YEB              [IF START IF FILE, OK
   7 5Q7F ...      STEPAGAIN 
  19 5Q7L ...      JBC      YBACK,3,BIENDBATR   [BACKSPACE AGAIN IF NOT THERE
   4 5Q7R ...YEB 
  14 5Q7Y ...      STEP                         [X3->REC 
  18 5Q85 ...      BZE   3  STARTED             [J IF ALL RECS UPDATED   
  18 5Q8= ...      JBC      YEB,3,BIUNFINR      [J IF NOT A NEW RECORD   
  11 5Q8C ...      JBS      YEB,3,BIAVESTR   
  10 5Q8J ...      MHUNTW   1,FILE,FINC  
  14 5Q8P ...      LDX   0  INCNO(1)            [INCR NO 
  19 5Q8W ...      BXE   0  AINCNOR(3),YFINC    [J IF THE 1ST FINC IS THE ONE
  19 5Q93 ...      HUNT2    1,FILE,FINC,1       [OTHERWISE IT MUST BE THE 2ND
   9 5Q98 ...      LDX   0  INCNO(1) 
  12 5Q9* ...      BXU   0  AINCNOR(3),(GEOERR)  
  19 5Q9G ...YFINC NAME     1,FILE,FGET         [RENAME IT TO DISTINGUISH IT 
   4 5Q9M ...#   
  19 5Q9S ...#  WE HAVE LOCATED A NEW RECORD AND HAVE NAMED THE CORRESPONDING
  17 5Q9_ ...#  FINC A FGET TEMPORARILY FOR EASE OF IDENTIFICATION.  
   4 5Q=6 ...#   
  20 5Q=? ...      STEPREWRITE                  [ENSURE INFORMATION SENT TO BS   
  21 5Q=D ...      BS    3,BIAVESTR             [SET 'AVAILABILITY OF INCR. VERIFIED'
   8 5Q=K ...      LDX   0  EDATE
   9 5Q=Q ...      STO   0  AINCDR(3)
  19 5Q=X ...      LDX   0  CLEANCT             [SET DATE AND TIME OF DUMP   
   9 5Q?4 ...      STO   0  AINCTR(3)
   9 5Q?9 ...      LDX   0  GMTNOW   
  19 5Q?B ...      STO   0  AINCGMTR(3)         [SET GMT (AT START) OF DUMP  
   6 5Q?H ...#UNS AMTGR  
   4 5Q?N ...(   
  18 5Q?T ...      JBC      NOTRESTBAT,,BMTGR    [ JUMP IF NOT USING MTGR
  20 5Q#2 ...      MHUNTW   1,FILE,FINCPROC       [ IF THERE ARE OTHER INCS IN   
  19 5Q#7 ...      BWZ      INCBATCH(1),NOTRESTBAT [ THE BATCH AND THIS MAY  
  20 5Q## ...      MHUNTW   1,FILE,FGET             [ BE THE LAST OF A RESTORABLE
  19 5Q#F ...      JBC      NOTRESTBAT,1,BINCGENRES  [ BATCH - SET BIRESTBAT 
  14 5Q#L ...      BS       3,BIRESTBATR              [  
   6 5Q#R ...NOTRESTBAT  
   4 5Q#Y ...)   
  20 5Q*5 ...      CALL  4  SETMONOUT           [SET PARAMETER BLOCK FOR MONOUT  
  21 5Q*= ...      INFORM   3,INCBEG,1          [OUTPUT 'START OF INCREMENT' MESSAGE 
  19 5Q*C ...                                   [   TO ALL OPERATORS CONSOLES
  10 5Q*J ...      MHUNTW   3,FILE,FGET  
   9 5Q*P ...      LDX   0  CLEANCT  
  18 5Q*W ...      STO   0  INCBEGDUMP(3)       [NOTE TIME DUMP STARTED  
  20 5QB3 ...      JBC      NOFERUS,3,BINCGENRES[FOR GENRES, DONT LIMIT MAKEDIRS 
   4 5QB8 ...#   
  20 5QB* ...#  IF WE ARE STARTING A RESTORABLE INCREMENT WE SET UP A FI FERUS   
  20 5QBG ...#  BLOCK ON THE END OF THE MISC CHAIN TO ACT AS AN INTERLOCK BETWEEN
  20 5QBM ...#  DUMPER,MAKEDIR AND ERASE TO PREVENT ANY POSSIBLITY OF TWO DIRS   
  16 5QBS ...#  WITH THE SAME USERNAME BEING DUMPED IN ONE INCR. 
   3 5QB_ ...
  16 5QC6 ...#  WE ALSO CLEAR BITS BNOWD AND BTOTD IN FIXED CORE.
   3 5QC? ...
   9 5QCD ...      MBC   ,BNOWD,BTOTD
  20 5QCK ...      SETNCORE FERUSER-FERUSRH,3,FI,FERUS [SET UP FI/ FERUS BLOCK   
  11 5QCQ ...      LDN   0  FERUSER-FERUSRH  
  17 5QCX ...      STO   0  FERUSRH(3)          [SET UP LOG LEN IN RH
  17 5QD4 ...      STOZ     FERUSNUM(3)         [INIT NO OF ENTRIES  
  19 5QD9 ...      CHAIN 3,BMISC+1              [CHAIN ON END OF MISC CHAIN  
  17 5QDB ...      BS    ,BFERUS                [SET FERUS EXISTS BIT
   5 5QDH ...NOFERUS 
   4 5QDN ...#   
  18 5QDT ...#  NEXT WE SET UP AND WRITE THE START OF INCREMENT SENTINEL 
   4 5QF2 ...#   
  17 5QF7 ...      MHUNTW   3,FILE,FQUAL        [X3-> QUALIFIER BLOCK
   9 5QF# ...      STOZ     ESEN1(3) 
  20 5QFF ...      LDN   4  ESEN1(3)            [ZEROISE ALL WORDS EXCEPT ESENTYP
  10 5QFL ...      LDN   5  ESEN1+1(3)   
   8 5QFR ...      MOVE  4  18   
   7 5QFY ...      LDN   0  6
  21 5QG5 ...      STO   0  ESENTYP(3)          [SET UP THAT START OF SUBFILE SENT.  
   8 5QG= ...      LDX   1  FX1  
  10 5QGC ...      LDN   4  NAMEINCR(1)  
  10 5QGJ ...      LDN   5  ESENLOC1(3)  
  18 5QGP ...      MOVE  4  3                   [SET UP INCREMENT NAME   
   9 5QGW ...      LDX   0  GVERSION 
  21 5QH3 ...      STO   0  ESENRL(3)           [MARK/ISSUE OF GEORGE IN WHICH INCR. 
   9 5QH8 ...      LDX   0  GMTNOW   
  18 5QH* ...      STO   0  ESEN10(3)           [SET ^GEORGE MEAN TIME^  
  20 5QHG ...      LDCT  0  #200                [SET ^START OF INCR. SENT.^ BIT  
  20 5QHM ...      ORN   0  #100                [FORMAT VERSION NO.=2 IN B15-18  
   9 5QHS ...      STO   0  ESENB(3) 
  10 5QH_ ...      LDX   0  MLANCOMP(1)  
  20 5QJ6 ...      STO   0  ESENLAN(3)          [SET LANGUAGE OF S/F AS COMPOSITE
  10 5QJ? ...      MHUNTW   1,FILE,FGET  
  15 5QJD ...      NAME     1,FILE,FINC         [NAME IT BACK
   9 5QJK ...      LDX   0  INCNO(1) 
  20 5QJQ ...      STO   0  ESENFG(3)           [SET UP INCR. NO. IN QUAL. BLOCK 
   9 5QJX ...      LDX   0  INCACT(1)
  21 5QK4 ...      BNZ   0  YEB                 [DONT WRITE TO TAPE IF FAILED OR MALP
  21 5QK9 ...      CHAIN    1,2                 [CHAIN FINC TO CPAT FOR EFFICIENCY   
  17 5QKB ...      DOWN     INCMON,3          [WRITE A START SENTINEL
  20 5QKH ...      BRN      YEB                 [LOOP UNTIL ALL NEW RECS UPDATED 
   4 5QKN ...#   
  19 5QKT ...#  ALL INCREMENTS ARE NOW OFFICIALY 'STARTED'. WE NOW FINISH OUR
  14 5QL2 ...#  PREPARATIONS AND GET ON WITH THE DUMP.   
   4 5QL7 ...#   
  16 5QL# ...#  A FLOCNB AND CREADL ARE USED BY THE DUMPING CODE 
   4 5QLF ...#   
   5 5QLL ...STARTED 
  14 5QLR ...      CLOSETOP                    [INCINDEX 
  17 5QLY ...      SETNCORE 6,2,FILE,FLOCNB     [SET UP 6 WORD FLOCNB
  21 5QM5 ...      SETNCORE CPDATA-A1+21,2,ADATA,CREADL   [SET UP BLOCK FOR OUTMONS  
  20 5QM= ...      JBC      NPL2,,BPARALLEL      [THE FOLLOWING IS ONLY NEEDED   
  18 5QMC ...                                   [DURING PARALLEL DUMPS   
   4 5QMJ ...#   
  19 5QMP ...#  DURING A PARALLEL DUMP THE FINC FOR THE RESTORABLE INCR MUST 
  17 5QMW ...#  BE NEXT TO THE CPAT. IN ADDITION, WE NEED A FINCCOPY 
  17 5QN3 ...#  BLOCK TO DUMP THE FINC BLOCK INTO UDER CERTAIN CIRCS 
   4 5QN8 ...#   
  11 5QN* ...      MHUNTW      3,FILE,FINC   
  20 5QNG ...      JBS      YGRFINC,3,BINCGENRES[IS FINC NEXT TO CPAT RESORABLE  
  20 5QNM ...      HUNT2    3,FILE,FINC,3       [IF NOT FIND AND RECHAIN THE REST
  14 5QNS ...      CHAIN     3,2                [FINC.   
   5 5QN_ ...YGRFINC 
  10 5QP6 ...      LDX   4  ALOGLEN(3)   
  11 5QP? ...      SETUPCORE 4,3,FI,FINCCOPY 
   4 5QPD ...#   
  19 5QPK ...#  ALSO IN PARALLEL MODE THERE MAY BE A FABSNB WHICH IS TO BE   
  17 5QPQ ...#  USED LEFT OVER FROM APREVIOUS ENTRY TO INCEND. IF SO 
  14 5QPX ...#  IT WILL BE CALLED A FI FDUMPFAB BLOCK.   
   4 5QQ4 ...#   
  20 5QQ9 ...      HUNTW    3,FI,FDUMPFAB       [IF IN PARALLEL AND FABSNB SET UP
  17 5QQB ...      BNG   3  NPL2                [DONT SET UP ANOTHER 
  17 5QQH ...      NAME     3,FILE,FABSNB       [RENAME IT  FOR REUSE
  10 5QQN ...      BRN      YFBALREADY   
   4 5QQT ...NPL2
  21 5QR2 ...      SETNCORE 4,2,FILE,FABSNB     [SET UP INITIAL FABSNB WITH MASTER   
   7 5QR7 ...      LDN   0  4
   8 5QR# ...      STO   0  A1(2)
   9 5QRF ...      LDN   0  MASTER(1)
   9 5QRL ...      LDN   1  A1+1(2)  
   7 5QRR ...      MOVE  0  3
   6 5QRY ...YFBALREADY  
  10 5QS5 ...      MHUNTW   3,FILE,FINC  
   6 5QS= ...#UNS AMTGR  
   4 5QSC ...(   
  19 5QSJ ...      JBC      NOTPGR,3,BINCGENRES   [ IF POTENTIALLY RESTORABLE
  15 5QSP ...      BS       FX2,MBPGENRES         [ SET BIT  
   5 5QSW ...NOTPGR  
   4 5QT3 ...)   
  17 5QT8 ...      JBC      NOTOTAL,3,BINCTOTD  [J IF NOT TOTAL DUMP 
   9 5QT* ...      MONOUT   INCTOT   
   5 5QTG ...NOTOTAL 
  14 5QTM ...    CALL  7  OUTBLANK            [BLANK LINE
  17 5QTS ...      MONOUT   INCMON1             [OUTPUT HEADINGS TO  
  18 5QT_ ...      MONOUT   INCMON2             [               MON. FILE
  15 5QW6 ...      CALL  7  OUTBLANK            [BLANK LINE  
  21 5QW? ...      LONGON   ASTTDUMP            [WAKE ACTS WAITING FOR START OF INCR 
   6 5QWD ...#UNS FNSAD  
   4 5QWK ...(   
  16 5QWQ ...      SETNCORE  4,1,FI,FDUMPCOM    [ SET UP FDUMPCOM
  17 5QWX ...      BC       1,BFDUMPCOM               [ CLEAR BIT AND
  17 5QX4 ...      BS        ,BDUMPING                [ SHOW DUMPING 
  20 5QX9 ...      CHAIN    1,BMISC+1           [ AND CHAIN AT END OF MISC CHAIN 
  11 5QXB ...      SETNCORE  5,1,FI,INDEXCHK 
  12 5QXH ...#     BLOCK FOR CHECKING DIRECTORY  
   4 5QXN ...)   
   4 5QXT ...#   
  15 5QY2 ...#  AT LAST WE GET ROUND TO DOING SOME DUMPING.  
   4 5QY7 ...#   
  17 5QY# ...      ACROSS    INCWRITE,1          [DUMP AN INCREMENT  
   4 5R52 ...#   
  15 5RJL ...#  SIMPLE CASE, ENOUGH END BATCH INCRS PRODUCED 
   4 5S4= ...#   
  18 5S#4 ...      SEGENTRY K81INCSTART         [USED BY 'PRLLDUMP OFF'  
   6 5SHW ...NPLLLDUMP   
   4 5SL_ ...#   
  20 5SN2 ...#  IF THERE IS AN INCR IN THE BATCH THAT WAS NOT TERMINATED BY FAIL 
  21 5SP3 ...#  OR BREAK A NON-RESTORABLE INCR IS NECESSARY. OTHERWISE WE CLEAR THE  
  20 5SQ4 ...#  LIST OF PREVIOUS INCRS IN THE BATCH AND ATTEMPT A RESTABLE INCR. 
   4 5SR5 ...#   
   6 5SR9 ...#UNS  AMTGR 
   4 5SR* ...(   
  19 5SRF ...#     UNLESS WE ARE PREPARED TO PRODUCE A RESTORABLE BATCH OF   
  18 5SRK ...#     INCREMENTS , CLEAR THE LIST OF INCREMENTS IN THE BATCH
  18 5SRP ...#     OTHERWISE  THE BATCH IS VITAL !*!*!*!*!*!*!*!*!*!*!*! 
  20 5SRT ...      JBS     YRESTABLE,,BMTGR       [ PRODUCE A 'RESTORABLE' INC.  
   4 5SR_ ...)   
  21 5SS6          BBS   1,6,NRESTBLE         [ JUMP IF EOT IN PREVIOUS POT GEN RES I
  18 5ST7 ...      CALL  4  ZEROBATCH           [CLEAR INCR BATCH LIST   
   9 5SW8 ...      BRN      YRESTABLE
   5 5X22 ...NRESTBLE
  19 5XFL ...      SETBIT   1,6                 [SHOW FILE ONLY INC WANTED   
  15 5XS3 ...      DOWN     INCSET,1            [SET IT UP   
   9 5Y6D ...      BRN      YINCAB3  
   9 5YDW ...      BRN      NOWACROSS
   4 5YYG ...#   
   4 5_D6 ...#   
  18 5_XQ ...#  SETTING UP SECOND OR LATER FILE INCR OF A PARALLEL DUMP  
   4 62CB ...#   
   5 62X2 ...PRLLSET 
  19 63BL ...      WIND                         [POSITION AT END OF INCINDEX 
  16 63W= ...      STEPAGAIN                    [GET LAST RECORD 
  17 64*W ...      LDX   7  AINCNOR(3)          [PICK UP ITS INCR NO 
  19 64TG ...      BRN      NRESTBLE            [AND SET UP A NEW FILEINCR   
   4 65*6 ...#   
   5 65SQ ...YINCAB2 
  12 66#B ...      JBS      PRLLSET,,BPARALLEL   
  11 66*8 ...#   THIS CODE CORRECTS BUG 1116 
  18 66B2 ...#   ENSURE INCINDEX CLOSED BEFORE GOING ACROSS TO INCWAIT   
   5 66BS ...YINCAB3 
  16 66CL ...#              TEST IF INCINDEX OPEN AT TOP LEVEL   
  16 66DD ...      TOPFCB   3                   [X3 -> TOP FCB   
   8 66F= ...      LDX   1  FX1  
   7 66G4 ...      LDN   6  2
   5 66GW ...YINCAB6 
   7 66HN ...      SMO      6
   9 66JG ...      LDX   7  FLOC1(3) 
   7 66K# ...      SMO      6
  10 66L6 ...      TXU   7  XINCIND(1)   
   9 66LY ...      BCS      YINCABAND
   7 66MQ ...      SBN   6  1
   9 66NJ ...      BPZ   6  YINCAB6  
  16 66PB ...#                   INCINDEX IS OPEN AT TOP LEVEL   
  16 66Q8 ...      CLOSETOP                     [CLOSE INCINDEX -
  19 66R2 ...#   ---------------------------------------------------------   
   6 66S2 ...YINCABAND   
   8 673S ...      LDCT  7  #200 
   9 67?L ...      ACROSS   INCWAIT,1
   4 7H9W    #   
   4 7HPG    #   
  11 7J96          MENDAREA 30,K99INCSTART   
   4 7JNQ    #   
   4 7K8B    #END
   8 ____ ...40363621000600000000
