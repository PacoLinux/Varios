  15 22FL ...      SEG      TRANFAIL,863,FRANK BLUNDELL,BSTS 
   4 23YG    #   
  11 24D6          SEGENTRY K1TRANFAIL,QK1TR 
   4 27TG    #   
  14 27Y5 ...#   ENTRY POINTS FOR UNANTICIPATED CLOSEDOWN
   4 282N ...#   
  14 285? ...      FSHENTRY K10TRANFAIL,XXSHFS,,XXSHFS   
  14 287W ...      FSHENTRY K11TRANFAIL,XSHFSB,,XSHFSB   
   4 28=F ...#   
   4 28*6    #   
  17 28SQ    # K1TRANFAIL DEALS WITH TRANSFER FAILS FROM TFA ACTIVITY
   4 29#B    #   
  10 29S2    # IDENTIFIERS FOR BSTB/BLUFF
  16 2=?L    #OPT  STRAN=A1                     [TYPE OF TRANSFER
   8 2=R=    #OPT  SLENG=STRAN+1 
   8 2?=W    #OPT  SBLNUM=SLENG+1
  15 2?QG    #OPT  SCORE=SBLNUM+1               [CORE ADDRESS
  15 2#=6    #OPT  SPCA=SCORE+1                 [PCA ADDRESS 
  18 2#PQ    #OPT  SDATUM=SPCA+1                [DATUMISED BLOCK NUMBER  
   8 2*9B    #OPT  SBSA=SDATUM+1 
  16 2*P2    #OPT  SNEWB=SBSA+1                 [NEW BLOCK NUMBER
  19 2B8L    #OPT  SMARK=SNEWB+1                [MARKER IF FAILED BLOCKLETS  
  19 2BN=    #OPT  SNEWDAT=SMARK+1              [DATUMISED BLOCK NUMBER (NEW)
   8 2C7W    #OPT  SLFN=SNEWDAT+1
   7 2CMG    #OPT  SP1=SLFN+1
  19 2D76    #OPT  SP2=SP1+1                    [(                          )
  19 2DLQ    #OPT  SP3=SP2+1                    [(BACKREAD/WRITE PARAMETERS )
  19 2F6B    #OPT  SP4=SP3+1                    [(                          )
  19 2FL2    #OPT  SP5=SP4+1                    [(                          )
  19 2G5L    #OPT  START=SP5+1                  [ START OF 128 WORD BUFFER   
   4 2GK=    #   
  15 2H4W    #OPT  SBONL=2                      [ONLINE BIT  
   4 2HJG    #   
  13 2HKX ...#   LINKS FOR UNANTICIPATED CLOSEDOWN   
   4 2HM# ...#   
   8 2HNP ...      FSHCODE  AORB 
   4 2HQ6 ...(   
   5 2HRH ...SCDLINK1
  10 2HSY ...      PAIR     TRANFAIL,10  
   5 2HW* ...SCDLINK2
  10 2HXQ ...      PAIR     TRANFAIL,11  
   4 2H_7 ...)   
   4 2J2J ...#   
   4 2J46    #   
   9 2JHQ    TERR1 GEOERR   0,WRNGBLK
   5 2JNY ...XNFILE  
   9 2JW6 ...      GEOERR   0,WRNGLFN
   7 2K3B    XM1   #77777774 
   4 2KH2    #   
   6 2L2L    QK1TR [*****
   5 2L5H ...PSTART  
  10 2L8D ...      HUNTW  1,BSTB,BWRITE  
   9 2L=Q ...      BNG   1  XSTART   
  18 2L*4 ...      DOWN     BSBLFAIL,1          [DEAL WITH BLANKET FAILS 
  19 2LCB ...      BRN      TDE                 [SEE IF ANYTHING MORE TO DO  
   5 2LFN ...XSTART  
   9 2LJ4 ...      ADN   2  BSCHANAD 
   8 2LL_ ...      TXU   2  0(2) 
   8 2LPW ...      BCC      TERR1
   9 2LSR ...      LDX   2  FPTR(2)  
  18 2LXN ...      SBN   2  CHAINADD            [ POINTER TO QUEUE BLOCK 
   9 2M2K ...      LDX   7  ATYPE(2) 
  16 2M5G ...      SBX   7  CACT                [ ACTIVITY TYPE  
  17 2M8C ...      BNG   7  XNACT               [ J IF NOT ACTIVITY  
  17 2M?# ...      LDX   0  BA1(2)              [ MODE FROM ACTIVITY 
   8 2MB9 ...      BRN      XMODE
  18 2MF6 ...XNACT LDX   0  ATRAN(2)            [ MODE FROM BSTS BQBLK   
   7 2MJ3 ...XMODE SLC   0  7
  16 2MLY ...      ANDN  0  #17                 [ ISOLATE MODE   
  19 2MPT ...      BNZ   0  XNR                 [ J NOT READ TRANSFER FAIL   
  18 2MSQ ...      DOWN     READFAIL,2           [ GO TO DEAL WITH FAIL  
  10 2MXM ...      LDX   3  BSCHANAD(2)  
  17 2N2J ...      OFFQUE   3                   [ DECHAIN QUEUE BLOCK
   9 2N5F ...      SBN   3  CHAINADD 
  17 2N8B ...      BPZ   7  XACT                [ J IF ACTIVITY BLOCK
  20 2N?? ...      LDX   7  BIT10               [ BACKWAIT BIT - SET BY ACTIVITY 
  19 2NB8 ...      ANDX  7  ATRAN(3)            [ TEST IF BIT SET IN BQBLK   
   7 2NF5 ...      LDX   1  3
  16 2NJ2 ...XBW   LDX   3  BPTR(3)             [ CHAIN BACK TO  
  15 2NLX ...      LDX   0  ATYPE(3)            [   ACTIVITY 
  15 2NPS ...      TXL   0  CACT                [     BLOCK  
   8 2NSP ...      BCS      XBW  
  15 2NXL ...      FREECORE 1                   [ FREE  BQBLK
  21 2P2H ...      BZE   7  XFIN                [ DONT FPUT IF ACTIVITY NOT WAITIG   
   7 2P5D ...XACT  LDX   2  3
  16 2P8* ...      FPUT                         [ FPUT ACTIVITY  
  18 2P?= ...XFIN  BRN      TDE                 [ J TO SEE IF MORE TO DO 
   4 2PB7 ...XNR 
   8 2PH_ ...      LDX   1  FX1  
  10 2PLW ...      HUNT     3,BSTB,BLUFF 
  20 2PPR ...      BPZ   3  TA1                 [JUMP IF ALREADY GOT DATA BLOCK  
  13 2PSN ...      SETNCORE START-A1+1,3,BSTB,BLUFF  
   9 2PXL    TA1   STOZ     SMARK(3) 
   8 2S72 ...      LDX   2  FX2  
   7 2S75 ...#UNS  FSHTEST   
   4 2S78 ...(   
  17 2S7? ...      LDX   2  BSCHANAD(2)         [POINTER TO QBLOCK   
   9 2S7B ...      SBN   2  CHAINADD 
   9 2S7F ...      LDX   0  ATYPE(2) 
   8 2S7J ...      TXL   0  CACT 
  17 2S7M ...      BCC      SFT2                [J IF ACTIVITY BLOCK 
   9 2S7Q ...SFT1  LDX   2  BPTR(2)  
   9 2S7T ...      LDX   0  ATYPE(2) 
   8 2S7Y ...      TXL   0  CACT 
   8 2S83 ...      BCS      SFT1 
  17 2S86 ...SFT2  JBC      SFT3,2,BFSHACTB     [J IF M/C 'A' QBLOCK 
   8 2S89 ...      LDX   2  FX2  
  18 2S8# ...      BS       2,BFSHACTB          [MAKE TFA RUN IN M/C B   
   6 2S8C ...      FPUT  
   6 2S8G ...      COOR1 
  10 2S8K ...      MHUNT    3,BSTB,BLUFF 
   8 2S8N ...SFT3  LDX   2  FX2  
   4 2S8R ...)   
  10 2S99 ...      LDX   2  BSCHANAD(2)  
   9 2S*Q          SBN   2  CHAINADD 
  17 2STB          LDX   0  ATYPE(2)            [CHECK  Q BLOCK TYPE 
   8 2T*2          TXL   0  CACT 
  17 2TSL          BCS      TA2                 [JUMP IF NOT ACTIVITY
  17 2W#=          LDN   4  BA1(2)              [RESCUE PARAMETERS   
  17 2WRW          LDX   6  ACOMMUNE9(2)        [ DATUMISED BLOCK NO.
  15 2X?G          LDX   7  ACOMMUNE8(2)        [ BSA ADDRESS
   8 2XR6          BRN      TA3  
   4 2Y=Q    TA2 
   9 2YQB    #SKI  K6TRANFAIL>99-99  
   4 2_=2    (   
  17 2_PL          TXU   0  STYPE(1)            [CHECK IF BSTB/BQBLK 
  17 329=          BCS      TERR1               [JUMP IF NOT - ERROR 
   4 32NW    )   
   9 338G          LDN   4  ATRAN(2) 
  10 33N6          LDX   6  ADBLKNO(2)   
   9 347Q          LDX   7  ABSA(2)  
   9 34MB    TA3   LDN   5  STRAN(3) 
   7 3572          MOVE  4  5
   9 35LL          STO   6  SDATUM(3)
   9 366=          STO   7  SBSA(3)  
   9 36KW          LDX   0  SLENG(3) 
   8 375G          ANDN  0  #377 
   9 37K6          STO   0  SLFN(3)  
   8 384Q          LDN   0  #400 
   9 38JB          ANDX  0  STRAN(3) 
   9 3942          BNZ   0  XCHANGE  
   7 397X ...#UNS  FSHTEST   
   4 39?S ...#SKI
   4 39CP ...(   
   9 39HL    #SKI  K6TRANFAIL>99-99  
   4 3=3=    (   
  12 3=GW          TRACE    SBLNUM(3),TRANFAIL   
   9 3?2G          TRACE    SLFN(3), 
   9 3?G6    #SKI  K6TRANFAIL>199-199
   4 3?_Q    (   
   9 3#FB          TRACE    STRAN(3),
   9 3#_2          TRACE    SBSA(3), 
   4 3*DL    )   
   4 3*Y=    )   
   4 3B84 ...)   
   9 3BCW    TZ2   LDX   7  SLFN(3)  
   8 3BXG ...# *      PHOTO    5 
   9 3C25 ...      FSHCODE  B,SHFSP1 
   4 3C4N ...(   
  21 3C7? ...      FSHACTPR                     [SET UP PAIRED BTFAIL IN MACHINE A   
   5 3C9W ...SHFSP1  
   4 3C#F ...)   
  19 3CC6          GETBACK                      [GET A BLOCK OF BACKING STORE
   9 3CFP ...      FSHCODE  B,SHFSK1 
   4 3CJ# ...(   
  20 3CLX ...      FSHKILL                      [KILL PAIRED BTFAIL IN MACHINE A 
   5 3CPG ...SHFSK1  
   4 3CS5 ...)   
   9 3CWQ ...# *      TESTMOVE 5,TA3A
  10 3DBB          MHUNT    3,BSTB,BLUFF 
   9 3DW2    TA3A  STO   7  SNEWB(3) 
   9 3F*L          LDX   1  SLFN(3)  
   7 3FT=          SLL   1  1
   8 3G#W          ADX   1  ALFTP
   8 3GSG          LDX   4  A1(1)
  21 3H#6          BNG   4  XNFILE              [JUMP IF THIS LFN DOES NOT EXIST-ERRO
   8 3HRQ          ANDN  4  #7777
  20 3J?B          ADX   4  AFDTP               [X4 POINTS TO ENTRY IN FD TABLE  
  14 3JR2          DATUMBLK SNEWB(3),4          [DATUMISE
  21 3K=L          BZE   1  XNFILE              [JUMP IF BLOCK NO. OUT OF RANGE -ERRO
  10 3KQ=          MHUNT    3,BSTB,BLUFF 
  18 3L9W          STO   1  SNEWDAT(3)          [SAVE DATUMISED ADDRESS  
   9 3LPG    #SKI  K6TRANFAIL>99-99  
   4 3M96    (   
  11 3MNQ          TRACE SNEWB(3),NEWBLOCK   
   9 3N8B    #SKI  K6TRANFAIL>199-199
  10 3NN2          TRACE    SNEWDAT(3),  
   4 3P7L    )   
   9 3PM=          LDX   0  SLENG(3) 
   7 3Q6W          SRL   0  8
   9 3QLG          SBN   0  3*128+1  
  21 3R66          BPZ   0  VA1                 [JUMP IF MORE THAN 3BLOCKLETS INVOLVE
  10 3RKQ          LDN   0  129+START-A1 
  10 3S5B          TXL   0  ALOGLEN(3)   
  21 3SK2          BCS      TA3B                [JUMP IF BLOCK LONG ENOUGH  FOR BUFFE
  11 3T4L          ALTLEND  3,129+START-A1   
  10 3TJ=          MHUNT    3,BSTB,BLUFF 
   9 3W3W    TA3B  LDX   5  SLFN(3)  
   9 3WHG          LDX   4  SBLNUM(3)
  18 3X36          ANDX  4  BSTSMASK            [ROUND DOWN FOR SEARCH   
  12 3XGQ          FINDFAIL 7,4,5,PFL,TA4,SBSA(3)
  12 3Y2B                                       [
  20 3YG2                                       [FIND PERMANENT FAIL ENTRY IF ANY
   7 3Y_L          SMO      7
   7 3_F=          LDX   0  1
  20 3_YW          SLC   0  6                   [GET BLOCKLET BITS FROM RECORD   
   8 42DG          ANDN  0  #17  
   9 42Y6          STO   0  SMARK(3) 
   9 43CQ    #SKI  K6TRANFAIL>199-199
  11 43XB          TRACE    SMARK(3),BLOCKLET
   7 44C2    TA4A  LOCK     3
   7 44WL          LDN   4  4
   9 45B=          LDX   5  SMARK(3) 
  20 45TW          LDN   0  128                 [PARAMETERS FOR BACKREAD/WRITE   
   9 46*G          STO   0  SP1(3)   
   9 46T6          LDX   0  SLFN(3)  
   9 47#Q          STO   0  SP2(3)   
   9 47SB          LDX   0  SBLNUM(3)
   9 48#2          ANDX  0  XM1(1)   
   9 48RL          STO   0  SP3(3)   
   9 49?=          LDN   0  START(3) 
   9 49QW          STO   0  SP4(3)   
   9 4==G          LDX   0  SNEWB(3) 
   9 4=Q6          STO   0  SP5(3)   
   7 4?9Q    TB2   LDN   6  0
   7 4?PB          SRL   56 1
  20 4#92          BNG   6  TB1                 [JUMP IF THIS BLOCKLET UNREADABLE
  17 4#NL          BACKREAD BSET+FLAW,SP1(3),TC1,SP2(3),SP3(3),SP4(3)
  18 4*8=          BACKWRITE  BSET+FLAW,SP1(3),TZ1,SP2(3),SP5(3),SP4(3)  
   7 4*MW    TB1   LDN   0  1
  18 4B7G          ADS   0  SP5(3)              [UPDATE BLOCKLET NUMBERS 
  12 4BM6          ADS   0  SP3(3)              [
   8 4C6Q          BCT   4  TB2  
  16 4CLB          UNLOCK   3                   [UNLOCK BUFFER   
   8 4D62          BRN      VA1  
   4 4DKL    #   
  15 4F5=    TA4   STOZ     SMARK(3)            [NO PFL ENTRY
   7 4FJW          LDN   7  0
   8 4G4G          BRN      TA4A 
   4 4GJ6    #   
   8 4H3Q    TC1   LDN   0  #20  
   7 4HHB          SMO      4
   7 4J32          SRL   0  0
  17 4JGL          ORS   0  SMARK(3)            [SET APPROPRIATE BIT 
   9 4K2=    #SKI  K6TRANFAIL>199-199
  11 4KFW          TRACE    SMARK(3),NEWFAIL 
   8 4K_G          BRN      TB1  
   4 4LF6    #   
  16 4LYQ    TZ1   UNLOCK   3                   [WRITE  FAILURE  
   9 4MDB    #SKI  K6TRANFAIL>199-199
  11 4MY2          TRACE    SNEWB(3),BLKFAIL 
   8 4NCL ...# *      PHOTO    5 
  17 4NX=          LISTFAIL SP2(3),SNEWB(3)     [PUT BLOCK NO. IN FFL
   9 4PBW ...# *      TESTMOVE 5,TZ2 
  10 4PWG          MHUNT    3,BSTB,BLUFF 
   8 4QB6          BRN      TZ2  
   4 4QTQ    #   
   4 4R*B    #   
   5 4RT2    XCHANGE 
   9 4S#L    #SKI  K6TRANFAIL>199-199
  12 4SS=          TRACE    SBLNUM(3),TFCHANGE   
   8 4SSF ...      FSHCODE  AORB 
   4 4SSN ...(   
   4 4SSX ...#   
   4 4ST6 ...#   
   4 4ST* ...#   
   4 4STJ ...#   
   9 4STR ...      LDX   7  SBLNUM(3)
  18 4SW2 ...      ANDX  7  BSTSMASK            [BLOCK NO ROUNDED DOWN   
   9 4SW9 ...      LDX   6  SLFN(3)  
  19 4SWD ...      FINDFAIL 4,7,6,PFL,XCERR,SBSA(3) [GET POINTER TO PFL ENTRY
   9 4SWM ...      LDX   1  SBLNUM(3)
   7 4SWW ...      ANDN  1  3
   7 4SX5 ...      SRC   1  2
  10 4SX# ...      LDCH  7  BSMAGIC(1)   
   7 4SXH ...      STO   7  0
   9 4SXQ ...      LDX   5  SLENG(3) 
   7 4SX_ ...      SRL   5  8
   5 4SY8 ...SHFS1   
   8 4SYC ...      SBN   5  GSBS4
   8 4SYL ...      BNG   5  SHFS2
   8 4SYT ...      BZE   5  SHFS2
   7 4S_4 ...      BCHX  1  /
  10 4S_? ...      LDCH  7  BSMAGIC(1)   
   7 4S_G ...      ADX   0  7
   8 4S_P ...      BRN      SHFS1
   5 4S_Y ...SHFS2   
  15 4T27 ...      LDX   1  4                   [PFL POINTER 
   8 4T2B ...      LDX   7  1(1) 
   7 4T2K ...      SLC   7  6
   8 4T2S ...      ANDN  7  #17  
   7 4T33 ...      ERX   0  7
   7 4T3= ...      ANDX  0  7
  19 4T3F ...      BZE   0  SHFSA               [J IF ALL CORRUPTION CLEARED 
   7 4T3N ...      SRC   0  6
  16 4T3X ...      STO   0  1(1)                [RESET PFL ENTRY 
   8 4T46 ...      DSA   6  1(1) 
   8 4T4* ...      LDCT  0  #200 
   8 4T4J ...      ORS   0  1(1) 
   9 4T4R ...      LDX   0  SLFN(3)  
  10 4T52 ...      STO   0  ACOMMUNE1(2) 
   9 4T59 ...      LDX   0  SBLNUM(3)
  10 4T5D ...      STO   0  ACOMMUNE2(2) 
   8 4T5M ...      LDX   0  1(1) 
  10 4T5W ...      STO   0  ACOMMUNE3(2) 
  19 4T65 ...      CALL  0  XTRAN               [RESET PFL ENTRY IN OTHER M/C
   9 4T6# ...      LDX   7  SBLNUM(3)
   9 4T6H ...      STO   7  SNEWB(3) 
   9 4T6Q ...      LDX   7  SDATUM(3)
  10 4T6_ ...      STO   7  SNEWDAT(3)   
  10 4T78 ...      LDX   2  BSCHANAD(2)  
   9 4T7C ...      SBN   2  CHAINADD 
   8 4T7L ...      BRN      TB3Z 
   4 4T7T ...#   
  20 4T84 ...#     SUBROUTINE TO GO TO ALIEN M/C TO UPDATE OR DELETE PFL ENTRY   
   4 4T8? ...#   
   5 4T8G ...XTRAN   
   8 4T8P ...      SBX   0  FX1  
   9 4T8Y ...      STO   0  AWORK4(2)
  12 4T8_ ...      SETNCORE 1,3,FSH,FSHAMBLES   [
  21 4T92 ...      LDX   0  SCDLINK1(1)         [ SET UP BLOCK FOR UNANT. CLOSEDOWN  
  12 4T93 ...      STO   0  FSHCDLINK(3)        [
  19 4T97 ...      TRANSBEG FSHTFID,RITEFAIL,7,NOBLOCKS,,ACOMMUNE4,XSHFS,SUP 
   8 4T98 ...      BRN      XSHFS
   5 4T99 ...XXSHFS  
   7 4T9= ...      FSHACCWK  
   5 4T9B ...XSHFS   
  10 4T9F ...      MFREEW   FSH,FSHAMBLES
  10 4T9K ...      MHUNT    3,BSTB,BLUFF 
   9 4T9S ...      LDX   0  AWORK4(2)
   8 4T=3 ...      ADX   0  FX1  
   7 4T== ...      EXIT  0  0
   5 4T=F ...SHFSA   
  10 4T=N ...      LDX   2  BSCHANAD(2)  
   9 4T=X ...      SBN   2  CHAINADD 
   8 4T?6 ...      LDN   0  #400 
   4 4T?* ...)   
   9 4T?W          LDX   7  STRAN(3) 
   8 4TRG          BPZ   7  XCH1 
   9 4W?6          ERS   0  ATRAN(2) 
   8 4WQQ          BRN      XCH2 
   9 4X=B    XCH1  ERS   0  BA1(2)   
   9 4XQ2    XCH2  LDX   7  SBLNUM(3)
  18 4Y9L          ANDX  7  BSTSMASK            [ROUND DOWN FOR SEARCH   
   9 4YP=          LDX   5  SLFN(3)  
   8 4YQ_ ...      FSHCODE  AORB 
   4 4YSN ...(   
   8 4YWC ...      LDX   2  FX2  
  13 4YY6 ...      STO   5  ACOMMUNE1(2)        [LFN 
  14 4Y_T ...      STO   7  ACOMMUNE2(2)        [BLOCK NO
  20 4_3J ...      STOZ     ACOMMUNE3(2)        [MARKER TO DELETE THIS PFL ENTRY 
  17 4_5? ...      CALL  0  XTRAN               [IN THE ALIEN MACHINE
   4 4_72 ...)   
   7 4_8W          LDX   6  3
  13 4_NG          FINDFAIL 3,7,5,PFL,XCERR,SBSA(3)  
   7 5286          NGN   1  1
   8 52MQ    XC2   LDX   0  2(3) 
   8 537B          STO   0  0(3) 
   7 53M2          TXU   0  1
   8 546L          BCC      XC1  
   7 54L=          ADN   3  1
   8 555W          BRN      XC2  
   7 55KG    XC1   LDX   3  6
  19 5656          BZE   4  VA2                 [J IF JUST REMOVING PFL ENTRY
   9 56JQ          STO   7  SNEWB(3) 
   9 574B          LDX   6  SBLNUM(3)
  17 57J2          ANDN  6  3                   [GET BLOCKLET BITS   
  18 583L          ORS   6  SNEWB(3)            [AND PUT IN NEW BLOCK NO.
   9 58H=          LDX   6  SBLNUM(3)
   9 592W          LDX   1  SDATUM(3)
  10 59GG          STO   1  SNEWDAT(3)   
   9 5=26          LDX   0  SBSA(3)  
   8 5=FQ          SMO      FX2  
   9 5=_B          STO   0  AWORK1   
   8 5?F2          BRN      XC3  
   9 5?YL    XCERR GEOERR   0,NOPFL  
  19 5#D=    # NOW TO CHANGE THE BLOCK NUMBERS .REQUEUE Q BLOCKS,IF NECESSARY
  10 5#XW    # AND UNSET ANY FAIL BITS   
   4 5*CG    #   
   8 5*X6    VA1   LDX   2  FX2  
   7 5BBQ          LDN   4  0
  20 5BWB          LDCT  0  #200                [IF ANOTHER FAILURE IN A CORRUPT 
  19 5CB2          ANDX  0  SBLNUM(3)           [BLOCK REMOVE OLD PFL ENTRY  
   8 5CTL          BNZ   0  XCH2 
   4 5D*=    VA2 
   9 5DSW          LDX   0  SLFN(3)  
  14 5F#G          STO   0  ACOMMUNE1(2)        [L.F.N   
   9 5FS6          LDX   0  SBLNUM(3)
   9 5G?Q          ANDX  0  BSTSMASK 
  15 5GRB          STO   0  ACOMMUNE2(2)        [BLOCK NUMBER
   9 5H?2          LDX   0  SBSA(3)  
  15 5HQL          STO   0  AWORK1(2)           [BSA ADDRESS 
  19 5J==          CALL  0  XLIST               [CHANGE ENTRY FROM CFL TO FFL
  10 5JPW          MHUNT    3,BSTB,BLUFF 
   9 5K9G          LDX   0  SMARK(3) 
  18 5KP6          BZE   0  TB3                 [JUMP IF NO ENTRY IN PFL 
   7 5KYY ...      FSHSKIP   
  12 5L8Q          HUNT2    2,BSTB,BFAIL,SBSA(3) 
   8 5L9? ...      FSHCODE  AORB 
   4 5L9S ...(   
   5 5L=* ...SHFS5   
  12 5L=W ...      HUNT2    2,BSTB,BFAIL,SBSA(3) 
   8 5L?C ...      LDX   0  A1(2)
  21 5L?Y ...      BPZ   0  SHFS6               [J IF BLOCK NOT BEING TAMPERED WITH  
  15 5L#F ...      COOR2                        [ELSE WAIT   
   8 5L*2 ...      BRN      SHFS5
   5 5L*H ...SHFS6   
   9 5LB4 ...      LDN   1  A1-1(2)  
   7 5LBK ...      NGN   0  1
   5 5LC6 ...SHFS7   
   7 5LCM ...      ADN   1  2
   8 5LD8 ...      TXU   0  0(1) 
  20 5LDP ...      BCS      SHFS7               [J IF TERMINATOR NOT FOUND YET   
   9 5LF= ...      SBN   1  A1-3(2)  
  20 5LFR ...      TXL   1  ALOGLEN(2)          [HAS BLOCK GOT TO BE LENGTHENED? 
  14 5LG# ...      BCS      SHFS8               [J IF NOT
  10 5LGT ...      LDX   4  ALOGLEN(2)   
  15 5LHB ...      ADN   4  AFAILEX             [NEW LENGTH  
   8 5LHX ...      LDX   5  GSIGN
   8 5LJD ...      ORS   5  A1(2)
   8 5LJ_ ...      ALTLEN   2,4  
  10 5LKG ...      MHUNT    3,BSTB,BLUFF 
  12 5LL3 ...      HUNT2    2,BSTB,BFAIL,SBSA(3) 
  20 5LLJ ...      ERS   5  A1(2)               [CLEAR 'BEING TAMPERED WITH' BIT 
   5 5LM5 ...SHFS8   
   4 5LML ...)   
  16 5LNB                                       [FIND THE CFL/PFL
   8 5M82          ADN   2  A1-1 
   7 5MML          NGN   0  1
  18 5N7=    TB4   ADN   2  2                   [SEARCH FOR END OF BLOCK 
   8 5NLW          TXU   0  0(2) 
  18 5P6G          BCS      TB4                 [JUMP IF NOT END OF LIST 
   8 5PL6          STO   0  2(2) 
   9 5Q5Q          LDX   0  SNEWB(3) 
  16 5QKB          STO   0  0(2)                [BLOCK  NUMBER   
   9 5R52          LDX   0  SP2(3)   
  17 5RJL          STO   0  1(2)                [L.F.N FOR THIS BLOCK
   9 5S4=          LDX   4  SMARK(3) 
  16 5SHW          SRC   4  6                   [BLOCKLET MARKERS
   8 5T3G          ORS   4  1(2) 
  18 5TH6          LDCT  0  BSPFL               [PERMANENT FAIL MARKER   
   8 5W2Q          ORS   0  1(2) 
  18 5WGB          ORS   0  SNEWB(3)            [FAILED BLOCKLET MARKER  
   9 5X22    #SKI  K6TRANFAIL>199-199
   4 5XFL    (   
   9 5X_=          TRACE    0(2),PFL 
   8 5YDW          TRACE    1(2) 
   8 5YYG          TRACE    2(2) 
   4 5_D6    )   
   8 5_FH ...      FSHCODE  AORB 
   4 5_GY ...(   
  14 5_J* ...#  CREATE NEW PFL ENTRY IN OTHER M/C ALSO.  
   9 5_KQ ...      LDX   5  SLFN(3)  
   8 5_M7 ...      LDX   6  0(2) 
   8 5_NJ ...      LDX   7  1(2) 
  12 5_NX ...      SETNCORE 1,3,FSH,FSHAMBLES   [
  21 5_P= ...      LDX   0  SCDLINK2(1)         [  SET UP BLOCK FOR UNANT. CLOSEDOWN 
  12 5_PK ...      STO   0  FSHCDLINK(3)        [
  19 5_P_ ...      TRANSBEG FSHTFID,RITEFAIL,8,NOBLOCKS,,ACOMMUNE4,SHFSB,SUP 
   8 5_Q# ...      BRN      SHFSB
   5 5_QM ...XSHFSB  
   7 5_R2 ...      FSHACCWK  
   5 5_RB ...SHFSB   
  10 5_S4 ...      MFREEW   FSH,FSHAMBLES
  10 5_SR ...      MHUNT    3,BSTB,BLUFF 
   4 5_W8 ...)   
  10 5_XQ    TB3   LDX   1  SNEWDAT(3)   
   9 62CB          LDX   5  SLFN(3)  
   9 62X2          LDX   6  SBLNUM(3)
   9 63BL          LDX   7  SNEWB(3) 
   7 63W=    XC    LDX   0  6
  17 64*W          ANDN  0  3                   [GET BLOCKLET BITS   
  17 64TG          ORS   0  1                   [TO DATUMISED ADDRESS
  10 65*6          ORS   0  SNEWDAT(3)   
   9 65SQ          ORS   0  SNEWB(3) 
   7 66#B          ORS   0  7
   4 66S2    XC3 
   9 67?L    #SKI  K6TRANFAIL>199-199
   4 67R=    (   
  10 68=W          TRACE    1,FAILURE1   
   8 68QG          TRACE    5,   
   8 69=6          TRACE    6,   
   7 69PQ          TRACE 7,  
   4 6=9B    )   
   8 6=P2          CALL  0  XFAIL
  20 6?8L          LDX   2  BSCHANAD(2)         [CHANGE BLOCK NO. IN THIS Q BLOCK
  10 6?N=          MHUNT    3,BSTB,BLUFF 
   9 6#7W          LDX   4  SNEWB(3) 
  10 6#MG          LDX   5  SNEWDAT(3)   
   9 6*76          SBN   2  CHAINADD 
   9 6*LQ          LDX   0  ATYPE(2) 
   8 6B6B          TXL   0  CACT 
   8 6BL2          BCS      TB3X 
   9 6C5L          STO   4  BA3(2)   
  10 6CK=          STO   5  ACOMMUNE9(2) 
   8 6D4W          BRN      TB3Z 
   9 6DJG    TB3X  STO   4  BLKNO(2) 
  10 6F46          STO   5  ADBLKNO(2)   
   7 6FHQ    TB3Z  LDX   6  3
  10 6G3B          LDN   3  CHAINADD(2)  
   7 6GH2          OFFQUE   3
   7 6H2L          LDX   3  6
  12 6HG=          OPTRAN   2,SNEWDAT(3),SBSA(3) 
   8 6H_W          LDX   1  FX1  
   8 6JFG          LDX   2  FX2  
   7 6J_6          LDX   3  6
   9 6KDQ          LDX   0  SBSA(3)  
   9 6KYB          STO   0  AWORK1(2)
   9 6LD2          LDX   0  SLFN(3)  
  10 6LXL          STO   0  ACOMMUNE1(2) 
   9 6MC=          LDX   0  SNEWB(3) 
   9 6MWW          ANDX  0  BSTSMASK 
  10 6NBG          STO   0  ACOMMUNE2(2) 
  19 6NW6          CALL  0  XWAIT               [REQUEUE BLOCKS FROM WAIT Q  
  10 6P*Q          MHUNT    3,BSTB,BLUFF 
   9 6PN7 ...      LDX   1  SBSA(3)  
  16 6Q2J ...      CALL  7  UNSTFB              [ UNSET FAIL BIT 
   4 6Q57 ...TDE 
   7 6Q7Q ...#UNS  FSHTEST   
   4 6Q=* ...(   
   8 6Q#Y ...      LDX   2  FX2  
  15 6QCH ...      BC       2,BFSHACTB          [TFA TO M/C A
   6 6QG6 ...      FPUT  
   6 6QJP ...      COOR1 
   4 6QM# ...)   
  21 6QPX ...      HUNTW 3,BSTB,BWRITE          [SEE IF ANY MORE FAILS TO DEAL WITG  
  21 6QSL          BPZ   3  TDB                 [J IF MORE FAILED BLANKET TRANSFERS  
   9 6R#=          ADN   2  BSCHANAD 
   8 6RRW          TXU   2  0(2) 
  18 6S?G          BCS      TDA                 [J IF MORE ORDINARY FAILS
  12 6SR6          HUNTW    3,BSTB,BLUFF        [
   8 6T=Q          BNG   3  TDC  
  16 6TQB          FREECORE 3                   [FREE DATA BLOCK 
  20 6W=2    TDC   COOR1                        [GO TO SLEEP-NOTHING MORE TO DO  
   9 6WPL          BRN      PSTART   
  10 6X9=    TDB   HUNTW    3,BSTB,BLUFF 
   8 6XNW          BNG   3  TDA  
  21 6Y8G          FREECORE 3                   [FREE BLUFF IF BLANKET FAILS TO DO   
   8 6YN6    TDA   LDX   1  FX1  
   8 6_7Q          LDX   2  FX2  
   9 6_MB          BRN      PSTART   
   4 83*G    #   
   4 83T6    #   
   5 842# ...STYPE   
  11 847G ...#HAL            +BSTB+BQBLK,0   
   4 84#Q    #   
   4 9YBL    #   
   4 9YW=    #   
  15 9_*W    #     SR TO REQUE  BLOCKS FROM  BSWQ TO BSA Q.  
  13 9_TG    #     AWORK1    CONTAINS  ADDRESS OF BSA
  13 =2*6    #     ACOMMUNE1 CONTAINS  LFN OR BSUN   
  18 =2SQ    #     ACOMMUNE2 CONTAINS  ZERO IF BSUN IN 1 OR  BLOCK NUMBER
  12 =3#B    #     LINK  X0  STORED IN ACOMMUNE6 
   5 =3S2    XWAIT   
  10 =4?L          STO   0  ACOMMUNE6(2) 
   9 =4R=          LDX   1  AWORK1(2)
   7 =5=W          LDN   7  0
  10 =5QG          LDX   0  ACOMMUNE2(2) 
  16 =6=6          BNZ   0  SCA                 [JUMP IF OFFLINE 
   7 =6PQ          LDN   7  2
  16 =79B    SCA   LDN   6  BSQWCHAN(1)         [BASE OF WAIT Q  
   7 =7P2          LDX   3  6
  17 =88L    SH    LDX   3  FPTR(3)             [ADDRESS OF Q BLOCK  
   8 =8N=    SHA   LDX   2  FX2  
   7 =97W          TXU   3  6
  16 =9MG          BCC      SD                  [JUMP IF END OF Q
   9 ==76          SBN   3  CHAINADD 
  21 ==LQ          LDN   4  BA1(3)              [RESCUE PARAMETERS BA AREA IF ACTIVIT
  18 =?6B          LDX   1  EXEC3(3)            [DATUMISED BLOCK NUMBER  
   9 =?L2          LDX   0  ATYPE(3) 
   8 =#5L          SMO      FX1  
   8 =#K=          TXU   0  STYPE
   8 =*4W          BCS      SR1  
  10 =*JG          LDX   1  ADBLKNO(3)   
   9 =B46          LDN   4  ATRAN(3) 
   9 =BHQ    SR1   LDN   5  AWORK2(2)
   7 =C3B          MOVE  4  3
   9 =CH2          LDX   0  AWORK2(2)
   7 =D2L          ANDN  0  2
   7 =DG=          ERX   0  7
  20 =D_W          BNZ   0  SG                  [JUMP IF WRONG TYPE OF TRANSFER  
  16 =FFG          BNZ   7  SY                  [JUMP IF ONLINE  
   9 =F_6          LDX   0  AWORK3(2)
   8 =GDQ          ANDN  0  #377 
  10 =GYB          TXU   0  ACOMMUNE1(2) 
  17 =HD2          BCS      SG                  [JUMP IF WRONG LFN   
   9 =HXL          LDX   0  AWORK4(2)
   9 =JC=          ANDX  0  BSTSMASK 
  10 =JWW          TXU   0  ACOMMUNE2(2) 
   8 =KBG          BCS      SG   
  10 =KW6    SY    LDX   4  CHAINADD(3)  
   9 =L*Q    #SKI  K6TRANFAIL>199-199
   4 =LTB    (   
   9 =M*2          TRACE    3,REQUE  
   8 =MSL          TRACE    6,   
   4 =N#=    )   
   9 =NRW          ADN   3  CHAINADD 
   7 =P?G          OFFQUE   3
   9 =PR6          SBN   3  CHAINADD 
  10 =Q=Q          OPTRAN   3,1,AWORK1(2)
   7 =QQB          LDX   3  4
   8 =R=2          BRN      SHA  
  16 =RPL    SG    ADN   3  CHAINADD            [ADJUST POINTER  
   8 =S9=          BRN      SH   
   8 =SNW    SD    LDX   1  FX1  
   8 =T8G          LDX   2  FX2  
  10 =TN6          LDX   0  ACOMMUNE6(2) 
   7 =W7Q          EXIT  0  0
  18 ?8CW    # SR TO REMOVE ANENTRY FROM THE CFL AND PLACE IT IN THE FFL 
  10 ?8XG    # X0 LINK STORED IN AWORK4  
  14 ?9C6    # LFN IN ACOMMUNE1,BLOCK NUMBER IN ACOMMUNE2
  11 ?9WQ    # USES ALL ACCS  X1=FX1 X2=FX2  
   4 ?=BB    #   
   5 ?=W2    XLIST   
   8 ??*L          SBX   0  FX1  
   9 ??T=          STO   0  AWORK4(2)
  10 ?##W          LDX   6  ACOMMUNE1(2) 
  10 ?#SG          LDX   7  ACOMMUNE2(2) 
  12 ?*#6    XDA   HUNT2    2,BSTB,BFRAIL,ATFAPTR
  19 ?*RQ          LDX   0  A1(2)               [CHECK IF LIST BEING ALTERED 
  15 ?B?B          BPZ   0  XDB                 [JUMP  IF OK 
   6 ?BR2          COOR2 
   8 ?C=L          BRN      XDA  
   9 ?CQ=    XDB   LDN   1  A1-1(2)  
   7 ?D9W          NGN   0  1
  21 ?DPG    XD    ADN   1  2                   [CHECK IF ROOM IN LIST FOR NEW ENTRY 
   8 ?F96          TXU   0  0(1) 
   8 ?FNQ          BCS      XD   
   9 ?G8B          SBN   1  A1-3(2)  
  10 ?GN2          TXL   1  ALOGLEN(2)   
   8 ?H7L          BCS      XE   
  10 ?HM=          LDX   4  ALOGLEN(2)   
   9 ?J6W          ADN   4  AFRAILEX 
  16 ?JLG          LDCT  0  #400                [LOCKOUT MARKER  
   8 ?K66          ORS   0  A1(2)
   7 ?KKQ          LDX   3  2
   8 ?L5B          ALTLEN   3,4  
  12 ?LK2          HUNT2    3,BSTB,BFRAIL,ATFAPTR
   8 ?M4L          LDCT  0  #400 
   8 ?MJ=          ERS   0  A1(3)
   7 ?N3W          LDX   4  3
  13 ?NHG    XEA   FINDFAIL 3,7,6,CFL,XERR,AWORK1(2) 
   7 ?P36          LDX   2  4
   7 ?PGQ          NGN   0  1
   7 ?Q2B    XF    ADN   2  2
  19 ?QG2          TXU   0  A1-1(2)             [SEARCH FOR END OF F.F LIST  
   8 ?Q_L          BCS      XF   
  16 ?RF=          LDX   0  0(3)                [SET UP RECORD   
  15 ?RYW          STO   0  A1-1(2)             [BLOCK NUMBER
   7 ?SDG          LDX   0  6
  12 ?SY6          SRC   0  6                   [
  17 ?TCQ          STO   0  A1(2)               [ L.F.N  IN  B0-B23  
   7 ?TXB          NGN   0  1
  17 ?WC2          STO   0  A1+1(2)             [END OF BLOCK MARKER 
   7 ?WWL          SBN   3  1
  21 ?XB=    PA    ADN   3  1                   [REMOVE RECORD FROM CFL  BY MOVING   
  15 ?XTW          LDX   4  2(3)                [REMAINDER UP
   8 ?Y*G          STO   4  0(3) 
  17 ?YT6          TXU   4  0                   [TEST FOR END OF LIST
   8 ?_#Q          BCS      PA   
   9 ?_B7 ...      FSHCODE  B,XIAMA  
   4 ?_CJ ...(   
  14 ?_D_ ...      LDN   4  3                   [LENGTH  
  18 ?_GB ...      SETUPCORE 4,3,FSH,FSHBDEL    [USED BY DELBLKS MACRO   
  16 ?_HR ...      STO   4  A1(3)               [RECORD HEADER   
  15 ?_K8 ...      STO   6  A1+1(3)             [RESIDENCE NO
  14 ?_LK ...      STO   7  A1+2(3)             [BLOCK NO
  21 ?_N2 ...      TRANSBEG FSHTFID,RITEFAIL,5,FSH,FSHBDEL,ACOMMUNE1,YLIST,AUT,SUP   
   5 ?_PC ...XIAMA   
   4 ?_QS ...)   
   8 ?_SB          LDX   2  FX2  
  13 #2#2    YLIST HUNT2    3,BSTB,BFAIL,AWORK1(2)   
   7 #2RL          LDN   0  1
  19 #3?=          SBS   0  A1(3)               [SUBTRACT ONE FROM CFL COUNT 
   8 #3QW          LDX   1  FX1  
   8 #4=G          LDX   2  FX2  
   9 #4Q6          LDX   0  AWORK4(2)
   8 #59Q          ADX   0  FX1  
   7 #5PB          EXIT  0  0
   7 #692    XE    LDX   4  2
   8 #6NL          LDX   2  FX2  
   8 #78=          BRN      XEA  
  10 #7MW    XERR  GEOERR   0,CFRECLST   
   8 #87G ...XFAIL LDX   2  FX2  
   8 #8M6 ...      SBX   0  FX1  
   9 #96Q ...      STO   0  AWORK4(2)
  10 #9LB ...      STO   1  ACOMMUNE1(2) 
  16 #=62 ...      DOWN     RITEFAIL,4          [FAILURE MACRO   
   9 #=KL          LDX   0  AWORK4(2)
   8 #=TD ...      ADX   0  FX1  
   7 #?5=          EXIT  0  0
   4 **P4 ...#   
   4 **QL    #   
  14 **ST ...#       SUBROUTINE TO UNSET OFFLINE FAIL BIT
   4 **X4 ...#   
   5 **_? ...UNSTFB  
   8 *B3G ...      UNSETFB  1,1  
   7 *B5P ...      EXIT  7  0
   4 *B7Y ...#   
   4 *BPW    #END
   6 ____ ...115302350002
