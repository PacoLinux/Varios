  18 22FL ...#LIS  K0SCHEDJOB>K0OPCA>K0OBJPROG>K0HLS>K0ALLGEO>K0GREATGEO 
  15 22_= ...#SEG  SCHEDJOB                     [V E PHIPPS  
  10 23DW                   8HSCHEDJOB   
   4 23J2 ...[   
   3 23M6 ...
  16 23Q= ...[   (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982
   4 23TB ...[   
  21 23YG    # THIS SEGMENT DEALS WITH THE HIGH LEVEL SCHEDULER EXTRACODES USED TO   
  21 24D6    # CONTROL THE FLOW AND PERFORMANCE OF JOBS HANDED OVER TO THE LOW LEVEL 
  21 24XQ    # SCHEDULER. EXTRACODES INCLUDED ARE THE JOBSTART EXTRACODE,RESET CPI   
  21 25CB    # EXTRACODE ETC. IN ADDITION THE SEGMENT CONTAINS ROUTINES INTERFACING  
  18 25X2    # WITH THE COMMAND PROCESSOR ETC. WHICH SERVICE THE HLSQ.   
   8 26BL    #     ENTRY POINTS  
   4 26W=    #   
  15 27*W          SEGENTRY K1SCHEDJOB,ZK1      [MAIN ENTRY  
  14 27TG          SEGENTRY K2SCHEDJOB,ZK2      [UNSAVING
  14 29S2          SEGENTRY K7SCHEDJOB,ZK7      [STOP JOB
  17 2=24 ...      SEGENTRY K8SCHEDJOB,GEOERR   [SCHEDULE JOBBLOCK   
  18 2=86 ...SAM1           +EVFSTART           [WAITING TO BE F.S. BIT  
  15 2=9W ...SAM2           +EVSTOPPED          [STOPPED BIT 
   8 2=?L    SA6            +6   
  18 2=L3 ...STYLE          #31                 [MOP WAITING TO BE F.S.  
  18 2=YD ...               #32                 [BACK WAITING TO BE F.S. 
   2 2?=W
   5 2?QG ...RLINK1  
  18 2#=6 ...      PAIR     HLSREAD,3           [LINK TO SCHED.PERI XCODE
   5 2BN=    SLINK1  
  18 2C7W          PAIR     SAVEJOB,2           [LINK TO SAVEJOB SEGMENT 
   5 2CMG    SLINK2  
  18 2D76          PAIR     SCHEDJOB,2          [LINK TO UNSAVING ROUTINE
  18 2DLQ          PAIR     SCHEDJOB,7          [LINK TO STOPPING ROUTINE
   2 2F6B
  13 2FL2 ...#DEF  VGACT =  +ACTSAVELEN+GENERALLEN   
   8 2G5L ...SIXTY          +60  
  10 2HY9 ...XMILLPEXP       #73737373   
  17 2HYH ...[ THIS SUBROUTINE SETS X3 POINTING TO THE HLSQ BLOCK,AND
  16 2HYT ...[ EXITS +1.IF THE BLOCK DOES NOT EXIST IT EXIXTS +0.
  14 2H_7 ...[ ON ENTRY X4 SHOULD CONTAIN THE JOBNUMBER  
   5 2H_F ...SHLSQ   
   8 2H_R ...      STO   0  GEN0 
   7 2J25 ...      ADN   0  1
   8 2J2C ...      STO   0  GEN1 
   9 2J2P ...      SHLSQ  4,3,NHLSQ  
   9 2J33 ...      BRN      (GEN1)   
   5 2J3* ...NHLSQ   
   9 2J3M ...      BRN      (GEN0)   
  11 2J46 ...[ THIS ROUTINE FINDS THE JOBLOCK
  17 2JHQ ...[ X4 HOLDS JOB NUMBER,X1 IS LINK,X2->JOBBLOCK ON EXIT   
   8 2K3B ...SJOB  STO   1  GEN0 
   8 2KH2 ...      JOBLOCK  4,2  
   9 2L2L ...      BRN      (GEN0)   
  12 2TC8 ...# SUBROUTINE TO FIND ADATA CSTORE   
   5 2TCG ...XHUNT   
  11 2TCS ...      MHUNTW   1,ADATA,CSTORE   
   7 2TD6 ...      EXIT  6  0
  16 2TDD ...# SUBROUTINE TO ALTER LENGTH TO THAT SPECIFIED IN X7
   5 2TDQ ...XALTLENG
   8 2TF4 ...      SBX   6  FX1  
  11 2TFB ...      ALTLENG  3,7,XALTSEARCH   
   8 2TFN ...      ADX   6  FX1  
   7 2TG2 ...      EXIT  6  0
  18 2TG# ...# SUBROUTINE TO SEE IF JOB IS STILL SAVED AFTER COORDINATION
   6 2TGL ...STILLSAVED  
  18 2TGS ...      CALL  0  SHLSQ                [SET X3 TO HLSQ BLOCK   
  17 2TH2 ...      BRN      XERR4                  [J IF NONEXISTANT 
  17 2TH= ...      HLSAV    3,CLEAR,SAVED,HLSQ,XERR4 [J IF NOT SAVED 
   7 2THJ ...      EXIT  6  0
  14 2THW ...# SUBROUTINE USED BY ALTLENG TO REFIND BLOCK
   6 2TJ8 ...XALTSEARCH  
  10 2TJG ...      SHLSQ    4,2,(GEOERR) 
   7 2TJS ...      EXIT  1  0
  17 2TK6 ...# SUBROUTINE TO SET WORD 6 OF RECORD SUCH THAT IT CAN BE
   9 2TKD ...# DEALT WITH IN A LOOP  
   5 2TKQ ...X6SET   
  18 2TL4 ...      LDXC  0  6(2)                [CONREC IE LATEST RESET  
  16 2TLB ...      BCS      X6EX                [DEFAULT THUS OK 
  19 2TLN ...      LDXC  5  6(3)                [LAST REST BUT ONE IN HLSQ   
  21 2TM2 ...      BCS      X6EX                [DEFAULT THUS CAN BE ENTIRLY OVERWRIT
  18 2TM# ...      ANDN  0  3                   [ISOLATE FS AND TS BITS  
  18 2TML ...      BNZ   0  TESTEP              [NOT DEFAULT IN CONREC   
  17 2TMY ...      ANDN  5  3                   [ISOLATE BITS IN HLSQ
  16 2TN= ...      ORS   5  6(2)                [STORE IN CONREC 
   8 2TNJ ...      LDX   5  6(3) 
   5 2TNW ...TESTEP  
   8 2TP8 ...      LDX   0  6(2) 
  18 2TPG ...      ANDN  0  #14                 [ISOLATE EP AND CCEP BITS
   8 2TPS ...      BNZ   0  X6EX 
  15 2TQ6 ...      ANDN  5  #14                 [ISOLATE BITS
  16 2TQD ...      ORS   5  6(2)                [STORE IN CONREC 
   7 2TQQ ...X6EX  EXIT  6  0
   4 2TSL ...[   
  17 2W#= ...[ THIS ROUTINE SETS UP THE JOB'S SCHEDULING REQUIREMENTS
  18 2WRW ...[ AWORK3 HOLDS REL.PTR.TO CONTROL RECORD IN ACTIVITY BLOCK  
  15 2X?G ...[ ON ENTRY,    X7  LINK ADDR., X4 HOLDS JOB NO. 
  10 2XR6 ...[ ON EXIT,     X5 = 0 IF OK 
  14 2Y=Q ...[                 = 2 IF JOB IS BEING SAVED 
  12 2YQB ...[                 = 3 IF BREAK-IN   
  13 2_=2 ...[                   4 IF NO JOB BLOCK   
  16 2_FS ...[                   5 IF CODE SELECTOR OUT OF RANGE 
   5 2_PL ...RESCHED 
   8 329= ...      SBX   7  FX1  
   8 32NW ...      CALL  1  SJOB 
   7 338G ...      LDN   5  4
  16 33N6 ...      BNG   2  RSC95               [J IF NO JOBBLOCK
   8 347Q ...      LDX   3  FX2  
  16 34MB ...      ADX   3  AWORK3(3)           [X3 -> CON. REC. 
  17 3572 ...      LDXC  0  7(3)                [PTR. TO NEEDS DATA  
  16 35LL ...      BCS      RSC3                [J IF NO NEEDS   
  11 366= ...      MHUNTW   3,ADATA,CSTORE   
   8 36KW ...      LDX   5  A1(3)
   9 375G ...      ADN   5  EXTJOB   
  19 37K6 ...      ALTLENG  2,5,SJOB            [REQ.SIZE OF JOBLOCK+NEEDS   
   8 384Q ...      CALL  1  SJOB 
  18 38JB ...RSC3  LDN   5  2               [BEING SAVED- ERROR INDICATOR
  12 3942 ...      HLSAV 2,SET,SAVING,JOBQ,RSC95 
  18 39HL ...      LDN   5  3               [BREAK-IN - ERROR INDICATOR  
   8 3=3= ...      FCAJO    3,2  
  10 3=GW ...      LDXC  0  JOBEVENTS(3) 
  17 3?2G ...      BCS      RSC95               [J IF BREAK-IN SET   
  18 3?G6 ...      LDX   1  2                   [SAVE ADDR. OF JOBBLOCK  
   8 3?_Q ...      LDX   2  FX2  
  10 3#FB ...      STO   3  ACOMMUNE3(2) 
  18 3#_2 ...      ADX   2  AWORK3(2)           [ADDR.OF CONTROL RECORD  
   8 3*DL ...      LDX   0  0(2) 
  15 3*Y= ...      BNZ   0  RSC31         [J IF NOT STARTJOB 
  15 3BCW ...      LDXC  0  8(2)            [OPTION REQUIRED 
   9 3BMN ...      BCC      RSC305   
   7 3BXG ...      LDN   0  0
   5 3C7# ...RSC305  
   7 3CC6 ...      LDN   5  5
   9 3CWQ ...      LDX   4  EXT+1(3) 
  17 3DBB ...      BXL   4  0,RSC95         [J IF OPTION OUT OF RANGE
   9 3DW2 ...      STO   0  EXT+1(3) 
   5 3F*L ...RSC31   
   8 3FT= ...      LDX   0  2(2) 
  15 3G#W ...      BNG   0  RSC4                [J IF NO CPI 
  17 3GSG ...      STO   0  JCPI(1)             [STO CPI IN JOBBLOCK 
   8 3H#6 ...RSC4  LDX   5  3(2) 
  15 3HRQ ...      BNG   5  RSC5                [J IF NO JT  
   8 3J?B ...      LDN   4  1000 
  18 3JR2 ...      MPY   5  4                   [CONVERT TO MILLISECONDS 
   9 3K=L ...      STO   5  ATIMEJ(1)
  17 3KQ= ...      STO   6  ATIMEJ+1(1)         [STORE JT IN JOBBLOCK
   8 3KWG ...RSC5  LDX   0  4(2) 
   8 3L2Q ...      BNG   0  RSC5A
   9 3L72 ...      ANDN   0  #7777   
   8 3L?= ...      SBN   0  #41  
   8 3LCG ...      BNG   0  RSC5A
   8 3LHQ ...      SBN   0  #32  
  16 3LN2 ...      BPZ   0  RSC5A     [ABOVE CHECKS FOR VALID URG
   8 3LS= ...      LDX   0  4(2) 
   8 3LYG ...      BRN      RSC6 
   5 3M4Q ...RSC5A   
   9 3M96 ...      LDEX  5  JURGE(1) 
  18 3MNQ ...      BNZ   5  RSC7                [J IF UR PREVIOUSLY SET  
   8 3MRW ...      LDX   0  6(2) 
  16 3MX2 ...      BNG   0 RSC7       [J IF NO CHANGE TO STATUS  
   7 3N26 ...      ANDN  0 2 
  15 3N5= ...      BZE   0  RSC7       [J IF NOT BECOMING FS 
  15 3N8B ...      LDN   0  #55                 [DEFAULT = M 
  17 3NN2 ...RSC6  DSA   0  JURGE(1)            [STORE UR IN JOBBLOCK
   8 3P7L ...RSC7  LDX   0  5(2) 
  19 3PM= ...      BNG   0  RSC8                [J IF NO MAXSIZE SPECIFIED   
  17 3Q6W ...      STO   0  JSIZE(1)            [STORE MZ IN JOBBLOCK
  15 3QLG ...RSC8  LDXC  3  7(2)                [PTR TO NEEDS
  17 3R66 ...      BCS      RSC84               [J IF NO NEED DATA   
  11 3RKQ ...      MHUNTW   3,ADATA,CSTORE   
   8 3S5B ...      LDN   3  A1(3)
  10 3SK2 ...      LDN   4  JBPERIS(1)   
   8 3T4L ...      SMO      0(3) 
  19 3TJ= ...      MOVE  3  0                   [STORE NEEDS REQ. IN JOBBLOCK
  11 3W3W ...      LDN   0  JBPERIS-JOBNUM   
  18 3WHG ...      STO   0  JNEEDSPTR(1)        [SET PTR TO NEEDS FIELD  
   5 3X36 ...RSC84   
   8 42Y6 ...      SMO      FX2  
   9 43CQ ...      LDX   3  ACOMMUNE3
  15 43DN ...      LDX   4  AMILLP(1)           [GET JOB'S MP
   8 43F2 ...      LDXC  0  6(2) 
  17 43F# ...      BCS      XCOREP              [NEITHER EP NOR CCEP 
   8 43FL ...      ANDN  0  #14  
  17 43FY ...      BZE   0  XCOREP              [NEITHER EP NOR CCEP 
   7 43G= ...      ANDN  0  4
  17 43GJ ...      BNZ   0  XCCEP               [J IF CCEP BIT SET   
   6 43GW ...# EP ISSUED 
  10 43H8 ...      BS       1,JBEXPREQ   
   8 43HG ...      SMO      FX1  
  19 43HS ...      LDX   4  XMILLPEXP           [DEFAULT FOR EXPRESS MILL P  
   8 43J6 ...      LDXC  0  10(2)
  17 43JD ...      BCC      STORECP             [IF NO CORE PRIORITY 
  17 43JQ ...      LDN   0  60                  [SPECIFIED SET CORE P
  14 43K4 ...      BRN      STORECP             [TO 60   
   5 43KB ...XCCEP   
  10 43KN ...      BC       1,JBEXPREQ   
   7 43L2 ...      LDN   4  0
   9 43L# ...      DEX   4  ACOREP(1)
   5 43LL ...XCOREP  
   8 43LY ...      LDXC  0  10(2)
  18 43M= ...      BCC      STORECP             [CORE PRIORITY SPECIFIED 
  15 43MJ ...      LDEX  0  ACOREP(1)           [NO ACTION IF
  16 43MW ...      BNZ   0  XLVECOR             [PREVIOUSLY SET  
  19 43N8 ...      CALL  0  TDEFCOR             [SET X0=DEFAULT CORE PRIORITY
   5 43NG ...STORECP 
   9 43NS ...      DEX   0  ACOREP(1)
   5 43P6 ...XLVECOR 
   7 43PD ...      LDN   6  4
   8 43PQ ...      LDX   5  11(2)
  19 43Q4 ...      BZE   5  XMOVEMILLP          [NO ACTION IF PREVIOUSLY SET 
   7 43QB ...      LDX   4  5
   6 43QN ...XMOVEMILLP  
  17 43R2 ...      BXE   4  AMILLP(1),XMOVEDMILL [J IF MP UNCHANGED  
   9 43R# ...      STO   4  AMILLP(1)
   7 43RL ...      LDX   5  1
   7 43RY ...      LDX   6  2
  17 43S= ...      CALL  4  XMEL                [MOVE MILLP TO PCA   
  15 43SJ ...      LDX   1  5                   [X1->JOBBLOCK
  17 43SW ...      LDX   2  6                   [X2->CONTROL RECORD  
   6 43T8 ...XMOVEDMILL  
   6 43TK ...#UNS  ICT   
   4 43W2 ...(   
   7 43WC ...[  SET ACT PRI  
  17 43WK ...      JBC      NOICT,,ICTSW   [DONT BOTHER IF ICT OFF   
  15 43WL ...      LDCH  3  10(2)        [GET ACT PRIORITY   
  16 43WM ...      ANDN  3  #37       [GET RID OF COREPRI NULLBIT
  15 43WN ...      LDN   0  #20           [CHECK FIXED BIT   
   7 43WP ...      ANDX  0  3
   7 43WQ ...      SLL   0  1
  19 43WR ...      ORX   3  0          [TRANSFER FIXED BIT TO WHERE APJOBSET 
   6 43WS ...[EXPECTS IT 
  13 43W_ ...      LDX   4  JOBNUM(1)   [JOB NUMBER  
  16 43X7 ...      APJOBSET 4,3         [UPDATE ACTPRI FOR JOB   
  12 43X* ...      CALL  1  SJOB        [X2->JOBQ
  12 43XH ...      FCAJO    3,2         [X3->CPAT
  12 43XP ...      LDX   1  2           [X1->JOBQ
   8 43XX ...      LDX   2  FX2  
  14 43Y5 ...      STO   3  ACOMMUNE3(2) [KEEP CPAT PTR  
  15 43Y? ...      ADX   2  AWORK3(2)    [X2->CONTROL RECORD 
   5 43YF ...NOICT   
   4 43YT ...)   
   8 43_? ...      FSHCODE  AORB 
   4 445B ...(   
  20 449F ...#     FOR SHARED FILESTORE, WE MAY HAVE TO UPDATE A JOB BLOCK ON 'B'
  14 44*J ...#     IN ADDITION TO THE JOB BLOCKS ON 'A'  
   4 44FM ...#   
  19 44KQ ...      JBC      XHOME,1,JBFSHALIEN  [JIF THIS IS NOT AN ALIEN JOB
   4 44PT ...#   
  21 44TY ...      LDX   4  JOBNUM(1)           [SET JOB NUMBER FOR UPDATE INTO X4   
  20 4523 ...      DOWN     SCHDJOBA,4          [GO TO DEAL WITH UPDATE ON 'B'   
   4 4566 ...#   
  21 45=9 ...      BRN      XBDOWN              [ERROR - 'B' MACHINE HAS GONE DOWN   
  19 45B# ...      BRN      XGONE               [ERROR - CAN'T FIND JOB BLOCK
   4 45GC ...#   
  18 45LG ...      CALL  1  SJOB                [RELOCATE THE JOB BLOCK  
  20 45QK ...      BNG   2  XGONE               [AND JIF IT HAS GONE AWAY NOW!!  
  17 45WN ...      LDX   1  2                   [COPY POINTER INTO X1
  18 462R ...      FCAJO    2,,N                [AND FIND THE JOB'S CPAT 
  15 466W ...      LDN   5  0                   [SET OK REPLY
   4 46=_ ...#   
  19 46C4 ...XIT   SMO      FX1                 [RETURN TO CALLING ROUTINE   
   7 46H7 ...      EXIT  7  0
   4 46M= ...#   
   5 46R* ...XGONE   
  20 46XD ...      LDN   5  4                   [SET 'JOB BLOCK GONE' ERROR REPLY
   8 473H ...      BRN      XIT  
   4 477L ...#   
   5 47?P ...XBDOWN  
  21 47CS ...      LDN   5  #101                [SET ''B' MACHINE DOWN' ERROR REPLY  
   8 47HX ...      BRN      XIT  
   4 47N2 ...#   
   5 47S5 ...XHOME   
   4 47Y8 ...)   
   8 485= ...      LDXC  0  6(2) 
  18 487F ...      BCS      RSC90               [J IF NO STATUS REQUESTED
   7 48#2 ...      ANDN  0  3
  18 48RL ...      BZE   0  RSC90               [J IF NO CHANGE TO STATUS
   7 49?= ...      SBN   0  1
  10 49QW ...      LDX   4  CONTEXT(3)   
  19 4==G ...      ANDN  4  #4000               [ZERO IF NOT FULLY STARTED   
  18 4=Q6 ...      BNZ   0  RSC86               [J IF TO BE FULLY STARTED
  17 4?9Q ...      BZE   4  RSC90               [J IF ALREADY T.S.   
   7 4?PB ...      NGN   4  1
   9 4#92 ...      BC       3,EXFULLY
   8 4#NL ...      BRN      RSC87
  17 4*8= ...RSC86 BNZ   4  RSC90               [J IF ALREADY F.S.   
   8 4*MW ...      LDX   0  0(2) 
  16 4B7G ...      BZE   0  RSC862              [J IF STARTJOB   
  21 4BM6 ...      BS       3,EXFULLY           [F.S. IND. FOR FS MESSAGE O/P BY HLSE
   5 4C6Q ...RSC862  
   7 4CLB ...      LDN   4  1
   8 4D62 ...RSC87 LDN   0  #6000
  17 4DKL ...      ERS   0  CONTEXT(3)          [SWITCH TS/FS BITS...
  16 4F5= ...      LDX   0  GSIGN               [... CPAT AND... 
  16 4FJW ...      ERS   0  JURGE(1)            [...IN JOBBLOCK  
  10 4G4G ...      LDX   3  CONTEXT(3)   
   7 4GJ6 ...      ANDN  3  4
  15 4H3Q ...      BZE   3  RSC88               [J IF MOP JOB
   7 4HHB ...      LDN   3  1
  18 4J32 ...RSC88 ADS   4  HLSCOUNT(3)         [UPDATE COUNT OF FS JOBS 
  18 4JGL ...      BPZ   4  RSC90               [J IF COUNT NOT REDUCED  
  14 4K2= ...      BZE   3  TESTMOP             [J IF MOP
  18 4KFW ...      JBS      RSC90,,HLSBFS      [J IF HLS FSING BACK JOBS 
   8 4K_G ...      BRN      XKICK
   5 4LF6 ...TESTMOP 
  18 4LYQ ...      JBS      RSC90,,HLSMFS       [J IF HLS FSING MOP JOBS 
   5 4MDB ...XKICK   
   8 4MY2 ...      SMO      FX1  
   9 4NCL ...      LDX   3  STYLE(3) 
   7 4NMD ...      LDX   4  1
   8 4NX= ...      LONGON   0(3) 
   7 4P74 ...      LDX   1  4
  16 4PBW ...RSC90 LDN   5  0                   [ OK INDICATOR   
   8 4PWG ...RSC95 ADX   7  FX1  
   8 4QB6 ...      SMO      FX2  
  18 4QTQ ...      LDX   2  ACOMMUNE3                   [PTR->JOB'S CPAT 
   7 4R*B ...      EXIT  7  0
  10 4RT2 ...[ ROUTINE TO RE-ACTIVATE JOB
  10 4S#L ...[              X1 ->JOBBLOCK
   5 4SS= ...STARTJOB
   9 4T?W ...      LDX   4  JOBNUM(1)
   6 4TRG ...STJOBMESS   
   9 4W?6 ...      LDX   0  JMISC(1) 
   9 4WQQ ...      BNG   0  STBACK   
   8 4X=B ...      LONGON   #31,4
   8 4XQ2 ...      BRN      STDUN
   5 4Y9L ...STBACK  
   8 4YP= ...      LONGON   #32,4
   5 4_8W ...STDUN   
   7 4_NG ...      EXIT  7  0
  12 4_PS ...[     MOVE MILL PRIORITIES TO PCA   
  14 4_Q= ...[     AND RE-ORDER PROGRAM RING IF NECESSARY
   4 4_QN ...XMEL
   9 4_R6 ...      LDEX  0  JMISC(1) 
  18 4_RJ ...      BZE   0  (4)                 [EXIT IF NO CORE IMAGE   
   8 4_S2 ...      MELADJUST 1   
   7 4_SD ...      EXIT  4  0
  13 4_SW ...[     GET DEFAULT CORE PRIORITY FOR JOB 
  17 4_T# ...[     ON ENTRY, X1->JOBBLOCK;   ON EXIT,X3->JOB'S CPAT  
   5 4_TQ ...TDEFCOR1
  15 4_W8 ...      STO   0  GEN0                [LINK ADDRESS
   8 4_WL ...      FCAJO    3,1  
   9 4_X4 ...      BRN      TDEFCP   
  13 4_XG ...[     GET DEFAULT CORE PRIORITY FOR JOB 
   8 4_XY ...[     X3->JOB'S CPAT
   5 4_YB ...TDEFCOR 
   8 4_YS ...      STO   0  GEN0 
   5 4__= ...TDEFCP  
   7 4__N ...      LDN   5  0
  10 5226 ...      LDX   0  CONTEXT(3)   
   8 522J ...      SLL   0  20   
  17 5232 ...      BPZ   0  XNOTMOP             [J IF BACKGROUND JOB 
   7 523D ...      LDN   5  1
   5 523W ...XNOTMOP 
  10 5248 ...      LDX   0  JSOURCE3(3)  
   7 524G ...      SLL   0  2
  16 524S ...      BNZ   0  XCLUSTER            [J IF CLUSTER JOB
   7 5258 ...      ORN   5  2
   5 525L ...XCLUSTER
   7 5264 ...      SMO      5
  10 526G ...      LDX   0  CLUSBACKCP   
   9 526Y ...      BRN      (GEN0)   
   4 5286 ...ZK1 
  10 52MQ ...      SMO      HLSCONREC(2) 
   8 537B ...      BRN      SWIT 
  21 94J2    #    THE ROUTINE BELOW IS A S/R TO SET UP A SAVEJOB ACTIVITY WITH THE   
   9 953L    #     APPROPRIATE LINK: 
  12 95H=    #        TO K2SAVEJOB TO SAVE A JOB 
  21 962W    #        TO K2SCHEDJOB TO UNSAVE A JOB (WHICH DOWNS TO K1UNSAVE UNLESS  
  14 96GG    #           THE JOB IS STILL BEING SAVED)   
  12 9726    #        TO K7SCHEDJOB TO STOP A JOB
  20 97FQ    #     THE NUMBER OF THE JOB TO BE SAVED IS PUT IN ACOMMUNE4 OF THE  
  20 97_B    #     SAVEJOB ACTIVITY, WHOSE OWN JOBNO IS LEFT ZERO = AUTONOMOUS   
   9 98F2    #     SYSTEM ACTIVITY.  
  21 98YL    #     THE SAVEJOB ACTIVITY IS CHAINED IMMEDIATELY BEFORE THE APET BLOCK 
  12 99D=    #     ENTERED WITH  1)X4=JOBNUMBER  
  18 99XW    #                   2)X3=MODIFIER TO PICK UP CORRECT LINK   
  16 9=CG    #     X0-X5INC ARE USED  [X5 BEING LINK ACCUMULATOR 
   5 9=X6    SAVEMOVE
  19 9?BQ          SBX   5  FX1                 [GET RELATIVE RETURN ADDRESS 
   8 9?K2 ...      LDN   6  VGACT
  10 9?R= ...      LDX   0  HLSCONREC(2) 
  18 9?_G ...      BNZ   0  SVM4                [J IF NOT STARTJOB XCODE 
  10 9#7Q ...      ADX   6  ALOGLEN(2)   
  10 9#B2 ...      SBN   6  HLSCONREC-A1 
  11 9#J= ...SVM4  LDN   7  ACTSAVE+GENERAL  
   8 9#QG ...      SLL   7  12   
   8 9#YQ ...      GETACTX  7,6  
  15 9*72 ...      ADN   1  SLINK1(3)           [X1-> LINK   
  17 9**=          LDX   3  BPTR(2)             [X3=PTR TO /ACTSAVE  
   9 9B#G          LINKSET  3,0(1)   
  10 9BJ# ...      STO   4  ACOMMUNE4(3) 
  20 9BS6          HUNTACT  APET,2,3            [SEARCH BACKWARDS FROM /ACTSAVE  
  16 9C?Q                                       [FOR APET BLOCK  
  21 9CRB          RINGACT  3,2                 [RE-RING AND RE CHAIN IN FRONT OF APE
  11 9CYJ ...      HUNTW    2,ADATA,CSTORE   
   8 9D5Q ...      BNG   2  SVM9 
   8 9D=Y ...      CHAIN    2,3  
  19 9DD6 ...SVM9  ADX   5  FX1                 [GENERATE ABS. RETURN ADDRESS
   8 9DK# ...      LDX   2  FX2  
   7 9DQL          EXIT  5  0
   4 9F==    #   
  20 9FPW    #   THE FOLLOWING S/R SEARCHES THE TEMP HLSQ FOR A BLOCK FOR A GIVEN
   5 9G9G    #  JOB. 
  12 9GP6    #  THE JOBNO. IS ASSUMED TO BE IN X4
   4 9H8Q    #   
   4 9HNB ...TEMP
  11 9J82 ...      STEMPQ   4,3,TEMP2,TEMP3  
   7 9MKB          EXIT  5  0
   7 9N52    TEMP2 EXIT  5  1
   3 9N6? ...
  19 9N7J ...# AT K1SCHEDJOB THE PERI CONTROL AREA & DATA RECORD HAVE BEEN   
  21 9N8T ...# READ &STORED ,AND ARE NOW FOLLOWED BY A SMO'D JUMP TO SWIT USING 1ST W
   9 9N=6 ...# OF THE DATA RECORD.   
  14 9N?C ...#    PERIS HAVE THE FOLLOWING CONTROL AREA  
  17 9N#N ...#   WORD0      51 IF SCHEDJOB; 52/#113 IF HLSACTIVE/1   
  11 9N*_ ...#   WORD1      0-THE REPLY WORD 
  17 9NC= ...#   WORD2      THE NO. OF WORDS IN THE CONTROL RECORD   
  16 9NDH ...#   WORD3      START ADDRESS OF THE CONTROL RECORD  
  20 9NFS ...#  THE FIRST WORD OF THE CONTROL RECORD CONTAINS AN INDEX NUMBER TO 
  12 9NH5 ...#    DISTINGUISH THE DIFFERENT CASES
   3 9NJB ...
   3 9NKM ...
  20 9NLY ...SWIT  BRN      SWIT0               [BRANCH FOR JOBSTART EXTRACODE   
  21 9NN9 ...      BRN      SWIT1               [BRANCH FOR RESET JOB CHARACTERISTICS
  17 9NNP ...      BRN      (GEOERR)            [IN OVERFLOW SEGMENT 
  16 9NQR ...      BRN      SWIT3               [TO STOP-A-JOB   
  16 9NS4 ...      BRN      SWIT4               [TO JOB-SAVING   
  19 9NT* ...      BRN      SWIT5               [BRANCH FOR MOPWAIT EXTRACODE
   9 9NTP ...      BRN      (GEOERR) 
   9 9NTT ...      BRN      (GEOERR) 
   3 9N_8 ...
  17 9P2F ...# THE ROUTINE BELOW DEALS WITH THE JOBSTART EXTRACODE   
  13 9P3Q ...#   IT HAS THE FOLLOWING CONTROL AREA   
   8 9P53 ...#   WORD0      51/1 
  11 9P6# ...#   WORD1      0-THE REPLY WORD 
  17 9P7K ...#   WORD2      5-THE NO.OF WORDS IN THE CONTROL RECORD  
  20 9P8W ...#   WORD3      START ADDRESS IN THE PROGRAM OF THE CONTROL RECORD   
  19 9P=7 ...#  THE CONTROL RECORD POINTED TO BY THE CONTROL AREA CONTAINS   
   7 9P?D ...#   WORD0      0
  16 9P#P ...#   WORD1      THE JOB.NO. OF THE JOB TO BE STARTED 
  12 9PB2 ...#   WORD2      THE CPI OF THE JOB   
  20 9PC? ...#   WORD3      THE MAX.JOB TIME FOR THE JOB TO BE RUN BY THE L.L.S. 
  13 9PDJ ...#   WORD4      THE URGENCY OF THE JOB   
  10 9PFT ...#   WORD5      THE MAX SIZE 
  13 9PH6 ...#   WORD6       THE FULLY STARTED STATUS
  13 9PJC ...#   WORD7      THE POINTER TO NEEDS DATA
  11 9PKN ...#   WORD8      THE CODE SELECTOR
  14 9PL_ ...#   WORD9      THE MAXIMUM QUOTA (G4 ONLY)  
   3 9PN= ...
  14 9PPH ...#  CHECK  THAT  THE  ENTRY IS IN  THE  HLSQ 
   9 9PQS ...SWIT0 LDN   0  HLSCONREC
   9 9PS5 ...      STO   0  AWORK3(2)
  16 9PTB ...      LDX   4  HLSCONREC+1(2)        [GET JOB NUMBER
  16 9PWM ...      CALL  5  TEMP                [SEARCH HLSTEMPQ 
  17 9PXY ...      BRN      S3                  [ENTRY FOR JOB FOUND 
   4 9PYJ ...S4B 
  18 9P_8 ...      CALL  0  SHLSQ                [SET X3 TO HLSQ BLOCK   
  17 9P_T ...      BRN      S3                   [J IF NONEXISTANT   
  15 9Q2G ...      BRN      S20                 [J IF FOUND  
   6 9Q3R ...SETBADREP   
  18 9Q54 ...S3    LDX   5  GSIGN               [SET -VE FOR REPLY WORD  
   8 9Q6* ...S5    LDX   2  FX2  
  16 9Q7L ...      LDX   3  EVENT2(2)           [ADDR. OF C.AREA 
  17 9Q8X ...      ADN   3  1                   [ADDR OF REPLY WORD  
  10 9Q=8 ...      LDX   1  APETADDR(2)  
  20 9Q?F ...      FADDRESS 1,3,HLOCK1          [GEORGE ADDR OF REPLY WORD TO X3 
  17 9Q#Q ...      STO   5  0(3)                [SET THE REPLY WORD  
  12 9QB3 ...#SKIP          K6SCHEDJOB>199-199   
  10 9QC# ...      TRACE    0(3),REPLY   
  10 9QDK ...      VFREEW   ADATA,CSTORE 
   8 9QFW ...      LDX   1  FX1  
  19 9QH7 ...SFIN9 OLPFIN1  RLINK1(1)           [RESTARTS SUBJ-PROG WITHOUT  
  21 9QJD ...                                   [LENGTHY CHARGING ROUTINES.(X7 POINTS
   5 9QKP ...SETOKREP
   4 9QM2 ...SFIN
  19 9QN? ...      LDN   5  0                   [SET TO ZERO FOR REPLY WORD  
   8 9QPJ ...      BRN      S5   
   5 9QQT ...SUNSAVE 
  21 9QS6 ...      LDN   3  HLINK                [SET MODIFIER TO PICK UP CORRECT LIN
  21 9QTC ...      CALL  5  SAVEMOVE            [CALL SUBROUTINE TO SET UP SAVEJOB   
  10 9QWN ...      LDN   5  HLSCONREC(2) 
  10 9QX_ ...      LDN   6  VGACT+A1(3)  
  10 9Q_= ...      LDX   1  ALOGLEN(2)   
  18 9R2H ...      MOVE  5  512-HLSCONREC+A1(1) [COPY CONTROL REC. ETC   
   7 9R3S ...      SBX   6  3
  18 9R55 ...      STO   6  AWORK3(3)           [SAVE DISP. OF CON.REC.  
   7 9R6B ...      LDX   2  3
  19 9R7M ...      FPUT                         [PUT SAVEJOB ACT. ON THE LIST
  21 9R8Y ...SPOS  LDN   5  1                   [SET REPLY WORD +VE ->JOB HAS FIRST  
  16 9R=9 ...      BRN      S5                  [TO BE UNSAVED   
   3 9R?G ...
  13 9R#R ...#  FIND  JOBLOCK AND WRITE IN  CPI  ETC 
  12 9RB4 ...S20   HLSAV    3,SET,UNSAVE,HLSQ,S3 
  13 9RC* ...      HLSAV    3,SET,SAFE,HLSQ,SUNSAVE  
  17 9RF= ...      CALL  7  RESCHED           [SET UP JOB SCHED. REQ.
  17 9RH8 ...      BNZ   5  S3OR                [J IF RESCHED FAILED 
   9 9RHB ...      LDX   4  JOBNUM(1)
  18 9RHG ...      CALL  0  SHLSQ                [SET X3 TO HLSQ BLOCK   
  17 9RHL ...      BRN      US2                  [J IF NONEXISTANT   
   7 9RHQ ...      LDX   4  1
   7 9RHY ...      FREECORE 3
   7 9RJ6 ...      LDX   1  4
  15 9RJF ...S13   CALL  7  STARTJOB            [ACTIVATE JOB
  21 9RKQ ...      BRN      SFIN                [J. TO RETURN TO JOBSTARTER PROGRAM  
   3 9RM3 ...
  19 9RN# ...# THE ROUTINE BELOW DEALS WITH THE RESET CPI,JOBTIME EXTRACODE  
  13 9RPK ...#   IT HAS THE FILLOWING CONTROL AREA   
   8 9RQW ...#   WORD0      51/1 
  11 9RS7 ...#   WORD1      0-THE REPLY WORD 
  17 9RTD ...#   WORD2      5-THE NO.OF WORDS IN THE CONTROL RECORD  
  20 9RWP ...#   WORD3      START ADDRESS IN THE PROGRAM OF THE CONTROL RECORD   
  19 9RY2 ...#  THE CONTROL RECORD POINTED TO BY THE CONTROL AREA CONTAINS   
   7 9R_? ...#   WORD0      1
  13 9S2J ...#   WORD1      THE JOB.NO. OF THE JOB   
  12 9S3T ...#    WORD2     THE CPI OF THE JOB   
  20 9S56 ...#   WORD3      THE MAX.JOB TIME FOR THE JOB TO BE RUN BY THE L.L.S. 
  11 9S6C ...#     OTHERS AS FOR START JOB   
   9 9S7N ...SWIT1 LDN   0  HLSCONREC
   9 9S8_ ...      STO   0  AWORK3(2)
  16 9S== ...      LDX   4  HLSCONREC+1(2)      [GET JOB NUMBER  
   8 9SB5 ...      JOBLOCK  4,3  
   9 9SB6 ...      BPZ   3  UNSAVED  
  18 9SB= ...      CALL  0  SHLSQ                [SET X3 TO HLSQ BLOCK   
  16 9SBB ...      BRN      S3                  [J IF NONEXISTANT
  16 9SBG ...      HLSAV    3,SET,UNSAVE,HLSQ,S3 [J IF UNSAVED   
  17 9SBH ...      HLSAV    3,SET,SAVING,HLSQ,XERR4 [J IF BEING SAVED
   7 9SBJ ...      LDN   7  0
   8 9SBK ...      LDX   2  FX2  
  21 9SBL ...      ADX   2  AWORK3(2)           [X2 POINTS TO START OF CONTROL RECORD
  17 9SBM ...      LDX   0  ALOGLEN(3)          [IF ALOGLEN = AHLS   
  18 9SBN ...      SBN   0  AHLS                [THEN THIS IS FIRST RESET
  17 9SBP ...      BNZ   0  NOTFIRSTRES         [J IF NOT FIRST RESET
  16 9SBQ ...      LDXC  0  7(2)                [IS NEEDS DEFAULT
  14 9SBR ...      BCS      NEEDSISDEF          [J IF YES
  17 9SBS ...      CALL  6  XHUNT               [X1 TO ADATA CSTORE  
  20 9SBT ...      LDX   7  A1(1)               [SIZE OF NEEDS DATA(+1 FOR A1)   
   6 9SBW ...NEEDSISDEF  
  20 9SBX ...      ADN   7  AHLS+ASTCON         [X7 CONTAINS NEWSIZE OF HLS BLOCK
  15 9SBY ...      CALL  6  XALTLENG            [ALTER LENGTH
  17 9SB_ ...      CALL  6  STILLSAVED          [ERROR IF NOW UNSAVED
  17 9SC2 ...      ADN   2  HLSCONREC+2         [X2 TO CPI IN CONREC 
  21 9SC3 ...      ADN   3  A1+AHLS             [X3 TO START OF EXTRA AREA IN HLSQ   
  16 9SC4 ...      MOVE  2  ASTCON              [MOVE DATA ACROSS
   7 9SC5 ...      SBN   2  2
  16 9SC6 ...      LDXC  0  7(2)                [IS NEEDS DEFAULT
  14 9SC7 ...      BCS      SPOS                [J IF YES
  17 9SC8 ...      CALL  6  XHUNT               [X1 TO ADATA/CSTORE  
  21 9SC9 ...      ADN   3  ASTCON              [X3 TO START AREA FOR NEEDS IN HLSQ  
  21 9SC= ...      LDN   2  A1(1)               [X2 TO START OF NEEDS IN ADATA CSTORE
   8 9SC? ...      SMO      0(2) 
  19 9SC# ...      MOVE  2  0                   [COPY NEEDS DATA INTO HLSQ   
  13 9SC* ...      BRN      SPOS                [EXIT
   6 9SCB ...NOTFIRSTRES 
  16 9SCC ...      LDXC  0  7(2)                [IS NEEDS DEFAULT
  14 9SCD ...      BCS      SECNEEDSDEF         [J IF YES
  17 9SCF ...      CALL  6  XHUNT               [X1 TO ADATA CSTORE  
   8 9SCG ...      LDX   7  A1(1)
  10 9SCH ...      ADN   7  AHLS+ASTCON  
  19 9SCJ ...      CALL  6  XALTLENG            [CHANGE LENGTH OF HLS BLOCK  
  10 9SCK ...      CALL  6  STILLSAVED   
  17 9SCL ...      CALL  6  XHUNT               [REFIND ADATA CSTORE 
   6 9SCM ...XCOPYNEEDS  
  11 9SCN ...      ADN   3  A1+AHLS+ASTCON   
   8 9SCP ...      LDN   2  A1(1)
   8 9SCQ ...      SMO      0(2) 
  16 9SCR ...      MOVE  2  0                   [COPY NEEDS DATA 
   8 9SCS ...      LDX   2  FX2  
   9 9SCT ...      ADN   2  HLSCONREC
  17 9SCW ...      SBN   3  A1+AHLS+ASTCON      [X3 TO AREA IN HLSQ  
   6 9SCX ...SECNEEDSDEF 
  17 9SCY ...# SO THAT WE CAN USE A LOOP WE DEAL WITH WORD 6 FIRST BY
  16 9SC_ ...# PUTTING THE CORRECT VALUE IN THE CONTROL RECORD   
  21 9SD2 ...      ADN   3  A1+AHLS-2           [VIRTUAL START OF CONTROL REC IN HLSQ
   8 9SD3 ...      CALL  6  X6SET
   9 9SD4 ...      LDN   4  ASTCON-1 
   4 9SD5 ...XX1 
  16 9SD6 ...      LDXC  0  2(2)                [CONTROL RECORD  
   8 9SD7 ...      BCS      XX2  
  15 9SD8 ...      STO   0  2(3)                [UPDATE HLSQ 
   4 9SD9 ...XX2 
   7 9SD= ...      ADN   2  1
   7 9SD? ...      ADN   3  1
   8 9SD# ...      BCT   4  XX1  
  11 9SD* ...# NOW DEAL WITH MILL PRIORITY   
   7 9SDB ...      LDN   4  4
   8 9SDC ...XX3   LDCH  0  2(2) 
   8 9SDD ...      BZE   0  XX4  
   8 9SDF ...      DCH   0  2(3) 
   7 9SDG ...XX4   BCHX  2  /
   7 9SDH ...      BCHX  3  /
   8 9SDJ ...      BCT   4  XX3  
   7 9SDK ...      LDX   1  3
   8 9SDL ...      BRN      SPOS 
   7 9SDM ...XERR4 LDN   5  4
   8 9SDN ...      BRN      S3OR 
   5 9SDY ...UNSAVED 
   7 9SD_ ...      LDX   1  3
   7 9SFY ...      NGN   3  1
  18 9SH9 ...      CALL  7  RESCHED             [SET UP JOB SCHED. REQ.  
  14 9SJG ...      BZE   5  SFIN                [J IF OK 
   4 9SKR ...S3OR
   8 9SM4 ...      ORX   5  GSIGN
   8 9SN* ...      BRN      S5   
   3 9SPL ...
   4 9XDQ ...#   
  17 9XG3 ...#     THE ROUTINE BELOW DEALS WITH THE STOPJOB EXTRACODE
  13 9XH# ...#     IT HAS THE FOLLOWING CONTROL AREA 
   8 9XJK ...#     WORD0    51/1 
  11 9XKW ...#     WORD1    0- THE REPLY WORD
  15 9XM7 ...#     WORD2    2- NO. OF WORDS IN CONTROL RECORD
  18 9XND ...#     WORD3    START ADDRESS OF CONTROL REC. IN PROG'S STORE
   4 9XPP ...#   
  13 9XR2 ...#     THE CONTROL RECORD IS AS FOLLOWS  
   7 9XS? ...#     WORD0    3
  13 9XTJ ...#     WORD1    NO. OF JOB TO BE STOPPED 
   4 9XWT ...#   
   5 9XY6 ...SWIT3   
  16 9XYD ...      LDX   4  HLSCONREC+1(2)      [GET JOB NUMBER  
   8 9Y2N ...      JOBLOCK  4,3  
  18 9Y3_ ...      BNG   3  S3                  [J IF NO JOBBLOCK FOUND  
  17 9Y5= ...      SPRIVJUMP JSISSUE,3,SPOS     [J IF SYS ISS. JOB   
  16 9Y5? ...      CALL  0  SHLSQ               [FIND HLSQ BLOCK 
  17 9Y5# ...      BRN      NOHLSQ              [J IF NONE PRESENT   
  20 9Y5* ...      LDEX  5  HLSYONQA(3)         [TEST AMD J IF JOB WAITING TO BE 
  21 9Y5B ...      TXU   5  SAM1(1)             [FULLY STARTED OR IS ALREADY STOPPED 
   8 9Y5C ...      BCC      S3   
   9 9Y5D ...      TXU   5  SAM2(1)  
   8 9Y5F ...      BCC      S3   
   5 9Y5G ...NOHLSQ  
  17 9Y5H ...      CALL  5  TEMP                [FIND TEMP HLSQ BLOCK
   9 9Y5J ...      BRN      TCHECK   
  16 9Y5K ...      BRN      SAMFIN              [J NONE PRESENT  
   5 9Y5L ...TCHECK  
  17 9Y5M ...      LDEX  5  HLSYONQA(3)         [ELSE TESTT AS ABOVE 
   9 9Y5N ...      TXU   5  SAM1(1)  
   8 9Y5P ...      BCC      S3   
   9 9Y5Q ...      TXU   5  SAM2(1)  
   8 9Y5R ...      BCC      S3   
  19 9Y5S ...      CALL  5  TEMP3               [FIND NEXT TEMP HLSQ BLOCK   
  19 9Y5T ...      BRN      TCHECK              [J TO CHECK IT IF PRESENTT   
   5 9Y5W ...SAMFIN  
   9 9Y6H ...      LDN   3  2*HLINK  
  21 9Y7S ...      BRN      TACT                [J TO ROUTINE TO SET UP STOPJOB ACT-Y
  17 9Y95 ...#     THE ROUTINE BELOW DEALS WITH THE SAVEJOB EXTRACODE
  12 9Y=B ...#     THE CONTROL AREA IS AS FOLLOWS
  10 9Y?M ...#           WORD0      51/1 
  13 9Y#Y ...#           WORD1      0 (REPLY WORD)   
  17 9YB9 ...#           WORD2      [2-NO.OF WORDS IN CONTROL RECORD 
  21 9YCG ...#           WORD3      START ADDRESS OF THE CONTROL RECORD IN PROG'S ST.
   4 9YDR ...#   
  13 9YG4 ...#     THE CONTROL RECORD IS AS FOLLOWS  
   9 9YH* ...#           WORD0      4
  15 9YJL ...#           WORD1      NO. OF JOB TO BE SAVED   
   4 9YKX ...#   
  16 9YM8 ...SWIT4 LDX   4  HLSCONREC+1(2)        [GET JOB NUMBER
  17 9YNF ...      CALL  5  TEMP                [LOOK FOR IT ON TEMPQ
  15 9YPQ ...      BRN      S3                  [J IF FOUND  
  18 9YQK ...      CALL  0  SHLSQ                [SET X3 TO HLSQ BLOCK   
  16 9YRD ...      BRN      S3                  [J IF NONEXISTANT
  19 9YS# ...                                   [J IF NO SUCH BLOCK EXISTS   
  10 9YTK ...      LDX   0  HLSYONQ(3)   
   8 9YWW ...      ANDN  0  #5   
   8 9YY7 ...      BZE   0  S3   
  12 9Y_D ...      HLSAV    3,SET,SAFE,HLSQ,SPOS 
  21 9_2P ...#     IT IS NOT NECESSARY IN THE ABOVE CASE TO CHECK THE UNSAVE BIT AS  
  14 9_42 ...#     THIS IS ALWAYS SET WITH THE SAVED BIT 
   4 9_5? ...#   
  20 9_6J ...#     UNTIL THE SETTING OF THE   SAVING BITS IN JOBQ AND HLSQ THERE 
  11 9_7T ...#     SHOUD BE NO COORDINATION  
  20 9_96 ...      JOBLOCK  4,1                 [FIND JOBLOCK OF JOB TO BE SAVED 
   9 9_=C ...      LDX   6  JMISC(1) 
  15 9_?N ...      BPZ   6  NEVER               [J IF MOP JOB
  18 9_#_ ...      SPRIVJUMP  JSISSUE,1,NEVER   [J IF SYSTEM ISSUED JOB  
  18 9_B= ...      SPRIVJUMP  JSSTART,1,NEVER   [J IF SYSTEM STARTED JOB 
   8 9_CH ...      LDCT  0  #100 
  19 9_DS ...      ANDX  0  6                   [STOPPED BIT - FOR BREAKIN   
   8 9_G5 ...      ANDN  6  #777 
  18 9_HB ...      BNZ   6  NOTYET              [J IF JOB HAS CORE IMAGE 
  20 9_JM ...      BNZ   0  NEVER               [J IF BRK = ABANDON OR CONNECT   
  15 9_KY ...      HLSAV    1,SET,SAVING,JOBQ   [SET MARKER  
   4 9_M9 ...#   
  21 9_NG ...#     WHEN THE SAVING BIT IS SET IN THE JOBLOCK WS AND WJ ASSUME THAT   
  20 9_PR ...#     THE HLSQ BLOCK IS SET UP WITH JOB AND USER NAME AND JSOURCE3  
   4 9_R4 ...#   
   9 9_S* ...      LDN   5  JNAME(1) 
   9 9_TL ...      LDX   7  JPRIV(1) 
   7 9_WX ...      FCAJO 1   
  10 9_Y8 ...      LDX   6  JSOURCE3(1)  
  18 9__3 ...      CALL  0  SHLSQ                [SET X3 TO HLSQ BLOCK   
  17 9__W ...      BRN      US2                  [J IF NONEXISTANT   
  11 =22Q ...      HLSAV    3,SET,SAVING,HLSQ
   9 =243 ...      CANTNOW  3,CLEAR  
   9 =25# ...      STO   6  HLSPCT(3)
  10 =26K ...      STO   7  HLSPRIV(3)   
  10 =27W ...      LDN   6  HLSJNAM(3)   
   7 =297 ...      MOVE  5  6
   7 =2=D ...      LDN   3  0
   4 =2?P ...TACT
   9 =2*2 ...      CALL  5  SAVEMOVE 
  20 =2B? ...      LDX   2  3                   [SET X2 =PTR TO SAVEJOB ACTIVITY 
  20 =2CJ ...      FPUT                         [PUT SAVEJOB ACTIVITY ON THE LIST
  17 =2DT ...      LDN   5  0                   [SET ZERO REPLY WORD 
   8 =2G6 ...      BRN      S5   
   5 =2HC ...NOTYET  
  17 =2JN ...      LDCT  5  #040                [TO SET B3 OF HLSYONO
   8 =2K_ ...      BRN      NEV1 
  17 =2M= ...NEVER LDCT  5  #100                [TO SET B2 OF HLSYONO
  10 =2NH ...NEV1  ORS   5  HLSYONQ(3)   
  17 =2PS ...      LDX   5  GSIGN               [SET REPLY WORD -VE. 
   8 =2R5 ...      BRN      S5   
   4 =2SB ...#   
  17 =2TM ...#     THE ROUTINE BELOW DEALS WITH THE MOPWAIT EXTRACODE
  12 =2WY ...#     THE CONTROL AREA IS AS FOLLOWS
   8 =2Y9 ...#     WORD0    51/1 
  11 =2_G ...#     WORD1    0  (REPLY WORD)  
  16 =32R ...#     WORD2    3  -NO. OF WORDS IN CONTROL RECORD   
  19 =344 ...#     WORD3    START ADDRESS OF CONTROL RECORD IN PROG'S STORE  
   4 =35* ...#   
  13 =36L ...#     THE CONTROL RECORD IS AS FOLLOWS  
   7 =37X ...#     WORD0    5
  17 =398 ...#     WORD1    JOB NO. OF MOP JOB WHICH CANNOT BE F.ST. 
  19 =3=F ...#     WORD2    MESSAGE NO. OF MESSAGE TO BE SENT TO MOP CONSOLE 
   4 =3?Q ...#   
  16 =3*3 ...SWIT5 LDX   4  HLSCONREC+1(2)        [GET JOB NUMBER
  18 =3*W ...      CALL  0  SHLSQ                [SET X3 TO HLSQ BLOCK   
  17 =3BP ...      BRN      TRYJL                  [J IF NONEXISTANT 
  13 =3CK ...      HLSAV    3,SET,SAVING,HLSQ,S3OUT  
  19 =3DW ...      JOBLOCK  4,1                 [FIND JOBLOCK FOR THIS JOB   
  17 =3G7 ...      BNG   1  S3OUT                  [J IF NO JOBLOCK  
   9 =3HD ...      LDN   3  BHTEMP   
   8 =3JP ...T3    CALL  5  TEMP3
   8 =3L2 ...      BRN      T1   
   8 =3M? ...      FCAJO    3,1  
  10 =3NJ ...      LDX   0  JOBEVENTS(3) 
   8 =3PT ...      LDX   2  FX2  
  17 =3R6 ...      BNG   0  S3OUT                  [J IF BREAKIN SET 
  17 =3SC ...      LDX   5  HLSCONREC+2(2)        [GET MESSAGE NUMBER
  20 =3TN ...      STO   5  JMESS(1)            [STORE IN MESSAGE WORD OF JOBLOCK
   7 =3W_ ...      LDN   4  0
   9 =3Y= ...      CALL  7  STJOBMESS
   5 =3_H ...OUTCHAIN
  10 =42S ...      HUNTW    1,GMON,HLSOUT
   8 =445 ...      BNG   1  NOOUT
   9 =44P ...      STO   5  A1+1(1)  
  10 =45B ...      LDX   3  ACTRING(3)   
   9 =46M ...      SBN   3  ACTRING  
   9 =47Y ...      CHAIN    1,BPTR(3)
   5 =499 ...NOOUT   
   9 =4=G ...      BRN      SETOKREP 
   5 =4?R ...TRYJL   
  10 =4*4 ...      HUNTW    3,GMON,HLSOUT
  15 =4B* ...      BPZ   3  SJLA       [J IF MESSAGE GIVEN   
  11 =4CL ...      SETNCORE 1,3,GMON,HLSOUT  
  11 =4DX ...      LDX   0  HLSCONREC+2(2)   
   8 =4G8 ...      ORX   0  GSIGN
   8 =4HF ...      STO   0  A1(3)
  11 =4L3 ...SJLA  LDX   5   HLSCONREC+2(2)  
  16 =4NK ...      STO   5  A1+1(3)       [KEEP MESSAGE NUMBER   
   4 =4R7 ...SJLB
   8 =4SD ...      LDX   3  FX2  
  12 =4TP ...SJL   FINDACTF 3,3,CPAT,,S3OUT,FPTR 
  12 =4X2 ...      HUNT2J   1,JWELL,COPYSYS,3,SJL
  11 =4Y? ...      BXU   4  JLJOBNO+A1(1),SJL
  17 =4_J ...      LONGON   #145     [KICK ALL (UNFORTUNATELY) MOPS  
   9 =52T ...      BRN      OUTCHAIN 
   8 =546 ...T1    LDN   0  #10  
  10 =55C ...      ANDX  0  HLSYONQ(3)   
  19 =56N ...      BZE   0  T3                  [J IF NOT BEING BROKEN IN ON 
   5 =57_ ...S3OUT   
  10 =59= ...      VFREEW   GMON,HLSOUT  
   8 =5=H ...      BRN      S3   
   4 =5?S ...ZK2 
  18 =5*5 ...      LDX   4  ACOMMUNE4(2)        [NO OF JOB TO BE UNSAVED 
  18 =5*Y ...      CALL  0  SHLSQ                [SET X3 TO HLSQ BLOCK   
  17 =5BR ...      BRN      US8                  [J IF NONEXISTANT   
  12 =5CM ...      HLSAV 3,SET,SAVING,HLSQ,US4   
   4 =5DY ...#   
  21 =5G9 ...#     THERE HAS BEEN A LONG TIME LAG SINCE LAST CHECKING THE SAVE(ING)  
  13 =5HG ...#     BITS SO WE ENSURE THE JOB IS SAVED
  13 =5JR ...      HLSAV    3,CLEAR,SAVED,HLSQ,US8   
  20 =5L4 ...#     DURING UNSAVE ALL SORTS OF THINGS HAPPEN SO SAVE THE ACOMMUNE 
   6 =5M* ...#     WORDS 
   4 =5NL ...#   
   7 =5P= ...      FSHSKIP   
   7 =5PX ...US6   UNSAVE   4
  20 =5R8 ...      BRN      US5                 [J IF UNSAVE WAS NOT SUCCESSFUL  
  19 =5SF ...      TESTREP  ALREADY,US8         [J IF UNSAVED BY SOMEONE ELSE
  17 =5TQ ...      BRN      US75                [IN SAVEJOB ACTIVITY 
   8 =5X3 ...US4   LDCT  5  #020 
  20 =5Y# ...      ORS   5  HLSYONQ(3)          [SET REVERSE SAVE BIT IN HLSBLOCK
  20 =5_K ...US7   COOR3    #34                 [SET ACTSAVE WAITING FOR SAVEJOB 
  20 =62W ...                                   [MACRO TO PICK UP THE MARKER SET 
  18 =63P ...      CALL  0  SHLSQ                [SET X3 TO HLSQ BLOCK   
  17 =64J ...      BRN      US2                  [J IF NONEXISTANT   
  10 =65D ...      LDX   5  HLSYONQ(3)   
  18 =66P ...      BNG   5  US6                 [J IF JOB HAS BEEN SAVED 
   7 =682 ...      SLL   5  1
  20 =69? ...      BNG   5  US7                 [J IF JOB IS STILL BEING SAVED   
  16 =6=J ...      FREECORE 3                   [FREE HLSQ BLOCK 
  17 =6?T ...US75  CALL  7  RESCHED           [SET UP JOB SCHED. REQ.
  18 =6*6 ...      BNZ   5  US8                [J IF BREAK-IN/BEING SAVED
  16 =6BC ...      CALL  7  STARTJOB           [PUT ACTIV ON LIST
   8 =6CN ...      BRN      US8  
   4 =6D_ ...US5 
  17 =6G= ...      LDX   6  4                   [COPY JOB NO. INTO K6
  19 =6H5 ...      CALL  0  SHLSQ                    [SET X3 TO HLSQ BLOCK   
  18 =6HY ...      BRN      US2                      [J IF NONEXISTANT   
  10 =6JS ...      LDX   5  HLSSTAT(3)   
   7 =6L5 ...      LDCT  7  1
   7 =6MB ...      ANDX  7  5
   7 =6NM ...      FREECORE 3
  17 =6PY ...      BZE   7  US1                 [J IF TENT STARTED   
   7 =6R9 ...      LDN   3  1
  15 =6SG ...      BNG   5  US1                 [J IF OFFLINE
   6 =6X4 ...      SUBJOB
   7 =6Y* ...      LDN   3  0
   8 =72X ...      BRN      US81 
   4 =748 ...US1 
   7 =783 ...      SUBJOBJ   
   4 =79# ...US81
  21 =7BD ...      DOWN     JOBLIST,3           [DELETE ENTRY FOR THIS JOB IN SYSTEM 
  20 =7CP ...                                   [JOBLIST -COULD NOT BE UNSAVED   
  20 =7F2 ...US8   SUICIDE                      [COMMIT SUICIDE ON THE ACTSAVE   
   9 =7G? ...US2   GEOERR   1,LOSTHLS
   3 =7HJ ...
   3 =#BK ...
  18 =#CW ...#     THE ROUTINE BELOW CONTAINS THE CODE FOR STOPPING A JOB
  14 =#F7 ...#     ACOMMUNE4 = NO. OF JOB TO BE STOPPED  
   4 =#GD ...#   
   4 =#HP ...ZK7 
  18 =#K2 ...      LDX   6  ACOMMUNE4(2)        [JOB NUMBER TO BE STOPPED
   4 =#L? ...RTRY
   8 =#MJ ...      JOBLOCK  6,3  
   8 =#NT ...      BNG   3  TEND 
  11 =#Q6 ...      SPRIVJUMP  JSISSUE,3,TEND 
  10 =#RC ...      DOWN     PROCONTX,1   
   8 =#SN ...      BRN      NSTOP
   7 =#T_ ...TEND  SUICIDE   
   5 =#X= ...NSTOP   
   8 =#YH ...      COOR3    #62  
   8 =#_S ...      BRN      RTRY 
   5 =*35 ...#  **** 
   4 =*JG    #END
   8 ____ ...52106745000100000000
