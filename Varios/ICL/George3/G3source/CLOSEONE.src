  15 229S ...      SEG      CLOSEONE,863,SECTION FILE,CLOSE  
   4 22FL ...#   
  15 22KH ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   7 22PD ...#     USES OF X7
   7 22_= ...#     ----------
   4 2394 ...#   
  14 23DW ...      BITDEFS  7,23,XBSET          [CLOSESET
   4 23NN ...#   
   4 23YG ...#   
   8 248# ...#     ENTRY POINTS  
   8 24D6 ...#     ------------  
   4 24MY ...#   
  17 24XQ          SEGENTRY K1CLOSEONE,XENT1    [IN AUTOCLOSE ENTRY. 
  19 25CB ...      SEGENTRY K3CLOSEONE,XENT3    [RE-OPEN SUPERIOR DIRECTORY. 
  18 25X2 ...      SEGENTRY K4CLOSEONE,XENT4    [SET UP AUTOCLOSE ENTRY. 
   4 26BL    #   
  20 26W=    #     CLOSEONE CONTAINS THE NEXT TWO STAGES IN THE DIRECTORY UPDATE:
  14 27?= ...#           AN AUTONOMOUS CLOSE IS SET UP.  
  14 27N= ...#           WHEN THAT STARTS, IT SETS UP A  
  20 285= ...#           FINTER3 TO HOLD THE FILE'S BLOCK NUMBERS, AN EMPTYB TO  
  20 28G? ...#           HOLD ANY SPARE BLOCK NUMBERS & A FI/CFINDEXF TO HOLD THE
  18 28X# ...#           INDEX (IF ANY). THE DIRECTORY IS THEN OPENED.   
   4 29#B    #   
   5 29J8 ...QLINK   
  10 29S2 ...      LINK     CLOSEONE,1   
   4 2=3S ...#   
   4 2=?L ...TAB 
  16 2=HD ...      TOPFCAB  3,2                 [X2->FCB  X3->FCA
   7 2=R=          EXIT  6  0
   4 2?=W ...#   
   4 2?QG ...#   
  20 2#7W ...#     CHECK THAT THE FILE WHOSE DIRECTORY ENTRY IS TO BE UPDATED IS 
  21 2#K= ...#     STILL RINGED IN (HAS NOT BEEN CLOSESHORTED OR CLOSEABANDONED).  IF
  21 2*2L ...#     IT ISN'T, GO ACROSS TO K8CLOSEALT IF FILES RINGED IN OR TO SUICIDE
   7 2*D2 ...#     IF NONE.  
  21 2*TB ...#     ON ENTRY, X2=FX2, X5= FCB NO. OF FILE WE ARE CLOSING.  X7 S/R LINK
  21 2B=Q ...#     ON EXIT, IF EXITS X2->FCB X3->FCA X5=FCB NO..   OVERWRITES X0,X6. 
   6 2BN=    SCLOSEFCB   
  10 2C7W ...      LDN   0  FILERING(2)  
  18 2CMG ...      BXE   0  FILERING(2),SUICIDA [J IF NO FILE RINGED IN  
  16 2D76 ...      CALL  6  TAB                 [X2->FCB, X3->FCA
  20 2DFH ...      BXE   5  BACK2(2),(7)        [EXIT IF FILE BEING CLOSED STILL 
  15 2DRY ...                                   [ RINGED IN  
  20 2F6B ...      ACROSS   CLOSEALT,8          [DIFFERENT FCB - OURS HAS GONE   
   4 2H4W    #   
   4 2HJG ...#   
   4 2J46    #   
  21 2JHQ    #     ENTRY EITHER FROM A NEWLY SET UP AUTONOMOUS CLOSE, OR FROM CLOSEND
  21 2K3B    #           BY AN AUTONOMOUS CLOSE ALREADY ESTABLISHED IN THE CAREFUL   
  21 2KH2 ...#           UPDATE LOOP, TO UPDATE THE DIRECTORY ENTRY OF THE TOP FILE. 
  21 2L2L    #     SET UP A FINTER3 TO HOLD THE BLOCK NUMBERS FROM THE FCB - LATER   
  20 2LG=    #           THE BLOCKS USED ARE DECANTED INTO THIS, AND THE BLOCKS  
  21 2L_W    #           RECORD UPDATED FROM IT, SINCE THE BLOCKS IN THE FCB MAY BE  
  13 2MFG    #           ALTERED OVER A COORDINATION.
  21 2M_6 ...#     SET UP AN EMPTYB TO HOLD ANY SPARE BLOCKS & A CFINDEXF TO CONTAIN 
  15 2NDQ ...#     THE INDEX (IF ANY). OPEN THE DIRECTORY.   
   5 2NYB    XENT1   
  21 2P=R ...      CALL  7  SCLOSEFCB           [CHECK IF FILE HAS BEEN CLOSESHORTED 
  17 2PK8 ...                                   [ OR CLOSEABANDONED  
  17 2PXL ...                                   [X2->FCB   X3->FCA   
  21 2Q#6 ...      BS       2,BFAUTO            [SET 'FILE NOW RINGED TO AUTOCLOSE'  
  14 2QNL ...                                   [ MARKER 
  20 2R56 ...      JBCC     NOWAKE,2,BFAUTOW    [J IF NO 'AUTOCLOSE TO BE SET UP'
  14 2RFL ...                                   [ WAITERS
  16 2RW6 ...      TRACEIF  K6CLOSEONE,99,299,FLOC1(2),REL CLWT  
  17 2S*Q ...      LONGON1  CLOSEWAIT,BACK2(2)  [RELEASE AUTO WAITERS
  16 2STB ...      CALL  6  TAB                 [X2->FCB, X3->FCA
   5 2WRW    NOWAKE  
   9 2X?G          LDX   3  FUSEBL(2)
   9 2XR6          SBX   3  FBLMOD(2)
  19 2Y=Q ...      BZE   3  NOSPARE             [J IF NO SPARE BLOCKS IN FCB 
  12 2YQB ...      SETNCORE 2(3),1,BSTB,EMPTYB   
  19 2_4R ...      CALL  7  SCLOSEFCB           [CHECK IF FILE STILL THERE   
  17 2_C8 ...                                   [ X2->FCB, X3->FCA   
  20 2_PL ...      BS       3,BAEMPTYB          [INDICATE IN FCA 'EMPTYB SET UP' 
   5 32NW    NOSPARE 
   9 338G ...      LDX   3  FBLMOD(2)
  19 33N6 ...      SBN   3  FBLKS-A1-2          [X3= NO. OF BLOCKS IN FCB + 2
  16 347Q ...      JFZ      NIND,2,FFINDEXNO    [J IF NOT INDEXED
  21 34MB ...      SETNCORE 1(3),1,FI,CFINDEXF  [SET UP FI/FINDEXF BLOCK TO CONTAIN  
  14 3572 ...                                   [ INDEX  
   4 384Q    NIND
  21 38*= ...      SETUPCOR 3,1,FILE,FINTER3    [SET UP FINTER3 TO HOLD NEW BLOCKS   
  14 38KQ ...                                   [ RECORD 
  20 38W# ...      SETNCORE 10,1,FILE,FABSNB    [SET UP A FABSNB FOR THE OPEND   
  17 396S ...      CALL  7  SCLOSEFCB           [SEE IF FILE HAS GONE
  17 39C# ...                                   [ X2->FCB, X3->FCA   
   8 39MW ...      LDN   6  10   
  19 3=4M ...      STO   6  A1(1)               [SET UP FABSNB RECORD HEADER 
  21 3=*7 ...      TRACEIF  K6CLOSEONE,99,299,FUSER1(2),CLOS SUP  [SUPERIOR USER OF  
  17 3=KM ...                                   [ FILE BEING CLOSED  
  17 3=W9 ...      JBC      NOTDIR,2,BFDIR      [J IF NOT A DIRECTORY
  21 3?5W ...      TRACEIF  K6CLOSEONE,99,299,FME1(2),CLOS USE  [USER NAME OF DIR.   
  16 3?*H ...                                   [ BEING CLOSED   
  21 3?K8 ...                                   [SET UP 10 WORD FABSNB FOR DIR. SINCE
  16 3?ST ...                                   [ DOING OPENDIR  
  20 3#4J ...                                   [MAKE IT OPTIMUM FORMAT FOR OPEN 
   9 3#F* ...      LDX   0  HCOLUSER 
  17 3#W6 ...      STO   0  A1+4(1)             [INSERT 'SP SP SP :' 
   9 3*=X ...      LDN   3  FME1(2)  
   9 3*MN ...      LDN   4  A1+5(1)  
  17 3B4F ...      MOVE  3  3                   [USER NAME TO FABSNB 
   8 3BF= ...      LDX   0  ACES 
  14 3BW3 ...      STO   0  A1+8(1)             [SPACES  
   9 3C=S ...      LDN   4  FLOC1(2) 
   7 3CMK ...      SUM   3  3
  21 3D4B ...      STO   3  A1+9(1)             [KEY (SUM OF LOCAL NAME) TO FABSNB   
   7 3DF7 ...      LDN   6  4
   9 3DW2          BRN      XDIRTER  
   5 3F*L    NOTDIR  
  21 3FT= ...      TRACEIF  K6CLOSEONE,99,299,FLOC1(2),CLOS LOC  [NAME OF TERMINAL   
  17 3G#W ...                                   [ FILE BEING CLOSED  
   5 3H#6    XDIRTER 
  21 3HNL ...      LDN   3  FUSER1(2)           [MOVE SUPERIOR USER NAME TO FABSNB   
   9 3J56 ...      LDN   4  A1+1(1)  
   7 3JFL ...      SMO      6
   8 3JW6 ...      MOVE  3  511  
   4 3K=L    #   
   9 3KQ=    #     OPEN THE DIRECTORY
  21 3L9W    #     K3CLOSEONE IS AN ENTRY FROM CLOSEFUL TO REOPEN THE DIRECTORY AFTER
  20 3LPG    #           WE HAD TO CLOSE IT WHILE WAITING FOR SOMEONE TO FINISH  
  18 3M96    #           WRITING TO, OR COPYING THE FILE WE'RE CLOSING   
   5 3ML6 ...XENT3   
  14 3N36 ...      SETMODE  4,GENERAL,DIR,QUERY,ERASING  
   6 3ND6 ...OPENAGAIN   
  21 3NT6 ...      OPEND    XBRK,4              [OPEN DIRECTORY OF FILE BEING CLOSED 
   7 3P=9 ...#UNS  CLOSESTATS
   4 3P#S ...(   
   7 3PCC ...      LDN   0  1
  20 3PG2 ...      ADS   0  CLSTAT2             [NO. OF OPENDIRS DONE BY CLOSE   
   4 3PJK ...)   
  18 3PL= ...      TESTRPN2 OK,VERR             [J IF ERROR ON OPENDIR   
  21 3PMX ...                                   [IF OPENS OK 2ND TIME ROUND, CARRY ON
  21 3PPJ ...                                   [SINCE MISREAD FROM BS ON 1ST CIRCUIT
  10 3PR9 ...      LDN   3  FILERING(2)  
  10 3PSW ...      SMO      FILERING(2)  
  17 3PWH ...      LDX   2  FPTRF               [X2-> FCA AT DEPTH 1 
  21 3PXL ...      BXE   2  3,SUICIDB           [J IF NO FCA AT DEPTH 1 (REMOVED BY  
  16 3PYP ...                                   [ CLOSEABANDON)  
   8 3P_T ...      PSTAC    2,2  
  17 3Q3G ...      BFCBX    2,2                 [X2-> FCB AT DEPTH 1 
  21 3Q57 ...      BXU   5  BACK2(2),XFCBGONE   [J IF OUR FCB GONE (CLOSEABANDONED)  
  21 3Q5* ...                                   [HAVING CHECKED THAT THE FCB IS STILL
  21 3Q5H ...                                   [ THERE, SINCE THE DIRECTORY IS OPEN,
  21 3Q5P ...                                   [ THERE IS NO POSSIBILITY OF IT BEING
  21 3Q5X ...                                   [ REMOVED HEREAFTER BY CLOSEABANDON  
  21 3Q65 ...                                   [ OR CLOSESHORT (BOTH OF WHICH CAN   
  20 3Q6? ...                                   [ ONLY REMOVE THE FCB WITH THE   
  17 3Q6F ...                                   [ DIRECTORY OPEN).   
  10 3Q6W          MHUNTW   2,FILE,ENT   
   9 3QLG ...      LDEX  0  ECOPS(2) 
  18 3R66 ...      BZE   0  NOCOP               [J IF NO COPIES RECORD   
  19 3RKQ ...      BACKSPACE                    [REPOSITION ON NAME RECORD   
   5 3S5B ...NOCOP   
  10 49?=          ACROSS   CLOSEFUL,1   
   4 49M= ...#   
   4 4=3= ...#   
  21 4=?= ...VERR                               [SHOULDN'T LOOP SINCE GEOERR IF NO   
  19 4=H= ...                                   [ QUERY MODE & NON-OK REPLY  
  19 4=R= ...      CALL  7  SCLOSEFCB           [CHECK FILE STILL RINGED IN  
  21 4?3= ...      SETMODE  4,GENERAL,DIR,ERASING [NO QUERY MODE THIS TIME SO WILL   
  18 4??= ...                                   [ BREAK WITH DIR. OPEN   
   9 4?H= ...      BRN      OPENAGAIN
   4 4?X= ...#   
   4 4#?= ...#   
   5 4#M= ...XFCBGONE
  19 4*3= ...      ACROSS   CLOSEALT,7          [FCB GONE & DIRECTORY OPEN   
   4 4*C= ...#   
   4 4*R= ...#   
   5 4B7G    SUICIDB 
  16 4BM6 ...      CLOSETOP                     [CLOSE DIRECTORY 
   5 4C6Q    SUICIDA 
  20 4CLB ...      TRACEIF  K6CLOSEONE,99,299,FX2,CLOSED1 [FILE CLOSEABANDONED OR
  21 4D62 ...                                   [ CLOSESHORTED & NO MORE FILES OPEN  
   7 4F5=          SUICIDE   
   4 4F=D ...#   
   4 4FCL ...#   
   4 4FJW    XBRK
  11 4G4G          GEOERR   BRIEFPM,CL BRKIN 
   4 4GJ6    #   
   4 4GP# ...#   
   4 4GWG ...#   
  20 4H3Q    #     SET UP AN AUTONOMOUS CLOSE ACTIVITY AND RERING THE FILE TO IT 
  20 4HHB    #           UNLESS IT'S A CLOSESET, WHEN WE SET UP A NEW FCA FOR THE
  20 4J32    #           CLOSING ACTIVITY, MARK IT AS A CLOSESET FCA AND LEAVE   
  21 4JGL    #           THE CLOSING ACTIVITY ATTACHED TO IT, WAITING FOR THE CAREFUL
  10 4K2=    #           UPDATE TO FINISH
   5 4KFW ...XENT4   
  10 4K_G          GETACT   BSA,AUTCLOSE 
   7 4L45 ...#UNS  CLOSESTATS
   4 4L6N ...(   
   7 4L9? ...      LDN   0  1
  19 4L?W ...      ADS   0  CLSTAT1             [NO. OF AUTOCLOSE ACTIVITIES 
   4 4LBF ...)   
   5 4LF6    XCHECK  
  10 4LYQ ...      LDN   0  FILERING(2)  
  20 4M#F ...      BXE   0  FILERING(2),NOFILE  [J IF NO FILE OPEN TO ACTIVITY   
  20 4MN8 ...                                   [ (SNATCHED AWAY BY CLOSESHORT OR
  16 4N3X ...                                   [ CLOSEABANDON)  
  16 4NCL ...      CALL  6  TAB                 [X2->FCB, X3->FCA
  20 4NX= ...      BXU   5  BACK2(2),NOFILE     [J IF TOP FILE OPEN SNATCHED AWAY
  17 4W?6 ...      JBC      NONEWFCA,,XBSET     [J IF NOT CLOSESET   
  16 4WQQ ...      SETUPFCA 3,2,XCHECK,4        [SET UP NEW FCA  
  14 4X=B ...      TOPFCB2  3                   [X3->FCB 
  21 4XJR ...      INITLFCA                     [INITIALISE RED TAPE OF RING ELEMENT 
  17 4XX8 ...                                   [ & UPDATE ARINGNO   
  16 4Y9L ...                                   [X2-> NEW ELEMENT
  18 4YP= ...      NGS   2  FREADWORD(2)        [INITILISE READ POINTERS 
  10 4_8W          NGS   2  FREADBLOCK(2)
  18 4_H? ...      STOZ     FGENERAL1(2)        [INITIALISE MARKER WORDS 
  20 4_Q9 ...      BS       2,BASET             [THIS ACT. IS ABOUT TO WAIT ON A 
  15 4__7 ...                                   [ CLOSESET   
  10 5286          STOZ     FGENERAL2(2) 
  21 52DD ...      BS       3,BFCLOSESET        [SET 'SOMEONE HAS DONE CLOSESET ON   
  16 52NQ ...                                   [ THIS FILE' BIT 
  16 52_6 ...      TRACEIF  K6CLOSEONE,99,299,FLOC1(3),WT CLST1  
  18 53*L ...      TOPFCA   3                   [X3-> CURRENT TOP ELEMENT
  20 53KY ...      ENRING   2,3                 [RERING NEW FCA IN FRONT OF TOP  
  14 53W= ...                                   [ ELEMENT
   5 546L    NONEWFCA
   8 54L=          SMO      FX2  
  18 555W ...      LDX   2  BPTR                [X2-> AUTOCLOSE ACTIVITY 
  16 55KG ...      TRACEIF  K6CLOSEONE,99,299,ACNUM(2),AUTCLOSE  
  19 5656 ...      LINKSET  2,QLINK(1)          [SET LINK IN AUTOCLOSE ACT.  
  19 56JQ ...      ADN   2  FILERING            [X2-> FILE RING OF AUTOCLOSE 
  21 56X7 ...      RERING   3,2                 [RERING FCA FOR FILE BEING CLOSED TO 
  15 579J ...                                   [ AUTOCLOSE  
  16 57J2 ...      LDX   2  BPTR(2)             [X2->AUTOCLOSE   
  20 583L ...      STO   5  ACC5(2)             [GIVE IT THE FCB NO. OF THE FILE 
  17 58H= ...      FPUT                         [KICK THE AUTOCLOSE  
  17 592W ...      JBC      UP,,XBSET           [J IF NOT CLOSESET   
   6 5BBQ          COOR1 
   4 5BG* ...UP  
   5 5BKY ...      UP
   4 5BPH ...#   
   4 5BT6 ...#   
   5 5BYP ...NOFILE  
   8 5C4# ...      SMO      FX2  
   8 5C7X ...      LDX   2  BPTR 
   7 5C?G ...      LOCK     2
   9 5CC5 ...      LDX   2  BPTR(2)  
   7 5CGN ...      MURDER   2
   5 5CL? ...      UP
   4 5CPW ...#   
   4 5CTL    #END
   8 ____ ...67225265000100000000
