   7 22FL    #SEG  RMTLFEND  
  14 22_=    #LIS  K0RMTLFEND>K0PCT>K0ALLGEO>K0GREATGEO  
  10 23DW                   8HRMTLFEND   
   7 23YG    # ENTRY POINTS  
  12 24D6          SEGENTRY K1RMTLFEND,Z1RMTLFEND
  12 24XQ          SEGENTRY K2RMTLFEND,Z2RMTLFEND
   4 25CB    #   
   4 25X2    [   
  20 26BL    [THIS SEGMENT DEALS WITH THE END OF FILE PROCESSING AFTER RMTLF HAS 
  20 26W=    [OUTPUT A FILE TO A REMOTE LP OR TP WHERE THE 7020 IS ATTACHED VIA A
  15 27*W    [MULTIPLEXOR/UNIPLEXOR/SCANNER -I.E. NOT A 7900.
  12 27TG    [THE CURRENT ACTIVITY IS AN ACTLF.  
   6 28*6    [ON ENTRY   
  15 28SQ    [AWORK1 = +VE FOR LP OUTPUT. -VE FOR TP OUTPUT  
   7 29#B    #DEF  NUM=IWORK1
   8 29S2    #DEF  REST=IWORK2   
   7 2=?L    #DEF  NLI=IWORK3
   8 2=R=    #DEF  TBITS=IWORK4  
   8 2?=W    #DEF  QUAL=IWORK5   
   7 2?QG    #DEF  ZFE5=#7635
   9 2#=6    ZDLEFE1        #76007631
   4 2#PQ    #   
   6 2*9B    SPLNUPAGE   
   8 2*P2          SMO      FX1  
   9 2B8L          LDN   1  ZDLEFE1  
   7 2BN=          MVCH  1  4
   8 2C7W          LDCT  1  #200 
   8 2CMG          SMO      FX2  
   9 2D76          LDX   0  IWORK11  
   8 2DLQ          SLL   0  12   
   8 2F6B          ADN   0  ZFE5 
   7 2FL2          MVCH  1  3
   8 2G5L          LDX   1  FX2  
   7 2GK=          SBN   6  7
   7 2H4W          SBN   7  3
   7 2HJG          EXIT  4  0
   4 2J46    #   
   9 2JHQ    STX            #76227632
   9 2K3B    SNULL          #76207620
  10 2KH2    PF1            2,#76327620  
  10 2L2L    PF2            4,#76327632  
   9 2LG=    PF12           #763301  
   4 2L_W    #   
   4 2MFG    #   
  20 2M_6    #     SUBROUTINE TO RELOCATE FILE RECORD EITHER IN FILE ITSELF OR IN
  10 2NDQ    #     THE ADATA,ACONV BLOCK 
   4 2NYB    #   
   5 2PD2    RECLOC  
   9 2PXL          LDXC  0  QUAL(2)  
  17 2QC=          BCC      STEPAGAIN           [J IF NO TRANSFORMING
  18 2QWW    RCONV MHUNTW   3,ADATA,ACONV       [LOCATE TRANSFORM BLOCK  
   8 2RBG          ADN   3  A1   
   8 2RW6          BRN      (4)  
   6 2S*Q    STEPAGAIN   
   8 2STB          SBX   4  FX1  
  16 2T*2          STEPAGAIN                    [RELOCATE RECORD 
   8 2TSL          ADX   4  FX1  
   8 2W#=          BRN      (4)  
   4 2WRW    #   
   4 2X?G    #   
  16 2XR6    #     SUBROUTINE TO WAKE UP THE PCT POLLING ACTIVITY
   4 2Y=Q    #   
   8 2YQB    WAKUP WAKEUP   LF   
   7 2_=2          EXIT  6  0
   4 2_PL    #   
  16 329=    #     SUBROUTINE TO GET THE LAST CHARACTER DEPOSITED
   4 32NW    #   
  15 338G    SBACK STO   0  GEN0                [SAVE LINK   
  19 33N6          SLC   2  2                   [STEP BACK THE OUTPUT POINTER
   7 347Q          SBN   2  1
   7 34MB          SRC   2  2
  16 3572          LDCH  0  0(2)                [GET THE CHAR.   
  13 35LL          BRN      (GEN0)              [EXIT
   4 366=    #   
  16 36KW    #     SUBROUTINE TO SET UP THE ^CLEAN FINISH^ COUNT 
   4 375G    #   
  20 37K6    SETCL LDN   0  CLEANUM             [CLEANUM = 8 WHEN I LAST LOOKED  
  14 384Q          ADXC  0  AWORK1(2)           [J IF TP 
   8 38JB          BCS      SETTP
  21 3942          SLL   0  2                   [*4 FOR *LP (APPROX 1/2 PAGE FULL)   
  16 39HL    SETTP SBN   0  1                   [FAIRLY ARBITRARY
  18 3=3=          STO   0  IWORK15(2)          [TEST WHEN IT GOES -VE   
   7 3=GW          EXIT  4  0
   4 3?2G    #   
  21 3?G6    #     SUBROUTINE TO FLIP, IF X6 IS NON-ZERO, THEN GET THE NEXT BUFFER   
   4 3?_Q    #   
  19 3#FB    SFLIP MHUNTW   3,A7020,ALIST       [FIND THE OUTPUT BUFFER BLOCK
  17 3#_2          STO   3  AWORK2(2)           [REMEMBER ITS ADDRESS
  18 3*DL          BZE   6  SLEEP               [J IF FLIP SWITCH UNSET  
  21 3*Y=          FLIP                         [TELL PCT ACTIVITY WE'RE READY TO GO 
  21 3BCW          BRN      SLEEP               [J TO SET UP THE NEXT OUTPUT BUFFER  
   4 3BXG    #   
  20 3CC6    #     SUBROUTINE TO STEP ON TO THE NEXT BUFFER WHEN IT IS AVAILABLE 
   4 3CWQ    #   
  16 3DBB    SLP1  SBX   4  FX1                 [RELATIVISE LINK 
  20 3DW2          CALL  7  WAIT                [WAIT FOR A BUFFER TO BECOME FREE
   7 3F*L          ADX   4  1
  21 3FT=          ORX   4  GSIGN               [SET LINK -VE TO SHOW COORDINATION   
   4 3G#W    #   
  21 3GSG    SLEEP LDCT  0  #103                [J IF ERROR, MOP OFF OR INOP. TERMIN 
   9 3H#6          ANDX  0  EWAIT(3) 
   8 3HRQ          BNZ   0  SLP1 
  19 3J?B          LDX   1  INTLK3(3)           [POINTER TO CURRENT BUFFER   
   8 3JR2          LDX   2  FX2  
   8 3K=L          CALL  6  SPL19
  16 3KQ=          BRN      U                   [J IF NOT 1901A  
   8 3L9W          BRN      XCLN 
  21 3LPG    U     LDXC  6  5                   [J UNLESS END OF TRANSFER COINCIDES  
  19 3M96          BNZ   6  XCLN                [WITH END OF RECORD (OR LINE)
  20 3MNQ          STO   4  GEN4                [SAVE LINK, THEN RESET THE COUNT 
  21 3N8B          LDX   6  GSIGN               [OF LINES TO THE NEXT FORCED CLEAN   
  20 3NN2          CALL  4  SETCL               [FINISH, AND MARK THIS BUFFER AS 
  21 3P7L          SMO      1                   [MESSAGE-CAN-END-WITH-THIS-TRANSFER  
   9 3PM=          ORS   6  INTLK2(3)
   8 3Q6W          LDX   4  GEN4 
  19 3QLG    XCLN  SBN   1  ALFBUFFNUM-1        [STEP ON TO THE NEXT BUFFER  
   8 3R66          BZE   1  NEXZO
  10 3RKQ          ADN   1  ALFBUFFNUM   
   7 3S5B          SMO      1
  18 3SK2    NEXZO LDXC  6  INTLK(3)            [HAS IT BEEN EMPTIED YET?
  14 3T4L          BCS      SLP1                [J IF NOT
  20 3TJ=          ANDN  6  -1                  [GET POINTER TO START OF BUFFER  
  18 3W3W          ORX   6  GSIGN               [ALLOW 2 CHARS. FOR STX  
   9 3WHG          ADN   6  ACTBUFF  
  18 3X36          STO   6  AWORK3(2)           [REMEMBER CURRENT POINTER
  15 3XGQ          SMO      FX1                 [PUT STX INTO
   8 3Y2B          LDX   0  STX  
   7 3YG2          SMO      6
   8 3Y_L          STO   0  0(3) 
  18 3_F=          LDX   0  IWORK14(2)          [SET THE RESTART LINE NO.
  17 3_YW          SMO      1                   [FOR THE LAST BUFFER 
   9 42DG          STO   0  INTLK2(3)
  19 42Y6          STO   1  INTLK3(3)           [SET POINTER TO NEW BUFFER   
   8 43CQ    #SKI  ALTEBCC<1$1   
   4 43XB    (   
  20 44C2          LDN   7  78                  [RESET 7-BIT BLOCK CHAR. COUNT   
  20 44WL          LDN   6  118                 [AND 6-BIT TRANSFER CHAR. COUNT  
   4 45B=    )   
   7 45TW    #SKI  ALTEBCC   
   4 46*G    (   
  21 46T6          STO   6  IWORK10(2)          [REMEMBER POINTER FOR START OF OUTPUT
  20 47#Q          LDN   7  72                  [RESET CHAR COUNTS ALLOWING FOR  
  17 47SB          LDN   6  112                 [SIX EXTRA BCC CHARS.
   4 48#2    )   
  21 48RL          EXIT  4  0                   [WILL SET V IF COORDINATED, AS B0 SET
   4 49?=    #   
  21 49QW    #     SUBROUTINE TO DO SLEEP MACRO, EXITING ONLY IF NO ERROR CONDITION  
   6 4==G    #     OCCUR 
   4 4=Q6    #   
  16 4?9Q    WAIT  SBX   7  FX1                 [RELATIVISE LINK 
  20 4?PB          SLEEP2   RLINE,REST1         [WAIT FOR BUFFER TO BECOME FREE  
  19 4#92          MHUNTW   3,A7020,ALIST       [RELOCATE THE BUFFER BLOCK   
  15 4#NL          ADX   7  1                   [RESET LINK  
  18 4*8=          STO   3  AWORK2(2)           [REMEMBER BLOCK ADDRESS  
   7 4*MW          EXIT  7  0
   9 4B7G    REST1 ACROSS   RMTLF,3  
   4 4BM6    #   
  21 4C6Q    #     SUBROUTINE TO CLEAR UP LAST BUFFER, STEP ONTO THE NEXT, & RESET PT
   4 4CLB    #   
  21 4D62    TREND SBX   3  ACOMMUNE3(1)        [RELATIVISE POINTER DOWN INPUT RECORD
  19 4DKL          SBX   4  FX1                 [RELATIVISE AND PRESERVE LINK
  10 4F5=          STO   3  IWORK11(1)   
  10 4FJW          STO   4  IWORK13(1)   
  18 4G4G          LDX   3  AWORK2(1)           [POINTER TO ALIST BLOCK  
   9 4GJ6          LDX   6  INTLK3(3)
   7 4H3Q          LDN   0  1
  18 4HHB          ADS   0  ALFTRANS(1)         [UPDATE TRANSFER COUNT   
   8 4J32          CALL  0  SBACK
  20 4JGL          SBN   0  #74                 [(OVERWRITE LAST CHAR. DEPOSITED 
  17 4K2=          BPZ   0  TRED1               [IF IT WAS A SHIFT). 
   7 4KFW          BCHX  2  /
   5 4K_G    TRED1   
   7 4LF6    #SKI  ALTEBCC   
  21 4LYQ          CALL  4  SBC                 [CALC. BCC FOR THIS BUFFER & STORE   
   8 4MDB          LDX   1  GSIGN
   8 4MY2          LDN   0  #7607
   7 4NCL          SMO      6
   9 4NX=          ORS   1  INTLK(3) 
   7 4PBW          MVCH  1  2
  21 4PWG    XRJE  CALL  6  WAKUP               [KICK THE OUTPUT ACTIVITY IF ASLEEP  
  18 4QB6          CALL  4  SLEEP               [STEP ON TO NEXT BUFFER  
  21 4QTQ          BVCR     NCOOR               [J UNLESS WE NEED TO RESET POINTERS  
  21 4R*B          LDX   3  ACOMMUNE3(2)        [J IF END OF FILE HAS BEEN REACHED   
   8 4RT2          BZE   3  NCOOR
  18 4S#L          CALL  4  RECLOC              [FIND INPUT RECORD AGAIN 
  18 4SS=          STO   3  ACOMMUNE3(2)        [SAVE POINTER TO RECORD  
  21 4T?W    NOREC MHUNTW   3,A7020,ALIST       [AND THE BUFFER BLOCK IF STEP COOR-ED
   9 4TRG          STO   3  AWORK2(2)
   8 4W?6    NCOOR LDX   1  FX2  
  18 4WQQ          LDX   2  AWORK2(1)           [SET NEW OUTPUT POINTER  
   9 4X=B          LDX   4  REST(1)  
   9 4XQ2          ADX   2  AWORK3(1)
  18 4Y9L          BNZ   4  XLINE               [IF LINE RESTARTS GET THE
  21 4YP=          LDX   0  IWORK19(1)          [RECORD NO. AT START OF LAST BUFFER  
  21 4_8W          STO   0  IWORK14(1)          [REMEMBER FOR INSERTION IN NEXT INTLK
  21 4_NG          LDX   0  NUM(1)              [RECORD NUMBER AT START OF THIS BUFF.
  21 5286          STO   0  IWORK19(1)          [SAVE FOR INSERTION IN NEXT IWORK14  
  10 52MQ    XLINE LDX   3  ACOMMUNE3(1) 
  10 537B          LDXC  4  IWORK13(1)   
   8 53M2          BCC      NSPLD
   9 546L          CALL  4  SPLNUPAGE
  10 54L=          LDXC  4  IWORK13(1)   
   8 555W          BRN      SPLE 
   5 55KG    NSPLD   
  18 5656          ADX   3  IWORK11(1)          [RESET THE INPUT POINTER 
  18 56JQ    SPLE  ADX   4  FX1                 [RECONSTITUTE THE LINK   
  15 574B          LDXC  0  AWORK1(1)           [EXIT IF LP  
   8 57J2          BCC      (4)  
  19 583L          LDN   0  #75                 [OR IF CURRENT SHIFT IS ALPHA
  11 58H=          BXU   0  IWORK16(1),(4)   
  20 592W          SBN   6  1                   [INSERT BETA AT START OF BUFFER  
  18 59GG          DCH   0  0(2)                [STEP DOWN TRANSFER COUNT
   8 5=26          BCHX  2  (4)  
   4 5=FQ    #   
   9 5=_B    SPL19 LDX   0  EWAIT(3) 
   7 5?F2          SRC   0  3
  16 5?YL          BNG   0  SPLYY               [J IF SPOOLING   
   7 5#D=          SLC   0  2
  17 5#XW          BPZ   0  (6)                 [J IF NOT 1901A RJE  
   8 5*CG    YSSTP SMO      FX2  
   9 5*X6          LDXC  0  AWORK1   
  14 5BBQ          BCS      (6)                 [J IF TP 
   7 5BWB    SPLYY EXIT  6  1
   4 5CB2    #   
   7 5CTL    #SKI  ALTEBCC   
   4 5D*=    (   
  20 5DSW    #     SUBROUTINE TO CALCULATE BCC FOR LAST BUFFER & PUT IN BUFFER AS
  12 5F#G    #     SIX CHARACTERS BEFORE ETX/ETB 
   4 5FS6    #   
   8 5G?Q    SBC   SMO      FX2  
   9 5GRB          LDXC  0  AWORK1   
  14 5H?2          BCS      (4)                 [J IF TP 
  17 5HQL          LDX   0  2                   [SAVE OUTPUT POINTER 
   8 5J==          LDX   2  FX2  
  15 5JPW          STO   4  ACOMMUNE4(2)        [SAVE LINK   
  10 5K9G          STO   0  ACOMMUNE2(2) 
  20 5KP6          LDX   4  IWORK10(2)          [GET RELATIVE POINTER FOR START  
  18 5L8Q          LDX   1  0                   [POINTER TO END OF DATA  
  18 5LNB          SBX   1  AWORK2(2)           [RELATIVISE END POINTER  
  17 5M82          SLC   1  2                   [CONVERT TO CHARS.   
  17 5MML          SLC   4  2                   [CONVERT TO CHARS.   
  20 5N7=          SBX   1  4                   [NO. OF CHARS. IN OUTPUT BUFFER  
   7 5NLW          LDX   0  1
   9 5P6G          LDX   1  AWORK2(2)
  18 5PL6          ADX   1  IWORK10(2)          [START ADDRESS OF DATA   
   7 5Q5Q          LDX   2  0
  16 5QKB          LDN   0  0                   [INITIALISE SBC  
  16 5R52    SBC1  LDCH  4  0(1)                [GET NEXT CHAR   
   7 5RJL          ERX   0  4
  15 5S4=          SLC   0  1                   [ADD TO SBC  
  17 5SHW          BCHX  1  /                   [J IF COUNT NOT ZERO 
   8 5T3G          BCT   2  SBC1 
   8 5TH6          SMO      FX2  
   9 5W2Q          LDX   2  ACOMMUNE2
   7 5WGB          LDN   4  6
  15 5X22          LDX   1  0                   [SBC IN X1   
   7 5XFL    SBC2  LDN   0  0
  18 5X_=          SLL   01 4                   [EXPAND 4 BITS TO A CHAR 
  16 5YDW          DCH   0  0(2)                [PUT IN BUFFER   
   7 5YYG          BCHX  2  /
  15 5_D6          BCT   4  SBC2                [TO 6 CHARS. 
   8 5_XQ          SMO      FX2  
  15 62CB          LDX   4  ACOMMUNE4           [RESTORE LINK
   7 62X2          EXIT  4  0
   4 63BL    )   
   4 63W=    #   
  13 64*W    #     SECTION TO DEAL WITH LINE RESTARTS
  20 64TG    #     IN THE CASE OF RESTARTS RENGAGE RESETS THE ACTLF BEFORE COMING
   7 65*6    #     BACK UP   
   4 65SQ    #   
  20 66#B    RLINE CALL  4  RECLOC              [GET POINTER TO RESTART RECORD   
  18 66S2          LDN   6  1                   [SET MARKER TO FORCE FLIP
   9 67?L          ACROSS   RMTLF,4  
   4 67R=    #   
   4 68=W    #   
   4 68QG    #   
   4 69=6    #   
   6 69PQ    Z1RMTLFEND  
  19 6=9B          BZE   6  RLOC                [J OUT IF NOT JUST ENTERED   
  19 6=P2          CALL  4  SFLIP               [FLIP, AND GET NEXT BUFFER   
  21 6?8L          CALL  3  OUTPT               [1ST. REC: SET UP ^FILE EMPTY^ MESS. 
  12 6?N=                   14,12,10HFILE EMPTY  
   4 6#7W    #   
   4 6#MG    #   
   4 6*76    #   
  19 6*LQ    RLOC  MHUNTW   3,A7020,ALIST       [RELOCATE THE BUFFER BLOCK   
  16 6B6B          LDN   4  0                   [SET ^END^ MARKER
   8 6BL2          BRN      NOWT 
   4 6C5L    #   
   4 6CK=    #   
  20 6D4W    OUTPT LDX   1  FX2                 [RESET X1 TO CURRENT ACTIVITY AND
  20 6DJG          LDX   2  AWORK2(1)           [X2 TO CURRENT POSITION IN OUTPUT
  21 6F46          LDX   5  AWORK1(1)           [LP/TP SWITCH WORD         [BUFFER   
   9 6FHQ          ADX   2  AWORK3(1)
  17 6G3B          BZE   5  XTP                 [J IF LINE PRINTER   
   8 6GH2          LDN   0  #74  
  20 6H2L          SBN   6  4                   [ALLOW FOR EXTRA RUNOUT CHARS.   
   7 6HG=          SBN   7  2
  19 6H_W          BXE   0  IWORK16(1),XTP      [J IF ALREADY IN ALPHA SHIFT 
  17 6JFG          DCH   0  0(2)                [OTHERWISE SET ALPHA 
  19 6J_6          SBN   6  1                   [ALLOW FOR EXTRA SHIFT CHAR. 
  10 6KDQ          STO   0  IWORK16(1)   
  17 6KYB          BCHX  2  /                   [STEP OUTPUT POINTER 
   4 6LD2    #   
  21 6LXL    XTP   SBX   6  0(3)                [NO OF TRANSMISSION CHARS IN MESSAGE 
  20 6MC=          BNG   6  TR6                 [J IF TOO LONG FOR THIS BUFFER   
  18 6MWW          SBXC  7  1(3)                [DITTO 7-BIT DATA CHARS. 
   8 6NBG          BCC      XTR6 
  18 6NW6    TR6   SBX   3  FX1                 [RELATIVISE CALL ADDRESS 
  18 6P*Q          CALL  4  TREND               [STEP ON TO NEXT BUFFER  
  21 6PTB          LDN   7  0                   [SET X7 SO WE DONT STEP BACK AT YCF4 
  20 6Q*2          ADX   3  FX1                 [NB X3 IS PRESERVED IF ACOMM3 = 0
   4 6QSL    #   
  18 6R#=    XTR6  BNG   5  TP7                 [J IF NOT LINE PRINTER   
  17 6RRW          SMO      FX1                 [INSERT 2 NEW LINES  
   8 6S?G          LDN   1  PF2+1
   7 6SR6          MVCH  1  4
  17 6T=Q    TP7   LDN   1  2(3)                [MOVE IN THE MESSAGE 
   8 6TQB          SMO      1(3) 
   8 6W=2          MVCH  1  -2   
  17 6WPL          BZE   5  YCF2                [J IF NOT TAPE PUNCH 
  19 6X9=          LDX   3  FX1                 [INSERT NEWLINE + 3 RUNOUTS  
   9 6XNW          LDN   1  PF1+1(3) 
   7 6Y8G          MVCH  1  4
   9 6YN6          LDN   1  SNULL(3) 
   7 6_7Q          MVCH  1  4
   4 6_MB    #   
  18 7272    YCF2  SMO      FX2                 [POINTER TO ALIST BLOCK  
   9 72LL          LDX   3  AWORK2   
   8 736=          BRN      YCF4A
   6 73KW    Z2RMTLFEND  
   4 745G    YCF4
  10 74K6          MHUNTW   3,A7020,ALIST
   8 754Q          SMO      FX2  
   9 75JB          STO   3  AWORK2   
   5 7642    YCF4A   
   7 76HL          LDX   2  3
   8 773=          SMO      FX2  
   9 77GW          ADX   2  AWORK3   
  21 782G          LDX   6  INTLK3(3)           [GET POINTER TO BUFFER'S RED TAPE WD 
   8 78G6          LDX   1  GSIGN
  18 78_Q          SMO      6                   [MARK BUFFER AS FILLED   
   9 79FB          ORS   1  INTLK(3) 
  15 79_2          SMO      6                   [AND ^CLEAN^ 
   9 7=DL          ORS   1  INTLK2(3)
  19 7=Y=          CALL  0  SBACK               [MOVE IN ETB TO END BLOCK.   
  20 7?CW          SBN   0  #74                 [(OVERWRITE LAST CHAR DEPOSITED  
  17 7?XG          BPZ   0  TRED2               [IF IT WAS A SHIFT)  
   7 7#C6          BCHX  2  /
  19 7#WQ    TRED2 ADN   1  ETXBUFF(3)          [MOVE IN #7635(#7620 IF TP)  
   8 7*BB          CALL  6  SPL19
  18 7*W2          BRN      XCTP                [J IF NOT 1901A RJE LP   
  16 7B*L          LDN   0  3                   [WILL BECOME DC3 
   8 7BT=    #SKI  ALTEBCC<1$1   
   4 7C#W    (   
   7 7CSG          MVCH  1  6
   7 7D#6          SLC   2  2
   7 7DRQ          SBN   2  3
   7 7F?B          SRC   2  2
   8 7FR2          DCH   0  0(2) 
   4 7G=L    )   
   7 7GQ=    #SKI  ALTEBCC   
   4 7H9W    (   
  17 7HPG          MVCH  1  4                   [CR DC3 INTO BUFFER  
   7 7J96          SLC   2  2
   7 7JNQ          SBN   2  1
   7 7K8B          SRC   2  2
   8 7KN2          DCH   0  0(2) 
   8 7L7L          SMO      FX2  
  17 7LM=          STO   1  ACOMMUNE1           [SAVE ETXBUFF POINTER
  19 7M6W          CALL  4  SBC                 [WORK OUT BCC & PUT IN BUFFER
   8 7MLG          SMO      FX2  
  16 7N66          LDX   1  ACOMMUNE1           [RESTORE POINTER 
  16 7NKQ          MVCH  1  2                   [ETX IN BUFFER   
   4 7P5B    )   
   8 7PK2          BRN      NOWT 
   7 7Q4L    XCTP  MVCH  1  2
  21 7QJ=          ORX   1  GSIGN               [MOVE IN #7623 ETX (PLAIN ETB SEEMS  
   7 7R3W    #SKI  ALTEBCC   
   4 7RHG    (   
   8 7S36          SMO      FX2  
   9 7SGQ          STO   1  ACOMMUNE1
   8 7T2B          CALL  4  SBC  
   8 7TG2          SMO      FX2  
   9 7T_L          LDX   1  ACOMMUNE1
   4 7WF=    )   
  21 7WYW          MVCH  1  2                   [TO UPSET THE LINE PRINTER SOMETIMES 
  20 7XDG    NOWT  CALL  6  WAKUP               [KICK THE PCT ACTIVITY IF DORMANT
  19 7XY6          LDN   6  0                   [SET MARKER SO WE DONT REFLIP
   8 7YCQ          SMO      FX2  
   9 7YXB          LDXC  0  IWORK20  
  16 7_C2          BCC      MAINFILE            [J IF MAINFILE   
  20 7_WL          LDCT  5  #200                [SET ^DC4^ MARKER IN LAST BUFFER 
  21 82B=          SMO      INTLK3(3)           [OF SETUP LISTING TO DISENGAGE *LP   
   9 82TW          ORS   5  INTLK2(3)
   4 83*G    #   
   5 83T6    MAINFILE
   9 84#Q          SMO      INTLK3(3)
   9 84SB          LDXC  0  INTLK2(3)
   8 85#2          BCS      ZCLOK
   8 85RL          CALL  4  SFLIP
  18 86?=          LDX   2  AWORK3(2)           [RESET THE BUFFER WORD   
   7 86QW          ADX   2  3
   8 87=G          BRN      YCF4 
   5 87Q6    ZCLOK   
  21 889Q          LDCT  6  #20                 [SET THE ^LISTING FINISHED^ MARKER   
  21 88PB    XWAKE LDCT  4  #100                [UNLESS WE'VE HAD A DEVICE ERROR, IN 
  21 8992          ORS   6  EWAIT(3)            [WHICH CASE ENSURE THAT IT IS NOT SET
   9 89NL          ANDX  4  EWAIT(3) 
   8 8=8=          BZE   4  YWAIT
   9 8=MW          ERS   6  EWAIT(3) 
  20 8?7G    YWAIT CALL  7  WAIT                [WAIT FOR THE OUTPUT TO FINISH   
  21 8?M6          LDCT  0  #10                 [WAIT AGAIN UNLESS THE ^OUTPUT FINISH
  18 8#6Q          ANDX  0  EWAIT(3)            [MARKER^ HAS NOW BEEN SET
   8 8#LB          BZE   0  XWAKE
  16 8*62          ERS   0  EWAIT(3)            [CLEAR IT IF SO  
   4 8*KL    #   
  10 8B5=          LDX   4  IWORK20(2)   
  21 8BJW          LDCT  0  #100                [RESET THE AWORK WORDS FOR LISTEND   
  18 8C4G          ANDX  0  4                   [(OR LISTING OF MAINFILE)
   9 8CJ6          STO   4  AWORK4(2)
   9 8D3Q          STO   0  AWORK2(2)
  16 8DHB          BZE   0  NOTSETUP            [J NOT SETUP FILE
  19 8F32          BPZ   4  SENOTLISTED         [J IF JUST FINISHED MAINFILE 
  19 8FGL          DOWN     RENGAGE,3           [OUTPUT PLEASE LINE UP MSG   
  18 8G2=          ACROSS   RMTLF,1             [TO LIST SETUP FILE AGAIN
   6 8GFW    SENOTLISTED 
   5 8G_G    NOTSETUP
  18 8HF6          LDXC  0  REST(2)             [J UNLESS PAGE RESTARTS, 
   8 8HYQ          BCC      YCLOS
  20 8JDB          MFREEW   ADATA,IPTEMP        [IN WHICH CASE THE RESTART BLOCK 
   5 8JY2    YCLOS   
  16 8KCL          ACROSS   PCTLISTB,2          [FOR LINE OF *'S 
   4 8KX=    #   
   4 8LBW    #   
   4 8LWG    #END
   6 ____ ...542340740002
