  13 22#C ...  SEG DTSCAN,860,C.R.JOHNSON,SCHEDULER  
   4 22LS ...[   
  11 22_=          SEGENTRY K1DTSCAN,X1DTSCAN
   4 23DW ...[   
   9 24D6    SFEND          4H@000   
   9 25X2    SENTEND        4H0007   
   9 26BL    SENTST         4H0006   
  11 26W=    NAMEINC        12HGEORGE3DUMP   
   4 27*W ...[   
  21 27TG    [     S.R.TO SEARCH OFLC FOR FREQ WHOSE INC.NO IS IN X4,FILE NO.IN X5   
  20 28*6 ...[     AND FILESTORE NUMBER IN X6 - IF FOUND,X2 POINTS TO IT,EXIT+0  
  10 28SQ ...[     IF NOT FOUND,EXIT+1   
   4 29#B ...[   
   8 29S2    SOFLC LDX   2  BOLC 
   8 2=?L          LDN   0  BOLC 
   5 2=R=    M1SFOLC 
   8 2?=W          LDX   2  0(2) 
  17 2?QG ...      BXE   2  0,XIT1               [J IF END OF CHAIN  
  17 2#=6 ...      BXL   4  FDTINCNUM(2),XIT1    [J IF INC NOT THERE 
  19 2#PQ ...      BXU   4  FDTINCNUM(2),M1SFOLC [J IF INC NOT YET REACHED   
  19 2*9B ...      BXL   6  FDTFILSTR(2),XIT1    [J IF FILESTORE NOT THERE   
  20 2*P2 ...      BXU   6  FDTFILSTR(2),M1SFOLC [J IF FILESTORE NOT YET REACHED 
  17 2B8L ...      BXL   5  FDTFILNUM(2),XIT1    [J IF FILE NOT THERE
  19 2BN= ...      BXU   5  FDTFILNUM(2),M1SFOLC [J IF FILE NOT YET REACHED  
  17 2C7W ...      EXIT  7  0                    [FILE REQUEST FOUND 
  17 2CMG ...XIT1  EXIT  7  1                    [REQUEST NOT FOUND  
   4 2D76 ...[   
   9 2DLQ ...[     SR TO FREEBLOCKS  
   4 2F6B ...[   
   5 2FL2    SRFREE  
   9 2G5L          MFREEW   FILE,FRB 
  10 2GK=          MFREEW   FILE,FABSNB  
   7 2H4W          EXIT  7  0
   4 2HJG    [   
  12 2J46    [     SR TO POINT TO FDT ENTRY IN X2
   4 2JHQ    [   
   5 2K3B    POINTFDT
   8 2KH2          SMO      FX2  
   8 2L2L ...      LDX   0  ADTSN
   8 2LG= ...      FDTENT   2,0  
   7 2L_W ...      EXIT  7  0
   4 2MFG    [   
   4 2M_6    [   
   4 2NDQ ...[   
  21 2NYB ...[     THIS SEGMENT POSITIONS ITS TAPE READY TO RESTORE A FILE,AND THEN  
  21 2PD2 ...[     GOES'DOWN'TO DTREST FOR RESTORING.THE FILE TO BE RESTORED IS THE  
  21 2PXL ...[     NEAREST UNRESTORED FILE ON THE TAPE WHICH WILL BE FOUND DURING A  
  19 2QC= ...[     FORWARD READ AND FOR WHICH THERE IS A REQUEST IN THE OFLC 
  21 2QWW ...[     IF THE DUMPER WANTS THE TAPE,IT IS LEFT POSITIONED AT END OF TAPE 
  21 2RBG ...[     IF THERE ARE NO REQUESTS,AND THE DUMPER DOES NOT REQUIRE THE TAPE,
  14 2RW6 ...[     CONTROL PASSES TO DTSTART TO UNLOAD IT
   4 2S*Q ...[   
   8 2STB ...[     ENTRY POINT 1 
   4 2T*2 ...[   
   5 2TSL    X1DTSCAN
  16 2W#=          SETNCORE 10,3,FILE,FABSNB    [SET UP FABSNB   
  11 2WRW          SETNCORE 20,3,FILE,FRB,1  
   5 2X?G    SEARCH  
  14 2XR6          LDX   1  FX1                 [RESET X1
  18 2Y=Q ...      CALL  7  POINTFDT            [X2 POINTS TO FDT ENTRY  
  21 2YQB ...      MBC      2,BIFDTCA,BIFDTPF   [CLEAR 'CHAIN ALTERED' AND 'PASSED FI
  15 2_=2 ...      SMO      FX2                 [JUST BECAUSE
  20 2_PL ...      STO   2  AWORK2              [PRESERVE POINTER TO FDT ENTRY   
  10 32NW          MHUNTW   3,FILE,FDINCS
  21 338G ...      LDX   0  FDINCNO(3)          [X0 => NUMBER OF INCREMENTS ON TAPE  
  16 33N6          BZE   0  NEWTAPE             [J IF NEW TAPE   
  18 33XY ...      LF       3,FDINCFSNO,6       [X6 => FILESTORE NUMBER  
   8 347Q          LDX   2  BOLC 
   5 34MB    SCHAIN  
   8 3572          LDN   0  BOLC 
  17 35LL          LDX   2  0(2)                [NEXT BLOCK IN OFLC  
  17 366=          BXE   2  0,MTFIN             [J IF END OF CHAIN   
  21 36F4 ...      JMBS     SCHAIN,2,BFILBRES,BTERMST [J IF FILE 'BEING' OR 'HAS BEEN
   4 36RW ...[   
  21 376N ...[     SEARCH FDINCS TO SEE IF THIS FILSTORE AND INCREMENT ARE ON THIS TA
  19 37FG ...[     SEARCH ASSUMES INCREMENTS IN FDINCS ARE IN ASCENDING ORDER
  11 37S# ...[               X2 -> FREQ BLOCK
  12 3876 ...[               X3 -> FDINCS BLOCK  
   4 38FY ...[   
  21 38SQ ...      LDX   0  FDTFILSTR(2)        [X0 => FILESTORE NUMBER OF THIS FILE 
  19 397J ...      BXU   0  6,SCHAIN            [J IF WRONG FILESTORE NUMBER 
  21 39GB ...      LDX   0  FDTINCNUM(2)        [X0 => INCREMENT NUMBER OF THIS FILE 
  17 39T8 ...      LDX   1  3                   [X1 -> FDINCS BLOCK  
  17 3=82 ...      LDX   5  FDINCNO(3)          [X5 => NO OF ENTRIES 
   5 3=GW    NXTFD   
  18 3?2G ...      BXE   0  FDINCINC(1),THISINC [J IF THE INC IS ON TAPE 
  20 3?G6 ...      BXL   0  FDINCINC(1),SCHAIN  [J IF THE INC IS NOT ON THIS TAPE
  17 3?_Q ...      ADN   1  FDINCLEN            [INCREMENT MODIFIER  
  18 3#FB          BCT   5  NXTFD               [LOOK AT NEXT INC ON TAPE
   9 3#_2          BRN      SCHAIN   
   5 3*DL    THISINC 
   4 3*ND ...[   
  21 3*Y= ...[     FILESTORE AND INCREMENT LOOK OK,NOW CHECK WHETHER TAPE BEFORE OR A
   4 3B84 ...[   
  12 3BCW ...      SMO      FX2                 [
  19 3BMN ...      LDX   1  AWORK2              [RESET POINTER TO FDT ENTRY  
  17 3BXG          LDX   4  FDTINCNUM(2)        [INC.NO.OF THIS FILE 
  18 3CC6 ...      LDX   5  FDTFILNUM(2)        [FILE NUMBER OF THIS FILE
  21 3CWQ          BXL   4  FDINC(1),PASSED     [J IF TAPE HAS PASSED THIS INCREMENT 
  18 3DBB          BXU   4  FDINC(1),MTBEFORE   [J IF NOT YET AT THIS INC
  20 3DW2          BXL   5  FDFILE(1),PASSED    [J IF TAPE HAS PASSED THIS FILE  
  19 3F*L          BXU   5  FDFILE(1),MTBEFORE  [J IF TAPE NOT YET AT FILE   
   4 3FM2 ...[   
  21 3FYB ...[     REQUEST  IS  TOO LATE TO PICK UP THE FILE IF THE TAPE IS ALREADY  
   9 3G9Q ...[     POSITONED AT IT   
   4 3GH6 ...[   
  10 3GSG          TRACE    5,TOO LATE   
   4 3H4# ...[   
  20 3H#6 ...[     TAPE HAS PASSED THE FILE IN THIS REQUEST-FIND ANOTHER REQUEST 
   4 3HHY ...[   
   5 3HRQ    PASSED  
  18 3J?B ...      BS       1,BIFDTPF           [SET 'PASSED FILE' BIT   
   9 3JR2          BRN      SCHAIN   
   4 3K4B ...[   
  20 3K*Q ...[     TAPE CANNOT SATISFY ANY MORE REQUESTS ON THIS PASS,SO CHECK   
  21 3KM6 ...[     WHETHER DUMPER WANTS IT OR NOT (AWORK2 CONTAINS POINTER TO FDT ENT
   4 3KYG ...[   
   5 3L9W    MTFIN   
  12 3LFN ...      SMO      FX2                 [
  19 3LPG ...      LDX   1  AWORK2              [RESET POINTER TO FDT ENTRY  
  21 3L_# ...      JBSC     MTFORMAT,1,BIFDTFE  [J IF FORMAT ERROR WHILE RV 'FILEHAND
  18 3M96 ...      JBS      MTDUMP,1,BIFDTD     [J IF DUMPER NEEDS TAPE  
   4 3MDY ...[   
  18 3MNQ ...[     TAPE IS ASSUMED TO BE REWOUND IF INC.NO IN FDT IS ZERO
   4 3MYJ ...[   
   9 3N8B          LDX   0  FDINC(1) 
  21 3NN2          BNZ   0  REWIND              [J TO REWIND TAPE,SINCE NOT DUMPER'S 
   4 3NXS ...[   
  11 3P7L    [     TAPE IS REWOUND,SO UNLOAD 
   4 3PCD ...[   
   5 3PM=    UNLOAD  
   9 3Q6W          CALL  7  SRFREE   
   9 3QLG          ACROSS   DTSTART,2
   4 3QXW ...[   
  21 3R9= ...[     DUMPER DOES NOT NEED TAPE,AND IT CAN SATISFY NO MORE REQUESTS ON  
  13 3RGL ...[     THIS PASS,SO REWIND AND TRY AGAIN 
   4 3RS2 ...[   
   5 3S5B    REWIND  
  16 3SK2          LDX   3  FDSN(1)             [GET TSN AGAIN   
  16 3T4L          STOZ     FDINC(1)            [RESET INC.NO.   
  16 3TJ=          STOZ     FDFILE(1)           [ AND FILE NO.   
  16 3W3W          MTREW                        [REWIND THE TAPE 
  10 3WHG          TESTRPN2  OK,REWFAIL  
  16 3X36 ...      CALL  7  POINTFDT            [X2 -> FDT ENTRY 
  21 3XGQ ...      JMBS     SEARCH,2,BIFDTCA,BIFDTPF [J IF 'CHAIN ALTERED' OR 'PASSED
   4 3Y2B ...[   
  19 3YG2 ...[     THE TAPE IS NOW REWOUND,AND THERE IS NO RESTORING TO DO   
  19 3Y_L ...[     TEST AGAIN TO SEE WHETHER DUMPER WANTS IT BEFORE UNLOADING
   4 3_F= ...[   
  12 3_YW ...MTJAM                              [
  19 42DG ...      JBC      UNLOAD,2,BIFDTD     [J IF DUMPER DOESNT NEED TAPE
   5 44C2    MTDUMP  
   7 45TW          LDN   4  0
   5 46*G    MTBEFORE
  16 46LW ...      CALL  7  POINTFDT            [X2 ->  FDT ENTRY
   4 46Y= ...[   
  17 479L ...[     IF TAPE AT BEGINNING,A SPECIAL ENTRY POINT IS USED
   4 47H2 ...[   
   9 47SB          LDX   7  FDINC(2) 
  19 48#2          BZE   7  STARTPOS            [IF INC.NO.ZERO,TAPE AT START
   4 48M7 ...[   
  21 492# ...[     THERE IS SOMETHING FOR THE TAPE TO DO.IF X4=0,IT IS NEEDED BY THE 
  20 49*F ...[     DUMPER,OTHERWISE X4=INC.NO,X5=FILE NO.OF FILE TO BE RESTORED. 
  21 49NL ...[     POSITION THE TAPE,WHILE TAKING NOTICE OF THE CHAIN ALTERED BIT,AND
  15 4=3R ...[     REASSESSING THE SITUATION IF IT GETS SET  
   4 4=BY ...[   
   5 4=Q6    POSITION
   5 4?9Q    NOWAIT  
  17 4?PB          CALL  6  SKIPSR              [READ NEXT SENTINEL  
  18 4#92          LDX   6  ESENTYP(3)          [PICK UP SENTINEL TYPE   
  17 4#NL          BXE   6  SFEND(1),TESTINC    [J IF END OF SUBFILE 
  16 4*8=          BXE   6  SENTEND(1),MTFORMAT [J IF END OF TAPE
  18 4*MW          BXU   6  SENTST(1),MTFORMAT  [J IF STARTDATASENTINEL  
   4 4*XN ...[   
  13 4B7G    [     IS IT START OF COMPOSITE SUBFILE? 
  10 4BM6    [     OR START OF DIRECTORY?
   4 4BWY ...[   
  18 4C6Q          BBS      0,ESENB(3),POSITION [J IF CSF OR DIRECTORY   
   4 4CBJ ...[   
   8 4CLB    [     START OF FILE 
  10 4D2W ...[     FIND FDTENTRY AGAIN   
   4 4DCB ...[   
  16 4DRW ...      CALL  7  POINTFDT            [X2 ->  FDT ENTRY
  16 4F8B ...      JBS      POSITION,2,BIFDTJ   [J IF IN A BSJAM 
  18 4FJW          LDX   6  ESENFN(3)           [FILE NUMBER FROM TAPE   
  17 4G4G          STO   6  FDFILE(2)           [ TO UPDATE FDT ENTRY
  17 4G*W ...      JMBS     UNLOAD,2,BIFDTOW,BIFDTUW [J IF WRONGED   
   4 4GM= ...[   
  20 4GYL ...[     IF X4 IS ZERO,THEN THE TAPE IS BEING POSITIONED FOR DUMPING   
   4 4H=2 ...[   
   9 4HHB          BZE   4  SKIPAGAIN
   4 4HR8 ...[   
  21 4J32 ...[     X4 NON-ZERO,SO A FILE IS BEING RESTORED.IS TAPE POSITIONED YET?   
   4 4J=S ...[   
  21 4JGL          BXL   4  FDINC(2),MTFORMAT   [J IF INC NO IN X4 LESS THAN IN FDT  
  17 4K2=          BXU   4  FDINC(2),SKIPAGAIN  [NOT YET POSITIONED  
  18 4KFW ...      BXL   5  FDFILE(2),TESTFH    [J IF HAVE GONE TOO FAR  
  20 4K_G          BXE   5  FDFILE(2),MTPOS     [J IF TAPE POSITIONED FOR RESTORE
   4 4L9# ...[   
  17 4LF6 ...[     CHECK WHETHER CHAIN OF REQUESTS HAS BEEN ALTERED  
   4 4LNY ...[   
   6 4LYQ    SKIPAGAIN   
  17 4MDB ...      JBS      SEARCH,2,BIFDTCA    [J IF CHAIN ALTERED  
   9 4MY2          BRN      POSITION 
  19 4M_L ...TESTFH                             [TEST FOR 'FILEHANDLERED' BIT
  18 4N3= ...      LF       2,FDTFSNO,6         [X6 => FILESTORE NUMBER  
  15 4N4W ...      CALL  7  SOFLC               [GO FIND FREQ
  16 4N6G ...      BRN      TESTFH1             [FREQ STILL THERE
  19 4N86 ...      BRN      SEARCH              [FREQ GONE ... START AGAIN   
  12 4N9Q ...TESTFH1                            [
  18 4N?B ...      JBC      MTFORMAT,2,BFHCE    [J IF NOT 'FILEHANDLERED'
  18 4N*2 ...      JBSS     SECONDFE,2,BFORMERR [SET 'FORMAT ERROR' BIT  
  16 4NBL ...      CALL  7  POINTFDT            [X2 -> FDT ENTRY 
  18 4ND= ...      BS       2,BIFDTFE           [SET 'FORMAT ERROR' BIT  
  15 4NFW ...      BRN      SEARCH              [START AGAIN 
  21 4NHG ...SECONDFE                           [SECOND FORMAT ERROR FOR THIS FILE   
  19 4NK6 ...      MBS      2,BTEMLOST,BTERMST  [SET 'TEMPORARILY LOST' BIT  
  14 4NLQ ...      STARTACT RET                 [KICK DTS
  15 4NNB ...      BRN      SEARCH              [START AGAIN 
   4 4NQ2 ...[   
  18 4NRL ...[     END OF SUBFILE SENTINEL-IS IT THE END OF AN INCREMENT 
   4 4NT= ...[   
   5 4NX=    TESTINC 
  20 4PBW          BBUS     1,ESENGR(3),POSITION    [J IF NOT END OF INCREMENT   
   4 4PLN ...[   
  17 4PWG    [     END OF INC,IS IT THE LAST ON TAPE ? SEARCH FDINCS 
   4 4Q6# ...[   
  10 4QB6          MHUNTW   3,FILE,FDINCS
  18 4QTQ ...      LDN   6  FDINCLEN            [X6 => LENGTH OF ENTRY   
  20 4R*B ...      MPY   6  FDINCNO(3)          [X7 => LENGTH*NUMBER OF ENTRIES  
  16 4RT2 ...      SBN   7  FDINCLEN            [ADJUST MODIFIER 
  12 4S#L ...      SMO      7                   [
  21 4SS= ...      LDX   3  FDINCINC(3)         [X3 => LAST INCREMENT IN FDINCS BLOCK
  16 4T?W ...      CALL  7  POINTFDT            [X2 -> FDT ENTRY 
  18 4WQQ          BXE   3  FDINC(2),MTEND      [J IF LAST ENTRY ON TAPE 
   4 4X2J ...[   
  13 4X=B ...[     READ START OF INCREMENT SENTINEL. 
   4 4XG8 ...[   
   5 4XQ2    STARTPOS
  17 4Y9L          CALL  6  SKIPSR              [READ NEXT SENTINEL  
   8 4YP=    #SKI K6DTSCAN>99-99 
   4 4_8W    (   
  10 4_NG          LDX   6  ESENTYP(3)   
  19 5286          BXU   6  SENTST(1),MTFORMAT  [J IF NOT STARTDATA SENTINEL 
  15 52MQ          LDX   6  ESENLOC1(3)         [CHECK NAME  
  12 537B          BXU   6  NAMEINC(1),MTFORMAT  
  10 53M2          LDX   6  ESENLOC2(3)  
  12 546L          BXU   6  NAMEINC+1(1),MTFORMAT
  10 54L=          LDX   6  ESENLOC3(3)  
  12 555W          BXU   6  NAMEINC+2(1),MTFORMAT
   4 55KG    )   
  17 5656 ...      CALL  7  POINTFDT            [X2 -> TO FDT ENTRY  
  20 56JQ          STOZ     FDFILE(2)           [ZEROISE FILE NUMBER IN FDT ENTRY
  18 574B          LDX   6  ESENFG(3)           [PICK UP INCREMENT NUMBER
  16 57J2          STO   6  FDINC(2)            [AND UPDATE FDT  
   8 57MF ...      SMO      FX2  
  20 57QY ...      STOZ     AWORK1              [CLEAR INCREMENT FORMAT MARKER   
  20 57WC ...      LDX   6  ESENB(3)            [PICK UP FORMAT VERSION NUMBER   
  21 57_W ...      SLL   6  15                  [   BITS 15 TO 18 HAVE FOLLOWING MEAN
  18 585* ...      SRL   6  20                  [     0 IS MK 5 FORMAT   
  19 588S ...      SMO      FX2                 [     1 IS MK 6 & 7 FORMAT   
  19 58#? ...      STO   6  AWORK1              [     2 IS MK 8.20+ FORMAT   
  21 58CQ ...      BRN      POSITION            [**** AWORK1 NOW CONTAINS INC FORMAT 
   4 58FG ...[   
  16 58H=    [     NEW TAPE-LEAVE POSITIONED AFTER HEADER LABEL  
   4 58R4 ...[   
   5 592W    NEWTAPE 
   4 59GG ...[   
  14 5=26 ...[     TAPE POSITIONED AFTER LAST INCREMENT  
   4 5=FQ ...[   
   5 5?F2    MTEND   
   5 5?GY ...#UNS IDC
   4 5?JW ...(   
  19 5?LS ...      LDX   1  2                   [X1 -> FDT ENTRY FOR REWIND  
  16 5?NQ ...      JBC      REWIND,2,BIFDTD     [DUMP CANCELLED? 
   4 5?QN ...)   
   5 5?SL ...#UNS IDC
   4 5?WJ ...#STR
  20 5?YL ...      JBC      MTFORMAT,2,BIFDTD   [END OF TAPE BUT NOT DUMPERS TAPE
   4 5#8D ...[   
  10 5#D= ...[     TAPE READY FOR DUMPER 
   4 5#N4 ...[   
   5 5#XW    POSDUMP 
  18 5*CG ...      JMBS     UNLOAD,2,BIFDTOW,BIFDTUW    [J IF  WRONGED   
  17 5*X6 ...      BC       2,BIFDTJ            [CLEAR 'BSJAM' BIT   
   9 5CB2          CALL  7  SRFREE   
  17 5CTL          ACROSS   DTSTART,3           [GO TO WAKE UP DUMPER
   4 5D4P ...[   
  13 5D?S ...[     TAPE IS POSITIONED TO RESTORE FILE
  17 5DGX ...[     CHECK  THAT FREQ FOR THIS FILE IS STILL IN OFLC   
   4 5DQ2 ...[   
  12 5D_5 ...MTPOS                              [
  18 5F88 ...      LF       2,FDTFSNO,6         [X6 => FILESTORE NUMBER  
  15 5FC? ...      CALL  7  SOFLC               [GO FIND FREQ
   4 5FLB ...[   
   7 5FTF ...[     FILE FOUND
   4 5G4J ...[   
   9 5G?Q          BRN      RESTORE  
   4 5GHJ ...[   
   8 5GRB ...[     FILE NOT FOUND
   4 5H38 ...[   
   9 5H?2          BRN      SEARCH   
   4 5HJB ...[   
  15 5HTQ ...[     X2 POINTS TO REQUEST BLOCK FOR THIS FILE  
  12 5J76 ...[     X3 POINTS TO SENTINEL BUFFER. 
   4 5JDG ...[   
   5 5JPW    RESTORE 
   6 5JTR ...#UNS FTS2   
   4 5J_N ...#STR
   4 5K5K ...(   
  10 5K9G          LDX   7  ESENLOC1(3)  
  11 5KP6          BXU   7  FLOC1(2),UNEQUAL 
  10 5L8Q          LDX   7  ESENLOC2(3)  
  11 5LNB          BXU   7  FLOC2(2),UNEQUAL 
  10 5M82          LDX   7  ESENLOC3(3)  
  11 5MML          BXU   7  FLOC3(2),UNEQUAL 
   4 5N7= ...)   
  21 5NLW ...      JMBS     SEARCH,2,BFILBRES,BTERMST [J IF FILE 'BEING' OR 'HAS BEEN
   4 5P6G ...[   
  21 5PL6 ...[     THIS FILE HAS NOT BEEN RESTORED-MARK IT'BEING RESTORED',AND SET   
   9 5Q5Q ...[     CHAIN ALTERED BITS
   4 5QKB ...[   
   5 5T3G    MTREST  
  18 5TH6 ...      BS       2,BFILBRES          [SET 'BEING RESTORED' BIT
  10 5W2Q          MHUNTW   1,FILE,FABSNB
   8 5WGB          LDN   6  10   
   8 5X22          STO   6  A1(1)
  10 5XFL ...      LDN   6  FFUSER1(2)   
   9 5X_=          LDN   7  A1+1(1)  
  19 5YDW ...      MOVE  6  9                   [USER NAME FROM FREQ IN OFLC 
   8 64*W          LDX   2  BOLC 
  17 65*6          LDX   1  A1(2)               [COUNT OF FDT ENTRIES
   8 65SQ          BZE   1  SACT2
   4 6677 ...SET8
  18 66FJ ...      BS       2,BIFDTCA           [SET 'CHAIN ALTERED' BIT 
  15 66S2          ADN   2  FDTLEN              [NEXT ENTRY  
   8 67?L          BCT   1  SET8 
   4 67HD ...[   
  11 67R= ...[     GO DOWN TO RESTORE FILE   
   4 6834 ...[   
   5 68=W    SACT2   
   9 68QG          DOWN     DTREST,1 
  16 69=6          LDX   7  ACOMMUNE1(2)        [AUX REPLY IN X7 
  10 69PQ          HUNTW    3,FILE,FABSNB
  20 6=9B          BNG   3  NOTEX5              [ J IF COPYFILE HAS GNT F/FABSNB 
   8 6=P2          ALTLEND  3,10 
   6 6?8L    XFILEREST   
   4 6?N=    [   
  10 6#7W    [     CHECK AUXILIARY REPLY 
   4 6#MG    [   
   7 6*76          LDX   0  7
  18 6*LQ          BZE   0  XMAINREP            [IF OK,CHECK MAIN REPLY  
  19 6B6B          SBN   0  ANSFAIL             [IF TAPE FAIL,GO TO MTFAIL   
   9 6BL2          BZE   0  MTFAIL   
   9 6C5L          BRN      MTFORMAT 
   5 6CK=    NOTEX5  
  17 6DJG          SETNCORE 10,3,FILE,FABSNB    [SET UP FABSNB AGAIN 
   9 6F46          BRN      XFILEREST
   5 6FHQ    XMAINREP
  19 6G3B          TESTREP2 OK,SEARCH,FAIL,MTFAIL,NOBAC,NOBAC,NOUSER,SEARCH  
  21 6GH2          TESTREP2 NOFILE,SEARCH,NAME,SEARCH,OLDDUMP,SEARCH,ONLINE,SEARCH   
  11 6GQS ...      TESTREP2 VRYWRONG,SEARCH  
   9 6H_W          CALL  0  SKIPERR  
   4 6JFG ...[   
  13 6J_6 ...[     NO BACKING STORE TO RESTORE FILE  
   4 6KDQ ...[   
   9 6KYB ...NOBAC CALL  7  POINTFDT 
  16 6LD2 ...      BS       2,BIFDTJ            [SET 'BSJAM' BIT 
   8 6LXL ...      BRN      MTJAM
   5 6PTB    SKIPERR 
  10 6Q*2          GEOERR   1,SCANSKIP   
   5 6R#=    REWFAIL 
   5 6RRW    MTFAIL  
   9 6S?G          CALL  7  SRFREE   
  15 6SR6          ACROSS   DTSTART,4           [TAPE FAIL   
   4 6T=Q ...[   
  11 6TQB ...[     FORMAT ERROR ON MAG TAPE  
   4 6W=2 ...[   
   5 6WPL    UNEQUAL 
   5 6X9=    MTFORMAT
   9 6XNW          CALL  7  SRFREE   
   9 6Y8G          ACROSS   DTSTART,5
   4 6YN6    [   
  20 6_7Q    [     S.R TO SKIP TO NEXT SENTINEL,READ QUAL.BLOCK AND TEST REPLIES 
   4 6_MB    [   
   5 7272    SKIPSR  
   8 72LL          SBX   6  FX1  
   9 72PQ ...      SEGENTRY K98DTSCAN
  19 72SW ...      BRN      K99DTSCAN           [THIS LINE SHOULD BE 'NULL'  
  21 72Y2 ...      DFSET    ,DUMP               [IF MULTIPLE READS ARE REQUIRED ALWAY
   9 7336 ...      SEGENTRY K99DTSCAN
   9 736=          MTSKIP   FILE,FRB 
  13 73KW          TESTREP2 FAIL,MTFAIL,SENT,MTFORMAT
   8 745G          ADX   6  FX1  
   7 74K6          EXIT  6  0
  10 754Q          MENDAREA 20,K99DTSCAN 
   4 75JB    #END
   2 7642
   8 ____ ...73766521000400000000
