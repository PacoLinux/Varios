   9 2278 ... SEG  GETFREE,,,,G167   
   4 22#B ...[   
  15 22FJ ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  13 22LQ ...[     THIS EXCLUDES CODE UNDER #SKI G167
   4 22RY ...[   
   8 22_=    #OPT  K0GETFREE=0   
  11 23DW    #LIS  K0GETFREE>K0PERI>K0ALLGEO 
   7 23JR ...#OPT G167 = 0   
   6 23NN ...#SKI G167&1 
  21 23SK ...# WITH UGUG EDIT M167 (ALLOW LF WITH NON-ATTRIBUTED PERMANENT PROPERTY) 
   4 23YG    #   
   4 24XQ    #   
  20 25CB    #     THIS SEGMENT IS PART OF THE PERIPHERAL CONTROLLER. IT SERVICES
  21 25X2    #     THE MACROS ISPER7, GETPER7, SETPROP. THE ENTRY POINTS USED ARE:-  
   4 26BL    #   
  14 26W=    #                    K1GETFREE  -  ISPER7   
  14 27*W    #                    K2GETFREE  -  GETPER7  
  14 27TG    #                    K3GETFREE  -  SETPROP  
  14 28*6    #                    K4GETFREE  -  ISPERG   
  14 28SQ    #                    K5GETFREE  -  CHECKATT 
  21 294J    #                    K11GETFREE -  ISPER7 - EXCEPT APERI/APROPNOS ALREAD
   4 29#B    #   
  21 29S2    #     FURTHER DETAILS OF THE PURPOSE OF EACH MACRO MAY BE FOUND ABOVE   
  16 2=?L    #     THE RELEVANT ENTRY POINT, AND ALSO IN PRM.6   
   4 2=R=    #   
   4 2?=W    #   
  10 2?QG    #              ENTRY POINTS 
   4 2#=6    #   
  12 2#PQ          SEGENTRY K1GETFREE,X1GETFREE  
  12 2*9B          SEGENTRY K2GETFREE,X2GETFREE  
  12 2*P2          SEGENTRY K3GETFREE,X3GETFREE  
  12 2B8L          SEGENTRY K4GETFREE,X4GETFREE  
  12 2BN=          SEGENTRY K5GETFREE,X5GETFREE  
  12 2C7W          SEGENTRY K11GETFREE,X11GETFREE
  15 2C*4          FSHENTRY K21GETFREE,,X21GETFREE,X21GETFREE
  15 2CG=          FSHENTRY K22GETFREE,X22GETFREE,,X22GETFREE
  15 2CJW          FSHENTRY K23GETFREE,X23GETFREE,,X23GETFREE
   4 2CMG    #   
   9 2D76    MAXATT         +ATTMAX+1
   6 2D9P    #UNS FLLP4B 
   4 2D##    (   
   5 2DBX    XACTLFC 
   8 2DFG    #HAL ACTLF+CENTRAL,0
   4 2DJ5    )   
  19 2DLQ    QBITS                              [DEVICE IDENTIFICATION TABLE 
  13 2F6B                   #4000               [TR  
  13 2FL2                   #2000               [TP  
  13 2G5L                   #1000               [LP  
  13 2GK=                   #0400               [CR  
  13 2H4W                   #0200               [CP  
   4 3?_Q    OPEN
  13 3#FB    #     SUBROUTINE OPENS :SYSTEM.PROPERTY 
   7 3#_2    #     LINK INX5 
   8 3*DL          SBX   5  FX1  
  12 3*Y=          OPENSYS  XBRKIN,PROPERTY,READ 
   8 3J?B          ADX   5  FX1  
   8 3JR2          BRN     (5)   
   4 3K=L    #   
   4 3KQ=    #   
   5 3L9W    PROPERTY
  20 3LPG    #             IF THE PROPERTY IS 'CENTRAL' OR A CONSOLE PROPERTY,   
  20 3M96    # DETAILS ARE STORED IN THE APROPNOS BLOCK, UNLESS THERE IS ALREADY 
  19 3MNQ    # ONE THERE, IN WHICH CASE EXIT 0. EXIT 1 IF COMMAND NOT ALLOWED
  11 3N8B    # TO SPECIFY A CONSOLE PROPERTY.
   7 3NN2    #     LINK IN X7
  18 3P7L          MHUNTW   3,APERI,APROPNOS    [LOCATE APROPNOS BLOCK   
  16 3PM=          LDX   4  ACOMMUNE5(2)        [LOAD PROPERTY NO
  10 3Q6W          LDX   0  BPROPCNT(3)  
  18 3QLG          BZE   0  N2                  [BR IF NO PROPS STORED   
   4 3R66    N1  
   7 3RKQ          SMO      0
  10 3S5B          TXU   4  BPROPS-1(3)  
  20 3SK2          BCC      N6                  [BR IF PROPERTY ALREADY STORED   
   8 3T4L          BCT   0  N1   
   4 3TJ=    N2  
  10 3W3W          LDXC  0  ACOMMUNE6(2) 
  19 3WHG          BCC      NOTCONS             [BR IF NOT CONSOLE PROPERTY  
   8 3X36          LDCT  0  #200 
  16 3XGQ          ANDX  0  AWORK4(2)           [MASK ONLINE BIT 
  21 3Y2B          BNZ   0  XFORBIDDEN          [BR IF ONLINE (- NO CONSOLE PROPERTY)
   5 3YG2    PCENTRAL
  10 3Y_L          LDX   0  CPROPNO(3)   
  21 3_F=          BNZ   0  (7)                 [BR IF CONSOLE PROP ALREADY STORED   
  16 3_YW          STO   4  CPROPNO(3)          [ELSE STORE IT   
   7 42DG          LDX   0  4
   9 42Y6          ANDX  0  BITS22LS 
  21 43CQ          SBN   0  1                   [DONT PUT ANY MORE DETAILS IN BLOCK  
  15 43XB          BZE   0  N6                  [FOR CENTRAL 
   8 44C2          BRN      N7   
   5 44WL    NOTCONS 
   7 45B=          LDX   0  4
  18 45TW          ANDX  0  BITS22LS            [REMOVE BIT 1 OF PR NO   
   7 46*G          SBN   0  1
  20 46T6          BZE   0  PCENTRAL            [BR IF THIS PROPERTY IS CENTRAL  
  10 47#Q    N7    SMO      BPROPCNT(3)  
  19 47SB          STO   4  BPROPS(3)           [STORE PROPERTY NO IN LIST   
   7 48#2          LDN   0  1
  19 48RL          ADS   0  BPROPCNT(3)         [UPDATE COUNT OF PROPERTIES  
   7 49?=          ADXC  4  4
  18 49QW          BCC      TEMPORARY           [BR IF PROPERTY TEMPORARY
  19 4==G          HUNTW    3,APERI,APERM       [SEE IF APERM IAS BEEN SET UP
  15 4=Q6          BPZ   3  PERM                [ & BR IF SO 
  16 4?9Q          SBX   7  FX1                 [RELATIVISE LINK 
  15 4?PB          SETNCORE ATTMAX+1*13+2/4+1,3,APERI,APERM  
   8 4#92          BRN      N3   
   6 4#NL    TEMPORARY   
  19 4*8=          HUNTW    3,APERI,BTEMP       [SEE IF BTEMP HAS BEEN SET UP
  15 4*MW          BPZ   3  PERM                [ & BR IF SO 
   8 4B7G          SBX   7  FX1  
  15 4BM6          SETNCORE ATTMAX+1*13+2/4+1,3,APERI,BTEMP  
   4 4C6Q    N3  
   8 4CLB          ADX   7  FX1  
   8 4D62          STOZ     A1(3)
  21 4DKL    PERM                               [MOVE PROPERTY NAME INTO APERM/BTEMP 
  17 4F5=          LDX   1  A1(3)               [CURRENT CHAR COUNT  
   7 4FJW          SRC   1  2
  19 4G4G          BZE   1  N4                  [BR IF BLOCK CURRENTLY EMPTY 
   7 4G75    #UNS G3SIGNET   
   4 4G9N    (   
  19 4G#?          SLC   4  8                   [TEST IF BIT9 SET IN PR NO   
  18 4GBW          BNG   4  PROPMEEP            [THIS IS A MEEP PROPERTY 
   4 4GFF    )   
  13 4GJ6          LDN   0  #26                 [ ^&^
   7 4H3Q          SMO      3
  19 4HHB          DCH   0  A1+1(1)             [APPEND ^&^ TO LAST PROP NAME
   8 4J32          BCHX  1  N4   
   4 4JGL    N4  
  19 4K2=          ADN   1  A1+1(3)             [START ADDRESS - DESTINATION 
  20 4KFW          LDN   0  ACOMMUNE2(2)        [START ADDRESS OF PROPERTY NAME  
  19 4K_G          SMO      ACOMMUNE1(2)        [NO OF SIGNIFICANT CHARACTERS
  18 4LF6          MVCH  0  0                   [MOVE IN CHARACTER STRING
   9 4LYQ          SBN   1  A1+1(3)  
   7 4MDB          SLC   1  2
  18 4MY2          STO   1  A1(3)               [RESET BLOCK CHAR COUNT  
   8 4NCL          LDX   1  FX1  
   4 4NX=    N6  
  14 4PBW          EXIT  7  2                   [AND EXIT
   4 4PWG    N5  
   6 4QB6    XFORBIDDEN  
   7 4QTQ          EXIT  7  1
   7 4QW#    #UNS G3SIGNET   
   4 4QWW    (   
  20 4QXD    PROPMEEP       [PLACE MEEP PROPERTY NAME AT HEAD OF THE (TEMPORARY) 
  18 4QY2                   [PROPERTY NAMES STRING (FOR CENTRAL LISTFILE)
  21 4QYJ          LDX   4  A1(3)               [MOVE CURRENT STRING OF (TEMPORARY)  
  21 4Q_6          ADN   1  A1+1(3)             [PROPERTY NAMES FORWARD BY 5 CHARS   
  20 4Q_N          LDX   2  1                   [STARTING WITH LAST STRING CHAR  
   7 4R2=    STRMV SLC   1  2
   7 4R2S          SBN   1  1
   7 4R3B          SRC   1  2
  20 4R3Y          LDCH  0  0(1)                [INITIALLY MOVE LAST STRING CHAR 
  20 4R4G          DCH   0  1(2)                [TO LAST STRING CHAR +5 POSITION 
   7 4R54          SLC   2  2
   7 4R5L          SBN   2  1
   7 4R68          SRC   2  2
  18 4R6Q          BCT   4  STRMV               [MOVE NEXT STRING CHAR   
   8 4R7#          LDX   2  FX2  
  21 4R7W          LDX   0  ACOMMUNE2(2)        [INSERT MEEP PROPERTY NAME (LPNN) +  
  19 4R8D          STO   0  A1+1(3)             [^&^ AT THE HEAD OF STRING   
   8 4R92          LDN   0  #26  
   9 4R9J          DCH   0  A1+2(3)  
  18 4R=6          LDN   0  5                   [UPDATE CHAR STRING COUNT
   8 4R=N          ADS   0  A1(3)
   8 4R?=          LDX   1  FX1  
   8 4R?S          BRN      N6   
   4 4R#B    )   
   4 4R*B    #   
   5 4RT2    SMHUNTW 
  20 4S#L    #     THIS SUBROUTINE HUNTS FOR AN APERI/APROPNOS BLOCK ON X2. IT IS
  12 4SS=    #     REQUIRED BY THE ALTLENG MACRO.
   7 4T?W    #     LINK IN X1
  11 4TRG          MHUNTW   2,APERI,APROPNOS 
   8 4W?6          BRN      (1)  
   4 4WQQ    #   
   6 4X=B    SCLOSETOP   
  15 4XQ2    #     THIS SUBROUTINE CLOSES :SYSTEM.PROPERTY   
   7 4Y9L    #     LINK IN X7
   8 4YP=          SBX   7  FX1  
  18 4_8W          CLOSETOP                     [CLOSE :S0STEM.PROPERTY  
   8 4_NG          ADX   7  FX1  
   8 5286          BRN      (7)  
   4 52MQ    #   
   5 537B    SVFREEW 
  21 53M2    #     THIS SUBROUTINE OPTIONALLY FREES THE APROPNOS, APROPS, APERM AND  
   8 546L    #     BTEMP BLOCKS  
   7 54L=    #     LINK IN X7
   5 555W    SVFREEW1
  10 55KG          VFREEW   APERI,APERM  
  10 5656          VFREEW   APERI,BTEMP  
  11 56JQ          VFREEW   APERI,APROPNOS   
   8 574B          BRN      (7)  
   4 57J2    #   
   6 583L    SCHECKATT   
  20 58H=    #     THIS SUBROUTINE BUZZES AROUND THE APIA POINTED AT BY X1, AND  
  20 592W    #     CHECKS THE PROPERTIES ATTRIBUTED AND REQUESTED IN THE APROPNOS
  20 59GG    #     BLOCK. THE MASTER MARKER IS UPDATED TO REFLECT THE SUITABILITY
  13 5=26    #     OF THE DEVICE UNDER INVESTIGATION.
   7 5=FQ    #     LINK IN X7
  21 5=_B          LDN   5  0                   [INITIALIASE MARKER FOR THIS DEVICE  
  19 5?F2          LDN   4  0                   [RELATIVE POINTER DOWN APIA  
   5 5?YL    XLOOP   
   7 5#D=          SMO      4
  20 5#XW          LDXC  0  APERPROP(1)         [LOAD 1ST/NEXT PROPERTY (LOSE B0)
  17 5*CG          BZE   0  PROPEND             [BR IF NONE/NO MORE  
  19 5*X6          BNG   3  NONWANTED           [BR IF NO PROPERTIES REQUIRED
  18 5BBQ          LDX   6  BPROPCNT(3)         [ELSE GET COUNT OF THEM  
   4 5BWB    R1  
   7 5CB2          SMO      6
  21 5CTL          TXU   0  BPROPS-1(3)         [COMPARE REQUESTS AND ATTRIBUTIONS   
  18 5D*=          BCC      WANTED              [BR IF THIS ONE WANTED   
  15 5DSW          BCT   6  R1                  [COUNT DOWN  
   6 5F#G    NONWANTED   
  11 5FS6    # THIS PROPERTY IS NOT WANTED   
   7 5G?Q          SMO      4
  10 5GRB          LDXC  0  APERPROP(1)  
  18 5H?2          BCC      WILLDO              [BR IF PROPERTY INCLUSIVE
   7 5HQL          SLL   0  1
  19 5J==          BPZ   0  TEMP                [BR IF TEMPORARY/EXCLUSIVE   
  20 5JPW          ORN   5  4                   [ELSE SET P/E NOT REQUIRED MARKER
  19 5K9G          BRN      WILLDO              [AND BR TO DO NEXT PROPERTY  
   4 5KP6    TEMP
  19 5L8Q          ORN   5  32                  [SET T/E NOT REQUIRED MARKER 
  15 5LNB          BRN      WILLDO              [AND CARRY ON
   5 5M82    WANTED  
   8 5MML          LDCT  0  256  
  21 5N7=          SMO      6                   [MARK PROPERTY AS BEING ATTRIBUTED   
  16 5NLW          ORS   0  BPROPS-1(3)         [TO THIS DEVICE  
   5 5P6G    WILLDO  
  20 5PL6          ADN   4  1                   [STEP POINTER DOWN PROPERTY LIST 
  21 5Q5Q          LDN   0  ATTMAX+1            [TEST IF REACHED MAXIMUM NUMBER OF   
  20 5QKB          TXL   4  0                   [DEVICE ATTRIBUTIONS PERMITTED   
  15 5R52          BCS      XLOOP               [BR IF NOT   
   5 5RJL    PROPEND 
  19 5S4=          BNG   3  NONREQ              [BR IF NO PROPERTIES REQUIRED
  18 5SHW          LDX   4  BPROPCNT(3)         [ELSE GET COUNT OF THEM  
   5 5T3G    RLOOP   
   7 5TH6          SMO      4
  20 5W2Q          LDXC  0  BPROPS-1(3)         [TEST IF PROPERTY ATTRIBUTED TO  
  17 5WGB          BCS      XATT                [DEVICE  - BR IF YES 
   7 5X22          SLL   0  1
  16 5XFL          BPZ   0  XTEMP               [BR IF TEMPORARY 
  19 5X_=          ORN   5  2                   [ELSE SET PERM NOT GOT MARKER
   7 5YDW          SRL   0  1
  20 5YYG          ANDX  0  BITS22LS            [STORE MISSING PERMANENT PROPERTY
  15 5_D6          SMO      FX2                 [IN ACOMMUNE8
   9 5_XQ          STO   0  ACOMMUNE8
   8 62CB          BRN      R2   
   5 62X2    XTEMP   
  18 63BL          ORN   5  32                  [SET TEMP NOT GOT MARKER 
   8 63W=          BRN      R2   
   4 64*W    XATT
  19 64TG          SMO      4                   [CLEAR B0 OF THIS ENTRY IN   
  16 65*6          STO   0  BPROPS-1(3)         [APROPNOS BLOCK  
   4 65SQ    R2  
  18 66#B          BCT   4  RLOOP               [COUNT DOWN AND PROCEED  
   5 66S2    NONREQ  
  13 67?L          BRN      (7)                 [EXIT
   4 67R=    #   
   7 67W?    #UNS G3SIGNET   
   4 67_#    (   
   6 684*    SCHECKMEEP  
  20 687B    #     THIS SUBROUTINE TESTS IF THE MEEP LP IS REQUIRED AND EXISTS   
  21 68=C    #     ON ENTRY : X3 -> APERI/APROPNOS BLOCK ; EXITS +0 WITH X1 -> MEEP  
  21 68*D    #     LP DEVICE LIST AND B0 SET IN AWORK4  IF TRUE ; EXITS +1 IF FALSE  
  18 68DF    #     REPLIES NOTAV IF MEEP LP IS REQUIRED BUT DOESN'T EXIST
   7 68HG    #     LINK IN X7
  17 68LH          LDX   0  AWORK4(2)           [TEST IF LP REQUIRED 
   8 68PJ          ANDN  0  #77  
   7 68SK          SBN   0  2
  16 68XL          BNZ   0  SMEEPEX             [LP NOT REQUIRED 
  17 692M          LDCT  6  #400                [FOR 'TWOMEEPS' CHECK
  21 695N          LDX   4  BPROPCNT(3)         [TEST IF A MEEP PROPERTY IS REQUIRED 
   5 698P    SMEEP1  
   7 69?Q          SMO      4
  10 69BR          LDX   0  BPROPS-1(3)  
   7 69FS          SLL   0  9
  21 69JT          BPZ   0  SMEEP2              [NOT A MEEP PROPERTY NO.(B9 UNSET)   
  21 69MW          LDX   0  CPROPNO(3)          [TEST IF A CONSOLE PROP ALSO REQUIRED
  20 69QX          BNZ   0  XMEEP               [NOT PERMITTED WITH MEEP PROPERTY
  21 69TY          BPZ   6  TWOMEEPS            [MORE THAN ONE MEEP PROPERTY REQUIRED
  10 69Y_          LDX   6  ASIGNETPTR   
  21 6=42          BZE   6  SMEEP2              [MEEP LP DOESN'T EXIST BUT CHECK FOR 
  15 6=73                                       ['TWOMEEPS'  
  20 6==4          STO   6  1                   [SET X1 -> MEEP LP DEVICE LIST   
  21 6=*5          LDCT  0  #400                [SET B0 OF AWORK4 TO INDICATE THAT   
  21 6=D6          ORS   0  AWORK4(2)           [THE MEEP LP IS REQUIRED AND EXISTS  
   5 6=H7    SMEEP2  
  18 6=L8          BCT   4  SMEEP1              [TEST NEXT PROPERTY NO.  
  21 6=P9          BZE   6  NOTAV               [MEEP LP REQUIRED BUT DOESN'T EXIST  
  21 6=S=          BPZ   6  (7)                 [MEEP LP IS REQUIRED&EXISTS: EXIT +0 
   5 6=X?    SMEEPEX 
  20 6?2#          EXIT  7  1                   [MEEP LP IS NOT REQUIRED: EXIT+1 
   4 6?5*    #   
   6 6?8B    TESTIFMEEP  
  21 6??C    #     THIS SUBROUTINE TESTS IF THE DEVICE OWNED BY THE CURRENT ACTIVITY 
  21 6?BD    #     IS THE MEEP(SIGNET) LINE PRINTER : EXITS +0 IF TRUE; EXITS +1 IF  
   6 6?FF    #     FALSE 
   7 6?JG    #     LINK IN X7
   8 6?MH          SMO      FX2  
   8 6?QJ          LDX   0  CPPTR
  17 6?TK          BZE   0  TMEEPEX             [REMOTE DEVICE OWNED 
  18 6?YL          BXE   0  ASIGNETPTR,(7)      [MEEP LP OWNED : EXIT +0 
   5 6#3M    TMEEPEX 
   7 6#6N          EXIT  7  1
   4 6#9P    #   
   6 6##Q    SCHECKMDEV1 
  18 6#CR          HUNTW    3,APERI,APROPNOS    [SETS X3 FOR SCHECKMDEV  
  16 6#GS          BNG   3  (5)                 [MUST BE REMOTE  
   6 6#KT    SCHECKMDEV  
  21 6#NW    #     THIS SUBROUTINE CHECKS THAT IF THE MEEP LP IS OWNED,THE REQUEST   
  21 6#RX    #     NEEDS IT AND THAT IF THE MEEP LP IS NOT OWNED,THE REQUEST DOESN'T 
   7 6#WY    #     NEED IT   
  21 6#__    #     ON ENTRY : X3 -> APERI/APROPNOS BLOCK ; IF CHECKS OK THE S/R EXITS
  13 6*52    #     ELSE THE REPLY PERMEXCL IS GIVEN  
   7 6*83    #     LINK IN X5
  21 6*?4          LTYPE    ,0                  [SET DEV TYPE IN AWK4 FOR SCHECKMEEP 
   9 6*B5          STO   0  AWORK4(2)
  21 6*F6          LDN   1  0                   [NOTE X1 WILL ONLY BE CHANGED IF MEEP
  19 6*J7                                       [LP IS REQUIRED AND EXISTS   
  10 6*M8          CALL  7  SCHECKMEEP   
  20 6*Q9          BRN      SCMO                [MEEP LP IS REQUIRED AND EXISTS  
  21 6*T=          CALL  7  TESTIFMEEP          [MEEP LP NOT REQUIRED : TEST IF OWNED
  21 6*Y?          BRN      PERMEXCL            [NOT REQUIRED BUT OWNED:NO ACCORDANCE
   7 6B3#          EXIT  5  0
  18 6B6*    SCMO  SBX   1  CPPTR(2)            [CHECK IF MEEP LP OWNED  
  21 6B9B          BNZ   1  PERMEXCL            [REQUIRED BUT NOT OWNED:NO ACCORDANCE
   7 6B#C          EXIT  5  0
   4 6BCD    #   
   4 6BGF    )   
   4 6BL2    #   
  14 6C5L    #                                  *  *   * 
  14 6CK=    #                                  * *   ** 
  14 6D4W    #                                  **     * 
  14 6DJG    #                                  * *    * 
  14 6F46    #                                  *  *  ***
   4 6FHQ    #   
   6 6G3B    X1GETFREE   
   4 6GH2    #   
  21 6H2L    #     THIS ENTRY POINT IMPLEMENTS THE ISPER7 MACRO. IT CHECKS WHETHER   
  20 6HG=    #     A DEVICE OF TYPE SPECIFIED IN B18-24 OF ACOMMUNE7 EXISTS WITH 
  20 6H_W    #     ANY CONSOLE AND/OR PERMANENT PROPERTIES WHICH MAY BE REQUESTED
  20 6JFG    #     PROPERTIES REQUIRED ARE IN AN APERI/APROPS BLOCK AND/OR APERI 
   8 6J_6    #     ASSOCS BLOCK. 
  15 6KDQ    #     THE COMMAND TYPE IS IN B0-17 OF ACOMMUNE7 
  14 6KYB    #              VIZ         B0=1    LISTFILE 
  14 6LD2    #                          B1=1    ONLINE   
   4 6LXL    #   
  16 6MC=          NGS   2  AWORK3(2)           [SET ISPER MARKER
   5 6MWW    XISPERG 
  10 6NBG          LDX   6  ACOMMUNE7(2) 
  18 6NW6          STO   6  AWORK4(2)           [TYPE/COMMAND INDICATOR  
   6 6P*Q    #SKI  K6PERI
   9 6PTB          GSCAN    6,ISPER  
  18 6Q*2          ANDN  6  63                  [MASK OUT PERIPHERAL TYPE
  19 6QSL          BZE   6  NOPROPS             [NO PROPERTIES FOR TAPE OR   
   7 6R#=          SBN   6  3
  15 6RRW          BZE   6  NOPROPS             [CARD-READER 
  21 6S?G          HUNT     3,APERI             [NO PROPERTIES ARE REQUIRED UNLESS   
  19 6SR6    #                                   A BLOCK OF TYPE APERI EXISTS
  19 6T=Q          BNG   3  NOPROPS             [BR IF NO PROPERTIES REQUIRED
  15 6TQB          SETNCORE ATTMAX*17+4/4+7,3,APERI,APROPNOS 
   8 6W=2          STOZ     A1(3)
   8 6WPL          LDN   7  A1(3)
   9 6X9=          LDN   0  A1+1(3)  
  20 6XNW          MOVE  7  6                   [ZEROISE FIXED PART OF APROPNOS  
  19 6Y8G ...      HUNTC    3,APERI,APROPS,NOAPROPS      [LOCATE APROPS BLOCK
  19 6_7Q          LDX   6  A1+1(3)             [NO OF PROPERTIES IN BLOCK   
   9 6_MB          TXL   6  MAXATT(1)
  21 7272          BCC      TOOMANY             [BR IF TOO MANY PROPERTIES SPECIFIED 
  18 72LL          CALL  5  OPEN                [OPEN :SYSTEM.PROPERTY   
   8 736=          LDN   5  A1+2 
   5 73KW    PROPLOOP
  17 745G          MHUNTW   3,APERI,APROPS      [LOCATE APROPS BLOCK 
  19 74K6          ADX   3  5                   [POINTER TO FIRST/NEXT ENTRY 
  10 754Q          LDN   4  ACOMMUNE1(2) 
  21 75JB          MOVE  3  4                   [MOVE PROPERTY RECORD INTO WORKSPACE 
   4 7642    STEP
  17 76HL          STEP                         [STEP TO NEXT RECORD 
  20 773=          BZE   3  PROPMISS            [BR IF PROPERTY NAME NOT FOUND   
  19 77GW          TESTREPN COORED,NOMOVE       [BR IF STEP NOT COORIDINATED 
  11 782G          MHUNTW   1,APERI,APROPS   
  19 78G6          ADX   1  5                   [ELSE RESET ACOMMUNE WORDS AS
  20 78_Q          ADN   2  ACOMMUNE1           [THEY MAY HAVE BEEN OVERWRITTEN  
   7 79FB          MOVE  1  4
   8 79_2          LDX   1  FX1  
   8 7=DL          LDX   2  FX2  
   5 7=Y=    NOMOVE  
  16 7?CW          TESTNAMX 3,ACOMMUNE2(2),APROPNAME(3),STEP,4   
  10 7?XG          LDX   0  APROPGROUP(3)
  20 7#C6          STO   0  ACOMMUNE6(2)        [STORE CONSOLE PROPERTY INDICATOR
  15 7#WQ          LDX   0  APROPNO(3)          [PROPERTY NO 
  19 7*BB          STOC  0  ACOMMUNE5(2)        [STORE PROPERTY NO (LESS B0) 
  19 7*W2          CALL  7  PROPERTY            [PROCESS THIS PROPERTY NAME  
  21 7B*L          BRN      TWOCONS             [MORE THAN ONE CONS PROP SPECIFIED   
  21 7BT=          BRN      NONOCONS            [COMMAND NOT ALLOWED CONSOLE PROPERTY
  19 7C#W          BCT   6  REWIND              [BR IF MORE PROPERTY NAMES   
  10 7CSG          BRN      NOMOREPROPS  
   5 7D#6    REWIND  
  18 7DRQ          REWIND                       [REWIND :SYSTEM.PROPERTY 
   7 7F?B          ADN   5  4
  19 7FR2          BRN      PROPLOOP            [BR TO PROCESS NEXT PROPERTY 
   6 7G=L    NOMOREPROPS 
  18 7GQ=          MFREEW   APERI,APROPS        [FREE THE APROPS BLOCK   
  18 7H9W          CALL  7  SCLOSETOP           [CLOSE :SYSTEM.PROPERTY  
   5 7HPG    NOAPROPS
  20 7J96    # THE CONTENTS OF ANY APERI/APROPS BLOCK HAVE NOW BEEN ANALYSED AND 
  16 7JNQ    # BUNGED IN THE APROPNOS, APERM, AND/OR BTEMP BLOCK.
  11 7K8B          HUNTW    3,APERI,ASSOCS   
  19 7KN2          BNG   3  NOASSOCS            [BR IF NO ASSOCIATIONS TO DO 
  17 7L7L          LDX   4  AWORK4(2)           [COMMAND/TYPE WORD   
  15 7LM=          LDCH  0  4                   [DEVICE TYPE 
   7 7M6W          SMO      0
  19 7MLG          LDX   0  QBITS(1)            [ASSOC KEY FOR DEVICE TYPE   
   7 7N66          DSA   0  4
  18 7NKQ          LDX   6  A1(3)               [COUNT OF ASSOCIATIONS   
   5 7P5B    PALOOP  
  20 7PK2          LDX   0  4                   [TEST IF KEY IN X4 IS CONTAINED  
  20 7Q4L          ANDX  0  A1+1(3)             [IN THE CIT PATTERN IN A1+1 OF   
  17 7QJ=          ERX   0  4                   [THE ASSOCS BLOCK....
  14 7R3W          BZE   0  SMATCH              [BR IF SO
  17 7RHG          ADN   3  ASSOCSLEN           [STEP TO NEXT RECORD 
   9 7S36          BCT   6  PALOOP   
  20 7SGQ          BRN      NOASSOCS            [BR IF NO RELEVANT ASSOCIATIONS  
   5 7T2B    SMATCH  
   8 7TG2          LDN   0  12   
  19 7T_L          STO   0  ACOMMUNE1(2)        [STORE CHARACTER COUNT OF 12 
  18 7WF=          LDN   7  A1+3(3)             [START OF PROPERTY NAME  
  10 7WYW          LDN   0  ACOMMUNE2(2) 
  21 7XDG          MOVE  7  3                   [MOVE PROPERTY NAME INTO WORKSPACE   
  17 7XGP          LDCT  1  #600                [ROUTINE TO EDIT..   
  18 7XJY          ORN   1  ACOMMUNE4(2)        [..OUT TRAING SPACES..   
  18 7XM7    NEXT  LDCH  0  0(1)                [..AND ALTER CHARACTER.. 
  17 7XPB          SBN   0  #20                 [..COUNT ACCORDINGLY 
   8 7XRK          BNZ   0  ZEDED
   7 7XTS          LDN   0  1
  10 7XY3          SBS   0  ACOMMUNE1(2) 
   7 7Y2=          SLC   1  2
   7 7Y4F          SBN   1  1
   7 7Y6N          SRC   1  2
  10 7Y8X          LDX   0  ACOMMUNE1(2) 
   8 7Y?6          BNZ   0  NEXT 
  16 7Y**    ZEDED LDX   0  A1+2(3)             [PROPERTY NUMBER 
  21 7YCQ          STO   0  ACOMMUNE6(2)        [STORE CONSOLE PROP INDICATOR (B0)   
  18 7YXB          STOC  0  ACOMMUNE5(2)        [STORE PROP NO (LESS B0) 
   8 7_78          LDX   1  FX1  
  18 7_C2          CALL  7  PROPERTY            [PROCESS THE ASSOCIATION 
  19 7_WL          BRN      NOASSOCS            [2ND CONS PROP - IGNORE IT   
   8 82B= ...      BRN      XFBD 
   5 82TW    NOASSOCS
  21 83*G    # ANALYSIS OF ASSOCIATIONS COMPLETED. NOW WE MUST COMPLETE THE APROPNOS 
  18 83T6          MHUNTW   3,APERI,APROPNOS    [LOCATE APROPNOS BLOCK   
  10 84#Q          LDX   5  BPROPCNT(3)  
  21 84SB          BNZ   5  P1                  [BR IF SOME PROPERTIES WERE STORED   
  19 85#2          FREECORE 3                   [ELSE FREE THE APROPNOS BLOCK
   9 85RL          BRN      SPORPON  
   4 86?=    P1  
   9 86QW          TXL   5  MAXATT(1)
  21 87=G          BCC      TOOMANY             [BR IF TOO MANY PROPERTIES REQUESTED 
  18 87Q6          LDX   0  CPROPNO(3)          [LOAD CONSOLE PROPERTY NO
   9 889Q          ANDX  0  BITS22LS 
  17 88PB          BCT   0  P2                  [BR IF NOT CENTRAL   
  17 8992          STOZ     CPROPNO(3)          [ELSE CLEAR CPROPNO  
   4 89NL    P2  
  19 8=8=          ADN   5  BPROPS              [DESTINATION ADDRESS POINTER 
  17 8=MW          HUNTW    1,APERI,APERM       [LOCATE APERM BLOCK  
  16 8?7G          BNG   1  NOAPERM             [BR IF NOT THERE 
   8 8?M6          LDX   6  A1(1)
  20 8#6Q          STO   6  CHPERM(3)           [STORE COUNT OF PERM PROP CHARS  
  19 8#LB          STO   5  ADPERM(3)           [START OF PERM PROP STRING   
   7 8*62          ADN   6  3
  18 8*KL          SRL   6  2                   [NO OF WORDS IN STRING   
   7 8B5=          ADX   5  3
   9 8BJW          LDN   4  A1+1(1)  
   7 8C4G          SMO      6
  18 8CJ6          MOVE  4  0                   [MOVE IN PERM PROP STRING
  16 8D3Q          SBX   5  3                   [RELATIVISE X5   
  16 8DHB          ADX   5  6                   [UPDATE POINTER  
  17 8F32          FREECORE 1                   [& FREE APERM BLOCK  
  18 8FGL    NOAPERM                            [NOW FOR THE BTEMP BLOCK 
  17 8G2=          HUNTW    1,APERI,BTEMP       [LOCATE BTEMP BLOCK  
  16 8GFW          BNG   1  NOBTEMP             [BR IF NOT THERE 
   8 8G_G          LDX   6  A1(1)
  20 8HF6          STO   6  CHTEMP(3)           [NO OF CHARS IN TEMP PROP STRING 
  18 8HYQ          STO   5  ADTEMP(3)           [START ADDRESS OF STRING 
   7 8JDB          ADN   6  3
  18 8JY2          SRL   6  2                   [WORD LENGTH OF STRING   
   7 8KCL          ADX   5  3
   9 8KX=          LDN   4  A1+1(1)  
   7 8LBW          SMO      6
  19 8LWG          MOVE  4  0                   [MOVE STRING INTO APROPNOS   
   7 8MB6          SBX   5  3
  20 8MTQ          ADX   5  6                   [TOTAL LENGTH OF APROPNOS USED   
  16 8N*B          FREECORE 1                   [FREE BTEMP BLOCK
   5 8NT2    NOBTEMP 
  17 8P#L          SBN   5  A1                  [X5 = RECORD HEADER  
  19 8PS=          STO   5  A1(3)               [STORE TOTAL RECORD HEADER   
   8 8Q?W          ADN   5  A1   
  18 8QRG          ALTLENG  3,5,SMHUNTW         [ALTER LENGTH OF BLOCK   
   5 8R?6    NOPROPS 
   5 8RQQ    SPORPON 
  19 8S=B          LDN   0  #77                 [CLEAR COMMAND INDICATOR, BUT
  18 8SQ2          ANDS  0  AWORK4(2)           [RETAIN PERIPHERAL TYPE  
  19 8T9L          BRN      PERIPHERAL          [BR TO LOOK FOR A SUITABLE   
  15 8TP=    #                                   PERIPHERAL  
  20 8W8W    XBRKIN                             [BREAK IN WHILE OPENING SYS.PROP 
  10 8WNG          MFREE    FILE,FABSNB  
  10 8X86          MFREEW   APERI,APROPS 
  11 8XMQ          MFREEW   APERI,APROPNOS   
  15 8Y7B          UP                           [BREAKIN EXIT
   4 8YM2    #   
  20 8_6L    TOOMANY                            [TOO MANY PROPERTIES SPECIFIED   
  18 8_L=          CALL  7  SVFREEW             [FREE APPROPRIATE BLOCKS 
  15 925W    Q1    SETREP2  MAXATT              [SET REPLY   
  15 92KG          UPPLUS   2                   [AND RETURN  
   4 9356    #   
  20 93JQ    PROPMISS                           [PROPERTY NOT IN :SYSTEM.PROPERTY
  18 944B          CALL  7  SCLOSETOP           [CLOSE :SYSTEM.PROPERTY  
  19 94J2          CALL  7  SVFREEW1            [FREE ALL BUT APROPS BLOCK   
  21 953L          STO   5  ACOMMUNE1(2)        [STORE RELATIVE POINTER TO ERRONEOUS 
  16 95H=          SETREP2  XPROP               [PROPERTY NAME   
   7 962W          UPPLUS   2
   4 96GG    #   
  20 9726    TWOCONS                            [2 CONSOLE PROPERTIES SPECIFIED  
  18 97FQ          CALL  7  SCLOSETOP           [CLOSE :SYSTEM.PROPERTY  
  17 97_B          CALL  7  SVFREEW             [AND FREE ALL BLOCKS 
   9 98F2          SETREP2  CPROPS   
   7 98YL          UPPLUS   2
   4 99D=    #   
  21 99XW    NONOCONS                           [COMMAND MAY NOT SPECIFY CONSOLE PROP
  18 9=CG          CALL  7  SCLOSETOP           [CLOSE :SYSTEM.PROPERTY  
  17 9=X6          CALL  7  SVFREEW1            [FREE ALL BUT APROPS 
   9 9?BQ          SETREP2  XCPROP   
   7 9?WB          UPPLUS   2
   4 9#B2    #   
   6 9#TL    SWIPEDCONS  
   5 9**=    SWITCHED
  18 9*SW          FREECORE 3                   [FREE APERI,ATTRIB BLOCK 
  18 9B#G          CALL  7  SCLOSETOP           [CLOSE :SYSTEM.PROPERTY  
   8 9BS6          BRN      T1   
   7 9BT9    #UNS G3SIGNET   
   4 9BW#    (   
   4 9BXC    #   
  21 9BYG    TWOMEEPS                           [MORE THAN 1 MEEP PROPERTY SPECIFIED 
   9 9B_K          SETREP2  TWOMEEPS 
   7 9C2N          UPPLUS   2
   4 9C3R    #   
  21 9C4W    XMEEP                              [MEEP PROPERTY MAY NOT BE SPECIFIED  
  18 9C5_                                       [WITH A CONSOLE PROPERTY 
   8 9C74          SETREP2  XMEEP
   7 9C87          UPPLUS   2
   4 9C9=    #   
   4 9C=*    )   
   5 9C?Q    NOPER   
  15 9CRB    # NO DEVICE OF REQUIRED TYPE IN RELEVANT CLUSTER
   9 9D?2          LDXC  0  AWORK3(2)
  15 9DQL          BCC      T1                  [BR IF GETPER
  11 9F==          VFREEW   APERI,APROPNOS   
   4 9FPW    T1  
   8 9G9G          SETREP2  NOPER
   7 9GP6          UPPLUS   2
   5 9H8Q    NOPROPA 
  18 9HNB          FREECORE 3                   [FREE APERI,ATTRIB BLOCK 
   4 9J82    #   
   5 9JML    PRNOPROP
  18 9K7=          CALL  7  SCLOSETOP           [CLOSE :SYSTEM.PROPERTY  
   5 9KLW    NOPROP  
  19 9L6G    # NO DEVICE OF SPECIFIED TYPE HAS ALL THE PERM PROPS REQUIRED   
   9 9LL6          SETREP   NOPROP   
   7 9M5Q          UPPLUS   2
   4 9MKB    #   
   5 9N52    PERMYUK 
  18 9NJL          FREECORE 3                   [FREE APERI,ATTRIB BLOCK 
   6 9P4=    PRPERMEXCL  
  18 9PHW          CALL  7  SCLOSETOP           [CLOSE :SYSTEM.PROPERTY  
   5 9Q3G    PERMEXCL
  15 9QH6    # DEVICES HAVE PERM EXCL PROPERTIES NOT REQUIRED
   9 9S_=          SETREP2  PERMEXCL 
   7 9TDW          UPPLUS   2
   4 9TYG    #   
   4 9WD6    WRNG
  17 9WXQ    # ALL DEVICES WITH SPECIFIED PERM EXCL PROPS ARE WRONGED
   8 9XCB          SETREP2  WRNG 
   7 9XX2          UPPLUS   2
   4 9YBL    #   
   5 9YW=    NOTAV   
  15 9_*W    # NO DEVICES WITH SPECIFIED PERM PROPS ARE FREE 
   8 9_TG          SETREP2  NOTAV
   7 =2*6          UPPLUS   2
   4 =2SQ    #   
   4 =3#B    PROK
  18 =3S2          CALL  7  SCLOSETOP           [CLOSE :SYSTEM.PROPERTY  
   4 =3TS    OK  
   8 =4M8          SETREP   OK   
   7 =4R=          UPPLUS   2
   4 =5=W    #   
   9 =5GN ...XFBD  GEOERR   1,FRBID  
  14 =5QG    #                                  *  *   **
  15 =6=6    #                                  * *   *  *   
  14 =6PQ    #                                  **      *
  14 =79B    #                                  * *    * 
  15 =7P2    #                                  *  *  ****   
   4 =88L    #   
   6 =8N=    X2GETFREE   
   4 =97W    #   
  20 =9MG    #     THIS ENTRY POINT IS USED BY THE GETPER7 MACRO. IT SEARCHES FOR
  19 ==76    #     A DEVICE OF THE REQUIRED TYPE WITH THE REQUIRED PROPERTY  
   8 ==LQ    #     CONFIGURATION 
   4 =?6B    #   
  18 =?L2          STO   2  AWORK3(2)           [INDICATE GETPER ENTRY   
  10 =#5L          LDCH  7  ACOMMUNE7(2) 
   6 =#K=    #SKI  K6PERI
   9 =*4W          GSCAN    7,GETPER 
  20 =*JG          STO   7  AWORK4(2)           [PRESERVE DEVICE TYPE REQUIRED   
   6 =B46    PERIPHERAL  
  13 =BHQ    #     NOW TO LOOK FOR A SUITABLE DEVICE 
  19 =C3B          LDXC  5  GSIGN               [SET MASTER MARRKER (- X5 =0)
  15 =CH2          STO   5  AWORK2(2)           [ = 'NOPER'  
  11 =D2L          HUNTW    3,APERI,APROPNOS 
  20 =DG=          BNG   3  PLOCAL              [NO PROPERTIES - MUST BE LOCAL   
   7 =DJF    #UNS G3SIGNET   
   4 =DLN    (   
  19 =DNX          CALL  7  SCHECKMEEP          [CHECK IF MEEP LP REQUIRED   
  20 =DR6          BRN      SCHECKDEV           [MEEP LP EXISTS AND IS REQUIRED  
  15 =DT*                                       [EXAMINE IT  
   4 =DXJ    )   
  10 =D_W          LDX   4  CPROPNO(3)   
  18 =FFG          BZE   4  PLOCAL              [BR IF NO CONS PROP REQ'D
  17 =F_6          IDFPER   XIDFBRK,OK          [LOOK FOR IDF DEVICE 
  19 =GDQ          LDX   0  ACOMMUNE9(2)        [NOT FOUND - BR TO ANALYSE   
   7 =GNJ ...#SKI G167&1$1   
  14 =GYB          BRN      XAMINE              [RESULT  
   6 =H2# ...#SKI G167&1 
   4 =H4= ...(   
  16 =H68 ...#     ALLOWS LISTFILES TO NON-ATTRIBUTED PROPERTIES 
   7 =H86 ...      SRL   0  1
   8 =H=4 ...      BZE   0  WRNG 
   9 =H#2 ...      BRN      XXAMINE  
   4 =H*Y ...)   
   5 =HD2    PLOCAL  
   9 =H_K          LDXC  0  AWORK3(2)
  20 =J4# ...      BCC      XGFND               [SEPAREATE FINDPEREC FOR ISPER   
  17 =J73          FINDPERE 1,APFOTY,AWORK4(2),QNEXTDVI,,NLINE,NLINE 
   8 =J9Q          BRN      NLINE
  14 =J#G ...XGFND FINDPERE 1,APFOTY,AWORK4(2),QNEXTDEV  
  15 =JC=    # FIND DEVICE LIST OF NEXT DEVICE OF THIS TYPE  
  16 =JN3    NLINE BNG   1  NODEV               [BR IF NO MORE   
   7 =JYS    #UNS G3SIGNET   
   4 =K2Q    (   
  21 =K4N          LDXC  0  AWORK4(2)           [IF MEEP LP REQUIRED,IGNORE OTHER LPS
  19 =K6L          BCS      NODEV               [AND ANALYSE MASTER MARKER   
  20 =K8J          BXE   1  ASIGNETPTR,QNEXTDV [MEEP LP NOT REQUIRED : IGNORE IT 
   6 =K=G    SCHECKDEV   
   4 =K#D    )   
   7 =KBG          LDN   5  0
   8 =KW6          LTYPE    1,0  
  20 =L*Q          BZE   0  SLINPUT             [BR IF TR   (NO ATTRIBUTIONS FOR)
   7 =LTB          SBN   0  3
  20 =M*2          BZE   0  SLINPUT             [OR CR      (SLOW INPUT DEVICES )
  19 =MSL          STO   1  ACOMMUNE1(2)        [PRESERVE DEVICE LIST POINTER
  17 =N#=          LADDP    1,1                 [LOAD ADDRESS OF APIA
  19 =NRW          CALL  7  SCHECKATT           [CHECK PROPERTY ATTRIBUTIONS 
  19 =P?G          LDX   1  ACOMMUNE1(2)        [RESTORE DEVICE LIST POINTER 
   5 =PR6    SLINPUT 
   8 =P_B ...      JNANON   1,XJR
  15 =Q7L          ORN   5  8               [SET WRG MARKER  
   8 =Q*W          BRN      XFREE
  17 =QJ6 ...XJR   JRIGHT   1,RIGHT             [BR IF DEVICE RIGHTED
  19 =QQB          ORN   5  8                   [ELSE SET 'WRONGED' MARKER   
   5 =R=2    RIGHT   
  17 =RPL          JFREE    1,XFREE             [BR IF DEVICE FREE   
  21 =S9=          ORN   5  16                  [ELSE SET ALREADY ALLOCATED MARKER   
   5 =SNW    XFREE   
   8 =T8G    #SKI  K6PERI>199$199
  17 =TN6          TRACE    5,GPMARK            [TRACE CHECKATT REPLY
  19 =W7Q          BZE   5  PERFECT             [BR IF DEVICE IS PERFECT FIT 
   7 =WMB          NGN   0  1
   4 =X72    S1  
  15 =XLL          ADN   0  1                   [SHIFT COUNT 
  20 =Y6=          SRC   5  1                   [FIND L.S. BIT SET IN MARKER FOR 
  15 =YKW          BPZ   5  S1                  [THIS DEVICE 
   7 =_5G          LDN   5  1
   7 =_K6          SMO      0
  20 ?24Q          SLL   5  0                   [AND SET IT IN THE MASTER MARKER 
   9 ?2JB          ORS   5  AWORK2(2)
  20 ?342          SBN   5  32                  [TEST IF OK EXCEPT FOR TEMP PROPS
  19 ?3HL          BNZ   5  QNEXTDV            [AND BR FOR NEXT DEVICE IF NOT
   9 ?43=          LDXC  0  AWORK3(2)
  19 ?4GW          BCS      QNEXTDVI            [BR FOR NEXT DEVICE IF ISPER 
  19 ?52G          LDX   0  CPPTR(2)            [BR IF DEVICE WITH WRONG TEMP
  16 ?5G6          BNZ   0  QNEXTDEV            [ALREADY STORED  
  18 ?5_Q          STO   1  CPPTR(2)            [ELSE REMEMBER THIS ONE  
   5 ?6CS    QNEXTDV 
   9 ?6D#          LDXC  0  AWORK3(2)
  16 ?6DS          BCS      QNEXTDVI               [ISPER LABEL  
   9 ?6FB          BRN      QNEXTDEV 
   5 ?6_2    NODEV   
   7 ?6_7    #UNS  FSHTEST   
   4 ?6_#    #SKI
   4 ?6_F    (   
   4 ?754    )   
  20 ?7DL    # ALL LOCAL DEVICES HAVE NOW BEEN EXAMINED. NOW TO ANALYSE MASTER   
  11 ?7Y=    # MARKER TO DECIDE ON THE REPLY 
   4 ?8CW    #   
  17 ?8XG          LDX   0  AWORK2(2)           [LOAD MASTER MARKER  
  17 ?9C6    XAMINE                             [ENTRY FROM IDFPERIS 
   8 ?9WQ    #SKI  K6PERI>199$199
  17 ?=BB          TRACE    0,GPMASTER          [TRACE MASTER MARKER 
  16 ?=W2          SRL   0  1                   [           REPLY
  16 ??*L          BZE   0  NOPER               [           NOPER
   6 ??GS ...#SKI G167&1 
   5 ??N2 ...XXAMINE 
   7 ??T=          SRL   0  1
  17 ?##W          BZE   0  NOPROP              [           NOPROP   
   7 ?#SG          SRL   0  1
  17 ?*#6          BZE   0  PERMEXCL            [           PERMEXCL 
   7 ?*RQ          SRL   0  1
  16 ?B?B          BZE   0  WRNG                [           WRNG 
   7 ?BR2          SRL   0  1
  16 ?C=L          BZE   0  NOTAV               [           NOTAV
   9 ?CQ=          LDXC  0  AWORK3(2)
  16 ?D9W          BCS      OK                  [IF ISPER - OK   
  17 ?DPG    # DEVICE OK EXCEPT FOR TEMPORARY PROPERTY CONFIGURATION 
  20 ?F96          SAWCEN   CENTRAL,NOTCENTRAL  [BR IF REMOTE DEVICE ALLOCATED   
  19 ?FNQ          LDX   1  CPPTR(2)            [LOCAL DEVICE - LOAD CPPTR   
   8 ?G8B    #SKI  K6PERI>99$99  
  10 ?GN2          TRACE    1,ALLOTPET   
  18 ?H7L          SETALLOC 1                   [MARK DEVICE ALLOCATED   
   9 ?HM=          LDCH  0  ATYPE(2) 
   9 ?J6W          SBN   0  ACTLF/64 
  15 ?JLG          BNZ   0  NOTLF               [BR IF NOT CO
   8 ?K66          LGEOG    1,0  
  16 ?KKQ          STO   0  JSOURCE1(2)         [SET UP JSOURCE1 
   8 ?L5B          LTYPE    1,0  
  10 ?LK2          DCH   0  JSOURCE1(2)  
   6 ?M4L    NOTCENTRAL  
  21 ?MJ=          SETREP2  PREQ                [SET 'CHANGES IN TEMPORARY PROPERTY  
  19 ?N3W    #                                   ATTRIBUTIONS REQUIRED' REPLY
  15 ?NHG          UPPLUS   2                   [AND RETURN  
   4 ?P36    #   
  14 ?PGQ    #                                  *  *  ***
  15 ?Q2B    #                                  * *      *   
  15 ?QG2    #                                  **     ***   
  15 ?Q_L    #                                  * *      *   
  14 ?RF=    #                                  *  *  ***
   4 ?RYW    #   
   6 ?SDG    X3GETFREE   
  21 ?SY6    # THIS ENTRY POINT SERVICES THE SETPROP MACRO. IT REQUESTS CHANGES IN   
  21 ?TCQ    # THE ATTRIBUTIONS OF TEMPORARY PROPERTIES TO THE DEVICE OWNED BY THE   
  20 ?TXB    # CURRENT ACTIVITY. GETPER7 DROPS STRAIGHT THROUGH TO HERE WHEN NOT 
   8 ?WC2    # ISSUED BY AN ACTLF
   4 ?WWL    #   
  10 ?XB=    NOTLF ACROSS   GETFREEB,1   
   4 ?XTW    #   
   4 ?Y*G    #   
   5 C??B    XIDFBRK 
  18 C?R2          VFREEW   APERI,APROPNOS      [FREE ANY APROPNOS BLOCK 
   8 C#2S          SETREP   CANT 
   5 C#=L          UP
   4 C#Q=    #   
  17 CF6W    PERFECT                            [PERFECT FIT FOUND   
   9 CFLG          LDXC  0  AWORK3(2)
  17 CG66          BCS      SETOK               [SET REPLY IF ISPER  
  18 CGKQ          SETALLOC 1                   [ELSE ALLOCATE THE DEVICE
   9 CH5B          LDCH  0  ATYPE(2) 
   9 CHK2          SBN   0  ACTLF/64 
  16 CJ4L          BNZ   0  NJS                 [BR IF NOT ACTLF 
   8 CJJ=          LGEOG    1,0  
  16 CK3W          STO   0  JSOURCE1(2)         [SET UP JSOURCE1 
   8 CKHG          LTYPE    1,0  
  10 CL36          DCH   0  JSOURCE1(2)  
   4 CLGQ    NJS 
  15 CM2B          STO   1  CPPTR(2)            [STORE CPPTR 
   8 CMG2    #SKI  K6PERI>99$99  
  10 CM_L          TRACE    1,ALLOTPER   
   5 CN3N    SETOK   
   6 CN3R    #UNS FLLP4B 
   4 CN3W    (   
   6 CN3_    #UNS FLLP4BC
   8 CN44       TRACEVER 2,GF1   
   8 CN47          LDX   2  FX2  
   7 CN4=          SMO   FX1 
   9 CN4*          LDX   1  XACTLFC  
   9 CN4D          IF 1,E,ATYPE(2)   
   6 CN4H    #UNS FLLP4BC
   8 CN4L       TRACEVER 1,GF2   
   9 CN4P          LDX   1  CPPTR(2) 
   6 CN4S    #UNS FLLP4BC
   8 CN4X       TRACEVER 1,GF3DEV
   9 CN52          AND  BS,1,FLP4B   
   6 CN55          THEN  
   6 CN58    #UNS FLLP4BC
   9 CN5?       TRACEVER 0,GF4TOFL2  
  10 CN5B             DOWN  FORMLOOP,2   
   6 CN5F          ELSE  
   4 CN5J    )   
  11 CN5Q          HUNT     1,APERI,APROPNOS 
  15 CN7S          IF       1,PZ                [ PROPERTIES 
  17 CN9W          AND      +CPROPNO(1),NZ      [ CONSOLE PROPERTY   
  11 CNB2             HUNT     1,AMXOR,ADCB  
  11 CND4          AND      1,PZ [ ON 7900   
  14 CNG6          AND      BS,1,ADCB7502LP     [ 7502 LP
   6 CNJ8          THEN  
   6 CNJW    #UNS FLLP4BC
   9 CNKJ       TRACEVER 0,GF5TOFL2  
  19 CNL=             DOWN     FORMLOOP,2          [ SET FORMAT LOOP BLOCK   
   6 CNN#          ELSE  
   8 CNQB             SETREP   OK
   5 CNSD          FI
   6 CNST    #UNS FLLP4BC
   9 CNT=       TRACEVER EXEC3(2),GF6
   6 CNTM    #UNS FLLP4B 
   5 CNW4          FI
   7 CNWG          UPPLUS   2
   4 CNYW    #   
  14 CPDG    #                                  *  *    *
  14 CPY6    #                                  * *    **
  14 CQCQ    #                                  **    * *
  15 CQXB    #                                  * *  *****   
  14 CRC2    #                                  *  *    *
   4 CRWL    #   
   6 CSB=    X4GETFREE   
  20 CSTW    #     THIS ENTRY POINT SERVICES THE ISPERG MACRO. IT COMBINES THE   
  12 CT*G    #     FUNCTIONS OF ISPER AND GETPER 
   4 CTT6    #   
  16 CW#Q          STO   2  AWORK3(2)           [MARK AS GETPER  
  17 CWSB          BRN      XISPERG             [AND BR TO DO GETPER 
   4 CX#2    #   
   4 CXRL    #   
  15 CY?=    #                                  *  *  ****   
  14 CYQW    #                                  * *   *  
  14 C_=G    #                                  **    ***
  15 C_Q6    #                                  * *      *   
  14 D29Q    #                                  *  *  ***
   4 D2PB    #   
   6 D392    X5GETFREE   
  19 D3NL    # THIS ENTRY POINT SERVICES THE CHECKATT MACRO. IT SETS A REPLY 
  21 D48=    # ACCORDING TO THE DEGREE OF ACCORDANCE BETWEEN THE PROPERTIES IN ANY   
  19 D4MW    # APROPNOS BLOCK AND THOSE ATTRIBUTED TO THE DEVICE OWNED BY THE
  13 D57G    # CURRENT ACTIVITY (AS DEFINED BY CPPTR)
   4 D5M6    #   
   7 D5S#    #UNS G3SIGNET   
   4 D5_G    #SKI
  18 D66Q          HUNTW    3,APERI,APROPNOS    [X3 -> APROPNOS OR -VE   
   7 D6?Y    #UNS G3SIGNET   
  21 D6F6          CALL  5  SCHECKMDEV1         [CHECKOUT MEEP LP OWNERSHIP/REQUEST  
  16 D6LB          LADDP    ,1                  [ADDRESS OF APIA 
  17 D762          CALL  7  SCHECKATT           [COMPARE PROPERTIES  
   8 D7KL    #SKI  K6PERI>199$199
  16 D85=          TRACE    5,CHECKATT          [CHECK ATT REPLY 
  14 D8JW          BZE   5  OK                  [   OK   
   7 D94G          LDN   0  2
   7 D9J6          ANDX  0  5
  15 D=3Q          BNZ   0  NOPROP              [   NOPROP   
  12 D=HB          LDN   0  4                   [
   7 D?32          ANDX  0  5
  15 D?GL          BNZ   0  PERMEXCL            [   PERMEXCL 
  14 D#2=          BRN      NOTCENTRAL          [   PREQ 
   4 D#35    #   
   4 D#3Y    #   
  15 D#4R    #                                  *  *   *    *
  15 D#5L    #                                  * *   **   **
  15 D#6F    #                                  **     *    *
  15 D#7#    #                                  * *    *    *
  16 D#87    #                                  *  *  ***  ***   
   4 D#92    #   
   6 D#9T    X11GETFREE  
  15 D#=N    # ENTRY USED BY CHANGELIST COMMAND TO DO ISPER  
  16 D#?H    # MACRO EXCEPT APERI/APROPNOS BLOCK ALREADY SET UP  
   4 D##B    #   
  15 D#*9          NGS   2  AWORK3(2)           [ ISPER MACRO
  10 D#B4          LDCH  7  ACOMMUNE7(2) 
  16 D#BX          STO   7  AWORK4(2)           [ PERIPHERAL TYPE
  10 D#CQ          BRN      PERIPHERAL   
   4 D#CR    #   
   4 D#DW    #   
   4 D#DX    #   
   5 D#FW    #PAGE   
  11 D#_G          MENDAREA 45,K100GETFREE   
   4 D*F6    #END
   8 ____ ...31754577000200000000
