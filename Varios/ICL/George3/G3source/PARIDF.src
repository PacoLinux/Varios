  15 22FL    #SEG  PARIDF8                      [A.C.PUTMAN  
   7 22_=    #OPT  K0PARIDF=0
  16 23DW    #LIS  K0PARIDF>K0PARSORT>K0IDF>K0COMMAND>K0ALLGEO   
   9 23YG                   8HPARIDF 
   4 24D6    #   
  20 24XQ    #     THIS SEGMENT VALIDATES THE CONTENTS OF CPAR/CPCT AND CPAR/ARPB
  19 25CB    #     BLOCKS IN THE IDF. IT IS ENTERED FROM SEGMENT PARSORT, AND
  12 25X2    #     PERFORMS THE FOLLOWING TASKS:-
   4 26BL    #   
  20 26W=    #              FOR CPAR/CPCT BLOCKS, THE CONSOLE PROPERTY ATTRIBUTED
  20 27*W    #              TO THE DEVICE IS STORED IN JSCEC,AND THE CLUSTERING  
  10 27TG    #              IS CHECKED   
   4 28*6    #   
  20 28SQ    #              FOR CPAR/AIPB BLOCKS, THE EXISTENCE OF THE IDENTIFIER
  17 29#B    #              IS CHECKED, THEN AS FOR CPAR/CPCT BLOCKS 
   4 29S2    #   
  15 29_8 ...#              AWORK1   PARAMETER OPTION WORD   
  20 2=6B ...#              AWORK3   =0 INDICATES PRESENCE OF VALID PARAMETERS   
   4 2=?L    #   
   4 2=R=    #   
  11 2?=W          SEGENTRY K1PARIDF,X1PARIDF
   4 2?QG    #   
   4 2#=6    #   
   5 2#PQ    #PAGE   
  17 2*9B    ZILLINO        +ILLINO             [ILLEGAL LINE NUMBER 
  17 2*P2    ZJNOPER        +JNOPER             [NOT ON INSTALLATRON 
  18 2B8L    ZNOCLUS        +INOCLUS            [NOT IN CORRECT CLUSTER  
  14 2BN=    # CONVERSION TABLE FOR IDF PERIPHERAL TYPES 
  19 2C7W    XCONFIG                            [7900 CONFIGURATION REPORT   
  14 2CMG                   +24                 [ 7071   
  15 2D76                   +24                 [ 5 TRACK T/W
  15 2DLQ                   +14                 [ 7153 AVDU  
  19 2F6B    TYPE7020                           [ PLUS PCT TYPES FROM HERE   
  14 2FL2                   +2                  [ 7021 LP
  14 2G5L                   +3                  [ 7022 CR
  15 2GK=                   +63                 [ 7023 T/W   
  14 2H4W                   +0                  [ 7024 TR
  14 2HJG                   +1                  [ 7025 CR
   4 2J46    #   
  14 2JHQ    #                                  *  *   * 
  14 2K3B    #                                  * *   ** 
  14 2KH2    #                                  **     * 
  14 2L2L    #                                  * *    * 
  14 2LG=    #                                  *  *  ***
   4 2L_W    #   
   5 2MFG    X1PARIDF
   4 2M_6    #   
  20 2NDQ    # AT THIS STAGE IN THE PROCEEDINGS WE HAVE A STRIDE (WHICH MAY BE OF
  21 2NYB    # ZERO LENGTH) OF CPAR/JSCE BLOCKS (LOCAL PERIPHERALS), CPAR/CPCT BLOCKS
  21 2PD2    # (PCT PERIPHERALS), AND CPAR/AIPB BLOCKS (IPB IDENTIFIERS), ALL MIXED  
  21 2PXL    # IN ANY ORDER. THIS SECTION OF THE CODE CHECKS THE PERIPHERAL DEFINED  
  17 2QC=    # IN THE CPAR BLOCK WITH THE CURRENT STATE OF THE IDF.  
   4 2QWW    #   
  15 2RBG          IDFOPEN  XBRK                [OPEN THE IDF
  19 2RW6          LDX   1  2                   [POINTER TO A1 OF FILE/FRRB  
   7 2S3# ...      LDN   0  1
   8 2S6R ...      SMO      FX2  
  19 2S== ...      STO   0  AWORK3              [SET VALID PARAMS. INDICATOR 
  19 2S*Q          BRN      PASSONE             [FIRST TIME THROUGH - BRANCH 
   6 2STB    NEXTPARAM   
  19 2T*2          LDN   5  0                   [SUBSEQUENT PASSES - RELOCATE
  16 2TSL          CALL  7  NEWREC              [IDF BASE CELL   
   5 2W#=    PASSONE 
   8 2WRW          LDX   2  FX2  
  14 2X?G          LDX   2  FX2                 [EXIT 1  
   9 2XR6          CALL  7  SHUNTPCT 
  19 2Y=Q          BNG   3  NOW4IPB             [BR IF NO MORE PCT PARAMETERS
  19 2YQB          LDEX  4  JSCEB(3)            [LOAD GEOGRAPHIC NO OF UNIT  
  18 2_=2          LDXC  5  BUNIT(1)            [LOAD BASE OF UNIT CHAIN 
  20 2_PL          BCC      N1                  [BR IF UNIT CELL IN THIS RECORD  
  19 329=    N0    CALL  7  NEWREC              [GET POINTER TO NEW FILE/FRRB
   4 32NW    N1  
  19 338G          BZE   5  ZAZOOM              [BR IF UNIT NOT FOUND IN IDF 
  20 33N6          LDEX  5  5                   [DISPLACEMENT PART OF IDF POINTER
   7 347Q          SMO      5
  19 34MB          LDEX  0  BTYPE(1)            [LOAD GEOG NO FOR THIS CELL  
   7 3572          TXU   0  4
  18 35LL          BCC      UNITCELL            [BR IF RIGHT CELL FOUND  
   7 366=          SMO      5
  19 36KW          LDXC  5  0(1)                [LOAD POINTER DOWN UNIT CHAIN
  20 375G          BCS      N0                  [BR IF CELL IN DIFFERENT RECORD  
   8 37K6          BRN      N1   
  18 384Q    UNITCELL                           [CORRECT UNIT CELL FOUND 
  18 38JB          CALL  7  SHUNTPCT            [RELOCATE CPAR/CPCT BLOCK
   9 3942          LDX   4  JSCEB(3) 
  15 39HL          SRL   4  9                   [LINE NUMBER 
   8 3=3=          ANDN  4  #77  
   7 3=GW          SMO      5
  18 3?2G          LDXC  5  BLINE(1)            [LOAD BASE OF LINE CHAIN 
   8 3?G6          BCC      N2   
   4 3?_Q    N3  
   9 3#FB          CALL  7  NEWREC   
   4 3#_2    N2  
  19 3*DL          BZE   5  NOLINE              [BR IF LINE CELL NOT FOUND   
   7 3*Y=          LDEX  5  5
   7 3BCW          SMO      5
  16 3BXG          LDX   0  BNO(1)              [LOAD LINE NUMBER
   7 3CC6          TXU   0  4
  18 3CWQ          BCC      SLINECELL           [BR IF CORRECT CELL FOUND
   7 3DBB          SMO      5
  19 3DW2          LDXC  5  0(1)                [LOAD POINTER DOWN LINE CHAIN
   8 3F*L          BCS      N3   
   8 3FT=          BRN      N2   
  18 3G#W    SLINECELL                          [CORRECT LINE CELL FOUND 
   9 3GSG          CALL  7  SHUNTPCT 
   9 3H#6          LDX   4  JSCEA(3) 
  18 3HRQ          BNZ   4  TERMINAL            [BR IF TERMINAL SPECIFIED
   7 3J?B          SMO      5
  20 3JR2          LDXC  5  BATT(1)             [LOAD POINTER TO ATTRIBUTION CELL
   8 3K=L          BCC      N4   
   9 3KQ=          CALL  7  NEWREC   
  20 3L9W    N4    BZE   5  NOATTS              [BR IF LINE HAS NO ATTRIBUTIONS  
   8 3LPG          ANDN  5  #777 
   7 3M96          SMO      5
  18 3MNQ          LDX   5  BTYPE(1)            [CONSOLE PROPERTY WORD   
  18 3N8B          BRN      SCLUSTER            [BR TO CHECK CLUSTERING  
  17 3NN2    TERMINAL                           [TERMINAL SPECIFIED  
  18 3P7L          LDX   6  4                   [PRESERVE TERM/INT NO.   
  19 3PM=          SRL   4  6                   [ISOLATE TERMINAL IDENTIFIER 
   7 3Q6W          SMO      5
  18 3QLG          LDXC  5  BLINE(1)            [LOAD BASE OF 7020 CHAIN 
   8 3R66          BCC      N5   
   4 3RKQ    N6  
   9 3S5B          CALL  7  NEWREC   
   4 3SK2    N5  
   8 3T4L          BZE   5  NOTER
   8 3TJ=          ANDN  5  #777 
   7 3W3W          SMO      5
   9 3WHG          LDX   0  BNO(1)   
  19 3X36          SRL   0  12                  [2 CHAR TERMINAL IDENTIFIER  
   7 3XGQ          TXU   0  4
  20 3Y2B          BCC      X7020CELL           [BR IF A 7020 CELL MATCH FOUND   
   7 3YG2          SMO      5
   8 3Y_L          LDXC  5  0(1) 
   8 3_F=          BCS      N6   
  17 3_YW          BRN      N5                  [ELSE TRY NEXT ONE   
   6 42DG    X7020CELL   
  19 42Y6          LDCH  4  6                   [LOAD INTERFACE NO SPECIFIED 
  19 43CQ          BNZ   4  N9                  [BR IF INTERFACE SPECIFIED   
   7 43XB          LDN   5  0
  19 44C2          BRN      SCLUSTER            [ESLE NO ATTS - DO CLUSTERING
   7 44WL    N9    SMO      5
   9 45B=          LDX   6  BTYPE(1) 
  17 45TW          LDCH  6  6                   [ISOLATE INTERFACE NO
   7 46*G          TXU   6  4
  18 46T6          BCC      XINT                [BR IF RIGHT 7020 CELL   
   7 47#Q          SMO      5
   8 47SB          LDCH  0  3(1) 
   7 48#2          ANDN  0  4
  20 48RL          BZE   0  N7                  [BR IF NOT CLUSTER CONSOLE CELL  
   7 49?=          SMO      5
   8 49QW          LDX   0  3(1) 
  20 4==G          ANDN  0  #77                 [INT NO OF OTHER HALF OF CONSOLE 
   7 4=Q6          TXU   0  4
  18 4?9Q          BCC      XINT                [BR IF MATCH NOW FOUND   
   7 4?PB    N7    SMO      5
   8 4#92          LDXC  0  4(1) 
  21 4#NL          BCS      NOTINTIDF           [BR IF NO MORE CELLS FOR THIS 7020   
   7 4*8=          SMO      5
  19 4*MW          LDXC  5  0(1)                [LOAD POINTER TO NEXT CELL   
   8 4B7G          BCC      N8   
   9 4BM6          CALL  7  NEWREC   
  15 4C6Q    N8    BZE   5  ZAZOOM              [FUNNY IDF   
   8 4CLB          ANDN  5  #777 
   8 4D62          BRN      N9   
  17 4DKL    XINT                               [CORRECT CELL FOUND  
   7 4F5=          SMO      5
  16 4FJW          LDCH  6  BTYPE(1)            [LOAD DEVICE TYPE
   8 4G4G          ADX   6  FX1  
   7 4GJ6          SMO      6
  20 4H3Q          LDX   6  TYPE7020-1          [CONVERT TO ^LOCAL^ DEVICE TYPE  
   9 4HHB          CALL  7  SHUNTPCT 
  20 4J32          DCH   6  JSCEA(3)            [STORE DEVICE TYPE IN CPAR BLOCK 
   7 4JGL          SMO      5
  20 4K2=          LDXC  5  BATT(1)             [LOAD POINTER TO ATTRIBUTION CELL
   8 4KFW          BCC      N10  
   9 4K_G          CALL  7  NEWREC   
   4 4LF6    N10 
  18 4LYQ          BZE   5  SCLUSTER            [BR TO CHECK CLUSTERING  
   8 4MDB          ANDN  5  #777 
   7 4MY2          SMO      5
  18 4NCL          LDX   5  BTYPE(1)            [CONSOLE PROPERTY WORD   
  18 4NX=          BRN      SCLUSTER            [BR TO CHECK CLUSTERING  
   4 4PBW    #   
  11 4PWG    # THAT'S ALL FOR PCT PARAMETERS 
   4 4QB6    #   
  10 4QTQ    # NOW FOR IPB IDENTIFIERS   
   4 4R*B    #   
   5 4RT2    NOW4IPB 
  19 4S#L          CALL  7  SHUNTIPB            [FIND NEXT CPAR/AIPB BLOCK   
  20 4SS=          BNG   3  SCLOSE              [BR TO CLOSE IDF IF NO MORE TO DO
  20 4T?W          LDEX  4  JSCEB(3)            [GEOG NO OF IPB (IF SPECIFIED)   
   9 4TRG          LDX   6  JSCEA(3) 
  16 4W?6          ANDN  6  #7777               [IDENTIFIER NO   
   7 4WQQ          LDN   5  0
  16 4X=B          CALL  7  NEWREC              [ALWAYS EXITS +1 
  15 4XQ2    SEVEN          +7                  [NEVER OBEYED
  17 4Y9L          LDXC  5  BIPB(1)             [BASE OF IPB CHAIN   
   8 4YP=          BCC      P1   
   4 4_8W    P2  
   9 4_NG          CALL  7  NEWREC   
   4 5286    P1  
  17 52MQ          BZE   5  P88                 [BR IF IPB NOT FOUND 
   8 537B          ANDN  5  #777 
   7 53M2          SMO      5
  18 546L          LDX   0  0(1)                [LOAD FPTR DOWN IPB CHAIN
  16 54L=          STO   0  AWORK4(2)           [STORE IN AWORK4 
   7 555W          SMO      5
  18 55KG          LDEX  7  BTYPE(1)            [IPB GEOGRAPHIC NUMBER   
  19 5656          BNZ   4  P1A                 [BR IF SPECIFIC IPB WANTED   
  15 56JQ          STO   6  AWORK2(2)           [PRESERVE X6 
  19 574B          CALL  6  XFINDPEREC          [ELSE CHECK THIS ONE EXISTS  
   8 57J2          SMO      FX2  
  16 583L          LDX   6  AWORK2              [THEN RESTORE X6 
  16 58H=          BNG   2  P7                  [AND BR IF NOT   
  19 592W          LTYPE    2,0                 [IGNORE THIS IPB IF NOT ON   
   8 59GG          SBN   0  #30  
  17 5=26          BNZ   0  P7                  [       INSTALLATION 
   8 5=FQ          LDX   2  FX2  
  20 5=_B          BRN      TRIDENT             [BR TO SEARCH IDENTIFIER CHAIN   
   4 5?F2    P1A 
   7 5?YL          TXU   7  4
  18 5#D=          BCC      TRIDENT             [BR IF CORRECT CELL FOUND
   7 5#XW    P3    SMO      5
  18 5*CG          LDXC  5  0(1)                [OTHERWISE TRY NEXT ONE  
   8 5*X6          BCS      P2   
   8 5BBQ          BRN      P1   
  19 5BWB    TRIDENT                            [SEARCH DOWN IDENTIFIER CHAIN
  14 5CB2          NGNC  0  1                   [SET C   
   7 5CTL          SMO      5
  21 5D*=          TXL   6  5(1)                [TEST X6 AGAINST LARGEST IDENTIFIER  
  17 5DSW          BCS      P4                  [BR IF FIT POSSIBLE  
  19 5F#G          BNZ   4  NOUNITIDF           [BR IF SPECIFIC IPB MENTIONED
  17 5FS6          BRN      P3                  [BR TO TRY NEXT IPB  
   4 5G?Q    P4  
   7 5GRB          SMO      5
  19 5H?2          LDXC  5  BLINE(1)            [POINTER TO IDENTIFIER CHAIN 
   8 5HQL          BCC      P5   
   4 5J==    P6  
   9 5JPW          CALL  7  NEWREC   
  19 5K9G    P5    BZE   5  P7                  [BR IF IDENTIFIER NOT FOUND  
   8 5KP6          ANDN  5  #777 
   7 5L8Q          SMO      5
   9 5LNB          TXU   6  BNO(1)   
  21 5M82          BCC      TRANSMOGRIF         [BR IF CORRECT IDENTIFIER CELL FOUND 
   7 5MML          SMO      5
  17 5N7=          LDXC  5  0(1)                [ELSE LOAD NEXT ONE  
   8 5NLW          BCS      P6   
   8 5P6G          BRN      P5   
   4 5PL6    P7  
  21 5Q5Q          BNZ   4  NOUNITIDF           [BR IF I NOT FOUND ON SPECIFIED UNIT 
   8 5QKB          LDX   2  FX2  
  18 5R52          LDXC  5  AWORK4(2)           [POINTER TO NEXT IPB CELL
  18 5RJL          BNZ   5  P2                  [CARRY ON IF MORE IPB(S) 
  17 5S4=    P88                                [IPB NOT FOUND IN IDF
  17 5SHW          BZE   4  NOUNITIDF           [BR IF NOT ESSENTIAL 
   9 5T3G          BRN      ZAZOOM   
   6 5TH6    TRANSMOGRIF 
  17 5W2Q          BNZ   4  P8                  [BR IF IPB SPECIFIED 
   7 5WGB          SMO      5
  16 5X22          LDEX  4  BTYPE(1)            [ELSE REMEMBER IT
   4 5XFL    P8  
   7 5X_=          SMO      5
  18 5YDW          LDCH  6  BTYPE(1)            [LOAD BASIC DEVICE TYPE  
   8 5YYG          ADX   6  FX1  
  21 5_D6          SMO      6                   [CONVERT CONFIGURATION REPORT TYPE   
  17 5_XQ          LDX   6  XCONFIG             [TO BASIC DEVICE TYPE
   7 62CB          SMO      5
  18 62X2          LDXC  5  BATT(1)             [PTR TO ATTRIBUTION CELL 
   8 63BL          BCC      P9   
   9 63W=          CALL  7  NEWREC   
   4 64*W    P9  
   8 64TG          BZE   5  P10  
   8 65*6          ANDN  5  #777 
   7 65SQ          SMO      5
  21 66#B          LDX   5  BTYPE(1)            [CONSOLE PROPERTY ATTRIBUTION WORD   
   4 66S2    P10 
  18 67?L          CALL  7  SHUNTIPB            [RELOCATE CPAR/AIPB BLOCK
  17 67R=          DCH   6  JSCEA(3)            [BASIC DEVICE TYPE   
   9 68=W          LDX   0  JSCEP(3) 
  19 68QG          BNZ   0  P12                 [BR IF IPB ALREADY LOCATED   
  15 69=6          DEX   4  JSCEB(3)            [IPB GEOG NO 
   7 69PQ          LDX   7  4
   7 6=9B          LDX   1  3
  17 6=P2          CALL  6  XFINDPEREC          [FIND IPB DEVICE LIST
  19 6?8L          BNG   2  P7                  [BR IF NOT ON INSTALLATION   
   8 6?N=          LTYPE    2,0  
  20 6#7W          SBN   0  #30                 [BR IF IPB NOT ON INSTALLATION   
   8 6#MG          BNZ   0  P7   
  19 6*76          BNG   2  P7                  [BR IF NOT ON INSTALLATION   
  19 6*LQ          STO   2  JSCEP(3)            [STORE DEVICE LIST POINTER   
   8 6B6B          LDX   2  FX2  
   4 6BL2    P12 
  16 6C5L    SCLUSTER                           [CHECK CLUSTERING
   5 6CK=    NOATTS  
  19 6D4W          SAWCE    CENTRAL,OK          [BR IF CENTRAL - ALWAYS OK   
  19 6DJG          BZE   5  NOTOK               [NOMINATED CONSOLE U0 - ERROR
  17 6F46          HUNTMISB 1,APERI,CONSOLE     [FIND HUIDGE BLOCK   
  20 6FHQ          BNG   1  NOTOK               [ERROR - NO NOMINATED CONSOLES   
  18 6G3B          LDX   4  A1(1)               [NO OF SIGNIFICANT WORDS 
   7 6GH2          SBN   4  2
   8 6H2L          LDCT  0  #200 
  10 6HG=          ANDX  0  JSOURCE2(2)  
  17 6H_W          BZE   0  SC1                 [BR IF PCT CONSOLE   
  10 6JFG          LDX   6  JSOURCE2(2)  
   8 6J_6          SLL   6  15   
  16 6KDQ          SRL   6  3                   [GEOG NO TO B0-11
  19 6KYB          LDX   0  JSOURCE1(2)         [IDENTIFIER IN B12-23 OF B0  
   8 6LD2          BRN      SC2  
   4 6LXL    SC1 
  19 6MC=          LDX   6  JSOURCE1(2)         [TERMINAL IDENTIFIER TO B0-11
   7 6MWW          SLL   6  6
  10 6NBG          LDX   0  JSOURCE2(2)  
   7 6NW6          SRL   0  9
  17 6P*Q          ANDN  0  #777                [LINE NO TO B15-23   
  21 6PTB    SC2                                [CONSTRUCT X6 TO LOOK LIKE 1ST WORD  
  16 6Q*2          DSA   0  6                   [OF HUIDGE ENTRY 
  18 6QSL          STO   5  ACOMMUNE1(2)        [X5 CONTAINS PROPERTY NO 
   9 6R#=          LDX   0  BITS22LS 
  15 6RRW          ANDS  0  ACOMMUNE1(2)        [REMOVE B0,1 
   4 6S?G    SC3 
   7 6SR6          SMO      4
  18 6T=Q          LDX   0  A1-1(1)             [CONSOLE PROPERTY NUMBER 
   9 6TQB          ANDX  0  BITS22LS 
  10 6W=2          TXU   0  ACOMMUNE1(2) 
  20 6WPL          BCC      THISONE             [BR IF THIS IS THE NOM CONSOLE   
   7 6X9=          SBN   4  1
   8 6XNW          BCT   4  SC3  
  19 6Y8G          BRN      NOCLUSIDF           [NOT FOUND IN HUIDGE - ERROR 
   5 6YN6    THISONE 
  18 6_7Q          SMO      4                   [RIGHT PROPERTY .... BUT 
   8 6_MB          TXU   6  A1(1)
  18 7272          BCS      NOCLUSIDF           [... BR IF WRONG CONSOLE 
   4 72LL    OK  
  18 736=          CALL  7  SHUNTPCT            [LOCATE CPAR/CPCT BLOCK  
   7 73KW          NGN   7  1
  16 745G          BPZ   3  SC4                 [BR IF PCT DEVICE
  18 74K6          CALL  7  SHUNTIPB            [ELSE LOCATE CPAR/AIPB   
   4 754Q    SC4 
  19 75JB          STO   5  JSCEC(3)            [STORE CONSOLE PROPERTY NO   
  16 7642          NAME     3,CPAR,JSCE         [AND RENAME BLOCK
  16 76?S ...      STOZ     AWORK3(2)           [VALID PARAMETER 
  18 76HL          BNG   7  NEXTPARAM           [BR IF WAS A PCT BLOCK   
  15 773=          BRN      NOW4IPB             [BR IF IPB   
   5 77GW    NOTOK   
  17 782G    NOCLUSIDF                          [INVALID CLUSTERING  
  17 78G6          CALL  7  SHUNTPCT            [LOCATE CPCT BLOCK   
   7 78_Q          NGN   7  1
  15 79FB          BPZ   3  SC5                 [BR IF FOUND 
  18 79_2          CALL  7  SHUNTIPB            [ELSE LOCATE AIPB BLOCK  
   4 7=DL    SC5 
   9 7=Y=          LDX   0  AWORK1(2)
   9 7?CW          ANDN  0  BNOCLUS  
  19 7?XG          BZE   0  WRONGCLUS           [BR TO REPORT ERROR IF WANTED
  19 7#C6          LDCT  0  #600                [AND SET CLUSTER CHECK FAILED
  14 7#WQ          ORS   0  JSCEP(3)            [MARKER  
  15 7*BB          BRN      SC4                 [AND CARRY ON
   5 7*W2    SCLOSE  
   9 7B*L          LDX   0  AWORK1(2)
   8 7BT=          ANDN  0  BIDF 
  20 7C6L ...      BZE   0  XFRCL               [BR IF IDF NOT TO BE LEFT OPEN   
   9 7CD2 ...      LDX   0  AWORK3(2)
  19 7CPB ...      BZE   0  UPPLUS1             [BR IF A VALID PARAM. EXISTS 
  18 7D2Q ...XFRCL MFREEALL FILE,FRRB           [FREE ALL IDF CORE BLOCKS
  16 7D#6          CLOSETOP                     [CLOSE THE IDF   
   5 7DRQ    UPPLUS1 
  15 7F?B          UPPLUS   1                   [AND ESCAPE  
   4 7FR2    XBRK
  20 7G=L          UP                           [BREAK-IN WHILE OPENING THE IDF  
   4 7GQ=    #   
   5 7H9W    ZAZOOM  
  10 7HPG          GEOERR   1,IDF TWIT   
   4 7J96    #   
  17 7JNQ    WRONGCLUS                          [ INVALID CLUSTERING 
   8 7K8B          LDX   1  FX1  
  10 7KN2          LDX   6  ZNOCLUS(1)   
   5 7L7L    REPORT  
  19 7LM=          CALL  7  SHUNTPCT            [RELOCATE APPROPRIATE BLOCK  
   9 7M6W          BNG   3  SHUNTIPB 
   9 7MLG          LDX   4  JSCEQ(3) 
  16 7N66          SRL   4  12                  [PARAMETER NUMBER
   7 7NKQ          FREECORE 3
  17 7P5B          LDN   5  0                   [ANALYSIS LEVEL ZERO 
  17 7PK2          SPARANOX 4,5                 [REACCESS PARAMETER  
  17 7Q4L          ERRORX   6                   [SEND ERROR MESSAGE  
  18 7QJ=          UNIFREE                      [AND FREE CPB/CUNI BLOCK 
   9 7R3W          BRN      NEXTPARAM
   6 7RHG    NOUNITIDF   
   6 7S36    NOTINTIDF   
   5 7SGQ    NOTER   
   8 7T2B          LDX   1  FX1  
  10 7TG2          LDX   6  ZJNOPER(1)   
   9 7T_L          BRN      REPORT   
  17 7WF=    NOLINE                             [ILLEGAL LINE NUMBER 
   8 7WYW          LDX   1  FX1  
  10 7XDG          LDX   6  ZILLINO(1)   
   9 7XY6          BRN      REPORT   
   4 7YCQ    #   
   6 7YXB    XFINDPEREC  
  21 7_C2    #     SUBROUTINES PUTS DEVICE LIST POINTER OF DEVICE WHOSE GEOG NO IS   
   8 7_WL    #     IN X7 IN X2.  
   7 82B=    #     LINK IN X6
   6 82FB ...#UNS AV7900S
  21 82JG ...      FINDPERE 2,APGEOG,7,,AV7900  [FIND DEVICE LIST.(INCLUDE V7900 DLS)
   6 82ML ...#UNS AV7900S
   4 82QQ ...#SKI
  10 82TW          FINDPERE 2,APGEOG,7   
   8 83*G          BRN      (6)  
   4 83T6    #   
   5 84#Q    NEWREC  
  20 84SB    # THIS SUBROUTINE SWITCHES X1 TO POINT TO A NEW RECORD OF THE IDF   
  15 85#2    # THE RECORD IS READ DOWN INTO CORE IF NECESSARY
  21 85RL    # ON ENTRY, X5 CONTAINS THE CELL POINTER WITH B0 CLEARED. ON EXIT, X1   
  12 86?=    # POINTS AT THE CELL SPECIFIED BY X5
   7 86QW    #     LINK IN X7
   8 87=G          SBX   7  FX1  
  15 87Q6          COOR2                        [HAVE A REST 
   4 889Q    #   
  20 88PB    # THIS IS PUT IN BECAUSE WE COULD OBEY AN AWFUL LOT OF INSTRUCTIONS 
  17 8992    # BEFORE CO-ORDINATING IN THE NORMAL COURSE OF AFFAIRS  
   4 89NL    #   
   7 8=8=          LDX   0  5
  17 8=MW          SRL   0  9                   [RECORD NO REQUIRED  
  10 8?7G          STO   0  ACOMMUNE3(2) 
   8 8?M6          LDX   1  FX2  
  19 8#6Q          MXB      1                   [GET POINTER TO NEW RECORD   
   8 8#LB          ADX   7  FX1  
  15 8*62          EXIT  7  1                   [EXIT+1 IF OK
   5 8*KL    SHUNTPCT
  10 8B5=    # HUNT CPAR/APCT BLOCK ON X3
   7 8BJW    #     LINK IN X7
  10 8C4G          HUNTW    3,CPAR,CPCT  
   8 8CJ6          BRN      (7)  
   4 8D3Q    #   
   5 8DHB    SHUNTIPB
  10 8F32    # HUNT CPAR/AIPB BLOCK ON X3
   7 8FGL    #     LINK IN X7
  10 8G2=          HUNTW    3,CPAR,AIPB  
   8 8GFW          BRN      (7)  
   4 8G_G    #   
  10 8HF6          MENDAREA 45,K100PARIDF
   4 8HYQ    #END
   8 ____ ...13567510000400000000
