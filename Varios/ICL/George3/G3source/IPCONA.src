  12 22FL ...      SEG      IPCONA,,KEITH MARTIN 
  19 22_=    #     THIS SEGMENT HANDLES REMOTELY INITIATED MESSAGE SEQUENCES 
   9 23DW    #     FOR TYPE 28 IPBS. 
   4 23YG    #   
  11 24D6          SEGENTRY K1IPCONA,X1IPCONA
  11 24XQ          SEGENTRY K2IPCONA,X2IPCONA
   8 25CB    #     STORED WORDS  
   9 25X2    RESBITS        #777700  
   9 26BL    TMESSCODES     #04000040
   9 26W=                   #04000041
   9 27*W                   #04000042
   9 27TG                   #04000043
   9 28*6                   #04000046
   9 28SQ                   #03000047
   9 29#B                   #07000060
   9 29S2                   #07000061
   9 2=?L                   #04000066
   9 2=R=                   #03000067
   9 2?=W                   #02000070
   9 2?QG                   #02000071
   9 2#=6                   #03000077
   9 2#PQ    TBIT1          #20000000
   9 2*9B    TBIT2          #10000000
   9 2*P2    TBIT3          #4000000 
   9 2B8L    TBIT5          #1000000 
   9 2BN=    TBIT11         #50000   
   9 2C7W    RNE            #02400000
   9 2CMG    TFREP          #3200000 
   9 2D76    TIREP          #2000000 
  16 2DLQ    #     SUBROUTINE USED TO SET OBJECT PROGRAM REPLIES 
   4 2F6B    #   
   4 2FL2    SREP
  10 2G5L          LDX   3  ACOMMUNE1(2) 
   9 2GK=          STO   4  ACC4(2)  
   9 2H4W          STO   5  ACC5(2)  
   9 2HJG          STO   7  ACC7(2)  
  10 2J46          LDX   5  AREPPTR(3)   
   9 2JHQ          LDX   3  FPTR(3)  
  16 2K3B          SBN   3  ARINGEL             [ADDRESS OF AOLPT
   5 2KH2    #SKI  G3
  18 2L2L          FINDACTFB 3,,APET,,,BPTR     [ADDRESS OF AOLPTS PCA   
   6 2LG=    #SKI  IPCG4 
  12 2L_W          HUNT2    2,ADATA,HLOCKDOWN,3  
   8 2MFG          SMO      FX2  
   9 2M_6          LDN   4  ACOMMUNE2
   7 2NDQ          LDN   7  1
  12 2NYB          FINMOVE  3,5,4,7,HLOCKB1,2,1  
   8 2PD2          LDX   2  FX2  
   9 2PXL          LDX   4  ACC4(2)  
   9 2QC=          LDX   5  ACC5(2)  
   9 2QWW          LDX   7  ACC7(2)  
  10 2RBG          LDX   3  ACOMMUNE1(2) 
   7 2RW6          NGNC  0  2
  21 2S*Q          ANDS  0  ASTATUS1(3)         [UNSET TR STORED & IN PROGRESS BITS  
   9 2STB          STOZ     ATIMCT(3)
   9 2T*2          STOZ     ASTR(3)  
   9 2TSL          STOZ  ALENTR(3)   
  10 2W#=          STOZ     AREPPTR(3)   
   9 2WRW          LDX   0  ASTMEL(3)
   9 2X?G          STO   0  AWORK4(2)
   9 2XR6          STOZ     ASTMEL(3)
   9 2Y=Q          LDX   3  FPTR(3)  
   9 2YQB          SBN   3  ARINGEL  
   6 2_=2    #SKI  IPCG4 
   4 2_PL    (   
  12 329=          HUNT2    2,ADATA,HLOCKDOWN,3  
  13 32NW          CLEAR    APETADDR(3),0,HLOCKB1,2,2
   8 338G          LDX   2  FX2  
   4 33N6    )   
   7 347Q          CCBB     3
  10 34MB          LDX   3  ARINGEL(3)   
   9 34TL ...      STO   5  ACC5(2)  
   9 353W ...      LDX   5  AWORK4(2)
  10 35=6 ...      TICKLE   JOBNO(3),5   
   9 35DB ...      LDX   5  ACC5(2)  
   7 35LL          EXIT  6  0
  16 366=    #     SUBROUTPNE TO LOCATE LINK ELEMENTS IN STACK   
  19 36KW    #     GIVEN THE LINK IDENTIFIER IN ACOMMUNE1(2)  [X2 MUDT = FX2 
  17 375G    #     ADDRESS IS LEFT IN X3. LINK IS X7. ON EXIT X1=FX1 
   4 37K6    #   
   4 384Q    SREL
  17 38JB          LDX 3 IPCPTR(2)              [FIND IPC ACTIVITY   
  16 3942          HUNT2    3,IPCD,IPSTACK      [FIND STACK BLOCK
  10 39HL          LDX   1  ARINGNO(3)   
   8 3=3=          ANDN  1  #7777
  15 3=GW          BZE   1  SFR1                [STACK EMPTY 
   8 3?2G          ADN   3  A1   
   9 3?G6    SFR3  LDX   0  ALINUM(3)
  11 3?_Q          BXE   0  ACOMMUNE1(2),SFR2
   9 3#FB          ADN   3  ARECLEN  
   8 3#_2          BCT   1  SFR3 
   7 3*DL    SFR1  NGN   3  1
   8 3*Y=    SFR2  LDX   1  FX1  
   7 3BCW          EXIT  7  0
  18 3BXG    #     S/R TO LOCATE ACTIVITY OWNING GEORGE THREE IPC LINK   
  16 3CC6    SLACT LDX   2  FPTR(3)             [CONTROL BLOCK   
  19 3CWQ          SBN   2  IPCRNG              [BEGINNING OF CONTROL BLOCK  
   5 3DBB    SLACT1  
   9 3DW2          LDX   2  BPTR(2)  
   9 3F*L          LDX   0  ATYPE(2) 
  17 3FT=          BXL   0  CACT,SLACT1         [J IF NOT ACTIVITY   
   7 3G#W          EXIT  7  0
  14 3GSG    #     S/R TO SET REPLY TO GEORGE TRANSFER   
   4 3H#6    SGSR
   8 3HRQ          CALL  7  SREL 
   9 3J?B          LDX   2  FPTR(3)  
  15 3JR2          STO   6  IPCGREP-IPCRNG(2)   [STORE REPLY 
   8 3K=L          LDX   0  GSIGN
  15 3KQ=          ERS   0  ASTATUS1(3)         [UNSET T.IP. 
   9 3L9W          STOZ     ATIMCT(3)
   8 3LPG          STOZ  ASTR(3) 
   9 3M96          STOZ     ALENTR(3)
   9 3N8B          STOZ     ASTMEL(3)
  16 3NN2          CALL  7  SLACT               [LOCATE ACTIVITY 
  14 3P7L          DESUSPEND                    [KICK IT 
   7 3PM=          EXIT  4  0
   4 3PMM ...TOPX
  10 3PN4 ...      MFREE      IPCD,ATASK 
   9 3PNF ...      BRN      TOPX12   
   5 3PNW ...TOPX1   
   7 3PP? ...      LDX   6  0
   5 3PPN ...TOPX12  
  17 3PQ5 ...      HUNTW    3,IPCD,ATASK               [ANY TASCKS   
  13 3PQG ...      BPZ   3  TOPXA        [J IF SO[   
  16 3PQX ...      EXIT  6  0               [ELSE SIT QUIESCENT  
   8 3PR# ...TOPXA LDX   0  A1(3)
  10 3PRP ...      STO   0  ACOMMUNE1(2) 
  12 3PS6 ...      LDX   0  A1+1(3)      [TYPE   
   7 3PSH ...      ANDN  0  1
   8 3PSY ...      BNZ   0  TOPXB
   8 3PT* ...      CALL  7  SREL 
  10 3PTQ ...      LDX   7  ASTATUS1(3)  
   7 3PW7 ...      ANDN  7  1
  16 3PWJ ...      BZE   7  TOPX        [J IF NO TRANSFER STORED 
   8 3PW_ ...      LDCT  7  #400 
  10 3PXB ...      ORS   7  ASTATUS1(3)  
   5 3PXR ...TOPXB   
  15 3PY8 ...      CIPCM     Q        [CLEAR QUIESCENT BIT   
  10 3PYK ...#          NOW CANCELL READ 
   7 3P_2 ...      LDN   0  2
  10 3P_C ...      DCA      ,DLA,0,MODE  
  10 3P_S ...      GPERIIPC      ,2,CT   
  18 3Q22 ...      TRACE   CPRW2(2),IPCANCRD [READ REPLY AFTER A CANCEL  
  13 3Q29 ...#    NOW TEST READ REPLY TO DETERMINE   
   9 3Q2L ...#    RESULT OF CANCELL  
  15 3Q33 ...#    READ REJECTED =>  CANCELL ACCEPTED    ETC  
  14 3Q3D ...      TIPCREP   SP,TIME,RESETA,NOP,,X1IPCONA
   8 3Q3T ...#  CANCELL REJECTED 
   5 3Q4= ...TOPXC   
   8 3Q4M ...      CALL  7  SREL 
   8 3Q54 ...      LDCT  7  #400 
  10 3Q5F ...      ANDX   7  ASTATUS1(3) 
  14 3Q5W ...      ERS   7  ASTATUS1(3)       [CLEAR TIP 
  16 3Q6? ...      BRN      NPUTOK   [LOOK AT WHAT WE READ NOW   
  12 3Q6W    #     ENTRY POINT WHEN IPB QUIESCENT
   5 3QLG    X1IPCONA
  11 3QN4 ...#     IF READ NOT IMP OP,THEN   
  12 3QNT ...#     PROBABLY AFTER A SUCCESSFUL   
  11 3QPL ...#     CANCEL WUTH NO WRITE DONE 
  12 3QQC ...#     AFTER IT,SO SET UP DUMMY CLOSE
   9 3QR8 ...      LDX   0  CPRW2(2) 
   8 3QR_ ...      SRL   0  12   
   8 3QSQ ...      SBN   0  #0300
   9 3QTH ...      BNZ   0  READOK   
  10 3QW# ...      HUNTW    3,IPCD,ATASK 
   9 3QX5 ...      TRACE    3,IPCRDNI
   9 3QXW ...      BPZ   3  NEXTASK  
   9 3QYM ...      STOZ     CPRW2(2) 
  11 3Q_D ...      SETNCORE 2,2,IPCD,ATASK   
  14 3Q_Y ...      NGN   0  1         [LINK NUMBER = 4095
  16 3R2D ...      STO   0  A1(2)         [USED FOR DUMMY WRITE  
   7 3R32 ...      LDN   0  3
   9 3R3R ...      STO   0  A1+1(2)  
   9 3R4J ...      BRN      NEXTASK  
   5 3R5* ...READOK  
   8 3R5H ...      FSHCODE  AORB 
  15 3R5P ...      TRACEIF  K6IPCONA,199,299,IPWRB(2),IPCONA1
   7 3R5X ...      FSHSKIP   
  11 3R66          TRACE    IPWRB(2),IPCONA1 
  15 3RKQ          HUNTW    3,IPCD,ATASK        [ANY TASKS ? 
   9 3S5B          BPZ   3  NEXTASK  
  17 3SK2          SIPCM    Q                   [SET QUIESCENT MARKER
  20 3T4L    #     ENTRY POINT WHEN CONTENTION LOST ATTEMPTING AN OUTPUT SEQUENCE
   5 3TJ=    X2IPCONA
   8 3TN7 ...      FSHCODE  AORB 
  15 3TS4 ...      TRACEIF  K6IPCONA,199,299,IPWRB(2),IPCONA2
   7 3TX_ ...      FSHSKIP   
  11 3W3W          TRACE    IPWRB(2),IPCONA2 
   8 3WHG    #     PUT UP READ   
   8 3X36    NPUT  LDN   0  #12  
  20 3XGQ          DSCA     ,DLA,0,MODE         [STORE READ MODE IN CONTROL AREA 
   8 3Y2B          LDN   4  11   
  10 3YG2          DSCA     ,DLA,4,COUNT 
   9 3Y_L          LDN   0  IPREB(2) 
  17 3_F=          DSCA     ,STO,0,CTSA         [STORE CORE ADDRESS  
  15 3_YW          GPERIIPC ,2,SP               [ISSUE PERI  
  16 42DG ...      TIPCREP  SPQ,TIME,RESETA,NOP,,NEXTASK,,,TOPX1 
   8 42Y6    #     INPUT RECEIVED
   4 43CQ    #   
   4 43XB    #   
   8 44C2    #     VALIDATE INPUT
   4 44WL    #   
   8 45B=    #     1 - CHECKSUM  
   5 45L4 ...NPUTOK  
   9 45TW          LDX   0  CPRW2(2) 
  16 46*G          ANDN  0  #777                [REMAINDER COUNT 
  17 46T6          SBX   4  0                   [COUNT TRANSFERRED   
   9 47#Q          LDN   7  IPREB(2) 
   7 47SB          SMO      4
  16 48#2          SUM   6  0                   [CALCULATE SUM   
  15 48RL          BNZ   6  WHOOPS              [J IF WRONG! 
  10 49?=    #     2 - COUNT CORRECT ?   
  19 49QW          LDCH  0  IPREB(2)            [COUNT FROM MESSAGE HEADER   
  15 4==G          BXU   0  4,WHOOPS            [J IF WRONG! 
  11 4=Q6    #     3 - VALID MESSAGE CODE ?  
   9 4?9Q          LDX   0  IPREB(2) 
  15 4?PB          ANDN  0  #77                 [MESSAGE CODE
   8 4#92          LDN   6  13   
   7 4#NL    PCHK1 SMO      6
  11 4*8=          LDEX  5  TMESSCODES-1(1)  
  15 4*MW          BXE   0  5,NSWIT             [J IF VALID  
  16 4B7G          BCT   6  PCHK1               [TRY NEXT,IF ANY 
  15 4BM6          BRN      WHOOPS              [NOT VALID   
   4 4C6Q    #   
  10 4CLB    #     SWITCH ON MESSAGE CODE
   9 4D62    NSWIT JIPCMN   R,NSWIT2 
   7 4DKL          CIPCM    R
   8 4F5=          SBN   6  12   
   9 4FJW          BZE   6  NSWIT3   
   7 4G4G          SBN   6  1
   9 4GJ6          BZE   6  NSWIT4   
   8 4H3Q          ADN   6  13   
   5 4HHB    NSWIT2  
   7 4J32          SMO      6
   5 4JGL    NSWIT1  
   9 4K2=          BRN      NSWIT1   
   9 4KFW          BRN      TRANREQ  
   9 4K_G          BRN      TRANREQ  
   9 4LF6          BRN      TRANREQ  
   9 4LYQ          BRN      TRANREQ  
   9 4MDB          BRN      WHOOPS   
   9 4MY2          BRN      WHOOPS   
   9 4NCL          BRN      OPENREQ  
   9 4NX=          BRN      OPENREQ  
   9 4PBW          BRN      WHOOPS   
   9 4PWG          BRN      PCLOSREQ 
   9 4QB6          BRN      WHOOPS   
   9 4QTQ          BRN      WHOOPS   
   9 4R*B          BRN      WHOOPS   
   5 4RT2    NSWIT3  
   9 4S#L          ACROSS   IPCONE,9 
   5 4SS=    NSWIT4  
   9 4T?W          BRN      NEXTASK  
  17 4TRG    #     THIS SECTION DEALS WITH AN OPEN REQUEST MESSAGE   
   4 4W?6    #   
  14 4WQQ    #     SEARCH LINK STACK FOR SUITABLE MATE   
   5 4X=B    OPENREQ 
   8 4XB? ...      FSHCODE  AORB 
  16 4XG8 ...      TRACEIF  K6IPCONA,199,299,IPREB+1(2),IPCONA3  
   7 4XL5 ...      FSHSKIP   
  12 4XQ2          TRACE    IPREB+1(2),IPCONA3   
   9 4Y9L          LDX   0  IPREB(2) 
   8 4YP=          ANDN  0  #1000
  17 4_8W          BNZ   0  OGR                 [OPEN FOR GEORGE LINK
  17 4_NG          LDX   3  IPCPTR(2)           [FIND IPC ACTIVITY   
  16 5286          HUNT2    3,IPCD,IPSTACK      [FIND LINK STACK 
  10 52MQ          LDX   6  ARINGNO(3)   
  16 537B          ANDN  6  #7777               [NR OF ENTRIES   
  13 53M2          BZE   6  NOLINK              [NONE
  11 546L    OPR3  LDX   0  A1+ASTATUS2(3)   
   9 54L=          ANDX  0  TBIT1(1) 
  17 555W          BZE   0  OPR1                [ALREADY ESTABLISHED 
  21 55KG ...      LDCT  0  #150   [J IF JUST DISEST,DISEST REP GIVEN,OR OPEN IN PROG
  11 5656          ANDX  0  A1+ASTATUS2(3)   
   8 56JQ          BNZ   0  OPR1 
   8 574B          LDCT  0  #200 
  11 57J2          ANDX  0  A1+ASTATUS1(3)   
   8 583L          BNZ   0  OPR1 
  10 588S ...      LDN   0  A1+FPTR(3)   
  11 58B2 ...      BXE   0  A1+FPTR(3),OPR1  
  10 58H=          LDX   0  A1+BPTR(3)   
  16 592W          BZE   0  OPR1                [NOT YET ENRINGED
  10 59GG          LDX   1  A1+FPTR(3)   
  12 5=26          LDX   0  ALINDIR-ARINGEL(1)   
   7 5=FQ          SRL   0  8
   9 5=_B          ERX   0  IPREB(2) 
   7 5?F2          ANDN  0  1
  16 5?YL          BZE   0  OPR1                [SAME DIRECTION  
  15 5#D=          LDX   0  AREQPR-ARINGEL(1)   [PROPERTIES  
  14 5#XW          BZE   0  OPR5                [NO REQS 
   8 5*CG          SRL   0  11   
   7 5*X6          ANDN  0  3
  14 5BBQ          BZE   0  OPR1                [NOT IMC 
   4 5BWB    OPR5
  15 5CB2          LDX   0  ALINAME-ARINGEL(1)  [CHECK NAMES 
  11 5CTL          BXU   0  IPREB+2(2),OPR1  
  12 5D*=          LDX   0  ALINAME-ARINGEL+1(1) 
  11 5DSW          BXU   0  IPREB+3(2),OPR1  
  12 5F#G          LDX   0  ALINAME-ARINGEL+2(1) 
  11 5FS6          BXU   0  IPREB+4(2),OPR1  
  10 5G?Q          LDX   0  A1+ALINUM(3) 
   9 5GRB          STO   0  AWORK1(2)
   9 5H?2          SBN   1  ARINGEL  
  12 5HQL          HUNT2OPT 3,APERI,APROPNOS,1   
   8 5J==          BPZ   3  OPR6 
   9 5JPW          ADN   1  ARINGEL  
   9 5K9G          LDX   3  FPTR(1)  
   8 5KP6          SBN   3  A1   
   8 5L8Q          BRN      OPR2 
  10 5LNB    OPR6  LDX   7  ALOGLEN(3)   
  12 5M82          SETUPCORE  7,5,APERI,APROPNOS 
   9 5MML          LDX   0  AWORK1(2)
  10 5N7=          STO   0  ACOMMUNE1(2) 
   8 5NLW          CALL  7  SREL 
   9 5P6G          BNG   3  OPENREQ  
   9 5PL6          LDX   1  FPTR(3)  
   9 5Q5Q          SBN   1  ARINGEL  
  12 5QKB          HUNT2OPT 3,APERI,APROPNOS,1   
  10 5R52          LDX   7  ALOGLEN(3)   
   8 5RJL          LDN   4  A1(3)
   7 5S4=          SMO      5
   8 5SHW          LDN   5  A1   
   7 5T3G          SMO      7
   7 5TH6          MOVE  4  0
   7 5W2Q          CHECKATT  
  11 5WGB          VFREE    APERI,APROPNOS   
   9 5X22          LDX   0  AWORK1(2)
  10 5XFL          STO   0  ACOMMUNE1(2) 
   8 5X_=          CALL  7  SREL 
   9 5YDW          BNG   3  OPENREQ  
   8 5YYG          SBN   3  A1   
  10 5_D6          LDX   1  A1+FPTR(3)   
  10 5_XQ          TESTREPNOT   OK,OPR1  
   8 62CB          BRN      OPR2 
   9 62X2    OPR1  ADN   3  ARECLEN  
   8 63BL          LDX   1  FX1  
  14 63W=          BCT   6  OPR3                [TRY NEXT
  15 64*W    #     NO SUITABLE MATE - SEND 'NO LINK' REPLY   
   5 64TG    NOLINK  
   8 65*6          LDX   1  FX1  
  11 65SQ          LDX   0  TMESSCODES+9(1)  
  14 66#B          STO   0  IPWRB(2)            [HEADER  
  10 66S2          LDX   0  IPREB+1(2)   
   8 67?L          SLL   0  12   
  16 67R=          STO   0  IPWRB+1(2)          [LINK IDENTIFIER 
   9 68=W          LDN   7  IPWRB(2) 
   7 68QG          SUM   6  2
  14 69=6          NGS   6  IPWRB+2(2)          [CHECKSUM
   7 69PQ          LDN   6  3
  17 6=9B          BRN      OPR4                [GO TO TRANSMIT REPLY
  16 6=P2    #     SUITABLE LINK - SEND 'LINK ESTABLISHED' REPLY 
   8 6?8L    OPR2  SMO      FX1  
  10 6?N=          LDX   0  TMESSCODES+8 
  14 6#7W          STO   0  IPWRB(2)            [HEADER  
  10 6#MG          LDX   0  IPREB+1(2)   
   8 6*76          ANDN  0  #7777
   8 6*LQ          SRC   0  12   
  10 6B6B          ORX   0  A1+ALINUM(3) 
  10 6BL2          STO   0  IPWRB+1(2)   
   9 6C5L          SBN   1  ARINGEL  
  10 6CK=          HUNT2B   1,AOBRES,0   
   9 6D4W          LDX   0  A1+1(1)  
  15 6DJG          STO   0  IPWRB+2(2)          [PROGRAM NAME
   9 6F46          LDN   7  IPWRB(2) 
   7 6FHQ          SUM   6  3
  17 6G3B          NGS   6  IPWRB+3(2)          [NEGATIVE CHECKSUM   
   7 6GH2          LDN   6  4
  17 6H2L    OPR4  LDN   0  #11                 [SET UP CONTROL AREA 
  15 6HG=          DCA      ,DLA,0,MODE         [STORE MODE  
  15 6H_W          DCA      ,DLA,6,COUNT        [STORE COUNT 
   9 6JFG          LDN   0  IPWRB(2) 
  17 6J_6          DCA      ,STO,0,CTSA         [STORE CORE ADDRESS  
   8 6KDQ          GPERIIPC ,2,CT
  14 6KYB ...      TIPCREP  CT,TIME,RESETA,NOPS,WHOOPS   
  13 6LD2    #     REPLY SENT - UPDATE RING ELEMENT  
   9 6LXL          LDX   0  IPWRB(2) 
   7 6MC=          ANDN  0  1
  16 6MWW          BNZ   0  NEXTASK             [SAID 'NO LINK'  
  10 6NBG          LDX   0  IPWRB+1(2)   
   8 6NW6          ANDN  0  #7777
  10 6P*Q          STO   0  ACOMMUNE1(2) 
  17 6PTB          CALL  7  SREL                [FIND RING ELEMENT   
  16 6Q*2          BNG   3  TOOLATE             [GONE - TOO LATE 
   9 6QSL          LDX   0  IPREB(2) 
   8 6R#=          ANDN  0  #1000
   8 6RRW          BNZ   0  OGR5 
  10 6S?G          LDX   0  ASTATUS2(3)  
   9 6SR6          ANDX  0  TBIT1(1) 
  17 6T=Q          BZE   0  TOOLATE             [ESTABLISHED ALREADY 
  16 6TQB          ERS   0  ASTATUS2(3)         [MARK ESTABLISHED
   8 6W=2          LDCT  0  #20  
  10 6WPL          ORS   0  ASTATUS2(3)  
  10 6X9=          ERS   0  ASTATUS2(3)  
  10 6XNW          LDX   0  ASTATUS2(3)  
   9 6Y8G          ANDX  0  TBIT5(1) 
   8 6YN6          BZE   0  PINC 
   9 6_7Q          LDX   2  ALINUM(3)
   8 6_MB          ANDN  2  #7777
   8 7272          STOPHANGER 2  
   7 72LL    PINC  INCLINK   
  10 736=          LDX   0  IPREB+5(2)   
  17 73KW          STO   0  ALTPROG(3)          [REMOTE PROGRAM NAME 
  10 745G          LDX   0  IPWRB+1(2)   
  16 74K6          STO   0  ALINUM(3)           [LINK IDENTIFIER 
   8 754Q          LGEOG    ,7   
   8 75JB          ANDN  7  #77  
   9 7642 ...      ORX   7  TBIT11(1)
   9 76HL          SMO      FPTR(3)  
  11 773=          STO   7  IPPROPS-ARINGEL  
   9 77GW          LDX   0  CPPTR(2) 
   9 782G          SMO      FPTR(3)  
  10 78G6          STO   0  CPPTR-ARINGEL
  10 78_Q          STO   2  APTRIPB(3)   
  10 79FB          LDX   0  ASTATUS1(3)  
  17 79_2          BPZ   0  NEXTASK             [NO TRANSFER STORED  
   9 7=DL          LDX   6  ALINUM(3)
  17 7=Y=          SETNCORE 2,3,IPCD,ATASK      [SET UP TRANSFER TASK
  16 7?CW          STO   6  A1(3)               [LINK IDENTIFIER 
   7 7?XG          LDN   0  2
  15 7#C6          STO   0  A1+1(3)             [TASK TYPE   
  17 7#WQ          BRN      NEXTASK             [J TO PERFORM TASK   
  18 7*BB    #     OPEN ANTICIPATED FROM ANOTHER SOURCE - SEND 'NO LINK' 
   5 7*W2    TOOLATE 
  17 7B*L          SETNCORE 2,3,IPCD,ATASK      [SET UP TASK BLOCK   
  10 7BT=          LDX   0  IPWRB+1(2)   
   8 7C#W          ANDN  0  #7777
   7 7CSG          LDN   0  7
  15 7D#6          STO   0  A1+1(3)             [TASK TYPE   
  17 7DRQ          BRN      NEXTASK             [J TO PERFORM TASK   
  14 7F?B    #              TIME-OUT WHEN SENDING REPLY  
   9 7FR2    TIME  ACROSS   IPCONE,5 
   5 7G=L    RESETA  
   9 7GQ=          ACROSS   IPCONE,3 
   4 7H9W    NOPS
   8 7HPG          SIPCM    SI   
   4 7J96    NOP 
   9 7JNQ          ACROSS   IPCONE,2 
  13 7K8B    #     DEAL WITH TRANSFER REQUEST MESSAGE
   4 7KN2    #   
   5 7L7L    TRANREQ 
   8 7L?H ...      FSHCODE  AORB 
  16 7LCD ...      TRACEIF  K6IPCONA,199,299,IPREB+1(2),IPCONA4  
   7 7LH* ...      FSHSKIP   
  12 7LM=          TRACE    IPREB+1(2),IPCONA4   
  10 7M6W          LDX   0  IPREB+1(2)   
   8 7MLG          SRC   0  12   
  10 7N66          STO   0  ACOMMUNE1(2) 
  15 7NKQ          CALL  7  SREL                [FIND RING EL
  15 7P5B          BNG   3  NOLINK              [NO SUCH LINK
   9 7PK2          LDX   0  BPTR(3)  
   9 7Q4L          BZE   0  NOLINK   
  11 7Q#D ...      BXE   3  FPTR(3),NOLINK   
   9 7QJ=          LDX   0  IPREB(2) 
   8 7QS4 ...      ANDN  0  #77  
   8 7R3W          SBN   0  #42  
   9 7RHG          BPZ   0  TRANR3   
   7 7S36          LDN   0  1
  10 7SGQ          ORS   0  ASTATUS2(3)  
   5 7T2B    TRANR3  
   9 7TG2          LDX   0  IPREB(2) 
  16 7T_L          ANDN  0  1                   [0=READ,1=WRITE  
   8 7WF=          LDCT  1  #200 
  10 7WYW          ANDX  1  ASTATUS1(3)  
   8 7XDG          BZE   1  TR4A 
  10 7XY6          LDX   1  ALTPROG(3)   
   8 7YCQ          BRN      TR4B 
   9 7YXB    TR4A  SMO      FPTR(3)  
  11 7_C2          LDX   1  ALINDIR-ARINGEL  
  16 7_WL          SRL   1  8                   [0=READ,1=WRITE  
  13 82B=    TR4B  BXE   0  1,WHOOPS            [EH? 
   8 82TW          LDX   1  FX1  
  10 83*G          LDX   0  ASTATUS1(3)  
   7 83T6          ANDN  0  1
  16 84#Q          BZE   0  PNOTR               [J IF NO TRANSFER
   8 84SB          LDX   0  GSIGN
  15 85#2          ORS   0  ASTATUS1(3)         [SET T.I.P.  
   8 85RL          LDCT  0  #200 
  10 86?=          ANDX  0  ASTATUS1(3)  
   8 86QW          BNZ   0  TGL  
  12 87=G    #     SEND 'TRANSFER ACCEPTED' REPLY
  10 87Q6          LDX   0  IPREB+1(2)   
   8 889Q          SRC   0  12   
  16 88PB          STO   0  IPWRB+1(2)          [LINK IDENTIFIER 
  11 8992          LDX   0  TMESSCODES+4(1)  
  14 89NL          STO   0  IPWRB(2)            [HEADER  
   9 8=8=          LDX   0  IPREB(2) 
   7 8=MW          ANDN  0  1
  17 8?7G          BZE   0  TREAD               [OTHER END READING   
   9 8?M6          LDX   0  ALENTR(3)
  10 8#6Q          SBX   0  IPREB+2(2)   
  16 8#LB          STO   0  IPWRB+2(2)          [DIFFERENCE COUNT
  17 8*62          BRN      TRANR1              [J TO TRANSMIT REPLY 
  10 8*KL    TREAD LDX   0  IPREB+2(2)   
   9 8B5=          SBX   0  ALENTR(3)
  16 8BJW          STO   0  IPWRB+2(2)          [DIFFERENCE COUNT
   5 8C4G    TRANR1  
   9 8CJ6          STO   0  AWORK3(2)
   9 8D3Q          LDN   7  IPWRB(2) 
   7 8DHB          SUM   6  3
  17 8F32          NGS   6  IPWRB+3(2)          [STORE NEG CHECKSUM  
   7 8FGL          LDN   4  4
  13 8G2=    #     SET UP CONTROL AREA & ISSUE PERI  
   5 8GFW    TRANR2  
   8 8G_G          LDN   0  #11  
  10 8HF6          DCA      ,DLA,0,MODE  
  10 8HYQ          DCA      ,DLA,4,COUNT 
   9 8JDB          LDN   0  IPWRB(2) 
  10 8JY2          DCA      ,STO,0,CTSA  
   8 8KCL          GPERIIPC ,2,CT
  14 8KX=          TIPCREP  CT,TIME,RESETA,NOPS,WHOOPS   
   9 8LBW          LDX   0  IPWRB(2) 
   7 8LWG          ANDN  0  1
  17 8MB6          BNZ   0  NEXTASK             [J IF ^NO TRANSFER^  
  17 8MTQ          SETNCORE 2,3,IPCD,ATASK      [SET UP TRANSFER TASK
  10 8N*B          LDX   0  IPWRB+1(2)   
   8 8NT2          STO   0  A1(3)
   7 8P#L          LDN   0  2
   9 8PS=          STO   0  A1+1(3)  
   9 8Q?W          ACROSS   IPCON,3  
  12 8QRG    #     DEAL WITH NO TRANSFER REPLY   
  11 8R?6    PNOTR LDX   0  TMESSCODES+5(1)  
  14 8RQQ          STO   0  IPWRB(2)            [HEADER  
  10 8S=B          LDX   4  IPREB+1(2)   
   8 8SQ2          SRC   4  12   
  16 8T9L          STO   4  IPWRB+1(2)          [LINK IDENTIFIER 
   7 8TP=          ADX   4  0
  14 8W8W          NGS   4  IPWRB+2(2)          [CHECKSUM
   7 8WNG          LDN   4  3
   9 8X86          BRN      TRANR2   
  15 8XMQ    #     THIS SECTION DEALS WITH CLOSE LINK MESSAGE
   4 8Y7B    #   
   5 8YM2    PCLOSREQ
   9 8_6L          ACROSS   IPCONC,2 
  11 8_L=    NEXTASK        ACROSS   IPCON,1 
  10 925W    WHOOPS      ACROSS  IPCONE,1
  13 92KG    #     OPEN REQUESTED FOR IPC GEORGE LINK
   4 9356    OGR 
  17 93JQ          LDX   3  IPCPTR(2)           [FIND IPC ACTIVITY   
  16 944B          HUNT2    3,IPCD,IPSTACK      [FIND STACK BLOCK
  14 94J2    #     SCAN STACK BLOCK FOR SUITABLE MATCH   
  10 953L          LDX   6  ARINGNO(3)   
  16 95H=          ANDN  6  #7777               [COUNT OF ENTRIES
  15 962W          BZE   6  NOLINK              [NONE AT ALL 
  10 96GG    OGR4  LDX   0  A1+BPTR(3)   
  15 9726          BZE   0  OGR1                [NOT ENRINGED
   8 97FQ          LDCT  0  #200 
  11 97_B          ANDX  0  A1+ASTATUS1(3)   
  15 98F2          BZE   0  OGR1                [OBJ ROG LINK
  11 98YL          LDX   0  A1+ASTATUS2(3)   
   9 99D=          ANDX  0  TBIT1(1) 
  17 99XW          BZE   0  OGR1                [ESTABLISHED ALREADY 
  11 9=CG          LDX   0  A1+ASTATUS1(3)   
   9 9=X6          ANDX  0  TBIT2(1) 
  17 9?BQ          BNZ   0  OGR1                [NOT YET ESTABLISHED 
   8 9?WB          LDCT  0  #140 
  11 9#B2          ANDX  0  A1+ASTATUS2(3)   
  19 9#TL          BNZ   0  OGR1                [A/R OR JUST DISESTABLISHED  
  10 9**=          LDX   0  A1+ALTPROG(3)
   9 9*SW          ERX   0  IPREB(2) 
   7 9B#G          ANDN  0  1
  16 9BS6          BZE   0  OGR1                [SAME DIRECTION  
  12 9C?Q          BXU   2  A1+APTRIPB(3),OGR1   
  10 9CRB          LDX   1  A1+FPTR(3)   
  10 9D?2    #     CHECK IF NAMES AGREE  
  10 9DQL          LDX   0  IPCGNAM-A1(1)
  11 9F==          BXU   0  IPREB+2(2),OGR1  
  11 9FPW          LDX   0  IPCGNAM-A1+1(1)  
  11 9G9G          BXU   0  IPREB+3(2),OGR1  
  11 9GP6          LDX   0  IPCGNAM-A1+2(1)  
  11 9H8Q          BXU   0  IPREB+4(2),OGR1  
  10 9HNB    #     CHECK IF DETAILS AGREE
   9 9J82          LDCH  4  IPREB(2) 
   7 9JML          SBN   4  7
  15 9K7=          BZE   4  OGR2                [NO DETAILS  
   7 9KLW    OGR3  SMO      4
  10 9L6G          LDX   0  IPCGDET-A1(1)
   7 9LL6          SMO      4
  10 9M5Q          TXU   0  IPREB+6(2)   
  16 9MKB          BCS      OGR1                [DETAILS DIFFER  
  17 9N52          BCT   4  OGR3                [NEXT WD OF DETAILS  
  17 9NJL    #     LINK ACCEPTABLE - SEND ^LINK ESTABLISHED^MESSAGE  
   4 9P4=    OGR2
   8 9PHW          LDX   1  FX1  
  11 9Q3G          LDX   0  TMESSCODES+8(1)  
  14 9QH6          STO   0  IPWRB(2)            [HEADER  
  10 9R2Q          LDX   0  IPREB+1(2)   
   8 9RGB          SLL   0  12   
  10 9S22          ORX   0  A1+ALINUM(3) 
  16 9SFL          STO   0  IPWRB+1(2)          [LINK IDENTIFIER 
  17 9S_=          STOZ     IPWRB+2(2)          [NULL ^PROGRAM NAME^ 
   9 9TDW          LDN   7  IPWRB(2) 
   7 9TYG          SUM   6  3
  10 9WD6          NGS   6  IPWRB+3(2)   
   7 9WXQ          LDN   6  4
   8 9XCB          BRN      OPR4 
  12 9XX2    #     TRY NEXT,IF NONE SEND NOLINK  
   9 9YBL    OGR1  ADN   3  ARECLEN  
   8 9YW=          BCT   6  OGR4 
   9 9_*W          BRN      NOLINK   
   9 9_TG          BRN      NOLINK   
  13 =2*6    #     REPLY SENT - MARK LINK ESTABLISHED
  10 =2SQ    OGR5  STOZ     ASTATUS2(3)  
  10 =3#B          LDX   0  IPWRB+1(2)   
  16 =3S2          STO   0  ALINUM(3)           [MARK ESTABLISHED
   7 =4?L          INCLINK   
  14 =4R=          BRN      NEXTASK             [FINISHED
  12 =5=W    #     TRANSFERS FOR GEORGE IPC LINKS
   4 =5QG    #   
   4 =6=6    TGL 
   9 =6PQ          ACROSS   IPCONG,2 
   4 =79B    #   
   4 =7P2    #   
  10 =88L          MENDAREA 100,K9IPCONA 
   4 =8N=    #   
   4 =97W    #   
   4 =9MG    #END
   8 ____ ...32503013000600000000
