  13 22FL ...      SEG      LOADERR,70,RUTH PORTER   
  11 22_=          SEGENTRY K1LOADERR,XENT1  
  11 23DW          SEGENTRY K2LOADERR,XENT2  
  11 23YG          SEGENTRY K3LOADERR,XENT3  
  16 24D6    #DEF  T=1                          [INCLUDE TRACING 
   8 24XQ    #DEF  XLDEX=EXTPS+3 
   8 25CB    #DEF  XLDEXA=EXTPS+4
   8 25X2    #DEF  YCOUNT=EXTPS  
   4 26BL    #   
  18 26W=    MASK1          #00060000           [LOADNOW,LOADNOWX BITS   
   9 27*W    PTR   PAIR     TROBJ,1  
   9 27TG    PCR   PAIR     CROBJ,1  
   9 28*6    PMT   PAIR     MTOBJ,1  
  10 28SQ    # LABELS OF ERROR MESSAGES  
   9 29#B    MPROG          +EMPROG  
   5 29S2    XENT2   
  16 2=?L          LDN   7  0                    [LOAD FILE OPEN 
  18 2=R=          LDN   6  0                   [NO SUPPLEMENTARY MESSAGE
   5 2?=W    XENT1   
   5 2?QG    #SKI  T 
   4 2#=6    (   
  10 2#PQ          TRACE    6,SUPPMESS   
   9 2*9B          TRACE    7,SP MARK
   4 2*P2    )   
   8 2B8L         LDCT   0  #200 
   9 2BN=          ANDX  0  XLDEXA(2)
  16 2C7W          BNZ   0  RCONT               [CONT EXTRACODE  
  16 2CMG    # B22 OF X7 SET MEANS CORE IMAGE NOT TO BE DELETED -
  11 2D76    # NOT SET UP OR ERROR IN RESUME 
  11 2DLQ    # ONLY USED IN CASES OF DOUBT   
   7 2F6B          LDN   0  2
   7 2FL2          ANDX  0  7
   8 2G5L          BNZ   0  NOCI 
  17 2GK=          LDN   0  #1000               [CORE IMAGE CONTEXT  
  10 2H4W          ANDX  0  CONTEXT(2)   
   8 2HJG          BZE   0  NOCI 
   5 2J46    #SKIP G4
   4 2JHQ    (   
  21 2K3B          FPCACA   3,2                 [CLEAR ANY LOCKDOWN COUNTS LEFT SET  
   9 2KH2          CLEAR    3,0,,3,2 
  21 2L2L          CALL  4  Z4302               [DEAL WITH SUD MARKED BEING LOADED   
   7 2LG=          BRN      /
   4 2L_W    )   
  10 2MFG          DOWN     PROSTART,16  
   4 2M_6    NOCI
   5 2NDQ    #SKIP G4
   4 2NYB    (   
   4 2PD2    #   
  18 2PXL    #     IF THERE IS NO CORE IMAGE WE MAY STILL HAVE TO FREE   
  14 2QC=    #     SWAPPING SPACE OR REDUCE USER COUNTS  
   4 2QWW    #   
   8 2RBG          CALL  4  Z4300
  20 2RW6          BRN      (GEOERR)            [ERROR IF REPLACEMENTS NON ZERO  
  14 2S*Q          BNG   3  NSWAPSP             [NO SCB  
  20 2STB          LDX   4  AMAXSIZE(3)         [AMOUNT OF SWAPPING SPACE BOOKED 
  14 2T*2          ANDX  4  BSP16               [FOR PROG
  14 2TSL          FREECORE 3                   [FREE SCB
   9 2W#=          BZE   4  NSWAPSP  
   7 2WRW          FREESS   4
   5 2X?G    NSWAPSP 
   4 2XR6    #   
   4 2Y=Q    )   
  12 2YQB    # X7 B23 = 1  -  FILE NOT OPENED XET
  21 2_=2    # FOR EMBEDDED LOAD IT IMPLIES TH_AT THE 1ST CARD HAS NOT BEEN READ AND 
  19 2_PL    # NOT BACKSPACE. THEREFORE B23 IS TESTED BEFORE EMBEEDDED LOAD  
   7 329=          ANDN  7  1
  17 32NW          BNZ   7  NSAV                [LOAD FILE NOT OPEN  
   8 338G          LDCT  0  #40  
   9 33N6          ANDX  0  XLDEXA(2)
  16 347Q          BNZ   0  SAVEF               [EMBEDDED LOAD   
   5 34MB    #SKIP G4
   4 3572    (   
  20 35LL          LDN   0  #2000               [IF SUBSEQ LOAD OF PURE PROCEDURE
  19 366=          ANDX  0  XLDEXA(2)           [THERE IS NO FILE TO CLOSE   
   8 36KW          BNZ   0  NSAV 
   4 375G    )   
  15 37K6          CLOSE                        [LOAD FILE   
   4 384Q    NSAV
   5 38JB    #SKIP G4
   7 3942          CHERRMESS 
  18 39HL          BZE   5  NDCOM               [ERROR ALREADY REPORTED  
  18 3=3=          BNZ   6  TWOERRS             [SUPP. MESSAGE AS WELL   
  17 3=GW          ERRORX   5                   [OUTPUT ERROR MESSAGE
   8 3?2G          BRN      NDCOM
   5 3?G6    TWOERRS 
  17 3?_Q          ERRORX   5,6                 [OUTPUT ERROR MESSAGE
   5 3#FB    NDCOM   
   7 3#H5 ...      LDCT  0  4
   9 3#JS ...      ANDX  0  XLDEXA(2)
   9 3#LH ...      BZE   0  NDCOM2   
  14 3#N= ...      HLSINFORM    NDCOM1,NOTLOAD,JOBNO(2)  
   9 3#P_ ...      BRN      NDCOM2   
   5 3#RN ...NDCOM1  
   9 3#TC ...      BRN      (GEOERR) 
   5 3#X6 ...NDCOM2  
   9 3#_2          LDX   0  MASK1(1) 
   9 3*DL          ANDX  0  XLDEXA(2)
  18 3*Y=          BNZ   0  UP2LOERR            [JUMP IF LOADNOW(X) MACRO
   6 3BCW          ENDCOM
   5 3BXG    UP2LOERR
   8 3CC6          ANDX  0  BIT9 
  20 3CWQ          BZE   0  NOCUNI              [JUMP IF NO CPB/CUNI WITH FILE   
  19 3DBB                                       [NAME WHICH MUST BE ERASED   
  16 3DW2          MFREE    CPB,CUNI            [FREE CPB,CUNI   
   5 3F*L    NOCUNI  
  17 3FT=          UPPLUS   1                   [UP TO ERROR LABEL   
   5 3G#W    SAVEF   
   9 3GSG          LDX   4  YCOUNT(2)
   7 3H#6          SBN   4  1
  19 3HRQ          BNG   4  NSTART              [ERROR WHILE READING COMMAND 
   8 3J?B          ADDRECS  3,4  
   7 3JR2          BACKSPACE 
   5 3K=L    NSTART  
  20 3KQ=          BNZ   6  NSAV                    [SUBSID MESS ALREADYSET UP   
  19 3L9W          LDX   6  MPROG(1)             [EMBEDDEDPROGRAM SUB/MESSAGE
   8 3LPG          BRN      NSAV 
   5 3M96    RCONT   
  17 3MNQ    # MAKE PROG DORMANT HERE WHEN ILLEGAL ALLOWED FROM PCA  
   9 3N8B          LDXC  0  XLDEXA(2)
  15 3NN2          BCS      NDILL               [ONLINE CONT 
  15 3P7L          FINDACTNO 3,XLDEX(2)         [FIND OLPA   
  13 3PM=          LOCK     3                   [OLPA
  17 3Q6W          CHANGEFILE 3                 [MOVE FILE TO OLPA   
   7 3QLG          UNLOCK   3
  13 3R66    #     PICK UP PERIPHERAL TYPE FROM OLPA 
   9 3RKQ          LDX   4  FTYPNO(3)
   8 3S5B          SRL   4  15   
  14 3SK2          BNZ   4  NTR                 [NOT TR  
   8 3T4L          ADN   1  PTR  
   8 3TJ=          BRN      ZLINK
   4 3W3W    NTR 
   7 3WHG          SBN   4  3
  14 3X36          BNZ   4  NCR                 [NOT CR  
   8 3XGQ          ADN   1  PCR  
   8 3Y2B          BRN      ZLINK
   4 3YG2    NCR 
  15 3Y_L          ADN   1  PMT                 [MUST BE MT  
   5 3_F=    ZLINK   
   9 3_YW          LINKSET  3,0(1)   
   5 42DG    NDILL   
  12 42Y6    # BRK/IN HAS BEEN INHIBITED FOR CONT
   7 43CQ          PERMITBRK 
   8 43XB          FPCACA   2,FX2
  14 44C2          CHANGEACT 2                  [TO PCA  
   8 44WL          ILLEGAL  CONT 
   4 45B=    #   
  15 45TW    #     ENTRY POINT TO TIDY UP & REPORT BREAKIN   
   4 46*G    #   
   5 46BG ...XENT3   
   4 46CG ...[   
  18 46DG ...[       THIS IS A CORRECTION FOR B 1774 (RA10)  AND CONCERNS
  16 46FG ...[         BREAK-IN DURING LOAD OF A PURE-PROCEDURE. 
  18 46GG ...[       IT  IS REQUIRED FOR LOADERR TO TIDY UP SO THAT IF WE
  17 46HG ...[        CONTINUE(CU) OUR PP LOAD LOAD DOESNOT GO WRONG 
  13 46JG ...[       DONE    12.FEB.75          :CENT
   4 46KG ...[   
   6 46LG ...#SKI   G4   
   4 46LP ...(   
   4 46LY ...[   
  14 46M7 ...[    CURES B2208 - IE  NO FPCACA IF NO PCA !
  12 46MB ...[        :CENT       11TH  MARCH  75
   4 46MK ...[   
   7 46MS ...      LDN   0  2
  20 46N3 ...      ANDX  0  7                   [LDSTART SETS B22 OF X7 IF NO PCA
   8 46N= ...      BNZ   0  NPCA 
  15 46NG ...      FPCACA   3,2                 [FIND  PCA   
   9 46PG ...      LDN   5  AWORK1(2)
   9 46QG ...      LDN   6  AWORK1(3)
  19 46RG ...      MOVE  5  4                    [AWORK WDS FROM CPA TO PCA  
   9 46SG ...      LDN   5  YCOUNT(2)
  10 46TG ...      LDN   6  ACOMMUNE1(3) 
  20 46WG ...      MOVE  5  5                   [NOW THE WDS USED BY LOAD IN CPA 
  21 46XG ...[                                         BACK TO ACOMMUNE WDS IN PCA   
   4 46YG ...[   
  20 46_G ...[    NOW WE MUST CLEAR THE LOAD OR LOADNOW MARKERS ( BIT 9 OR 10)   
  19 472G ...[      WHICH WAS HELD IN XLDEXA IN CPA NOW AN ACCOMMUNE? IN PCA 
   4 473G ...[   
   8 474G ...      LDX   0  BIT9 
   8 475G ...      ORX   0  BIT10
  15 476G ...      ORS   0  ACOMMUNE1 + XLDEXA - YCOUNT(3)   
  15 4777 ...      ERS   0  ACOMMUNE1 + XLDEXA - YCOUNT(3)   
   4 477S ...NPCA
   4 478G ...[   
  10 479G ...[        END  OF  CORRECTION
   4 47=G ...[   
   4 47?G ...)   
   7 47#Q          LDN   6  0
   8 47SB          LDX   0  BIT9 
   9 48#2          ANDX  0  XLDEXA(2)
  18 48RL          BNZ   0  XMACRO              [JUMP IF LOADNOW MACRO   
  20 49?=          LDN   0  2                   [AS IN OTHER ENTRY PTS B22 OF X7 
  19 49QW          ANDX  0  7                   [MEANS THERE IS NO CORE IMAGE
   8 4==G          BNZ   0  NOCIB
   8 4=Q6          LDN   0  #1000
  18 4?9Q          ANDX  0  CONTEXT(2)          [J UNLESS'CORE IMAGE'CON 
   8 4?PB          BZE   0  NOCIB
   5 4#92    #SKIP G4
   4 4#NL    (   
   8 4*8=          FPCACA   3,2  
   9 4*MW          CLEAR    3,0,,3,2 
  21 4B7G          CALL  4  Z4302               [DEAL WITH SUD MARKED BEING LOADED   
   7 4BM6          BRN      /
   4 4C6Q    )   
  10 4CLB          DOWN     PROSTART,16  
   5 4D62    NOCIB   
   5 4DKL    #SKIP G4
   4 4F5=    (   
   4 4FJW    #   
  20 4G4G    #     IF THERE IS NO CORE IMAGE WE MAY STILL HAVE TO FREE SWAPPING  
  12 4GJ6    #     SPACE OR REDUCE USER COUNTS   
   4 4H3Q    #   
   8 4HHB          CALL  4  Z4300
   9 4J32          BRN      (GEOERR) 
  15 4JGL          BNG   3  NSWAPSP1            [J IF NO SCB 
  20 4K2=          LDX   4  AMAXSIZE(3)         [SWAPPING SPACE BOOKED FOR PROG  
   8 4KFW          ANDX  4  BSP16
  14 4K_G          FREECORE 3                   [FREE SCB
   9 4LF6          BZE   4  NSWAPSP1 
  16 4LYQ          FREESS   4                   [FREE SWAP SPACE 
   5 4MDB    NSWAPSP1
   4 4MY2    )   
   4 4NCL    #   
  16 4NX=    #     B23 OF X7 INDICATES THAT THERE IS NO FILE OPEN
  21 4PBW    #     IF SET IN THE CASE OF EMBEDDED LOAD INDICATES NO BACKSPACING TO DO
   4 4PWG    #   
   7 4QB6          ANDN  7  1
   9 4QTQ          BNZ   7  NCLOSE   
  21 4R*B          LDCT  0  #40                 [IF EMBEDDED LOAD J TO DO BACKSPACING
   9 4RT2          ANDX  0  XLDEXA(2)
   8 4S#L          BNZ   0  NBKSP
   5 4SS=    #SKIP G4
   4 4T?W    (   
  21 4TRG          LDN   0  #2000               [J IF SUBSEQ LOAD OF PP-NO FILE OPEN 
   9 4W?6          ANDX  0  XLDEXA(2)
   9 4WQQ          BNZ   0  NCLOSE   
   4 4X=B    )   
  15 4XQ2          CLOSE                        [CLOSE FILE  
   5 4Y9L    NCLOSE  
   5 4YP=    TESTUP  
   7 4YQ_ ...      LDCT  0  4
   9 4YSN ...      ANDX  0  XLDEXA(2)
  20 4YWC ...      BZE   0  TESTUP2             [LOAD EVENT NOT REPORTED TO HLS  
  14 4YY6 ...      HLSINFORM    TESTUP1,NOTLOAD,JOBNO(2) 
   9 4Y_T ...      BRN      TESTUP2  
   5 4_3J ...TESTUP1 
   9 4_5? ...      BRN      (GEOERR) 
   5 4_72 ...TESTUP2 
   9 4_8W          LDX   0  MASK1(1) 
   9 4_NG          ANDX  0  XLDEXA(2)
  18 5286          BNZ   0  UP2LOBRKIN          [JUMP IF LOADNOW(X) MACRO
   7 52MQ          COMBRKIN  
   6 537B    UP2LOBRKIN  
  17 53M2          UP                           [UP TO BREAK-IN LABEL
   5 546L    XMACRO  
   5 54L=    #SKIP G3
   4 555W    (   
   8 55KG          FPCACA   3,2  
  18 5656          HUNT2J   3,BSTB,BSCB,,XNOSS  [JUMP IF NO SWAP SPACE   
  20 56JQ          LDX   4  BSHEET(3)           [TOTAL NO OF SHEETS FOR PROGRAM  
  18 574B          ADN   4  2                   [ALLOWANCE FOR THE HEADER
  21 57J2          SETUPCORE 4,3,FILE,FRB       [SET UP THE BACKING STORE LIST BLOCK 
   7 583L          FPCACA   2
  10 58H=          HUNT2    2,BSTB,BSCB  
  17 592W          STO   2  1                   [PRESERVE SCB POINTER
  20 59GG          STO   4  A1(3)               [INSERT THE LENGTH OF THE LIST   
   9 5=26          LDX   4  AOBJLF(2)
  19 5=FQ          STO   4  A1+1(3)             [STORE LFN IN BS LIST BLOCK  
  15 5=_B          LDX   5  BSHEET(2)           [NO OF SHEETS
  19 5?F2    STOW  LDX   4  AOBJST(2)           [THE FIRST O/P BLOCK ENTRY   
  17 5?YL          STO   4  A1+2(3)             [INSERT ENTRY IN LIST
  19 5#D=          ADN   2  1                   [STEP X2 TO NEXT O/P BS HOME 
  21 5#XW          ADN   3  1                   [STEP X3-POINTER TO NEXT FREE WORD   
  21 5*CG          BCT   5  STOW                [ARE ALL THE O/P BS HOMES INSERTED?  
  15 5*X6          FREECORE 1                   [RELEASE SCB 
   7 5BBQ          FREEALL   
   4 5BWB    )   
   5 5CB2    #SKIP G4
   4 5CTL    (   
   8 5D*=          FPCACA   3,2  
  10 5DSW          HUNT2    1,BSTB,BSCB,3
  10 5F#G          LDX   0  ALOGLEN(1)   
   9 5FS6          SBN   0  HSUD-A1  
  16 5G?Q          LDXC  7  AMAXSIZE(1)         [SIZE OF PROGRAM 
  10 5GRB          STOZ     AMAXSIZE(1)  
  19 5H?2          BZE   7  XNOSS               [JUMP IF NO SWAP SPACE BOOKED
  20 5HQL          BZE   0  NOSUD               [JUMP IF NOT ATTACHED TO A SUD   
   9 5J==          LDX   3  HSUD(1)  
   8 5JPW          BZE   3  NOSUD
   9 5K9G          LDX   0  HSUD+1(1)
  17 5KP6          ANDN  0  #7777               [NO OF SHARED PAGES  
   7 5L8Q          SBX   7  0
   9 5LNB          STOZ     HSUD+1(1)
  10 5M82          FSUD     2,3,(GEOERR) 
  20 5MML          UNSETBIT 1,HUSERS(2)         [CLEAR BIT SAYING SWAP SPACE GOT 
  19 5N7=          FON      HLOADPPWT           [LET SOMEONE ELSE LOAD IT.   
   5 5NLW    NOSUD   
  16 5P6G          FREESS   7                   [FREE SWAP SPACE 
   4 5PL6    )   
   5 5Q5Q    XNOSS   
   8 5QKB          FJOCA    3,2  
  10 5R52          TBS      5,JMISC(3),0 
  18 5RJL          BZE   0  NOPCACA             [JUMP IF PCACA BIT UNSET 
  16 5S4=          ERS   0  JMISC(3)            [CLEAR PCACA BIT 
   5 5SHW    NOPCACA 
  16 5T3G          SETBIT   14,JMISC(3)         [SET LOAD MARKER 
  10 5TH6          BRN      UP2LOBRKIN   
   4 5W2Q    #   
  20 5WGB    NBKSP LDX   4  YCOUNT(2)           [BACKSPACE BY NO OF RECORDS READ 
   9 5X22          BZE   4  NCLOSE   
   7 5XFL    NBK   BACKSPACE 
   8 5X_=          BCT   4  NBK  
   9 5YDW          BRN      NCLOSE   
   5 5YYG    #SKIP G4
   4 5_D6    (   
  20 5_XQ    #     THIS SUBR EXAMINES THE SCB & REDUCES THE USER COUNT ASSOC WITH
  21 62CB    #     A SUD WITH NO REPLACEMENTS RECORDED - PROGDEL DOES NOT CATER FOR  
  16 62X2    #     THIS.IT ALSO RETURNS THE ADDRESS OF THE SCB   
   4 63BL    #   
   8 63W=    Z4300 LDX   3  FX2  
   8 64*W          BRN      Z4302
   8 64TG    Z4301 FPCACA   3,FX2
  15 65*6    Z4302 HUNT2OPT 3,BSTB,BSCB         [LOCATE SCB  
  17 65SQ          BNG   3  Z4306               [J TO EXIT IF NONE   
  10 66#B          LDX   0  ALOGLEN(3)   
  20 66S2          SBN   0  HSUD-A1             [J IF PROG NOT ATTACHED TO SUD   
   8 67?L          BZE   0  Z4306
   9 67R=          LDX   0  HSUD(3)  
   8 68=W          BZE   0  Z4306
  20 68QG          LDX   2  HSUD+1(3)           [J IF REPLACEMENTS TO SUD-THIS   
  19 69=6          BNZ   2  Z4330               [IMPLIES SUD MARKED AS LOADED
  21 69PQ          STOZ     HSUD(3)             [SO THAT PROSTART WILL NOT SEE SUD   
  10 6=9B          FSUD     2,0,(GEOERR) 
  20 6=P2          LDX   0  HUSERS(2)           [REDUCE NO OF USERS OF SUD BY 1  
   7 6?8L          SBN   0  1
   9 6?N=          STO   0  HUSERS(2)
  16 6#7W          BPZ   0  Z4320               [J IF SUD LOADED 
   9 6#MG          ANDX  0  BITS22LS 
  17 6*76          BNZ   0  Z4310               [J IF OTHER'USERS'   
  14 6*LQ          FREECORE 2                   [FREE SUD
   8 6B6B    Z4306 LDX   1  FX1  
   8 6BL2          LDX   2  FX2  
   7 6C5L          EXIT  4  1
   8 6CK=    Z4310 LDN   0  #4000
   8 6D4W          SMO      FX2  
   9 6DJG          ANDX  0  XLDEXA   
  19 6F46          BZE   0  Z4306               [J UNLESS INITIAL LOAD OF PP 
  20 6FHQ          FON      HLOADPPWT           [WAKE UP ANY ONE WAITING FOR IT  
  13 6G3B          BRN      Z4306               [EXIT
  19 6GH2    Z4320 BNZ   0  Z4306               [EXIT IF USER COUNT NON ZERO 
  21 6H2L          BSOFF    G4SUDBIT,Z4306            [JMP IF THERE ARE SUDS TO DELET
  21 6HG=          OFF      G4SUDBIT                  [INDICATE THAT THERE IS A SUD T
  21 6H_W          FON      HSWAPSPWT           [WAKE UP THOSE WAITING FOR SWAP SPACE
  17 6JFG          FON      #105                [OR FOR OBJ PROG CORE
   8 6J_6          BRN      Z4306
  21 6KDQ    Z4330 SBN   4  1                   [EXIT + 0 IF THERE ARE REPLACEMENTS  
  14 6KYB          BRN      Z4306               [TO SUD  
   4 6LD2    )   
  10 6LXL          MENDAREA 30,K99LOADERR
   4 6MC=    #END
   8 ____ ...47264644000900000000
