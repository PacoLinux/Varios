  15 22FL          SEG      BSUNJAM,83,PETER CAMPBELL,BSUNJ  
   4 22_=    [   
   4 23DW    [   
  16 23YG    [  (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982   
   4 24D6    [   
   4 24XQ    [   
   9 25CB    #DEF Z=K6BSUNJAM>599-599
  12 25X2          SEGENTRY K1BSUNJAM,X1BSUNJAM  
  19 26BL    MASTER         12HMASTER           [ NAME  OF  MASTER  DIRECTORY
   9 26W=          SEGENTRY   BSUJSW 
  21 27*W                   +0                  [THIS IS A SWITCH WORD USED TO LIST  
  21 27TG                                       [ STATISTICS IF NON-ZERO,BY ENTRY TO 
  17 28*6                                       [ BSUJSTAT SEGMENT   
  20 28SQ    NFRES          +4                  [NO. OF WORDS PER RESIDENCE USED 
  18 29#B                                       [IN FILE/FRESNUM BLOCK   
   6 29S2    #SKI IFS<1$1
   4 2=?L    (   
   5 2=R=    SMAPP   
   8 2?=W    #HAL  FILE+FMAPP,0  
   4 2?QG    )   
  21 2#=6    PARALT         12HFORMULA          [INSTALLATION PARAMETER USED BY INSTP
  20 2#PQ    #     THIS   ROUTINE   IS  CALLED   IN   BY   THE   BACKING   STORE 
  20 2*9B    #     ALLOCATION  SYSTEM   WHEN  A   CERTAIN  PERCENTAGE   OF  ANY  
  11 2*P2    #     BACKING  STORE  IS  USED  
   4 2B8L    [   
  17 2BN=          BITDEFS  AWORK3,0,XBDESPDUMP,XBABJOBS,XBNONSPEC   
  14 2C7W          BITDEFS  AWORK3,22,XBDUMPED,XBNOTFIRST
  10 2CMG    [     AWORK3=0 IF FIRST PASS
  11 2D76    [     B23 SET IF NOT FIRST PASS 
  16 2DLQ    [     B0 SET IF UNJAMMER HAS DONE A 'DESPERATE'DUMP 
  16 2F6B    [     B1 SET IF ABANDON JOBS MESSAGE HAS BEEN OUTPUT
  15 2FL2    [     B2 SET IF A NON SPECIAL RESIDENCE JAMMED  
  19 2G5L    [     AWORK4 CONTAINS NO.CLEANCTS SPENT WAITING FOR DIRECTORIES 
   4 2GK=    [   
   9 2H4W    [     BEGINNING OF LOOP 
   4 2HJG    [   
   5 2J46    RETURNA 
  10 2JHQ          BS       2,XBNOTFIRST 
  20 2K3B          IFUJSTAT 2                     [GO DOWN TO WRITE MAIN RECORD  
  19 2KH2    #              X6 -VC IF ONLY SPECIAL RESIDENCES WERE JAMMED AT 
  19 2L2L    #              END OF LAST PASS - IN THIS CASE FORMULA SHOULD   
  12 2LG=    #              NOT BE RECALCULATED  
   8 2L_W    TCCAO BNG   6  NRCAL
  20 2MFG          NGX   5  5                   [ X5 := NO OF JAMMED RESIDENCES  
  18 2M_6          CALL  0  PMISC               [ FIND FIRST BSLIST BLOCK
  18 2NDQ          BRN      XNUMRES             [ JUMP IF END OF CHAIN   
  19 2NYB          ADN   5  1                   [ ADD 1 FOR EACH RESIDENCE   
  16 2PD2          BRN      NMISC               [ FIND NEXT RES  
   5 2PXL    XNUMRES 
  10 2QC=          MHUNTW   1,FILE,INCUNJ
  19 2QWW          STO   5  ANOJAM(1)           [ STORE NO OF UNJAMMED RES   
   8 2RBG          LDX   2  FX2  
   5 2RW6    #SKI  Z 
   4 2S*Q    (   
  12 2STB          TRACE    BSNDUMP(1),BSNDUMP   
  11 2T*2          TRACE    BSLEFT(1),BSLEFT 
   4 2TSL    )   
  12 2W#=          JBSC     NODUMP2,2,XBDUMPED   
  20 2WRW          LDX   0  BSNDUMP(1)          [ NUMBER OF BLOCKS TO BE DUMPED  
  21 2X?G          BXL   0  BSLEFT(1),NODUMP2   [ JUMP IF LESS THAN BLOCKS TO CLEAR J
  18 2XR6          LDN   2  0                   [ COUNT OF BLOCKS ONLINE 
  19 2Y=Q          LDX   0  BSAB                [ TYPE OF BSTB/BSLIST BLOCK  
  19 2YQB          LDX   3  BMISC               [ PICK UP BASE OF MISC CHAIN 
  17 2_=2    TODU1 LDX   3  FPTR(3)             [ PICK UP NEXT BLOCK 
  18 2_PL          BXE   3  CXMI,TODU2          [ JUMP IF END OF CHAIN   
  17 329=          BXU   0  ATYPE(3),TODU1      [ JUMP IF NOT BSLIST 
  17 32NW          LDX   4  BSLETH(3)           [ JUMP IF SWAP FILE  
  12 338G          BNG   4  TODU1               [
  19 33N6          ADX   2  BSTOT(3)            [ ADD TOTAL SIZE OF RESIDENCE
  18 347Q          SBX   2  BSFREE(3)           [ SUBTRACT FREE BLOCKS   
  12 34MB          BRN      TODU1               [
  20 3572    TODU2 SRL   2  2                   [ DIVIDE SIZE OF ONLINE F/S BY 4 
  19 35LL          BXL   2  BSNDUMP(1),TRYDP    [ JUMP IF > 25% TO BE DUMPED 
   5 366=    NODUMP2 
   8 36KW          LDX   2  FX2  
   9 375G          LDX   0  FORMULA  
  20 37K6          BNZ   0  NTDMP               [GO ON UNJAMMING UNLESS DESPERATE
   4 384Q    [   
  19 38JB    [     IF THE UNJAMMERS FORMULA IS ZERO,THEN WE ONLY WANT TO DO A
  20 3942    [     'DESPERATE' DUMP ONCE.OTHERWISE ONLY DUMP WHEN FORMULA=ZERO IF
  13 39HL    [     THERE IS ENOUGH TO CLEAR THE JAM  
   4 3=3=    [   
  11 3=GW          JBSS   NDDMP,2,XBDESPDUMP 
   5 3?2G    TRYDP   
   9 3?G6    #SKI  K6BSUNJAM>99-99   
  12 3?_Q          TRACE    BSNDUMP(1),DUMPING   
   9 3#FB          OUTPAR   TIMENOW  
  20 3#_2          CALL  3  OPMESS              [OUTPUT 'BACKING STORE JAM DUMPER
  19 3*DL                   +BSJDUM             [                    STARTED'
  16 3*Y=                   +3                  [TO ALL CONSOLES 
  17 3BCW          DUMPINC                      [SET THE DUMPER GOING
   9 3BXG          BS      2,XBDUMPED
   7 3CC6          LDN   6  0
   8 3CWQ          IFUJSTAT  3   
   8 3DBB          BRN     NRCAL 
  11 3DW2    NDDMP JBSS   NABAN,2,XBABJOBS   
  18 3F*L          CALL  3  OPMESS              [OUTPUT 'ABANDON JOBS'   
   9 3FT=                   +BSABAN  
  17 3G#W                   +1                  [TO CENTRAL CONSOLE  
   5 3GSG    NTDMP   
   5 3H#6    NABAN   
   7 3HRQ          LDN   6  1
  21 3J?B          IFUJSTAT 3                   [ APPEND FILE/INCUNJ TO STSTS FILE   
   8 3JR2          BRN      NRCAL
   6 3K=L    X1BSUNJAM   
  11 3KQ=          OUTPAR   DATENOW,TIMENOW  
  10 3L9W          OUTPACK  GMTNOW,1,NUMA
  19 3LPG          CALL  3  OPMESS              [OUTPUT 'BACKING STORE JAM'  
   9 3M96                   +BACJAM  
  17 3MNQ                   +3                  [TO ALL RESIDENCES   
  10 3N8B          SEGENTRY K10BSUNJAM   
   8 3NN2          BRN      NOFF 
   4 3P7L    #   
  18 3PM=    #     OUTPUT 'PARAMETERS FOR FILE FORMULA ARE X1,X2,X3,X4'  
   4 3Q6W    #   
   9 3QLG          LDN   4  BSUJFORM1
   7 3R66          LDN   5  1
   9 3RKQ          LDN   6  JPDNUMA  
   9 3S5B          CALL  7  PACKMESS 
   9 3SK2          LDN   4  BSUJFORM2
   9 3T4L          CALL  7  PACKMESS 
   9 3TJ=          LDN   4  BSUJFORM3
   9 3W3W          CALL  7  PACKMESS 
   9 3WHG          LDN   4  BSUJFORM4
   9 3X36          CALL  7  PACKMESS 
   9 3XGQ          MONOUT   BSFFPARS 
   8 3Y2B          BRN      NNOFF
  10 3YG2          SEGENTRY K11BSUNJAM   
  16 3Y_L    NOFF  MONOUT   BSFFNORM            [STANDARD FORMULA
  16 3_F=    NNOFF LDX   7  BSUJSW(1)           [PICK UP SWITCH  
  21 3_YW          IFUJSTAT 1                   [ TRY TO OPEN STATS FILE ,TURN OFF SW
   8 42DG    [ IF UNSUCCESSFUL   
  21 42Y6    #              CARRY OUT A MISC CHAIN SEARCH TO FIND OUT THE NUMBER OF  
  16 43CQ    #              ONLINF RESIDENCES DURING THIS JAM.   
  20 43XB    #              THIS VALUE IS STORED IN IRESNUM OF THE INCUNJ BLOCK  
   7 44C2          LDN   5  0
  17 44WL          CALL  0  PMISC               [FIND NEXT RESIDENCE 
  18 45B=          BRN      XOUT                [EXIT FOR END OF CHAIN   
   7 45TW          ADN   5  1
   8 46*G          BRN      NMISC
   4 46T6    XOUT
   7 47#Q          LDX   4  5
  11 47SB          SETNCORE 8,2,FILE,INCUNJ  
  19 48#2          STO   4  IRESNUM(2)          [NUMBER OF RESIDENCES IN X4  
   7 48RL          LDN   6  4
  18 49?=          MPY   4  6                   [ALLOW FOUR WORDS PER RES
  18 49QW          ADN   5  1                   [PLUS ONE FOR REC HEDDR  
  11 4==G          SETUPCORE 5,3,FILE,FRESNUM
   7 4=Q6          SBN   5  2
   9 4?9Q          STOZ     A1+1(3)  
   9 4?PB          LDN   1  A1+1(3)  
   8 4#92          LDN   2  1(1) 
   7 4#NL          SMO      5
   7 4*8=          MOVE  1  0
  12 4*MW    #     ZEROISE WORDS IN FILE,FRESNUM 
  11 4B7G          SETNCORE 6,1,FILE,FLOCNB  
  11 4BM6          SETNCORE 4,3,FILE,FABSNB  
   7 4C6Q          LDN   0  4
   8 4CLB          STO   0  A1(3)
   8 4D62          ADN   3  A1+1 
   9 4DKL          LDN   2  MASTER(1)
   7 4F5=          MOVE  2  3
   5 4FJW    NRCAL   
  10 4G4G          MHUNTW   2,FILE,INCUNJ
   4 4GJ6    [   
   9 4H3Q    [     START OF MAIN LOOP
   4 4HHB    [   
   5 4J32    TINCUJ  
   9 4JGL    #SKI  K6BSUNJAM>99-99   
   4 4K2=    (   
  11 4KFW          TRACE    CLEANCT,BSU ENT  
  11 4K_G          TRACE    FORMULA,FORMULA  
   4 4LF6    )   
   7 4LYQ          LDN   0  8
   9 4MDB          STO   0  INREC(2) 
  10 4MY2          STOZ     INREC+2(2)   
  10 4NCL          LDN   0  INREC+2(2)   
   9 4NX=          LDN   1  ANOJAM(2)
   7 4PBW          MOVE  0  5
  19 4PWG    #              CARRY OUT A MISC CHAIN SEARCH TO DETERMINE WHICH 
  18 4QB6    #              RESIDENCES ARE ABOVE THRESHOLD ANM/OR JAMMED 
  18 4QTQ    #              FRESNUM HOLDS 4 WORD ENTRIES PER RESIDENCE   
  12 4R*B    #              1ST =RESIDENCE NUMBER
  11 4RT2    #              2ND = STATE WORD 
  14 4S#L    #              3RD = NO. OF FILES THROWN OFF
  15 4SS=    #              4TH = SIZE OF FILES THROWN OFF   
  11 4T?W          MHUNTW   3,FILE,FRESNUM   
   9 4TRG          STOZ     FRREC(3) 
  20 4W?6          LDN   1  0                   [USE X1 TO HOLD NUM. OF JAMMED   
  15 4WQQ                                       [RESIDENCES  
  17 4X=B          CALL  0  PMISC               [FIND NEXT RESIDENCE 
  17 4XQ2    RESET BRN      TOUT                [EXIT FOR END O CHAIN
  19 4Y9L          BC      2,BSLTHRESH          [ CLEAR YHRESHOLD BIT IF SET 
   9 4YP=          LDX   4  BACK1(2) 
  18 4_8W          STO   4  FRNUM(3)            [INSERT RESUDENCE NUMBERS
  17 4_NG          JBS     ZHARDJAM,2,BSLJAM    [ JUMP IF JAM BIT SET
   9 5286          LDX   5  BSFREE(2)
  16 52MQ          BNG   5  ZHARDJAM            [MUST BE JAMMED  
  18 537B          BXGE  5  BSTHRESH(2),NSET    [J IF NOT ABOVE THRESHOLD
  17 53M2          BS      2,BSLTHRESH           [ SET THRESHOLD BIT 
   5 546L    ZHARDJAM
  16 54L=          LDCT  4  #600                [PRESERVE B0/B1  
  10 555W          ANDS  4  FRSTATE(3)   
  10 55KG          BS       3,BFTHRESH   
  10 5656          JBC     NOSET,2,BSLJAM
  21 56JQ    ZJAM  ADN   1  1                   [INCREASE NO. OF JAMMED RESIDENCES   
  20 574B          MBI     3,BFJAM,BFTHRESH     [ SET JAM UNSET THRESHOLD BITS   
   8 57J2          BRN     NOSET 
  10 583L    NSET  STOZ     FRSTATE(3)   
   5 58H=    NOSET   
   5 592W    #SKI  Z 
   4 59GG    (   
  11 5=26          TRACE    FRNUM(3),RESNUM  
  12 5=FQ          TRACE    FRSTATE(3),RESTATE   
   4 5=_B    )   
  10 5?F2          STOZ     FRNTOFF(3)   
  10 5?YL          STOZ     FRSTOFF(3)   
  11 5#D=          MHUNTW   3,FILE,FRESNUM   
   8 5#XW          SMO      FX1  
   8 5*CG          LDX   0  NFRES
   9 5*X6          ADS   0  FRREC(3) 
   9 5BBQ          ADX   3  FRREC(3) 
   8 5BWB          SMO      FX1  
  20 5CB2          LDN   0  RESET               [RESTORE LINK ADDRESS TO PMISC   
   8 5CTL          BRN      NMISC
   4 5D*=    TOUT
  19 5DSW          LDX   6  1                   [X1->X6 WHILE MESSAGES OUTPUT
   8 5F#G          LDX   2  FX2  
  12 5FS6          JBS     NOSTAT4,2,XBNONSPEC   
   7 5G?Q          IFUJSTAT 4
   5 5GRB    NOSTAT4 
   5 5H?2    #SKI  Z 
   9 5HQL          TRACE    6,TOTJRES
  18 5J==          CALL  3  OUTMESS             [JAM ON RESIDENCE . . .  
   8 5JPW                   +0   
   9 5K9G                   +BSJAMRES
  20 5KP6          BRN      NOSPEC1              [EXIT 3 3 IF A  SPEC RES JAMMED 
  20 5L8Q                                       [EXIT 3 2 IF NO SPEC RES JAMMED  
  18 5LNB          CALL  3  OUTMESS             [CC AO ON RESIDENCE . . .
   8 5M82                   +4   
  10 5MML                   +BSSPECRES   
  21 5N7=          BRN      NOSPEC1              [EXIT 3 2 IF BOTH TYPES OR RES JAMME
  20 5NLW                                       [EXIT 3 3 - ONLY SPEC RES JAMMED 
   7 5P6G          LDN   5  0
   9 5PL6          BRN      NOSPEC2  
   5 5Q5Q    NOSPEC1 
   8 5QKB          LDCT  5  #400 
   5 5R52    NOSPEC2 
  10 5RJL          MHUNTW   2,FILE,INCUNJ
  16 5S4=          OPENINC  ,READ               [OPEN INCINDEX   
   7 5SHW          LDN   6  0
  16 5T3G    RINC  STEP                         [READ NEXT RECORD
  15 5TH6          BZE   3  OVER                [LAST RECORD 
  16 5W2Q          LDX   6  AINCNO-A1(3)        [HIGHEST INC.NO  
  11 5WGB          JBS   REDUMP,3,BIREDUMPR  
  16 5X22          LDX   2  AMTSR(3)                [ NO TAPES ? 
  17 5XFL          BZE   2  REDUMP           [ NO NOT WRONGED TAPES  
   5 5X_=    NEXTTAPE
  19 5YDW          JMBAC RINC,3,BMUSWRR,BMOPWRR   [ AT LEAST ONE TAPE OK !   
   7 5YYG          ADN   3  2
  18 5_D6          BCT   2  NEXTTAPE                 [ LOOK AT NEXT TAPE 
   4 5_XQ    #   
   9 62CB    #      ALL TAPES WRONGED
   4 62X2    #   
   5 63BL    REDUMP  
  17 63W=          LDX   4  6                   [NO OF THIS INCREMENT
   5 64*W    #SKI Z  
   4 64TG    (   
  13 65*6          TRACE    AINCSTATE-A1(3),UNRINCST 
   9 65SQ          TRACE    4,UNRINC 
   4 66#B    )   
   9 66S2          CALL  1  NUMINC   
   8 67?L          BRN      RINC 
  10 67R=    OVER  MHUNTW   3,FILE,INCUNJ
   7 68=W          ADN   6  1
   8 68QG          SMO      A1(3)
  16 69=6          STO   6  A1-1(3)             [LATEST INC.NO+1 
  14 69PQ          CLOSETOP                     [INCINDEX
   8 6=9B          BZE   5  SPECS
   9 6=P2          BNG   5  NOSPEC   
   9 6?8L          GEOERR   1,FORTEST
  16 6?N=    #              ONLY SPECIAL RESIDENCES ARE JAMMED   
   4 6#7W    #   
   5 6#MG    SPECS   
  16 6*76          LDX   5  ASYSTEMMESS         [IP MESSAGETIME  
   9 6*LQ          LDN   1  FONINTP  
   7 6B6B          DVS   4  1
   5 6BL2    SLEEP   
  20 6C5L          COOR3    #62                 [SLEEP THE LENGTH OF MESSAGETIME 
  20 6CK=          CALL  0  PMISC               [UNLESS A NON SPECIAL TES JAMMED 
   8 6D4W          BRN      NDOZE
  17 6DJG          LDX   1  BACK1(2)            [IF SPECIAL RES -CAN 
  18 6F46          SBN   1  48                  [SLEEP A LITTLE LONGER   
   8 6FHQ          BPZ   1  NDOZE
   8 6G3B          ANDN  4  #4000
   8 6GH2          BZE   4  NMISC
   8 6H2L          LDX  2  FX2   
  10 6HG=          BS      2,XBNOTFIRST  
  14 6H_W          BRN      NRCAL               [RESET X2
   5 6JFG    NDOZE   
   8 6J_6          BCT   5  SLEEP
   8 6KDQ          LDCT  3  #400 
   8 6KYB          BRN      SONLY
   4 6LD2    #   
   5 6LXL    NOSPEC  
  19 6MC=          CALL  3  OUTMESS             [ABOVE THRESHOLD ON RES . . .
   8 6MWW                   +1   
  10 6NBG                   +BSTHRESHR   
  11 6NW6          JBS      PASS2,2,XBNONSPEC
   7 6P*Q          REFORMUL  
   8 6PTB          SMO      FX2  
  21 6Q*2          STO   6  AWORK2              [ SAVE VALUE OF FORMULAFROM 1ST PASS 
   8 6QSL          BRN      NEWF 
  11 6R#=    PASS2 JBC      NODUMP,2,XBDUMPED
  18 6RRW    [     IF DUMPER WAS STARTED ON LAST PASS LEAVE FORMULA UNC  
   9 6S?G          BRN      ZDUMPED  
   7 6SR6    NODUMP REFORM   
  17 6T=Q    NEWF  STO   6  FORMULA             [CHANGE VALUE IN CORE
  20 6TQB          OUTPACK  FORMULA,1,NUMA      [OUTPUT WORKING VALUE OF FORMULA 
  19 6W=2          CALL  3  OPMESS              [OUTPUT MESSAGE TO CONSOLE   
   9 6WPL                   +BSUFORM 
   8 6X9=                   +1   
   5 6XNW    ZDUMPED 
   5 6Y8G    #SKI Z  
  10 6YN6          TRACE    0(3),BEGINMAS
  10 6_7Q          OPENMAS  XBRK,GENERAL 
   4 6_MB    [   
  14 7272    [      DOWN TO BSPRUNE FOR FILESTORE PASS   
   4 72LL    [   
   9 736=          DOWN     BSPRUNE,1
   4 73KW    [   
   4 745G    [   
   4 74K6    [   
  10 754Q          LDX   0  ACOMMUNE1(2) 
  18 75JB          STO   0  AWORK4(2)           [PRESERVE CLEANCT TOTAL  
   5 7642    #SKI Z  
   9 76HL          TRACE    0,DIRWTIM
  15 773=    MASTL CLOSETOP                     [CLOSE MASTER
  10 77GW          BS      2,XBNONSPEC   
   7 782G          LDN   3  0
  17 78G6    SONLY CALL  0  PMISC               [FIND NEXT RESIDENCE 
  17 78_Q    SRSET BRN      SOUT                [EXIT IF END OF CHAIN
  11 79FB          MHUNTW   1,FILE,FRESNUM   
   5 79_2    NERES   
   9 7=DL          LDX   0  FRNUM(1) 
  19 7=Y=          BXE   0  BACK1(2),THRES      [J IF FOUND RIGHT RESIDENCE  
   8 7?CW          SMO      FX1  
  19 7?XG          ADX   1  NFRES               [STEP ON TO NEXT RESIDENCE   
   8 7#C6          BRN      NERES
  17 7#WQ    THRES LDX   5  BSFREE(2)           [NO. OF BLOCKS LEFT  
  17 7*BB          BNG   5  SJAM                [STILL IN HARD JAM   
  18 7*W2          JBC     NJAM,2,BSLJAM        [ JUMP IF WASNT JAMMED   
  21 7B*L          BXL   5  BSJAML(2),SJAM      [     STILL JAMMED - ADD INTO BSLEFT 
  21 7BT=          BXL   5  BSTHRESH(2),NTHRESH [WAS JAMMED- STILL ABOVE THRESHOLD   
  21 7C#W          MBC     2,BSLJAM,BSLTHRESH   [ WAS JAMMED-NOW BELOW THRESHOLD-CLEA
   8 7CSG    [     THRESHOLD BITS
  10 7D#6          BS       1,BFTHRESHCL 
   8 7DRQ          BRN     SNEXT 
   5 7F?B    NTHRESH 
  21 7FR2    #              WAS JAMMED BUT STILL ABOVE THRESHOLD- UNSET JAM MARKER   
  16 7G=L    #              ONLY IN BSLETH AND SET B21 OF STATEWD
   9 7GQ=          BC      2,BSLJAM  
   9 7H9W          BS      1,BFJAMCL 
   8 7HPG          BRN     SNEXT 
   4 7J96    SJAM
  15 7JNQ          LDX   4  BSJAML(2)           [JAM LIMIT   
  18 7K8B          SBX   4  BSFREE(2)           [ - NO. OF BLOCKS LEFT   
   8 7KN2          BNG   4  SNEXT
  19 7L7L          LDX   5  BACK1(2)            [DONT ADD INTO BSLETH IF FILE
  18 7LM=          SBN   5  48                  [ON A SPECIAL RESIDENCE  
   8 7M6W          BPZ   5  WRES 
  10 7MLG          MHUNTW   1,FILE,INCUNJ
  19 7N66          ADS   4  BSLEFT(1)           [BLOCKS INTO JAM TO BSLEFT   
   4 7NKQ    WRES
  21 7P5B          ADN   3  1                   [INCREASE NUMBER OF UNITS STILL JAMME
  17 7PK2          BRN      SNEXT               [PICK UP NEXT FPTR   
  11 7Q4L    NJAM  JBC      SNEXT,1,BFTHRESH 
  20 7QJ=          BXL   5  BSTHRESH(2),SJAM    [STILL ABOVE THRESH - LEAVE IT - 
  20 7R3W                                       [BUT FIRST CHECK IT HASNT JAMMED 
  17 7RHG    #              NOW BELOW THRESHOLD - UNSET THRESHOLD BIT
  10 7S36          BC      2,BSLTHRESH   
  10 7SGQ          BS       1,BFTHRESHCL 
   8 7T2B    SNEXT SMO      FX1  
  19 7TG2          LDN   0  SRSET               [RESET LINK ADDRESS FOR PMISC
   8 7T_L          BRN      NMISC
   4 7WF=    #   
  20 7WYW    SOUT  LDX   6  3                   [NO. OF JAMMED UNITS- B0 SET IF  
  17 7XDG                                       [FROM CC AO MESSAGE  
  18 7XY6          CALL  3  OUTMESS             [JAM CLEARED ON RES . . .
   8 7YCQ                   +2   
   9 7YXB                   +BSJAMCL 
   7 7_C2          LDEX  5  6
  17 7_WL          BNZ   5  STILJAM             [J IF NOT END OF JAM 
  18 82B=          MHUNTW   3,FILE,INCUNJ       [PREPARE FILE/INCUNJ FOR 
  17 82TW          LDN   0  8                   [BSUPTHRESH MESSAGE  
   9 83*G          STO   0  INREC(3) 
   5 83T6    STILJAM 
   8 84#Q          BNG   6  PCCAO
  21 84SB          CALL  3  OUTMESS             [RES .  . . CLEARED BELOW THRESHOLD  
   8 85#2                   +3   
  10 85RL                   +BSTHRESHCL  
   5 86?=    PCCAO   
   9 86QW    #SKI  K6BSUNJAM>99-99   
  10 87=G          TRACE CLEANCT,BSU END 
   5 87Q6    #SKI Z  
   4 889Q    (   
  10 88PB          TRACE    6,STILJAMD   
   9 8992          TRACE    0,CLCTEND
   4 89NL    )   
   7 8=8=          IFUJSTAT 4
  16 8=MW          LDEX  5  6                   [IGNORE B0 IF SET
   4 8?7G    #   
   4 8?M6    #   
   9 8#6Q          BNZ   5  RETURNA  
   8 8#LB    [     JAM CLEARED   
   7 8*62          IFUJSTAT 5
   9 8*KL          OUTPAR   TIMENOW  
  21 8B5=          CALL  3  OPMESS              [OUTPUT'BACKING STORE JAM CLEARED'   
   9 8BJW                   +BSJCL   
  16 8C4G                   +3                  [TO ALL CONSOLES 
  21 8CJ6          CALL  3  OPPMESS             [RESIDENCE%A %B NOW ABOVE THREXHOLD  
   8 8D3Q                   +5   
  10 8DHB                   +BSUPTHRESH  
   8 8F32          LDX   2  FX2  
  11 8FGL          JBC     NOUPD,2,XBNONSPEC 
  19 8G2=          LDX   6  AWORK2(2)         [PICK UP VALUE FROM 1ST PASS   
  19 8GFW          CALL  3  NEWFORM             [ CHANGE VALUE IN PARAMINDEX 
  10 8G_G          OPENMAS  XBRK,GENERAL 
   9 8HF6          LDX   0  GMTNOW   
   5 8HYQ    #SKI IFS
   7 8JDB          MASFCB   3
   6 8JY2    #SKI IFS<1$1
  15 8KCL          LDX   3  BFILE               [MASTERS FCB 
  15 8KX=          STO   0  FLASTJAM(3)         [CURRENT GMT 
  19 8LBW          MBS      3,BFALTB,BFALTR     [SET FILE BEING ALTERED BITS 
   5 8LWG    #SKI IFS
   9 8MB6          SFMAPP   3,3,XBRK 
   6 8MTQ    #SKI IFS<1$1
   4 8N*B    (   
   5 8NT2    NMAPP   
  19 8P#L          LDX   3  FPTR(3)             [SERCH FILE CHAIN FOR F/FMAPP
   9 8PS=          LDX   4  ATYPE(3) 
  17 8Q?W          BXE   4  FILEPLUSFCB,XBRK    [GEOERR IF NO F/FMAPP
  11 8QRG          BXU   4  SMAPP(1),NMAPP   
   4 8R?6    )   
  14 8RQQ    [     X3 NOW POINTS TO MASTERS F/FMAPP BLOCK
   7 8S=B          LDN   0  3
  19 8SQ2          STO   0  FBCOMM(3)           [REQUIRED IN UPDATING MASTER 
   7 8T9L          CLOSETOP  
   5 8TP=    NOUPD   
  10 8W8W          MFREEW   FILE,FABSNB  
  10 8WNG          MFREEW   FILE,FLOCNB  
  10 8X86          MFREEW   FILE,FRESNUM 
  10 8XMQ          MFREEW   FILE,INCUNJ  
  18 8Y7B          CALL  0  PMISC               [TEST TO SEE IF A NEW JAM
  16 8YM2          BRN      NOMOREJAMS          [END OF BSLIST   
   8 8_6L          ANDN  4  #4000
  21 8_L=          BNZ   4  NUJAM               [ JUMP IF JAM MARKER IN BSLETH SET   
  20 925W          BRN      NMISC               [NO.  SO LOOK AT NEST RESIDECE   
   6 92KG    NOMOREJAMS  
  21 9356          LONGON   #21                 [WAKE UP ACTS WAITING FOR END BSJAM  
  17 93JQ                                       [E.G.MK3 PROCESSOR   
  17 944B          STARTACT RET                 [KICK D.T. SCHEDULER 
   6 94J2    NOSTARTACT  
  18 953L          COOR3    #11                 [SET WAITING FOR NEXT JAM
   5 95H=    NUJAM   
   8 962W          LDX   2  FX2  
  15 96GG          STOZ     AWORK3(2)           [SET'NEW JAM'
  17 9726          BRN      K1BSUNJAM           [NEW JAM HAS OCCURRED
   4 97FQ    #   
  14 97_B    XBRK  GEOERR   1,BRK BSUJ          [BREAKIN 
   4 98F2    #   
  14 98YL    #     SUBROUTINE TO SEND MESSAGE TO CONSOLE 
   4 99D=    #   
   5 99XW    OUTMESS 
  10 9=CG          MHUNTW   1,FILE,INCUNJ
  19 9=X6          LDX   5  IRESNUM(1)          [NUMBER OF ONLINE RESIDENCES 
  18 9?BQ          ADN   5  2                   [INCLUDE 1 FOR REC HEDDR 
  20 9?WB                                       [AND ANOTHER FOR A1+1 (NOT USED) 
   8 9#B2          SBX   3  FX1  
  11 9#TL          SETUPCORE 5,2,FLIB,FLINC  
   8 9**=          ADX   3  FX1  
   7 9*SW          LDN   0  1
  19 9B#G          STO   0  A1(2)               [SET UP REC HEDDR FOR FLINC  
   7 9BS6          LDN   5  0
  11 9C?Q          MHUNTW   1,FILE,FRESNUM   
  19 9CRB          ADX   2  A1(2)               [INCREASE POINTER FOR F/FLINC
   5 9D?2    NRESN   
  20 9DQL          LDX   0  0(3)                [PICK UP FIRST PARAMETER OF CALL 
  16 9F==          BZE   0  XJAMRES             [JAM ON RESIDENCE
   7 9FPW          SBN   0  1
  16 9G9G          BZE   0  XTHRESHR            [ABOVE THRESHOLD 
   7 9GP6          SBN   0  1
  15 9H8Q          BZE   0  XJAMCL              [JAM CLEARED 
   7 9HNB          SBN   0  1
  18 9J82          BZE   0  XTHRESHCL           [CLEARED BELOW THRESHOLD 
  18 9JML          BRN      XJAMRESCCAO         [CC AO MESSAGE WITH X0=1 
   4 9K7=    TRES
  19 9KLW          LDX   4  FRNUM(1)            [INSERT RESNUM INTO F/FLINC  
   9 9L6G          STO   4  A1+1(2)  
  10 9LL6          MHUNTW   2,FLIB,FLINC 
   7 9M5Q          LDN   0  1
  19 9MKB          ADS   0  A1(2)               [INCREASE POINTER IN F/FLINC 
  16 9N52          ADX   2  A1(2)               [AND UPDATE X2   
   5 9NJL    NSUIT   
  11 9P4=          MHUNTW   1,FILE,FRESNUM   
   7 9PHW          ADN   5  4
  17 9Q3G          BXE   5  FRREC(1),RESEN      [J IF END OF SEARCH  
  20 9QH6          ADX   1  5                   [UPDATE F/FRESNUM POINTER BY 4   
  18 9R2Q          BRN      NRESN               [LOOK AT NEXT RES NUMBER 
   4 9RGB    #   
  13 9S22    #              FINALLY OUTPUT MESSAGE   
   4 9SFL    #   
   5 9S_=    RESEN   
  10 9TDW          MHUNTW   2,FLIB,FLINC 
  19 9TYG          LDX   5  A1(2)               [CHECK IF ANY VALUES TO O/P  
   7 9WD6          SBN   5  1
  20 9WXQ          BZE   5  NOMESS              [DONT BOTHER WITH MESSAGE IF NOT 
  19 9XCB          LDX   4  1(3)                [PICK UP UNIVERSAL IDENTIFIER
   8 9XX2          SBX   3  FX1  
  18 9YBL          OUTINCS                      [SET UP OUTPARAM FOR O/P 
   8 9YW=          SMO      FX2  
  18 9_*W          STO   7  AWORK1              [PRESERVE STATS SWITCH   
  19 9_TG          LDN   7  0                   [MESSAGE TO CENTRAL CONSOLE  
   8 =2*6          INFORMX  7,4,1
  17 =2SQ          ADX   3  FX1                 [RESTORE CALL ADDRESS
  18 =3#B          LDX   7  AWORK1(2)           [RESTRORE STATS SWITCH   
   5 =3S2    NOMESS  
   8 =4?L          LDX   4  0(3) 
  19 =4R=          BZE   4  PDEAL               [CHECK IF NEED TO DEALLOCATE 
   7 =5=W          SBN   4  4
  19 =5QG          BZE   4  SRES                [J IF COME FROM CCAO MESSAGE 
   5 =6=6    NORMAL  
  10 =6PQ          MFREEW   FLIB,FLINC   
  15 =79B          EXIT  3  2                   [NORMAL EXIT 
  18 =7P2    PDEAL BPZ   6  NORMAL              [NO NEED TO DEALLOCATE   
  16 =88L                                       [ - EXIT NORMALLY
   5 =8N=    SPECONLY
  10 =97W          MFREEW   FLIB,FLINC   
   7 =9MG          EXIT  3  3
  15 ==76    SRES  LDEX  0  6                   [DONT NEED B0
  17 ==LQ          BXE   0  5,SPECONLY          [ONLY SPEC RES JAMMED
  19 =?6B          BRN      NORMAL              [NORMAL EXIT DO UNJAM PASS   
   4 =?L2    #   
   4 =#5L    #   
  15 =#K=    #              SETS B20 - JAM ON RESIDENCE. . . 
   4 =*4W    [   
   4 =*JG    [   
  16 =B46    XJAMRES                        [JAM ON RESIDENCES   
  21 =BHQ          JBC     NSUIT,1,BFJAM        [ IF NOT JAMMED LOOK AT NEXT RESIDENC
  17 =C3B          LDX   4  FRNUM(1)            [ RESIDENCE NUMBER   
   8 =CH2          SBN   4  48   
  18 =D2L          BNG   4  NSPECJ              [ JUMP IF NOT SPEC RES   
  19 =DG=          LDCT  4  #400                [ IF SPEC RES SET BIT 0 IN X2
   7 =D_W          ORS   4  6
   5 =FFG    NSPECJ  
  21 =F_6          JBSS     NSUIT,1,BFJAMOP     [ IF MESSAGE ALREADY OUTPUT DONT REPE
  16 =GDQ          BRN      TRES                [ INSERT RES NO  
   4 =GYB    [   
   4 =HD2    [   
  18 =HXL    XTHRESHR                           [ ABOVE THRESHOLD ON RES 
  21 =JC=          JBC      NSUIT,1,BFTHRESH    [ DONT OUTPUT MESSAGE IN NOT ABOVE TH
  20 =JWW          JBSS     NSUIT,1,BFTHRESHOP  [ OR IF MESSAGE ALREADY OUTPUT   
   8 =KBG          BRN      TRES 
   4 =KW6    [   
   4 =L*Q    [   
  19 =LTB    XJAMCL                             [ JAM CLEARED ON RESIDENCE   
  18 =M*2          JBC      NSUIT,1,BFJAM       [ JUMP IF IT WASNT JAMMED
  15 =MSL          JMBAC    NSUIT,1,BFJAMCL,BFTHRESHCL      [
  20 =N#=          MBC      1,BFJAMOP,BFTHRESHOP    [ CLEAR MESSAGE OUTPUT BITS  
   8 =NRW          BRN      TRES 
   4 =P?G    [   
   4 =PR6    [   
   6 =Q=Q    XTHRESHCL   
  15 =QQB          JMBAC    NSUIT,1,BFJAM,BFTHRESH,BFTHRESHCL
  12 =R=2          JBS      THRESHB,1,BFTHRESHCL 
   7 =RPL          LDEX  4  6
  12 =S9=          BNZ   4  NSUIT               [
  12 =SNW          LDX   4  FRNUM(1)            [
  18 =T8G          CALL  1  NUMINC              [ INSERT RES NO IN INCUNJ
  10 =TN6          MHUNTW    2,FLIB,FLINC
  19 =W7Q          ADX   2  A1(2)               [ RESET POINTER TO FLIB FLINC
   8 =WMB          BRN      NSUIT
   5 =X72    THRESHB 
  12 =XLL          MBC      1,BFJAMOP,BFTHRESHOP 
   8 =Y6=          BRN      TRES 
   4 =YKW    [   
   4 =_5G    [   
   6 =_K6    XJAMRESCCAO 
  10 ?24Q          JBC      NSUIT,1,BFJAM
   9 ?2JB          LDX   4  FRNUM(1) 
   8 ?342          SBN   4  48   
   8 ?3HL          BPZ   4  TRES 
   8 ?43=          BRN      NSUIT
   4 ?4GW    [   
   4 ?52G    [   
   5 ?5G6    OPMESS  
  17 ?5_Q          LDX   1  0(3)                [ADDRESS OF MESSAGE  
  17 ?6FB          LDX   5  1(3)                [ROUTING PARAMETER   
   8 ?6_2          SBX   3  FX1  
   8 ?7DL          INFORMX  5,1,1
   8 ?7Y=          ADX   3  FX1  
   7 ?8CW          EXIT  3  2
   5 ?8XG    OPPMESS 
  10 ?9C6          MHUNTW   2,FILE,INCUNJ
  21 ?9WQ          LDN   4  INREC(2)            [FIND OUT THE NUMBER OF RESIDENCES   
  18 ?=BB          ADX   4  INREC(2)            [STILL ABOVE THRESHOLD   
   9 ?=W2          SBN   4  INCUNR(2)
   8 ??*L          SBX   3  FX1  
  19 ??T=          SETUPCORE 4,2,FLIB,FLINC     [AND SET UP FLINC FOR THEM   
   8 ?##W          ADX   3  FX1  
   7 ?#SG          SBN   4  1
  18 ?*#6          STO   4  A1(2)               [REMEMBER A1+1 IN FLINC  
   7 ?*RQ          SBN   4  1
   9 ?B?B          BZE   4  NORMAL   
   8 ?BR2          ADN   2  A1+2 
  10 ?C=L          MHUNTW   1,FILE,INCUNJ
   9 ?CQ=          ADN   1  INCUNR+1 
   7 ?D9W          SMO      4
   7 ?DPG          MOVE  1  0
   8 ?F96          BRN      RESEN
   4 ?FNQ    #   
  19 ?G8B    #              NUMINC IS USED AT THE START OF THE PASS TO INSERT
  20 ?GN2    #              UNRELIABLE INCREMENT NUMBERS INTO FILE/INCUNJ BLOCK  
  19 ?H7L    #                ALSO USED AT END OF JAM TO HOLD NUMBERS OF ANY 
  15 ?HM=    #              RESIDENCES STILL ABOVE THRSHOLD  
   4 ?J6W    #   
   5 ?JLG    NUMINC  
   8 ?K66          SBX   3  FX1  
   8 ?KKQ          SBX   1  FX1  
   8 ?L5B          LDX   2   FX2 
  10 ?LK2          STO   3  ACOMMUNE2(2) 
  20 ?M4L          STO   1  ACOMMUNE1(2)        [TEMP STORAGE FOR LINK ADDRESS   
  10 ?MJ=          MHUNTW   3,FILE,INCUNJ
  16 ?N3W          SMO      INREC(3)            [STORE NUMBER IN 
  17 ?NHG          STO   4  INREC-1(3)          [NEXT WORD OF INCUNJ 
   7 ?P36          LDN   0  1
  19 ?PGQ          ADS   0  INREC(3)            [INCREASE INCUNJ REC HEDDR.  
   9 ?Q2B          LDX   4  INREC(3) 
  11 ?QG2          BXL   4  ALOGLEN(3),NUFF  
  18 ?Q_L          ALTLEN   3,4,FILE,INCUNJ     [INCREASE INCUNJ LENGTH  
  18 ?RF=    NUFF  LDX   1  ACOMMUNE1(2)        [RESTORE LINKS IN X3 & X1
   2 ?RYW
  10 ?SDG          LDX   3  ACOMMUNE2(2) 
   8 ?SY6          ADX   3  FX1  
   8 ?TCQ          ADX   1  FX1  
   7 ?TXB          EXIT  1  0
   4 ?WC2    #   
  15 ?WWL    #              S/R PICKS UP NEXT BSLIST BLOCK   
  13 ?XB=    #              EXIT 0 0 IF END OF CHAIN 
  16 ?XTW    #              EXIT 0 1 IF SUITABLE RESIDENCE FOUND 
   4 ?Y*G    #   
   8 ?YT6    PMISC LDN   2  BMISC
  18 ?_#Q          LDX   6  BSAB                [HAMTYPE OF BSLIST BLOCK 
  17 ?_SB    NMISC LDX   2  FPTR(2)             [NEXT BLOCK IN CHAIN 
  16 #2#2          BXE   2  CXMI,NOUT           [END OF CKAIN?   
  17 #2RL          BXU   6  ATYPE(2),NMISC      [NOT A BSLIST BLOCK  
   9 #3?=          LDX   4  BSLETH(2)
  16 #3QW          BNG   4  NMISC               [J IF SWAP FILE  
   7 #4=G          EXIT  0  1
   7 #4Q6    NOUT  EXIT  0  0
   4 #59Q    #   
  14 #5PB    #              S/R TO ALTER VALUE OF FORMULA
  16 #692    #              IN CORE AND ON B.S. IF GMT NOT ZERO  
  13 #6NL    #              OR JUST IN CORE IF ZERO  
   4 #78=    #   
   5 #7MW    NEWFORM 
   8 #87G          SMO      FX1  
   9 #8M6          LDN   4  PARALT   
   8 #96Q          SBX   3  FX1  
  11 #9LB          INSTPARA  XBRK,4,6,OVERL  
   5 #=62    NWLIM   
   8 #=KL          ADX   3  FX1  
   7 #?5=          EXIT  3  0
   5 #?JW    OVERL   
  18 ##4G          OUTPACK  6,1,NUMA            [PICK UP VALUE OF FORMULA
  10 ##J6          INFORM   1,BSLIMIT,1  
   8 #*3Q          BRN      NWLIM
  18 #*HB    #              S/R FOR PARAMETERS FOR FILE FORMULA MESSAGE  
   5 #B32    PACKMESS
   8 #BGL          SBX   7  FX1  
   8 #C2=          OUTPACKX 4,5,6
   8 #CFW          ADX   7  FX1  
   7 #C_G          EXIT  7  0
  10 #DF6          MENDAREA 20,K99BSUNJAM
   4 #DYQ    #END
   6 ____ ...042113120002
