   7 228= ...#SEG  SOUP867   
   4 22BG ...#   
  15 22JQ ...#   COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982
   4 22R2 ...#   
   6 22_=    #LIS  K0SOUP
   6 23DW          8HSOUP
   4 23YG    #   
  18 24D6    # STORAGE DEVICE ONLINE COMMAND AND UNANTICIPATED OPEN  PERI
  18 24XQ    # -              -                  -                   -   
   4 25CB    #   
   4 25X2    #   
  11 26BL          SEGENTRY K1SOUP,QK1SOUP   
  11 26W=          SEGENTRY K2SOUP,QK2SOUP   
   4 27*W    #   
   4 27TG    #   
   9 28*6    #              ONLINE   
   4 28SQ    #   
  20 29#B    #     THIS SECTION DEALS WITH ONLINE COMMAND FOR SD,EXCEPT SCRATCH  
  21 29S2    #     ON ENTRY,PARAMETER 1 HAS BEEN ANALYSED AND RESULT IS IN A CPAR/   
  21 2=?L    #     GPERCON BLOCK.PARAMETER 2 IS FILENAME,WHICH WE ANALYSE.EXITS TO   
  18 2=R= ...#     EXOPEN WITH EWDAS/GEXD AND EWDAS/EXOF BLOCKS SET UP   
  10 2?=W ...#     FOR OPENING THE FILE  
   4 2?QG    #   
   4 2#=6    #   
   9 2#PQ    #DEF           RVGE=20  
  19 2*9B    XGIVE          +AGIVE              [FOR MASKING GIVE QUALIFIER  
  18 2*P2    X4096          +4096               [GENERATION NR COMPARISON
  17 2B8L    SIX7S          #777777             [MASK FOR SERIAL NR  
  18 2BN=    RMESS          +JEXCSN             [INVALID SERIAL NUMBER   
   4 2C7W    #   
   4 2CMG    #   
   5 2D76    QK1SOUP 
  11 2F6B          MHUNTW   1,CPAR,GPERCON   
   9 2FL2          LDX   0  A1+1(1)  
   8 2G5L          ANDN  0  #777 
   8 2GK=          SBN   0  #600 
  16 2H4W          BNZ   0  NOTS                [J IF NOT SCRATCH
   9 2HJG          ACROSS   SCRATCH,2
   4 2J46    NOTS
   8 2JHQ    #SKI  K6SOUP>199-199
   4 2K3B    (   
  19 2KH2          ADN   0  #600                [CHECK THAT MODE IS #100-#400
   7 2L2L          LDN   6  4
   8 2LG=    NTB   SBN   0  #100 
   8 2L_W          BZE   0  NTA  
   8 2MFG          BCT   6  NTB  
   9 2M_6    NTC   GEOERR   1,SD OL  
   8 2PD2    NTA   LDX   0  A1(1)
   8 2PXL          SRL   0  15   
   7 2RW6          SBN   0  6
   8 2W#=          BZE   0  NTD  
   8 2WRW ...      SBN   0  20   
   8 2X?G          BNZ   0  NTC  
   4 2XR6    NTD 
   4 2Y=Q    )   
  21 2YQB          SPARANOT 2                   [GET SECOND PARAMETER IN UNI BLOCK   
  10 2_=2          MHUNT    3,CPB,CUNI   
  10 2_PL          NAMETOP  3,FILE,FNAME 
   7 329=          PHOTO    6
  16 32NW          FNORM    1                   [CONVERT FILENAME
   9 338G          TESTMOVE 6,MOVF   
  10 33N6          MHUNT    3,FILE,FNAME 
  10 347Q    MOVF  NAMETOP  3,CPB,CUNI   
  16 34MB          TESTREPNOT NAMEFORM,NF       [J IF FILENAME OK
  20 3572          ENDCOM                       [ELSE END,FNORM HAS OUTPUT ERROR 
  17 35LL    NF    SETNCORE FDCELLS-A1,1,EWDAS,EXOF   [CREATE EXOF   
   9 366=          STOZ     BACK1(1) 
  16 36KW          STOZ     BACK2(1)            [CLEAR BACK1,ETC 
   9 375G          STOZ     COPEN(1) 
   9 37K6          STOZ     EXMARK(1)
  16 384Q          LDX   7  1                   [SAVE ITS ADDRESS
   7 38JB          PHOTO    6
  17 3942          SETNCORE GEXDLEN,3,EWDAS,GEXD      [CREATE GEXD   
   8 39HL          STOZ     A1(3)
  17 3=3=          LDN   4  A1(3)                     [AND ZEROISE IT
   9 3=GW          LDN   5  A1+1(3)  
   9 3?2G          MOVE  4  GEXDLEN-1
  19 3?G6          LDCT  4  256                 [REVISED GENERATION NUMBER   
  20 3?_Q          TESTREPNOT ADJUNCTS,NAJ      [J IF NO QUALIFIER TO FILE NAME  
  17 3#FB          MHUNTW   2,FILE,ADJUNCTS     [FIND ADJUNCTS BLOCK 
   4 3#FY ...#   
  15 3#GG ...#     LOOK FOR 'NOWAIT' AND 'REPLY' QUALIFIERS  
   4 3#H4 ...#   
   7 3#HL ...      LDN   5  0
  18 3#J8 ...      LDX   0  A1+1(2)             [PICK UP QUALIFIERS WORD 
   9 3#JQ ...      ANDN  0  AREPLY   
  18 3#K# ...      BZE   0  NOTREPLY            [JIF NO REPLY QUALIFIER  
  19 3#KW ...      ORN   5  BREPQUAL            [ELSE SET MARKER FOR /EXOF   
   4 3#LD ...#   
   5 3#M2 ...NOTREPLY
  20 3#MJ ...      LDX   0  A1+1(2)             [NOW WE LOOK AGAIN FOR THE NOWAIT
  15 3#N6 ...      ANDN  0  ANOWAIT             [QUALIFIER   
  18 3#NN ...      BZE   0  NOTNOWAIT           [JIF NO NOWAIT QUALIFIER 
  21 3#P= ...      ORN   5  BSUPP               [SET DON'T SUSPEND MARKER FOR /EXOF  
   4 3#PS ...#   
   6 3#QB ...NOTNOWAIT   
  19 3#QY ...      BZE   5  TRYGIVE             [JIF NOTHING TO ADD TO /EXOF 
  20 3#RG ...      LDX   1  7                   [ELSE GET BACK POINTER TO /EXOF  
  20 3#S4 ...      TESTMOVE 6,NOTMOVED          [JIF POINTER TO /EXOF IS STILL OK
  17 3#SL ...      MHUNTW   1,EWDAS,EXOF        [ELSE REFIND BLOCK   
   4 3#T8 ...#   
   5 3#TQ ...NOTMOVED
  21 3#W# ...      ORS   5  EXMARK(1)           [NOTE PRESENCE SPECIFIED QUALIFIERS  
   8 3#WW ...      LDX   1  FX1  
   4 3#XD ...#   
   5 3#Y2 ...TRYGIVE 
   9 3#_2          LDX   0  XGIVE(1) 
   9 3*DL          ANDX  0  A1+1(2)  
  19 3*Y=          BZE   0  NGIV                [J IF GIVE QUALIFIER ABSENT  
   7 3BCW          LDN   0  1
  17 3BXG          STO   0  GIVEX(3)            [SET MARKER IN GEXD  
  10 3CC6    NGIV  LDX   0  ALOGLEN(2)   
  19 3CWQ          SBN   0  ADPAIR-A1           [SEE IF ANY MORE QUALIFIERS  
  15 3DBB    NEXT  BZE   0  NRVG                [BRANCH IF NO
   7 3DW2          SMO      0
  10 3F*L          LDX   5  ADPAIR-2(2)  
  20 3FT=          SBN   5  RVGE                [SEE IF REVISED GENERATION....   
  19 3G#W          BZE   5  NOKR                [...NUMBER QUALIFIER(REVGEN) 
   7 3GSG          SBN   0  2
   8 3H#6          BRN      NEXT 
   7 3HRQ    NOKR  SMO      0
  16 3J?B          LDX   4  ADPAIR-1(2)         [REMEMBER REVGEN 
  17 3JR2    NRVG  FREECORE 2                   [FREE ADJUNCTS BLOCK 
  11 3K=L    NAJ   MHUNTW   2,CPAR,GPERCON   
  20 3KQ=          STO   4  ENEWG(3)            [STORE REVGEN IN GEXD FOR CHINTEG
   9 3L9W          LDX   0  A1+1(2)  
   8 3LPG          ANDN  0  #777 
  16 3M96          BNZ   0  NOQ                 [J IF QUALIFIER  
  16 3MNQ          LDN   0  #200                [ELSE SET AS READ
  10 3N8B    NOQ   STO   0  EOPMODE(3)   
   8 3NN2          BNG   4  NNRV 
   8 3P7L          SBN   0  #300 
   8 3PM=          BZE   0  NNRV 
   8 3Q6W          LDCT  0  256  
   9 3QLG          STO   0  ENEWG(3) 
   4 3R66    NNRV
   8 3RKQ          LDX   0  A1(2)
   8 3SK2          ANDX  0  BSP16
  18 3W3W          STO   0  EXCHNL(3)           [AND CHANNEL NR IN GEXD  
  17 3WHG          FREECORE 2                   [FREE /GPERCON BLOCK 
   7 3X36          LDX   1  7
   9 3XGQ          TESTMOVE 6,MOVN   
  18 3Y2B          MHUNTW   1,EWDAS,EXOF        [RESET POINTER TO EXOF   
  10 3YG2    MOVN  MHUNT    2,FILE,FABSNB
   9 3Y_L          LDX   0  HDREC(2) 
  20 3_F=          SBN   0  10                  [CHECK RECORD LENGTH=10,OTHERWISE
  18 3_YW          BNZ   0  NER                 [NAME IS OF WRONG SORT   
   9 42DG          LDX   0  A1+4(2)  
  18 42Y6          BZE   0  NER                 [ERROR IF LOCAL NAME ZERO
   9 43CQ          LDX   0  ATYPE(2) 
  21 43XB          ANDN  0  #5200               [FOR CORRECT NAME,B14=1,B12,16=0 IN  
  14 44C2          ERN   0  #1000               [ATYPE,  
  14 44WL          BZE   0  NOK                 [J IF SO,
  16 45B=    NER   FREECORE 1                   [ELSE FREE BLOCKS
   7 45TW          FREECORE 3
  18 46*G          COMERR   EWNAME              [AND REPORT NAME ERRORS  
   9 46T6    NOK   LDN   4  A1+4(2)  
   9 47#Q          LDN   5  FIP(1)   
  19 47SB          MOVE  4  3                   [PUT FILENAME IN FIP IN EXOF 
   9 48#2          LDX   0  A1+8(2)  
  19 48RL          BZE   0  NEG                 [J IF GEN WORD ZERO (NO GEN) 
  17 49?=          BPZ   0  POZ                 [J IF GEN POSITIVE,  
  19 49QW    NEG   LDN   0  BHIGH               [ELSE HIGHEST GEN REQUIRED,  
  15 4==G          BRN      RP                  [SET BHIGH   
   8 4=Q6    POZ   SMO      FX1  
   8 4?9Q          TXU   0  X4096
  17 4?PB          BCS      NANY                [J UNLESS GEN=4096,  
  16 4#92          LDN   0  BANY                [ELSE SET BANY   
   8 4#NL          BRN      RP   
   8 4*8=    NANY  SLL   0  12   
  18 4*MW          STO   0  FIP+4(1)            [PUT SPECIFIED GEN IN FIP
  16 4B7G          LDN   0  BSPEC               [AND SET BSPEC   
   9 4BM6    RP    ORS   0  EXMARK(1)
  10 4C6Q          LDX   0  ALOGLEN(2)   
   8 4CLB          SBN   0  10   
  19 4D62          BZE   0  NRP                 [J IF NO RP OR SERIAL NUMBER 
   9 4DKL          LDX   0  A1+10(2) 
  18 4F5=          BNG   0  NRP                 [J IF NO RETENTION PERIOD
  17 4FJW          DSA   0  ENEWRP(3)           [ELSE SAVE RP IN GEXD
   8 4G4G          BRN      SER  
   8 4GJ6    NRP   LDCT  0  #200 
  19 4H3Q          STO   0  ENEWRP(3)           [SET B1=1 TO INDICATE NO RP  
   9 4HHB    SER   LDX   0  ATYPE(2) 
   8 4J32          ANDN  0  #2000
  18 4JGL          BZE   0  NSER                [J IF NO SERIAL NUMBER   
   9 4K2=          LDX   0  A1+11(2) 
   8 4KFW          SMO      FX1  
  17 4K_G          LDX   4  SIX7S               [TEST CSN NOT GREATER
  17 4LF6          TXL   4  0                   [THAN 7 DIGITS AND   
  14 4LYQ          BCC      XOK                 [J IF OK 
  15 4MDB          FREECORE 1                   [FREE EXOF   
  15 4MY2          FREECORE 3                   [FREE GEXD   
   9 4NCL          COMERRX  RMESS(1) 
  16 4NDR ...XOK   STO   0  ASUSN(1)            [PUT CSN IN EXOF 
   4 4NFY ...#   
  10 4NGN ...      SEGENTRY AOLDA2SJMES  
   9 4NHD ...      BRN      NOMESS   
   4 4NJ= ...#   
  19 4NKC ...#     SPECIAL CODE TO SEND OL MESSAGES FOR DISC EXOFILES TO SJ  
  19 4NLJ ...#   - ONLY OBEYED IF RESTORE TIME MACRO ^OLDA2SJMESS^ IS USED   
   4 4NMP ...#   
  18 4NNW ...      OUTBLOCN 10                  [ GET A PARAMETER BLOCK  
  10 4NQ3 ...XLOC  MHUNTW   3,EWDAS,EXOF 
  12 4NR8 ...      OUTPACK  FIP(3),3,LOCNAME,XLOC
  10 4NS* ...XGEN  MHUNTW   3,EWDAS,EXOF 
  13 4NTG ...      OUTPACK  FIP+4(3),1,GENNUM,XGEN   
   4 4NWM ...#   
  18 4NXS ...#     FIP+4 : B0-11=GEN NO ; B12-23=VERSION.  IF GEN NO NOT 
  17 4NY_ ...#   SPECIFEID IN OL COMMAND FIP+4 IS PROBABLY RUBBISH   
   4 4P26 ...#   
  10 4P3? ...XSER  MHUNTW   3,EWDAS,EXOF 
  12 4P4D ...      OUTPACK  ASUSN(3),1,OCTA,XSER 
  10 4P5K ...XCHN  MHUNTW   3,EWDAS,GEXD 
  12 4P6Q ...      OUTPACK  EXCHNL(3),1,NUMA,XCHN
  17 4P7X ...      MONOUT   BISJ                [ PUT MESSAGE IN SJ  
  10 4P94 ...      MHUNTW   1,EWDAS,EXOF 
  10 4P=9 ...      MHUNT    2,FILE,FABSNB
  10 4P?B ...      MHUNTW   3,EWDAS,GEXD 
  21 4P#H ...NOMESS                             [ OLDA2SJMESS NULLS BRANCH TO HERE   
   4 4P*N ...#   
   8 4PBW          LDN   0  BSERL
  16 4PWG          ORS   0  EXMARK(1)           [AND SET BSERL   
   8 4QB6    NSER  LDN   0  BOPEN
  15 4QTQ          ORS   0  EXMARK(1)           [SET BOPEN   
   8 4R*B          LDCT  0  #400 
  17 4RT2          ORS   0  GIVEX(3)            [SET ONLINE MARKER   
  15 4S#L          FREECORE 2                   [FREE FABSNB 
  21 4SMJ ...#              INPUT BLOCKS ARE NOW CORRECTLY SET UP FOR EXOPEN/WRITEFIP
   8 4T2G ...      BRN      TFIN 
   4 4TRG    #   
   4 4W?6    #   
   4 4WQQ    #   
   4 4X=B    #   
   4 4XQ2    #   
   9 4Y9L    #                  UNANT
   4 4YP=    #   
  21 4_8W    #              THIS SECTION DEALS WITH UNANTICIPATED OPEN PERIS,OTHER   
  20 4_NG    #              THAN MODE #600.IT IS ENTERED FROM PERIXCOD (ON PCA,  
  20 5286    #              PROGRAM UNPLUGGED) IF MODE OF UNANTICIPATED PERI IS  
  19 52MQ    #              *100-*400 OR *1200 (+*2000 &/ *4000).ON ENTRY,   
  15 537B    #              X4 CONTAINS CHANNEL NO IN B14-23 
  18 53M2    #              X5 CONTAINS A COPY OF WORD 0 OF CONTROL AREA 
  20 546L    #              XCA(=EXEC/ANEVENT2) CONTAINS CONTROL AREA ADDRESS.   
  21 54L=    #              ROUTINE CHECKS CONTROL AREA,AND SETS UP EWDAS/GEXD AND   
  21 555W ...#              EWDAS/EXOF BLOCKS FOR EXOPEN/WRITEFIP,THEN GOES  TO OPEN 
   9 55KG    #              FILE,ETC.
   4 5656    #   
   4 56JQ    #   
   7 574B    #DEF  XCA=EVENT2
   4 57J2    #   
   4 583L    #   
  15 58H=    UC             #1200               [CREATE MODE 
   4 592W    #   
   4 59GG    #   
   5 5=FQ    QK2SOUP 
   8 5=_B    #SKI  K6SOUP>199-199
   4 5?F2    (   
  14 5?YL          LDX   0  5                   [CHECK X5
   8 5#D=          SRL   0  15   
   8 5#XW          ANDN  0  #377 
   7 5*CG          SBN   0  6
   8 5*X6          BZE   0  XCH1 
  19 5BBQ ...      SBN   0  20                  [ERROR IF TYPE NOT 6 OR 26   
   8 5DSW          BNZ   0  XCH4 
   7 5F#G    XCH1  LDX   0  5
  18 5FS6          ANDX  0  XCMASK(1)           [TAKE OUT ADDITIVE MODES 
   8 5G?Q          SBN   0  #1200
  17 5GRB          BZE   0  XCH2                [J IF MODE *1200,OK, 
   8 5H?2          ADN   0  #1200
   7 5HQL          LDN   6  4
   8 5J==    XCH3  SBN   0  #100 
  17 5JPW          BZE   0  XCH2                [OR *100-*400,ELSE   
   8 5K9G          BCT   6  XCH3 
  16 5KP6    XCH4  GEOERR   1,SD UNANT          [ERROR,WRONG MODE
   9 5L8Q    XCMASK         #71777   
   4 5LNB    XCH2
   4 5M82    )   
   7 5MML          LDX   6  5
  16 5N7=          ANDN  6  #1777               [GET BASIC MODE  
   7 5NLW          LDN   7  9
  16 5P6G          TXU   6  UC(1)               [J UNLESS CREATE 
  21 5PL6          BCS      NC                  [SET LENGTH OF CA TO 9(OPEN) OR 17(C)
   8 5Q5Q          LDN   7  17   
  14 5QKB    NC    CHECKEXT XCA(2),7,RVIOL,,W,WCOR,HLOCK1
  18 5R52    WCOR  SETNCORE GEXDLEN,3,EWDAS,GEXD    [SET UP GEXD BLOCK,  
   8 5RJL          STOZ     A1(3)
   8 5S4=          LDN   0  A1(3)
   9 5SHW          LDN   1  A1+1(3)  
  16 5T3G          MOVE  0  GEXDLEN-1               [ZEROISE IT  
   7 5TH6          LDN   0  1
  20 5W2Q          STO   0  GIVEX(3)            [SET GIVE MARKER,AND PERI (B0=0) 
  16 5WGB          STO   6  EOPMODE(3)          [STORE  OPEN MODE
   8 62CB          ANDN  4  #1777
  15 62X2          STO   4  EXCHNL(3)           [CHANNEL NO. 
   9 63W=          LDX   0  XCA(2)   
  17 64*W          STO   0  CNR(3)              [CONTROL AREA ADDRESS
   7 64TG          PHOTO    6
  18 65*6          SETNCORE FDCELLS-A1,1,EWDAS,EXOF [SET UP EXOF BLOCK   
   8 65SQ          TESTMOVE 6,MVE
  10 66#B          MHUNTW   3,EWDAS,GEXD 
  17 66S2    MVE   STOZ     BACK1(1)                [CLEAR BACK1,ETC 
   9 67?L          STOZ     BACK2(1) 
   9 67R=          STOZ     COPEN(1) 
   9 68=W          LDN   4  EXMARK(1)
   7 68QG          LDX   6  3
   9 69=6          LDX   3  XCA(2)   
  11 69PQ          FOUTMOVE 2,3,4,7,HLOCK1   
   7 6=9B          LDX   3  6
   8 6=P2    #SKI  K6SOUP>199-199
   4 6?8L    (   
   9 6?N=          TXU   5  EXMARK(1)
  19 6#7W          BCS      XCH4                    [ERROR IF WORD 0 WRONG]  
   4 6#MG    )   
  21 6*76          STOZ     EXMARK(1)           [CLEAR MARKER WORD:-NOW SERIAL NR IS 
  21 6*LQ          LDX   0  5                   [IN ASUSN,FILENAME,ETC IN FIP,ETC.   
  20 6B6B          ANDN  0  #2000               [J IF ADD.MODE *2000 ABSENT (IN  
  19 6BL2          BZE   0  NAD                 [ WHICH CASE *4000 IS NULL)  
   8 6C5L          LDN   0  BSERL
  19 6CK=          ORS   0  EXMARK(1)           [SET 'UNIT SPECIFIED' MARKER 
   8 6D4W          SMO      FX1  
   8 6DJG          LDX   0  SIX7S
  19 6F46          ANDX  0  ASUSN(1)            [MASK SERIAL NR TO 18 BITS   
  16 6FHQ          BZE   0  XINV                [ERROR IF ZERO   
   9 6G3B          STO   0  ASUSN(1) 
   8 6GH2          ANDN  5  #4000
  19 6H2L          BZE   5  NAD                 [J IF ADD.MODE *4000 ABSENT, 
   8 6HG=          LDN   0  BSUPP
  19 6H_W          ORS   0  EXMARK(1)           [ELSE SET 'SUPPRESS LOAD' BIT
   9 6JFG    NAD   LDCH  0  FIP(1)   
  16 6J_6          SBN   0  #41                 [CHECK FILE SPEC 
  16 6KDQ          BNG   0  XINV                [ERROR IF FIRST  
  15 6KYB          SBN   0  #32                 [CHAR OF NAME
  16 6LD2          BPZ   0  XINV                [NOT ALPHABETIC  
  18 6LXL                                       [CHECK REST OF FILENAME  
  15 6MC=          STO   1  AWORK1(2)             [SAVE X1   
  20 6MWW          LDN   7  11                    [COUNT OF CHARS TO BE CHECKED  
  18 6NBG          BCHX  1  NCH                   [POINT TO 2ND CHARACTER
  16 6NW6    NCH   LDCH  0  FIP(1)                [NEXT CHARACTER
   8 6P*Q          SBN   0  10   
  16 6PTB          BNG   0  XOKFN                 [OK IF NUMERIC 
   8 6Q*2          SBN   0  #23  
  15 6QSL          BZE   0  XOKFN                 [OK IF A - 
   8 6R#=          ADN   0  #15  
  16 6RRW          BZE   0  XOKFN                 [OK IF A SPACE 
   8 6S?G          SBN   0  #21  
  16 6SR6          BNG   0  XINV2                 [ERROR IF < A  
   8 6T=Q          SBN   0  #32  
  16 6TQB          BPZ   0  XINV2                 [ERROR IF > Z  
   7 6W=2    XOKFN BCHX  1  /
   8 6WPL          BCT   7  NCH  
  15 6X9=          LDX   1  AWORK1(2)             [RESTORE X1
   8 6XNW          BRN      XCONT
   9 6Y8G    XINV2 LDX   1  AWORK1(2)
   8 6YN6          BRN      XINV 
   5 6_7Q    XCONT   
  10 6_MB          LDX   0  EOPMODE(3)   
   8 7272          SBN   0  #1200
  15 72LL          BZE   0  XCR                 [J IF CREATE 
   8 736=          LDN   0  BOPEN
  16 73KW          ORS   0  EXMARK(1)           [SET OPEN MARKER 
   9 745G          LDX   0  FIP+4(1) 
  19 74K6          BPZ   0  NNEG                [J IF GENERATION NR POSITIVE 
   8 754Q          LDN   0  BHIGH
  19 75JB          BRN      NEWG                [ELSE SET HIGHEST GEN MARKER 
   8 7642    NNEG  SMO      FX1  
   8 76HL          TXU   0  X4096
  19 773=          BCS      NFNS                [J UNLESS GENERATION NR=4096 
   8 77GW          LDN   0  BANY 
  19 782G          BRN      NEWG                [SET ANY GENERATION MARKER   
  19 78G6    NFNS  SLL   0  12                  [PUT GENERATION NR IN B0-11  
   9 78_Q          STO   0  FIP+4(1) 
  19 79FB          LDN   0  BSPEC               [SET SPECIFIED GENERATION BIT
   9 79_2    NEWG  ORS   0  EXMARK(1)
   9 7=DL          LDX   0  FIP+6(1) 
  18 7=Y=          STO   0  ENEWG(3)            [SAVE NEW GENERATION NR  
   8 7?CW          LDCT  0  #400 
  13 7?XG          STO   0  ENEWRP(3)           [AND 
   9 7#C6          LDX   0  FIP+5(1) 
  18 7#WQ          ANDS  0  ENEWRP(3)           [B0 AND B12-23 OF NEW RP 
   9 7*BB          DSA   0  ENEWRP(3)
  17 7*W2          BRN      OFF                 [GO TO CPA AND OPEN  
   4 7B5S ...#   
   8 7B*L ...#      CREATE MODE  
   4 7BKD ...#   
  10 7BT=    XCR   LDN   0  BCREATE+BSPEC
  21 7C#W          ORS   0  EXMARK(1)           [SET CREATE AND SPECIFIED GEN BITS   
   8 7CSG          LDCT  0  #600 
   9 7D#6          ANDX  0  FIP+5(1) 
  18 7DRQ          BZE   0  XINV                [OR INTEGRITY CODE=ZERO  
  19 7F?B          STO   0  FIP+5(1)            [SYFIL BIT/NR OF AREAS = 0   
   9 7FR2          LDX   0  FIP+8(1) 
  17 7G=L          ANDN  0  #77                 [CHECK BUCKET SIZE   
   7 7GQ=          LDN   2  4
   8 7H9W    SNX   LDN   4  16   
   8 7HPG          SRL   4  0(2) 
   7 7J96          SBX   4  0
  16 7JNQ          BZE   4  SOK                 [J IF 1,2,4 OR 8 
   8 7K8B          BCT   2  SNX  
  15 7KN2          BRN      XINV                [ELSE ERROR  
   7 7L7L    SOK   LDN   2  5
  20 7LM=    ZNX   SMO      2                   [CHECK THAT LAST 5 WORDS ARE ZERO
   9 7M6W          LDX   0  FIP+9(1) 
  15 7MLG          BNZ   0  XINV                [ERROR IF NOT
   8 7N66          BCT   2  ZNX  
   8 7NKQ          LDX   0  EDATE
  15 7P5B          STO   0  FIP+6(1)            [INSERT DATE,
   7 7PK2          LDN   0  1
  15 7Q4L          STO   0  FIP+10(1)           [AND EOF=1   
   4 7QJ=    [   
  20 7R3W    OFF   CAPCA    EWDAS,GEXD,EWDAS,EXOF  [GO TO CPA,TAKING DATA BLOCKS,
   4 7R4X ...TFIN
  10 7R5= ...      MHUNTW   1,EWDAS,GEXD 
  13 7R5K ...      LDCT  5  6                   [*DA 
  15 7R5Y ...      ADX   5  EXCHNL(1)           [TYPE/UNIT NO
  17 7R6? ...      LDX   7  GIVEX(1)            [X7 -VE IF COMMAND   
  20 7R6L ...      HLSXONCH XBRK,5,XER1         [SEE IF OK WITH HLS TO CARRY ON  
  10 7R6P ...      MHUNTW   1,EWDAS,EXOF 
   9 7R6S ...      LDX   0  EXMARK(1)
   9 7R6X ...      ANDN  0  BCREATE  
   8 7R72 ...      BNZ   0  WFIP 
   9 7R75 ...      ACROSS   EXOPEN,2 
  10 7R78 ...WFIP  ACROSS   WRITEFIP,2   
   4 7R7# ...[   
   9 7R7M ...[     HLS REJECTS OPEN  
   4 7R82 ...[   
   4 7R8* ...XBRK
   7 7R8N ...      NGNC  6  1
   4 7R93 ...XER1
  20 7R9B ...      LDN   6  0                   [X6=0 IF REJECT ;X6=1 IF BREAKIN 
  10 7R9P ...      MFREEW   EWDAS,EXOF   
  10 7R=4 ...      MFREEW   EWDAS,GEXD   
  15 7R=C ...      BZE   6  XER2                [J IF REJECT 
  15 7R=Q ...      BNG   7  VCOM                [J IF COMMAND
  16 7R?5 ...      STEPBACK                     [PUT ONR BACK ONE
   7 7R?D ...      PROGBRKIN 
   4 7R?R ...VCOM
   7 7R#6 ...      COMBRKIN  
   4 7R#F ...[   
   9 7R#S ...[     REJECTION BY HLS  
   4 7R*7 ...[   
   4 7R*G ...XER2
  15 7R*T ...      BNG   7  VCOM1               [JIF COMMAND 
   7 7RB8 ...      STEPBACK  
  17 7RBH ...      MONOUT   EWHLSRJ             [OUTPUT HLS MESSAGE  
   4 7RBW ...XPAR
  14 7RC9 ...      OUTPACK  5,1,PERINAME,XPAR   [*DA FAIL
   9 7RCJ ...      ILLEGAL  PERFAIL  
   5 7RCX ...VCOM1   
   9 7RD= ...      COMERR   ERHLSONL 
   4 7RDK ...[   
   4 7RDY ...[   
   4 7S36    [   
  18 7SGQ    RVIOL ILLEGAL  NOCA                [RESERVATION VIOLATION   
   4 7T2B    [   
  18 7TG2    XINV  FREECORE 1                   [INVALID CONTROL AREA,   
  17 7T_L          FREECORE 3                   [FREE DATA BLOCKS AND
  14 7WF=          ILLEGAL  INVAL               [ILLEGAL 
   4 7WYW    #   
   4 7XDG    #   
   4 7XY6    #   
  10 7YCQ          MENDAREA 30,K99SOUP   
   4 7YXB    #END
   8 ____ ...02572777000100000000
