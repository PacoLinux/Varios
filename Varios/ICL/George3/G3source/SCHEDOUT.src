  17 22FL ...#SEG  SCHEDOUT                     [DEK BEASLEY    :CENT
   8 22_=    #OPT  K0SCHEDOUT=0  
  11 23DW    #LIS  K0SCHEDOUT>K0ALLGEO>K0LLS 
   7 23YG          8HSCHEDOUT
   4 24D6    #   
   4 24XQ    #   
  21 25CB    #           THIS SEGMENT IS THE PART OF THE PROGRAM CONTROLLER WHICH DEC
  21 25X2    #     WHICH OBJECT PROGRAM SHOULD VACATE CORE SPACE TO MAKE ROOM FOR ANO
  21 26BL    #     UNPLUGGING THE PROGRAM FROM EXECUTIVE IF NECESSARY BEFORE INITIATI
   7 26W=    #     SWAPOUT.  
  14 27*W    #           THE ENTRY POINTS ARE AS FOLLOWS:
   4 27TG    #   
  18 28*6    #     K1 :     MAIN ENTRY TO SELECT A PROGRAM FOR SWAPOUT   
  21 28SQ    #     K2 :     ENTRY WHEN QUOTA NOT MET OR FINGER IS ON SOME SPECIFIC   
   9 29#B    #              PROGRAM  
  20 29S2    #     K3 :     CORE UNJAMMER WANTS A SWAPOUT SO LOOK BY SLLSOUT 1ST,
   4 2=?L    #   
  11 2#=6          SEGENTRY K1SCHEDOUT,XX1S  
  11 2#PQ          SEGENTRY K2SCHEDOUT,XX2S  
  11 2*9B          SEGENTRY K3SCHEDOUT,XX3S  
   2 2BN=
   4 2C7W    UNPL
   9 2CMG          LINK     UNPLUG,7 
   9 2D76    P11   PAIR  CHARGESC,1  
   5 2F6B    #SKI  G3
  14 2FL2    P13   PAIR  SWAP,2                 [SWAP OUT
   5 2G5L    #SKI  G4
   9 2GK=    P13   PAIR  SWAPOUT,1   
  17 2H4W    P17   PAIR  PERFAIL,2              [ON-LINE PERI FAIL   
  17 2JHQ    P18   PAIR     EVENTS,5            [FOR PROGRAM TIMEUP  
   4 2K3B    #   
   4 2KH2    #   
   4 2L2L    TJOB
  15 2LG=          FINDJOBQ 3,6,(GEOERR)     [X3 -> JOBBLOCK.
   7 2L_W          EXIT  7  0
   4 2MFG    [   
  20 2M_6    [  THIS SUBROUTINE DECIDES WHAT TO SWAPOUT TO MAKE WAY FOR A SWAPIN 
   4 2NDQ    [   
  20 2NHD ...[  WE INSPECT ALL THE SWAPPED IN PROGRAMS FOR EACH JOB AND COMPARE  
  17 2NL6 ...[  ITS CP WITH THAT OF THE PROGRAM TO BE SWAPPED IN.IF  
   4 2NNS ...[   
  18 2NRG ...[  (1) CP IS >,THEN NOT CONSIDERED AS A CANDIDATE FOR S/O   
   4 2NW8 ...[   
  21 2NYW ...[  (2) CP <OR= BUT DIFFERENCE IS < D(SWAPDIFF) THEN IF A MOP ENVIRONMENT
  20 2P3J ...[      EXISTS & ITS CP IS <OR= TO HIGHEST CP OF ANY MOP JOBS WAITING
  21 2P6= ...[      TO COME IN(VALUE IN AWORK4) & IT HAS EXPIRED ITS TIMESLOT THEN   
  12 2P8Y ...[      IT IS CONSIDERED AS IN (3)   
   4 2P?L ...[   
  21 2PB# ...[  (3) CP <OR= BUT DIFFERENCE >D,THEN ITS CP IS COMPARED WITH THE CP OF 
  13 2PF2 ...[      THE BEST CANDIDATE FOUND SO FAR  
  14 2PHN ...[      (A) IF >,THEN PROGRAM NOT CONSIDERED 
  17 2PLB ...[      (B) IF <,THEN PROGRAM MADE BEST CANDIDATE SO FAR 
  21 2PP4 ...[      (C) IF =,SIZES OF THE TWO ARE COMPARED WITH THE AMOUNT WHICH LLS 
  20 2PRQ ...[          UNABLE TO OBTAIN.THE SMALLEST PROGRAM LARGER THAN THIS IS
  17 2PWD ...[          CHOSEN,IF ONE EXISTS,& IF NOT,THE LARGEST.   
   4 2P_6 ...[   
  14 2Q3S ...[  ON ENTRY,ACOMMUNE1 = AMOUNT REQ'D BY LLS 
  19 2Q6G ...[           AWORK4 +VE IF MOP ENVIRONMENT,CONTENTS BEING HIGHEST
  19 2Q98 ...[                   PRIORITY OF ANY MOP JOB WAITING TO GET IN   
  20 2Q?W ...[           X4 = CORE PRI. OF PROGRAM WHICH LLS FAILED TO SWAP IN   
   4 2QC=    [   
  18 2QWW    [   LINK X7,EXITS +0  IF NONE OR ALREADY SWAPOUT IN PROGRESS
  16 2RBG    [                 +1  IF SELECTED REQUIRES UNPLUG   
  16 2RW6    [                 +2  IF SELECTED ALREADY  UNPLUGGED
   4 2S*Q    [   
  19 2STB ...[  ON EXIT,X3 -> JOB BLOCK OF CANDIDATE SELECTED FOR SWAP OUT   
  11 2T*2 ...[          X1 = FX1, X2 = FX2   
   4 2TSL ...[   
  13 2W#= ...[  DURING SUBROUTINE,X3 -> ALONG JOBQ   
  18 2WRW ...[          X5 = CP OF BEST CANDIDATE SO FAR(I.E. LOWEST CP) 
  11 2X?G ...[          X6 -> ITS JOB BLOCK  
  19 2XFQ ...[     IF SWAPPING OF TRULY DORMANT PROGRAMS IS SWITCHED OFF -   
  20 2XN2 ...[          AWORK1 = CP OF BEST TRULY DORMANT CANDIDATE FOUND SO FAR 
  12 2XW= ...[          AWORK2 = ITS JOB BLOCK   
  18 2Y4G ...[      NB  TRULY DORMANT PROGRAMS ARE THE BEST CANDIDATES   
   4 2Y=Q    [   
   4 2YQB    [   
   5 2_=2    SLLSOUT 
   8 2_PL          LDX   1  FX1  
   8 329=          LDX   2  FX2  
   9 32NW    #SKI  K6LLSTEST>499-499 
  12 338G          TRACEVER ACOMMUNE1(2),COREREQD
  19 33N6          JBS      (7),,ASWOUTING      [SWAPOUT ALREADY IN PROGRESS 
   7 347Q          LDN   6  0
   7 34MB ...      LDN   5  0
   6 34X8 ...#UNS INOSTDP
   4 3572 ...(   
   9 35BS ...      STOZ     AWORK1(2)
   9 35LL ...      STOZ     AWORK2(2)
   4 35WD ...)   
   8 366=          LDN   3  BJOBQ
  18 36KW    ROUND LDX   3  FPTR(3)             [X3 -> NEXT JOBQ BLOCK   
   9 375G          BXE   3  CXJO,QEND
  10 37K6          JBC      ROUND,3,JBSI 
   9 384Q    #SKI  K6LLSTEST>499-499 
  11 38JB          TRACEVER JOBNUM(3),SWAPDIN
  15 38S8 ...[      DONT SWAP OUT REALTIME OR BEING PLUGGED  
  17 3942          JMBS     ROUND,3,JBREEF,JBPLUGGING,JBAXES,JBSTOP  
   6 394R ...#UNS INOSTDP
   4 395J ...(   
  11 396* ...      JBS      YNOTTD,3,JBRUN   
  18 3976 ...                                     [J IF NOT TRULY DORMANT
  17 397X ...      LDX   0  AWORK2(2)             [JOB BLK ADDR OF   
  21 398N ...                                    [BEST TRULY DORMANT PROGRAM SO FAR  
  20 399F ...      BZE   0  YBESTYET             [J IF THIS THE FIRST TR DORMANT 
  19 39== ...      LDX   0  AWORK1(2)           [BEST TR DORMANT CP SO FAR   
   9 39?3 ...      LDEX  1  ACOREP(3)
  17 39?S ...      BXL   0  1,ROUND             [J IF THIS CP BIGGER 
  10 39#K ...      BXU   0  1,YBESTYET   
   6 39*B ...YTESTSIZE   
   9 39B7 ...      SMO      AWORK2(2)
   9 39BY ...      LDX   0  JCSIZE   
  18 39CP ...      BXL   0  ACOMMUNE1(2),YUNDER [J IF SIZE SO FAR < REQD 
   9 39DG ...      LDX   0  JCSIZE(3)
  12 39F? ...      BXL   0  ACOMMUNE1(2),ROUND   
  18 39G4 ...                                  [J IF SIZE OF CAND. < REQD
   9 39GT ...      SMO      AWORK2(2)
  19 39HL ...      BXGE  0  JCSIZE,ROUND     [J IF CAND.SIZE >OR= BEST SO FAR
   5 39JC ...YBESTYET
  15 39K8 ...#          WE HAVE NEW TRULY DORMANT BEST SO FAR
  17 39K_ ...      STO   3  AWORK2(2)        [STORE JOB BLK ADDRESS  
   9 39LQ ...      LDEX  0  ACOREP(3)
  18 39MH ...      STO   0  AWORK1(2)       [STORE NEW TRULY DORMANT CP  
   8 39N# ...      BRN      ROUND
   5 39P5 ...YUNDER  
   9 39PW ...      LDX   0  JCSIZE(3)
  20 39QM ...      BXGE  0  ACOMMUNE1(2),YBESTYET [J IF CAND.SIZE > REQD AMOUNT  
   9 39RD ...      SMO      AWORK2(2)
  18 39S9 ...      BXL   0  JCSIZE,ROUND   [J IFCAND.SIZE < BEST SO FAR  
  18 39T2 ...      BRN      YBESTYET        [USE THIS AS NEARER REQD AMT 
   5 39TR ...YNOTTD  
   9 39WJ ...      LDX   0  AWORK2(2)
   8 39X* ...      BNZ   0  ROUND
   4 39Y6 ...)   
  20 39YX ...      LDEX  1  ACOREP(3)           [LOAD CP, NB AVOIDING TOP CHAR.  
  18 39_N ...      BXL   4  1,ROUND             [J IF CP > INCOMING CP   
  21 3=3= ...      LDX   0  4                   [DO NOT CONSIDER TIMESLOT EXCEEDED   
  19 3=GW ...      SBX   0  1                   [  IF INCOMING CP-THIS CP IS 
  16 3?2G ...      BXGE  0  ISWAPDIFF,XLOWCP    [  >OR= SWAPDIFF 
   9 3?G6 ...      LDX   0  AWORK4(2)
  18 3?_Q ...      BZE   0  ROUND               [J IF NOT MOP ENVIRONMENT
  20 3#FB ...      BXL   0  1,ROUND             [ONLY USE IF CP <OR= MOP JOBS CP 
   9 3#_2 ...      LDX   0  HTIMES(3)
  19 3*DL ...      BXL   0  ATIMES(3),ROUND     [J IF TIMESLOT NOT EXPIRED   
   5 3*Y= ...XLOWCP  
  18 3BCW ...      BZE   5  XBESTYET            [J IF NO BEST SO FAR YET 
  19 3BXG ...      BXE   5  1,TESTSIZE          [J IF CP'S = TO COMPARE SIZE 
  20 3CC6 ...      BXL   5  1,ROUND             [J IF BEST SO FAR HAS LOWER CP   
  20 3CWQ ...      BRN      XBESTYET            [THIS CANDIDATE NEW BEST SO FAR  
   5 3DBB ...TESTSIZE
   7 3DW2 ...      SMO      6
   9 3F*L ...      LDX   0  JCSIZE   
  20 3FT= ...      BXL   0  ACOMMUNE1(2),UNDER  [J IF SIZE OF BEST SO FAR < REQ'D
   9 3G#W ...      LDX   0  JCSIZE(3)
  19 3GSG ...      BXL   0  ACOMMUNE1(2),ROUND  [J IF SIZE OF CAND. < REQ'D  
   7 3H#6 ...      SMO      6
  20 3HRQ ...      BXGE  0  JCSIZE,ROUND        [J IF CAND.SIZE >OR= BEST SO FAR 
  18 3J?B ...XBESTYET                           [WE HAVE NEW BEST SO FAR 
  18 3JR2 ...      LDX   6  3                   [STORE JOB BLOCK ADDR.   
  15 3K=L ...      LDEX  5  ACOREP(3)           [STORE NEW CP
   8 3KQ= ...      BRN      ROUND
  21 3L9W ...UNDER                              [SIZE OF BEST SO FAR < REQ'D AMOUNT  
   9 3LPG ...      LDX   0  JCSIZE(3)
  21 3M96 ...      BXGE  0  ACOMMUNE1(2),XBESTYET  [J IF CAND. SIZE > REQ'D AMOUNT   
   7 3MNQ ...      SMO      6
  20 3N8B ...      BXL   0  JCSIZE,ROUND        [J IF CAND. SIZE < BEST SO FARS  
  20 3NN2 ...      BRN      XBESTYET            [USE THIS AS NEARER REQ'D AMOUNT 
   4 3Y2B    QEND
   8 3YG2          LDX   1  FX1  
   6 3YHY ...#UNS INOSTDP
   4 3YKW ...(   
   9 3YMS ...      LDX   3  AWORK2(2)
  19 3YPQ ...      BZE   3  QEND1                [J IF NO TRULY DORMANT FOUND
   7 3YRN ...      EXIT  7  2
   5 3YTL ...QEND1   
   4 3YXJ ...)   
  16 3Y_L          BZE   6  (7)                 [J IF NONE FOUND 
   7 3_F=          LDX   3  6
  10 3_YW          JBS      PLUG,3,JBPLUG
   9 42DG    #SKI  K6LLSTEST>499-499 
  12 42Y6          TRACEVER JOBNUM(3),UNPLUGED   
  18 43CQ          EXIT  7  2                   [UNPLUGGED ALREADY EXIT  
   4 43XB    [   
  19 44C2    [ IF PLUGGED SET STOP WORD AND TRY TO STOP IT SO CAN FPUT PCA   
  13 44WL    [    IF WE CAN'T WE TRY ALGORITHM AGAIN 
   4 45B=    [   
  14 45TW    PLUG  FPCAJO   1,3                 [X1-> PCA
   9 46*G    #SKI  K6LLSTEST>499-499 
  11 46T6          TRACEVER JOBNUM(3),PLUGIN 
  12 47#Q          JBS      (GEOERR),1,ASTOPSLOT 
  20 47SB          BS       1,ASTOPSLOT         [SET TIME SLOT EXPIRED STOP BIT  
  19 48#2 ...      STOPIT   1               [ ENSURE IT IS STOPPED (DUALS)   
   9 48RL          LA       ASTOPSLOT
  20 49?=          BXU   0  ASTOP(1),YIELD      [IF NOT STOPPED ONLY COS OF HERE 
  10 49QW          BS       1,ASTOPEXEC  
  17 4==G          LDX   1  FX1                 [PCA CAN BE ACTIVE   
   7 4=Q6          EXIT  7  1
  18 4?9Q    YIELD BC       1,ASTOPSLOT         [ENSURE LEAVE BIT CLEAR  
   9 4?PB    #SKI  K6LLSTEST>499-499 
  11 4#92          TRACEVER ASTOP(1),STOPWORD
   8 4#NL          LDX   1  FX1  
  16 4*8=          EXIT  7  0                   [SAY NONE FOUND  
   4 4*MW    [   
   9 4MDB ...[ ENTRY FROM CORESHED   
  14 4MY2    [     CORESHED  IF PROGRAM MOVE FRUSTRATED  
   4 4NX=    [   
  15 4QTQ    XX2S                                [K2SCHEDOUT 
   4 4R*B    [   
   4 4RT2    [   
  15 4S#L    [  MAIN ENTRY: FROM CORESHED IF SWAPOUT REQUIRED
   4 4SS= ...[   
  13 4T?W ...[     ENTRY CONDITIONS FOR K1 & K2 ARE  
  13 4TRG ...[       AWORK4 -VE IF MOP ENVIRONMENT   
  18 4W?6 ...[       ACOMMUNE1 = AMOUNT THAT LLS REQ'D(SET BY CORESHED)  
  19 4WQQ ...[       X6 - JOB NO. OF PROGRAM WHICH FAILED TO GET SWAPPED IN  
   4 4X=B    [   
  15 4XQ2    XX1S                                [K1SCHEDOUT 
   9 4Y9L    #SKI  K6LLSTEST>499-499 
  11 4YP=          TRACEVER AWORK4(2),SCHOUT1
   7 4_49 ...#UNS  ADPLLSKIP 
   4 4_C8 ...(   
  17 4_Q7 ...      LDN   0  2                    [- DATAPASS COUNT   
  16 5256 ...      STO   0  ADPSWPSW             [- (LLS SWAPOUT)
   4 52D5 ...)   
  20 52R4 ...      CALL  7  TJOB                [FIND JOB BLOCK FOR JOB NO. IN X6
  19 53=N ...      LDEX  4  ACOREP(3)           [THEN FIND ITS CORE PRIORITY 
   5 57XK ...SELECT  
   9 58=J ...      CALL  7  SLLSOUT  
  13 58KH ...      BRN      XCORE               [NONE
  16 58YG ...      BRN      UNPLUG              [UNPLUG REQUIRED 
  16 59?F ...      BRN      SWAP                [JUST SWAP OUT   
   5 59LD ...XCORE   
   9 59_C ...      LDX   6  CJJOBNO  
   7 5=#B ...      COBJUSE 4 
  21 5=M* ...      MOBJQUOTA MIN                [IF REACHING OQ MIN,NOWT ELSE TO DO  
   9 5?2# ...      BXGE  4  0,NOBAT  
  19 5?*? ...      LDN   5  0                   [INDICATE NOT REALTIME FRAG. 
  19 5?N= ...      ACROSS LLSMESS,1             [OUTPUT APPROPRIATE MESSAGE  
   4 5#39 ...R11 
   7 5#B8 ...#UNS  ISTDPSTATS
   9 5#P7 ...      TRACEDP ASCHEDFPC 
   8 5*46 ...      BRN      R12  
   4 5*CG    [   
  19 5JPW    SWAP  LDN   1  P13(1)              [UNPLUGGED: X1-> SWAP LINK   
   8 5K9G          BRN      MEET 
   5 5KP6    UNPLUG  
   9 5L8Q    #SKI  K6LLSTEST>499-499 
  12 5LNB          TRACEVER ACOMMUNE1(2),UNPLUG  
  16 5M82          LDN   1  UNPL(1)             [X1-> UNPLUG LINK
   8 5MML    MEET  FPCAJO   2,3  
  17 5N7=          LINKSET  2,0(1)              [SET APPROPRIATE LINK
  17 5N=4 ...      MBI      3,JBSI,JBBEING      [MARK BEING SWAPPED  
   4 5N#W ...[   
   4 5NCN ...[   
  20 5NGG ...[        THE   FOLLLOWIG CURES B3211WHEREBY BEFORE STARTIG SWAPOUT  
  13 5NK# ...[        WE  DID NOT DO A PROGAXES !!   
  16 5NN6 ...[           DONE     11TH    JULY    75       :CENT 
   4 5NQY ...[   
  10 5NTQ ...      PROGAXES    3,(GEOERR)
  12 5NYJ ...      TRACE    JOBNUM(3),SWAPOUTS   
   4 5P3B ...[   
  15 5P6G          FPUT                         [LET IT RUN  
  19 5PL6          BS       ,ASWOUTING          [INDICATE SWAPOUT IN PROGRESS
   4 5Q5Q    #   
   7 5QKB    #     TERMINATE 
  21 5R52    R12   LDX   2  FX2                  [NO SWAPPABLE PROGS FOUND BY SLLSOUT
   9 5RJL    #SKI  K6LLSTEST>499-499 
  11 5S4=          TRACEVER COBJFAIL,NONFOU  
  10 5SHW          BXE   2  BSWINT,NOBAT 
  20 5SW? ...      BC ,ASWCORJAM                [CLEAR UNJAM IN PROGRESS MARKER  
   9 5T8N ...      ACROSS CHARGESC,1 
   5 5TH6    NOBAT   
   9 5W2Q          OFF      BSTRAT   
   4 5WGB    R12C
   8 5X22    R12B  LDX   2  FX2  
   7 5XFL          ENDCCYCLE 
   4 6C5L    [   
   4 6CK=    [   
  19 6D4W    [  ENTRY ON BEHALF OF CORE UNJAMMER TO SEE IF ANYTHING SWAPPABLE
  16 6DJG    [     IF NOT WE RETURN TO UNJAMMER TO DECIDE WHETHER
  11 6F46    [      WE SHOULD ENTER JAMSWAP  
   4 6FHQ    [   
   4 6G3B    XX3S
   9 6GH2    #SKI  K6LLSTEST>499-499 
  10 6H2L          TRACEVER FX1,SCHOUT3  
  18 6H6H ...      JBS      R12,,ASWOUTING     [J IF SWAPOUT IN PROGRESS 
   7 6H=D ...#UNS  ADPLLSKIP 
   4 6HB* ...(   
  17 6HG= ...      LDN   0  1                    [- DATAPASS COUNT   
  17 6HL7 ...      STO   0  ADPSWPSW             [- (COREJAM SWAPOUT)
   4 6HQ4 ...)   
  21 6HT_ ...      LDN   4  63                  [SET CP OF 63 AS WE REQUIRE ANYTHING 
  21 6H_W          STOZ     ACOMMUNE1(2)        [WE WANT ANYTHING SLLSOUT CAN GIVE US
  18 6JFG ...      STO   4  AWORK4(2)           [TERAT AS MOP ENVIRONMENT
   9 6KDQ          CALL  7  SLLSOUT  
   8 6KYB          BRN      NONE 
  17 6LD2          BRN      UNPLUG              [FOUND:NEEDS UNPLUG  
  18 6LXL          BRN      SWAP                [FOUND:NO UNPLUG NEEDED  
  17 6MC=    NONE  ACROSS   UNJAMMER,2          [TRY SPEC SWAPOUT NOW
   4 6NW6    #END
   8 ____ ...62254301000300000000
