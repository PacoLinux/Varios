   9 22FL    #LIS K0SETUPARA>K0ALLGEO
  15 22_= ...      SEG      SETUPARA,84,OWEN N JOHN(:CENT)   
   4 2394 ...[   
  15 23DW ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   4 23NN ...[   
   4 23YG    [   
  11 24D6          SEGENTRY K1SETUPARA,XSTRT 
  12 24K# ...      SEGENTRY K2SETUPARA,XTENTBLOK 
  15 24QG ...      FSHENTRY K3SETUPARA,XALTVALUE,,XALTVALUE  
   4 24XQ    [   
  20 25CB    [     THIS SEGMENT IS PART OF EARLY MORNING START.ITS FUNCTION IS AS
   7 25X2    [      FOLLOWS:-
  21 26BL    [                  1) SET THE TIME AND DATE UP AND INDICATE THEY ARE TO 
  18 26W=    [                           BE O/P BY THE FIRST SRING CLEAN 
   4 27*W    [   
  21 27TG    [                  2) CHECK THAT THE IDENTITY MACRO HAS BEEN IMPLEMENTED
   4 28*6    [   
  21 28SQ    [                  3) COMPARE ALL IPS IN PARAMINDEX WITH THEIR MAX/MIN  
  21 29#B    [                           VALUES IF TEST FAILS WE TAKE THE FIXED CORE 
  20 29S2    [                           DEFAULT AND PUT DEFAULT INTO PARAMINDEX 
   4 2=?L    [   
  17 2=R=    [                  4) PERFORM CERTAIN SPECIAL CHECKS:-  
  18 2?=W    [                           MOPLIMIT+BACKGROUND <= JOBLIMIT 
  17 2?QG    [                           MOPJOBS+BACKJOBS <= JOBLIMIT
  18 2#=6 ...[                           OBJECTQUOTA+CHAPTERQUOTA < AFREE
  21 2*P2    [                           COREOBJECT  < AFREE (G3) OR < EBSWAPSP (G4) 
  21 2B8L    [                           SIZEDEFAULT < AFREE (G3) OR < EBSWAPSP (G4) 
  16 2BN= ...[                           MAXQUOTA < AFREE   (G4) 
   4 2C7W    [   
  21 2CMG    [                       THE CHECK IS REPEATED UNTIL ALL POSSIBLE DEFAULT
  18 2D76    [                                           VALUES ARE TAKEN
   4 2DLQ    [   
  21 2F6B    [                  5) IF VALUE IN PARAMINDEX IS UNSET - THE DEFAULT IS  
  19 2FL2    [                           TAKEN - IN THIS CASE NO MESSAGE O/P 
   4 2G5L    [   
   7 2GK=    [       NOTE:   
  21 2H4W    [              ONCE AN IP DEFAULT IS TAKEN NO FURTHER CHECKS ARE MADE - 
  14 2HJG    [         E.G   SO IF DEFAULT > MAX VALUE   
  20 2J46    [                                *** BAD LUCK -- DEFAULT STILL TAKEN
   4 2JHQ    [   
  11 2JKN ...#UNS  IPEXTENSION+1*IPEXTENSION 
   7 2JML ...#DEF  XIPEXT=1  
  11 2JPJ ...#UNS  IPEXTENSION+1*IPEXTENSION 
   4 2JRG ...#SKI
   7 2JTD ...#DEF  XIPEXT=0  
   5 2K3B    #PAGE   
  17 2KH2    [       THE FOLLOWING TABLE IS A LIST OF SPECIAL IP'S   
   4 2L2L    [   
  20 2LG=    [               --  LOCATION =  FIXED CORE ADDRESS (TO BE HELD AS AN
  20 2L_W    [                                        ADDR. REL. TO INSTPARAM )  
   4 2MFG    [   
  21 2M_6    [               --  BRANCH   =  PLACE WHERE RELAVENT CODING IS FOR CHECK
   4 2NDQ    [   
   4 2NYB    [   
  13 2PD2    TABIPST        [    LOCATION     BRANCH 
   4 2PXL    [   
  13 2QC=          TABRN        BACKGROUND  , +YBGD  
  13 2QWW          TABRN        BACKJOBS    , +YBCJ  
  18 2RBG          TABRN        AOBJCORES   , +YCOR         [COREOBJECT  
  13 2RW6          TABRN        IMOPJOBS    , +YMOJ  
  13 2S*Q          TABRN        IMOPLIMIT   , +YMOL  
  13 2STB          TABRN        JOBLIMIT    , +YJOL  
  18 2T*2          TABRN        COREDEFAULT , +YCOR         [SIZEDEFAULT 
  18 2WRW          TABRN        COBJQUOTA   , +YOBJ         [OBJECTQUOTA 
  18 2X?G          TABRN        CHAPQUOTA   , +YCAP         [CHAPTERQUOTA
  18 2Y=Q    #SKI G4                            [THESE ARE FOR G4 ONLY :-
  17 2YQB ...      TABRN        AMAXQUOTA   , +YMAX         [MAXQUOTA
  19 338G    #DEF TABIPLGN = 0? - TABIPST       [LENGTH OF IP SPECIAL TABLE  
   5 33N6    #PAGE   
   4 347Q    [   
  15 34MB    [    NOW WE SET UP THE REQUIRED POINTER WORDS :-
   4 3572    [   
  17 35LL    [         WORD:  XDEFAULT  =   B2  - IF DEFAULT TAKEN =1
  17 366=    [                              B5  - IF IP WAS UNSET  =0
  17 36KW    [                              B8  - IF CHECK FAILED  =1
  20 375G    [                              B11 - IF  ERROR HEADING BEEN O/P = 1 
  18 37K6    [                              B14 - IF TRACE/CONTEXT IP=1  
  16 384Q    [                              B17 - IF CONTEXT =1  
   4 38JB    [   
   9 3942    #DEF XDEFAULT = AWORK4  
   4 39HL    [   
  20 3=3=          BITDEFS  XDEFAULT,2,TOOKDEF,,,UNSETDEF,,,TESTFAIL,,,VANYERR   
   4 3=GW    [   
  14 3?2G          BITDEFS  XDEFAULT,14,TRACON,,,TCONT   
   5 3?G6    #PAGE   
   4 3?_Q    [   
  21 3#FB    [    AS GEORGE IS PURE CODE WE MUST SOMEHOW HOLD THE VALUE OR POINTERS  
  21 3#_2    [    TO THE VALUE OF THE SPECIAL IP'S AS WE DO OUR SCAN OF PARAMINDEX   
  12 3*DL    [    IN ORDER TO PERFORM THE CHECKS 
   4 3*Y=    [   
  21 3BCW    [              SO WE CREATE A 13 WORD ADATA/CSTORE BLOCK TO CONTAIN THE 
  18 3BXG    [    INFORMATION. THE ADATA BLOCK IS IN GROUPS OF 4 WORDS   
   4 3CC6    [   
  19 3CWQ    [                  THE VERY FIRST WORD (XCALC) IS A POINTER TO  
  16 3DBB    [                        WHERE WE ARE IN THE BLOCK  
   4 3DW2    [   
  19 3F*L    [                            A GROUP OF  4 WORDS CONSITS OF :-  
   4 3FT=    [   
  21 3G#W    [          WORD  1 - TELLS OF THE 3 IP'S WHETHER THE DEFAULT HAS BEEN   
  18 3GSG    [                      TAKEN  AND  WHY  -- STRUCTURED AS:-  
   4 3H#6    [   
  21 3HRQ          BITDEFS  0,0,,,TOOK1,,,UNSET1,,,TOOK2,,,UNSET2,,,TOOK3,,,UNSET3   
   4 3J?B    [   
  21 3JR2    [          WORD  2 - IF A1 DEFAULT HAS BEEN TAKEN THEN THE VALUE OF THE 
  21 3K=L    [                         [P DEFAULT ELSE POINTER TO POSITION IN PARMNDX
   4 3KQ=    [   
  15 3L9W    [          SIMILARY WORDS 3+4, THE CHECK BEING:-
   4 3LPG    [   
  14 3M96    [                 WORD 2 + WORD 3  <= WORD 4
   4 3MNQ    [   
  12 3N8B    [    SET UP  POINTERS TO WORD 1'S   
   7 3NN2    #DEF  XDUMP=A1  
   8 3P7L    #DEF  XCALC=XDUMP+1 
   9 3PM=    #DEF  XCALCLIMIT=XCALC+1
  18 3Q6W    #DEF XCALCJOB = XCALCLIMIT + 4     [JOBLIMIT/BACKJOB/MOPJOB 
  18 3R66    #DEF XCALCQUOTA = XCALCJOB + 4     [CHAPTERQUOTA/COREOBJECT 
   5 3RKQ    #SKI G4 
   9 3S5B    #DEF  XAFEB = EFSWAPSP  
   8 3SK2    #OPT XAFEB = AFREE  
   5 3T4L    #PAGE   
   4 3TJ=    [   
   8 3W3W    [    FATAL  ERRORS  
   4 3WHG    [   
  21 3X36    YBRK  GEOERR   1,SETPARAB          [BREAK-IN ON ATTEMPT TO OPEN PARAMNDX
  21 3XGQ    YFAL  GEOERR   1,OPENFAIL          [SOMETHINK WRONG ON OPENING PARAMNDX 
   5 3Y2B    #PAGE   
   5 3YG2    TRACEIP 
   4 3Y_L    [   
  21 3_F=    [   THE FOLLOWING ARE THE VALUE OF IPNUM FOR CONTEXT + THE TRACING LEVEL
  21 3_YW    [       IP'S  OVER WHICH SPECIAL COMPARISIONS OF MAX/MIN ARE REQUIRED   
   4 42DG    [   
  17 42Y6                  +ICONTEXT-INSTPARAM  [ADDR. OF  CONTEXT   
  17 43CQ                  +JOBTRACE-INSTPARAM  [          JOBTRACE  
  17 43XB                  +IMINTRACE-INSTPARAM [          MINTRACE  
  17 44C2                  +IMOPTRACE-INSTPARAM [          MOPTRACE  
  17 44WL                  +IOPTRACE-INSTPARAM  [          OPTRACE   
  10 45B=    #DEF TRACEED =  0?-TRACEIP  
   5 45TW    #PAGE   
   4 46*G    [   
  12 46T6    [    INTERFACE  WITH  MESSAGE SYSTEM
   4 47#Q    [   
  19 47SB    WIDFAIL        +IDISACES           [THIS RESTORE HAS NO IDENTITY
  19 48#2    WIPHEAD        +IPDEFHEAD          [IP'S RESET TO DEFAULT ARE:- 
  14 48RL    WIPLIS         +IPDEFSET           [ %A = %B
   8 49?=    UNSC           +5   
   9 49QW    UNSET          5HUNSET  
  20 4==G    #DEF ULNOUTB      = 6                   [LGTH OF A STANDARD OUTBLOCK
  19 4=Q6                                       [    NUMBER=1 W : IPNAME= 3 W
   5 4?9Q    #PAGE   
   4 4?PB    [   
  20 4#92    [    THERE NOW FOLLOWS THE  3  SUBROUTINES REQUIRED TO HANDLE THE   
  11 4#NL    [            ADATA/CSTORE  IP'S 
   4 4*8=    [   
  20 4*MW    [       1)SINPUT  -- PUTS  THE IP INTO THE BLOCK AND UPDATES WORD 1 
  21 4B7G    [                                CALLING O/P ROUTINES IF DEFAULT TAKEN  
  21 4BM6    [       2)SOUTPUT -- TAKES THE 3 IP'S OUT OF ADATA BLOCK AND PLACING IN 
  19 4C6Q    [                         X4,X5,X6  SUCH THAT  X4+X5 TO BE <= X6
   4 4CLB    [   
  21 4D62    [       3)STAKEDEF -- SEES IF WHEN CHECK FAILS WHICH DEFAULT TO BE TAKEN
  21 4DKL    [                                                                  NEXT 
   4 4F5=    [   
   5 4FJW    SINPUT  
   4 4G4G    [   
   7 4GJ6    [    RULE  :-   
  20 4H3Q    [              IF DEFAULT TAKEN THEN DEFAULT VALUE IS STORED IN THE 
  21 4HHB    [     ADATA/CSTORE AND RELAVENT TOOK? IS SET. OTHERWISE THE RELAATIVE   
  19 4J32    [     POSITION IN PARAMINDEX IS STORED AND TOOK% IS LEFT CLEAR  
   4 4JGL    [   
   9 4K2=    #SKI  K6SETUPARA>200-200
   9 4KFW ...     TRACE     0,SETUP00
  20 4K_G          STO   3  AWORK2(2)           [STORE CURRENT POS IN PARAMINDEX 
  11 4LF6          MHUNTW   3,ADATA,CSTORE   
  20 4LYQ          ADX   3  1                   [RELITIVIZER FOR WHICH XCALC WORD
  21 4MDB          LDX   1  5                   [STORE LINK ADDR IN X1 SO WE CAN MOD 
   4 4MQQ ...(   
  16 4MY2          JBC      SJN,2,UNSETDEF      [JUMP IF IP UNSET
  17 4NCL          OBEY     0(1)                [ VIZ    BS  3,UNSET?
   8 4NX=          OBEY     1(1) 
   4 4P74 ...)   
  19 4PBW    SJN   JBC      SJM,2,TOOKDEF       [JUMP IP DEFAULT NOT TAKEN   
   4 4PSD ...(   
  17 4PWG          OBEY     2(1)                [VIZ    BS  3,TOOK?  
   8 4QB6          OBEY     3(1) 
   4 4QDP ...)   
   6 4QH# ...#SKI  XIPEXT
  18 4QKX ...      IPUSEX   LDX   0,4           [PUT DEFAULT VALUE IN X0 
   7 4QNG ...#SKI  XIPEXT<1$1
   4 4QR5 ...(   
   7 4QTQ          SMO      4
  18 4R*B          LDX   0  INSTPARAM           [PUT DEFAULT VALUE IN X0 
   4 4RK8 ...)   
   8 4RT2          BRN      SEND 
   9 4S#L    SJM   LDX   0  AWORK2(2)
  21 4SS=          SBX   0  ACOMMUNE6(2)        [PUT REL POINTER TO PARAMNDX IN X0   
  17 4T?W    SEND  OBEY     4(1)                [VIZ    ADN  3  1/2/3
  17 4TRG          STO   0  0(3)                [STORE REQUIRED VALUE
  20 4W?6          LDX   3  AWORK2(2)           [RESTORE POINTER TO PARAMINDEX   
  18 4WQQ          LDX   1  FX1                 [PUT FX1 INTO X2 AGAIN   
   7 4XQ2          EXIT  5  5
   5 4Y9L    #PAGE   
   5 4YP=    SOUTPUT 
   9 4_8W    #SKI  K6SETUPARA>200-200
   9 4_NG ...     TRACE     1,SETUP01
   8 5286          STO   0  GEN0 
  11 52GH          MHUNTW   3,ADATA,CSTORE   
  16 52SY           LDX   2  FX2            [BEST BE CAREFUL !!! 
  10 537B          LDX   7  ACOMMUNE6(2) 
  20 53M2          ADN   7  IPVAL-A1            [X7 NOW HOLDS ADDR OF PARAMINDEX 
   8 546L          SMO      GEN0 
  18 54L=          OBEY     0                   [ VIZ  ADX  3   XCALC??? 
   8 555W          LDX   4  1(3) 
  19 55KG          JBS      SJ1,3,TOOK1         [JUMP IF 1ST IP IS DEFAULT   
   7 5656          ADX   4  7
   7 56JQ          SMO      4
   7 574B          LDX   4  0
   8 57J2    SJ1   LDX   5  2(3) 
  17 583L          JBS      SJ2,3,TOOK2         [REPEAT FOR 2ND IP   
   7 58H=          ADX   5  7
   7 592W          SMO      5
   7 59GG          LDX   5  0
   8 5=26    SJ2   LDX   6  3(3) 
  15 5=FQ          JBS      SJ3,3,TOOK3         [AND THE 3RD 
   7 5=_B          ADX   6  7
   7 5?F2          SMO      6
   7 5?YL          LDX   6  0
   8 5#D=    SJ3   LDX   0  GEN0 
   7 5#XW          EXIT  0  1
   5 5*CG    #PAGE   
   4 5*X6    [   
   5 5BBQ    STAKEDEF
   9 5BWB    #SKI  K6SETUPARA>200-200
   9 5CB2 ...     TRACE     2,SETUP02
  11 5CTL          MHUNTW   3,ADATA,CSTORE   
  16 5D*=          SMO      7                   [LINK ADDR IN  X7
  18 5DSW          OBEY     0                   [PICK UP WHICH XCALC WORD
  19 5F#G          JBSS     STOK1,3,TOOK1       [JUMP IF IP 1 - IS DEFAULT   
  19 5FS6          LDX   3  1(3)                [REL POINTER TO PARAMINDEX   
  20 5G?Q          ADX   3  ACOMMUNE6(2)        [MAKE X3 ABSOLUTE POINTER TO PARA
   6 5G*N ...#SKI  XIPEXT
   4 5GCL ...(   
  10 5GFJ ...      LDX   0  IPNUM-A1(3)  
  18 5GHG ...      IPUSEX   LDX   4,0           [PUT DEFAULT VALUE IN X4 
   4 5GKD ...)   
   7 5GMB ...#SKI  XIPEXT<1$1
   4 5GP# ...(   
  10 5GRB          SMO      IPNUM-A1(3)  
  18 5H?2          LDX   4  INSTPARAM           [PUT DEFAULT VALUE IN X4 
   4 5HGS ...)   
   8 5HQL          BRN      STRM 
  21 5J==    STOK1 JBSS     STOK2,3,TOOK2       [REPEAT ABOVE FOR 2ND IP -DEFAUT INX5
   8 5JPW          LDX   3  2(3) 
  10 5K9G          ADX   3  ACOMMUNE6(2) 
   6 5K?D ...#SKI  XIPEXT
   4 5K*B ...(   
  10 5KC# ...      LDX   0  IPNUM-A1(3)  
  18 5KF= ...      IPUSEX   LDX   5,0           [PUT DEFAULT VALUE IN X5 
   4 5KH8 ...)   
   7 5KK6 ...#SKI  XIPEXT<1$1
   4 5KM4 ...(   
  10 5KP6          SMO      IPNUM-A1(3)  
   9 5L8Q          LDX   5  INSTPARAM
   4 5LDJ ...)   
   8 5LNB          BRN      STRM 
  21 5M82    STOK2 JBS      YSUC,3,TOOK3        [IF 3RD IP IS DEFAULT ALL POSS DEFS  
  20 5MML                                       [                 TAKEN SO  JUMP 
  20 5N7=          STOZ     AWORK3(2)           [POINTER TO SAY ALL DEFS TAKEN   
  17 5NLW          LDX   3  3(3)                [REL POINTER TO PARA 
  15 5P6G          ADX   3  ACOMMUNE6(2)        [ABS POINTER 
   8 5PL6          BRN      STRM1
  16 5Q5Q    STRM  STO   4  AWORK1(2)           [STORE THE VALUES
   9 5QKB          STO   5  AWORK2(2)
   9 5R52          STO   6  AWORK3(2)
   5 5RJL    STRM1   
  10 5S4=          LDX   4  IPNUM-A1(3)  
  21 5SHW          SBX   7  FX1                 [AS WE ARE TO COORD SAVE REL LINK ADD
   8 5SRN ...      CALL  5  YHD  
  19 5T3G ...       MHUNTW   3,ADATA,CSTORE            [RE-FIND ADATA-CSTORE 
  18 5T?# ...       ADX   3  XCALC(3)                  [AND RE-POSITION  
   9 5TH6    SBAK  LDX   6  AWORK3(2)
  16 5W2Q          ADX   7  1                   [RESTORE LINK ADD
  20 5WGB          BZE   6  YSUC                [JUMP IF ALL 3 IP'S ARE DEFAULT  
  18 5X22          LDX   4  AWORK1(2)           [ELSE RESTORE ALL VALUES 
   9 5XFL          LDX   5  AWORK2(2)
   7 5X_=          EXIT  7  1
   5 5YDW    #PAGE   
   4 5YYG    [   
  10 5_D6    [   WELL  HERE  WE  GO  ----
   4 5_XQ    [   
   5 62CB    XSTRT   
   4 62X2    [   
   9 63BL    #SKI  K6SETUPARA>200-200
   9 63CW ...      TRACE    3,SETUP03
  21 64*W    [   FIRST  SET  ADTO  LARGE ENOUGH TO FORCE SRING CLEAN TO O/P DATE AND 
  18 64TG    [                                      TIME ON OPS CONSOLE  
   4 64_Q ...[   
  21 6562 ...[         THE FOLLOWING 2 LINES HAVE BEEN ALTERED DUE TO G5441(B 0911)  
  18 65== ...[      THIS WAS CAUSED BY THE FACT THE CURRENT SPRING CLEAN 
  18 65BG ...[      ONLY RCOGNISWS B0 + B12 SET IN ADTO,IF ALL BITS SET  
  10 65GQ ...[      IT GETS CONFUSED....!
   4 65M2 ...[   
  13 65R= ...[          DONE  6.SEPT.74         :CENT
   4 65XG ...[   
  18 663Q ...TOFORCEOP      #40004000            [CORRECT FORMAT FOR S-P 
  10 6682 ...      LDX   0  TOFORCEOP(1) 
  16 66#B          STO   0  ADTO                [ALL BITS NOW SET
   4 66S2    [   
  16 67?L    [   CHECK THAT IDENTITY MACRO HAS BEEN IMPLEMENTED  
   4 67BH ...[   
   4 67FD ...[   
  18 67J* ...[       THE FOLLOWING CORRECTION IS DUE TO G5448 (B 0983)   
  17 67M= ...[    THIS RESULTED IN NO DATAE BEING PRESENT IN THE 'PM'
  20 67Q7 ...[     GEOERGE LOADED MESSAGE.THIS WAS CAUSED BY SETUPARA ASSUMMING  
  20 67T4 ...[      THAT A DOWN TO DATECON HAD OCCURED AND FDATE HELD THE CORRECT
  19 67X_ ...[       DATE ---- THIS IS NOT TRUE --- SO DATECON MUST BE CALLED
  17 682W ...[       WITH %A .NE. 'EDATE' SO FORCING A DOWN DATECON,1
  15 685R ...[              DONE   18.SEPT.74         :CENT  
   4 688N ...[   
   8 68?K ...#DEF  TEDATE = EDATE
   4 68BG ...[   
  20 68FC ...[       WE CAN NOW CALL DATECON WITH PARAMETER TEDATE FORCING DOWN  
   4 68J# ...[   
  21 68M9 ...      DATECON  TEDATE               [CONVERT NO OF DAYS TO DDMMMYY IN   
  17 68QG                                       [   ACOMMUNE1 + 2.   
  10 69=6          LDX   4  ACOMMUNE1(2) 
  10 69PQ          LDX   5  ACOMMUNE2(2) 
  10 6=9B          STO   4  IDENTITY+4   
  17 6=P2 ...      STO   5  IDENTITY+5          [STORE AWAY THE DATE 
   4 6?8L    [   
  17 6?N=    [    NOW CHECK IF IDENTITY MACRO HAS BEEN IMPLEMENTED   
  18 6#7W    [                              (WORD - ACES HOLDS  4 SPACES 
   8 6#MG          LDX   4  ACES 
  14 6*76          TXU   4  IDENTITY            [DO CHECK
  10 6*LQ          TXU   4  IDENTITY+1   
  17 6B6B          BCS      YIMP                [JUMP IF IMPLEMENTED 
  19 6BL2          MONOUTX  WIDFAIL(1)          [O/P RESTORE ERROR MESSAGE   
   4 6C5L    YIMP
  20 6CK=          TIMECON  JTIME               [CONVERT NO.OF K9'5 TO  HH.MM.SS 
  10 6D4W          LDX   4  ACOMMUNE1(2) 
  10 6DJG          LDX   5  ACOMMUNE2(2) 
  10 6F46          STO   4  IDENTITY+2   
  17 6FHQ          STO   5  IDENTITY+3          [STORE AWAY THE TIME 
   9 6G3B    #SKI K6SETUPARA>299-299 
  12 6GH2 ...      TRACE    IDENTITY+2,FIRSTBIT  
   4 6H2L    [   
  20 6HG=    [   OPEN PARAMINDEX --- HOPEFULLY!!    MODE CLEAN AS NOT ALTERING   
   4 6H_W    [   
  12 6JFG          OPENSYS  YBRK,PARAMINDEX,CLEAN
  19 6J_6          TESTREPN OK,YFAL             [IF NOT OK -- GO AND GEOERR  
   9 6KDQ    #SKI  K6SETUPARA>200-200
  10 6KJM ...      TRACE     4,SETUP04   
  17 6LD2          SETNCORE 14,3,ADATA,CSTORE   [SET UP HOLDING BLOCK
   8 6LXL          STOZ     A1(3)
   8 6MC=          LDN   4  A1(3)
   9 6MWW          LDN   5  A1+1(3)  
  16 6NBG          MOVE  4  13                  [ZEROISE BLOCK   
  10 6NW6          LDN   5  XCALCLIMIT   
   9 6P*Q          STO   5  XCALC(3) 
  10 6PTB          STOZ     XDEFAULT(2)  
   6 6PW2 ...XFSHNBSETN  
   6 6PX= ...#SKI  XIPEXT
   4 6PXW ...(   
   9 6PYG ...#SKI  K6SETUPARA>299-299
  10 6P_6 ...      TRACE    3,SETUPFRB   
  17 6P_Q ...      DOWN     SETUPARA,2          [SETUP FILE/FRB BLOCK
  18 6Q82 ...      MHUNT    3,FILE,FRB          [PTR TO FILE/FRB RECORD  
  20 6Q8L ...      ADN   3  A1                  [PTR TO START OF ACTUAL RECORD   
   6 6Q9= ...XFSHNBHUNT  
   4 6Q=G ...)   
   7 6Q#B ...#SKI  XIPEXT<1$1
  17 6Q*2          STEP                         [READ THE ONLY RECORD
   5 6QSL    #SKI IPL
   4 6R#=    (   
  10 6RRW          SEGENTRY K97SETUPARA  
   6 6S?G          NULL  
   4 6SR6    )   
  21 6T=Q          STO   3  ACOMMUNE6(2)        [STORE START ADDR IN EVENT OF COORD  
  19 6TQB          SBN   3  IPLINE              [MERELY TO EASE POSITIONING  
   9 6W=2    YLOP  ADN   3  IPLINE   
  10 6WPL          SMO      ACOMMUNE6(2) 
   9 6X9=          LDX   0  IPHEAD-A1
  10 6XNW          ADX   0  ACOMMUNE6(2) 
  21 6Y8G          SBN   0  IPNUM-A1(3)            [SUBTRACT ADDR OF NEXT IP LOCATION
  20 6YN6          BNG   0  YALL                   [J IF END OF PARAMNDEX REACHED
   4 6_7Q    [   
  14 6_MB    [   WE ARE NOW (STILL) READING PARAMINDEX   
   4 7272    [   
  17 72LL          MBC      2,TOOKDEF,UNSETDEF,TESTFAIL,TRACON,TCONT 
   9 736=    #SKI  K6SETUPARA>200-200
  12 73KW ...      TRACE     XDEFAULT(2),SETUP08 
  18 745G          LDX   6  IPVAL-A1(3)         [PUT VALUE OF IP IN XD   
  20 74K6          LDX   4  IPNUM-A1(3)         [PUT REL FIXED CORE ADDR INTO X4 
   6 74KT ...#SKI  XIPEXT
  18 74LJ ...      BNG   4  XTNUM               [J IF IN EXTENSION TABLE 
  18 74M* ...      LDN   5  INSTGAPEND-INSTPARAM  [MAX NO OF IPS ALLOWED 
   6 74MJ ...#SKI  XIPEXT
   4 74MR ...(   
   9 74N2 ...      BRN      XTNUMCOMP
   5 74N9 ...XTNUM   
  20 74ND ...      LDX   5  IPEXTCOUNT          [NUMBER OF IPS IN EXTENSION TABLE
  15 74NM ...      ORX   5  GSIGN               [SET BIT 0   
   6 74NW ...XTNUMCOMP   
   4 74P5 ...)   
  14 74PJ ...      BXL   4  5,NUMOK             [J IF OK 
   8 74RR ...      OUTNUM   4,0  
  19 74W2 ...      MONOUT   IPHIGH              [%A IS TOO HIGH FOR AN IP NO 
  20 74W7 ...      SBX   3  ACOMMUNE6(2)        [OBTAIN REL PTR WITHIN FILE/FRB  
  15 74W# ...      STO   3  4                   [SAVE IN X4  
   7 74WF ...#SKI  XIPEXT<1$1
  17 74WL ...      STEPAGAIN                    [REFIND PTR TO RECORD
   6 74WR ...#SKI  XIPEXT
   4 74WY ...(   
  19 74X5 ...      MHUNT    3,FILE,FRB          [REFIND PTR TO FILE/FRB BLOCK
   8 74X= ...      ADN   3  A1   
   4 74XC ...)   
  21 74XJ ...      STO   3  ACOMMUNE6(2)        [STORE START ADDR IN EVENT OF COORD  
  20 74XP ...      ADX   3  4                   [REFIND CURRENT PLACE IN FILE/FRB
   8 74Y9 ...      BRN      YLOP 
   5 752D ...NUMOK   
   4 754Q    [   
   9 75JB    [   IS THE IP UNSET ?   
   4 7642    [   
  16 76HL          BXE   6  GSIGN,YSTD          [JUMP IF IT IS   
   4 773=    [   
  15 77GW ...[   ARE WE DEALING WITH A TRACE IP OR CONTEXT   
   4 782G    [   
   9 78G6          LDN   5  TRACEED  
   7 78_Q    YRD   SMO      5
  12 79FB          BXE   4  TRACEIP-1(1),YTRAC   
   8 79_2          BCT   5  YRD  
   4 7=DL    [   
  19 7=Y=    [   IF WE GET HERE NOT TRACE/CONTEXT  AND NOW  X1 = FX1 AGAIN   
   4 7?CW    [   
  15 7?XG    [   SO NOW WE CAN TEST IPVAL AGAINST  MAX/MIN   
   9 7#C6    #SKI  K6SETUPARA>200-200
   9 7#WQ ...     TRACE     6,SETUP06
  18 7*BB          BXL   6  IPMIN-A1(3),YSBD    [JUMP IF LESS THAN MIN   
  20 7*W2          BXL   6  IPMAX-A1(3),YOK     [JUMP IF LESS THAN MAX-- I.E OKAY
  20 7B*L          BXE   6  IPMAX-A1(3),YOK     [JUMP IF EQUAL TO MAX -- I.E.OKAY
   4 7BT=    [   
  20 7C#W    [   IF WE GET HERE WE HAVE TO SET XDEFAULT AS WE MUST TAKE A DEFAULT
   4 7CSG    [   
  21 7D#6    YSBD  BS       2,UNSETDEF          [IP WAS NOT UNSET-- TEST HAS FAILED  
   9 7DRQ    #SKI  K6SETUPARA>200-200
  12 7F?B ...     TRACE     XDEFAULT(2),SETUP09  
  17 7FR2    YSTD  BS       2,TOOKDEF           [DEFAULT TO BE TAKEN 
   9 7G=L    #SKI  K6SETUPARA>200-200
  12 7GQ= ...     TRACE     XDEFAULT(2),SETUP19  
   4 7H9W    [   
  16 7HPG    [   NOW TO TEST IF WE ARE DEALING WITH A SPECIAL IP 
   4 7J96    [   
   4 7JNQ    YOK 
  18 7K8B          LDN   0  TABIPLGN            [LENGTH OF SP IP TABLE   
   7 7KN2    YRDA  SMO      0
  21 7L7L          LDX   5  TABIPST-2(1)        [LOAD A SP IPNUM - START AT END OF TB
  21 7LM=          SBN   0  1                   [ADJUST COUNT AS DOUBLE ENTRY TABLE  
  17 7M6W          BXU   4  5,YEND              [JUMP IF NOT A MATCH 
   9 7MLG    #SKI  K6SETUPARA>200-200
   9 7N66 ...     TRACE     0,SETUP10
   7 7NKQ          SMO      0
  19 7P5B          BRN      TABIPST             [BRANCH TO REL SP IP CODING  
  15 7PK2    YEND  BCT   0  YRDA                [ROUND WE GO 
   4 7Q4L    [   
  12 7QJ=    [   IF WE FALL THROUGH NOT A SP IP  
   4 7R3W    [   
  15 7RHG    [   THE FOLLOW    INTEROGATES THE XDEFAULT WORD 
   4 7S36    [   
  21 7SGQ    YNOR  LDN   5  YLOP(1)             [THIS CODE MAY RETURN ELSEWHERE THAN 
   9 7T2B    #SKI  K6SETUPARA>200-200
   9 7TG2 ...     TRACE     1,SETUP11
  21 7T_L                                       [                             YLOP   
  19 7WF=    YOPT  JBS      YTAK,2,TOOKDEF      [JUMP IF DEFAULT TO BE TAKEN 
   6 7WJB ...#SKI  XIPEXT
  18 7WMG ...      IPUSEX   STO   6,4           [STORE NEW VALUE OF IP   
   7 7WQL ...#SKI  XIPEXT<1$1
   4 7WTQ ...(   
   7 7WYW          SMO      4
   9 7XDG          STO   6  INSTPARAM
   4 7XN# ...)   
   9 7XY6    #SKI  K6SETUPARA>200-200
   9 7YCQ ...     TRACE     4,SETUP24
  16 7YXB          BRN      (5)                 [FINI - SO RETURN
   4 7_C2    [   
  13 7_WL    [   IF HERE THE DEFAULT IS TO BE TAKEN  
   4 82B=    [   
  18 82TW    YTAK  JBC      (5),2,UNSETDEF      [IF UNSET WE CAN RETURN  
   9 83*G    #SKI  K6SETUPARA>200-200
   9 83T6 ...     TRACE     2,SETUP12
   4 84#Q    [   
  13 84SB    [   SO WE HAVE TO O/P AN ERROR MESSAGE  
   4 85#2    [   
  10 85RL    YHD  SBX   3  ACOMMUNE6(2)  
   6 85WQ ...#SKI  XIPEXT
  15 85_W ...      IPUSEX   LDX   6,4           [VALUE OF IP 
   7 8652 ...#SKI  XIPEXT<1$1
   4 8686 ...(   
   7 86?=          SMO      4
   9 86QW          LDX   6  INSTPARAM
   4 872N ...)   
   7 87=G          LDX   4  3
   8 87Q6          SBX   5  FX1  
  19 889Q           JBS      YERR,2,VANYERR      [JUMP IF HEADING ALREADY O/P
  17 88PB          MONOUTX  WIPHEAD(1)          [O/P DEFAULT HEADING 
  18 8992          BS       2,VANYERR           [SET HEADING O/P SWITCH  
   4 89NL    YERR
   4 8=8=    [   
   9 8=MW    [   OUTPUT THE  %A = %B 
   4 8?7G    [   
   9 8?M6    #SKI  K6SETUPARA>200-200
   9 8#6Q ...     TRACE     0,SETUP20
  19 8#LB          OUTBLOCN ULNOUTB             [CREATE THE GMON/ASET BLOCK  
   5 8*62    TRYAGAIN
   6 8*7Y ...#SKI  XIPEXT
   4 8*9W ...(   
  18 8*?S ...      MHUNT    3,FILE,FRB          [PTR TO FILE/FRB BLOCK   
  20 8**Q ...      ADN   3  A1                  [PTR TO START OF ACTUAL RECORD   
  19 8*CN ...      STO   3  ACOMMUNE6(2)        [STORE ADDR IN EVENT OF COORD
   4 8*FL ...)   
   7 8*HJ ...#SKI  XIPEXT<1$1
   7 8*KL          STEPAGAIN 
   7 8B5=          ADX   3  4
  15 8BJW          OUTPACK IPNAME-A1(3),3,LOCNAME,TRYAGAIN   
  10 8C4G          BXU   6  GSIGN,YNUNS  
  18 8CJ6          LDX   3  UNSET(1)            [SO OUR VALUE IS -- UNSET
  19 8D3Q          STO   3  ACOMMUNE1(2)        [SO WE OUTPUT AN INTELLIEGENT
  15 8DHB          LDX   3  UNSET+1(1)          [ERROR REMARK
  10 8F32          STO   3  ACOMMUNE2(2) 
  19 8FGL          LDX   3  UNSC(1)             [NOW THE NO. OF CHARACHTERS  
   8 8G2=          BRN      YPAR 
   5 8GFW    YNUNS   
  21 8G_G ...      JBSC     YTR,2,TRACON        [JUMP IF CONTEXT OR TRACE IP- CLEAR B
  19 8HF6    YNUM  OUTNUM   6,0                 [PUT IP VALUE INTO GMON BLOCK
   8 8HYQ          BRN      YOUT 
  19 8JDB    YTR   JBSC     YCON,2,TCONT        [JUMP IF CONTEXT - CLEAR BIT 
  19 8JY2          SYNTHMON 6                   [TRANSLATE MASK AND OUTPAR   
   8 8KCL          BRN      YOUT 
  18 8KX=    YCON  BZE   6  YNUM               [TRAP CASE OF CONTEXT=0 !!
  19 8LBW          IPCONT   6,3                 [TRANSLATE CONTEXT INTO CHARS
  17 8LWG    YPAR  OUTPARAM 3,ACOMMUNE1         [PUT INTO GMON BLOCK 
  17 8MB6    YOUT  MONOUTX  WIPLIS(1)           [DO THE ACTUAL O/P   
   7 8MKY ...#SKI  XIPEXT<1$1
  21 8MTQ          STEPAGAIN                    [.. WE WISH TO RETURN TO THE POSITION
   6 8MY* ...#SKI  XIPEXT
   4 8N2Y ...(   
  10 8N5H ...      MHUNT    3,FILE,FRB   
  21 8N86 ...      ADN   3  A1                  [.. WE WISH TO RETURN TO THE POSITION
   4 8N=P ...)   
  20 8N*B          STO   3  ACOMMUNE6(2)        [.. IN PARAMINDEX BEFORE WE O/P  
   7 8NK8 ...#SKI  XIPEXT<1$1
  20 8NT2          STEPREWRITE                  [MARK TO WRITE AWAY AS INSERTING 
  19 8P#L                                       [ ...  DEFAULT IN PARAMINDEX 
   7 8PS=          ADX   3  4
  17 8Q?W          STO   6  IPVAL-A1(3)         [WRITE DEFAULT AWAY  
   8 8QRG          ADX   5  FX1  
   8 8R?6          BRN      (5)  
  21 8RQQ    [    THE MAIN BODY OF THE SEGMENT IS COMPLETE WHAT FOLLOWS IS MERELY..  
   4 8S=B    [   
  18 8SQ2    [      1)  THE MAX/MIN COMPARISON FOR CONTEXT+TRACE LEVELS  
   4 8T9L    [   
  13 8TP=    [      2)  THE CHECKS ON SPECIAL  IP'S  
   4 8W8W    [   
   4 8WNG    [   
  19 8X86    [                MAX/MIN COMPARISION FOR  CONTEXT+TRACE LEVELS  
   4 8XMQ    [   
  15 8Y7B    YTRAC LDX   1  FX1                 [RESET  FX1  
  19 8YM2          BS       2,TRACON            [SET TRACE/CONTEXT INDICATOR 
  18 8_6L          BXU   4  TRACEIP(1),YNCN        [JUMP IF NOT CONTEXT  
  18 8_L=          BS       2,TCONT             [SET CONTEXT INDICATOR   
   4 925W    [   
   9 92KG    #SKI  K6SETUPARA>200-200
   9 9356 ...     TRACE     3,SETUP13
   7 93JQ    [   TEST 'MIN'  
   4 944B    [   
   4 94J2    YNCN
  10 953L          LDX   5  IPVAL-A1(3)  
  10 95H=          ERX   5  IPMIN-A1(3)  
  10 962W          ANDX  5  IPMIN-A1(3)  
  18 96GG          BNZ   5  YMFL                [JUMP IF MIN HAS FAILED  
   9 9726    #SKI  K6SETUPARA>200-200
   9 97FQ ...     TRACE     4,SETUP14
   4 97_B    [   
   7 98F2    [   TEST 'MAX'  
   4 98YL    [   
  10 99D=          LDX   5  IPVAL-A1(3)  
  10 99XW          ORX   5  IPMAX-A1(3)  
  10 9=CG          ERX   5  IPMAX-A1(3)  
  19 9=X6          BZE   5  YNOR                [IF SUCCESS ON MAX/MIN  JUMP 
   9 9?BQ    #SKI  K6SETUPARA>200-200
   9 9?WB ...     TRACE     5,SETUP15
  17 9#B2    YMFL  MBS      2,TOOKDEF,UNSETDEF  [SET BITS TO FAILURE 
   8 9#TL          BRN      YNOR 
   5 9**=    #PAGE   
   4 9*SW    [   
  18 9B#G    [   NOW FOR CODE TO SET UP THE ADATA/CSTORE WITH THE SP-IP'S
   4 9BS6    [   
   9 9C?Q    #SKI  K6SETUPARA>200-200
   9 9CRB ...     TRACE     6,SETUP16
  15 9D?2    YBGD  LDN   1  XCALCLIMIT          [BACKGROUND  
   9 9DQL    YFST  CALL  5  SINPUT   
   9 9F==          BS       3,UNSET1 
   9 9FPW          BS       3,TOOK1  
   7 9G9G          ADN   3  1
   8 9GP6 ...      BRN      YLOP 
  14 9H8Q    YBCJ  LDN   1  XCALCJOB            [BACKJOB 
   8 9HNB          BRN      YFST 
  14 9J82    YMOJ  LDN   1  XCALCJOB            [MOPJOB  
   9 9JML    YSND  CALL  5  SINPUT   
   9 9K7=          BS       3,UNSET2 
   9 9KLW          BS       3,TOOK2  
   7 9L6G          ADN   3  2
   8 9LL6 ...      BRN      YLOP 
  14 9M5Q    YMOL  LDN   1  XCALCLIMIT          [MOPLIMIT
   8 9MKB          BRN      YSND 
  14 9N52    YJOL1 LDN   1  XCALCJOB            [JOBLIMIT
   9 9NJL    #SKI  K6SETUPARA>200-200
   9 9P4= ...     TRACE     1,SETUP21
   8 9PHW          BRN      YTRD 
  10 9Q3G    YJOL  LDN   1  XCALCLIMIT   
   7 9QH6          LDN   6  2
   9 9R2Q    YTRD  CALL  5  SINPUT   
   9 9RGB          BS       3,UNSET3 
   9 9S22          BS       3,TOOK3  
   7 9SFL          ADN   3  3
   8 9S_=          BCT   6  YJOL1
   8 9TDW ...      BRN      YLOP 
   4 9TYG    [   
  14 9WD6    [   END OF INSERTING DATA INTO ADATA/CSTORE 
   4 9WXQ    [   
  11 9YBL    YOBJ  MHUNTW   2,ADATA,CSTORE   
  19 9YW=          LDX   6  AFREE               [STORE  AFREE IN CALC WORD   
  18 9_*W           ADN  2  XCALCQUOTA               [POSITION OURSELVES 
   8 9_TG          STO   6  3(2) 
  17 =2*6          BS       2,TOOK3             [AND SET DEFAULT BIT 
   8 =2SQ          LDX   2  FX2  
  15 =3#B          LDN   1  XCALCQUOTA          [OBJECTQUOTA 
   8 =3S2          BRN      YFST 
  15 =4?L    YCAP  LDN   1  XCALCQUOTA          [CHAPTERQUOTA
   8 =4R=          BRN      YSND 
   5 =5QG    #SKI G4 
   4 =6=6    (   
  20 =6PQ ...YMAX  JBS      YNOR,2,TOOKDEF      [JUMP IF DEFAULT ALREADY TAKEN   
  17 =79B ...                                   [THIS FOR G4 MAXQUOTA
  16 =7P2 ...      BXL   6  AFREE,YNOR      [JUMP IF CHECK OKAY  
  12 =88L          MBS      2,TOOKDEF,UNSETDEF   
   8 =8N=          BRN      YNOR 
   4 =?6B    )   
  21 =?L2    YCOR  JBS      YNOR,2,TOOKDEF      [FOR SIZEDEFAULT/COREOBJECT   G3+G4  
  21 =#5L    #SKI G4                            [IF G4 -AS EBSWAPSP IN K  MUST DIVIDE
  17 =#99 ...      SRL   6  10                  [  IPVAL(X0) BY 1024 
   4 =##S ...[   
  18 =#DC ...[        THE FOLLOWING LINES HAVE BEEN ALTERED TO CORRECT   
  11 =#J2 ...[               BUG 0742(G 5425)
   4 =#MK ...[   
  20 =#R8 ...[          THIS WAS CAUSED BY THE FACT IF OLD VALUS OF COREOBJECT   
  20 =#WR ...[       AND SIZEDEFAULT WERE OKAY THE VALUES PUT IN NEW PARAMINDEXG4
  16 =*2B ...[       WAS  OLD VALUE/1024    (GEORGE 4 BUG ONLY)  
   4 =*5_ ...[   
  16 =*9J ...[             DONE    7.AUGUST.74           :CENT   
   4 =**7 ...[   
  21 =*DQ ...      BXL   6  XAFEB,YYNR          [NOTE ROUNING TAKES CARE OF ITSELF   
  17 =*JG                                       [IF  EBSWAPSP = 10K  
  21 =B46                                       [X0 = 10002 - SRL = 10 FAILS ON BXL  
   9 =BHQ    #SKI  K6SETUPARA>200-200
   9 =BS_ ...      TRACE    0,SETUP30
  12 =C68 ...      MBS      2,TOOKDEF,UNSETDEF   
   4 =C92 ...[   
  10 =C?S ...[         THIS IS NEW G5425 
   4 =CBL ...[   
   4 =CFD ...YYNR
   6 =CJ= ...#SKI   G4   
  17 =CM4 ...       LDX   6  IPVAL-A1(3)        [RESTORE OLD VALUE   
   4 =CPW ...[   
  11 =CSN ...[         AND  THATS IT   !!!!  
   4 =CXG ...[   
   8 =D2L          BRN      YNOR 
   4 =DG=    [   
   9 =D_W    [   END OF ALL CHECKS   
   4 =FFG    [   
  11 =F_6    [   NOW  THE  NUMERICAL CHECKS  
   4 =GDQ    [   
  11 =GYB    YALL  MHUNTW    3,ADATA,CSTORE  
  17 =HD2 ...      LDN   0  3          [POST VOPA ALL CHECKS IN G3+4 
  15 =HXL          STO   0  XDUMP(3)            [KEEP COUNT  
  19 =JC=    YCHK  CALL  0  SOUTPUT             [PUT IP VALUES INTO X4,X5,X6 
   9 =JWW          ADX   3  XCALC(3) 
   7 =KBG    YAGN  LDX   0  4
   7 =KW6          ADX   0  5
  20 =L*Q          BXGE  6  0,YSUC1             [JUMP IF CONDITION IS SUCCESSFUL 
   9 =LTB    #SKI  K6SETUPARA>200-200
   9 =M*2 ...     TRACE     2,SETUP22
  17 =MSL          CALL  7  STAKEDEF            [PICK ANOTHER DEFAULT
   9 =N#=          ADX   3  XCALC(3) 
   8 =NRW          BRN      YAGN 
   5 =P?G    YCHK1   
   9 =PR6    #SKI  K6SETUPARA>200-200
  12 =Q=Q ...     TRACE     XDEFAULT(2),SETUP18  
   7 =QQB          LDN   0  4
  17 =R=2          ADS   0  XCALC(3)            [UPDATE XCALC POINTER
   8 =RPL          BRN      YCHK 
  21 =S9=    YSUC1 LDX   2  ACOMMUNE6(2)           [NOW TO TEST IF ANY IP'S IN A CHEC
  18 =SNW                                       [ ARE OKAY (NOT DEFAULT) 
  17 =T8G          JBS      YN1,3,TOOK1         [ X3 = ADATA BLOCK   
   8 =TN6          SMO      1(3) 
  10 =W7Q          LDX   0  IPNUM-A1(2)  
   6 =W=W ...#SKI  XIPEXT
  15 =WB2 ...      IPUSEX   STO   4,0           [1ST  IP IN  
   7 =WF6 ...#SKI  XIPEXT<1$1
   4 =WJ= ...(   
   7 =WMB          SMO      0
  15 =X72          STO   4  INSTPARAM           [1ST  1P IN  
   4 =XBS ...)   
  10 =XLL    YN1   JBS      YN2,3,TOOK2  
   8 =Y6=          SMO      2(3) 
  10 =YKW          LDX   0  IPNUM-A1(2)  
   6 =YP2 ...#SKI  XIPEXT
  14 =YS6 ...      IPUSEX   STO   5,0           [2ND IP  
   7 =YX= ...#SKI  XIPEXT<1$1
   4 =_2B ...(   
   7 =_5G          SMO      0
  14 =_K6          STO   5  INSTPARAM           [2ND IP  
   4 =_SY ...)   
   9 ?24Q    #SKI  K6SETUPARA>200-200
   9 ?2JB ...     TRACE     3,SETUP23
  10 ?342    YN2   JBS      YSUC2,3,TOOK3
   8 ?3HL          SMO      3(3) 
  10 ?43=          LDX   0  IPNUM-A1(2)  
   6 ?46B ...#SKI  XIPEXT
  14 ?49G ...      IPUSEX  STO   6,0            [3RD IP  
   7 ?4#L ...#SKI  XIPEXT<1$1
   4 ?4CQ ...(   
   7 ?4GW          SMO      0
  14 ?52G          STO   6  INSTPARAM           [3RD IP  
   4 ?5=# ...)   
   5 ?5G6    YSUC2   
  11 ?5_Q    YSUC  MHUNTW   3,ADATA,CSTORE   
   9 ?6FB          LDX   0  XDUMP(3) 
   7 ?6_2          SBN   0  1
  16 ?7DL          STO   0  XDUMP(3)            [KEEP NEW COUNT  
  18 ?7Y=          BNZ   0  YCHK1               [J IF NOT FINISHED CHECKS
   7 ?8CW          FREECORE 3
   6 ?8DC ...#SKI  XIPEXT
   4 ?8DY ...(   
  18 ?8FF ...      REWIND                       [REWIND TO START OF FILE 
   9 ?8G2 ...#SKI  K6SETUPARA>299-299
  10 ?8GH ...      TRACE    3,IPREWIND   
  20 ?8H4 ...      LDN   7  A1+1                [RELATIVE PTR TO VERSION NUMBER  
   6 ?8HK ...NEXTRECOUT  
  18 ?8J6 ...      STEP                         [READ FIRST/NEXT RECORD  
  18 ?8JM ...      BZE   3  WRITEEND            [J IF REACHED END OF FILE
  18 ?8K8 ...      MHUNT    2,FILE,FRB          [PTR TO FILE/FRB BLOCK   
  21 ?8KP ...      ADX   2  7                   [PTR TO START OF NEXT ENTRY IN /FRB  
  20 ?8L= ...      LDX   1  0(3)                [RECORD HEADER OF CURRENT RECORD 
  20 ?8LR ...      SBN   1  1                   [DON'T WANT TO MOVE HEADER ITSELF
  20 ?8M# ...      ADX   7  1                   [UPDATE RELATIVE PTR IN FILE/FRB 
  20 ?8MT ...      ADN   3  1                   [PTR TO WORD AFTER RECORD HEADER 
  20 ?8NB ...      MOVE  2  0(1)                [MOVE NEW ENTRY INTO FILE/FURB   
  18 ?8NX ...      SBN   3  1                   [RESET POINTER TO RECORD 
  20 ?8PD ...      STEPREWRITE                  [RENAME /FURB TO /FUWB TO REWRITE
  10 ?8P_ ...      BRN      NEXTRECOUT   
   5 ?8QG ...WRITEEND
  14 ?8R3 ...      LDX   2  FX2                 [RESET X2
  17 ?8RJ ...      MFREE    FILE,FRB            [FREE FILE/FRB BLOCK 
   9 ?8S5 ...#SKI  K6SETUPARA>299-299
  10 ?8SL ...      TRACE    3,IPWRITEN   
   4 ?8T7 ...)   
   6 ?8TN ...XFSHNBALTER 
   7 ?8XG          CLOSETOP  
   7 ?9C6          IPBACKJAM 
   6 ?9J# ...XFSHNBBKJM  
   9 ?9WQ          ACROSS   SETUP,2  
   5 ?9WW ...#PAGE   
   6 ?9X2 ...XTENTBLOK   
   7 ?9X6 ...#SKI  XIPEXT<1$1
  21 ?9X= ...      GEOERR   1,NOTXTTAB          [SHOULD ONL ENTER IF EXTENSION TABLE 
   6 ?9XB ...#SKI  XIPEXT
   4 ?9XG ...(   
   4 ?9XL ...#   
  20 ?9XQ ...#  THIS ENTRY POINT SETS UP A FILE/FRB BLOCK WHEN AN EXTENSION TABLE
  20 ?9XW ...#   EXISTS TO SAVE HAVING TO MAKE LARGE ALTERATIONS TO THE IP CODE  
   4 ?9Y2 ...#   
  18 ?9Y6 ...      SETNCORE 512,4,FILE,FRB      [CREATE FILE/FRB BLOCK   
  19 ?9Y= ...      STEP                         [READ FIRST RECORD OF FILE   
  19 ?9YB ...      MHUNT    1,FILE,FRB          [PTR TO FILE/FRB BLOCK IN X1 
  14 ?9YG ...      LDX   4  1                   [  IN X4 
  19 ?9YL ...      ADN   4  A1                  [PTR TO START OF RECORD BLOCK
  20 ?9YQ ...      MOVE  3  2                   [MOVE FIRST RECORD INTO FILE/FRB 
  19 ?9YW ...      LDN   7  2                   [NUMBER OF WORDS IN FILE/FRB 
  20 ?9_2 ...      LDN   6  512                 [CURRENT SIZE OF FILE/FRB BLOCK  
   6 ?9_6 ...NEXTRECIN   
  16 ?9_= ...      STEP                         [READ NEXT RECORD
  18 ?9_B ...      BZE   3  READEND             [J IF REACHED END OF FILE
  21 ?9_G ...      MHUNT    1,FILE,FRB          [REFIND PTR TO FILE/FRB BLOCK IN X1  
  14 ?9_L ...      LDX   4  1                   [   IN X4
  19 ?9_Q ...      ADN   4  A1                  [PTR TO START OF RECORD AREA 
  20 ?9_W ...      ADX   4  7                   [PTR TO NEXT ENTRY IN FILE/FRB   
  19 ?=22 ...      LDEX  1  0(3)                [NUMBER OF WORDS IN RECORD   
  19 ?=26 ...      SBN   1  1                   [NUMBER OF WORDS TO BE MOVED 
  21 ?=2= ...      ADX   7  1                   [UPDATE COUNT OF WORDS IN FILE/FRB   
  19 ?=2B ...      BXGE  7  6,XALTLEN           [J IF BLOCK NOT BIG ENOUGH   
   5 ?=2G ...XALTERED
  20 ?=2L ...      ADN   3  1                   [PTR TO START OF AREA TO BE MOVED
  17 ?=2Q ...      MOVE  3  0(1)                [MOVE IN RECORD ENTRY
  18 ?=2W ...      BRN      NEXTRECIN           [REPEAT FOR NEXT RECORD  
   5 ?=32 ...XALTLEN 
   8 ?=36 ...      ADN   6  64   
   9 ?=3= ...#SKI  K6SETUPARA>299-299
  10 ?=3B ...      TRACE    6,IPALTLEN   
   7 ?=3G ...      SBX   4  7
   7 ?=3L ...      ADX   4  1
  21 ?=3Q ...      SBN   4  A1                  [REFIND PTR TO FILE/FRB BLOCK IN X4  
  21 ?=3W ...      ALTLEN   4,6,FILE,FRB        [INCREASE LENGTH OF FILE/FRB BY 64   
  19 ?=42 ...      STEPAGAIN                    [REFIND PTR TO NEXT RECORD   
  21 ?=46 ...      MHUNT    1,FILE,FRB          [REFIND PTR TO FILE/FRB BLOCK IN X1  
  14 ?=4= ...      LDX   4  1                   [   IN X4
   8 ?=4B ...      LDEX  1  0(3) 
  21 ?=4G ...      SBN   1  1                   [RESET NUMBER OF WORDS TO BE MOVED   
   8 ?=4L ...      ADN   4  A1   
   7 ?=4Q ...      ADX   4  7
  19 ?=4W ...      SBX   4  1                   [RESET PTR IN FILE/FRB BLOCK 
  18 ?=52 ...      BRN      XALTERED            [RETURN TO NORMAL PATH   
   5 ?=56 ...READEND 
  21 ?=5= ...      MHUNT    1,FILE,FRB          [REFIND PTR TO FILE/FRB BLOCK IN X1  
  21 ?=5B ...      STO   7  A1(1)               [INSERT NEW VALUE OF RECORD HEADER   
   9 ?=5G ...#SKI  K6SETUPARA>299-299
  10 ?=5L ...      TRACE    7,FRBSETUP   
  19 ?=5Q ...      UP                           [RETURN TO CALLING SEGMENT   
   4 ?=5W ...)   
  11 ?=BB          MENDAREA 50,K99SETUPARA   
   5 ?=W2    #SKI IPL
  11 ??*L          MENDAREA 30,K98SETUPARA   
   4 ??T=    #END
   8 ____ ...10330510000300000000
