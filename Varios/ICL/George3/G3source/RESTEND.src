  15 22FL ...      SEG   RESTEND,850,PETER CAMPBELL,RESTORE  
   4 22_=    #   
   9 23DW          SEGENTRY K1RESTEND
   4 23YG    #   
  20 24D6    #   THIS SEGMENT ENABLES COPIES OF INCREMENT DUMP TAPES TO BE MADE  
  21 24XQ    #     IT USES THE NUMBER OF THE INCREMENT DUMP TAPE USED TO RESTORE THE 
  20 25CB    #   FILESTORE. IT CHECKS WHETHER THE NUMBER ALREADY EXISTS IN FILE  
  21 25X2    #   SYSTEM.INCINDEX AND IF NOT FINDS A TAPE WHICH MAY BE A COPY OF THIS 
  20 26BL    #   TAPE AND REPLACES IT IN INCINDEX,SERIAL AND DUMPER FILES UNDER  
   6 26W=    #   SYSTEM. 
   4 27*W    #   
  19 27TG    #     ENTRY    THIS SEGMENT IS ENTERED FROM RESTOREG WHEN THE   
  14 28*6    #              FILESTORE HAS BEEN RESTORED. 
  20 28SQ    #              A BLOCK TYPE FILE/FINTER7 HAS BEEN SET UP CONTAINING 
  15 29#B    #              A1      FIRST INCREMENT ON TAPE  
  15 29S2    #              A1+1    CURRENT INCREMENT ON TAPE
  15 2=?L    #              A1+2    CURRENT TSN OF DUMP TAPE 
   4 2=R=    #   
  20 2?=W    #     EXIT     INCINDEX CONTAINS THE TSN OF THIS TAPE AS THE FIRST  
  21 2?QG    #              TSN IN THE RECORD FOR EACH INCREMENT UP TO THE CURRENT   
   8 2#=6    #              ONE. 
  16 2#PQ    #              IT MAY HAVE REPLACED AN EXISTING TSN.
  20 2*9B    #              THE TAPE NUMBER WILL ALSO EXIST IN SYSTEM.SERIAL AND 
  10 2*P2    #              SYSTEM.DUMPER
   4 2B8L    #   
  21 2BN=    #     ERRORS   IF THE CURRENT TAPE IS NOT COMPATIBLE WITH THE EXISTING  
  21 2C7W    #              FILESTORE AN ERROR MESSAGE WILL BE OUTPUT AND THE ROUTINE
  17 2CMG    #              WILL EXIT UPPLUS 1 TO REPEAT THE MESSAGE 
   4 2D76    #   
   4 2DLQ    #   
  14 2F6B    #   THIS SEGMENT IS ENTERED FROM RESTOREG   
  20 2FL2    #    THIS SECTION OPENS INCINDEX AND CHECKS WHETHER THE INCREMENTSON
  20 2G5L    #    THE TAPE ARE COMPATIBLE WITH THOSE IN INCINDEX AND WHETHER THIS
  11 2GK=    #    TAPE IS ALREADY IN INCINDEX
   8 2H4W    #    IF PRESENT X7=1
   8 2HJG    #    IF NOT     X7=0
   4 2J46    #   
  17 2JHQ          STOZ     AWORK4(2)           [ NEW TAPE INDICATOR 
  16 2K3B          OPENINC  ,GENERAL            [OPEN INCINDEX   
  16 2KH2          OPENSYS  XERROR2,SERIAL,GENERAL  [OPEN SERIAL 
  18 2L2L          FILEMOVE 0,1                 [SERIAL NOW AT DEPTH 1   
  17 2LG=                                       [INCINDEX AT DEPTH 0 
  14 2L_W          STOZ     7                   [SWITCH  
   5 2MFG    STEP1   
  16 2M_6          STEP                         [READ NEXT RECORD
  15 2NDQ          BZE   3  TEST1               [END OF FILE 
  10 2NYB          LDX   4  AINCNO-A1(3) 
  10 2PD2          MHUNTW   1,FILE,FINTER
  10 2PXL          TXU   4  FIRSTINC(1)  
   9 2QC=          BCS      NOTFIRST 
  17 2QWW          LDEX  4  FRH(3)              [ALTER BLOCK LENGTH  
  21 2RBG          SBN   4  AMAG-A1             [AND STORE TAPE NOS. FROM INCINDEX   
   7 2RW6          LDX   6  4
  11 2S*Q          MHUNTW   3,FILE,FINTER7   
  10 2STB          ADX   4  ALOGLEN(3)   
  11 2T*2          ALTLEN   3,4,FILE,FINTER7 
   7 2TSL          STEPAGAIN 
  21 2W#=          MHUNTW   2,FILE,FINTER7      [MOVE TSNS & STATE INFORM. TO FINTER7
  10 2WRW          LDN   0  AMAG-A1(3)   
   9 2X?G          LDN   1  AMAGS(2) 
   7 2XR6          SMO      6
   7 2Y=Q          MOVE  0  0
   5 2YQB    NOTFIRST
  11 2_=2          MHUNTW   2,FILE,FINTER7   
  10 2_PL          LDX   6  CURRTSN(2)   
  19 329=          CALL  2  TSN                 [SEE IF CURRENT TSN IN RECORD
  13 32NW          BRN      STEP1               [NO  
  16 338G          LDN   7  1                   [YES SET SWITCH  
   9 33N6    #SKI   K6RESTEND>100-100
   9 347Q          TRACEVER 4,INC1   
   8 34MB          BRN      STEP1
  20 3572    #    THIS SECTION TESTS WHETHER TAPE IS ALREADY KNOWN TO INCINDEX   
   9 35LL    #    IF SO GOES TO X1   
  15 366=    TEST1                              [END OF FILE 
   9 36KW    #SKI   K6RESTEND>100-100
   4 375G    (   
  10 37K6          TRACEVER 7,ISTHERE?   
  11 384Q          MHUNTW   1,FILE,FINTER7   
   9 38JB          BLOCKTRA 1,FINTER7
  10 3942          MHUNTW   1,FILE,FINTER
   9 39HL          BLOCKTRA 1,FINTER 
   4 3=3=    )   
  17 3=GW          BNZ   7  X1                  [TAPE ALREADY THERE  
  17 3?2G    #    IF TAPE IS NOT IN INCINDEX FURTHER CHECKING IS DONE
  20 3?G6    #    THE NEXT FEW LINES CHECK WHETHER THE TAPE IS IN SYSTEM.SERIAL  
  11 3?_Q          MHUNTW   1,FILE,FINTER7   
  10 3#FB          LDX   6  CURRTSN(1)   
  19 3#_2          CALL  4  SSER                [SEARCH SERIAL FOR NEW TAPE  
  16 3*DL          BRN      NOTINSER            [  J IF NOT KNOWN
   5 3*Y=    XER13   
  20 3BCW          ACROSS   RESTERR,13          [NEW TAPE ALREADY KNOWN TO SERIAL
  20 3BXG    #    THIS SECTION OF PROGRAM DETERMINES WHETHER THERE IS A TAPE NO. 
  17 3CC6    #    IN INCINDEX WHICH MAY BE A COPY OF THE CURRENT TSN.
  15 3CWQ    #    IF ONE IS NOT FOUND AN ERROR IS REPORTED   
  13 3DBB    #    OTHERWISE THE NO. IS HELD IN X6.   
   4 3DW2    #   
   5 3F*L    NOTINSER
  15 3FT=          LDN   7  0                   [CLEAR SWITCH
  15 3G#W          NGN   4  INCMAGLEN           [TSN MODIFIER
   5 3GSG    REWIND  
  16 3H#6          REWIND                       [REWIND INCINDEX 
  15 3HRQ          ADN   4  INCMAGLEN           [ADD MODIFIER
  11 3J?B          MHUNTW   1,FILE,FINTER7   
  10 3JR2          LDX   0  ALOGLEN(1)   
   7 3K=L          SMO      4
   9 3KQ=          SBN   0  AMAGS-A1 
  21 3L9W          BZE   0  XER14               [ERROR IF CAN'T FIND TAPE WITH SAME I
  18 3LPG                                       [   AS THAT RESTORED FROM
   7 3M96          SMO      4
  16 3MNQ          LDX   6  AMAGS(1)            [PICK UP NEXT TSN
  17 3N8B          LDN   5  0                   [SET INC. MODIFIER   
   5 3NN2    STEP2   
  18 3P7L          STEP                         [NEXT RECORD (INCREMENT) 
   8 3PM=          BZE   3  TEST2
  19 3Q6W          CALL  2  TSN                 [IS THIS TSN IN THIS RECORD? 
  13 3QLG          BRN      STEP2               [NO  
  10 3R66          MHUNTW   1,FILE,FINTER
   7 3RKQ          SMO      5
  10 3S5B          LDX   0  FIRSTINC(1)  
  21 3SK2          BXU   0  AINCNO-A1(3),REWIND [J IF THIS TAPE DOES NOT CONTAIN REQU
  15 3T4L                                       [   INCRS.   
  15 3TJ=          LDN   7  1                   [SET SWITCH  
  18 3W3W          ADN   5  1                   [ADD 1 TO INC. MODIFIER  
   8 3WHG          BRN      STEP2
   5 3X36    TEST2   
  19 3XGQ          BZE   7  REWIND              [TSN AS COPY NOT YET FOUND   
  20 3Y2B    #    THE TAPE SERIAL NO. FOUND AS A COPY IS REPLACED BY THE CURRENT 
  14 3YG2    #    TSN IN SYSTEM.SERIAL AND SYSTEM.DUMPER.
  18 3Y_L          CALL  4  SSER                [FIND THIS TAPE IN SERIAL
  17 3_F=          BRN      XERROR3             [ERROR IF NOT THERE  
   8 3_YW          READAGAIN 1   
  10 42DG          MHUNTW   3,FILE,FRB   
  10 42Y6          NAME     3,FILE,FWB   
  11 43CQ          MHUNTW   1,FILE,FINTER7   
  18 43XB          LDX   5  6                   [X5= TAPE TO BE REPLACED 
  10 44C2          LDX   6  CURRTSN(1)   
  15 44WL          STO   6  A1+1(3)             [REPLACE TSN 
   7 45B=          DELETE   1
  21 45TW          CALL  4  SSER                [FIND PLACE FOR NEW TAPE IN SERIAL   
   8 46*G          BRN      XCOPY
  21 46T6          BRN      XER13               [ERROR  -COPY TAP5 KNOWN TO SERIAL   
   5 47#Q    XCOPY   
  18 47SB          INSERT  1                    [INSERT NEW TAPE RECORD  
   9 48#2          MFREEW   FILE,FWB 
  19 48RL          DOWN  INCTAPES,2             [OPEN :DUMPER IN GENERAL MODE
   5 49?=    STEP4   
   6 49QW          STEP  
  18 4==G          BZE   3  XERROR5             [J IF TSN NOT IN :DUMPER 
  10 4=Q6          LDX   0  ERES-A1(3)   
  17 4?9Q          BNZ   0  STEP4               [J IF NOT NAME RECORD
  18 4?PB          BXU   5  ESER-A1(3),STEP4    [J 96 NOT R5QU9R5D TSN   
  11 4#92          MHUNTW   1,FILE,FINTER7   
  17 4#NL          LDX   0  BMEREEL(1)          [PICK UP THE REEL NO 
  18 4*8=          STO   0  EREEL-A1(3)         [ & INSERT IN DIR. ENTRY 
  19 4*MW          LDX   0  BMEGEN(1)           [ PICK UP THE GENERATION NO. 
  18 4B7G          STO   0  EGEN-A1(3)          [ & INSERT IN DIR. ENTRY 
  15 4BM6          STO   6  ESER-A1(3)          [REPLACE TSN 
  18 4C6Q          LDX   6  5                   [X6= TAPE BEING REPLACED 
  16 4CLB          STEPREWRITE                  [REWRITE RECORD  
  16 4D62          CLOSETOP                     [CLOSE :DUMPER   
   7 4DKL          LDN   0  1
  15 4F5=          STO   0  AWORK4(2)           [ COPY TAPE  
  20 4FJW    #    IN THE FILE SYSTEM.ININDEX THE RECORD FOR EACH INCREMENT ON THE
  11 4G4G    #    CURRENT TAPE IS ALTERED.   
  20 4GJ6    #    THE TSN FOR THE COPY OR THE CURRENT TSN ITSELF IF IT EXISTS IS 
  18 4H3Q    #    REMOVED AND THE CURRENT TSN INSERTED AS THE FIRST TSN. 
   4 4HHB    X1  
  16 4J32          REWIND                       [REWIND INCINDEX 
   5 4JGL    STEP3   
   6 4K2=          STEP  
  18 4KFW          BZE   3  XER14               [ERROR IF NOT IN INCINDEX
  10 4K_G          MHUNTW   1,FILE,FINTER
  10 4LF6          LDX   0  AINCNO-A1(3) 
  10 4LYQ          TXU   0  FIRSTINC(1)  
   8 4MDB          BCS      STEP3
   9 4MY2          BRN      STOTSN   
   4 4NCL    NEXT
   6 4NX=          STEP  
  14 4PBW          BZE   3  XEND1               [END OF  
   5 4PWG    STOTSN  
  21 4QB6          LDEX  0  FRH(3)              [X0= NO. OF TAPES IN THIS INCREMENT  
   9 4QTQ          SBN   0  AMAG-A1  
  15 4R*B          LDN   7  0                   [CLEAR SWITCH
  18 4RT2    SEARCH                             [LOOK FOR THIS TAPE -COPY
   9 4S#L          SBN   0  INCMAGLEN
   8 4SS=          BNG   0  XEND 
   8 4T?W          BNZ   7  MOVE 
   7 4TRG          SMO      0
  10 4W?6          TXU   6  AMAG-A1(3)   
   9 4WQQ          BCS      SEARCH   
  15 4X=B          LDN   7  1                   [TAPE FOUND  
   9 4XQ2          BRN      SEARCH   
   4 4Y9L    MOVE
   7 4YP=          SMO      0
  10 4_8W          LDN   1  AMAG-A1(3)   
  10 4_NG          LDN   2  INCMAGLEN(1) 
   9 5286          LDN   4  INCMAGLEN
   8 52MQ          SMO      FX2  
   9 537B          STO   0  ACOMMUNE2
   5 53M2    TSNMOVE 
  17 546L          LDX   5  0(2)                [MOVE TSNS DOWN LIST 
  17 54L=          LDX   0  0(1)                [MOVING UP COPY TSN  
   8 555W          STO   0  0(2) 
   8 55KG          STO   5  0(1) 
   7 5656          ADN   2  1
   7 56JQ          ADN   1  1
   9 574B          BCT   4  TSNMOVE  
   8 57J2          SMO      FX2  
   9 583L          LDX   0  ACOMMUNE2
   9 58H=          BRN      SEARCH   
   4 592W    XEND
  21 59GG          BZE   7  XER18               [ERROR IF NOT COPY OF CURRENT DUMP TA
  11 5=26          HUNT     1,FILE,FINTER7   
   9 5=FQ          LDX   4  JINCLAST 
  10 5=_B          SBX   4  AINCNO-A1(3) 
   9 5?F2          BNZ   4  XNOTSN   
  10 5?YL          LDX   4  BMINTO1(1)   
  11 5#D=          STO   4  AINCFILES-A1(3)  
  10 5#XW          LDX   4  BMINTO2(1)   
  11 5*CG          STO   4  AINCSIZE-A1(3)   
   5 5*X6    XNOTSN  
   8 5BBQ          SMO      FX2  
   9 5BWB          LDX   0  AWORK4   
   8 5CB2          BZE   0  WRITE
  11 5CTL          HUNT     1,FILE,FINTER7   
   8 5D*=          LDCT  0  #400 
  11 5DSW          ANDX  0  AMAGSTATE-A1(3)  
  20 5F#G          ERS   0  AMAGSTATE-A1(3)     [REMOVE B0 OF STATE WORD IF SET  
  10 5FS6          LDX   0  CURRTSN(1)   
  10 5G?Q          STO   0  AMAG-A1(3)   
   5 5GRB    WRITE   
   8 5H?2          LDX   2  FX2  
   8 5HQL          STEPREWRITE   
   6 5HXS ...#UNS  FPD   
   4 5J52 ...#SKI
   8 5J==          BRN      NEXT 
   6 5J=R ...#UNS  FPD   
   4 5J?# ...(   
  21 5J?T ...      LDX   4  JINCLAST            [ IF WE HAVE NOT UPDATED RESTORED INC
  21 5J#B ...      BXU   4  AINCNO-A1(3),NEXT   [ CONTINUE STEPPING THROUGH INCINDEX 
  19 5J#X ...#     THE RESTORED INCREMENT HAS BEEN UPDATED - WAS IT PARALLEL 
  11 5J*D ...      MHUNTW   1,FILE,FINTER7   
  21 5J*_ ...      LDX   4  GRPINCNUM(1)        [ IF GRPINCNUM IS ZERO INCREMENT WAS 
  17 5JBG ...      BZE   4  NOTPARALLEL         [ PARALLEL INCREMENT 
  18 5JC3 ...      WIND                         [ GO TO END OF INCINDEX  
  18 5JCJ ...      STEPAGAIN                    [ AND READ LAST RECORD   
  13 5JD5 ...      BXE   4  AINCNO-A1(3),PARALLELINC 
  16 5JDL ...#     JUMP - WE HAVE FOUND THE PARALLEL INCREMENT   
  19 5JF7 ...      BACKSPACE                    [ READ PENULTIMATE INCREMENT 
  12 5JFN ...      STEPAGAIN                    [
  13 5JG9 ...      BXU   4  AINCNO-A1(3),PARALLELEND 
  16 5JGQ ...#     IF THIS IS NOT THE PARALLEL INCREMENT GIVE UP 
  21 5JH? ...PARALLELINC                        [ WE HAVE FOUND THE PARALLEL INCREMEN
  11 5JHS ...      MHUNTW   1,FILE,FINTER7   
  21 5JJ* ...      LDX   0  AINCSIZE-A1(3)      [ IF THE SIZE HAS ALREADY BEEN INSERT
  18 5JJW ...      BNZ   0  NOTPARALLEL         [ DONT UPDATE THE RECORG 
  20 5JKC ...      LDX   0  GRPINCFILES(1)      [ UPDATE NO OF FILES IN INCREMENT
  12 5JKY ...      STO   0  AINCFILES-A1(3)     [
  19 5JLF ...      LDX   0  GRPINCSIZE(1)       [ UPDATE SIZE OF INCREMENT   
  11 5JM2 ...      STO   0  AINCSIZE-A1(3)   
  21 5JMH ...      STEPREWRITE                  [ REWRITES RECORD FOR PARALLEL FILES 
   6 5JN4 ...NOTPARALLEL 
   6 5JNK ...PARALLELEND 
   4 5JP6 ...)   
   5 5JPW    XEND1   
  16 5K9G          CLOSETOP                     [CLOSE INCINDEX  
  15 5KP6          CLOSETOP                     [CLOSE SERIAL
  10 5L8Q          MFREEW   FILE,FINTER  
  10 5LNB          MFREE    FILE,FINTER7 
   5 5M82          UP
   4 5MML    #   
   4 5N7=    #   
  11 5NLW    XER14       ACROSS RESTERR,14   
  11 5P6G    XERROR3   ACROSS   RESTERR,15   
  11 5PL6    XERROR5   ACROSS   RESTERR,17   
  11 5Q5Q    XER18       ACROSS RESTERR,18   
   4 5QKB    #   
   5 5R52    XERROR2 
  17 5RJL          GEOERR   BRIEFPM,GRBREAK     [BREAK-IN IMPOSSIBLE 
   4 5S4=    #   
  20 5SHW    #   THIS SUBROUTINE CHECKS WHETHER A TSN IS IN A RECORD OF INCINDEX 
  13 5T3G    #   ON ENTRY   X3=STEP POINTER TO RECORD
   9 5TH6    #              X6=TSN   
  10 5W2Q    #              X2=CALL ACC  
  17 5WGB    #   EXIT  0                            IF NOT PRESENT   
  16 5X22    #   EXIT  1                            IF PRESENT   
   4 5XFL    TSN 
   9 5X_=          LDEX  0  FRH(3)   
  20 5YDW          SBN   0  AMAG-A1             [X0= LENGTH OF MT PART OF RECORD 
   4 5YYG    TSN1
   7 5_D6          SMO      0
  12 5_XQ          LDX   1  AMAG-A1-INCMAGLEN(3) 
   7 62CB          TXU   1  6
  16 62X2          BCC      TSN2                [IF EQUAL EXIT+1 
   9 63BL          SBN   0  INCMAGLEN
   8 63W=          BNZ   0  TSN1 
  16 64*W          EXIT  2  0                   [NOT EQUAL EXIT  
   4 64TG    TSN2
   7 65*6          EXIT  2  1
   4 65SQ    #   
  18 66#B    #  THIS SUBROUTINE SEARCHES :SYSTEM.SERIAL FOR THE TSN IN X6
   9 66S2    #  EXITS +1 IF TSN FOUND
  10 67?L    #  EXITS +0 IF TSN NOT FOUND
   4 67R=    SSER
   8 68=W          SBX   4  FX1  
  16 68QG          SSEARCH  SNOTSN,6,1          [SEARCH FOR TSN  
   8 69=6          ADX   4  FX1  
   7 69PQ          EXIT  4  1
   5 6=9B    SNOTSN  
   8 6=P2          ADX   4  FX1  
   7 6?8L          EXIT  4  0
   4 6?N=    #   
  10 6#7W          MENDAREA 30,K99RESTEND
   4 6#MG    #   
   4 6*76    #END
   8 ____ ...26562220000100000000
