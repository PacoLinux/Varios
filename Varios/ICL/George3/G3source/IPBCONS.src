  14 22FL    #SEG  IPBCONS8                     [R.WEYAND
  11 22_=    #LIS  K0IPBCONS>K0IPB>K0ALLGEO  
   9 23DW                   8HIPBCONS
   4 23YG    #   
  12 24D6          SEGENTRY K1IPBCONS,SK1IPBCONS 
  21 24XQ    # THIS SEGMENT CONTROLS A 7023 (REMOTE CONSOLE) CONNECTED VIA AN IPB :  
  21 25CB    # EACH OUTPUT MESSAGE MUST BE ON THE CONSOLE BEFORE THE NEXT IS OUTPUT ;
  21 25X2    # A CPAT IS SET UP TO DEAL WITH ANY DATA INPUT ; WHEN WOKEN , CHECK FOR 
  21 26BL    # SUPERVISORY INPUT , OUTPUT , DATA INPUT & SUP. INPUT AGAIN BEFORE COOR
   4 26W=    #   
   9 27*W    NL             #76327620
   9 27KN ...SM16           #77777577
   8 27TG    SHIFT          #74  
   9 28*6 ...PROLINK PAIR   COMPROA,1
  14 28SQ    SUPCL          #01260000           [CLOSE   
   9 29#B    SUPSEG         #01240000
   5 29S2    XADCB   
   8 2=?L    #HAL  AMXOR+ADCB,0  
   5 2=R=    XIDATSG 
   8 2?=W    #HAL  AMXOR+IDATSG,0
  14 2?QG    OFFMES         #44000070,#76327627,#76277627
  17 2#=6                   36H_'THIS TERMINAL HAS BEEN MOPPED OFF   
  14 2#PQ                   #76277627,#76277627,#76327632
   5 2*9B    OFFEND  
  14 2*P2    OLMESS         #44000040,#76327627,#76273220
  12 2B8L                   16HYOU ARE ONLINED   
  12 2BN=                   #32207627,#76277632  
   5 2C7W    OLEND   
  14 2C7_ ...TLFAILMESS     #44000070,#76327627,#76273220
  16 2C84 ...               32HTHIS TERMINAL/LINE FAILED AT ___  
  10 2C87 ...TIME           8H00.00.00   
  12 2C8= ...               #20327627,#76277632  
   5 2C8* ...TLFEND  
   6 2C8G ...#UNS  CLSKI 
   4 2C96 ...(   
  19 2C9S ...NLAHMK         #56757777             [FOR UNSETTING ATTACH BITS 
  17 2C?Q ...VRDCB                              [DERING AND FREE DCB 
  15 2C*N ...      LDX   1  A1+FPTR(3)          [ADEVS ENTRY 
   9 2CCL ...      STOZ     BPTR(1)  
  10 2CFJ ...      STOZ     A1+BPTR(3)   
   7 2CHG ...      FREECORE 3
   7 2CKD ...      EXIT  5  0
   4 2CLF ...)   
   4 2CMG    #   
  20 2C_X ...#     WAIT FOR SUPERVISORY REPLY BEFORE CLOSING DOWN OR RESTARTING  
  18 2D## ...#     OUTPUT(AFTER A LINE/TERMINAL FAIL) : CLOSE DOWN IF IPB
  21 2DLQ    #     INOPERABLE ; EXIT IF IDENT. INOPERABLE ; EXIT+1 IF SUPERVISORY SEG
   4 2F6B    #   
  10 2FL2    REWA  MHUNTW   3,AMXOR,ADCB 
   9 2G5L          SMO      IADIN(3) 
   8 2GK=          LDXC  4  IPBSW
  19 2H4W          BCS      SUC                 [IPB INOPERABLE : CLEAR UP   
  10 2HJG          LDXC  4  ISTATUS(3)   
  19 2J46          BCS      (7)                 [IDENTIFIER INOPERABLE : EXIT
   8 2JHQ    RESL  LDCT  0  #400 
   8 2K3B          SBX   7  FX1  
   9 2KH2          ORS   0  IWAIT(3) 
   6 2L2L          COOR1 
   8 2LG=          ADX   7  FX1  
  19 2LQ4 ...REHU  LDX   1  ACTRING(2)          [CHECK FOR SUPERVISORY REPLY 
  21 2L_W ...      SBN   1  ACTRING             [(OLDEST) SUPER REPLY WILL BE BEFORE 
  10 2M4F ...      MHUNTW   3,AMXOR,ADCB 
   4 2M74 ...RNEX
  16 2M9N ...      LDX   1  BPTR(1)             [NEXT ACTIVITY   
   9 2M*K ...      BXE   1  3,REWA   
   9 2MFG ...      LDX   0  ATYPE(1) 
   8 2MP# ...      SMO      FX1  
   9 2M_6 ...      TXU   0  XIDATSG  
  18 2N8Y ...      BCS      RNEX                [NO SUPERVISORY REPLY YET
   9 2NDQ          LDX   3  A1+2(1)  
   7 2NYB          FREECORE 1
   8 2PXL          SRL   3  12   
  15 2QC=          ANDN  3  #77                 [ISOLATE CODE
   7 2QWW          EXIT  7  1
   4 2RBG    #   
  19 2RW6    #     RING AMXOR/IOUT BLOCK AFTER IPBOUT AND PUT IT ON THE LIST 
  21 2S*Q    #     NEEDS ADDRESS OF AMXOR/IOUT IN X1 AND ADDRESS OF AMXOR/ADCB IN X3 
   4 2STB    #   
  19 2T*2    RINGO LDX   2  IADIN(3)            [ADDRESS OF IPBIN ACTIVITY   
  10 2TSL          LDX   3  IPBRING(2)   
  19 2W#=          SBN   3  IPBRING             [ADDRESS OF IPBOUT ACTIVITY  
  10 2WRW          LDX   2  ACTRING(3)   
  18 2X?G          SBN   2  ACTRING             [ACTIVITY AFTER IPBOUT   
   9 2XR6          CHAIN    1,BPTR(2)
   8 2Y=Q          LDCT  0  #100 
  18 2YQB          ANDX  0  IPBSW(3)            [B2 SET IF IPBOUT ASLEEP 
   8 2_=2          BZE   0  WAKE 
   9 2_PL          ERS   0  IPBSW(3) 
   7 329=          LDX   2  3
   6 32NW          FPUT  
   7 338G    WAKE  EXIT  7  0
   4 33N6    #   
  21 347Q    #     TEST WHETHER 'CLOSE IDENTIFIER' RECEIVED FOR ONLINE-T&ST REQUEST :
  21 34MB    #     SEND 'CLOSE' AND WAIT FOR 'CLOSE IMPLEMENTED' REPLY ; CALL ON X5  
   4 3572    #   
   9 35LL    OLTE  LDX   0  IWAIT(3) 
  10 366=          SMO      A1+FPTR(3)   
   9 36KW          LDX   4  FOURTHWD 
   8 375G          ANDN  0  #40  
   8 37K6          ANDX  4  BIT9 
  17 384Q          BZE   0  (5)                 [TEST NOT REQUESTED  
  16 38JB          BNZ   4  (5)                 [ALREADY CLOSED  
   7 3942    SECL  LDN   4  3
   8 39HL          SBX   5  FX1  
   8 3=3=          LDCT  6  #400 
  19 3=GW          CALL  7  SETU                [GET IOUT BLOCK FOR 'CLOSE'  
   9 3?2G          ADX   4  SUPCL(1) 
   9 3?G6          STO   4  IPBIDE(2)
   7 3?_Q          LDX   1  2
  16 3#FB          CALL  7  RINGO               [OUTPUT 'CLOSE'  
  19 3#_2          CALL  7  REWA                [WAIT FOR CLOSE IMPLEMENTED  
  18 3*DL          BRN      RESL                [I KNOW IT'S INOPERABLE !
   8 3*Y=          ERN   3  #66  
  18 3BCW          BNZ   3  REHU                [NOT CLOSE IMPLEMENTED   
   8 3BXG          ADX   5  FX1  
   9 3CC6          LDX   0  AWORK2(2)
  18 3CWQ          BZE   0  (5)                 [NO OUTPUT IN PROGRESS   
   9 3DBB          STOZ     AWORK2(2)
  21 3DW2          MFREEW   CONBUFF,IREMTW      [OUTPUT SENT WILL HAVE BEEN DISCARDED
   7 3F*L          EXIT  5  0
   4 3FT=    #   
  20 3G#W    #     GET AMXOR/IOUT BLOCK : LENGTH IN X4 ; BLOCK STATUS WORD IN X6 
   4 3GSG    #   
   8 3H#6    SETU  SBX   7  FX1  
  11 3HRQ          SETUPCORE 4,2,AMXOR,IOUT,1
   8 3J?B          ADX   7  FX1  
  17 3JR2          STO   6  IBSW(2)             [BLOCK STATUS WORD   
  10 3K=L          MHUNTW   3,AMXOR,ADCB 
  10 3KQ=          LDX   4  IDENTNO(3)   
   7 3L9W          EXIT  7  0
   4 3LPG    #   
  21 3M96    #     LOOK FOR 'SUCCESS' OR 'FAIL' REPORT ON PREVIOUS OUTPUT ; IF FAIL ,
  21 3MNQ    #     REPORT INOPERABILITY OR REPEAT OUTPUT ; IF SUCCESS , PROCEED WITH 
  21 3N8B    #     NEXT OUTPUT AND/OR DEAL WITH ANY INPUT ; IF NEITHER & IDLE , COOR1
   4 3NN2    #   
   6 3P7L    SK1IPBCONS  
  21 3PM=          STOZ     AWORK2(2)           [INITIALISE 'OUTPUT IN PROGRESS' AND 
  16 3Q6W    WASID LDN   7  0                   ['IDLE' MARKERS  
  21 3QLG    SKSUP LDX   3  ACTRING(2)          [ENTRY WHEN NO INPUT OR OUTPUT TO DO 
  18 3R66          SBN   3  ACTRING             [ADDRESS OF NEXT ACTIVITY
  21 3RKQ    NEB   LDX   3  BPTR(3)             [SEARCH BACK FOR SUPERVISORY SEGMENT 
   9 3S5B          LDX   0  ATYPE(3) 
  20 3SK2          TXU   0  XADCB(1)            [IF DCB ENCOUNTERED FIRST , THEN 
  20 3T4L          BCC      NOSUP               [NO SUPERVISORY SEGMENTS WAITING 
  10 3TJ=          TXU   0  XIDATSG(1)   
   8 3W3W          BCS      NEB  
   7 3WHG          BCHX  3  /
  19 3X36          LDCH  4  A1+2(3)             [CODE OF SUPERVISORY SEGMENT 
   9 3XGQ          ANDX  3  BITS22LS 
   7 3Y2B          FREECORE 3
   7 3Y7J ...#UNS  CCERROR   
   7 3Y#Q ...      LDX   0  4
   8 3YG2          SBN   4  53   
  16 3Y_L          BZE   4  REPFL               ['FAIL' REPORTED 
  19 3_F=          SBN   4  7                   [CODE 60 = OUTPUT SUCCESSFUL 
  20 3_YW          BZE   4  NOGE                [SUCCESS : READY FOR MORE OUTPUT 
   7 42DG          ADN   4  2
  21 42Y6          BZE   4  REPFL               [CODE 58 : UNAVAILABLE = 7020 INOP   
   8 43CQ          ADN   4  10   
  18 43G? ...      BNZ   4  ZERRJ               [J IF NOT IDENT OPENED   
  10 43H6 ...      MHUNTW   3,AMXOR,ADCB 
   9 43H_ ...      LDX   0  IMDNPU(3)
  21 43JS ...      ANDX  0  BSP16               [ISOLATE NPU RECEIVED IN ID OPENED SE
  15 43KM ...      BNZ   0  SUNAV               [J IF VALID  
  21 43LG ...[                                   VIRTUAL 7900 MAY SEND ZERO NPUSIZE  
  21 43M* ...[                                   INDICATES IT IS IRRELEVANT- USE OLD 
   8 43N8 ...      SMO      FX2  
   9 43P3 ...      LDX   0  AWORK1   
   9 43PW ...      STO   0  IMDNPU(3)
   5 43Q8 ...SUNAV   
  10 43QG ...      LDXC  0  ISTATUS(3)   
  20 43QS ...      BCS      REPFL               [J. IDENT OPENED BUT UNAVAILABLE 
   8 43R6 ...      BRN      SKSUP
   5 43RJ ...ZERRJ   
   7 444J ...#UNS  CCERROR   
   4 449Q ...#SKI
  10 44C2          GEOERR   1,WRONGSUP   
   7 44F9 ...#UNS  CCERROR   
   4 44HD ...(   
   7 44KM ...      LDX   4  0
  10 44MW ...      CCERROR  CCWRONGSUP,4 
   8 44Q5 ...      BRN      SUC  
   4 44S# ...)   
   9 44WL    NOGE  LDX   0  AWORK2(2)
  20 45B=          BZE   0  SKSUP               [NO OUTPUT IN PROGRESS : IGNORE  
  17 45TW          MFREEW   CONBUFF,IREMTW      [FREE OUTPUT BLOCK   
  10 46*G          BRN      SK1IPBCONS   
   4 46T6    #   
  16 47#Q    NOSUP BZE   7  NOTID               [NON-ZERO IF IDLE
  17 47SB    SLEEP LDX   1  A1+FPTR(3)          [POINT TO ADEVS ENTRY
  20 48#2          LDCT  0  #101                [B8 SET IF ONLINING CONCEPTUAL   
   8 48RL          LDCT  4  #100 
  19 49?=          ANDX  0  FOURTHWD(1)         [B2 SET IF BEING MOPPED OFF  
  21 49QW          ANDX  4  FINISH              [B2 SET IF 'FINISH NOW' COMMAND GIVEN
   7 4==G          SLL   0  2
   8 4=Q6          BNG   0  OPOF 
   8 4?9Q          BNZ   4  OFFI 
   8 4?PB          BNZ   0  ONLI 
   5 4?QF ...SLEEP1  
   9 4?Y3 ...      LDX   0  IMDNPU(3)
   8 4#2= ...      SMO      FX2  
  20 4#4F ...      STO   0  AWORK1              [SAVE NPU IN CASE GOES ON TEST   
  21 4#92          CALL  5  OLTE                [TEST WHETHER ONLINE-TEST REQUESTED  
  10 4#NL          MHUNTW   3,AMXOR,ADCB 
   8 4*8=          LDCT  7  #400 
  21 4*MW          ORS   7  IWAIT(3)            [SET B0 TO INDICATE 'ASLEEP' IN DCB  
   6 4B7G          COOR1 
  10 4BM6          MHUNTW   3,AMXOR,ADCB 
   8 4C6Q          LDCT  0  #100 
  10 4CLB          ANDX  0  ISTATUS(3)   
  20 4D62          BNZ   0  SLEEP [IF IDENTIFIER CLOSED(ON TEST),GO BACK TO SLEEP
   8 4DKL          BRN      WASID
   4 4F5=    #   
   9 4FJW    NOTID LDX   0  AWORK2(2)
  21 4G4G          BNZ   0  SKIN                [OUTPUT IN PROGRESS : LOOK FOR INPUT 
  10 4G6D ...      LDX   4  ISTATUS(3)   
   7 4G8B ...      SLC   4  6
  21 4G=# ...      BNG   4  SENDFAILMES         ['SEND FAIL MESS TO TERMINAL' FLAG   
   4 4G#= ...#   
  11 4GB8 ...#     LOOK FOR CONSOLE OUTPUT   
   4 4GD6 ...#   
   5 4GG4 ...SKOUT   
  18 4GJ6          HUNTW    1,CONBUFF,IREMTW    [NO OUTPUT IN PROGRESS   
  17 4H3Q          BNG   1  SKIN                [NO OUTPUT WAITING   
   8 4HHB          LDCT  0  #400 
  10 4J32          ANDX  0  ISTATUS(3)   
  19 4JGL          BNZ   0  SETFL               [INOPERABLE OR FAIL BIT SET  
  18 4K2=          LDEX  0  ICHARS(3)           [LENGTH OF CONSOLE LINE  
  10 4KFW          STO   0  ACOMMUNE1(2) 
   4 4K_G    #   
  20 4LF6    #     CONVERT OUTPUT INTO LINE FORM AND ENLIST IPBOUT TO OUTPUT IT  
   4 4LYQ    #   
   9 4MDB          LDX   3  IMDNPU(3)
  19 4MY2          ANDN  3  #7777               [NPU. SIZE ON THIS IDENTIFIER
  10 4NCL          LDN   4  IPBIDE-A1(3) 
   7 4NX=          LDN   6  0
   8 4PBW          CALL  7  SETU 
  17 4PWG          STO   4  IPBIDE(2)           [IDENTIFIER NUMBER   
   8 4QB6          LDCT  0  #440 
   9 4QTQ          LDX   6  IMDNPU(3)
  19 4R*B          ANDN  6  #7777               [NPU SIZE ON THIS IDENTIFIER 
  21 4RT2          STO   0  IPBIDE+1(2)         [B0 : ETX ; B3 : REPORT SUCCESS/FAIL 
   7 4S#L          SLL   6  2
   7 4SS=          LDN   0  1
  16 4T?W          SBN   6  4                   [SAFETY MARGIN   
  19 4TRG          STO   0  IMOPTY(2)           [WAKE CONSA AFTER OUTPUT SENT
   8 4W?6          ADN   2  IDATA
  11 4WQQ          MHUNTW   3,CONBUFF,IREMTW 
   8 4X=B          LDEX  7  A1(3)
  20 4XQ2          SLL   7  2                   [CONVERT WORD COUNT INTO CHARS   
  19 4Y9L          BZE   7  STNL                [NULL OUTPUT : NEWLINE ONLY  
   8 4YP=          LDN   4  #76  
   9 4_8W          ADN   3  ICBUFF   
   8 4_NG    STMOR SMO      FX2  
  16 5286          LDX   5  ACOMMUNE1           [CONSOLE WIDTH   
   7 52MQ          SBX   5  7
  17 537B          BNG   5  OVEL                [MORE THAN ONE LINE  
  16 53M2          LDN   5  0                   [ONLY ONE LINE   
   8 546L          BRN      WLOOP
  17 54L=    OVEL  ADX   7  5                   [CONSOLE WIDTH IN X7 
  18 555W          NGX   5  5                   [REMAINDER COUNT IN X5   
   8 55KG    WLOOP LDCH  0  0(3) 
   7 5656          BCHX  3  /
  13 56JQ          TXL   0  SHIFT(1)            [#74 
   8 574B          BCS      WLOP 
   7 57J2          SBN   6  1
  21 583L          BZE   6  STNL                [NPU FULL : TRUNCATE MESS. IF NEC.   
  20 58H=          DCH   4  0(2)                [TRANSLATE #74-#76 INTO LINE CODE
   7 592W          BCHX  2  /
   8 59GG          SBN   0  #10  
   8 5=26    WLOP  DCH   0  0(2) 
   7 5=FQ          BCHX  2  /
   7 5=_B          SBN   6  1
  14 5?F2          BZE   6  STNL                [NPU FULL
   8 5?YL          BCT   7  WLOOP
  20 5#D=          ADS   5  7                   [COUNT OF CHARS STILL TO CONVERT 
  17 5#XW    STNL  LDX   0  NL(1)               [#76327620 : NEWLINE 
   7 5*CG          LDN   1  0
   7 5*X6          MVCH  1  4
   7 5BBQ          SBN   6  4
   8 5BWB          LDX   1  FX1  
  18 5CB2          BNG   6  NFUL                [NPU FULL : OUTPUT BLOCK 
  17 5CTL          BZE   6  NFUL                [AND TRUNCATE MESSAGE
  19 5D*=          BNZ   7  STMOR               [END OF LINE BUT MORE TO COME
   4 5DSW    #   
  10 5F#G    NFUL  MHUNTW   3,AMXOR,ADCB 
   9 5FS6          LDX   4  IMDNPU(3)
  14 5G?Q          ANDN  4  #7777               [NPU SIZE
   7 5GRB          SLL   4  2
  16 5H?2          SBN   4  4                   [SAFETY MARGIN   
  18 5HQL          SBX   4  6                   [SUBTRACT REMAINDER COUNT
   8 5J==          SMO      FX2  
   8 5JPW          LDX   2  FPTR 
  19 5K9G          DSA   4  IPBIDE+1(2)         [COUNT OF CHARS. IN MESSAGE  
  10 5KP6          ADN   4  IDATA-A1*4+3 
   7 5L8Q          SRL   4  2
  18 5LNB          ALTLEN   2,4                 [ALTER TO CORRECT LENGTH 
  10 5M82          MHUNTW   3,AMXOR,ADCB 
  10 5MML          MHUNTW   1,AMXOR,IOUT 
  20 5N7=          CALL  7  RINGO               [ENLIST IPBOUT TO OUTPUT BLOCK   
   7 5NLW          LDN   0  1
  20 5P6G          STO   0  AWORK2(2)           [SET 'OUTPUT IN PROGRESS' MARKER 
   4 5PL6    #   
  21 5Q5Q    #     LOOK FOR CONSOLE INPUT , CONVERT FROM LINE CODE AND SET UP CPAT TO
  21 5QKB    #     DEAL WITH IT ; IDATSG BEFORE DCB IS INPUT & AFTER DCB IS SUP. SEG.
   4 5R52    #   
   8 5RJL    SKIN  LDX   1  FX1  
   8 5S4=          LDCT  7  #400 
   8 5SHW    NINT  LDX   3  FX2  
  20 5T3G    NIB   LDX   3  FPTR(3)             [LOOK FORWARDS FOR NORMAL INPUT  
   9 5TH6          LDX   0  ATYPE(3) 
  18 5W2Q          TXU   0  XADCB(1)            [NONE IF DCB MET FIRST   
  18 5WGB          BCC      SKSUP               [NOW LOOK FOR SUP. SEG.  
  10 5X22          TXU   0  XIDATSG(1)   
  19 5XFL          BCS      NIB                 [NO AMXOR/IDATSG FOUND YET   
  10 5XHN ...      HUNTW    1,FILE,FRB   
  20 5XKQ ...      BNG   1  NOLON               [NOT SECOND PART OF LONG INPUT   
  17 5XMS ...      LDX   5  A1(1)               [COUNT OF FIRST PART 
   8 5XPW ...      LDX   3  A1(3)
   8 5XRY ...      ADS   3  A1(1)
  17 5XW2 ...      ANDN  3  #7777               [COUNT OF SECOND PART
  11 5XY4 ...      LDN   4  CPDATA-A1*4+3(3) 
   7 5Y26 ...      ADX   4  5
   7 5Y48 ...      LDX   6  1
   7 5Y6= ...      SRL   4  2
  17 5Y8# ...      ALTLENG  6,4,RIOT            [LENGTHEN FILE/FRB   
  11 5Y=B ...      MHUNTW   1,AMXOR,IDATSG   
   7 5Y#D ...      SRC   5  2
  10 5YBG ...      MHUNTW   2,FILE,FRB   
   9 5YDJ ...      LDN   4  CPDATA(1)
   9 5YGL ...      ADN   5  CPDATA(2)
  18 5YJN ...      BZE   3  PASSA               [J IF ZERO MESSAGE COUNT 
  20 5YLQ ...      MVCH  4  0(3)                [APPEND SECOND PART OF MESSAGE   
   7 5YNS ...PASSA LDX   3  2
   7 5YQW ...      FREECORE 1
   8 5YSY ...      BRN      ZTES 
  21 5YX2 ...RIOT  MHUNTW   2,FILE,FRB          [RE-FIND IF CORE MOVES DURING ALTLENG
   7 5Y_4 ...      EXIT  1  0
  10 5_36 ...NOLON NAME     3,FILE,FRB   
   8 5_58 ...      LDX   1  FX1  
   8 5_7= ...ZTES  LDX   6  A1(3)
   8 5_9# ...      LDN   0  #7777
  18 5_?B ...      ANDS  0  A1(3)               [ISOLATE CHARACTER COUNT 
  21 5_*D ...      BPZ   6  RECK                [NOT EOM:WAIT FOR REST OF INPUT MESS 
  19 5_D6          CHANG                        [CONVERT BACK FROM LINE CODE 
   8 5_XQ          GETACT   CPAT 
   9 62CB          LDX   3  BPTR(2)  
  16 62X2          LINKSET  3,PROLINK(1)        [LINK TO K1COMPRO
   8 63BL          LDN   0  #222 
   8 63W=          LDN   4  #61  
  21 64*W          STO   0  CONTEXT(3)          [SET REMOTE/NO-USER/OPERATOR CONTEXT 
  18 64TG          STO   4  AMXMSK(3)           [SET MONITORING FILE MASK
  19 65*6          LDN   4  AWORK3(2)           [AWORK3-4 SET UP BY IPBSCAN  
  10 65SQ          LDN   5  JSOURCE1(3)  
   7 66#B          MOVE  4  2
  19 66J7 ...      LDX  0  CPPTR(2)             [LD CPPTR FROM CONSA/I7023   
  16 66LQ ...      STO  0  CPPTR(3)             [ST IN NEW CPAT  
  10 66S2          MHUNTW   1,FILE,FRB   
  19 67?L          CHAIN    1,3                 [CHAIN INPUT AFTER NEW CPAT  
   7 67R=          LDX   2  3
  17 68=W          FPUT                         [ACTIVATE NEW CPAT   
   4 68GN ...RECK
  21 68QG          LDN   7  0                   [CHECK EVERYTHING AGAIN BEFORE COOR1 
   8 69=6          BRN      NINT 
   4 69PQ    #   
  19 6=9B    #     MOP OFF IN PROGRESS : STOP INPUT AND CLEAR , THEN CLEAR UP
   4 6=P2    #   
  13 6?8L ...OPOF  LDN   4  OFFEND-OFFMES+IDATA-A1-1 
  17 6?N=          LDN   6  0                   ['MOP OFF' INDICATOR 
  10 6#7W          LDXC  0  ISTATUS(3)   
  20 6#MG          BCC      OFT                 [IDENT OPERABLE : SEND MESSAGE   
   7 6*76    OFFI  LDN   4  3
  18 6*LQ          LDCT  6  #400                ['FINISH NOW' INDICATOR  
  17 6B6B    OFT   SMO      IADIN(3)            [ACCESS AMOP/IPBOUT  
   9 6BL2          LDX   1  IPBRING  
  20 6C5L          LDCT  0  #620                [B0 : INOP ; B1 : SYS OVERLOAD   
   8 6CK= ...      LDCT  5  #100 
  16 6D4W          ANDX  0  IPBSW-IPBRING(1)    [B4 : MOP OFF,PM 
  10 6DJG ...      ANDX  5  ISTATUS(3)   
  16 6F46          BNZ   0  SUC                 [SEND NO OUTPUT  
  19 6FHQ          BNZ   5  SUC                 [CLOSED ALREADY : WAS ON TEST
  16 6G3B          CALL  7  SETU                [GET IOUT BLOCK  
  18 6G?8 ...      STOZ     IMOPTY(2)           [DON'T WAKE AFTER OUTPUT 
  10 6GH2          LDX   0  IDENTNO(3)   
  18 6H2L          BNG   6  OFIN                [NO MESSAGE IF FINISHING 
  19 6HG= ...      STO   0  IPBIDE(2)         [IDENTIFIER FOR DATA PORTION   
   9 6JFG          LDN   4  OFFMES(1)
  10 6J_6 ...      LDN   5  IPBIDE+1(2)  
  20 6KDQ          MOVE  4  OFFEND-OFFMES       [MOP OFF MESSAGE : DATA PORTION  
   7 6KGF ...      LDX   1  2
  15 6KJ8 ...      CALL  7  RINGO               [SEND MESSAGE
  10 6KKX ...      LDN   4  IPBIDE+1-A1  
  21 6KML ...      CALL  7  SETU                [SET UP NEW BLOCK FOR STOP INPUT SEG 
   8 6KP* ...      LDCT  0  #400 
  15 6KR4 ...      STO   0  IBSW(2)             [NO DATA SEG 
  18 6KSR ...      STOZ     IMOPTY(2)           [DONT WAKE AFTER OUTPUT  
  15 6KWG ...      LDX   0  4                   [IDENT NUMBER
  20 6KYB    OFIN  ADX   0  SUPSEG(1)           [WORD COUNT + 'STOP INPUT' CODE  
   9 6LD2          STO   0  IPBIDE(2)
   7 6LXL          LDX   1  2
   8 6MC=          CALL  7  RINGO
   8 6MWW          CALL  7  REWA 
  18 6NBG          BRN      SUC                 [IDENT INOP : CLEAR UP   
   8 6NW6          ERN   3  #74  
  15 6P*Q          BZE   3  SUC                 ['SUCCESS'   
   9 6PTB          ERN   3  #64$#74  
  17 6Q*2 ...      BNZ   3  REHU                [NOT 'INPUT CLEARED' 
  21 6QSL          BNG   6  SUC                 [NO 'SUCCESS' EXPECTED IF FINISHING  
   7 6R#=          LDN   5  0
   8 6RRW          BRN      REHU 
  10 6TQB    SUC   MHUNTW   3,AMXOR,ADCB 
  10 6W=2          LDX   1  A1+FPTR(3)   
  17 6WPL          STOZ     BPTR(1)             [CLEAR ADEVS POINTER 
  10 6X9=          STOZ     A1+BPTR(3)   
   7 6XNW          SUICIDE   
   4 6Y8G    #   
  20 6YN6    #     CONCEPTUAL BEING ONLINED : OUTPUT ONLINED MESSAGE AND CLOSE   
   4 6_7Q    #   
   4 6_#Y ...ONLI
  17 6_G6 ...      LDCT  0  #200                [TEST BIT 1 - ATTACH 
  10 6_M# ...      ANDX  0  FOURTHWD(1)  
  17 6_SG ...      BZE   0  SLEEP1                 [J IF NOT ATTACHED
   9 6__N ...      SMO      IADIN(3) 
   8 7272          LDXC  0  IPBSW
  17 72LL          BCS      SETOL               [IPB INOP : CLEAR UP 
  10 736=          LDXC  0  ISTATUS(3)   
  19 73KW          BCS      NOWC                [IDENTIFIER INOP : NO MESSAGE
   7 745G ...      SLL   0  2
  19 74K6 ...      BNG   0  SETOL               [CLOSED ALREADY : WAS ON TEST
  13 754Q          LDN   4  OLEND-OLMESS+IDATA-A1-1  
   7 75JB          LDN   6  0
   8 7642          CALL  7  SETU 
  10 76HL          STO   4  IDENTNO(2)   
   9 773=          STOZ     IMOPTY(2)
   9 77GW          LDN   4  OLMESS(1)
  10 782G          LDN   5  IPBIDE+1(2)  
  10 78G6          MOVE  4  OLEND-OLMESS 
   7 78_Q          LDX   1  2
   8 79FB          CALL  7  RINGO
   8 79_2          CALL  7  REWA 
  18 7=DL          BRN      NOWC                [IDENTIFIER INOPERABLE   
   8 7=Y=          ERN   3  #74  
  14 7?CW          BZE   3  NOWC                [SUCCESS 
   8 7#WQ          BRN      REHU 
  10 7*BB    NOWC  MHUNTW   3,AMXOR,ADCB 
  15 7*W2          CALL  5  SECL                [SEND 'CLOSE'
  10 7B*L    SETOL MHUNTW   3,AMXOR,ADCB 
   6 7B*R ...#UNS  CLSKI 
   4 7B*Y ...(   
  10 7BB7 ...      LDX   7  IDENTNO(3)   
  10 7BBN ...      LDX   1  A1+FPTR(3)   
   8 7BC9 ...      LDCT  0  #200 
  10 7BCQ ...      ANDX  0  FOURTHWD(1)  
  18 7BD? ...      BZE   0  NLAH                [J IF NO LONGER ATTACHED 
   8 7BDS ...      LDX   0  BIT10
  10 7BF* ...      ANDX  0  FOURTHWD(1)  
  20 7BFW ...      BZE   0  SLONL               [J IF ONLINE ACT STILL RUNNING   
  10 7BGC ...      ERS   0  FOURTHWD(1)  
   9 7BGY ...      SMO      IADIN(3) 
  16 7BHF ...      LDX   6  CPPTR               [DEV LIST ADDR   
  21 7BJ2 ...      WHICHCON (GEOERR),NLAH,6,7            [FIND OUT WHICH CONCEPTUAL  
  10 7BJH ...      MHUNTW   3,AMXOR,ADCB 
   8 7BK4 ...      LDCT  0  #10  
  10 7BKK ...      SMO      A1+FPTR(3)   
   9 7BL6 ...      ORS   0  FOURTHWD 
  14 7BLM ...      CALL  5  VRDCB               [FREE DCB
  19 7BM8 ...      ACROSS   ATTACHB,4           [GO TO COMPLETE ATTACHMENT   
  10 7BMP ...NLAH  MHUNTW   3,AMXOR,ADCB 
   8 7BN= ...      SMO      FX1  
   9 7BNR ...      LDX   0  NLAHMK   
  10 7BP# ...      SMO      A1+FPTR(3)   
  20 7BPT ...      ANDS  0  FOURTHWD            [UNSET ATTACHED MARKERS IN ADEVS 
  15 7BQB ...      LDX   4  IADIN(3)            [AMOP ADDRESS
  14 7BQX ...      CALL  5  VRDCB               [FREE DCB
  17 7BRD ...      SETIDENT 4,7                 [REINITIALISE IDENT  
   7 7BR_ ...      SUICIDE   
   5 7BSG ...SLONL   
   4 7BSW ...)   
   8 7BT=          LDCT  0  #10  
  10 7C#W          SMO      A1+FPTR(3)   
  20 7CSG          ORS   0  FOURTHWD            [SET 'ONLINE' BIT IN ADEVS ENTRY 
   8 7D#6          BRN      SUC  
   4 7D#L ...#   
  21 7D*6 ...#     REPORT TERMINAL/LINE FAILURE TO TERMINAL OPERATOR IF RE-OPERABLE  
   4 7D*L ...#   
   6 7DB6 ...SENDFAILMES 
   7 7DBL ...      SRC   4  6
  19 7DC6 ...      BNG   4  SKOUT               [IDENTIFIER STILL INOPERABLE 
  14 7DCL ...      LDN   4  TLFEND-TLFAILMESS+IDATA-A1-1 
   7 7DD6 ...      LDN   6  0
   8 7DDL ...      CALL  7  SETU 
  10 7DF6 ...      STO   4  IDENTNO(2)   
   9 7DFL ...      STOZ     IMOPTY(2)
  17 7DG6 ...      LDN   4  TLFAILMESS(1)       [MOVE IN FAIL MESSAGE
  10 7DGL ...      LDN   5  IPBIDE+1(2)  
  11 7DH6 ...      MOVE  4  TLFEND-TLFAILMESS
   7 7DHL ...      LDX   1  2
   8 7DJ6 ...      SMO      FX2  
  17 7DJL ...      LDN   4  CPRW1               [INSERT TIME OF FAIL 
  11 7DK6 ...      ADN   5  TIME-TLFAILMESS  
   7 7DKL ...      MOVE  4  2
  15 7DL6 ...      CALL  7  RINGO               [Q MESSAGE   
  15 7DLL ...      CALL  7  REWA                [AWAIT REPLY 
  18 7DM6 ...      BRN      SKOUT               [IDENTIFIER INOPERABLE   
   7 7DML ...      LDX   4  3
  10 7DN6 ...      MHUNTW   3,AMXOR,ADCB 
   8 7DNL ...      ERN   4  #74  
  21 7DP6 ...      BNZ   4  SKOUT               [NOT OUTPUT SENT : DON'T UNSET FLAG  
  20 7DPL ...      LDCT  0  4                   [OK : UNSET 'SEND FAIL MESS TO   
  16 7DQ6 ...      ERS   0  ISTATUS(3)          [TERMINAL' FLAG  
   8 7DQL ...      BRN      SKOUT
   4 7DRQ    #   
  17 7F67 ...#     INOPERABLE : 7020 INOPERABLE IF LINE/TERMINAL FAIL
  19 7FDJ ...#                  7023 INOPERABLE IF UNAVAILABLE OR DEVICE FAIL
   4 7FR2    #   
  19 7G=L    REPFL STOZ     AWORK2(2)           [NO OUTPUT NOW IN PROGRESS   
  10 7GQ=          MHUNTW   3,AMXOR,ADCB 
  10 7H9W          LDX   5  ISTATUS(3)   
   7 7HPG          LDN   7  0
  20 7J96          BPZ   5  NOTID               [CONSOLE OPERABLE : REPEAT OUTPUT
  10 7J#_ ...      LDX   2  A1+FPTR(3)   
  20 7JB= ...      BBUS     16,FOURTHWD(2),NOTFR     [7023 FREED IF BIT 16 IS SET
   8 7JCH ...      SMO      FX1  
   8 7JDS ...      LDX   0  SM16 
  15 7JG5 ...      ANDS  0  FOURTHWD(2)      [UNSET BIT 16   
   8 7JHB ...      LDX   2  FX2  
   8 7JJM ...      BRN      NINT 
   5 7JKY ...NOTFR   
  17 7JNQ          LDX   4  IDENTNO(3)          [IDENTIFIER NUMBER   
   7 7K8B          SLL   5  5
  18 7KN2          LDX   2  IADIN(3)            [ADDRESS OF AMOP/IPBIN   
   8 7L7L          LGEOG    ,6   
  18 7LM=          BPZ   5  NOLIF               [J IF NOT LINE FAILURE   
  21 7LX4 ...      IDFTPSCN XBI,SKIN,CPPTR(2),4 [GET 7020 LINE NUMBER ; J IF MOPOFF  
  15 7MLG          LDX   4  ACOMMUNE3(2)        [LINE NUMBER 
   7 7N66    NOLIF OUTBLOCN 4
   8 7NKQ          GEOPACK  6,6,6
  10 7P5B          OUTPACKC 6,1,GEOPER   
   8 7PK2          OUTNUM   4,0  
   8 7Q4L          BPZ   5  RELIF
  16 7QJ=          INFORM   1,JINOP,1           [LINE INOPERABLE 
  10 7R3W          MHUNTW   3,AMXOR,ADCB 
   7 7R5? ...      LDCT  0  4
  10 7R6N ...      ANDX  0  ISTATUS(3)   
  21 7R85 ...      BNZ   0  SETFL               ['SEND FAIL MESS TO TERMINAL' FLAG   
  20 7R9G ...                                   [SET, IE A PREVIOUS TERMINAL/LINE
  21 7R=X ...                                   [FAIL NOT YET REPORTED TO TERMINAL.  
   7 7R## ...      LDCT  0  4
  20 7R*P ...      ERS   0  ISTATUS(3)          [FLAG NOT SET : SET IT AND RECORD
  20 7RC6 ...      LDN   4  FTIME               [TIME OF FAIL IN THIS ACTIVITY'S 
  19 7RDH ...      LDN   5  CPRW1(2)            [UNUSED WORDS:- CPRW1 & CPRW2
   7 7RFY ...      MOVE  4  2
  21 7RHG    SETFL CALL  5  OLTE                [TEST WHETHER ONLINE-TEST REQUESTED  
  19 7S36    NOREQ DOWN     CONSROOT,2          [RE-ROUTE OUTPUT IF REQUIRED 
  21 7SGQ          LDN   7  0                   [CHECK EVERYTHING AGAIN BEFORE COOR1 
  17 7T2B          BRN      NINT                [LOOK FOR INPUT FIRST
  18 7TG2    RELIF INFORM   1,HINOP,1           [IDENTIFIER INOPERABLE   
  19 7T_L          BRN      NINT                [LOOK FOR UNPROCESSED INPUT  
  17 7W5G ...XBI   GEOERR   1,BREAKIN!          [CANT HAVE BREAK-IN  
  11 7WF=          MENDAREA 50,K100IPBCONS   
   4 7WYW    #END
   8 ____ ...37377276000200000000
