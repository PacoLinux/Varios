   9 22FL    #OPT           K0OPERA=0
  15 22_=    #LIS           K0OPERA > K0ALLGEO > K0GREATGEO  
  17 23DW    #SEG           OPERA               [COMM - STIG TOWNSEND
   4 23YG    [   
   4 24D6    [   
   8 24XQ                8HOPERA 
  18 25CB       SEGENTRY K1OPERA,OPERAK1        [SHOW TO A MONITOR SCREEN
  19 25X2       SEGENTRY K2OPERA,OPERAK2        [SHOW TO INTERACTIVE SCREEN  
  20 26BL       SEGENTRY K3OPERA,OPERAK3        [CLEAR FCOUTBUF OF BSTS MESSAGES 
   4 26W=    [   
   4 27*W    [   
  17 27TG    [ AWORK4 IS USED TO HOLD THE NUMBER OF THE SCREEN FOR   
  18 28*6    [    WHICH THE SHOW IS TO BE DONE. IN THE CASE OF A MONITOR 
  16 28SQ    [    SHOW, THIS IS TAKEN FROM ACOMMUNE4 WHEN ENTERED
   4 29#B    [   
   5 29S2    #PAGE   
   4 2=?L    [   
   4 2=R=    [   
  19 2?=W    #DEF  WIDTH = 40                   [WIDTH OF OPER SCREEN (CHARS)
  12 2?QG                                       [
  19 2#=6    #DEF  PAGESIZE = 2                 [SIZE OF PAGE IN B/S BLOCKS  
  12 2#PQ                                       [
  20 2*9B    #DEF  SIZEDATE = 7                 [LENGTH OF DATE STRING (CHARS)   
  12 2*P2                                       [
  18 2B8L    #DEF  SIZETIME = 5                 [LENGTH OF HH.MM (CHARS) 
  12 2BN=                                       [
  17 2C7W    #DEF  WRITEMODE = 3                [EXTENDED WRITE MODE 
  12 2CMG                                       [
  21 2D76    #DEF  UNLOCK = #200                [ADDITIONAL WRITE MODE - UNLOCK KEYS 
  12 2DLQ                                       [
  12 2F6B    SHOWINGMSG     40H*** SHOWING ***   
  12 2FL2                                       [
  18 2G5L          FIXTRA   K90OPERA            [FOR MACRO 'KEEPPAGES'   
  21 2GK=    MAXBLKLET      +20 * PAGESIZE      [HIGHEST BLOCKLET NO IN RECALL FILE  
  12 2H4W                                       [
  15 2HJG    NEWLINES       4H_*_*              [2 NEWLINES  
  12 2J46                                       [
  18 2JHQ    TYPESHOW       +CONBUFF+CSHOW@L 12 [CONBUFF/CSHOW BLOCKTYPE 
  12 2K3B                                       [
  18 2KH2    TYPECOUT       +CONBUFF+COUT @L 12 [CONBUFF/COUT BLOCKTYPE  
   5 2L2L    #PAGE   
   4 2LG=    [   
   4 2L_W    [   
   4 2MFG    [   
  21 2M_6    [*********************************************************************  
   4 2NDQ    [   
   6 2NYB    SHUNTFRRB   
   4 2PD2    [   
  21 2PXL    [*********************************************************************  
   4 2QC=    [   
  13 2QWW    [ SUBSIDIARY HUNT ROUTINE FOR 'LOCKC'   
   4 2RBG    [   
   9 2RW6       MHUNTW   2,FILE,FRRB 
   7 2S*Q       EXIT  1  0   
   5 2STB    #PAGE   
   4 2T*2    [   
   4 2TSL    [   
   4 2W#=    [   
  21 2WRW    [*********************************************************************  
   4 2X?G    [   
   6 2XR6    SHOWBLOCK   
   4 2Y=Q    [   
  21 2YQB    [*********************************************************************  
   4 2_=2    [   
  18 2_PL    [ POINTS X3 AT THE FIRST CONBUFF/CSHOW ON THE CONSOLE OUTPUT
  19 329=    [ CHAIN WHOSE DISPLAY NUMBER IS THE SAME AS THAT HELD IN AWORK4 
   4 32NW    [   
   6 338G    [ X4 LINK   
   4 33N6    [   
   9 347Q    [ ON EXIT,  X0 CORRUPT  
   9 34MB    [           X1,2 = FX'S 
   9 3572    [           X3 -> BLOCK 
   4 35LL    [   
   7 366=       LDX   1  FX1 
   7 36KW       LDX   2  FX2 
   8 375G       LDN   3  BCONSOUT
  20 37K6       WHILE    TRUE                [LOOP TILL FIND /CSHOW FOR SCREEN   
   9 384Q          LDX   3  FPTR(3)  
  14 38JB       AND      EITHER,+ATYPE(3),U,TYPESHOW(1)  
  12 3942       OR       +AWORK4(2),U,CSVID(3)   
   5 39HL       DO   
  16 3=3=       REPEAT                       [X3 -> SHOW BLOCK   
   7 3=GW       EXIT  4  0   
   4 3?2G    [   
   5 3?G6    #PAGE   
   4 3?_Q    [   
   4 3#FB    [   
  21 3#_2    [*********************************************************************  
   4 3*DL    [   
   6 3*Y=    SREADPAGE   
   4 3BCW    [   
  21 3BXG    [*********************************************************************  
   4 3CC6    [   
  20 3CWQ    [ READS THE PAGE SPECIFIED IN THE FIRST SHOW BLOCK FOR THE SCREEN WE
   8 3DBB    [ ARE CONSIDERING.  
   4 3DW2    [   
   6 3F*L    [ LINK X6   
  14 3FT=    [ ON ENTRY, AWORK4 CONTAINS SCREEN NUMBER   
  18 3G#W    [           /CSHOW BLOCK FOR THIS SCREEN IS ON CONSOLE CHAIN
   4 3GSG    [   
  15 3H#6    [ ON EXIT,  PAGE IS SET UP IN A FILE/FRRB BLOCK 
   9 3HRQ    [           X1,2 = FX'S 
   9 3J?B    [           X3 -> BLOCK 
   9 3JR2    [           X5 CORRUPT  
   4 3K=L    [   
   7 3KQ=       SBX   6  FX1 
  15 3L9W       CALL  4  SHOWBLOCK           [X3 -> /CSHOW   
   9 3LPG       LDX   2  CONSCNTRL   
   8 3M96       ADN   2  COVID   
  16 3MNQ       ADX   2  CSVID(3)            [X2 -> SCREEN CELL  
  20 3N8B       LDX   4  CSPAG(3)            [SET X4 TO START BLOCKLET OF PAGE   
  17 3NN2       IF       4,NG                [IF START OF 'AUTO' THEN
   5 3P7L       THEN 
  15 3PM=          BS       2,COAUTO            [SET AUTO BIT
  15 3Q6W          LDN   4  1                   [READ PAGE @1
  18 3QLG          STO   4  CSPAG(3)            [PUT REAL PAGE NUMBER IN 
  19 3R66       ELSF     +CSVID(3),NZ        [ELSF FOR MONITOR SCREEN THEN   
   5 3RKQ       THEN 
  16 3S5B          BC       2,COAUTO            [CLEAR AUTO BIT  
  12 3SK2       FI                           [FI 
   7 3T4L       LDX   0  4   
   9 3TJ=       SLL   0  PAGESIZE/2  
   9 3W3W       SMO      CONSCNTRL   
   8 3WHG       LDX   4  COBLK   
   7 3X36       SBX   4  0   
   7 3XGQ       IFR      4,NG
   7 3Y2B       OR       4,ZE
   5 3YG2       THEN 
   8 3Y_L          LDX   1  FX1  
  10 3_F=          ADX   4  MAXBLKLET(1) 
  16 3_YW       FI                           [X4 = START BLOCKLET
   9 42DG       LDN   5  PAGESIZE*128
  16 42Y6       READED   4,1,5               [READ REQUESTED PAGE
   9 43CQ       MHUNTW   3,FILE,FRRB 
   7 43XB       ADX   6  FX1 
   7 44C2       EXIT  6  0   
   5 44WL    #PAGE   
   4 45B=    [   
   4 45TW    [   
  19 46*G    [***************************************************************
   4 46T6    [   
   5 47#Q    SPREPERI
   4 47SB    [   
  19 48#2    [***************************************************************
   4 48RL    [   
  17 49?=    [ EXTRACTS BSTS MESSAGES FROM FCOUTBUF, TAKING COUNT AND
  14 49QW    [ ADDRESS FROM THE OPER DEVICE LIST ENTRY   
  16 4==G    [ THE MESSAGE IS INCORPORATED AS A STRING OF /COUT'S
  15 4=Q6    [ AT THE !! FRONT !! OF THE CONSOLE OUTPUT CHAIN
  12 4?9Q    [ THE MESSAGE MUST BE OF THE FORMAT:
   8 4?PB    [    <LINE>* <NULL>*
  17 4#92    [    WHERE <LINE> ::=  <NULL>* <DATA CHARS>* <NEWLINE>  
  13 4#NL    [          <NULL> ::= UPARROW SPACE ^_ ^
  14 4*8=    [          <NEWLINE> ::= UPARROW STAR ^_*^  
  17 4*MW    [          A FOLLOWING ASTERISK MEANS THE ELEMENT MAY BE
  17 4B7G    [              REPEATED ANY NUMBER OF TIMES (INCL ZERO) 
   4 4BM6    [   
   6 4C6Q    [ LINK X7   
   4 4CLB    [   
  10 4D62    [ ON EXIT:  COBS HOLDS ZERO 
   9 4DKL    [           X1,2 = FX'S 
   4 4F5=    [   
   7 4FJW       SBX   7  FX1 
   7 4G4G       WHILE    TRUE
   8 4GJ6          LDX   1  FX1  
   8 4H3Q          LDX   2  FX2  
  10 4HHB          WHILE    BUSY,CPPTR(2)
   5 4J32          DO
   9 4JGL             COOR3    #50   
   6 4K2=          REPEAT
   9 4KFW          LDX   3  CONSCNTRL
   9 4K_G       AND      +COBS(3),NZ 
   5 4LF6       DO   
   9 4LYQ          LDX   3  CPPTR(2) 
  10 4MDB          DCA      3,LDX,4,COUNT
  11 4MY2          ADN   4  (CPDATA-A1)*4 + 3
  17 4NCL          SRL   4  2                   [SET UP BLOCK TO ... 
  17 4NX=          SETUPCOR 4,2,ADATA,CREADL    [HOLD COPY OF MESSAGE
   4 4PBW       [
  20 4PWG          WHILE    TRUE                [ENSURE LONGER BSTS MESSAGE ...  
  20 4QB6             DCA      3,LDX,5,COUNT    [NOT WRITTEN DURING 'SETUPCOR'   
  12 4QTQ             ADN   5  (CPDATA-A1)*4 + 3 
   8 4R*B             SRL   5  2 
   8 4RT2          AND      4,L,5
   5 4S#L          DO
   8 4SS=             LDX   4  5 
  12 4T?W             ALTLEN   2,4,ADATA,CREADL  
  11 4TRG             MHUNTW   2,ADATA,CREADL
   6 4W?6          REPEAT
   4 4WQQ       [
  17 4X=B          LDN   4  FCOUTBUF+1          [COPY OUT THE MESSAGE
   9 4XQ2          LDN   5  CPDATA(2)
  17 4Y9L          LDN   6  FCOUTBUF+1          [SET X6 TO CHAR COUNT
  10 4YP=          DCA      3,SBX,6,CTSA 
   7 4_8W          SLL   6  2
  19 4_NG          DCA      3,ADX,6,COUNT       [X6 = CHAR COUNT LESS ANY ...
  18 5286          SMO      6                   [... PRECEDING ^CANCEL^  
   7 52MQ          MVCH  4  0
   4 537B       [
  16 53M2                                       [INITIALISE LOOP:
  21 546L                                       [X6 COUNT OF CHARS TO CHECK FOR N/L (
  21 54L=          LDX   3  CONSCNTRL           [COBS NONZERO MEANS BSTS WROTE ...   
  21 555W          STOZ     COBS(3)             [   AGAIN AFTER THIS MSG WAS COPIED  
  20 55KG          BS       3,COVERWRITE        [REMEMBER SCREEN NEEDS RESETTING 
  16 55NL ...      IF       BSC,3,COPAGEWAIT    [DONT PAGEWAIT   
   6 55RQ ...      THEN  
  10 55WW ...         STOZ     CODAT(3)  
   5 5622 ...      FI
  20 5656          LDN   1  CPDATA              [X1 DISPLACEMENT OF START OF ... 
  20 56JQ                                       [   CURRENT LINE WITHIN MESSAGE  
   4 574B       [
  15 57J2          WHILE    TRUE                [WHILE ...   
  11 583L             MHUNTW   2,ADATA,CREADL
   8 58H=             ADX   1  2 
  20 592W             WHILE    6,NZ                [   (IGNORING NULLS (^_ ^) )  
  19 59GG                                          [FIND ^_*^ OR END OF MSG  
  10 5=26                LDCH  0  0(1)   
   9 5=FQ                SBN   0  #76
   9 5=_B             AND      0,ZE  
   9 5?F2                BCHX  1  /  
  10 5?YL                LDCH  0  0(1)   
   9 5#D=                SBN   0  #32
   9 5#XW                SBN   6  2  
   9 5*CG                BCHX  1  /  
   9 5*X6             AND      0,NZ  
   6 5BBQ             DO 
   7 5BWB             REPEAT 
  18 5CB2                                          [AT END OF NULLS LOOP:
  20 5CTL                                          [X6 = 0   => END OF MESSAGE   
  20 5D*=                                          [X6 NZERO => END OF LINE, WHEN
  20 5DSW                                          [   X0 = 0   => NEWLINE FOUND 
  21 5F#G                                          [               X1 -> NEXT LINE   
  21 5FS6                                          [   X0 NZERO => DATA CHAR FOUND   
  21 5G?Q                                          [               X1 -> DATA CHAR   
  20 5GRB          AND      6,NZ                [... NOT RUN OFF END OF MSG DO   
   5 5H?2          DO
  19 5HQL             LDX   3  1                   [X3 -> START OF THIS LINE 
  18 5J==             IF       0,NZ                [IF LINE NOT NULL THEN
   7 5JPW             THEN   
  10 5K9G                WHILE    TRUE   
  19 5KP6                DO                           [FIND NEXT DELTA CHAR  
  10 5L8Q                   LDCH  0  0(1)
  10 5LNB                   SBN   6  1   
  10 5M82                   BCHX  1  /   
  10 5MML                   SBN   0  #76 
  18 5N7=                REPEAT   UNTIL,0,ZE          [FOUND _ OF DELTA  
  10 5NLW                LDCH  0  0(1)   
   9 5P6G                SBN   6  1  
   9 5PL6                BCHX  1  /  
  18 5Q5Q                SBN   0  #32                 [GEOERR IF NOT _* -
  21 5QKB                BNZ   0  (GEOERR)            [CAN'T COPE WITH DELTA IN LINE 
  17 5R52             FI                           [FI LINE NOT NULL 
   5 5RJL          [ 
  10 5S4=          [ HAVE FOUND NEWLINE  
   9 5SHW          [ X1 -> BEYOND IT 
  11 5T3G          [ X2 -> BSTS MESSAGE BLOCK
  14 5TH6          [ X3 -> START OF THIS LINE WITHIN IT  
   5 5W2Q          [ 
  21 5YDW             SBX   1  2                   [X1 -> START NEXT LINE (W.R.T. FPT
  21 5_D6             SBX   3  2                   [X3 -> START THIS LINE (W.R.T. FPT
   8 5_GP ...         SLC   3  2 
   8 5_K# ...         LDX   4  1 
   8 5_MX ...         SLC   4  2 
  20 5_QG ...         SBN   4  2(3)                [X4 = CHARS IN LINE EXCL ^_*^ 
   8 5_T5 ...         SRC   3  2 
   9 5_XQ             LDX   2  FX2   
  10 62CB             STO   1  AWORK1(2) 
  10 62X2             STO   4  AWORK2(2) 
  20 63BL             IF       4,NZ                [IF THIS LINE NOT NULL THEN   
   7 63W=             THEN   
  13 64*W                ADN   4  (ATEXT-AREC)*4 + 3 
   9 64TG                SRL   4  2  
  21 65*6                SETUPCOR 4,1,CONBUFF,COUT    [X1 -> CONBUFF-COUT FOR ONE LIN
  17 65SQ                MHUNTW   2,ADATA,CREADL      [X2 -> CREADL  
  17 66#B                STO   4  AREC(1)             [SET WORD COUNT
   9 66S2                LDX   4  2  
  20 67?L                ADX   4  3                   [X4 -> START OF THIS LINE  
  11 67R=                LDN   2  AREC-1(1)  
  10 68=W                ADX   2  AREC(1)
  10 68QG                LDX   0  ACES   
  21 69=6                STO   0  0(2)                [SPACEFILL LAST WORD IN /COUT  
  16 69PQ                LDX   2  FX2                 [X2 = FX2  
  21 6=9B                LDN   5  ATEXT(1)            [X5 -> START OF /COUT TEXT AREA
  11 6=P2                LDX   3  AWORK2(2)  
  18 6?8L                MVCH  4  0(3)                [MOVE THIS LINE IN 
  17 6?N=             FI                           [FI LINE NOT NULL 
  10 6#7W             LDX   1  AWORK1(2) 
  19 6#MG          REPEAT                       [REPEAT UNTIL END OF MESSAGE 
   9 6*76          MFREE ADATA,CREADL
   4 6*LQ       [
  16 6B6B       [ LINES OF MSG ARE ON ACT CHAIN IN REVERSE ORDER 
  20 6BL2       [ PUTTING THEM AT FRONT OF CONSOLE CHAIN RESTORES ORIGINAL ORDER 
   4 6C5L       [
  21 6CK=          MHUNTW   3,CONBUFF,COUT      [FROM 1ST /COUT (LAST LINE OF MSG) DO
   8 6D4W          WHILE    TRUE 
   5 6DJG          DO
  10 6F46             LDX   4  FPTR(3)   
  18 6FHQ             CHAIND   3,BCONSOUT          [CHAIN /COUTS AT FRONT
   8 6G3B             LDX   3  4 
  14 6GH2          REPEAT   UNTIL,+ATYPE(3),U,TYPECOUT(1)
   4 6H2L       [
   6 6HG=       REPEAT   
   4 6H_W    [   
   7 6JFG       ADX   7  FX1 
   7 6J_6       EXIT  7  0   
   5 6KDQ    #PAGE   
   4 6KYB    [   
   4 6LD2    [   
  11 6LXL    [                    WW   WW   W
  11 6MC=    [                    WW  WW   WW
  11 6MWW    [                    WW WW   WWW
  11 6NBG    [                    WWWW     WW
  11 6NW6    [                    WWWW     WW
  11 6P*Q    [                    WW WW    WW
  11 6PTB    [                    WW  WW   WW
  11 6Q*2    [                    WW   WW  WW
   4 6QSL    [   
   4 6R#=    [   
   5 6RRW    OPERAK1 
   4 6S?G    [   
  20 6SR6    [ SHOWS A PAGE TO A MONITOR SCREEN (OR CLEARS SCREEN IF THE ACTIVE  
   8 6T=Q    [ PAGE IS SPECIFIED)
   4 6TQB    [   
  14 6W=2    [ ON ENTRY, ACOMMUNE4 CONTAINS SCREEN NUMBER
  18 6WPL    [           /CSHOW BLOCK FOR THIS SCREEN IS ON CONSOLE CHAIN
   4 6X9=    [   
   9 6XNW       LDX   0  ACOMMUNE4(2)
  16 6Y8G       STO   0  AWORK4(2)           [SET SCREEN NUMBER  
   9 6YN6       CALL  4  SHOWBLOCK   
  16 6_7Q       IF       +CSPAG(3),ZE        [IF ^SHOW OFF^ THEN 
   5 6_MB       THEN 
   9 7272          CALL  7  SPREPERI 
  20 72LL          LDX   0  NEWLINES(1)         [SET UP BUFFER TO CLEAR SCREEN   
   9 736=          STO   0  FCOUTBUF 
   9 73KW          LDN   4  FCOUTBUF 
  10 745G          LDN   5  FCOUTBUF+1   
  16 74K6          MOVE  4  HEIGHTPAGE+5/2      [BUFFER IS SET UP
  17 754Q          LDX   3  CPPTR(2)            [SET UP CONTROL AREA 
   9 75JB          LDN   0  WRITEMODE
  10 7642          DCA      3,DLA,0,MODE 
   9 76HL          LDN   0  FCOUTBUF 
  10 773=          DCA      3,STO,0,CTSA 
   9 77GW          LDX   0  AWORK4(2)
   7 782G          SLL   0  9
  18 78G6          DCA      3,STO,0,EXTRA       [INCLUDING SCREEN NUMBER 
  11 78_Q          LDN   0  HEIGHTPAGE+5/2*4 
  10 79FB          DCA      3,DLA,0,COUNT
  15 79_2          GPERI    3,2                 [ISSUE PERI  
   6 7=DL          COOR1 
   9 7=Y=          LDN   3  CPRW1(2) 
  16 7?CW          IF       MBAC,3,CONSFAIL,CONSINOP [IF OK THEN 
   6 7?XG          THEN  
  10 7#C6             LDX   3  AWORK4(2) 
  10 7#WQ             SMO      CONSCNTRL 
   9 7*BB             ADN   3  COVID 
  20 7*W2             MBC      3,COSHOWING,COAUTO  [CLEAR 'SCREEN SHOWING' BITS  
  11 7B*L             FC       3,COCURRENT   
  13 7BT=          FI                           [FI  
  17 7C#W       ELSE                         [ELSE (NOT ACTIVE PAGE) 
   9 7CSG          CALL  6  SREADPAGE
  19 7D#6          DOWN     OPER,3              [PUT RESPONSE LINE IN BLOCK  
   9 7DRQ          CALL  4  SHOWBLOCK
   9 7F?B          LDX   3  CSVID(3) 
   9 7FR2          ADX   3  CONSCNTRL
   8 7G=L          ADN   3  COVID
   9 7GQ=          CALL  1  SHUNTFRRB
  16 7H9W          IF       BC,3,COAUTO         [IF NOT AUTO THEN
   6 7HPG          THEN  
  17 7J96             LDX   0  ACES                [BLANK ^(AUTO)^   
  14 7JNQ             STO   0  (ACHCT+1)+(WIDTH/4)-2(2)  
  14 7K8B             STO   0  (ACHCT+1)+(WIDTH/4)-1(2)  
  13 7KN2          FI                           [FI  
  17 7L7L          LOCKC    2,SHUNTFRRB         [LOCK BUFFER BLOCK   
   9 7LM=          CALL  7  SPREPERI 
  17 7M6W          LDX   3  CPPTR(2)            [SET UP CONTROL AREA 
   9 7MLG          LDN   0  WRITEMODE
  10 7N66          DCA      3,DLA,0,MODE 
   9 7NKQ          CALL  1  SHUNTFRRB
   9 7P5B          LDN   0  ACLOG(2) 
  10 7PK2          DCA      3,STO,0,CTSA 
   9 7Q4L          LDX   0  ACHCT(2) 
  10 7QJ=          DCA      3,DLA,0,COUNT
   8 7R3W          LDX   2  FX2  
   9 7RHG          LDX   0  AWORK4(2)
   7 7S36          SLL   0  9
  10 7SGQ          DCA      3,STO,0,EXTRA
  15 7T2B          GPERI    3,2                 [ISSUE PERI  
   6 7TG2          COOR1 
  17 7T_L          MFREE    FILE,FRRB           [FREE BUFFER BLOCK   
   9 7WF=          LDN   3  CPRW1(2) 
  18 7WYW          IF       MBAC,3,CONSFAIL,CONSINOP [IF SHOWED OK THEN  
   6 7XDG          THEN  
  10 7XY6             LDX   3  AWORK4(2) 
  10 7YCQ             SMO      CONSCNTRL 
  19 7YXB             ADN   3  COVID               [SET 'SCREEN SHOWING' BIT 
  11 7_C2             BS       3,COSHOWING   
   8 7_WL             STO   3  5 
  10 82B=             CALL  4  SHOWBLOCK 
  19 82TW             LDX   1  CSPAG(3)            [X1 = CURRENT PAGE NUMBER 
   8 83*G             LDX   3  5 
  19 83T6             STF      3,COCURRENT,1       [SET IT IN THE INFO CELL  
  13 84#Q          FI                           [FI  
  17 84SB       FI                           [FI ACTIVE PAGE / OTHER 
   5 85#2       UP   
   5 85RL    #PAGE   
   4 86?=    [   
   4 86QW    [   
   4 87=G    [   
  12 87Q6    [                    WW   WW   WWWWW
  13 889Q    [                    WW  WW   WW   WW   
  13 88PB    [                    WW WW         WW   
  13 8992    [                    WWWW          WW   
  12 89NL    [                    WWWW        WW 
  12 8=8=    [                    WW WW     WW   
  11 8=MW    [                    WW  WW   WW
  13 8?7G    [                    WW   WW  WWWWWWW   
   4 8?M6    [   
   4 8#6Q    [   
   5 8#LB    OPERAK2 
   4 8*62    [   
   4 8*KL    [   
  20 8B5=    [ SHOWS A PAGE ON THE INTERACTIVE SCREEN, SETTING APPROPRIATE BITS  
   8 8BJW    [ AND TIMER WORDS.  
   4 8C4G    [   
  18 8CJ6    [ ON ENTRY, /CSHOW BLOCK FOR THIS SCREEN IS ON CONSOLE CHAIN
   4 8D3Q    [   
  18 8DHB       STOZ     AWORK4(2)           [WE KNOW IT'S SCREEN ZERO   
   9 8F32       CALL  6  SREADPAGE   
  20 8FGL       LDN   5  SHOWINGMSG(1)       [X56 READY TO INSERT *** SHOWING ***
   9 8G2=       LDN   6  ACHCT+1(3)  
   7 8GFW       LDX   2  3   
  20 8G_G       ADX   2  ACLOG(3)            [ACLOG WILL BE O-WRITTEN BY ***'S   
  21 8HF6       SBN   2  SIZETIME+SIZEDATE+4/4 [X2 -> FIELD FOR CURRENT TIME & DATE  
  17 8HYQ       MOVE  5  WIDTH/4             [INSERT *** SHOWING *** 
  19 8JDB       LDN   1  FTIME               [INSERT CURRENT DATE AND TIME   
   8 8JY2       MVCH  1  SIZETIME
   7 8KCL       LDN   1  ACES
   7 8KX=       MVCH  1  4   
   8 8LBW       LDN   1  FDATE   
  17 8LWG       MVCH  1  SIZEDATE            [TIME AND DATE INSERTED 
   9 8MB6       CALL  1  SHUNTFRRB   
   9 8MTQ       LOCKC    2,SHUNTFRRB 
   5 8N*B       DO   
   9 8NT2          CALL  7  SPREPERI 
  17 8P#L          LDX   3  CPPTR(2)            [SET UP CONTROL AREA 
   9 8PS=          CALL  1  SHUNTFRRB
  11 8Q?W          LDN   0  WRITEMODE+UNLOCK 
  10 8QRG          DCA      3,DLA,0,MODE 
   9 8R?6          LDN   0  ACLOG(2) 
  10 8RQQ          DCA      3,STO,0,CTSA 
   9 8S=B          LDX   0  ACHCT(2) 
  10 8SQ2          DCA      3,DLA,0,COUNT
   7 8T9L          LDN   0  0
  10 8TP=          DCA      3,STO,0,EXTRA
   8 8W8W          LDX   2  FX2  
  15 8WNG          GPERI    3,2                 [ISSUE PERI  
   6 8X86          COOR1 
   9 8XMQ          LDN   3  CPRW1(2) 
  14 8Y7B       REPEAT   UNTIL,MBAC,3,CONSFAIL,CONSINOP  
   9 8YM2       CALL  4  SHOWBLOCK   
  18 8_6L       LDX   1  CSPAG(3)            [X1 = CURRENT PAGE NUMBER   
   9 8_L=       LDX   3  CONSCNTRL   
  16 925W       BC       3,COPAGEWAIT        [CLEAR 'PAGEWAITING'
   8 92KG       LDX   0  EDATE   
  18 9356       STO   0  CODAT(3)            [REMEMBER WHEN SHOW STARTED 
   8 93JQ       LDX   0  CLEANCT 
   9 944B       STO   0  CODAT+1(3)  
   8 94J2       ADN   3  COVID   
  15 953L       BS       3,COSHOWING         [SET 'SHOWING'  
  18 95H=       STF      3,COCURRENT,1       [SET PAGE NO. IN INFO CELL  
   9 96GG       MFREE    FILE,FRRB   
   5 9726       UP   
   5 97FQ    #PAGE   
   4 97_B    [   
   4 98F2    [   
  12 98YL    [                    WW   WW   WWWWW
  13 99D=    [                    WW  WW   WW   WW   
  13 99XW    [                    WW WW         WW   
  12 9=CG    [                    WWWW       WWWW
  13 9=X6    [                    WWWW          WW   
  13 9?BQ    [                    WW WW         WW   
  13 9?WB    [                    WW  WW   WW   WW   
  12 9#B2    [                    WW   WW   WWWWW
   4 9#TL    [   
   4 9**=    [   
   5 9*SW    OPERAK3 
   4 9B#G    [   
  12 9BS6    [ CLEAR FCOUTBUF OF BSTS MESSAGES   
   4 9C?Q    [   
   8 9CRB       CALL  7  SPREPERI
   5 9D?2       UP   
   4 9DQL    #END
   6 ____ ...064631200003
