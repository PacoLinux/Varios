  14 22FL ...      SEG      ADDICT,864,EDWARD MOON,BMAP  
   4 22_=    [   
  11 23DW          SEGENTRY K1ADDICT,START   
  11 23JR ...      SEGENTRY K2ADDICT,Z2ADDICT
   7 23NN ...#OPT  BMTRACE=0 
   5 23R? ...SERLINK 
   9 23TW ...      LINK     ADDICT,2 
   8 23YG    NDICT          +10  
  10 24XQ    NDLOC          12HDICTIONARY
   8 26BL                   4HB1 
   8 26N2 ...NSER           +16  
   9 26_B ...               12HMASTER
   9 27=Q ...               12HSYSTEM
   9 27J6 ...               1,1,4HC1 
   9 27TG                   12HSERIAL
   9 28*6                   1,1,4HB1 
   4 28*D ...[   
  15 28*Q ...[**********************************************:
  14 28B4 ...[  ENTRY FOR 'NEWDICTENT' RESTORE TIME MACRO
  10 28BB ...[    PUTS NEW PASSWORD HERE.
   4 28BN ...[   
   9 28C4 ...      SEGENTRY K50ADDICT
   5 28F2 ...PASSWORD
   8 28GY ...               12H  
   5 28JW ...TYPEFWB 
   7 28LS ...#HAL +FILE+FWB,0
   5 28NQ ...NINFUSER
  13 28QN ...               24H :  PLEASE INFORM USER
   7 28SQ    RTIME 8HREALTIME
   9 29#B    SPACEMT        8HSPACEMT
   7 29S2    WORDS +GWORDS   
   7 2=?L    MER   +GERASE   
   9 2=CH ...NGCLASS        +GCLASS  
   8 2=HD ...NGTOR          +GTOR
   9 2=L3 ...NGWORDS        +GWORDS  
   9 2=NL ...NFUSMASK       #3777777 
   5 2=R=    OUTPACK 
   8 2?=W ...      SBX   7  FX1  
   8 2?QG ...      OUTPACKX 2,4,3
   7 2#=6          ADX   7  1
   7 2#PQ          EXIT  7  0
   6 2#TM ...STEPREWRITE 
   8 2#_J ...      STEPREWRITE   
   7 2*5F ...      EXIT  7  0
   4 2*9B    SUB 
  10 2*P2          MHUNTW   2,FI,FUTILITY
   7 2B8L          EXIT  1  0
   4 2BN=    [   
  14 2C7W    START          [ENTRY ACROSS FROM BMAPONE   
   4 2CMG ...[   
   7 2D42 ...      LDN   0  4
   7 2DDG ...      BMSTATS  0
  16 2DT2 ...      FILEMOVE 2,0                [ DICT,MAST,SER   
   4 2DT6 ...[   
  18 2DT= ...[********************************************************** 
  17 2DTB ...[  PROCESSING OF SERIAL CAN BE DONE AUTONOMOUSLY IF NO  
  20 2DTG ...[  P. R. HAS BEEN DONE (I.E. IFTHER IS NO POSSIBILITY OF UPDATING   
  17 2DTL ...[  SERIAL'S ENTRY IN :SYSTEMBECAUSE IT WAS RESTORED).   
  16 2DTQ ...[  THE ONLY RESTRICTION IS THAT THE SLAVE CAN'T BE  
  18 2DTW ...[  ALLOWED TO FINISH UNTIL THE 'LEXICON' HAS BEEN CREATED   
  15 2DW2 ...[  AS OPENING 'INCINDEX' IN INCEND WILL NEED IT.
   4 2DW6 ...[   
   5 2DWF ...SETFLAG 
  20 2DXY ...      POP      SETFLAG,2,FTABWAIT  [ SET SEMAPHOR FOR SERIAL ACT.   
  16 2D_C ...      LDX   1  BFILE               [ NOT TO FINISH  
  19 2F2W ...      JBS      NONAUTO,1,BFPARTRES [ NO AUTO ACT. IF P.R. DONE  
  18 2F3K ...      BWNZ     GINCTSN,NONAUTO   [ JUMP IF GENERAL RESTORE  
  10 2F4* ...      GETACT   BSA,AUTOMAP  
   9 2F5S ...      LDX   3  BPTR(2)  
  10 2F7? ...      LINKSET  3,SERLINK(1) 
   7 2F8Q ...      LOCK     3
  16 2F=9 ...      CHANGEFI 3,,-2               [ SERIAL TO SLAVE
   9 2F?N ...      LDX   3  BPTR(2)  
   7 2F*7 ...      UNLOCK   3
   7 2FBL ...      LDX   2  3
   6 2FD5 ...      FPUT  
  20 2FFJ ...      FILEMOVE -1,0                [ CLOSE INCINDEX AS IT WON'T BE  
  18 2FH3 ...      CLOSETOP                     [ WANTED FOR P.R. UPDATES
   5 2FJG ...NONAUTO 
  10 2FL2 ...      BWZ      GINCTSN,XREC 
   7 2FR8 ...      REWIND   1
   5 2HJG    NEXTDIC 
  15 2J46 ...      STEP     1                   [ON MASTER   
   8 2JHQ ...      BZE   3  XBR  
   9 2K3B          LDX   0  ERESN(3) 
  17 2KH2          BNZ   0  NEXTDIC             [IF NOT A NAME RECORD
  14 2L2L          TESTNAMX 3,NDLOC(1),ELOC1N(3),NEXTDIC 
   9 2MFG          LDN   0  INDEXDICT
   7 2M_6          BMINDEX  0
   9 2NDQ          TESTRPN2 OK,XINDEX
   4 2NR6 ...XREC
  14 2NYB          CLOSEDIR                     [MASTER  
   9 2PXL          STOZ     AWORK3(2)
   9 2Q4S ...      DOWN     CREALEX,2
   8 2Q=2 ...      FILEMOVE 1,0  
   5 2QC=    XREC1   
   6 2QWW          STEP  
   8 2RBG          BZE   3  XBR  
  10 2RW6          LDX   0  CNEWZ-A1(3)  
   7 2S*Q          SBN   0  1
   8 2STB          BNZ   0  XBR  
  12 2SX# ...      LDX   0  IZLIST+IZLINE-A1(3)  
   7 2S_= ...      SBN   0  5
  18 2T38 ...      BZE   0  NEWUSEROK          [ J. IF BIT PATTERN O.K.  
  10 2T56 ...      CALL  7  STEPREWRITE  
   7 2T74 ...      LDN   0  5
  16 2T92 ...      STO   0  IZLIST+IZLINE-A1(3) [ ELSE CORRECT IT
   6 2T=Y ...NEWUSEROK   
  17 2T*2          LDX   4  IZCOUNT-A1(3)       [NO. OF BUDGET TYPES 
   9 2TSL          LDX   5  RTIME(1) 
  10 2W#=          LDX   6  RTIME+1(1)   
  14 2WRW          LDX   7  3                   [SAVE X3 
   4 2X?G    RB  
  11 2XR6          TXU   5  IZLIST+1-A1(3)   
  11 2Y=Q          TXU   6  IZLIST+2-A1(3)   
  18 2YQB          BCC      RBX                 [IF REALTIME RECORD FOUND
  16 2_=2          ADN   3  IZLINE              [SIZE OF RECORD  
  16 2_PL          BCT   4  RB                  [TRY NEXT TYPE   
  16 329=          GEOERR   1,NORLTIME          [MUST BE THERE   
   4 32NW    RBX 
  10 338G          LDX   0  IZLIST-A1(3) 
   9 33N6          STO   0  AWORK2(2)
  15 347Q          LDX   3  7                   [RESTORE X3  
  10 34MB          LDX   4  IZCOUNT-A1(3)
  10 3572          LDX   5  SPACEMT(1)   
  10 35LL          LDX   6  SPACEMT+1(1) 
   4 366=    RMT 
  11 36KW          TXU   5  IZLIST+1-A1(3)   
  11 375G          TXU   6  IZLIST+2-A1(3)   
   8 37K6          BCC      RMTX 
   9 384Q          ADN   3  IZLINE   
   8 38JB          BCT   4  RMT  
  10 3942          GEOERR   1,NOSPACMT   
   4 39HL    RMTX
  10 3=3=          LDX   0  IZLIST-A1(3) 
   9 3=GW          STO   0  AWORK1(2)
  18 3?G6          SETNCORE 41,3,FI,FUTILITY    [FOR BUDGERASE TABULATION
  18 3?_Q          STOZ     A1(3)               [NUMBER OF TAB RECORDS   
  19 3#FB ...      STOZ     AWORK4(2)           [ADDICTENT BLOCK NUM COUNT   
   4 3#_2    XF1 
   6 3*DL          STEP  
  17 3*Y= ...      BZE   3  NDDICT               [END OF DICTIONARY  
   9 3BCW          LDX   0  CNEWZN(3)
   7 3BXG          SBN   0  1
   9 3CC6          BZE   0  OKDICTENT
   7 3CWQ          SBN   0  1
  16 3DBB          BZE   0  XF1                 [IF LIST RECORD  
   9 3DW2          BRN      XDICTBENT
   5 3DYK ...NDDICT  
  19 3F38 ...      HUNTMISB 1,FILE,FWB          [ END OF DICTIONARY BUT CHECK
  18 3F5R ...      BPZ   1  NEWENTRY            [ IF ANY INSERTS AT END !
   9 3F8B ...      LDX   6  AWORK4(2)
   8 3F=_ ...      BRN      XF2  
   6 3F*L    OKDICTENT   
  10 3FT=          HUNTMISB 1,FI,FUSER   
   9 3G#W          SMO      AWORK3(2)
   9 3GSG ...      LDX   0  A1+5(1)  
   9 3H#6 ...      BNG   0  MARKED   
  10 3HRQ ...      CALL  7  STEPREWRITE  
   8 3J?B ...      LDCT  7  #20  
  21 3JR2          ORS   7  CPSEUN(3)           [MARK DICTIONARY EDTRY FOR DELETION  
   8 3K=L          LDCT  0  #200 
  10 3KQ=          ANDX  0  CPSEU-A1(3)  
  16 3L9W ...      BNZ   0  QDELPSEUD                [J IF TEMP  
   7 3LPG          OUTBLOCN 8
   7 3M96 ...      READAGAIN 
  10 3MNQ ...      MHUNTW   2,FILE,FRB   
  11 3N8B ...      NAME     2,FILE,ADICTENT  
   8 3NN2 ...      ADN   2  CUSER
   7 3P7L ...      LDN   4  3
  10 3PM= ...      LDN   3  JPDUSERNAME  
   9 3Q6W ...      CALL  7  OUTPACK  
  11 3QLG ...      MHUNTW   2,FILE,ADICTENT  
   9 3R66 ...      ADN   2  CSUPUSER 
   9 3RKQ          CALL  7  OUTPACK  
   9 3S5B          MONOUT   BMNODIR  
  11 3SK2          MHUNTW   1,FILE,ADICTENT  
   9 3T4L          LDX   0  CPSEU(1) 
  12 3TJ= ...      BPZ   0  QDELN               [
  18 3W3W ...      FREECORE 1                   [ ONLY DELETE DICTIONARY 
  19 3WHG ...QDELPSEUD                          [ ENTRIES HERE IF THEY ARE   
  19 3X36 ...      DELETE                       [ PSEUDO - PROPER USERS NEED 
  20 3XGQ ...      BRN      UPFUSERPOS          [ BUDGETS TO BE RETURNED LATER   
  12 3Y2B ...QDELN                              [
   9 3_B6 ...      LDX   6  AWORK4(2)
  18 3_JB ...      ADN   6  1                   [INCREMENT BLOCK NUMBER  
   9 3_QL ...      STO   6  AWORK4(2)
  16 3_YW          STO   6  CLOCN+1(1)          [INTO ADDICTENT  
  10 42DG          MHUNTW   3,FI,FUTILITY
   8 42Y6          LDX   4  A1(3)
  19 43CQ          SLL   4  2                   [FOR USED PART OF FUTILITY   
  10 43XB          LDX   5  ALOGLEN(3)   
   7 44C2          SBN   5  1
  18 44WL          BXL   4  5,QDEL2             [IF FUT BLOCK HAS ROOM   
  18 45B=          ADN   5  41                  [NO,LENGHTHEN FUTILITY   
  17 45TW          ALTLENG  3,5,SUB             [  -BLOCK BY 41 WORDS
  11 46*G          MHUNTW   1,FILE,ADICTENT  
   5 46T6    QDEL2   
  10 47#Q          MHUNTW   3,FI,FUTILITY
   7 47SB          LDN   5  1
   8 48#2          ADS   5  A1(3)
   8 48RL          ADN   3  A1+1 
   7 49?=          ADX   3  4
  16 49QW          LDX   5  CUSER(1)            [- - - - - - - - 
   8 4==G          STO   5  0(3) 
  17 4=Q6          LDX   5  CUSER+1(1)          [FILLING IN FUT/TAB  
  18 4?9Q          STO   5  1(3)                [    ENTRY OF USERNAME   
  20 4?PB          LDX   5  CUSER+2(1)          [      +ADDICTENT BLOCK NUMBER   
   8 4#92          STO   5  2(3) 
  17 4#NL          STO   6  3(3)                [- - - - - - - - - - 
   2 4*8=
  10 4*MW          BRN      UPFUSERPOS   
   4 4*MX ...[   
  19 4*MY ...[***************************************************  NEWENTRY  
  18 4*M_ ...[  THERE IS NO DICTIONARY ENTRY FOR THIS USER - ONE MUST BE 
  17 4*N2 ...[  INSERTED. THERE SHOULD BE A  FWB  IN THE MISC. CHAIN 
  16 4*N3 ...[  CONTAINING A SKELETON ENTRY SET UP BY BMAPDIR.   
   4 4*N4 ...[   
   5 4*N? ...NEWENTRY
  10 4*NN ...      HUNTMISB 3,FI,FUSER   
   9 4*P5 ...      SMO      AWORK3(2)
   9 4*PG ...      LDN   4  A1+1(3)  
  10 4*PX ...      LDN   5  ACOMMUNE4(2) 
  17 4*Q# ...      MOVE   4  3                  [ REQUIRED USERNAME  
   9 4*QP ...      LDX   3   BMISC+1 
   8 4*R6 ...      LDX   1  FX1  
   4 4*R8 ...[   
  17 4*R= ...[****************************************************** 
  10 4*R# ...[  FIRST FIND THE RIGHT FWB 
   4 4*RB ...[   
   5 4*RH ...NEXTMISB
   9 4*RY ...      LDX   0  ATYPE(3) 
  11 4*S* ...      BXU   0  TYPEFWB(1),NXBLK 
  14 4*SQ ...      TESTNAMX 3,ACOMMUNE4(2),CUSER(3),NXBLK
   8 4*T7 ...      CHAIN    3,2  
  18 4*TJ ...      BRN      NEWINSERT           [ CHAIN FWB IN ACT.CHAIN 
   5 4*T_ ...NXBLK   
  17 4*W6 ...      LDX   3  BPTR(3)             [ PTR. TO NEXT BLOCK 
  18 4*W? ...      BXU   3  CXMI,NEXTMISB       [ J. IF NOT BACK AT BMISC
  18 4*WD ...      BRN      XBR                 [ ELSE NOT FOUND - GEOERR
   4 4*WK ...[   
  18 4*WQ ...[***********************************************************
  17 4*WX ...[  FWB FOUND - IF ITS PSEUDO CAN INSERT IT AS IT IS. IF 
  17 4*X4 ...[  PROPER USER WHO OWNS TAPES WE MUST PUT IN 'SPACEMT'  
  19 4*X9 ...[ BUDGET RECORD.ALSO FOR PROPER USERS CHECK PASSWORD + SECURITY 
   4 4*XB ...[   
   6 4*XK ...NEWINSERT   
   9 4*Y2 ...      LDX   0  CPSEU(3) 
   9 4*YC ...      BNG   0  SUDOINS  
   9 4*YS ...      BZE   6  OKINSERT 
   7 4*_9 ...      LDN   0  1
  19 4*_L ...      ADS   0  CNUMQ(3)            [ NUMBER OF QUANTIT. BUDGETS 
  10 4B23 ...      STOZ     CQUANT+1(3)  
  17 4B2D ...      STO   6  CQUANT+2(3)         [ NO. OF TAPES OWNED 
   7 4B2T ...      LDN   0  5
   8 4B3= ...      ADS   0  A1(3)
   9 4B3M ...      LDX   0  AWORK1(2)
  18 4B44 ...      STO   0  CQUANT(3)           [ 'SPACEMT' BUDGET TYPE  
   5 4B4F ...OKINSERT
   4 4B4G ...[   
  18 4B4H ...[***********************************************************
  17 4B4J ...[  WE DON'T KNOW WHAT SECURITY THE USER USED TO HAVE.   
  18 4B4K ...[  SET PASSWORD TO 12 SPACES(UNLESS 'NEWDICTEN' USED) AND   
  16 4B4L ...[ SET SECURITY 'LOW' ( UNLESS 'NEWDICTENT' USED).   
   4 4B4M ...[   
   8 4B4W ...      LDX   0  ACES 
  10 4B5? ...      SBX   0  PASSWORD(1)  
  18 4B5N ...      BZE   0  NOTSEHIGH           [ J. IF PASSWORD = SPACES
  17 4B65 ...      LDN   0  1                   [ ELSE SET SECURITY  
  19 4B6G ...      ORS   0  CINDIC(3)           [ 'HIGH' (NEWDICTENT USED)   
   6 4B6X ...NOTSEHIGH   
  10 4B7# ...      LDN   4  PASSWORD(1)  
   9 4B7P ...      LDN   5  CPASS(3) 
  17 4B86 ...      MOVE  4  3                   [ MOVE IN PASSWORD   
   5 4B8H ...SUDOINS 
   9 4B8Y ...      LDX   5  CPSEU(3) 
   9 4B9* ...      LDN   2  CUSER(3) 
   7 4B9Q ...      LDN   4  3
  12 4B=7 ...      LDN   3  JPDUSERNAME         [
  17 4B=J ...      CALL  7  OUTPACK             [ OUTPUT DIC. ENTRY  
  19 4B=_ ...      BNG   5  NOPARAB             [ RECREATED MESSAGE - ONLY   
  19 4B?B ...      LDN   2  NINFUSER(1)         [ ADD 'PLEASE INFORM USER' IF
  17 4B?R ...      LDN   4  6                   [ IT'S A PROPER USER 
  12 4B#8 ...      LDN   3  JPDVARCHAR          [
   9 4B#K ...      CALL  7  OUTPACK  
   5 4B*2 ...NOPARAB 
  10 4B*C ...      MONOUT   BMDICTMADE   
  16 4B*S ...      INSERT                       [ NEW DICT. ENTRY
   9 4BB9 ...      MFREE    FILE,FWB 
   7 4BBL ...      BACKSPACE 
   7 4BC3 ...      STEPAGAIN 
  10 4BCD ...      HUNTMISB 1,FI,FUSER   
   4 4BCF ...[   
  20 4BCG ...[*********************************************************  MARKED  
  16 4BCH ...[  FUSER IS MARKED FOUND. CHECK THAT DICT. ENTRY IS 
  16 4BCJ ...[  FOR THE SAME USER - IF NOT IT MEANS THAT A FUSER 
  20 4BCK ...[  ENTRY HAS BEEN INSERTED BY BMAPDIR AND THAT THER IS A SKELETON   
  16 4BCL ...[  DICTIONARY ENTRY IN A FWB IN THE MISC. CHAIN TO  
   7 4BCM ...[  BE INSERTED. 
  18 4BCN ...[  WE FIRST CHECK THAT THE 'FREEZE' STATE OF THE DIRECTORY  
  18 4BCP ...[  IS THE SANE IN ITS SUPERIOR (REMEMBERED IN FUSER ENTRY)  
  17 4BCQ ...[  AS IT IS IN DICTIONARY - IF NOT CHANGE DICTIONARY.   
   4 4BCR ...[   
   5 4BCT ...MARKED  
   9 4BD= ...      SMO      AWORK3(2)
   9 4BDM ...      LDX   6  A1+5(1)  
   8 4BF4 ...      LDCT  0  #100 
   7 4BFF ...      ANDX  0  6
   7 4BFW ...      STOZ     7
   9 4BG? ...      BZE   0  NOTUSFROZ
   7 4BGN ...      LDN   7  1
   6 4BH5 ...NOTUSFROZ   
   8 4BHG ...      SBN   3  A1   
  12 4BHX ...      JBC      NOTFROZDIC,3,BFROZEN 
   7 4BJ# ...      SBN   7  1
   6 4BJP ...NOTFROZDIC  
  17 4BK6 ...      BZE   7  NOINVERT            [ J. IF SETTINGS SAME
  18 4BKH ...      BI       3,BFROZEN           [ ELSE CHANGE DICTIONARY 
   8 4BKP ...      ADN   3  A1   
  10 4BKY ...      CALL  7  STEPREWRITE  
   8 4BL6 ...      SBN   3  A1   
   5 4BL* ...NOINVERT
   8 4BLQ ...      ADN   3  A1   
   4 4BLS ...[   
  18 4BLW ...[********************************************************   
  16 4BLY ...[  NOW CHECK IF DICT. ENT. IS FOR SAME USER AS IS   
  13 4BM2 ...[  FUSER ENTRY. J. TO 'NEWENTRY' IF NOT 
   4 4BM4 ...[   
  10 4BM7 ...      HUNTMISB 1,FI,FUSER   
   3 4BMJ ...
   8 4BM_ ...      SMO      FX1  
   9 4BNB ...      ANDX  6  NFUSMASK 
   9 4BNR ...      ADX   1  AWORK3(2)
  15 4BP8 ...      TESTNAMX 3,CUSER-A1(3),A1+1(1),NEWENTRY   
   4 4BP= ...[   
  15 4BP# ...[***********************************************
  16 4BPB ...[  ZEROISE COUNT OF JOBS RUNNING UNLESS 'JOURNAL'   
  11 4BPD ...[  ACCOUNTING' IS BEING USED.   
   4 4BPG ...[   
   8 4BPK ...      LDX   1  FX1  
   9 4BQJ ...      BSJ      NOSTOZJC 
  10 4BS? ...      LDX   0  CJOBCT-A1(3) 
   9 4BW2 ...      BZE   0  NOSTOZJC 
  10 4BXP ...      CALL  7  STEPREWRITE  
  10 4B_G ...      STOZ      CJOBCT-A1(3)
   5 4C8D ...NOSTOZJC
   4 4C9S ...[   
  18 4C?8 ...[*********************************************************  
  12 4C#J ...[  CLEAR 'ERASE' BIT IF IT IS SET   
   4 4C*Y ...[   
   9 4CCB ...      LDX   7  MER(1)   
  15 4CLB          ANDX  7  CPSEU-A1(3)         [ERASE BIT   
   9 4CRJ ...      BZE   7  NOERASE  
  10 4CYQ ...      CALL  7  STEPREWRITE  
   9 4D3B ...      LDX   7  MER(1)   
  10 4D62          ERS   7  CPSEU-A1(3)  
   5 4D*S ...NOERASE 
  10 4DKL          LDX   0  CPSEU-A1(3)  
  15 4F5=          BNG   0  UPFUSERPOS          [IF PSEUDO   
   4 4F7T ...[   
  18 4F=D ...[********************************************************** 
  18 4F*3 ...[ NOW CORRECT 'REALTIME' AND 'SPACEMT' BUDGETS FOR PROPER   
  10 4FCL ...[  USERS ( IF THEY HAD ANY) 
   4 4FG9 ...[   
   9 4FJW          LDX   7  CNUMQN(3)
  19 4G4G          BZE   7  UPFUSERPOS          [USER HAS NO QUANT BUDGETS   
   7 4GJ6          LDN   4  0
   4 4GM= ...[   
  18 4GQB ...[*********************************************************  
  13 4GTG ...[  ZEROISE 'REALTIME' BUDGET IF ANY USED
   4 4GYL ...[   
   4 4H3Q    RBL 
   7 4HHB          SMO      4
  10 4J32          LDX   0  CQUANT-A1(3) 
  17 4JGL ...      BXE   0  AWORK2(2),RBF       [J IF REALTIME RECORD
   9 4K2= ...      ANDX  0  WORDS(1) 
   7 4KFW          ADX   4  0
   8 4K_G          BCT   7  RBL  
  18 4LF6          BRN      NRB                 [USER HAS REALTIME BUDGET
   4 4LYQ    RBF 
   7 4M3W ...      SMO      4
  11 4M72 ...      LDX   0  CQUANT+2-A1(3)   
   8 4M=6 ...      BZE   0  NRB  
  10 4M*= ...      CALL  7  STEPREWRITE  
   7 4MDB          SMO      4
  11 4MY2          STOZ     CQUANT+2-A1(3)   
   4 4NCL    NRB 
  10 4NX=          LDX   7  CNUMQ-A1(3)  
   7 4PBW          LDN   4  0
   4 4PFF ...[   
  18 4PJ4 ...[*********************************************************  
  16 4PLM ...[  UPDATE 'SPACEMT' BUDGET FROM COUNT IN FUSER ENTRY
  11 4PP= ...[  IF DICTIONARY ENTRY INCORRECT
   4 4PRT ...[   
   4 4PWG    RMTB
   7 4QB6          SMO      4
  10 4QTQ          LDX   0  CQUANT-A1(3) 
  15 4R*B ...      BXE   0  AWORK1(2),RMTF      [J IF SPACEMT
   9 4RT2 ...      ANDX  0  WORDS(1) 
   7 4S#L          ADX   4  0
   8 4SS=          BCT   7  RMTB 
  10 4T?W          BRN      UPFUSERPOS   
   4 4TRG    RMTF
   7 4TW5 ...      SMO      4
  10 4TYN ...      LDX   0  CQUANTN+2(3) 
   7 4W3? ...      SBX   0  6
  10 4W5W ...      BZE   0  UPFUSERPOS   
  10 4W8F ...      CALL  7  STEPREWRITE  
   7 4W?6          SMO      4
  21 4WQQ          STO   6  CQUANTN+2(3)        [UPDATE MAG TAPE BUDGET OROM FUSER   
   6 4X=B    UPFUSERPOS  
   7 4XQ2          LDN   0  5
   8 4Y9L          LDX   2  FX2  
  18 4YP=          ADS   0  AWORK3(2)           [UPDATE FUSER POSITION   
  10 4_8W          HUNTMISB 1,FI,FUSER   
   8 4_NG ...      LDX   0  A1(1)
   7 5286 ...      ADN   0  2
  10 52MQ ...      BXL   0  AWORK3(2),XBR
   8 537B          BRN      XF1  
   4 53M2    XF2 
   4 546L ...[   
  18 54L= ...[*********************************************************  
  17 555W ...[  NOW CHECK IF THERE ARE ANY PROPER USERS TO DELETE.   
  16 55KG ...[  EACH ONE WILL HAVE AN ADICTENT BLOCK AND AN ENTRY
  15 5656 ...[  IN THE FUTILITY. IF THERE ARE WE MUST FIRST  
  17 56JQ ...[  RETURN ALL BUDGETS BEFORE DELETEING THE DICT. ENTS.  
   4 574B ...[   
   4 57_D ...XF4 
   9 58BW ...      STO   6  AWORK4(2)
   5 58S# ...XF4PLUS 
  11 599Q ...      HUNT     1,FILE,ADICTENT  
   8 59M8 ...      BNG   1  XF4A 
  19 5=4L ...      LDX   5  CUSER(1)            [USERNAME OF THIS ADICTENT   
  10 5=G4 ...      LDX   6  CUSER+1(1)   
  10 5=XG ...      LDX   7  CUSER+2(1)   
  14 5?#Y ...      TRACEIF  K6ADDICT,199,299,5,ADFOUND   
  16 5?QB ...      TRACEIF  K6ADDICT,199,299,CLOCN+1(1),ADICNUM2 
  20 5#7S ...      READDICT ,5,NOADICT          [TO POSITION DICT AT THIS ENTRY  
   7 5#K= ...      STEPAGAIN 
   9 5*2N ...      LDX   4  CNUMQN(3)
   9 5*9C ...      BZE   4  NOBUDGETS
   4 5*D6 ...XF8B
  10 5*TJ ...      LDX   5  CQUANTN(3)   
  20 5B?2 ...      ANDX  5  NGCLASS(1)          [TO TELL TRENSIENT FROM STACLE   
  10 5BND ...      STOZ     CQUANTN+1(3) 
  10 5C5W ...      STOZ     CQUANTN+2(3) 
  10 5CH# ...      STOZ     CQUANTN+3(3) 
  18 5CYQ ...      BXE   5  NGTOR(1),XF7B       [J IF TRANSIENT BUDGET   
  10 5DB8 ...      STOZ     CQUANTN+4(3) 
   4 5DRL ...XF7B
  10 5F94 ...      LDX   5  CQUANTN(3)   
  18 5FLG ...      ANDX  5  NGWORDS(1)          [NUMBER OF WORDS IN ENTRY
  16 5G3Y ...      ADS   5  3                   [J OVER INF WORDS
   8 5GFB ...      BCT   4  XF8B 
   6 5GN5 ...NOBUDGETS   
  11 5GWS ...      MHUNT    1,FILE,ADICTENT  
  17 5H#= ...      LDX   5  CSUPUSER(1)         [SUPERIOR USER NAME  
  14 5HPN ...      LDX   6  CSUPUSER+1(1)       [ -FROM  
  15 5J76 ...      LDX   7  CSUPUSER+2(1)       [ -ADDICTENT 
  13 5JJJ ...      TRACEIF  K6ADDICT,199,299,5,ADINTO
  19 5K22 ...      BUDGERASE                    [ RETURN BUDGETS TO SUPERIOR 
  10 5KCD ...      MFREEW   FILE,ADICTENT
  10 5KSW ...      MHUNTW   1,FI,FUTILITY
  17 5L=# ...      LDX   2  A1(1)               [INITIALISE POINTER  
   7 5LMQ ...      SBN   1  4
  15 5M58 ...      ADN   1  10                  [   -RECORD  
   4 5MGL ...XF5B
  17 5MY4 ...      ADN   1  4                   [POINT AT NEXT RECORD
   8 5N*G ...      TXU   7  2(1) 
   8 5NQY ...      TXU   6  1(1) 
   8 5P8B ...      TXU   5  0(1) 
  18 5PKS ...      BCC      XF4B                [IF NAMES ARE THE SAME   
  15 5Q3= ...      BCT   2  XF5B                [NEXT RECORD 
   9 5QDN ...      BRN      XF4PLUS  
   4 5QW6 ...XF4B
  14 5R?J ...      TRACEIF  K6ADDICT,199,299,5,NAMINFUT  
  15 5RP2 ...      TRACEIF  K6ADDICT,199,299,3(1),BLOKNOFS   
   8 5S6D ...      READDICT ,5   
  11 5SHW ...      MHUNTW   1,FILE,ADICTENT  
  15 5S_# ...      STO   1  3                   [SAVE POINTER
   5 5TBQ ...XF10B   
  16 5TS8 ...      HUNT2J   1,FILE,ADICTENT,,XF9B [DUMMY HUNT TO 
  17 5W9L ...      STO   1  2                   [    -END OF ACTIVITY
  15 5WM4 ...      BRN      XF10B               [    - CHAIN 
   4 5X4G ...XF9B
  16 5XFY ...      CHAIN    3,2                 [RECHAIN AT END  
   9 5XXB ...      LDX   7  AWORK4(2)
  17 5Y#S ...      ADN   7  1                   [GIVE NEW ADICTENT   
  17 5YQ= ...      STO   7  CLOCN+1(3)          [  -A BLOCK NUMBER   
   9 5_7N ...      STO   7  AWORK4(2)
  18 5_K6 ...      LDX   4  CUSER(3)            [NEW ADICTENT USERNAME   
  10 622J ...      LDX   5  CUSER+1(3)   
  10 62D2 ...      LDX   6  CUSER+2(3)   
  10 62TD ...      MHUNTW   1,FI,FUTILITY
   8 63=W ...      LDX   2  A1(1)
  19 63N# ...      ADN   1  10-4                [IMITIALISE TAB BLOCK POINTER
   5 645Q ...XF12B   
  15 64H8 ...      ADN   1  4                   [NEXT RECORD 
  17 64YL ...      TXU   4  0(1)                [TEST IF SAME NAME   
   8 65B4 ...      TXU   5  1(1) 
   8 65RG ...      TXU   6  2(1) 
  13 668Y ...      BCC      XF11B               [YES 
  19 66LB ...      BCT   2  XF12B               [TO NEXT RECORD IN TAB BLOCK 
  10 673S ...      GEOERR   1,BLOKWARP   
   5 67F= ...XF11B   
  19 67WN ...      LDX   4  3(1)                [OLD BLOCK NUM FROM TAB BLOK 
  19 68#6 ...      STO   7  3(1)                [NEW BLOK NUM INTO TAB BLOK  
   8 68PJ ...      LDX   2  FX2  
   5 6972 ...XF14B   
  12 69JD ...      HUNT2J   2,FILE,ADICTENT,,XF6B
  17 69_W ...      BXU   4  CLOCN+1(2),XF14B    [HUNT FOR OLD BLOCK  
  16 6=C# ...      TRACEIF  K6ADDICT,199,299,CLOCN+1(2),OLDSUPD  
  17 6=SQ ...      FREECORE 2                   [FREE IT WHEN FOUND  
   4 6?=8 ...XF6B
   7 6?ML ...      STO   7  6
   8 6#54 ...      BRN      XF4  
  15 6#GG ...XF4A  FASTREWI 1                   [DICTIONARY  
  10 6#KT ...      MHUNTW   1,FI,FUTILITY
   8 6#P8 ...      LDX   0  A1(1)
   9 6#SH ...      BZE   0  NODELS   
   6 6#XY ...      STEP  
  16 6**B ...XF4C  STEP                         [TO USER RECORD  
   8 6*QS ...      BNZ   3  XF4D 
   5 6*_H ...NODELS  
  10 6B8= ...      MFREEW   FI,FUTILITY  
   8 6BKN ...      BRN      XF3  
   4 6C36 ...XF4D
   8 6CDJ ...      LDCT  4  #20  
   9 6CW2 ...      ANDX  4  CPSEUN(3)
  17 6D?D ...      BZE   4  XF4C                [IF NOT BE DELETED   
  17 6DNW ...      DELETE                       [DICTIONARY RECORD   
   8 6F6# ...      BRN      XF4C 
   4 6FKY ...XF3 
   4 6FP4 ...[   
  18 6FS8 ...[********************************************************** 
  19 6FX# ...[  NOW GO DOWN TO CREATE LEXICON AND JOIN-UP ANY RESTORED LIMBS 
   4 6G2D ...[   
   9 6G5J ...      DOWN     CREALEX,1
  21 6G9F ...      BSOFF    BMBLKTWICE,NOTWICES [ J. IF NO DOUBLE ALLOCATION OF B.S. 
  18 6G*B ...      DOWN     BMFIXIT,3          [ ELSE SWAP FOR NEW BLOCKS
   5 6GF? ...NOTWICES
   4 6GFX ...[   
  18 6GGH ...[********************************************************** 
  16 6GH7 ...[  CHECK IF :DUMPER WAS RESTORED. IF SO, CHECK IF   
  19 6GHR ...[  :DUMPER AGREES WITH SERIAL AND INCINDEX. (DONE IN INCPTRES)  
   4 6GJC ...[   
  10 6GK8 ...      DOWN     INCPTRES,1   
   4 6GM6 ...[   
  18 6GP4 ...[*********************************************************  
  17 6GR2 ...[  NOW CHECK IF 'SERIAL' PROCESSING IS BEING DONE BY AN 
  16 6GSY ...[  AUTONOMOUS ACTIVITY - IT WILL BE UNLESS PARTIAL  
  15 6GWW ...[  RESTORES HAVE BEEN INITIATED, IN WHICH CASE  
  11 6GYS ...[  CARRY ON TO PROCESS SERIAL.  
   4 6H2Q ...[   
  19 6H3R ...      BWNZ     GINCTSN,Z2ADDICT    [ NO SLAVE IF GENERAL RESTORE
   8 6H4S ...      LDX   1  BFILE
  12 6HJD ...      JBC      SERDONE,1,BFPARTRES  
   4 ==76    [   
   4 ==9P ...[   
  20 ==## ...[********************************************************  Z2ADDICT 
  14 ==BX ...[  ENTRY POINT FOR SLAVE ACTIVITY TO PROCESS
  14 ==FG ...[  :SYSTEM.SERIAL AGAINST THE FTAPE BLOCK.  
   4 ==J5 ...[   
   4 ==LQ ...[   
   5 ==WJ ...Z2ADDICT
  12 =?6B ...      BWZ      GINCTSN,NOINDEXSER   
  17 =?L2 ...      SETNCORE 16,2,FILE,FABSNB    [FOR OPENING SYSTEM  
   8 =#5L          ADN   1  NSER 
   8 =#K=          ADN   2  A1   
   8 =*4W ...      MOVE  1  16   
  18 =*JG          OPENDIR  XBR,GENERAL         [SYSTEM AT SERIALS ENTRY 
   7 =B46          BACKSPACE 
  19 =BHQ          MFREEW   FILE,ENT            [NOT NEEDED AS SERIAL IS OPEN
  17 =C3B          FILEMOVE 0,1                 [SERIAL =0,SYSTEM=1  
   9 =DG=          LDN   0  INDEXSER 
   7 =D_W          BMINDEX  0
   9 =FFG          TESTRPN2 OK,XINDEX
   7 =F_6          CLOSEDIR  
   6 =GDQ    NOINDEXSER  
  10 =GYB          HUNTMISB 3,FI,FTAPE   
  18 =HD2          LDX   6  A1(3)               [USED DATA SPACE IN FTAPE
  19 =HXL          STOZ     5                   [INITIALISE FTAPE ENT POINTER
  20 =JC=          BZE   6  NEMPTFTAPE          [NO NEED TO CORRELATE IF EMPTY   
   6 =JWW    NEXTFTAPENT 
   6 =KBG          STEP  
  10 =KW6          BZE   3  NEMPTFTAPE   
  10 =L*Q ...      HUNTMISB 2,FI,FTAPE   
   7 =LTB ...      SMO      5
  16 =M*2          LDX   0  A1+1(2)             [TSN I N ENTRY   
  20 =MSL          BNG   0  UPFTAPEPOS          [IF ENTRY WAS FOUND IN BMAPONE   
   7 =N2W ...      SMO      5
   9 =N96 ...      LDX   0  A1+2(2)  
  10 =NCB ...      BZE   0  UPFTAPEPOS   
   7 =NKL ...      SMO      5
  19 =NRW          ADN   2  A1+2                [ADDRESS OF USERNAME IN FTAPE
   9 =P?G          LDN   3  AWORK1   
   8 =PR6 ...      ADX   3  FX2  
   7 =Q=Q ...      MOVE  2  3
  16 =QQB          DELETE                       [ENTRY IN SERIAL 
   7 =R=2          OUTBLOCN 7
   7 =RN4 ...      LDN   4  1
  10 =S66 ...      HUNTMISB 2,FI,FTAPE   
   7 =SJ8 ...      SMO      5
   8 =T2= ...      ADN   2  A1+1 
   9 =TD# ...      LDN   3  JPDTSN   
   9 =TWB ...      CALL  7  OUTPACK  
   9 =W#D ...      LDN   2  AWORK1(2)
   7 =WQG ...      LDN   4  3
  10 =X8J ...      LDN   3  JPDUSERNAME  
   9 =XLL          CALL  7  OUTPACK  
  19 =Y6=          MONOUT   BMTD                [MESSAGE THAT TAPE DELETED   
   6 =YKW    UPFTAPEPOS  
  19 =_5G          ADN   5  4                   [POSITION OF NEXT FTAPE ENTRY
  20 =_K6 ...      TXU   5  6                   [HAVE WE PROCESSED ALL OF FTAPE  
  10 ?24Q ...      BCS      NEXTFTAPENT  
   6 ?2JB    NEMPTFTAPE  
  14 ?342 ...      CLOSETOP                     [ SERIAL 
  10 ?3HL          HUNTMISB 3,FI,FTAPE   
   7 ?43=          FREECORE 3
   4 ?43T ...[   
  15 ?44D ...[********************************************** 
  16 ?453 ...[  IF PROCESSING IN SLAVE ACTIVITY WE MUST NOT CLOSE
  16 ?45L ...[  INCINDEX - WAS DONE ALREAY WHEN SLAVE WAS SET UP.
   4 ?469 ...[   
  20 ?46H ...      BWNZ     GINCTSN,TOCLOSE   [ MUST CLOSE INCINDEX IF GEN RES   
   8 ?46T ...      LDX   1  BFILE
  12 ?4=D ...      JBC      NOTCLOSE,1,BFPARTRES 
   5 ?4#9 ...TOCLOSE 
  15 ?4B3 ...      CLOSETOP                     [  INCINDEX  
   5 ?4FL ...NOTCLOSE
  16 ?4HC ...      LDX   2  BSACHAPTR           [ => MAST. ACT.  
  18 ?4K9 ...      POP      NOTCLOSE,2,FTABWAIT    [ CHECK FLAG BEFORE   
  20 ?4NS ...      VOP      2,FTABWAIT          [ USING LEXICON TO OPEN INCINDEX 
  10 ?4QX ...      DOWN     INCCHECK,2   
   4 ?4T2 ...[   
  18 ?4TK ...[*********************************************************  
  15 ?4W8 ...[  ONLY WANT TO 'SUICIDE' IF IN SLAVE ACTIVITY. 
  13 ?4WR ...[  ELSE GO ON TO PROCESS THE JOURNAL.   
   4 ?4XB ...[   
   8 ?4Y2 ...      LDX   1  BFILE
  12 ?53K ...      JBS      SERDONE,1,BFPARTRES  
  11 ?55B ...      BWNZ     GINCTSN,SERDONE  
   7 ?578 ...      SUICIDE   
   5 ?5=R ...SERDONE 
   9 ?5BB ...      ACROSS   BMSJRNL,1
   4 ?5G6    NFB 
   9 ?5_Q          LDX   3  FPTR(3)  
   4 ?6FB    NFB1
   9 ?6_2          LDX   0  ATYPE(3) 
  17 ?7DL          BXGE  0  CACT,(4)            [J IF NO MORE FWBS   
   8 ?7Y=          SRL   0  12   
   9 ?8CW          SBN   0  FILE+FWB 
   8 ?8XG          BNZ   0  NFB  
   7 ?99X ...      EXIT  4  1
   4 ?9PH ...XBR 
   9 ?=97 ...      GEOERR   1,ADDICT 
   5 ??*L    XINDEX  
  10 ??KD ...      GEOERR   1,DICIXERR   
   6 ??T= ...XDICTBENT   
  10 ?#54 ...      GEOERR   1,DICTBENT   
   4 ?##W    [   
  11 ?#SG          MENDAREA BMAPGAP,K99ADDICT
   4 ?*#6    #END
   6 ____ ...566747470002
