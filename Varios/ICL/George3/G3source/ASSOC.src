  14 228= ...      SEG      ASSOC,867,SECTION COMM,,G402 
   4 22BG ...[   
  16 22JQ ...[  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982 
   4 22R2 ...[   
   9 22_=    #OPT           K0ASSOC=0
   7 23DW    #OPT  K6ASSOC=0 
  14 23YG    #LIS           K0ASSOC>K0COMMAND>K0ALLGEO   
   4 24D6 ...[   
   7 24XQ    # ENTRY POINTS  
  11 25CB          SEGENTRY K1ASSOC,Z1ASSOC  
  11 25X2          SEGENTRY K2ASSOC,Z2ASSOC  
  20 26BL    #              THIS SEGMENT DEALS WITH THE ASSOCIATE COMMAND, E.G:- 
  16 26W=    #              ASSOCIATE U29.L10,PR BILLS,LF *LP *TP
  20 27*W    #              THE COMMAND IN PARAM 3 IS CHECKED AGAINST A TABLE OF 
  20 27TG    #              VALID ASSOCIATED COMMANDS AND THE PERIPHS ARE CHECKED
  20 28*6    #              TO BE OF TYPE 5. X6 IS SET TO CONTAIN THE RELEVANT   
  20 28SQ    #              COMMAND BIT IN B0-B11 & THE RELEVANT PERIPH BITS IN  
  20 29#B    #              B12-B23. NEXT PARAMETER 2 IS CHECKED TO BE A VALID   
  20 29S2    #              PROPERTY NAME,AND THE RELEVANT PROPERTY NO. IS SET   
  20 2=?L    #              IN X7. FINALLY PARAMETER 1 IS CHECKED TO BE A VALID  
  20 2=R=    #              PERIPHERAL DESCRIPTION,AND X6,X7 ARE COPIED TO THE   
  15 2?=W    #              ASSOCIATIONS ENTRY IN THE IDF.   
   4 2?QG    #   
  15 2#=6    #              ENTRY AT K1 MAKES AN ASSOCIATION 
  16 2#PQ    #              ENTRY AT K2 CANCELS AN ASSOCIATION   
   4 2*9B    #   
  15 2*P2    #              AWORK2 CONTAINS THE UNIT NUMBER  
  18 2B8L    #              AWORK3 : USED AS ENTRY MARKER :- 0=K1, -VE=K2
  21 2BN=    #              AWORK4 HOLDS THE PARAMETER NUMBER OF THE PERIPH.DESCRIPT 
   4 2C7W    #   
   4 2CMG    #   
   4 2D76    #   
  20 2DLQ    XPR            8HPROPERTY          [USED IN VALIDATION OF PROPERTY  
  15 2F6B                                       [PARAMETER.  
  16 2FL2    XTAB           [ TABLE FOR VALIDATION OF'COMMAND'   
  10 2G5L          ASSOCTAB LISTFILE,8,LF
   5 2GK=    #GAP  5 
  16 2H4W    XENDTAB        0                   [END OF TABLE.   
  21 2HJG    THREE          3                   [TO TEST FOR SHORT FORM OF COMMANDS  
   4 2J46    XPLT
  21 2JHQ    #              TABLE FOR VALIDATION OF 3RD PARAM,IE:-ARE THE PL TYPES   
  19 2K3B    #              MENTIONED VALID FOR COMMAND MENTIONED? EG:-LF *TR
  21 2KH2    #              IS NOT A VALID COMBINATION. ONE WORD ENTRY PER SIMPLE PL 
  20 2L2L    #              (IE TYPE<5) WHICH HAS BITS SET CORRESPONDING TO THE  
  21 2LG=    #              COMMANDS FOR WHICH THEY ARE VALID. THIS TABLE IS ACCESSED
  21 2L_W    #              BY TYPE AND THE CONTENTS MASKED WITH THE COMMAND BIT OF  
  21 2MFG    #              THE CURRENT COMMAND.THUS ZERO RESULT = INVALID PL TYPE.  
   4 2M_6    #   
  16 2NDQ                   0                   [TR -NOT VALID   
  17 2NYB                   +IVLF_IVOL_IVTP     [TP-VALID FOR LF,OL  
  17 2PD2                   +IVLF_IVOL_IVLP     [LP-VALID FOR LF,OL  
  16 2PXL                   0                   [CR -NOT VALID   
  17 2QC=                   +IVLF_IVOL_IVCP     [CP-VALID FOR LF,OL  
   7 2QWW                   0
   7 2RBG                   0
   7 2RW6                   0
   7 2S*Q                   0
   7 2STB                   0
   7 2T*2                   0
   7 2TSL                   0
   7 2W#=                   0
   7 2WRW                   0
   7 2X?G                   0
   7 2XR6                   0
   7 2Y=Q                   0
   7 2YQB                   0
   7 2_=2                   0
   7 2_PL                   0
  17 329=                   +IVLF_IVOL_IVGP     [GP-VALID FOR LF,OL  
   4 32NW    #   
  21 338G    #              TABLE OF VALID COMMANDS CONTAINING THE DEFAULT SETTINGS  
  21 33N6    #              OF VALID PERIPHERAL TYPES. ACCESSED BY COMPARING COMMAND 
   8 347Q    #              BITS.
   4 34MB    #   
   5 3572    XDEFTY  
  21 35LL                   +IVLF_IVLP_IVTP_IVCP_IVGP   [LP,TP,CP AND GP VALID FORLF 
   4 366=    #   
   4 36KW    #   
   4 375G    #   
  17 37K6    #                      COMMAND ERRORS FOR THIS SEGMENT  
  17 384Q    #                     --------------------------------- 
   7 38JB          ZERRORY   
   5 3942    ZENDER  
  16 39HL          COMERR   JNOPER              [ILLEGAL UNIT NO 
   6 39L9 ...#SKI  JSKI27
   4 39NS ...(   
  16 39RC ...XTM   COMERR   JTOOMANY            [TOO MANY PARAMS 
  20 39W2 ...XNU   COMERR   JOPUCON             [ONLY OPERATOR AND USER CONTEXT  
   4 39YK ...)   
  19 3=3=    ZCE1  COMERR   JPARMIS,JCOMA       [COMMAND PARAMETER MISSING   
  16 3=GW    ZCE2  COMERR   JTOOMANY            [TOO MANY PARAS  
  19 3?2G    ZCE3  ZERRORX  APFERR              [PARAMETER FORMAT ERROR (UT#)
  16 3?G6    ZCE4  ZERRORX  JPNAL               [PL NOT ALLOWED  
  17 3?_Q    ZCE5  ZERRORX  JPARMIS             [PARAMETER MISSING   
  16 3#FB    ZCE6  ZERRORX  JPARNULL            [PARAMETER NULL  
   9 3#_2    ZCE8  ZERRORX  ERUNPAIR 
   5 3*DL    ZNOMATCH
  18 3*Y=    ZCE9  ZERRORX  ASSOC2              [NOT VALID COMMAND PARAM 
  19 3BCW    XERB  ZERRORX  JASSOCA             [ASSOCIATION ALREADY DEFINED 
  20 3BXG    XERC  ZERRORX  JASSOCB             [ONLY 3 ASSOCIATIONS PERMITTED   
   7 3C4N ...#UNS  ANSTOOMANY
  19 3C9W ...XERD  ZERRORX  JMAXPAR             [MAX NO OF PARAMS EXCEEDED   
   6 3CC6    ZENDE ENDCOM
   4 3CWQ    #   
  17 3DBB    #              NORMAL SUCCESS REPLIES TO THIS COMMAND:- 
   4 3DW2    #   
  18 3F*L    XASSOCR        +ASSOCR1        [ASSOCIATION '%A' NOW EXISTS 
  20 3FT=                   +ASSOCR2        [ASSOCIATION '%A' NOW NON-EXISTANT   
   4 3G#W    #   
   4 3GSG    #   
   4 3H#6    #   
  21 3HRQ    #              PARAL:DOES A PARALYSX, XPARAL:DOES A PARAFREE & PARALYSX 
  20 3J?B    #              UPAR:DOES A PARANOTX AND THEN LOCATES CPB/CUNI IN X3 
   4 3JR2    #   
   4 3K=L    UPAR
   8 3KQ=          SBX   5  FX1  
   7 3L9W          PARANOTX 4
   9 3LPG          MHUNT   3,CPB,CUNI
   8 3M96    UPAR1 ADX   5  FX1  
   7 3MNQ          EXIT  5  0
   4 3N8B    #   
  19 3NN2    PARAL          [FORM A MULTI-BLOCK. PARAM IN X4,SEPARATOR IN X3 
   8 3P7L          SBX   5  FX1  
   8 3PM=    PARA1 PARALYSX 3,,4 
   7 3PQB ...#UNS  ANSTOOMANY
  13 3PTG ...      TESTREP2 UNPAIR,ZCE8,TOOMANY,XERD 
   7 3PYL ...#UNS  ANSTOOMANY
   4 3Q3Q ...#SKI
  10 3Q6W          TESTREP  UNPAIR,ZCE8  
   8 3QLG          BRN      UPAR1
   4 3R66    #   
   5 3RKQ    XPARAL  
   8 3S5B          SBX   5  FX1  
   7 3SK2          PARAFREE  
   8 3T4L          BRN      UPAR1
  21 3TJ=    PARA                               [SEARCHES FOR A PARAMETER BEGINNING  
  21 3W3W          SBX   7  FX1                 [WITH A SPECIFIED STRING OF CHARACTS.
  16 3WHG          SPARABEX 4,5,XTAB(1)         [PRESERVE SPACES.
  10 3X36          MHUNT    2,CPB,CUNI   
   9 3XGQ          NGX   0  ANUM(2)  
   8 3Y2B          ADX   7  FX1  
   7 3YG2          EXIT  7  0
   5 3Y_L    XFREE   
  21 3_F=          FREECORE 2                   [FREE THE CPB/CUNI BLOCK HOLDING THE 
  21 3_YW                                       [REST OF THE PARAMETER.(CREATED BY   
  15 42DG          EXIT  7  0                   [SPARABEX)   
   4 42Y6    #   
   4 43CQ    #   
   5 43XB    Z1ASSOC 
  16 44C2          STOZ     AWORK3(2)           [MARK K1 ENTRY   
  10 44WL          PARANUMB 3,AWORK3(2)  
   7 45B=          SBN   3  4
  16 45TW          BPZ   3  ZCE2                [TOO MANY PARAMS 
   4 46*G    Z10 
  16 46T6          STOZ     AWORK4(2)           [ED BYSPARABEX   
   7 47#Q          STOZ     3
   7 47SB          LDN   4  1
   4 48#2    XX1 
   7 48RL          SMO      3
  18 49?=          LDX   5  XTAB(1)             [NUMBER OF CHARACTERS.   
  21 49QW          BZE   5  ZCE1                [NOT A LEGITIMATE COMMAND PARAMETER  
   7 4==G          ADN   3  1
   7 4=Q6    XX2   ADX   1  3
  20 4?9Q                                       [SEARCH FOR THE COMMND PARAMETER.
   8 4?PB          CALL  7  PARA 
  16 4#92          BZE   0  YYX                 [LF  ACCEPTABLE  
  17 4#NL          BPZ   0  XX3                 [PARAMETER NOT FOUND.
   4 4*8=    YYX 
  18 4*MW          LDX   4  JPARNUM(2)          [NO. OF PARAMETER FOUND. 
  10 4B7G          BXL   5  THREE(1),XX4 
   7 4BM6          ADN   3  3
  20 4C6Q    XX4   SMO      3                   [POSITION OF THE WORD HOLDING THE
  20 4CLB                                       [COMMAND BIT IN THE COMMAND TABLE
   9 4D62          LDX   6  XTAB(1)  
  15 4DKL          SLL   6  12                  [COMMAND BIT 
  20 4F5=          BRN      START               [GO TO VALIDATE THE REST OF THE  
  17 4FJW                                       [COMMAND PARAMETER   
   4 4G4G    XX3 
  16 4GJ6          CALL  7  XFREE               [FREE THE BLOCK  
  20 4H3Q          BXL   5  THREE(1),XX5        [SEARCH FOR THE COMMAND PARAMETER
  21 4HHB          ADN   3  3                   [STARTING WITH A DIFFERENT CHAR.STR. 
   7 4J32          LDN   5  2
   8 4JGL          BRN      XX2  
   8 4K2=    XX5   BUX   3  XX1  
   5 4KFW    START   
   8 4K_G          CALL  7  XFREE
   9 4LF6          ADS   4  AWORK4(2)
   8 4LYQ          LDN   3  #20  
  17 4MDB          STO   5  7                   [NO OF CHS IN COMMAND
  21 4MY2                                       [INVESTIGATE THE PERIPHERAL PART OF  
  18 4NCL          CALL  5  PARAL               [THE COMMAND PARAMETER.  
   7 4NX=          LDN   4  1
   8 4PBW          CALL  5  UPAR 
  17 4PWG          LDEX  5  ANUM(3)             [SPACES MUST SEPARATE
  18 4QB6          BXU   5  7,ZCE3              [COMMAND FROM PL TYPES   
   7 4QTQ          FREECORE 3
  16 4R*B    #              NOW CHECK PL TYPES IN THIS PARAMETER 
  16 4RT2    #              ------------------------------------ 
   4 4S#L    #   
  20 4SS=          LDN   4  2                   [POINT TO 2ND UNIT = 1ST PL TYPE 
  16 4T?W    Z60   CALL  5  UPAR                [GET NEXT UNIT   
   9 4TRG          LDX   5  ANUM(3)  
  18 4W?6          BNG   5  ZNP1                [J IF END OF THIS PARAM. 
  19 4WQQ          BZE   5  Z70                 [J IF NULL TO TRY NEXT UNIT  
  20 4X=B          PERCONA                      [CONVERT PL IF VALID TO T#,UT#   
  17 4XQ2          TESTREP  PARERR,ZENDE        [J IF COMMAND ERROR  
  15 4Y9L          LDX   5  ACOMMUNE1(2)        [GET T# & UT#
  11 4YP=    #SKI           K6ASSOC>199-199  
   9 4_8W          TRACE   5,ASSPL   
  14 4_NG          ANDN  5  #777                [GET UT# 
  20 5286          BNZ   5  ZCE3                [J IF NON-ZERO FOR COMMAND ERROR 
  10 52MQ          LDX   5  ACOMMUNE1(2) 
  14 537B          SRL   5  15                  [X5=T#   
   7 53M2          SMO      5
  20 546L          LDX   7  XPLT(1)             [GET TYPE-COMMAND VALIDATION WORD
  21 54L=          ANDX  7  6                   [IS PL TYPE VALID WITH THE COMMAND?  
  19 555W          BZE   7  ZCE4                [J IF NOT FOR COMMAND ERROR  
  10 55KG          MHUNT    3,CPB,CUNI   
  16 5656          FREECORE 3                   [RELEASE CPB/CUNI
   7 56JQ          SMO      5
  20 574B          LDX   5  XPLT(1)             [PERIPHERAL BIT FOR THIS TYPE OF 
  14 57J2          ANDN  5  -1                  [DEVICE  
  16 583L          ORX   6  5                   [SET PL BIT IN X6
  20 58H=    Z70   ADN   4  1                   [STEP X4 FOR NEXT UNIT OF PARAM  
   8 592W          BRN      Z60  
  17 59GG    ZNP1                               [END OF 3RD PARAM.   
  16 5=26          FREECORE 3                   [FREE CUNI BLOCK 
   8 5=FQ          LDN   0  #7777
  16 5=_B          ANDX  0  6                   [GET PL TYPE BITS
  19 5?F2          BNZ   0  ZNP1C               [J IF PL TYPE(S) SPECIFIED   
   8 5?YL          LDX   1  FX1  
   7 5#D=    ZNP1A LDX   0  6
   9 5#XW          ANDX  0  XDEFTY(1)
   8 5*CG          BNZ   0  ZNP1B
   8 5*X6          BUX   1  ZNP1A
   4 5BBQ    XX6 
   8 5BWB          CALL  7  XFREE
  18 5CB2          BXL   5  THREE(1),ZCE5       [NO PROPERTY PARAMETER   
   7 5CTL          LDN   5  2
   8 5D*=          BRN      XX7  
   9 5DSW    ZNP1B LDX   6  XDEFTY(1)
   5 5F#G    ZNP1C   
   9 5FS6          CALL  5  XPARAL   
   7 5G?Q          LDN   4  1
   7 5GRB          LDN   5  8
   7 5H?2    XX7   SBN   1  2
  21 5HQL          CALL  7  PARA                [SEARCH FOR THE PROPERTY PARAMETER   
   8 5J==          BPZ   0  XX6  
  20 5JPW          LDX   4  JPARNUM(2)          [MAKE A NOTE OF THE POSITION OF  
  21 5K9G          SMO      FX2                 [THIS PARAMETER INTHE CPB1CALAS BLOCK
   9 5KP6          ADS   4  AWORK4   
   7 5KXB ...#SKI  JSKI18<1$1
   7 5L5L ...      ISPROPN  7
   6 5L?W ...#SKI  JSKI18
  21 5LG6 ...      ISPROPN  XBRK,7              [CHECK PROP NAME IN CUNI BLOCK AND IF
  21 5LNB                                       [PRESENT IN SYSPROP PUT PROPNO IN X7 
  15 5M82                                       [ELSE X7=0.  
   9 5MML          MHUNT 3,CPB,CUNI  
  19 5N7=          FREECORE 3                   [FREE CUNI BLOCK (PROP NAME) 
   8 5NLW          BZE   7  ZENDE
  11 5P6G    #SKI           K6ASSOC>199-199  
   9 5PL6          TRACE   7,ASSPROP 
   4 5Q5Q    #   
  19 5Q=8 ...      BPZ   7  NOTCONS             [IF CONSOLE PROP,THROW OUT   
   8 5QBL ...      ORN   6  IVCP 
  20 5QH4 ...      ERN   6  IVCP                [*CP BIT,IN CASE IT WAS SET ABOVE
  21 5QMG ...NOTCONS                            [SEARCH FOR THE REMAINING PARAMETER  
   6 5QRY ...#SKI  JSKI27
   4 5QYB ...(   
  10 5R4S ...      LDX   0  CONTEXT(2)   
   7 5R9= ...      LDX   1  0
   7 5R*N ...      ANDN  0  1
  17 5RG6 ...      BNZ   0  XUSER               [J IF IN USER CONTEXT
   8 5RLJ ...      ANDN  1  #10  
  15 5RR2 ...      BNZ   1  XNU                 [MOP-NO-USER 
   4 5RXD ...)   
   6 5R_3 ...#UNS  G402  
   6 5S2L ...OPCOMMAND   
  20 5S4=          LDN   4  6                   [(WE ASSUME THAT THIS COMMAND HAS
  18 5SHW          SBX   4  AWORK4(2)           [THREE PARAMETRS ONLY).  
  11 5T3G ...      PARSORTX XBRK1,4,NOCLUS   
  10 5TH6          HUNTW    3,CPAR,JSCE  
  14 5W2Q          BNG   3  ZENDE               [ERROR   
   9 5WGB          LDEX  5  JSCEQ(3) 
   9 5X22          LDX   0  JSCEB(3) 
  15 5XFL          BNG   0  XREM              [J IF REMOTE   
  13 5X_=          LDEX  0  JSCEA(3)          [GEOGNO
   9 5YDW          STO   0  AWORK2(2)
  15 5YYG          LDCH  4  JSCEA(3)          [DEVICE TYPE   
  14 5_D6          LDX   3  JSCEP(3)          [DEVICE PTR
  14 5_XQ          BZE   4  XTR                 [J IF TR 
   7 62CB          SBN   4  3
   8 62X2          BZE   4  XTR  
   4 63BL    XEXY
  11 63W=          OUTPARAM 5,JSCER,CPAR,JSCE
   8 64*W          BRN      ZCE4 
  15 64TG    XTR   LADDP    3,3                 [APIA POINTER
   9 65*6          LDX   0  AWORK3(2)
  18 65SQ          BNG   0  XCANAS              [GO TO CANCEL ASSOCIATION
   9 66#B          LDN   4  ASSMAX   
   7 66S2          STOZ     5
  10 67?L    XLOC3 LDX   0  APERPROP(3)  
  17 67R=          BZE   0  XLOC1A              [NO ASSOCIATION HERE 
   7 68=W          ORX   0  6
  19 68QG          BXE   0  APERPROP(3),XERB    [ASSOCIATION ALREADY DEFINED 
   7 69=6          ERX   0  6
   9 69PQ          ANDX  0  HALFTOP  
  19 6=9B          BNZ   0  XLOC1               [DIFFERENT COMMAND PARAMETER 
  10 6=P2          LDX   0  APERPROP(3)  
   7 6?8L          ANDX  0  6
   8 6?N=          ANDN  0  #7777
  16 6#7W          BNZ   0  XERB                [ALREADY DEFINED 
  20 6#MG          BXE   7  APERPROP+1(3),XLOC2 [SAME PROPERTY NO-JUST ADD NEW 1 
   8 6*76          BRN      XLOC1
   5 6*LQ    XLOC1A  
  16 6B6B          LDN   5  1                   [FREE PAIR EXISTS
   5 6BL2    XLOC1   
   8 6C5L          BCT   4  XLOC4
  16 6CK=                                       [END OF ASS. AREA
  20 6D4W          BZE   5  XERC                [NO ROOM FOR MORE ASSOCIATIONS   
  18 6DJG          SBN   3  ASSMAX-1*2          [PUT IN FIRST EMXTY PAIR 
  18 6F46    XLOC6 LDX   0  APERPROP(3)         [SEARCH FOR A FREE PAIR  
   8 6FHQ          BNZ   0  XLOC5
  18 6G3B          STO   6  APERPROP(3)         [STORE  ASSOCIATION PAIR 
  10 6GH2          STO   7  APERPROP+1(3)
   9 6H2L          BRN      XLOC2A   
  17 6HG=    XLOC5 ADN   3  2                   [STEP TO NEXT PAIR   
   8 6H_W          BRN      XLOC6
   8 6JFG    XLOC4 BDX   3  XLOC3
  10 6J_6    XLOC2 ORS   6  APERPROP(3)  
   5 6KDQ    XLOC2A  
  21 6KYB          PROPAE   6,7,AWORK2(2),XGEO  [PUT IN SYSPROP IF PERMANENT PROPERTY
   8 6LD2          BRN      OKREP
  16 6LXL    XGEO  GEOERR   1,ASSOC             [SYSPROP SAYS NO 
  17 6MC=    XCANAS                             [CANCEL ASSOCIATIONS 
   9 6MWW          LDN   4  ASSMAX   
  10 6NBG    YLOC5 LDX   0  APERPROP(3)  
  17 6NW6          BZE   0  YLOC2               [LOOK AT NEXT PAIR   
  16 6P*Q          BXU   7  APERPROP+1(3),YLOC2 [GO TO NEXT PAIR 
   7 6PTB          ANDX  0  6
   9 6Q*2          ANDX  0  HALFTOP  
  19 6QSL          BZE   0  YLOC2               [DIFFERENT COMMAND PARAMETERS
   7 6R#=          LDX   0  6
  10 6RRW          ANDX  0  APERPROP(3)  
   8 6S?G          ANDN  0  #7777
  19 6SR6          ERS   0  APERPROP(3)         [CANCEL PART OF AE SPECIFIED 
  10 6T=Q          LDX   0  APERPROP(3)  
   8 6TQB          ANDN  0  #7777
   8 6W=2          BNZ   0  YLOC3
   8 6WPL    YLOCS BCT   4  YLOCY
  10 6X9=          STOZ     APERPROP(3)  
  17 6XNW          STOZ     APERPROP+1(3)       [CANCEL ASSOCIATION  
  17 6Y8G          BRN      YLOC3               [NOTHING TO MOVE UP  
  17 6YN6    YLOCY LDX   0  APERPROP+2(3)       [MOVE MP NEXT PAIR   
  10 6_7Q          STO   0  APERPROP(3)  
  10 6_MB          LDX   0  APERPROP+3(3)
  10 7272          STO   0  APERPROP+1(3)
   8 72LL          BDX   3  YLOCS
   5 736=    YLOC3   
  20 73KW          PROPCAE  6,7,AWORK2(2),OKREP [CANCEL FROM SYSPROP IF PERMANENT
   8 745G          BRN      OKREP
   8 74K6    YLOC2 BCT   4  YLOC4
  21 754Q          BRN      OKREP               [ASSOCIATION TO BE CANCELLED NON-EXIS
  13 75JB                                       [ENT 
   8 7642    YLOC4 BDX   3  YLOC5
   5 76HL    XREMA   
  20 773=          HUNTW    3,CPAR,JSCE         [IPB,UX,MX ONLY ALLOWED IF LINES 
  16 77GW          LDX   0  JSCEB(3)            [ETC SPECIFIED   
  16 782G          BPZ   0  XEXY                [J IF NOT REMOTE 
   4 78G6    XREM
  16 78_Q          LDX   0  AWORK3(2)           [GET ENTRY MARKER
  21 79FB          BNG   0  ZCNCL               [J IF ENTRY WAS FOR CANCELLATION (K2)
   8 79ML ...#SKI  IDFMARK8<1$1  
   9 79TW ...      MIDFAS   6,7,ZENDE
   7 7=46 ...#SKI  IDFMARK8  
  21 7==B ...      MIDFAS   XBRK1,6,7,ZENDE           [SET ASSOCIATION IN IDF OR J TO
  21 7=DL                                       [IF COMMAND ERROR FOUND ON PARAM 1   
  21 7=Y=    OKREP                              [NOW SEND AN 'OK' REPLY FOR THIS COM 
  20 7?CW          PARALAS                      [GET MSGE PARAM INTO ADATA/CSTORE
  19 7?XG          MHUNTW  3,ADATA,CSTORE       [A1=CHAR CT, A1+1 ON = PARAM 
  18 7#C6          OUTPARAM A1(3),A1+1,ADATA,CSTORE [PREPARE REPLY PARAM 
  10 7#WQ          MHUNTW  1,ADATA,CSTORE
   7 7*BB          FREECORE 1
   9 7*W2          LDXC  0  AWORK3(2)
   7 7B*L          ADN   1  0
  18 7BT=          LDX   5  XASSOCR(1)          [MSGE IDENTIFIER IN X5   
  15 7C#W          MONOUTX  5                   [SEND MSGE   
  15 7CSG          BRN      ZENDE               [J FOR ENDCOM
   5 7D#6    ZCNCL   
   8 7DGB ...#SKI  IDFMARK8<1$1  
   9 7DNL ...      CIDFAS   6,7,ZENDE
   7 7DWW ...#SKI  IDFMARK8  
  21 7F56 ...      CIDFAS   XBRK1,6,7,ZENDE           [CANCEL ASSOCIATION IN IDF OR J
  18 7F?B                                       [ZENDE IF ERROR FOUND.   
  17 7FR2          BRN      OKREP               [J TO SEND OK REPLY  
   4 7G=L    #   
   4 7GQ=    #   
   5 7H9W    Z2ASSOC 
  15 7HPG          NGS   2  AWORK3(2)           [MARK K2ENTRY
   8 7J96          BRN      Z10  
   6 7J9F ...#SKI  JSKI27
   4 7J9S ...(   
   6 7J9T ...#UNS  G402  
   4 7J9W ...(   
  12 7J9X ...XUSER JBS      OPCOMMAND,2,EXOPCOM  
   7 7J9Y ...      LDN   1  0
   4 7J9_ ...)   
   6 7J=2 ...#UNS  G402  
   4 7J=3 ...#SKI
   7 7J=7 ...XUSER LDN   1  0
   8 7J=G ...      PARANUMB 4,1  
   7 7J=T ...      SBN   4  3
  16 7J?8 ...      BPZ   4  XTM                [J IF >2 PARAMS   
  10 7J?H ...      LDX   0  CONTEXT(2)   
   8 7J?W ...      ANDN  0  #10  
  15 7J#9 ...      BZE   0  XUSRA               [J IF NOT MOP
   7 7J#J ...      MOPASAT  4
  11 7J#X ...XUSRA HUNTW    3,APERI,ASSOCS   
   8 7J*= ...      BPZ   3  XUSR 
  14 7J*K ...      SETNCORE ASSOCSLEN+1,3,APERI,ASSOCS   
   7 7J*Y ...      LDN   0  1
  15 7JB? ...      STO   0  A1(3)               [ONE ENTRY   
  17 7JBL ...XUSRB STO   6  A1+1(3)            [CM,PERIPHERAL TYPES  
  14 7JB_ ...      STO   7  A1+2(3)             [PROP NO 
   5 7JC# ...XUSRC   
   7 7JCM ...#SKI  JSKI18<1$1
   7 7JD2 ...      PROPASSO  
   6 7JD* ...#SKI  JSKI18
  18 7JDN ...      PROPASSO XBRKU               [FILL IN PROPERTY NAMES  
   7 7JF3 ...#SKI  JWPHASE2  
  18 7JFB ...      JLASSOCS                     [PUT ASSOCS IN JOBLIST   
   8 7JFP ...      BRN      OKREP
  21 7JG4 ...XUSR  SETUPCORE ALOGLEN(3),2,APERI,ASSOCS1  [TEMPORARY BLOCK(FOR BRKN)  
  11 7JGC ...      MHUNTW   3,APERI,ASSOCS   
  19 7JGQ ...      LDN   0  A1(3)               [KEEP A COPY OF LATEST AES   
   8 7JH5 ...      LDN   1  A1(2)
  10 7JHD ...      SMO      ALOGLEN(3)   
   7 7JHR ...      MOVE  0  0
   8 7JJ6 ...      LDX   5  A1(3)
  16 7JJF ...      LDX   2  3                   [SAVE ASSOCS PTR 
   9 7JJS ...XUSRG LDX   0  A1+1(3)  
   7 7JK7 ...      ERX   0  6
   9 7JKG ...      ANDX  0  HALFTOP  
  18 7JKT ...      BNZ   0  XUSRD               [J IF DIFFERENT COMMANDS 
   9 7JL8 ...      LDX   0  A1+2(3)  
   7 7JLH ...      ERX   0  7
  17 7JLW ...      BNZ   0  XUSRE               [J IF DIFF PROPNOS   
  15 7JM9 ...      ORS   6  A1+1(3)             [ADD NEW ONES
   8 7JMJ ...      BRN      OKREP
   9 7JMX ...XUSRE LDX   0  A1+1(3)  
   7 7JN= ...      ANDX  0  6
   8 7JNK ...      ANDN  0  #7777
  18 7JNY ...      BZE   0  XUSRD               [NO CHANGE TO THIS ENTRY 
  19 7JP? ...      ERS   0  A1+1(3)             [UNSET THE ONES THAT ARE...  
  17 7JPL ...      LDX   0  A1+1(3)             [...BEING REDEFINED  
   8 7JP_ ...      ANDN  0  #7777
  17 7JQ# ...      BZE   0  XUSRB               [NOTHING LEFT IN HERE
  16 7JQM ...XUSRD ADN   3  ASSOCSLEN           [GO TO NEXT ENTRY
   8 7JR2 ...      BCT   5  XUSRG
   8 7JR* ...      LDX   0  A1(2)
   9 7JRN ...      SBN   0  ASSMAX   
  16 7JS3 ...      BZE   0  XERC                [NO MORE ALLOWED 
   9 7JSB ...      ADN   0  1+ASSMAX 
  16 7JSP ...      STO   0  A1(2)                 [ADD NEW ENTRY 
   9 7JT4 ...      LDN   4  ASSOCSLEN
   7 7JTC ...      MPY   0  4
  17 7JTQ ...      LDN   4  1(1)               [NEW LENGTH OF BLOCK  
   7 7JW5 ...      LDX   3  2
   9 7JWD ...      ALTLENG  3,4,XFU  
  11 7JWR ...      MHUNTW   3,APERI,ASSOCS   
  10 7JX6 ...      SBN   4  ASSOCSLEN+1  
   7 7JXF ...      ADX   3  4
  17 7JXS ...      BRN      XUSRB               [GO TO PUT NAME IN   
  11 7JY7 ...XFU   MHUNTW   2,APERI,ASSOCS   
   7 7JYG ...      EXIT  1  0
  10 7JYT ...XBRKU MFREEW   APERI,ASSOCS 
  11 7J_8 ...      HUNTW    3,APERI,ASSOCS1  
   8 7J_H ...      BNG   3  XBRKA
  11 7J_W ...      NAME     3,APERI,ASSOCS   
   7 7K29 ...XBRKA COMBRKIN  
   4 7K2J ...)   
   4 7K2X ...#   
   4 7K3= ...XBRK
   6 7K3K ...#SKI  JSKI27
   4 7K3Y ...(   
  10 7K4? ...      LDX   0  CONTEXT(2)   
   7 7K4L ...      ANDN  0  1
  17 7K4_ ...      BNZ   0  XBRKA               [J IF USER-CONTEXT   
   4 7K5# ...)   
  10 7K5M ...XBRK1 GEOERR   1,BKINPROP   
  10 7K62 ...      MENDAREA 30,K100ASSOC 
   4 7K8B    #   
   4 7KN2    #END
   8 ____ ...34410563000100000000
