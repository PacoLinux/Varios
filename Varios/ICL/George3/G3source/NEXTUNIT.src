   8 228= ...#SEG  NEXTUNIT867   
   4 22BG ...#   
  16 22JQ ...#     COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982  
   4 22R2 ...#   
   7 22_=    #LIS  K0NEXTUNIT
   7 23DW          8HNEXTUNIT
  20 23YG    #              THIS SEGMENT IS USED BY SCEX TO GET NEXT AREA UNIT   
  12 24D6    #              INITIAL ENTRY AT K1  
  13 24XQ    #              SUBSEQUENT ENTRIES AT K2 
  16 25CB    #              K3 IS ENTRY TO GENERAL ERROR ROUTINE 
   4 25X2    #   
   4 26BL    #   
  12 26W=          SEGENTRY K1NEXTUNIT,QK1NEXT   
  12 27*W          SEGENTRY K2NEXTUNIT,QK2NEXT   
  12 27TG          SEGENTRY K3NEXTUNIT,QK3NEXT   
  12 28*6          SEGENTRY K4NEXTUNIT,QK4NEXT   
   4 28SQ    #   
   4 29#B    #   
  19 29DK ...XEWSUTC        +EWSUTC             [SU NOT SUITABLE-TRAPS CLOSED
  20 29G2 ...XEWSUNE        +EWSUNE             [SU NOT SUITABLE-NO EXOFILE ENTRY
  19 29HC ...XEWSTOINS      +EWSTOINS           [CANT EXTEND SECURE TO INS SU
  19 29JS ...XEWINSTOS      +EWINSTOS           [CANT EXTEND INS TO SECURE SU
   4 29ML ...#   
   4 29P3 ...#   
   4 29QD ...#   
  15 29S2    #                  BREAKIN ON GETTING AREA UNIT 
  16 29YN ...RPB   NGNC  5  1                   [IPB DOWN ENTRY  
  14 2=2= ...XBRK  LDN   5  0                   [BREAKIN 
  10 2=3S ...XBRK2 MHUNTW   3,EWDAS,GEXD 
  10 2?=W          MFINDEXOF 1,CEFN(3)   
   8 2?QG          LDCT  0  #40  
   9 2#=6          ANDX  0  EMARK(3) 
  15 2#PQ          BZE   0  XH7                 [J IF EXTEND 
  18 2*9B          FREECORE 1                   [ELSE SCRATCH,FREE EXOF  
  14 2*P2          BRN      XH5                 [AND END 
   7 2B8L    XH7   LDN   6  1
  18 2BN=          SBS   6  FIP+5(1)            [REMOVE DUMMY FILE AREA  
   9 2C7W          LDX   0  EMARK(3) 
  18 2CMG          BNG   0  XH5                 [J TO END IF SCRATCH FILE
  10 2D76          MFINDSU  1,HMUNIT(3)  
  21 2DLQ          SBS   6  CEXTEND(1)          [ELSE SUBTRACT 1 FROM COUNT OF EXTEND
  17 2F6B          RELST    HMUNIT(3)           [RELEASE HOME UNIT   
  10 2FL2    XH5   MHUNTW   3,EWDAS,GEXD 
   8 2G5L          LDCT  6  #100 
   9 2GK=          ANDX  6  EMARK(3) 
  15 2H4W          FREECORE 3                   [FREE GEXD   
  16 2H9J ...      BNZ   5  RPB1                [J IF NOT BREAKIN
  15 2H?6 ...XR5   BZE   6  XH6                 [J IF PERI   
  10 2H#N ...      MHUNTW   3,EWDAS,ASCOL
   7 2HB= ...      FREECORE 3
   7 2J46          COMBRKIN  
  14 2JHQ    XH6   STEPBACK                     [BREAKIN 
   7 2K3B          PROGBRKIN 
  15 2K7K ...RPB1  TWAIT    7,XR5               [WAIT FOR IPB
  15 2K92 ...      BNZ   6  XCOM                [J IF COMMAND
  12 2K=C ...      STEPBACK                     [
  17 2K?S ...      RUNPROG                      [PROG REPEATS PERI   
  18 2K*9 ...XCOM  MHUNTW   3,EWDAS,ASCOL       [SEARCH FOR SAVED''GEXD' 
  16 2K*K ...      SETUPCORE ALOGLEN(3),3,EWDAS,GEXD [SET UP GEXD
  10 2K*_ ...      MHUNTW   1,EWDAS,ASCOL
   8 2KB* ...      LDN   2  A1(1)
   8 2KBP ...      LDN   3  A1(3)
  10 2KC5 ...      SMO      ALOGLEN(1)   
  17 2KCF ...      MOVE  2  0                   [COPY ASCOL TO GEXD  
  18 2KD3 ...      ACROSS   SCEX,1              [AND TRY OL SCRATCH AGAIN
   4 2KH2    #   
   4 2L2L    #   
   4 2L3F ...#   
  19 2L3Q ...#           SUBROUTINE TO CHECK WHETHER THE USER HAS AN APPEND  
  14 2L43 ...#        TRAP TO EXOFILE ENTRY ICLSCAFILE.  
  18 2L4# ...#        EXIT+0 IF TRAPS OK AND EXOFILE TO BE EXTENDED IS   
  19 2L4K ...#              SECURE OR A SCRATCH FILE AND IF SU IS INSECURE   
  21 2L4W ...#              AND EXOFILE TO BE EXTENDED IS INSECURE OR A SCRATCH FILE.
  17 2L57 ...#        EXIT+1 IF INCOMPATABILITY OF ANY KIND WHEREUPON
  17 2L5D ...#              A MESSAGE IS OPTMESS'D GIVING REASONS.   
   4 2L5P ...#   
   5 2L62 ...XCHECK  
  16 2L6? ...      SBX   4  FX1                 [PRESERVE LINK   
   8 2L6F ...      SMO      FX2  
   9 2L6M ...      STO   5  AWORK1   
  20 2L6T ...      LDN   5  CAPPEND             [CHECK WHETHER USER HAS APPEND   
  18 2L76 ...      EXCHOFE  ICL,7,5,XCHK4A      [TRAPS TO E.E. ICLSCAFILE
  10 2L7C ...      MHUNTW   3,EWDAS,GEXD 
  16 2L7N ...      TESTREP2 OK,XCHK3,NOFILE,XCHK2,NOTRAP,XCHK1   
  10 2L7_ ...      GEOERR   1,XOFREPER   
   4 2L8= ...#   
  12 2L8H ...#        NOTRAP REPLY FROM EXCHOFE  
   5 2L8S ...XCHK1   
  19 2L95 ...      OPTMESS  XEWSUTC(1),7        [SU UNSUITABLE - TRAPS CLOSED
   8 2L9B ...      BRN      XCHK4
   4 2L9M ...#   
  12 2L9Y ...#       NOFILE REPLY FROM EXCHOFE   
   5 2L=9 ...XCHK2A  
  18 2L=G ...      OPTMESS  XEWSUNE(1),7        [SU UNSUITABLE - NO E.E. 
   8 2L=R ...      BRN    XCHK4  
   5 2L?4 ...XCHK2   
  17 2L?* ...      LDX   0  EDSQ                [LEVEL OF SECURITY   
  16 2L?L ...      BNG   0  XCHK2A              [J IF FULL IN USE
  21 2L?X ...      LDX   0  EXOTRAP(3)          [TRAPS RECORD PRESERVED SINCE OPEN   
  16 2L#8 ...      BNG   0  XCHK5               [J IF INSECURE   
  19 2L#F ...      OPTMESS  XEWSTOINS(1),7      [CANT EXTEND ONTO INSECURE SU
   8 2L#Q ...      BRN      XCHK4
   4 2L*3 ...#   
  11 2L*# ...#       OK REPLY FROM EXCHOFE   
   5 2L*K ...XCHK3   
  15 2L*W ...      LDX   0  EXOTRAP(3)          [TRAPS RECORD
  15 2LB7 ...      BPZ   0  XCHK3A              [J IF SECURE 
   9 2LBD ...      LDX   0  EMARK(3) 
  17 2LBP ...      BNG   0  XCHK3A              [J IF SCRATCH FILE   
  20 2LC2 ...      OPTMESS  XEWINSTOS(1),7      [CANT EXTEND INS ONTO SECURE SU  
   8 2LC3 ...      BRN      XCHK4
   5 2LC4 ...XCHK3A  
  21 2LC5 ...      EXCHOFE  ICL,7,,XCHK4A,AW    [D&T WRITTEN+ACCESSED FOR ICLSCAFILE 
   9 2LC6 ...      BRN      XCHK4A   
   5 2LC? ...XCHK4   
  15 2LCJ ...      ADN   4  1                   [TO EXIT+1   
   5 2LCT ...XCHK4A  
  10 2LD6 ...      MHUNTW   3,EWDAS,GEXD 
   5 2LDC ...XCHK5   
   8 2LDK ...      SMO      FX2  
   9 2LDR ...      LDX   5  AWORK1   
   8 2LD_ ...      ADX   4  FX1  
   7 2LF= ...      EXIT  4  0
   4 2LG=    #   
  21 2L_W    #              THIS ROUTINE FINDS,GETS ONLINE,AND LOCKS NEXT AREA UNIT, 
  20 2MFG    #              USING INFORMATION IN CSNCT,CSNTAB,CSNP.J TO UGOT WITH
  17 2M_6    #              ARUNIT SET IF OK.J TO XEND IF NO MORE.   
   5 2NDQ    QK1NEXT 
  17 2NYB    SUNIT                              [INITIAL ENTRY POINT 
  10 2PD2          MHUNTW   3,EWDAS,GEXD 
   9 2PXL          LDX   0  CSNCT(3) 
  18 2QC=          BNZ   0  SLIST               [J IF SEARCH LIST CASE   
   9 2QWW          LDX   7  CSNTAB(3)
  18 2RBG          BPZ   7  UT1                 [J UNLESS ALL-ONLINE CASE
  18 2RW6          STO   7  CSNCT(3)            [SET CSNCT -VE,FIRST PASS
  10 2S*Q          LDX   7  EOFUNIT(3)   
  21 2STB          BNZ   7  UT2                 [J IF EOFUNIT EXISTS,TO SEARCH IT 1ST
  19 2T*2          BRN      SONL                [ELSE GO ALL-ONLINE ROUTINE  
  20 2TSL    UT1   BNZ   7  UT2                 [J IF SPECIFIED SERN,TO GET IT   
  17 2W#=          LDX   7  EOFUNIT(3)          [ELSE TAKE EOFUNIT,  
  16 2WRW          BNZ   7  UT2                 [IF THERE IS ONE;
  19 2X?G          NGS   3  CSNCT(3)            [IF NOT,DO ALL-ONLINE ROUTINE
   8 2XR6          BRN      SONL 
   4 2XTY ...UT2 
  21 2Y6B ...      CALL  4  XCHECK              [CHECK FOR APPEND TRAPS TO,ICLSCAFIL 
  14 2Y98 ...      BRN      XCHOK1              [J IF OK 
  16 2Y#2 ...      LDN   5  2                   [CANTDO MARKER   
   8 2YBS ...      BRN      UT6  
   5 2YFL ...XCHOK1  
  17 2YM= ...      CALL  4  SERF                [FIND DUMMY FILE AREA
  18 2YQB          STO   7  FDCELLS(1)          [PUT SERN IN DUMMY AREA  
   8 2_=2    #SKI  K6SCEX>299-299
  11 2_PL          TRACE    CSNCT(3),FIRSTAR 
   8 329=    #SKI  K6SCEX>399-399
   9 32NW          TRACE    CSNTAB(3)
  17 338G          TXU   7  HMUNIT(3)           [J UNLESS HOME UNIT, 
   8 33N6          BCS      UT3  
  15 33WB ...      PLOCKSU  7,XBRK,RPB          [GET LOCK OUT
  10 34MB    UT4   MHUNTW   3,EWDAS,GEXD 
  17 3572          STO   7  ARUNIT(3)           [PUT SERN IN ARUNIT  
  14 35LL          BRN      UGOT                [AND END 
   8 366=    UT3   LDCT  6  #10  
  16 36KW          ANDX  6  EMARK(3)            [GET *4000 MARKER
  19 36T2 ...      USEST    7,UT5,,XBRK,RPB     [KEEP UNIT ONLINE IF ONLINE  
  10 36XB ...UT4A  MHUNTW   3,EWDAS,GEXD 
   9 36_Q ...      LDX   4  HMUNIT(3)
  17 3746 ...      BZE   4  UT4                 [J IF NO HOME UNIT   
  15 376G ...      UNLOCKSU 7                   [.ELSE UNLOCK
  17 378W ...      LOCKM    7,4,XBRK1,RPB2      [..AND LOCK IN ORDER 
   8 37?= ...      BRN      UT4  
   4 37*L ...#   
  10 37D2 ...#     IPB DOWN DURING LOCKM 
   4 37GB ...#   
  20 37JQ ...RPB2  LDX   4  ACOMMUNE9(2)        [UNIT WE WERE TRYING TO LOCK WHEN
  16 37M6 ...      NGNC  5  1                   [..IPB WENT DOWN 
   4 37PG ...#   
  10 37RW ...#     BREAKIN  DURING LOCKM 
   4 37W= ...#   
   7 37YL ...XBRK1 LDN   5  0
  15 3832 ...RPB3  RELST    7                   [RELEASE AREA
   7 385B ...      LDX   7  4
   8 387Q ...      BRN      XBRK2
  19 39HL    UT5   LDN   5  4                   [IF NOT ONLINE,  (REPBIT=N6) 
  17 3=3=          BNZ   6  UT6                 [J IF MODE *4000 SET 
  15 3=GW          LDN   5  2                   [REPBIT=NF   
  11 3?7N ...      MUSEST   7,UT6,XBRK,,RPB  
   8 3?BL ...      BRN      UT4A 
  17 3*DL    UT6   MHUNTW   3,EWDAS,GEXD        [CANTDO,OR *4000 SET,
  15 3*Y=          ORS   5  EMARK(3)            [SET REPBIT  
   8 3BCW          CALL  4  SERF 
  17 3BXG          STOZ     FDCELLS(1)          [CLEAR DUMMY FD CELL 
   9 3CC6          LDX   0  CSNCT(3) 
  17 3CWQ          BNG   0  SONL                [J IF ALL-ONLINE CASE
  15 3DBB          BRN      XEND                [ELSE GIVE UP
   4 3HRQ    #   
  18 3J?B    #                      S/R TO GIVE POINTER TO DUMMY FDCELL  
  10 3JR2    SERF  MFINDEXOF 1,CEFN(3)   
   9 3K=L    SERF1 LDEX  0  FIP+5(1) 
   7 3KQ=          SBN   0  1
   7 3L9W          SLL   0  1
   7 3LPG          ADX   1  0
   7 3M96          SLL   0  1
   7 3MNQ          ADX   1  0
   7 3N8B          EXIT  4  0
   4 3NN2    #   
  21 3P7L    #                  SEARCH LIST ROUTINE:LIST OF LENGTH [CSNCT] STARTS AT 
  21 3PM=    #                  CSNTAB.CSNP IS POINTER TO CURRENT ENTRY;IF 2ND PASS, 
   9 3Q6W    #                  B0=1.
   5 3QLG    QK2NEXT 
  10 3QQP ...      MHUNTW   3,EWDAS,GEXD 
   9 3QS6 ...      LDX   7  HMUNIT(3)
   9 3QTH ...      TXU   7  ARUNIT(3)
  16 3QWY ...      BCC      NHME                [J IF AREA=HOME  
  17 3QY* ...      BZE   7  NHME                [OR HOME NOEXISTANT  
  15 3Q_Q ...      UNLOCKSU 7                   [UNLOCK AREA 
   4 3R37 ...NHME
  20 3R66    NUNIT                              [SUBSEQUENT ENTRY FOR ALL CASES:-
  10 3RKQ          MHUNT    3,EWDAS,GEXD 
   8 3S5B          CALL  4  SERF 
  17 3SK2          STOZ     FDCELLS(1)          [CLEAR DUMMY FD CELL 
   9 3T4L          LDX   7  CSNCT(3) 
  17 3TJ=          BNG   7  NONL                [J IF ALL-ONLINE CASE
   9 3W3W          LDX   6  ARUNIT(3)
   9 3WHG          TXU   6  HMUNIT(3)
  20 3X36          BCC      UT7                 [J IF CURRENT AREA UNIT=HOME UNIT
  18 3XGQ          RELST    6                   [ELSE RELEASE AREA UNIT  
  10 3Y2B          MHUNTW   3,EWDAS,GEXD 
  17 3YG2    UT7   BZE   7  XEND                [J IF NO LIST,GIVE UP
   8 3Y_L    #SKI  K6SCEX>399-399
  11 3_F=          TRACE    CSNP(3),NUNITL   
  16 3_YW          LDX   5  CSNP(3)             [SEARCH LIST CASE
  16 42DG    UT8   ADN   5  1                   [UPDATE POINTER  
   7 42Y6          LDXC  0  5
   9 43CQ          ERX   0  CSNCT(3) 
  17 43XB          BNZ   0  UT9                 [J UNLESS END OF LIST
  16 44C2          BNG   5  XEND                [END IF 2ND PASS,
   8 44WL          LDCT  0  #10  
   9 45B=          ANDX  0  EMARK(3) 
  18 45TW          BZE   0  UT14                [J UNLESS MODE *4000 SET 
   7 46*G          LDN   0  4
  16 46T6          ORS   0  EMARK(3)            [SET REPBIT N6   
  14 47#Q          BRN      XEND                [AND END 
   4 47SB    UT14
   8 48#2    #SKI  K6SCEX>299-299
  10 48RL          TRACE    5,2ND PASS   
  17 49?=          LDCT  5  #400                [ELSE START 2ND PASS 
   7 49QW    UT9   SMO      5
  19 4==G          LDX   7  CSNTAB(3)           [GET NEXT SERN OUT OF LIST   
  17 4=Q6          BZE   7  UT8                 [J IF NONE FOR NEXT  
  21 4=W* ...      CALL  4  XCHECK              [CHECK FOR APPEND TRAPS TO ICLSCAFIL 
  14 4=XQ ...      BRN      XCHOK2              [JIF OK  
   7 4=_7 ...      SMO      5
  16 4?2J ...      STOZ     CSNTAB(3)           [ZEROISE ENTRY   
   7 4?3_ ...      LDN   4  2
   8 4?5B ...      BRN      UT12A
   5 4?6R ...XCHOK2  
   8 4?9Q          CALL  4  SERF 
  19 4?PB          STO   7  FDCELLS(1)          [PUT SERN IN DUMMY FD CELL   
   9 4#92          TXU   7  HMUNIT(3)
  18 4#NL          BCS      UT10                [J IF NOT HOME UNIT,ELSE 
  15 4#WW ...      PLOCKSU  7,XBRK,RPB          [GET LOCK OUT
  17 4*MW          BRN      UT11                [AND USE THIS UNIT   
  17 4B7D ...UT10  USEST    7,UT12,,XBRK,RPB    [KEEP ONLINE IF THERE
  10 4B#L ...UT11A MHUNTW   3,EWDAS,GEXD 
   9 4BFS ...      LDX   4  HMUNIT(3)
  17 4BM2 ...      BZE   4  UT11                [J IF NO HOME UNIT   
   7 4BS8 ...      UNLOCKSU 7
  16 4B_B ...      LOCKM    7,4,XBRK1,RPB2      [LOCK IF ORDER   
  13 4D62    UT11  MHUNTW   3,EWDAS,GEXD        [OK  
   7 4DKL          SMO      5
  17 4F5=          STOZ     CSNTAB(3)           [CLEAR TABLE ENTRY   
  15 4FJW          STO   5  CSNP(3)             [SAVE POINTER
  17 4G4G          STO   7  ARUNIT(3)           [SET NEW AREA UNIT   
  15 4GJ6          BRN      UGOT                [END,UNIT GOT
  15 4H3Q    UT12  LDN   4  0                   [NOT ONLINE, 
   5 4HB6 ...UT12A   
  16 4HHB          BPZ   5  UT13                [J IF FIRST PASS,
  15 4J32          LDN   4  2                   [(REPBIT=NF) 
  15 4JMS ...      MUSEST   7,UT13,XBRK,,RPB    [DEMAND UNIT 
   8 4JWQ ...      BRN      UT11A
  14 4LYQ    UT13  MHUNTW   3,EWDAS,GEXD        [CANTDO, 
  15 4MDB          ORS   4  EMARK(3)            [SET REPBIT  
   8 4MY2          CALL  4  SERF 
  17 4NCL          STOZ     FDCELLS(1)          [CLEAR DUMMY FD CELL 
  16 4NX=          BRN      UT8                 [AND TRY AGAIN   
  18 4TRG    #                  INITIAL ENTRY TO SEARCH LIST ROUTINE:-   
  17 4W?6    SLIST LDN   5  0                   [INITIALISE POINTER  
   8 4WQQ    #SKI  K6SCEX>299-299
  11 4X=B          TRACE    CSNCT(3),UNITLIST
  16 4XQ2          BRN      UT9                 [AND ORFT WE GO  
   4 4Y9L    #   
  21 4YP=    #                  SEARCH ALL ONLINE ROUTINE-SUBSEQUENT ENTRY.AREA UNIT 
  18 4_8W    #                  IS USED,FIND NEXT SUITABLE UNIT AFTER IT 
   4 4_NG    #   
   4 5286    NONL
   9 52MQ          LDX   0  CSNTAB(3)
  17 537B          BNG   0  US1                 [J IF INITIAL ENTRY  
  18 53M2    US2   MFINDSU  2,ARUNIT(3)         [FIND ASUC FOR AREA UNIT 
   9 546L    US3   LDX   5  HMUNIT(3)
   9 54L=          LDX   6  ARUNIT(3)
  14 555W          NGN   4  1                   [MARKER  
  21 55KG    US5   LDX   2  FPTR(2)             [SEARCH FORWARD DOWN CCB CHAIN FOR   
   8 5656    #SKI  K6SCEX>399-399
  11 56JQ          TRACE    BACK1(2),NEXT OL 
  17 574B          TXU   2  CXCC                [NEXT SUITABLE UNIT  
  17 57J2          BCC      US10                [J IF END OF CHAIN,  
   9 583L          LDX   7  BACK1(2) 
  16 58H=          BZE   7  US10                [OR NOT AN ASTUC 
  21 592W    US4   BNG   7  US5                 [J IF NOT SUITABLE,IE UNIT IS DEAD,  
   8 59GG          LDCT  0  #300 
   9 5=26          ANDX  0  ASMARK(2)
  19 5=FQ          BNZ   0  US5                 [OR DISENGAGED OR REDONED.   
  10 5=_B          TXU   7  EOFUNIT(3)   
  20 5?F2          BCC      US5                 [OR EOF UNIT (ALREADY SEARCHED), 
   8 5?YL          LDCT  0  #100 
   9 5#D=          ANDX  0  HSTUN(2) 
  17 5#XW          BNZ   0  US5                 [OR SCRATCH INHIBITED
  14 5*CG          CALL  4  SERF                [ELSE OK,
  20 5*X6          STO   7  FDCELLS(1)          [PUT SERIAL NR IN DUMMY FD CELL  
   8 5BBQ    #SKI  K6SCEX>399-399
   4 5BWB    (   
   7 5CB2          TXU   7  6
   8 5CTL          BCS      US11 
  10 5D*=          GEOERR   1,NEW=OLD?   
   4 5DSW    US11
   4 5F#G    )   
   7 5FS6          TXU   7  5
  17 5G?Q          BCS      US6                 [J IF NOT HOME UNIT  
  15 5GG2 ...      PLOCKSU  7,XBRK,RPB          [GET LOCK-OUT
  21 5GSM ...      CALL  4  XCHECK              [CHECK FOR APP TRAPS TO ICLSCAFILE   
   9 5GTY ...      BRN      XCHOK3   
   8 5GX9 ...      CALL  4  SERF 
  18 5GYG ...      STOZ     FDCELLS(1)          [ZEROISE DUMMY FD CELL   
   7 5G_R ...      LDN   0  2
  20 5H34 ...      ORS   0  EMARK(3)            [SET CD MARKER TO INDUCE *DA FAIL
   7 5H4* ...      UNLOCKSU 7
  10 5H5L ...      MHUNTW   3,EWDAS,GEXD 
   8 5H6X ...      MFINDSU  2,7  
   8 5H88 ...      BRN      US3  
   5 5H9F ...XCHOK3  
  15 5H?2          LDN   4  0                   [MARKER=OK   
   8 5HQL          BRN      US7  
   9 5J==    US6   LDX   4  CEFN(3)  
  11 5JPW ...      USESTX   7,US8,,XBRK,US8  
   9 5L8T ...      CALL  4  XCHECK   
   9 5L8Y ...      BRN      XCHOK4   
   9 5L93 ...      LDX   4  CEFN(3)  
   7 5L96 ...      UNLOCKSU 7
   8 5L99 ...      BRN      US8  
   5 5L9# ...XCHOK4  
  10 5L=X ...      MHUNTW   3,EWDAS,GEXD 
   9 5L?L ...      LDX   4  HMUNIT(3)
  17 5L#* ...      BZE   4  US10A               [J IF NO HOME UNIT   
   7 5L*4 ...      UNLOCKSU 7
  16 5L*R ...      LOCKM    7,4,XBRK4,RPB4      [LOCK IN ORDER   
   8 5LBG ...      BRN      US10A
  14 5LC9 ...RPB4  NGNC  4  1                   [IPB DOWN
  14 5LCY ...XBRK4 LDN   4  0                   [BREAKIN 
  10 5LDM ...      LDX   3  ACOMMUNE9(2) 
   7 5LFB ...      TXU   6  5
   8 5LG5 ...      BCC      US9A 
   8 5LGS ...      BZE   6  US9A 
  17 5LHH ...      RELST    6                   [RELEASE SAVED UNIT  
   7 5LJ= ...US9A  LDX   5  4
   7 5LJ_ ...      LDX   4  3
   8 5LKN ...      BRN      RPB3 
   5 5LLC ...US10A   
  15 5LNB          LDN   4  0                   [MARKER,OK   
   7 5M82    US10  TXU   6  5
  19 5MML          BCC      US9                 [J IF AREA UNIT=HOME UNIT,   
  17 5N7=    US7   BZE   6  US9                 [OR AREA UNIT ZERO,  
  17 5NLW          RELST    6                   [ELSE FREE AREA UNIT 
  10 5P6G    US9   MHUNTW   3,EWDAS,GEXD 
  14 5PL6          BNG   4  XEND                [J IF END
  17 5Q5Q          STO   7  ARUNIT(3)           [SAVE NEW AREA UNIT  
  16 5QKB          BZE   4  UGOT                [J IF OK TO EXIT 
  21 5R52          BRN      US2                 [ELSE CONTINUE SEARCH FROM NEW ARUNIT
  16 5RJL    US8   MFINDEXOF 1,4                [UNIT IN USE BY  
  15 5S4=          CALL  4  SERF1               [S-TRUSTED,  
  16 5SHW          STOZ     FDCELLS(1)          [CLEAR DUMMY CELL
  19 5T3G          LDN   4  1                   [SET MARKER=CONTINUE SEARCH  
   8 5TH6          BRN      US10 
   4 5W2Q    #   
  20 5WGB    #                  INITIAL ENTRY POINT TO SEARCH-ALL-ONLINE ROUTINE 
   7 5X22    SONL  LDN   0  1
  20 5XFL          ORS   0  EMARK(3)            [SET REPBIT NR IN CASE NOT FOUND 
   8 5X_=    #SKI  K6SCEX>299-299
  11 5YDW          TRACE    EOFUNIT(3),ALL-OL
  20 5YYG    US1   STOZ     CSNTAB(3)           [SET MARKER FOR SUBSEQUENT ENTRY 
  20 5_D6          LDN   2  BCCB                [SET POINTER TO START OF CHAIN   
  15 5_XQ          BRN      US3                 [BEGIN SEARCH
   4 62CB    #   
   4 62X2    #   
   4 63BL    #   
   4 63F9 ...UGOT
  21 63P2 ...      GETAREA  7,RPB5              [GET SCRATCH LIST FROM OTHER MACHINE 
  18 64*W          ACROSS   SCEXEND,1           [NEXT UNIT IS NOW ONLINE 
  10 64D_ ...#     IPB DOWN ON GETAREA   
   4 64G2 ...#   
  16 64H3 ...RPB5  UNLOCKSU 7                   [UNLOCK AREA UNIT
  10 64J4 ...      MHUNTW   3,EWDAS,GEXD 
   9 64K5 ...      LDX   6  HMUNIT(3)
  12 64L6 ...      TXU   6  ARUNIT(3)           [
  16 64M7 ...      BCC      RPB                 [J IF AREA=HOME  
  15 64N8 ...      RELST    7                   [RELST AREA  
  15 64P9 ...      BZE   6  RPB                 [J IF SCRATCH
  15 64Q= ...      UNLOCKSU 6                   [UNLOCK HOME 
   8 64R? ...      BRN      RPB  
   4 64TG    #   
   4 65*6    #   
   4 65SQ    #   
   5 66#B    QK3NEXT 
  18 66S2    XEND                               [GENERAL FAILURE ROUTINE 
  10 67?L          MHUNTW   3,EWDAS,GEXD 
  10 67R=          MFINDEXOF 1,CEFN(3)   
   8 68=W          LDCT  0  #40  
   9 68QG          ANDX  0  EMARK(3) 
  20 69=6          BZE   0  XN1                 [J UNLESS OPENING SCRATCH FILE   
   8 69PQ    #SKI  K6SCEX>399-399
   4 6=9B    (   
   9 6=P2          LDX   0  EMARK(3) 
   8 6?8L          BNG   0  XE1  
   9 6?N=    XE2   GEOERR   1,SCEX?? 
   4 6#7W    XE1 
   4 6#MG    )   
   7 6#RP ...NXN2  FREECORE 1
  10 6#T6 ...      HUNTW    1,EWDAS,ASCOL
   8 6#WH ...      BNG   1  XN2  
   8 6#XY ...      BRN      NXN2 
   9 6B6B    XN1   LDEX  0  FIP+5(1) 
   7 6BL2          SBN   0  1
   8 6C5L    #SKI  K6SCEX>399-399
   8 6CK=          BNG   0  XE2  
  20 6D4W          DEX   0  FIP+5(1)            [REMOVE DUMMY FILE AREA FROM EXOF
   9 6DJG          LDX   0  EMARK(3) 
  17 6F46          BNG   0  XN2                 [J IF SCRATCH FILE   
  13 6FHQ          MFINDSU  1,HMUNIT(3)         [ELSE
   7 6G3B          LDN   0  1
  20 6GH2          SBS   0  CEXTEND(1)          [SUBTRACT 1 FROM COUNT OF EXTENDS
   8 6H2L    #SKI  K6SCEX>399-399
   4 6HG=    (   
  10 6H_W          LDX   0  CEXTEND(1)   
   8 6JFG          BNG   0  XE2  
   4 6J_6    )   
  18 6KDQ          RELST    HMUNIT(3)           [AND RELEASE HOME UNIT   
  16 6KYB    XN2   EXMESS   ALL                 [OUTPUT MESSAGES 
  10 6LD2          MHUNTW   3,EWDAS,GEXD 
   8 6LXL          LDCT  0  #100 
   9 6MC=          ANDX  0  EMARK(3) 
  15 6MWW          BZE   0  XN3                 [J IF PERI   
  16 6NBG          FREECORE 3                   [ELSE FREE GEXD  
  17 6NW6          COMERR   EWSCRX              [AND COMMAND ERROR   
   4 6P*Q    #   
   7 6PTB    SEVEN #777777   
   4 6Q*2    #   
  18 6QSL    XN3   NGN   4  1                   [GET REPLY,FROM REPBITS  
   9 6R#=          LDX   0  EMARK(3) 
  16 6RRW          SRC   0  6                   [B18-23 OF EMARK 
  20 6S?G          LDX   7  BAVAIL(3)           [REPLY IS LARGEST AVAILABLE AREA 
   8 6SR6          SMO      FX1  
   8 6T=Q          ANDX  7  SEVEN
   8 6TQB          ORX   7  GSIGN
  17 6W=2          BNG   0  XN4                 [IF B18=1 (NO ROOM)  
  21 6WPL    XN5   SLC   0  1                   [ELSE SHIFT TO FIND MOST SIGNIFICANT 
  15 6X9=          ADN   4  1                   [REPLY BIT   
   8 6XNW          BPZ   0  XN5  
   8 6Y8G    #SKI  K6SCEX>399-399
   4 6YN6    (   
   7 6_7Q          SBN   4  5
   8 6_MB          BPZ   4  XE2  
   7 7272          ADN   4  5
   4 72LL    )   
  13 736=          SMO      4                   [AND 
  20 73KW          LDX   7  RTAB(1)             [GET CORRESPONDING REPLY IN X7   
  18 745G    XN4   LDX   4  CNR(3)              [CONTROL AREA ADDRESS,   
  14 74K6          LDX   5  EXCHNL(3)           [CHANNEL,
  15 754Q          FREECORE 3                   [FREE GEXD   
  17 75JB    #              ENTRY POINT FROM SCEX FOR EARLY FAILURES 
   5 7642    QK4NEXT 
  10 768N ...XN8   HUNTW    1,EWDAS,ASCOL
   8 76== ...      BNG   1  XN8B 
   7 76?S ...      FREECORE 1
   7 76*B ...XN8B  ADN   4  1
  14 773=          GETWORD  4,2,WRITE           [GET IT  
   8 77GW    #SKI  K6SCEX>399-399
   4 782G    (   
   9 78G6          TESTREP  OK,XN8A  
  19 78_Q          GEOERR   1,EXFAILRP          [IF BAD REPLY FROM GETWORD   
   4 79FB    XN8A
   4 79_2    )   
  15 7=DL          STO   7  0(2)                [SET REPLY   
   8 7=Y=    #SKI  K6SCEX>299-299
   9 7?CW          TRACE    7,SCEXREP
   7 7?XG          WORDFIN   
   7 7#C6          SLL   7  3
  16 7#WQ          BNG   7  XN9                 [J IF FAIL ,ELSE 
  16 7*BB          RUNPROG                      [RESTART PROGRAM 
  14 7*W2    #                      TABLE OF FAIL REPLIES
  18 7B*L    RTAB           +0.2                [NO ROOM IN SCA REPBIT=SC
  18 7BT=                   #440:0              [FAIL,TRANSFER FAIL ^ =F 
  18 7C#W                   +6.2                [UNIT NOT ONLINE    ^ =N6
  18 7CSG                   #440:0              [FAIL,UNIT CANTDOED ^ =NF
  18 7D#6                   +1.2                [NO ONLINE UNITS    ^ =NR
   4 7DRQ    #   
   7 7F?B ...XN9   LDCT  4  6
   7 7FR2 ...      ADX   4  5
  11 7G=L ...      OUTPACK  4,1,PERINAME,XN9 
  15 7N66          ILLEGAL  PERFAIL             [FAIL PROGRAM
   4 7NKQ    #   
   4 7P5B    #   
   4 7PK2    #   
  11 7Q4L          MENDAREA 50,K99NEXTUNIT   
   4 7QJ=    #END
   8 ____ ...51306576000200000000
