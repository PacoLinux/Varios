  14 22FL    #LIS  K0FINISH>K0ALLGEO>K0GREATGEO>K0COMMAND
  12 22PD ...      SEG FINISH,867,SECTION CENT   
   4 22_= ...[   
   4 2394 ...[   
  16 23DW ...[  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982 
   4 23NN ...[   
   8 23YG    [ THE ENTRY POINTS  
  19 24D6    [     THESE ENTRY POINTS MUST BE KEPT FIXED WITHIN THE SEGMENT  
  11 24XQ          SEGENTRY K1FINISH,QENTRY1 
  11 25CB          SEGENTRY K2FINISH,QENTRY2 
   4 25X2    [   
  13 26BL          FSHENTRY K3FINISH,,QENTRY3,QENTRY3
  13 26W=          FSHENTRY K4FINISH,,QENTRY4,QENTRY4
  13 27*W          FSHENTRY K5FINISH,,QENTRY5,QENTRY5
  13 27TG          FSHENTRY K7FINISH,QENTRY7,,QENTRY7
  11 28*6          SEGENTRY K8FINISH,NOPAR   
  11 28SQ          SEGENTRY K9FINISH,PHAST   
  11 29#B          SEGENTRY K10FINISH,WAIT   
  14 29S2          FSHENTRY K11FINISH,,QENTRY11,QENTRY11 
  14 2=?L          FSHENTRY K12FINISH,,QENTRY12,QENTRY12 
  14 2=R=          FSHENTRY K13FINISH,,QENTRY13,QENTRY13 
   4 2?=W    [   
  18 2?QG    [     SEGENTRY K14FINISH IS MENDING POINT FOR MACRO FNDELE  
   4 2#=6    [   
   4 2#PQ    [   
  21 2*9B    #     THIS SEGMENT IMPLEMENTS THE FINISH COMMAND (SURPRISE! SURPRISE!)  
  10 2*P2    #     THERE ARE 3 CASES:-   
   9 2B8L    #     1) PARAMETER =NOW 
  16 2BN=    #        ALL JOBS ARE ABANDONED ,ALL LF/IN STOPPED  
   9 2C7W    #     2) PARAMETER =TIME
  20 2CMG    #        ALL JOBS ABANDONED AFTER SPECIFIED TIME. NO MORE JOBS MADE 
  18 2D76    #        FULLY STARTED DURING TIME, LF/IN ALLOWED TO FINISH 
  10 2DLQ    #     3) PARAMETER MISSING  
  13 2F6B    #        NO MORE JOBS MADE FULLY STARTED
   4 2FL2    #   
  17 2G5L    #     IN ALL CASES NO NEW JOBS ARE ACCEPTED AFTER FINISH
   4 2GK=    #   
  20 2H4W    #     WORD IN FIXED USED AS MARKER TO OTHER ACTIVITIES  ON STATE OK 
   9 2HJG    #     FINISH  COMMAND :-
  13 2J46    #       B0=1   FINISH COMMAND BEEN GIVEN
   9 2JHQ    #       B1=1   TIME UP  
  10 2K3B    #       B2=1   FINISH NOW   
  17 2KH2    #       B3=1   ALL BUT SYSTEM ISSUED JOBS HAVE FINISHED 
  12 2L2L    #       B4-23  TIME LEFT IN CASE 2  
   4 2LG=    #   
   6 2NDQ    XIN   4HIN  
   6 2NYB    NOW   4HNOW 
   6 2PD2    TIME  4HSECS
   6 2PXL          4HMINS
   6 2QC=          4HHRS 
   6 2QWW    TLIMH +73   
   6 2RBG    TLIMM +4321 
   7 2RW6    TLIMS +259201   
  10 2S*Q    XBACTRING      +BACT+ACTRING
  21 2STB    STOPPED        #52700002           [STOPPED FOR ABANDON BITS IN JOBEVENT
   4 2T*2    [   
   4 2TSL    #   
   4 2X?G    #   
  21 2XR6    [ S/R TO UNSET THE 'CURRENT FINISH ACTIVITY' BIT.  FIRST IT TEST THAT   
   9 2Y=Q    [     THE BIT IS SET.   
   8 2YQB    UNSET STO   0  GL2  
   8 2_=2          LDCT  0  #20  
   9 2_PL          ANDX  0  EXT+11(2)
  19 329=          BNZ   0  X1                  [ IF ZERO, THE BIT IS UNSET  
   9 32NW          GEOERR   0,OOPS!  
   9 338G    X1    ERS   0  EXT+11(2)
   8 33N6          BRN      (GL2)
   4 347Q    [   
  20 34MB    [ S/R TO STOP A FINISH ACTIVITY BY FORCING AN ENDCOM.  IT LOADS THE 
  20 3572    [     ENDCOM LINK INTO THE ACTIVITY BLOCK AND THEN PUTS THE BLOCK ON
  21 35LL    [     THE ACTIVITY LIST SO THAT IT CAN END ITSELF TIDILY.  THIS ALLOWS  
  19 366=    [     FINISH TO BE ISSUED FROM A MACRO.  THE S/R ALSO UNSETS THE
  12 36KW    [     'CURRENT FINISH ACTIVITY' BIT 
   6 375G    NL    ENDCOM
   5 37K6    STOPFIN 
   8 384Q          STO   1  GL1  
   7 38JB          LDX   2  3
   8 3942          LDX   1  FX1  
  20 39HL          LINKSET  2,NL+1(1)           [ PUTS ENDCOM LINK IN ACTIVITY   
   8 3=3=          CALL  0  UNSET
  20 3=GW          FPUT                         [ PUTS ACTIVITY ON ACTIVITY LIST 
   8 3?2G          BRN      (GL1)
   4 3?_Q    #   
   4 3#FB    #   
  20 3#_2    #     S/R TO STOP A JOB. CHECKS WHY NOT STOPPABLE (IF THIS IS THE   
  19 3*DL    #     CASE) AND UPDATES X7 EVERY TIME FINDS A JOB NOT CURRENTLY 
   7 3*Y=    #     STOPPABLE 
  19 3BCW    STOP2 SBX   5  FX1                 [STOP JOB FOR WHENEVER ONLY  
  10 3BXG          DOWN     PROCONTX,7   
  16 3CC6          BRN      WHY                 [NOT STOPPABLE   
  14 3CWQ          BRN      STOP3               [STOPPED 
  14 3DBB    STOP  SBX   5  FX1                 [STOP JOB
  10 3DW2          DOWN     PROCONTX,2   
  16 3F*L          BRN      WHY                 [NOT STOPPABLE   
  14 3FT=          BRN      XGER                [SYS ISS 
   5 3G#W    STOP3   
  13 3GSG          JOBLOCK  6,3                 [OK  
   8 3H#6          BNG   3  TOURA
   8 3HRQ    XIT   ADX   5  FX1  
  17 3J?B          EXIT  5  1                   [EXIT +1 IF STOPPED  
  15 3JR2    WHY   JOBLOCK  6,3                 [CHECK WHY   
   9 3K=L          BPZ   3  PHOUND   
   5 3KQ=    TOURA   
   8 3L9W          LDN   3  BJOBQ
   9 3LPG    TOUR  LDX   3  FPTR(3)  
   8 3M96          TXU   3  CXJO 
   8 3MNQ          BCC      REND 
  11 3N8B          BXGE  6  JOBNUM(3),TOUR   
  21 3NN2    REND  LDX   3  BPTR(3)             [THUS RIGHT ADDRESS WILL BE PICKED   
  18 3P7L          BRN      XIT                 [UP NEXT TIME THRU LOOP  
   5 3PM=    PHOUND  
  12 3Q6W          HLSAV 3,SET,SAVING,JOBQ,XINHIB
   8 3QLG          FCAJO    2,3  
   9 3R66    #SKI  K6FINISH>199-199  
  12 3RKQ          TRACE    JOBEVENTS(2),YNOTSTOP
  10 3S5B          LDX   0  JOBEVENTS(2) 
   8 3SK2          LDX   2  FX2  
  15 3T4L          BPZ   0  XINHIB              [J IF INHIBIT
  19 3TJ=          ANDX  0  STOPPED(1)          [J IF ALREADY BEING ABANDONED
   8 3W3W          BNZ   0  XIT  
   5 3WHG    XINHIB  
   8 3X36          ADX   5  FX1  
  18 3XGQ          EXIT  5  0                   [EXIT +0 IF NOT STOPPABLE
  14 3Y2B    #     SUBROUTINE TO CHECK BG JOBS: LINK X5  
  15 3YG2    #     EXITS  0 IF STILL SOME LEFT, WITH X7 -VE  
  15 3Y_L    #     EXITS  1 IF DONE, WITH X7= NO OF MOP JOBS 
   5 3_F=    WHATBG  
   9 44WL          HUNTMISB 3,JWELL  
  11 45B=          LDEX  7  JOBENTS+JOBCTS(3)
   7 45TW          NGX   7  7
   8 47SB          BZE   7  XIT1A
  20 4J32          LDN   3  BHLSQ               [NOW TAKE OFF NO OF SAVED JOBS!  
  20 4JGL    SEARCH                             [AS THESE WILL STAY OVER A SYSTEM
  18 4K2=          LDX   3  FPTR(3)             [BREAK AND BE RESTARTED  
   8 4KFW          TXU   3  CXHL 
  17 4K_G          BCC      QENDX               [J IF END OF QUEUE   
  13 4LF6          HLSAV    3,CLEAR,SAVED,HLSQ,SEARCH
  21 4LYQ          ADN   7  1                   [REDUCE (NEGATIVE) COUNT IF SAVED.   
   9 4MDB          BRN      SEARCH   
   5 4MY2    QENDX   
  18 4NCL          BZE   7  XIT1A               [ZERO IF ONLY MOP LEFT   
   9 4NX=    #SKI  K6FINISH>199-199  
  10 4PBW          TRACE    7,JOBSLEFT   
   7 4PWG          EXIT  5  0
   9 4S#L    XIT1A HUNTMISB 3,JWELL  
  13 4SS=          LDEX  7  JWENTRY+JOBENTS+JOBCTS(3)
   7 4Y9L    XIT1  EXIT  5  1
   4 5SHW    #   
  17 5W2Q    #     SUBROUTINE TO FIND CPA FOR CANCEL OR SECOND FINISH
   5 5WGB    SEEKCPA 
  10 5X22          LDN   2  BACT+ACTRING 
   5 5XFL    XLOOP9  
   9 5X_=          LDX   2  FPTR(2)  
  10 5YDW          TXU   2  XBACTRING(1) 
   8 5YYG          BCC      QEND9
   7 5_D6          LDX   3  2
  18 5_XQ          SBN   3  ACTRING             [ADJUST TO START OF BLOCK
   9 62CB          LDCH  0  ATYPE(3) 
   9 62X2          SBN   0  CPAT/64  
   9 63BL          BNZ   0  XLOOP9   
   8 63W=          LDCT  0  #20  
  16 64*W          ANDX  0  EXT+11(3)           [TEST IF FINISH  
  14 64TG          BZE   0  XLOOP9              [J IF NOT
  14 65*6          LDX   2  FX2                 [RESET X2
   7 65SQ          EXIT  6  1
  17 66#B    QEND9 EXIT  6  0                   [EXIT 0 IF NOT FOUND 
   4 66S2    #   
   4 67R=    #   
   5 68=W    QENTRY1 
   8 68QG    #SKI  K6FINISH>99-99
  10 69=6          TRACE    FINISH,FINISH
   9 6=P2          LDX   6  FINISH   
  18 6?8L          BPZ   6  PHIRST              [J IF NOT A SECOND FINISH
   8 6?N=          LDCT  0  #200 
   7 6#7W          ANDX  0  6
  16 6#MG          BNZ   0  TOOLATE             [J IF PAST TIME  
   8 6*76          BRN     NOMARK
   5 6*LQ    PHIRST  
   8 6B6B          LDCT  0  #20  
  19 6BL2          ORS   0  EXT+11(2)           [SET FINISH ACTIVITY MARKER  
   5 6DJG    NOMARK  
   5 6G3B    NOTWRMC 
  14 6GH2          LDN   4  0                   [SET ZERO
  18 6H2L          LDN   7  0                   [MARKER FOR NO PARAMETER 
   7 6HG=          PARAPASS  
  10 6H_W          MHUNT    3,CPB,CUNI   
   9 6JFG          LDX   0  ANUM(3)  
  16 6J_6          BNG   0  SLOW                [J IF NO PARAM   
  16 6KDQ          BZE   0  NULL                [ERROR IF NULL   
  16 6KYB          NGN   7  1                   [MARKER FOR NOW  
   9 6LD2          LDX   0  APARA(3) 
   9 6LXL          TXU   0  NOW(1)   
  14 6MC=          BCC      SLOW                [J IF NOW
  16 6S?G          LDN   7  1                   [MARKER FOR TIME 
  15 6SR6          PARSPLIT 5                   [CONVERT TIME
  21 6T=Q          STO   5  AWORK2(2)           [SAVE MARKER ON WHETHER UNITS SPEC   
  10 6TQB          LDX   3  ACOMMUNE1(2) 
  19 6W=2          BZE   5  MINS                [J IF NO CHAR STRING= MINS   
  16 6WPL ...      BPZ   5  XEND                [ERROR OTHERWISE 
   9 6X9=          TXU   5  TIME(1)  
   8 6XNW          BCC      SECS 
   9 6Y8G          TXU   5  TIME+1(1)
   8 6YN6          BCC      MINS 
   9 6_7Q          TXU   5  TIME+2(1)
   8 6_MB          BCS      WOOPS
   8 7272          LDN   5  60   
   9 72LL          TXL   3  TLIMH(1) 
  16 736=          BCC      YUK                 [TOO LONG A TIME 
   7 73KW          MPY   3  5
   7 745G          LDX   3  4
   8 74K6    MINS  LDN   5  60   
   9 754Q          TXL   3  TLIMM(1) 
   8 75JB          BCC      YUK  
   7 7642          MPY   3  5
   8 76HL          BRN      SECS1
   7 773=    SECS  LDX   4  3
  16 77GW          BZE   4  NOTIM               [J IF TIME ZERO  
   9 782G          TXL   3  TLIMS(1) 
   8 78G6          BCC      YUK  
   7 78_Q          STOZ     3
  19 79FB          ADN   4  FONINTP-1           [IN CASE TIME<FONINTP SECONDS
  16 79_2    SECS1 BZE   4  NOTIM               [J IF TIME ZERO  
   9 7=DL          LDN   5  FONINTP  
   7 7=Y=          DVR   3  5
   5 7?CW    NOTIM   
   4 7?XG    SLOW
  17 7MLG          BPZ   7  SOON                [J IF NOT NOW CASE   
   8 7N66          OUTMESS  HNOW 
  15 7NKQ          OUTPAR   TIMENOW             [ O/P TIME   
  14 7RHG          INFORM   3,HWARN,1           [TELL OPS
  17 7WYW    SOON  BNG   6  SECOND              [J IF SECOND FINISH  
  17 7_WL          STO   4  FINISH              [STORE TIME OR ZERO  
   8 82B=          LDCT  0  #400 
  16 82TW          ORS   0  FINISH              [SET FINISH BIT  
   8 83*G          LDCT  0  #600 
  17 83T6          ORS   0  HLSWAIT             [SET FINISH BIT AND  
  17 84#Q          LDCT  0  #200                [ENSURE CANCEL FINISH
  15 84SB          ERS   0  HLSWAIT             [BIT UNSET   
  16 8JDB    #     THIS SECTION DOES STOPJOB FOR WHENEVER FINISH 
   4 8JY2    #   
  17 8KCL    #     MUST BE DONE IN M/C 'B' FOR 'B' MACHINE JOBS AS   
  17 8KX=    #     BITS 14 & 17 MAY ONLY BE SET IN THE 'B' M/C CPAT  
   4 8LBW    #   
   4 8LWG    XTH 
   8 8QRG          LDN   3  BJOBQ
   5 8R?6    XLOOP8  
   9 8RQQ          LDX   3  FPTR(3)  
   8 8S=B          TXU   3  CXJO 
   8 8SQ2          BCC      QEND8
   9 8Y7B          LDX   6  JOBNUM(3)
  12 9356          HLSAV 3,SET,SAVING,JOBQ,XLOOP8
   8 93JQ          FCAJO    2,3  
  15 944B    #     B14 OF EXT+11=1 => A WE FN HAS BEEN GIVEN 
  21 94J2    #     B17 OF EXT+11=1 => RUN OF END OF JD FILE, IF SET DO NOT DO STOPJOB
  12 953L    #     FOR WE FN,EVEN IF B14 IS SET  
  20 95H=          LDN   0  #1100               [TEST IF WE FN OR WAITING: E.O.F 
  21 962W          ANDX  0  EXT+11(2)           [THE END OF ITS JDF FILE,IT CAN HAVE 
  19 96GG          BZE   0  XLOOP8              [IF NO WE FN OR WAITING E.O.F
  18 9726          ERN   0  #1000               [CLEAR OUT WE FINISH BIT 
   9 97FQ          BNZ   0  XLOOP8   
  21 97_B                                       [IN CASE WE WANT TO ABANDON IT LATER 
  17 98F2          BPZ   7  STOP4               [J IF NOT NOW CASE   
  12 98YL          SPRIVTEST    JSISSUE,3,XLOOP8 
   8 99D=    STOP4 CALL  5  STOP2
   9 99XW          BRN      XLOOP8   
   9 9=CG          BRN      XLOOP8   
   5 9=X6    QEND8   
  18 9B#G          FON      #33                 [WAKE HLS ACT (IF ASLEEP)
  16 9BS6          BZE   7  NOPAR               [J IF NO PARAM   
  15 9C?Q          BPZ   7  WAIT                [J IF TIME   
   5 9CRB    PHAST   
   8 9MKB          LDCT  0  #300 
  16 9N52          ORS   0  FINISH              [SET OTHER BITS  
   6 9R2Q    SNOTRETINA  
  21 9RGB    #     THIS SECTION ATTEMPTS TO ABANDON ALL USER JOBS IN THE JOB QUEUE   
  21 9S22    # THIS LOOP WILL WAIT FOR NOUSER INPUT JOBS WHILE THERE ARE NON-SAVED   
  20 9SFL    # USER-JOBS (ACCORDING TO JOBCOUNT) IN CASE THESE INPUTS TURN OUT TO
  21 9S_=    # BE JOBS - BUT NOT IN THE NOUSER-NOJOBBLOCL CASE, WHERE IT MIGHT FAIL. 
  21 9TDW    # IN BOTH CASES IT NEEDS REWRITING - PARTLY BECAUSE OF THE WAY IT DECIDE
   8 9TYG    # TO RERUN THE LOOP.
   7 9WD6    XCOM  LDN   4  0
   8 9WXQ          LDN   3  BJOBQ
   9 9XCB    XLOOP LDX   3  FPTR(3)  
   8 9XX2          TXU   3  CXJO 
  18 9YBL          BCC      QEND                [J IF END OF JOB QUEUE   
   9 =4?L          LDX   6  JOBNUM(3)
  16 =?L2          SPRIVJUMP JSISSUE,3,XLOOP    [J IF SYS ISSUED 
  14 =#5L    STOP1 CALL  5  STOP                [STOP JOB
  21 =#K=          ADN   4  1                   [INCREMENT COUNT OF UNSUCCESFUL ABAND
   8 =*4W          BRN      XLOOP
   9 =*JG    QEND  CALL  5  WHATBG   
   8 =B46          BRN      WAITF
   8 =BHQ          BZE   7  XDONE
  17 =C3B    WAITF COOR3    #62                 [WAIT FOR A FONINT   
  21 =CH2          BNZ   4  XCOM                [REPEAT LOOP IF SOME ABANS UNSUCCESSF
   8 =D2L          BRN     QEND  
   5 =DG=    XDONE   
  15 =FFG          LONGON   #145           [FOR MOP WELL JOBS
  16 =F_6    #     NOW WE CHECK LF AND IN AND THEN SYS ISS JOBS  
   4 =GDQ    #   
  21 =GYB    #     THIS NEXT SECTION CAN BE REWRITTEN TO BE MUCH MORE EFFICIENT AS   
  13 =HD2    #     SOON AS BOTH IDENTIFIERS ARE SET  
   4 =HXL    #   
   7 =JC=          LDN   4  0
  10 =QQB    QEND1 LDN   3  BACT+ACTRING 
   5 =R=2    XLOOP2  
  19 =RPL          LDX   3  FPTR(3)             [SEARCH ACTIVITY RING FOR LFS
  10 =S9=          TXU   3  XBACTRING(1) 
   8 =SNW          BCC      QEND2
   7 =T8G          LDX   2  3
   9 =TN6          SBN   2  ACTRING  
   9 =W7Q          LDCH  0  ATYPE(2) 
   9 =WMB          SBN   0  ACTLF/64 
  10 =Y6=          SBN   0  CPAT-ACTLF/64
   9 =YKW          BNZ   0  XLOOP2   
   8 =_5G          LDN   0  #2000
   9 =_K6          ANDX  0  EXT+11(2)
   9 ?24Q          BZE   0  XLOOP2   
   5 ?6FB    XADDONE 
   8 ?8CW          BZE   0  QEND2
  16 ?8XG          ADN   4  1                   [UPDATE CT IF LF 
   5 ?9C6    QEND2   
   8 ?H7L          LFNOTFIN QEND4
   5 ?HM=    XCHEKIN 
   8 ?J6W          BZE   4  XLFIN
   5 ?JLG    QEND4   
   9 ?K66    #SKI  K6FINISH>199-199  
  10 ?KKQ          TRACE    4,LFINDONE   
  17 ?L5B          COOR3    #62                 [WAIT A FEW SECONDS  
  15 ?LK2          BRN      XDONE               [TRY AGAIN   
   5 ?M4L    XLFIN   
  19 ?Q2B          MONOUT   HDONE               [TELL OPS ALL USER JOBS DONE 
   5 #3QW    QENTRY5 
   8 #4=G          LDCT  0  #40  
  16 #4Q6          ORS   0  FINISH              [SET FINISHED BIT
  18 #8M6    #    'FINISHED' HAS OCCURRED SO WE MUST TELL SYS.ISSUED JOBS
   5 #96Q    SYSIS   
   7 #9LB          LDN   7  1
   5 #=62    REPEAT  
   8 #=KL          LDN   3  BJOBQ
  19 #?5=    XLOOP3                             [NOW CHECK SYS ISSUED JOBS   
   9 #?JW          LDX   3  FPTR(3)  
   8 ##4G          TXU   3  CXJO 
   8 ##J6          BCC      QEND3
  11 #C_G          SPRIVTEST JSISSUE,3,XLOOP3
   7 #HWG          ADN   4  1
  21 #JB6          BZE   7  XLOOP3              [DONT STOP JOB IF NON-FIRST PASS OF J
   9 #JTQ          LDX   6  JOBNUM(3)
   8 #K*B    RETRY CALL  5  STOP2
   8 #KT2          BRN      ODEAR
   9 #L#L          BRN      XLOOP3   
   8 #LS=    QEND3 BZE   4  XFIN 
   9 #MRG          COOR3    ASIFIN   
   5 #N?6    NOT3INB 
   5 #PQ2    TIS4INB 
   7 #Q9L          LDN   4  0
   7 #QP=          LDN   7  0
  15 #R8W          BRN      REPEAT              [TRY AGAIN   
   4 #RNG    XFIN
   5 #S86    QENTRY11
  10 #TM2          LDXC  0  AWORK4(2)   [
  19 #W6L          BCS      NOWAKEDUMP  [ DON'T WAKE DUMPER IF 'FN LK' ELSE  
  17 #WL=          FON      ADUMPWAIT   [MAKE SURE DUMPER HAS A GO   
   6 #X5W    NOWAKEDUMP  
   8 #_4B          PERCOUNT ALL  
  19 *?#G ...      MONOUT   HDONE1,,NONAUT    [O/P EVERYTHING CONE MESSAGE   
   4 *F82    WEND
   8 *FML          LDX   2  FX2  
  20 *G7=          LDCT  0  #20                 [ ENSURE THAT THE 'CURRENT FINISH
  16 *GLW          ORS   0  EXT+11(2)           [   ACTIVITY' BIT
  15 *H6G          ERS   0  EXT+11(2)           [   IS UNSET 
   4 *HL6    [   
  15 *J5Q    [     NB  FNDELE ASSUMES THAT ACROSS AND ENDCOM 
  15 *JKB    [     ARE THE SAME LENGTH  (AT PRESENT 2 WORDS) 
  12 *K52    [     MENDING POINT FOR MACRO FNDELE
   9 *KJL          SEGENTRY K14FINISH
   6 *L4=          ENDCOM
   4 *L9D ...[   
   4 *LBL ...[   
  15 *LHW    ODEAR COOR3    #62                 [WAIT A WHILE
  15 *M3G          BRN      RETRY               [TRY AGAIN   
   4 *M8N ...[   
   4 *M*W ...[   
  17 *MH6    #     THIS SECTION IMPLEMENTS THE TIME CASE OF FINISH   
  15 *N2Q    WAIT  STOZ     AWORK1(2)           [CLEAR MARKER
   7 *NGB    WAIT1 OUTBLOCN 8
   9 *P22    #SKI  K0FINISH>199-199  
   9 *PFL          TRACE    4,FINTIME
  16 *P_=          BNZ   4  XTIME               [J IF NOT ZERO   
   8 *QDW          OUTMESS  HNOW 
   8 *QYG          BRN      XBBC 
   9 *RD6    XTIME LDX   5  AWORK2(2)
   7 *X*6          SPARANOT 1
  15 *XSQ          LDN   6  XIN(1)              [MOVE IN 'IN'
  10 *Y#B          LDN   7  ACOMMUNE1(2) 
   7 *YS2          MVCH  6  3
   9 *_?L          MHUNT 3,CPB,CUNI  
   9 *_R=          LDX   2  ANUM(3)  
   8 B2=W          ANDN  2  4095 
   9 B2QG          LDN   6  APARA(3) 
  15 B3=6          MVCH  6  0(2)                [MOVE IN TIME
   8 B3PQ          BNZ   5  SPEC 
   8 B49B          LDN   5  #20  
  19 B4P2          LDX   2  7                   [IF NOT SPEC =MINS SO MOVE   
  15 B58L          DCH   5  0(2)                [IN 'MINS'   
   7 B5N=          BCHX  7  /
   9 B67W          LDN   6  TIME+1(1)
   7 B6MG          MVCH  6  4
   8 B776    SPEC  LDX   2  FX2  
  10 B7LQ          SBN   7  ACOMMUNE1(2) 
   7 B86B          SLC   7  2
  10 B8L2          OUTPARAM 7,ACOMMUNE1  
  15 B95L    XBBC  OUTPAR   TIMENOW             [ O/P TIME   
   9 B9K=          LDX   0  AWORK1(2)
  16 B=4W          BNZ   0  OPSINF              [J IF TO TELL OPS
   9 B*2L          BROADCAST HWARN   
  15 BDXL          STO   1  AWORK1(2)           [SET MARKER  
   8 BFC=          BRN      WAIT1
   5 BFWW    OPSINF  
  14 BJSL          INFORM   3,HWARN,1           [TELL OPS
   8 BNPL          BNZ   4  SOME 
  15 BY42          LDCT  0  #200                [IF TIME ZERO
  16 BYHL          ORS   0  FINISH              [SET TIME UP BIT 
  18 B_3=          BRN      XCOM                [AND JOIN MAIN ROUTINE   
  14 B_GW    #     IN THIS SECTION X7 IS SET AS FOLLOWS:-
  12 C22G    #     X7= 0  IF  ALL JOBS FINISHED  
  13 C2G6    #     X7 +VE IF  ALL BG JOBS FINISHED   
  13 C2_Q    #     X7 -VE IF  STILL SOME BG JOBS LEFT
   7 C3FB    SOME  NGN   7  1
   5 C3_2    XLOOP4  
   8 C4DL          BPZ   7  WHILE
  16 C4Y=          CALL  5  WHATBG              [CHECK BG JOBS   
  16 C5CW          BRN      WHILE               [J IF SOME LEFT  
  17 C5XG          BZE   7  WHILE               [J IF NO JOBS LEFT   
   8 C6C6          OUTNUM   7,0  
  20 C6WQ          MONOUT   HDONEBG             [O/P 'ALL BG JOBS DONE' MESSAGE  
   9 C7BB    WHILE LONGWAIT XBRK,#62 
   7 CBNQ          LDN   0  1
  18 CC8B          SBS   0  FINISH              [UPDATE TIME IN FINISH   
   5 CCN2    SUBFINB 
   9 CD7L          BCT   4  XLOOP4   
  16 CT*G          LDCT  0  #200                [WAIT FINISHED   
  16 CTT6          ORS   0  FINISH              [SET TIME UP BIT 
   5 CW#Q    XYNSETA 
   8 CWSB          BZE   7  XDONE
   9 CX#2          CALL  5  WHATBG   
  21 CXRL          BRN      XCOM                [JOIN ABANDON ROUTINE IF JOBS LEFT   
   8 CY?=          BZE   7  XDONE
   8 CYQW          OUTNUM   7,0  
  15 C_=G          MONOUT   HDONEBG             [O/P MESSAGE 
   8 C_Q6          BRN      XCOM 
  20 D29Q    #     THIS SECTION IMPLEMENTS THE CASE WHEN THERE ARE NO PARAMETERS 
  14 D2PB    NOPAR LDN   4  1                   [MARKER  
   5 D392    NOPAR1  
   8 D3NL          OUTMESS  HSOON
  15 D48=          OUTPAR   TIMENOW             [ O/P TIME   
  16 D4MW          BZE   4  OPS                 [J TO TELL OPS   
  18 D7KL          BROADCAST HWARN              [O/P WARNING TO MOP JOBS 
   7 D?GL          LDN   4  0
   9 D#2=          BRN      NOPAR1   
   4 D#FW    OPS 
   9 DCCL          INFORM   3,HWARN,1
   5 DH#L    TESTBG  
  16 DHS=          CALL  5  WHATBG              [CHECK BG JOBS   
  17 DJ?W          BRN      TARRY               [J IF STILL SOME LEFT
  17 DJRG          BZE   7  NOMORE              [J IF NO JOBS LEFT.  
  18 DK?6          BNZ   4  TARRY               [J IF MESS ALREADY O/P   
   8 DKQQ          OUTNUM   7,0  
  19 DNNG          MONOUT   HDONEBG             [O/P B.G JOBS DONE MESSAGE   
   7 DSKG          LDN   4  1
  13 DT56    TARRY COOR3    #44                 [WAIT
   9 DTJQ          BRN      TESTBG   
   5 DW4B    NOMORE  
   8 F6TL          LDCT  0  #200 
   9 F7*=          ORS   0  FINISH   
   8 F=?2          BRN      XDONE
   5 F=QL    SECOND  
   9 F?==          ACROSS   FINISHB,4
  19 F?PW    QENTRY2                            [ 'CANCEL FINISH' ENTRY POINT
   9 F#9G          ACROSS   FINISHB,5
   4 FMYG    XBRK
   4 FND6    XGER
  10 FNXQ          GEOERR   1,FINWRONG   
   9 FPCB    XMDF  CALL  1  ZERROR   
   7 FPX2          +HNEMDF   
   5 FQBL    TOOLATE 
   9 FQW=          CALL  1  ZERROR   
   7 FR*W          +HTOOLATE 
   9 FRTG    WOOPS CALL  1  ZERROR   
   7 FS*6          +APFERR   
   9 FSSQ    NULL  CALL  1  ZERROR   
   7 FT#B          +JPARNULL 
   9 FTS2    YUK   CALL  1  ZERROR   
   7 FW?L          +JTOOLONG 
   5 FWR=    ZERROR  
   8 FX=W          COMERRX  0(1) 
   4 FX#S ...[   
   4 FXBQ ...XEND
  17 FXDN ...[     PARAMETER ERROR FOUND, AND REPORTED, BY PARSPLIT  
  20 FXGL ...      BC       2,EXFINACT          [CLEAR ^CURRENT FINISH ACTIVITY^ 
  16 FXJJ ...[                                   BIT IN EXT+11   
   6 FXLG ...      ENDCOM
   4 FXND ...[   
  10 FXQG          MENDAREA 30,K99FINISH 
   4 FY=6    #END
   6 ____ ...156230530001
