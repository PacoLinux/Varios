  15 22#C ...#SEG  PROPERTY                     [LEN WAGSTAFF
  15 22FK ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   8 22LS ...#OPT  K0PROPERTY=0  
   7 22_=          8HPROPERTY
  12 23DW    #LIS  K0PROPERTY>K0PROPSYS>K0ALLGEO 
   4 23YG    #   
   4 24D6    #   
  20 24XQ    #     THIS SEGMENT IMPLEMENTS THE PROPERTY & CC PROPERTY COMMANDS   
  11 25CB    #     IT HAS TWO ENTRY POINTS   
   9 25X2    #     K1       PROPERTY 
  10 26BL    #     K2       CC PROPERTY  
   4 26W=    #   
   4 27*W    #   
  12 27TG          SEGENTRY K1PROPERTY,PROPERTYK1
  12 28*6          SEGENTRY K2PROPERTY,PROPERTYK2
   4 28SQ    #   
  14 28TT ...[ UNANTICIPATED CLOSEDOWN ENTRY (PR COMMAND)
   4 28WY ...[   
  12 28Y3 ...      FSHENTRY K3PROPERTY,XK3A,,XK3A
   4 28_6 ...[   
  13 2929 ...[ ENTRY TO GET IDFANDPROP SEMAPHOR IN B 
   4 293# ...[   
  12 294C ...      FSHENTRY K4PROPERTY,,XK4,XK4  
   4 295G ...[   
  11 296K ...[ ENTRY TO DO PUTCONS IN M/C B  
   4 297N ...[   
  13 298R ...      FSHENTRY K5PROPERTY,XK5,XK5,XK5   
   4 299W ...[   
  14 29=_ ...[ ENTRY FOR UNANTICIPATED CLOSEDOWN (CC PR) 
   4 29#4 ...[   
  12 29*7 ...      FSHENTRY K7PROPERTY,XK7A,,XK7A
   4 29B= ...[   
  11 29C* ...[ ENTRY TO DO PROPCANC IN M/C B 
   4 29DD ...[   
  13 29FH ...      FSHENTRY K8PROPERTY,XK8,XK8,XK8   
   4 29GL ...[   
  18 29HP ...[   ENTRY POINT TO DO 'PARSORTX' IN M/C B FOR M/C B COMMAND 
   4 29JS ...[   
  12 29KX ...      FSHENTRY K9PROPERTY,,XK9,XK9  
   4 29M2 ...[   
  21 29N5 ...[  ENTRY POINT IN M/C B TO RELEASE SEMAPHOR & SUICIDE @ END OF COMMAND  
   4 29P8 ...[   
  13 29Q? ...      FSHENTRY K10PROPERTY,,XK10,XK10   
   4 29S2    #   
   9 2=?L    ONEBIT         #57777777
  18 2=R=    TEMEX          #40000000           [DEFAULT :TEMP & EXCL/S  
   9 2?=W    ONE            #20000000
   9 2?QG    TWO            #37777777
  12 2#=6    PCATEGORY      16HPERMINCLEXCLTEMP  
   9 2#PQ    PCONS          7HCONSOLE
   9 2*9B    TWOBITS        #17777777
   9 2*P2    TESTBIT        #60000000
   9 2B8L    PCENTRAL       8HCENTRAL
   9 2BN=    PERMBIT        #20000000
   8 2C7W    PTAPE          +1   
   8 2CMG    PCARDS         +4   
   8 2D76    PRINTER        +2   
   8 2DLQ    PDRUM1         +9   
   8 2F6B    PDRUM2         +25  
   7 2FL2    PIPC        +28 
   8 2G5L    PTREADER       +0   
   8 2GK=    PCREADER       +3   
   7 2GL_ ...#SKI  G3SIGNET  
   4 2GNN ...(   
   9 2GQC ...PLPL           4H000L   
   9 2GS6 ...PLPP           4H000P   
   9 2GTT ...PLPA           4H000A   
   9 2GXJ ...PLPF           4H000G   
   8 2G_? ...TEN            +10  
   4 2H32 ...)   
   4 2H4W    #   
   4 2QC=    #   
  16 2QWW    #              ASSORTED MESSAGES TO THE OPERATOR:   
   4 2RBG    #   
  17 2RW6    ZPARMIS        +JPARMIS            [UC PARAMETER MISSING
  16 2S*Q    ZPROPNA        +JPROPNA            [PROPERTY NAME   
  19 2STB    ZNOTPAIR       +JNOTPAIR           [DELIMITERS MUST BE PAIRED   
  21 2T*2    ZILLCOMB       +ILLCOMB            [THIS COMBINATION OF QUALIFIERS IS IL
  18 2TSL    ZPROPUNK       +JPROPUNK           [PROPERTY NAME %C UNKNOWN
  21 2W#=    ZQUALIFIER     +JQUALIFIER         [ONLY ONE SET OF QUALIFIERS MAY BE GI
  21 2WRW    ZNLFNF         +JNLFNF             [%C IS NOT A CORRECTLY FORMED NAME   
  19 2X?G    ZPROPEXISTS    +JPROPEXIST         [PROPERTY %C ALREADY EXISTS  
  19 2XR6    ZCONSABS       +JCONSABS           [CONSOLE PARAMETER MISSING   
  17 2Y=Q    ZNOTCONS       +JNOTCONS           [%C IS NOT A CONSOLE 
  21 2YQB    ZCENTRAL       +JCENTRAL           [PROPERTY NAME'CENTRAL'EXISTS & MAY N
  15 2_=2                                       [BE CANCELLED
  19 2_PL    ZPROPNEED      +JPROPNEED          [PROPERTY %C STILL BEING USED
  17 2_SQ ...#SKI  JSKI04                       [FOR COMMAND ISSUERS 
  18 2_XW ...ZCIINUSE       +CIINUSE            [COMMAND ISSUER IN USE   
  17 3232 ...#SKI  JSKI32                       [FOR 15-BIT PROP NOS 
  18 3266 ...ZMAXPROP       +JMAXPROP           [MAX NO OF PROPS DEFINED 
   7 326L ...#SKI  G3SIGNET  
   4 3276 ...(   
  20 327L ...ZPMEEP         +JPROPMEEP          [ERROR IN %B %A : MEEP PROPERTY  
  21 3286 ...                                   [%C MUST BE TEMPORARY AND EXCLUSIVE  
   4 328L ...)   
   7 328N ...#UNS  ANSTOOMANY
  18 328Q ...ZPARMAX       +JMAXPAR             [MAX NO PARAMS EXCEEDED  
   4 328T ...#   
  20 3294 ...VOP                               [ S/ROUTINE TO CLEAR 'IDFANDPROP' 
  10 329? ...      V        IDFANDPROP   
   7 329G ...      EXIT  7  0
   4 329P ...#   
   4 329Y ...#   
  20 32=7 ...POP                               [ S/ROUTINE TO GET 'IDFANDPROP'   
   8 32=B ...      SBX   4  FX1  
  10 32=K ...      P        IDFANDPROP   
   8 32=S ...      ADX   4  FX1  
   7 32?3 ...      EXIT  4  0
   4 32?= ...#   
   4 32?F ...#   
   4 32M4 ...#   
  15 32NW    #              WAY OUT IN CASE OF COMMAND ERROR 
   4 338G    #   
   4 33?5 ...QER1
   8 3455 ...      COMERRX  6,5  
   4 347Q    #   
   4 34?M ...QER2
  16 3535 ...      CALL  7  VOP                 [FREE SEMAPHORE  
   6 3572          CLOSE 
   4 359K ...QER3
   7 363H ...      COMERRX  6
  13 366=    #     LOADING OF REQUIRED ERROR MESSAGE 
   4 36KW    #   
   5 375G    PANON   
   8 37K6          LDX   1  FX1  
  10 384Q          LDX   6  ZPARMIS(1)   
  10 38JB ...      LDX   5  ZPROPNA(1)   
   8 3942          BRN      QER1 
   5 39HL    UNPAIR  
   8 3=3=          LDX   1  FX1  
  10 3=GW          LDX   6  ZNOTPAIR(1)  
   8 3?2G          BRN      QER3 
   5 3?G6    ZCATMOD 
   8 3?_Q          LDX   1  FX1  
  10 3#FB          LDX   6  ZILLCOMB(1)  
   8 3#_2          BRN      QER2 
   5 3*DL    PRUNK   
   8 3*Y=          LDX   1  FX1  
  10 3BCW          LDX   6  ZPROPUNK(1)  
   8 3BXG          BRN      QER2 
   5 3CC6    TOOMANY 
   8 3CWQ          LDX   1  FX1  
  10 3DBB          LDX   6  ZQUALIFIER(1)
   8 3DW2          BRN      QER3 
   5 3F*L    PFORMAT 
   8 3FT=          LDX   1  FX1  
   9 3G#W          LDX   6  ZNLFNF(1)
   8 3GSG          BRN      QER3 
   5 3H#6    XEXISTS 
   8 3HRQ          LDX   1  FX1  
  11 3J?B          LDX   6  ZPROPEXISTS(1)   
   8 3JR2          BRN      QER2 
   5 3K=L    XCONSABS
   8 3KQ=          LDX   1  FX1  
  10 3L9W          LDX   6  ZCONSABS(1)  
   8 3LPG          BRN      QER2 
   5 3M96    PNOTCONS
   7 3MNQ          LDN   5  0
   7 3N8B          SPARAPAS 5
   9 3NN2          MHUNT 3,CPB,CUNI  
   7 3P7L          FREECORE 3
   7 3PM=          SPARAPAS 5
  10 3Q6W          LDX   6  ZNOTCONS(1)  
   8 3QLG          BRN      QER2 
   5 3R66    XCENTRAL
   8 3RKQ          LDX   1  FX1  
  10 3S5B          LDX   6  ZCENTRAL(1)  
   8 3SK2          BRN      QER3 
   5 3T4L    PROPNEED
  10 3TJ=          LDX   6  ZPROPNEED(1) 
   8 3W3W          BRN      QER2 
  13 3W54 ...#SKI  JSKI04                       [CI  
   4 3W6= ...(   
   8 3W7D ...USECI LDX   1  FX1  
  10 3W8L ...      LDX   6  ZCIINUSE(1)  
   8 3W9S ...      BRN      QER2 
   4 3W?2 ...)   
  15 3W#8 ...#SKI  JSKI32                       [15BIT PROPS 
   4 3W*B ...(   
   8 3WBJ ...NOROM LDX   1  FX1  
  10 3WCQ ...      LDX   6  ZMAXPROP(1)  
   8 3WDY ...      BRN      QER2 
   4 3WG6 ...)   
   7 3WG? ...#SKI  G3SIGNET  
   4 3WGD ...(   
   7 3WGK ...XMEEP LDN   7  1
   8 3WGQ ...      SPARANOT 1,7  
   9 3WGX ...      LDX   6  ZPMEEP(1)
   8 3WH4 ...      BRN      QER2 
   4 3WH9 ...)   
   7 3WJM ...#UNS  ANSTOOMANY
   4 3WL5 ...(   
   5 3WMH ...PARMAX1 
  10 3WN_ ...      LDX   6  ZPARMAX(1)   
   8 3WQC ...      BRN      QER2 
   5 3WRT ...PARMAX2 
  10 3WT? ...      LDX   6  ZPARMAX(1)   
   8 3WWP ...      BRN      QER3 
   4 3WY7 ...)   
   4 3W_K ...#   
  21 3X36    #     THIS SUBROUTINE PASSES ACROSS THE FIRST PARAM.,CHECKS FOR LOCAL NA
  21 3XGQ    # CHECKS NAME NOT 'CENTRAL',SETS UP BLOCK TO OPEN :SUS.PROP,OPENS :SYS.P
  20 3Y2B    # FREES NAME BLOCK&EXITS.IF :SYS.PROP NOT OPEN OK-GEOERR'PROPFILE'  
   4 3YG2    #   
   4 3Y_L    #   
   8 3_F=    NAMEX SBX   7  FX1  
   7 3_YW          SPARAPAS  
  15 43CQ    PARFD CHEKLFN2 PANON,PFORMAT       [CHECK FORMAT
  15 43XB          TESTNAMX 2,APARA(3),PCENTRAL(1),NOTCENT,4 
  20 44C2          BRN      XCENTRAL            [THIS IS THE ILLEGAL 'CENTRAL'   
   5 44WL    NOTCENT 
  18 45B=          LDX   4  ACES                [SPACEFILL BED FOR NAME  
   9 45TW          STO   4  AWORK1(2)
   9 46*G          STO   4  AWORK2(2)
   9 46T6          STO   4  AWORK3(2)
   9 47#Q          LDN   0  APARA(3) 
   9 47SB          LDN   1  AWORK1(2)
   9 48#2          LDEX  2  ANUM(3)  
  20 48RL          MVCH  0  0(2)                [MOVE PROP NAME INTO AWORK WORDS 
  16 4?_8 ...      CALL  4  POP                 [SET SEMAPHORE   
  15 4B7G          OPENSYS SBRKIN,PROPERTY,GENERAL,CAREFUL   
  18 4BM6                                       [OPEN :SYSTEM.PROPERTY   
   8 4JGL          ADX   7  FX1  
   7 4K2=          EXIT  7  0
   5 4LF6    SBRKIN  
  18 4LYQ          COMBRKIN                     [END COMMAND IF BROKEN-IN
   4 4MY2    #   
   4 4NCL    #   
   4 4NX=    #   
  20 4PBW    #     THIS SUBROUTINE GETS RID OF'()'IN PARAM.,SPLITS AT ',',PASSES 
  19 4PWG    # ACROSS FIRDT QUAL.,TESTS IF LEGAL QUAL.,SETS APPROPRIATE BIT  
  20 4QB6    # IN APROPNO.IN APPEND BLOCK,&GOES BACK TO PASS ACROSS NEXT QUAL.   
  21 4QTQ    #    IF SAME QUAL.GIVEN TWICE OR'TEMP'&'PERM'OR'EXCL'&'INCL'GIVEN-ERROR 
   4 4R*B    #   
   8 4RT2    TEST  SBX   7  FX1  
   7 4S#L          LDN   4  2
  16 4SS=          PARALYSE ,,4                 [GET RID OF ()   
   7 4S_D ...#UNS  ANSTOOMANY
  11 4T6L ...      TESTREP2 TOOMANY,PARMAX1  
  18 4T?W          PARALYSE #34                 [SPLIT CAT/MOD QUALIFIERS
   7 4TL? ...#UNS  ANSTOOMANY
  11 4TYN ...      TESTREP2 TOOMANY,PARMAX1  
  17 4W?6          NGN   4  1                   [SET EXCL/INCL SWITCH
  17 4WQQ          NGN   6  1                   [SET PERM/TEMP SWITCH
   7 4X=B    START SPARAPAS  
   9 4XQ2          MHUNT 3,CPB,CUNI  
   9 4Y9L          LDX   5  ANUM(3)  
  17 4YP=          BZE   5  START               [IF NULL TRY AGAIN   
  12 4_8W          CHEKLFN2 NOPROPMOD,ZCATMOD,3  
   9 4_NG          LDX   0  APARA(3) 
  14 5286          LDN   5  4                   [COUNT   
  10 52MQ          LDN   3  PCATEGORY(1) 
   5 537B    TRYAGAIN
  16 53M2          TXU   0  0(3)                [TEST IF EQUAL   
   9 546L          BCC      XFOUND   
   7 54L=          ADN   3  1
   9 555W          BCT   5  TRYAGAIN 
   9 55KG          BRN      ZCATMOD  
  19 5656    XFOUND                             [BRANCH TO APPROPRIATE LABEL 
   7 56JQ          SBN   5  2
   8 574B          BNG   5  TEMP 
   8 57J2          BZE   5  PEXCL
   7 583L          SBN   5  2
   8 58H=          BNG   5  PINCL
   8 592W          BRN      PERM 
   4 59GG    TEMP
   9 5=26          BZE   6  ZCATMOD  
  15 5=FQ          ADN   6  1                   [SET SWITCH 0
   8 5=_B          BRN      START
   5 5?F2    PEXCL   
   9 5?YL          BZE   4  ZCATMOD  
  15 5#D=          ADN   4  1                   [SET SWITCH 0
   8 5#XW          BRN      START
   5 5*CG    PINCL   
   7 5*G5 ...#SKI  G3SIGNET  
   4 5*JN ...(   
   9 5*M? ...      LDX   0  AWORK4(2)
  19 5*PW ...      BNZ   0  XMEEP               [J. TEMP-EXCL ONLY FOR MEEP  
   4 5*SF ...)   
   9 5*X6          BZE   4  ZCATMOD  
   7 5B6Y ...#SKI  JSKI32<1$1
   9 5BBQ          MHUNTW 3,FILE,FAPB
   6 5BHY ...#SKI  JSKI32
  10 5BP6 ...      MHUNTW   3,FILE,FWB   
  10 5BWB          LDX   5  APROPNO+A1(3)
   9 5CB2          ANDX  5  TWO(1)   
  10 5CTL          STO   5  APROPNO+A1(3)
  15 5D*=          ADN   4  1                   [SET SWITCH 0
   8 5DSW          BRN      START
   4 5F#G    PERM
   7 5FC5 ...#SKI G3SIGNET   
   4 5FFN ...(   
   9 5FJ? ...      LDX   0  AWORK4(2)
  19 5FLW ...      BNZ   0  XMEEP               [J. TEMP-EXCL ONLY FOR MEEP  
   4 5FPF ...)   
   9 5FS6          BZE   6  ZCATMOD  
   7 5G3Y ...#SKI  JSKI32<1$1
   9 5G?Q          MHUNTW 3,FILE,FAPB
   6 5GDY ...#SKI  JSKI32
  10 5GL6 ...      MHUNTW   3,FILE,FWB   
  10 5GRB          LDX   5  APROPNO+A1(3)
   9 5H?2          ORX   5  ONE(1)   
  10 5HQL          STO   5  APROPNO+A1(3)
  15 5J==          ADN   6  1                   [SET SWITCH 0
   8 5JPW          BRN      START
   6 5K9G    NOPROPMOD   
   8 5KP6          ADX   7  FX1  
   7 5L8Q          EXIT  7  0
   4 5LNB    #   
   4 5M82    #   
   4 5MML    #   
  20 5N7=    #     THIS SUBROUTINE PASSES ACROSS THE CONSOLE PARAM.,OVERWRITES   
  17 5NLW    # 'CONSOLE ' IN THE BLOCK WITH THE DEVICE DESCRIPTION   
   4 5P6G    #   
   5 5PL6    TESTCONS
   8 5Q5Q          SBX   7  FX1  
   7 5QKB          PARAFREE  
   7 5R52          LDN   4  7
  21 5RJL          SPARABEG 1,4,PCONS(1),,0     [PUT PARAM STARTING WITH 'CONSOLE'   
  16 5S4=          MHUNT 3,CPB,CUNI             [INTO CPB/CUNI   
   9 5SHW          LDX   5  ANUM(3)  
  18 5T3G          BZE   5  T1                  [JUMP TO T1 IF NULL PARAM
  18 5TH6          BNG   5  T2                  [JUMP TO T2 IF NO PARAM  
  10 5W2Q          LDX   5  JPARNUM(3)   
  18 5WGB          PARAMOVE 4,5                 [MOVE PERIPH DESC UP TO  
  17 5X22                                       [OVERWRITE 'CONSOLE' 
   7 5XFL          LDN   5  2
   7 5XPD ...#SKI  JSKI04<1$1
  17 5X_=          PARSORTX T3,5                [ANALYSE PERIPH DESC 
   6 5Y6D ...#SKI  JSKI04
  18 5Y?L ...      PARSORTX  T3,5,SYSPROP         [ANALYSE PERIPH DESC   
  18 5YDW                                       [JUMP TO T3 IF BREAK-IN  
   5 5YX6 ...XCHKJSCE
   9 5YYG          HUNTW 3,CPAR,JSCE 
  20 5_D6          BNG   3  T3                  [JUMP TO T3 IF PARAM NOT VALID   
   8 5_XQ          ADX   7  FX1  
  13 62CB          EXIT  7  3                   [OK  
   8 62X2    T1    ADX   7  FX1  
  15 63BL          EXIT  7  1                   [NULL PARAM  
   8 63W=    T2    ADX   7  FX1  
  17 64*W          EXIT  7  2                   [NON-EXISTENT PARAM  
   4 64TG    T3  
   8 65*6          ADX   7  FX1  
  19 65SQ          EXIT  7  0                   [INVALID PARAM OR BREAK-IN   
   4 66#B    #   
   4 66S2    #   
  19 67?L    #     THIS SUBROUTINE CANCELS THE ATTRIBUTIONS OF THE PROPERTY  
  16 67R=    # IN THE APIA.X6 CONTAINS THE TYPE OF PERIPHERAL.   
  10 68=W    # X5 CONTAINS PROP NUMBER   
   4 68QG    #   
   5 69=6    RECORD  
   8 69PQ          SBX   7  FX1  
  21 6=47 ...      FINDPEREC 1,APFOTY,6,R2,,RSWOK      [FIND FIRST DEVICE LIST FOR TH
   5 6=BJ ...RSWOK   
  20 6=P2          BNG   1  R1                  [BRANCH IF NO MORE OF THIS TYPE  
  17 6?8L          LADDP 1,2                    [FIND POS'N IN APIA  
   7 6?N=          LDN   4  4
   4 6#7W    R4  
  10 6#MG          TXU   5  APERPROP+1(2)
  20 6*76          BCC      R3                  [BRANCH IF PROP NUMBERS THE SAME 
   7 6*LQ          ADN   2  1
   8 6B6B          BCT   4  R4   
  18 6BL2          BRN      R2                  [FIND NEXT DEVICE LIST   
   4 6C5L    R3  
   7 6CK=          SBN   4  1
  19 6D4W          BZE   4  R23                 [BRANCH IF LAST ATTRIBUTION  
  12 6DJG    R20   LDX   0  APERPROP+2(2)       [
  20 6F46          BZE   0  R23                 [MOVE UP REMAINING ATTRIBUTIONS  
  19 6FHQ          STO   0  APERPROP+1(2)       [TO O'WRITE UNWANTED ENTRY   
  12 6G3B          ADN   2  1                   [
   8 6GH2          BCT   4  R20  
  17 6H2L    R23   STOZ     APERPROP+1(2)       [ZEROISE LAST ENTRY  
  18 6HG=          BRN      R2                  [FIND NEXT DEVICE LIST   
   4 6H_W    R1  
   8 6JFG          ADX   7  FX1  
   8 6JP# ...      LDX   1  FX1  
   7 6J_6          EXIT  7  0
   4 6KDQ    #   
  19 6KYB    #     THIS SUBROUTINE CANCELS THE ASSOCIATIONS OF THE PROPERTY  
   9 6LD2    #  IN THE DEVICE LIST.  
  15 6LXL    #  [THE CODE IS AS FOR S/R RECORD EXCEPT THAT   
  16 6MC=    #  THE ENTRIES FOR ASSOCIATIONS ARE 2 WORDS LONG]   
   4 6MWW    #   
   5 6NBG    RECORD2 
   8 6NW6          SBX   7  FX1  
  13 6P8H ...      FINDPEREC 1,APFOTY,6,R6,,RSWOK2   
   5 6PGY ...RSWOK2  
   8 6PTB          BNG   1  R5   
   7 6Q*2          LADDP 1,2 
   7 6QSL          LDN   4  3
   4 6R#=    R8  
  10 6RRW          TXU   5  APERPROP+1(2)
   8 6S?G          BCC      R7   
   7 6SR6          ADN   2  2
   8 6T=Q          BCT   4  R8   
   8 6TQB          BRN      R6   
   4 6W=2    R7  
   7 6WPL          SBN   4  1
   8 6X9=          BZE   4  R53  
  10 6XNW    R50   LDX   0  APERPROP+3(2)
   8 6Y8G          BZE   0  R53  
  10 6YN6          STO   0  APERPROP+1(2)
  10 6_7Q          LDX   0  APERPROP+2(2)
  10 6_MB          STO   0  APERPROP(2)  
   7 7272          ADN   2  2
   8 72LL          BCT   4  R50  
  10 736=    R53   STOZ     APERPROP(2)  
  10 73KW          STOZ     APERPROP+1(2)
   8 745G          BRN      R6   
   4 74K6    R5  
   8 754Q          ADX   7  FX1  
   7 75JB          EXIT  7  0
   4 7642    #   
   4 76HL    #   
   9 773=    #     #   #           # 
   9 77GW    #     #  #            # 
   9 782G    #     # #             # 
   9 78G6    #     ##              # 
   9 78_Q    #     # #             # 
   9 79FB    #     #  #            # 
   9 79_2    #     #   #           # 
   4 7=DL    #   
   4 7=Y=    #   
  13 7?CW    #     ENTRY POINT FOR PROPERTY COMMAND  
  21 7?XG    # FIRST SPLIT PARAM INTO NAME & QUALS(IF ANY),IF MORE THAN 2 COMPONENTS 
  21 7#C6    # PARAM-ERROR,CALLS'NAMEX',STEPS DOWN :SYS.PROP COMPARING EXISTINGNAMES 
  19 7#WQ ...# TO NEW ONE.IF NAME EXISTS-BRANCH,SET UP WRITE BLOCK,MOVE NAME,
  21 7*BB ...# STORE FRH,GET LOWEST UNUSED PROP.NO.&SET DEFAULT TEMP/EXCL BITS& STORE
  20 7*W2 ...# WRITE BLOCK,ZEROIZE COUNT OF ATTRIBUTIONS & 'CONSOLE'PROP. WORD   
  21 7B*L    # IF ANY QUALS.-CALL 'TEST',FREE ALL CMULTI BLOCKS,PICK UP'CONSOLE'PARAM
  21 7BT=    # (IF DOES NOT EXIST-BRANCH),OVER WRITE CH'S'CONSOLE',SPLIT PARAM. AT'.'
  21 7C#W    # 0 APROPGROUP TO SHOW'CONSOLE'PROP.,SET PERM.BIT ,PASS INF TO I.D.F.APP
  12 7CSG    # TO :SYS.PROP.,CLOSE :SYS.PROP,END.
  21 7D#6    #   IF PROP NAME ALREADY EXISTED IN :SYS.PROP &WAS'CONSOLE PROP.-FREE   
  21 7DRQ    # CMULTI BLOCKS,PICK UP 'CONSOLE'PARAM(IF NONE-ERROR),OVER WRITE CH'S   
  19 7F?B    # 'CONSOLE',SPLIT PARAM AT'.',PASS INF TO I.D.F.,CLOSE ESYSPROP,
   5 7FR2    # END.  
   4 7G=L    #   
   4 7GQ=    #   
   6 7H9W    PROPERTYK1  
   7 7HPG          PARALYSE  
   7 7HSL ...#UNS  ANSTOOMANY
  14 7HXQ ...      TESTREP2 UNPAIR,UNPAIR,TOOMANY,PARMAX2
   7 7J2W ...#UNS  ANSTOOMANY
   4 7J62 ...#SKI
  19 7J96          TESTREP  UNPAIR,UNPAIR       [IF DELIMITERS UNPAIRED-ERROR
   7 7JNQ          LDN   5  1
   8 7K8B          PARANUMB 6,5  
   7 7KN2          SBN   6  3
  19 7L7L          BPZ   6  TOOMANY             [COMERR IF MORE THAN 1 SET   
  16 7LM=                                       [OF QUALIFIERS   
  21 7M6W          CALL  7  NAMEX               [VALIDATE PROPNAME AND OPEN SYSPROP  
   7 7M7C ...#SKI  G3SIGNET  
   4 7M7Y ...(   
  19 7M88 ...      JENVNOT  NAMOK,MEEP           [J. IF NOT MEEP ENVIRONMENT.
   8 7M8F ...      LDX   1  FX1  
   9 7M92 ...      LDCH  0  AWORK1(2)
  17 7M9H ...      BXU   0  PLPL(1),NAMOK       [J. 1ST CHAR NOT 'L' 
   7 7M=4 ...      BCHX  2  /
   9 7M=K ...      LDCH  0  AWORK1(2)
  17 7M?6 ...      BXU   0  PLPP(1),NAMOK       [J. 2ND CHAR. NOT 'P'
   7 7M?M ...      LDN   4  2
   7 7M#8 ...NAM   BCHX  2  /
   9 7M#P ...      LDCH  0  AWORK1(2)
  17 7M*= ...      BXL   0  TEN(1),NAMB         [J. CHAR IN RANGE 0-9
  19 7M*R ...      BXL   0  PLPA(1),NAMOK       [J. CHAR NOT IN RANGE A-F <A 
  18 7MB# ...      BXGE  0  PLPF(1),NAMOK       [J. CHAR OUT OF RAOGE > F
   8 7MBT ...NAMB  BCT   4  NAM  
   8 7MCB ...      LDX   2  FX2  
   9 7MCX ...      LDX   4  AWORK2(2)
  10 7MDD ...      BXU   4  ACES,NAMOK   
   9 7MD_ ...      LDX   4  AWORK3(2)
  10 7MFG ...      BXU   4  ACES,NAMOK   
   8 7MG3 ...      LDX   4  BIT9 
   9 7MGJ ...      STO   4  AWORK4(2)
   9 7MH5 ...      BRN      NAMOK1   
   8 7MHL ...NAMOK LDX   2  FX2  
   9 7MJ7 ...      STOZ     AWORK4(2)
   5 7MJN ...NAMOK1  
   4 7MK9 ...)   
   4 7MLG    #   
   6 7N66    STEP1 STEP  
  16 7NKQ          BZE   3  NOPROP              [PROPERTY UNKNOWN
  15 7P5B          TESTNAMX 3,APROPNAME(3),AWORK1(2),STEP1,4 
  17 7PK2          BRN      XCHANGE             [JUMP IF PROP EXISTS 
   4 7Q4L    #   
   5 7QJ=    NOPROP  
   4 7QN7 ...#   
   7 7QS4 ...#SKI  JSKI32<1$1
   4 7QX_ ...(   
  17 7R3W          SETNCORE 7,3,FILE,FAPB       [SETUP APPEND BLOCK  
   9 7RHG          LDN   4  AWORK1(2)
  11 7S36          LDN   5  APROPNAME+A1(3)  
  20 7SGQ          MOVE  4  3                   [MOVE PROP NAME-> APPEND BLOCK   
   7 7T2B          LDN   0  7
  19 7TG2          STO   0  A1+FRH(3)           [PUT HEADER IN APPEND BLOCK  
  17 7T_L          LDX   4  APROPWORD           [GET CURRENT NUMBER  
  15 7WF=          ADN   4  1                   [UPDATE BY 1 
  18 7WYW          STO   4  APROPWORD           [UPDATE APROPWORD BY 1   
  19 7XDG          ORX   4  TEMEX(1)            [SET DEFAULT TEMP & EXCL BITS
   9 7XY6          ANDX  4  ONEBIT(1)
   7 7Y5# ...#SKI  G3SIGNET  
  18 7Y=G ...      ORX   4  AWORK4(2)           [BIT 9 TO BE SET IF MEEP 
  18 7YCQ          STO   4  APROPNO+A1(3)       [STORE IN APPEND BLOCK   
   7 7YXB          LDN   5  0
  11 7_C2          STO   5  ATTCOUNT+A1(3)   
  11 7_WL          STO   5  APROPGROUP+A1(3) 
   4 826D ...)   
   4 82B=    #   
   6 82BP ...#SKI  JSKI32
   4 82C8 ...(   
   4 82CM ...#   
  18 82D6 ...# NOW FIND PLACE TO INSERT NEW RECORDS: PROP NUMBERS ARE IN 
  16 82DK ...# ASCENDING ORDER - WE CHOOSE THE LOWEST FREE ONE   
   4 82F4 ...#   
   6 82FH ...      REWIND
   7 82FJ ...#SKI  G3SIGNET  
   4 82FK ...(   
   8 82FL ...      LDX   7  BIT9 
   9 82FM ...      ANDX  7  AWORK4(2)
  19 82FN ...      BZE   7  SFIND               [J NOT A MEEP PR. NO REQUIRED
   5 82FP ...NEXTM   
  19 82FQ ...      STEP                         [POSITION ON IST MEEP PR NO. 
  21 82FR ...      BZE   3  SPACE               [NO MEEP EXIST IN SYSPROP:INSERT MEEP
  10 82FS ...      LDX   0  APROPNO(3)   
  20 82FT ...      SLL   0  9                   [ SHIFT TO SEE IF BIT 9 SET -MEEP
  21 82FW ...      BNG   0  TFREE               [FIRST MEEP PROP FOUND :FD LOWEST FRE
   8 82FX ...      BRN      NEXTM
   5 82FY ...SFIND   
   4 82F_ ...)   
   7 82G2 ...      LDN   7  1
   6 82GF ...NEXT  STEP  
  17 82GY ...      BZE   3  SPACE               [J IF NO GAPS FOUND  
   7 82H5 ...#SKI G3SIGNET   
   5 82H= ...TFREE   
  16 82HC ...      LDX   0  APROPNO(3)          [PROPERTY NUMBER 
   8 82HW ...      ANDX  0  BSP16
   7 82J* ...      TXL   7  0
  18 82JS ...      BCS      SPACE               [J IF LOWEST UNUSED FOUND
   7 82K? ...      LDX   7  0
   7 82KQ ...      ADN   7  1
   8 82L9 ...      TXL   7  BSP16
  17 82LN ...      BCC      NOROM               [NO ROOM AT THE INN  
   8 82M7 ...      BRN      NEXT 
   4 82ML ...#   
  16 82N5 ...SPACE SETNCORE 7,3,FILE,FWB        [FOR NEW RECORD  
   7 82NJ ...      LDN   0  7
   9 82P3 ...      STO   0  A1+FRH(3)
  19 82PG ...      ORX   7  TEMEX(1)            [SET DEFAULT TEMP,EXCL BITS  
   9 82P_ ...      ANDX  7  ONEBIT(1)
  10 82QD ...      STO   7  APROPNO+A1(3)
  18 82QX ...      STOZ     ATTCOUNT+A1(3)      [NO AU OR AE (NATURLICH) 
   9 82RB ...      LDN   4  AWORK1(2)
  11 82RT ...      LDN   5  APROPNAME+A1(3)  
  16 82S# ...      MOVE   4  3                   [PROPERTY NAME  
  11 82SR ...      STOZ     APROPGROUP+A1(3) 
   4 82T= ...)   
   7 82TW          LDN   5  1
   8 83*G          PARANUMB 4,5  
   7 83T6          SBN   4  2
  18 84#Q          BNG   4  XCONSPROP           [JUMP IF NO QUALIFIERS   
   5 84SB    XQUALS  
   8 85#2          CALL  7  TEST 
   4 85RL    #   
   6 86?=    XCONSPROP   
  17 86QW          CALL  7  TESTCONS            [TEST IF CONSOLE PROP
  16 87=G          BRN      PEND                [PARAM NOT VALID 
  17 87Q6          BRN      XCONSABS            [CONSOLE PARAM NULL  
  18 889Q          BRN      PAPPEND             [JUMP IF NOT CONSOLE PROP
   4 88PB    [   
  12 8992    [ROUTINE FOR NEW CONSOLE PROPERTY   
   4 89NL    [   
   2 8=8=
   7 8=D4 ...#SKI  JSKI32<1$1
   9 8=MW          MHUNTW 3,FILE,FAPB
   6 8=T4 ...#SKI  JSKI32
  10 8?2= ...      MHUNTW   3,FILE,FWB   
   9 8?7G          LDX   5  TEMEX(1) 
  16 8?M6          ORS   5  APROPGROUP+A1(3)    [SET CONSOLE BIT 
   4 8#6Q    #   
  10 8#LB          LDX   5  APROPNO+A1(3)
  17 8*62          ORX   5  PERMBIT(1)          [CONSPROP ALWAYS PERM
  20 8*KL          STO   5  APROPNO+A1(3)       [STORE PROP NO. IN APPEND BLOCK  
  17 8BCL ...      PUTCONS  XBRK,5,PEND,PNOTCONS  [PUT PROP IN IDF   
   4 8BJW    #   
   5 8C4G    PAPPEND 
   7 8C## ...#SKI  JSKI32<1$1
  18 8CJ6          APPEND                       [APPEN BLOCK TO SYSPROP  
   6 8CP# ...#SKI  JSKI32
   6 8CWG ...      INSERT
  15 8D3Q                                       [CLOSE & END 
  16 8D88 ...PEND  CALL  7  VOP                 [FREE SEMAPHORE  
   6 8F32          CLOSE 
   5 8F4C ...XENDC   
   6 8FGL          ENDCOM
   4 8G2=    #   
   5 8GFW    XCHANGE 
  10 8G_G          LDX   5  APROPGROUP(3)
  18 8HF6          BPZ   5  XEXISTS             [IF NOT CONS PROP ERROR  
   4 8HYQ    [   
  12 8JDB    [ROUTINE FOR EXISTENT CONSOLE PROP  
   4 8JY2    [   
  10 8KCL          LDX   6  APROPNO(3)   
   9 8KX=          CALL  7  TESTCONS 
  16 8LBW          BRN      PEND                [INVALID PARAM   
  15 8LWG          BRN      XCONSABS            [NULL PARAM  
  16 8MB6          BRN      XCONSABS            [NO CONSOLE PARAM
  17 8N86 ...      PUTCONS  XBRK,6,PEND,PNOTCONS  [PUT PROP IN IDF   
   8 8N*B          BRN      PEND 
   4 8NT2    #   
   4 8P#L    #   
   9 8PS=    #     #   #          ###
  10 8Q?W    #     #  #          #   #   
  10 8QRG    #     # #               #   
   9 8R?6    #     ##               #
   9 8RQQ    #     # #             # 
   9 8S=B    #     #  #           #  
  10 8SQ2    #     #   #         #####   
   4 8T9L    #   
   4 8TP=    #   
  15 8W8W    #     ENTRY POINT FOR CANCEL PROPERTY COMMAND   
  21 8WNG    # CALLS'NAMEX',CHECKS PROPERTY IN :SYS.PROP.,IF 'CONSOLE PROPERTY-BRANCH
  17 8X86    # FOR EACH PERIPH,REMOVE ATTRIBUTIONS AND ASSOCIATIONS  
  10 8XMQ    # OF THE PROPERTY FROM APIA.
  21 8Y7B    # UNSET  BITS0&1 OF PROP.NO.,CANCEL PROPERTY IN I.D.F.,DELETE RECORD IN 
  21 8YM2    # :SYS.PROP.,OUT PUT 'PROPERTY CANCELLED'MESSAGE,CLOSE :SYS.PROP.,END   
  21 8_6L    #    IF 'CONSOLE'PROPERTY-UNSET B1&SETBIT0 OF PROP.NO.,CANCEL PROPERTY  
  20 8_L=    # IN I.D.F.,DELETE RECORD IN :SYS.PROP.,OUT PUT 'PROPERTY CANCELLED'
  11 925W    # MESSAGE,CLOSE :SYS.PROP.,END  
   4 92KG    #   
   6 9356    PROPERTYK2  
  20 93JQ          CALL  7  NAMEX               [VALIDATE NAME AND OPEN SYSPROP  
   5 944B    STEP2   
   6 94J2          STEP  
   8 953L          BZE   3  OUT2 
  15 95H=          TESTNAMX 3,APROPNAME(3),AWORK1(2),STEP2,4 
   4 962W    OUT2
  16 96GG          BZE   3  PRUNK               [ERROR IF UNKNOWN
  20 9726          LDCT  0  8                   [UNSET BIT 5 TO TELL CANCEL THAT 
  16 97FQ          ERS   0  EXT+11(2)           [PARAMETER WAS OK
  10 97_B          LDX   5  APROPNO(3)   
  10 98F2          LDX   6  APROPGROUP(3)
  19 98YL          PROPUSE 5,PROPNEED           [CHECK IF PROPERTY IS NEEDED 
   9 99D=          STO   6  AWORK1(2)
  17 99XW ...#SKI  JSKI04                       [FOR COMMAND ISSUERS 
   4 9=CG ...(   
   8 =3#B          LDX   1  FX1  
  17 =3S2          ANDX  5  TWOBITS(1)          [UNSET BIT0 & BIT1   
   9 =4?L          LDX   6  AWORK1(2)
  18 =4R=          BPZ   6  XNIDF               [IF NOT CONS PROP BRANCH 
  15 =5=W          ORX   5  TEMEX(1)            [SET BIT 0   
   5 =5QG    XNIDF   
  12 =672 ...      PROPCANC XBRK,5,USECI,STPCI   
  17 =6HG ...      STOZ     7                   [SET MARKER ^NOT CI^ 
   8 =6Y2 ...      BRN      NOTCI
  16 =7#G ...STPCI LDN   7  1                   [SET MARKER ^CI^ 
   9 =7P2          MHUNT 3,CPB,CUNI  
   9 =88L          LDX   4  ANUM(3)  
   8 =8N=          ANDN  4  #7777
  11 =97W          OUTPARAM 4,APARA,CPB,CUNI 
   9 =984 ...      INFORM 1,JCIGONE,1
   4 =98= ...)   
   5 =98? ...NOTCI   
   4 =98# ...[   
  12 =98* ...[ ENTRY POINT FOR PROPCANC IN M/C B 
   4 =98B ...[   
   4 =98C ...XK8 
   4 =98D ...[   
  17 =98F ...      BNG   6  PAPIA              [JUMP IF CONSOLE PROP.
   4 =98L ...[   
  14 =98S ...[CANCEL ALL ATTRIBUTIONS OF PROP FROM APIA  
   4 =992 ...[   
   3 =998 ...
   9 =99B ...      LDX   6  PTAPE(1) 
  20 =99J ...                                   [FIND THE NEXT TP DEVICE LIST-   
   9 =99Q ...      CALL  7  RECORD   
   9 =9=6 ...      LDX   6  PCARDS(1)
  20 =9=# ...                                   [FIND THE NEXT CP DEVICE LIST-   
   9 =9=G ...      CALL  7  RECORD   
  10 =9=W ...      LDX   6  PRINTER(1)   
  20 =9?4 ...                                   [FIND THE NEXT LP DEVICE LIST-   
   9 =9?= ...      CALL  7  RECORD   
   9 =9?L ...      LDX   6  PDRUM1(1)
  19 =9?S ...                                   [FIND THE NEXT DR DEVICE LIST
   9 =9#2 ...      CALL  7  RECORD   
   9 =9#B ...      LDX   6  PDRUM2(1)
  20 =9#J ...                                   [FIND THE NEXT HS DR DEVICE LIST 
   9 =9#Q ...      CALL  7  RECORD   
   9 =9*6 ...      LDX   6  PIPC(1)  
  20 =9*# ...                                   [FIND THE NEXT IPC DEVICE LIST   
   9 =9*G ...      CALL  7  RECORD   
   5 =9*N ...XAECAN  
   8 =9*W ...      LDX   2  FX2  
   8 =9B4 ...      LDX   1  FX1  
   9 =9B= ...      LDX   6  AWORK1(2)
   9 =9BD ...      BZE   6  PNCONS   
   9 =9BL ...      ORX   5  TEMEX(1) 
   8 =9BS ...      BRN      PAPIA
   5 =9C2 ...PNCONS  
   9 =9C8 ...      ANDX  5  TWO(1)   
   5 =9CB ...PAPIA   
   4 =9CJ ...[   
  14 =9CQ ...[CANCEL ALL ASSOCIATIONS OF PROP FROM APIA  
   4 =9CY ...[   
  10 =9D6 ...      LDX   6  PTREADER(1)  
  17 =9D# ...                                   [FIND TR DEVICE LIST 
   9 =9DG ...      CALL  7  RECORD2  
   8 =9DN ...      LDX   1  FX1  
  10 =9DW ...      LDX   6  PCREADER(1)  
  17 =9F4 ...                                   [FIND CR DEVICE LIST 
   9 =9F= ...      CALL  7  RECORD2  
   8 =9FD ...      LDX   2  FX2  
   4 =9FL ...#   
   4 =9FS ...#   
   7 =9G2 ...#SKI  JSKI04<1$1
   4 =9G8 ...(   
   5 =9GB ...XIDFCAN 
   8 =9GJ ...      LDX   1  FX1  
  17 =9GQ ...      ANDX  5  TWOBITS(1)          [UNSET BIT0 & BIT1   
   9 =9GY ...      LDX   6  AWORK1(2)
  18 =9H6 ...      BPZ   6  XNIDF               [IF NOT CONS PROP BRANCH 
  15 =9H# ...      ORX   5  TEMEX(1)            [SET BIT 0   
   5 =9HG ...XNIDF   
   9 =9J= ...      PROPCANC XBRK,5   
   4 =9JP ...)   
  21 =9JQ ...      DELETE                       [DELETE RECORD OF PROPERTY FROM SYSPR
  21 =9JS ...                                   [OUTPUT 'PROPERTY CANCELLED'MESSAGE  
   9 =9K2 ...      MHUNT 3,CPB,CUNI  
   9 =9K8 ...      LDX   4  ANUM(3)  
   8 =9KB ...      ANDN  4  #7777
  11 =9KJ ...      OUTPARAM 4,APARA,CPB,CUNI 
  10 =9MG          INFORM 1,JPRCANOK,1   
   8 ==76          BRN      PEND 
   4 ==LQ    #   
  10 ==_7 ...XBRK  GEOERR   1,CCPRBKIN   
   4 =??J ...#   
   4 =?L2    #END
   8 ____ ...70052073000100000000
