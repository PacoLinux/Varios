  14 22FL          SEG      RENAMDIR,,N.R.BOULT,LIBRARY  
  13 22_=          SEGENTRY K1RENAMDIR,QK1RENAMDIR   
  10 23DW    ZNOTNOWN       +ERUNKNOWN   
   9 23YG    ZNOTOWN        +ERNOTOWN
  10 24D6    ZPRONG         +ERDESCWRNG  
   9 24XQ    ZERMAK         +ERMAKAL 
   9 25CB    ZERNAM         +ERNAME  
   9 25X2    ZJGLOCNA       +JGLOCNA 
  10 26BL    ZOPWRNG        +EROPWRONG   
   9 26W=    QMASK          #77377777
  10 27*W    POOLHD         12HPOOL TAPE 
   9 27TG    XDUMP          12HDUMPER
   6 28*6    QK1RENAMDIR 
  10 28SQ          MHUNT    3,FILE,FABSNB
  10 29#B          LDX   6  ALOGLEN(3)   
   8 29S2          SMO      A1(3)
  13 2=?L          LDX   7  A1+1(3)             [TSN 
  20 2=R=          SSEARCH  NOTNOWN,7           [LOOK FOR ENTRY IN :SYSTEM.SERIAL
   8 2?=W          LDX   0  5(3) 
   7 2?QG          SLC   0  3
   7 2#=6          ANDN  0  7
   9 2#PQ          BNZ   0  NOTOWN   
   8 2*9B          LDX   0  5(3) 
   7 2*P2          SLC   0  5
  20 2B8L          BNG   0  OPWRNGA             [BRANCH IF TAPE OPERATOR WRONGED 
   7 2BN=          SLC   0  2
   7 2C7W          ANDN  0  1
  16 2CMG          STO   0  AWORK1(2)           [INITIAL Q STATUS
  10 2D76          MHUNT    2,FILE,FABSNB
   7 2DLQ          LDN   4  3
   4 2F6B    OWNL
   7 2FL2          SMO      4
   8 2G5L          LDX   0  A1(2)
   7 2GK=          SMO      4
  18 2H4W          BXU   0  1(3),NAME           [BRANCH IF WRONG DIR NAME
   8 2HJG          BCT   4  OWNL 
   7 2J46          STEPWRITE 
   7 2JHQ          LDCT  0  4
  15 2K3B          ORS   0  5(3)                [SET Q STATUS
   7 2KH2          LDN   0  1
   8 2L2L          ADS   0  5(3) 
   8 2LG=          LDX   2  FX2  
   9 2L_W          STO   0  AWORK2(2)
   7 2MFG          CLOSETOP  
   9 2M_6    #     GET   TAPE PUT UP 
  14 2NDQ          TAPEOPEN YBRKIN,7,,NOQUERY,NOTOL,NOSS 
  20 2NYB ...NOPEN REPERR   OPEN                [OUTPUT ERROR MESSAGE UNLESS OK  
   9 2PD2          BRN      NOTAVA   
   4 2PXL    OPEN
  13 2QC=          OPENSYS  YBRKIN,DOCUMENT,GENERAL  
  20 2QWW          SDSEARCH ,TSN,7              [FIND ENTRY IN :SYSTEM.DOCUMENT  
  12 2RBG          OPENSYS  XBRKIN,SERIAL,GENERAL
  19 2RW6          SSEARCH  ,7                  [FIND ENTRY IN :SYSTEM.SERIAL
   8 2S*Q          LDX   0  5(3) 
  18 2STB          BNG   0  TNOTOWN             [BRANCH IF TO BE RETURNED
   7 2T*2          SLL   0  2
  18 2TSL          BNG   0  TNOTOWN             [BRANCH IF TO BE DEADED  
   7 2W#=          SLL   0  3
  19 2WRW          BNG   0  OPWRNG              [BRANCH IF OPERATOR WRONGED  
  14 2X?G          LDX   5  4(3)                [OWNER   
   8 2XR6          LDX   4  3(3) 
   8 2Y=Q          LDX   3  2(3) 
   9 2YQB          TXU   3  XDUMP(1) 
  10 2_=2          TXU   4  XDUMP+1(1)   
  10 2_PL          TXU   5  XDUMP+2(1)   
  21 329=          BCC      OPEN2               [BRANCH IF TAPE BELONGS TO :DUMPER   
   8 32NW          STEPAGAIN 1   
   7 338G          LDN   4  3
   5 33N6    PLOOP   
   7 347Q          SMO      4
   8 34MB          LDX   0  2(3) 
   7 3572          SMO      4
  11 35LL          BXU   0  POOLHD-1(1),OPEN2
   8 366=          BCT   4  PLOOP
  10 36B4 ...      SEGENTRY K40RENAMDIR  
   8 36KW          BRN      POOL 
   5 375G    OPEN2   
  11 37K6          SETUPCORE 6,2,FILE,FABSNB 
  11 384Q          HUNT2    1,FILE,FABSNB,2  
   9 38JB          LDX   0  ATYPE(1) 
   7 3942          ORN   0  1
  15 39HL          DSA   0  ATYPE(2)            [SYNTAX BITS 
   8 3=3=          ADN   1  A1   
   8 3=GW          ADN   2  A1   
   7 3?2G          LDX   3  6
  15 3?G6          MOVE  1  0(3)                [COPY FABSNB 
   8 3?_Q          ADX   2  0(2) 
   7 3#FB          SBN   2  7
   7 3#_2          LDN   1  6
   5 3*DL    OPEN3   
   7 3*Y=          SMO      1
  17 3BCW          STOZ     0(2)                [REMOVE LOCAL NAME   
   8 3BXG          BCT   1  OPEN3
  12 3CC6          OPENDIR  WBRKIN,GENERAL,QUERY 
   9 3CWQ          TESTREP  OK,OPEN4 
  14 3DBB          ERRTEST  3,TEND2,NAME,NOUSER,NOFILE   
   9 3DW2          GEOERR   1,RENDREP
   5 3F*L    OPEN4   
  10 3FT=          MFREE    FILE,FABSNB  
  13 3G#W    #     IS DIRECTORY ENTRY ALREADY RIGHT? 
   8 3GSG          STEPAGAIN 2   
  10 3H#6          MHUNT    2,FILE,ENT   
   7 3HRQ          LDN   1  4
   4 3J?B    SAME
   7 3JR2          SMO      1
  19 3K=L          LDX   0  ELOC1-1(2)          [LOCAL NAME FROM DIRECTORY   
   7 3KQ=          SMO      1
  10 3L9W          BXU   0  2(3),SAME1   
   8 3LPG          BCT   1  SAME 
  15 3M96          LDX   0  7(3)                [GENERATION  
   8 3MNQ          ANDN  0  #7777
  10 3N8B          BXU   0  EGEN(2),SAME1
   8 3NN2          SMO      FX2  
   9 3P7L          STOZ     AWORK2   
   8 3PM=          BRN      SHDDR
  14 3Q6W    #     IS THE NAME IN THE PARAMETER RIGHT?   
   5 3QLG    SAME1   
  10 3R66          MHUNT    2,FILE,FABSNB
   7 3RKQ          LDN   1  3
   8 3S5B          ADX   2  A1(2)
   5 3SK2    SAME2   
   7 3T4L          SMO      1
  17 3TJ=          LDX   0  A1-7(2)             [NAME FROM PARAMETER 
   7 3W3W          SMO      1
  19 3WHG          BXU   0  2(3),RERR2          [BRANCH IF PARAMETER WRONG   
   8 3X36          BCT   1  SAME2
   9 3XGQ          LDX   0  A1-3(2)  
  19 3Y2B          ANDN  0  #777                [REEL NUMBER FROM PARAMETER  
  17 3YG2          BXU   0  6(3),RERR2          [J IF WRONG REEL NO. 
  19 3Y_L          LDX   0  7(3)                [GENERATION NO. FROM HEADER  
  18 3_F=          ANDN  0  #7777               [TRUNCATE GENERATION NO. 
   9 3_YW          LDX   1  A1-2(2)  
  20 42DG          ANDN  1  #7777               [GENERATION NUMBER FROM PARAMETER
   9 42Y6          BXU   0  1,RERR2  
  11 43CQ          SETNCORE 6,3,FILE,FLOCNB  
  14 43XB    #     CHECK NAME NOT ALREADY USED BY FILE   
  10 44C2          MHUNT    2,FILE,FABSNB
   8 44WL          ADN   3  A1   
   8 45B=          ADX   2  A1(2)
   8 45TW          ADN   2  A1-6 
  19 46*G          MOVE  2  3                   [MOVE LOCAL NAME INTO FLOCNB 
   8 46T6          STOZ     3(3) 
   8 47#Q          STOZ     4(3) 
   8 47SB          STOZ     5(3) 
   7 48#2          SETUPTAB  
  19 48RL          TABSET                       [REMEMBER POSITION IN DIR.   
   7 49?=          GETDIR   1
  21 49QW          TESTREP  OK,RERR3            [BRANCH IF NAME ALREADY USED BY FILE 
  20 4==G          TABULATE                     [REPOSITION AT DIRECTORY ENTRY   
   7 4=Q6          FREETAB   
  20 4?9Q    #     CORRECT LOCAL NAME, GENERATION AND REL NO. IN DIRECTORY ENTRY 
  10 4?PB          MHUNT    2,FILE,FABSNB
   8 4#92          ADX   2  A1(2)
   8 4#NL          ADN   2  A1-6 
  10 4*8=          MHUNT    3,FILE,ENT   
   8 4*MW          ADN   3  ELOC1
  20 4B7G          MOVE  2  6                   [ALTER NAME IN DIR5CTORY ENTRY   
   8 4BM6          SBN   3  ELOC1
   8 4C6Q          LDN   0  #777 
   9 4CLB          ANDS  0  EREEL(3) 
   8 4D62          LDN   0  #7777
   9 4DKL          ANDS  0  EGEN(3)  
  10 4F5=          MHUNT    1,FILE,FLOCNB
   7 4FJW          LDN   4  1
  16 4G4G          ORS   4  ATYPE(1)            [SET MAG TAPE BIT
   7 4GJ6          LDX   4  2
   8 4H3Q          LDN   5  A1(1)
  19 4HHB          MOVE  4  5                   [SET UP DETAILS FOR GETDIR   
   8 4J32          LDN   0  #7777
  14 4JGL          ANDX  0  A1+4(1)             [GEN NO. 
  18 4K2=          BNZ   0  UPD                 [J IF NOT ZERO OTHERWISE 
  19 4KFW          LDCT  0  #100                [SET BIT INDICATING ZERO GEN.
   9 4K_G    UPD   STO   0  A1+4(1)  
   9 4LF6          LDX   4  ENUSE(3) 
  10 4LYQ          NAMETOP  3,FILE,FRB   
  19 4MDB          LDX   5  4                   [LOAD NO. OF TRAPS RECORDS   
   8 4MY2          BZE   4  UPD2 
   6 4NCL    UPD1  READ  
   8 4NX=          BCT   4  UPD1 
   7 4PBW    UPD2  LDX   4  5
   7 4PWG          ADN   4  1
   5 4QB6    UPD2A   
   6 4QTQ          DELETE
   8 4R*B          BCT   4  UPD2A
   7 4RT2          ADN   5  1
   7 4S#L          GETDIR   2
  12 4SS=          TESTREP  OK,UPD3,NOFILE,UPD3  
  10 4T?W          GEOERR   1,RENAMDIR   
   4 4TRG    UPD3
  10 4W?6          MFREE    FILE,FLOCNB  
   4 4WQQ    UPD4
   7 4X=B          LDX   4  5
   4 4XQ2    UPD5
   9 4Y9L          HUNT2 2,FILE,FRB  
   8 4YP=          BCT   4  UPD5 
  17 4_8W          NAMETOP  2,FILE,FWB          [RECORD TO WRITE AWAY
  19 4_NG          INSERT                       [SET UP NEW DIRECTORY ENTRY  
  12 5286          TESTREP  OK,UPD6,FNEARLY,UPD6 
  10 52MQ          GEOERR   1,RENAMDIR   
   4 537B    UPD6
   9 53M2          MFREE    FILE,FWB 
   8 546L          BCT   5  UPD4 
   9 54L=          STOZ     AWORK2(2)
   5 555W    SHUTDIR 
   7 55KG          CLOSETOP  
  12 5656    #     UPDATE QUERY STATUS AND FINISH
   4 56JQ    TEND
   7 574B          CLOSEDIR  
   5 57J2    TEND0   
   7 583L          STEPWRITE 
  19 58H=          SSCOR    0(3),DEC            [DECREASE COUNT OF REQUESTERS
  15 592W          LDX   0  AWORK2(2)           [QUERY STATUS
  18 59GG          BNZ   0  TEND1               [LEAVE AT QUERY STATUS   
   9 5=26          LDX   4  QMASK(1) 
  17 5=FQ          ANDS  4  5(3)                [REMOVE Q STATUS BIT 
   5 5=_B    TEND1   
   7 5?F2          CLOSETOP  
   9 5?YL          LDX   0  CPPTR(2) 
   9 5#D=          BZE   0  TEND1A   
  10 5#XW          UNLOAD   ,,,AUT,UNL   
   5 5*CG    TEND1A  
   6 5*X6          ENDCOM
   5 5BBQ    TEND2   
   8 5BWB          LDX   2  FX2  
   9 5CB2          LDX   0  AWORK1(2)
   9 5CTL          STO   0  AWORK2(2)
   8 5D*=          BRN      TEND 
  14 5DSW    #     THIS SECTION DEALS WITH ERROR MESSAGES
   5 5F#G    RERR2   
   8 5FS6          SMO      FX1  
   9 5G?Q          LDX   3  ZPRONG   
   7 5GRB          NGN   4  1
   8 5H?2          BRN      RERR 
   5 5HQL    RERR3   
   8 5J==          LDX   1  FX1  
   9 5JPW          LDX   3  ZERMAK(1)
  10 5K9G          LDX   4  ZJGLOCNA(1)  
   4 5KP6    RERR
   8 5L8Q          ERRORX   3,4  
   9 5LNB          LDX   0  AWORK1(2)
  21 5M82          BNZ   0  SHUTDIR            [BRANCH UNLESS Q STATUS JUST SPOTTED  
  10 5MML          DOWN     LIBQUERY,2   
   8 5N7=          BRN      TEND 
   5 5NLW    SHDDR   
  10 5P6G          ERROR    ERHDDRSAME   
   9 5PL6          BRN      SHUTDIR  
   5 5Q5Q    NOTNOWN 
  10 5QKB          LDX   4  ZNOTNOWN(1)  
   8 5R52          BRN      TERR 
   5 5RJL    NOTOWN  
   8 5S4=          SMO      FX1  
   9 5SHW          LDX   4  ZNOTOWN  
   8 5T3G          BRN      TERR 
   4 5TH6    NAME
   9 5W2Q          LDX   4  ZERNAM(1)
   8 5WGB          BRN      TERR 
   5 5X22    OPWRNGA 
  10 5XFL          LDX   4  ZOPWRNG(1)   
   4 5X_=    TERR
   7 5YDW          CLOSETOP  
   7 5YYG          COMERRX  4
   5 5_D6    NOTAVA  
  12 5_XQ          OPENSYS  NOTAVA,SERIAL,GENERAL
   8 62CB          SSEARCH  ,7   
   8 62X2          BRN      TEND0
   5 63BL    OPWRNG  
  10 63W=          LDX   4  ZOPWRNG(1)   
   8 64*W          BRN      TNOT 
   5 64TG    TNOTOWN 
   8 65*6          LDX   0  5(3) 
   7 65SQ          SLC   0  6
   7 66#B          ANDN  0  1
   9 66S2          STO   0  AWORK2(2)
   8 67?L          SMO      FX1  
   9 67R=          LDX   4  ZNOTOWN  
   4 68=W    TNOT
   7 68QG          ERRORX   4
   8 69=6          BRN      TEND 
   9 69PQ    #     DEAL WITH BREAKIN 
   5 6=9B    WBRKIN  
  18 6=P2          CLOSEDIR                     [CLOSE SYSTEMHDOCUMENT   
   8 6?8L          BRN      YBRKA
   5 6?N=    XBRKIN  
  18 6#7W          CLOSETOP                     [CLOSE SYSTEM.DOCUMENT   
   5 6#MG    YBRKIN  
  12 6*76          OPENSYS  YBRKIN,SERIAL,GENERAL
   9 6*LQ          TESTREP  OK,YBRKA 
   9 6B6B          GEOERR   1,RENDREP
   5 6BL2    YBRKA   
   8 6C5L          SSEARCH  ,7   
   7 6CK=          STEPWRITE 
  19 6D4W          SSCOR    0(3),DEC            [DECREASE COUNT OF REQUESTERS
   7 6DJG          CLOSETOP  
   9 6F46          LDX   0  CPPTR(2) 
  19 6FHQ          BZE   0  YBRKB               [BRANCH IF TAPE NOT ALLOCATED
  10 6G3B          UNLOAD   ,,,AUT,UNL   
   5 6GH2    YBRKB   
   7 6H2L          COMBRKIN  
   9 6HG=    #     RETURN POOL TAPE  
  15 6HMD ...[ ENTRY POINT FOR RENAMEPOOL RESTORE TIME MACRO 
  10 6HSL ...      SEGENTRY K41RENAMDIR  
   4 6H_W    POOL
   7 6JFG          STEPAGAIN 
   7 6J_6          STEPWRITE 
  19 6KDQ          SSCOR    0(3),DEC            [DECREASE COUNT OF REQUESTERS
   7 6KYB          CLOSETOP  
   7 6LD2          CLOSETOP  
  10 6LXL          UNLOAD   ,,,AUT,UNL   
  11 6MC=          SETNCORE 2,3,AONBS,GLIST  
   7 6MWW          LDN   0  1
   8 6NBG          STO   0  A1(3)
   9 6NW6          STO   7  A1+1(3)  
  10 6P*Q          MHUNT    3,FILE,FABSNB
   7 6PTB          LDN   0  6
   8 6Q*2          SBS   0  A1(3)
   7 6QSL          LDN   0  0
   9 6R#=          DSA   0  ATYPE(3) 
   7 6RRW          RETURNB   
  10 6S?G          COMERR   ERPOOLTAPE   
  14 6S?_ ...[ ENTRYPOINT FOR ON ACTION FOR RENAMEPOOL   
  10 6S#D ...      SEGENTRY K42RENAMDIR  
   7 6S#X ...      CLOSETOP  
   7 6S*B ...      CLOSETOP  
   8 6SBR ...      JWPR     ,STAR
   9 6SC= ...      UNLOAD   ,,,UNL   
  15 6SCP ...      TAPEOPEN YBRKIN,7,WRITE,NOQUERY,NOTOL,NOSS
   8 6SCX ...      BRN      NOPEN
   9 6SD5 ...STAR  OUTPACK  7,1,TSN  
   9 6SD? ...      MONOUT   ARENPOOL 
  11 6SDF ...      SETNCORE 7,3,AONBS,GREN   
   8 6SDM ...      STOZ     A1(3)
  10 6SF6 ...      STOZ     ACOMMUNE2(2) 
  10 6SFK ...      HUNT     2,FILE,FABSNB
   8 6SG4 ...      ADX   2  A1(2)
   8 6SGH ...      ADN   2  A1-6 
   8 6SH2 ...      ADN   3  A1+1 
   7 6SHF ...      MOVE  2  6
  10 6SHY ...      DOWN     RENAMEMT,2   
   6 6SJC ...      NULL  
  10 6SJW ...      MFREE    AONBS,GREN   
   7 6SK* ...      PICKREP2 4
  13 6SKS ...      OPENSYS  YBRKIN,DOCUMENT,GENERAL  
   9 6SL? ...      SDSEARCH ,TSN,7   
  12 6SLQ ...      OPENSYS  XBRKIN,SERIAL,GENERAL
   8 6SM9 ...      SSEARCH  ,7   
   7 6SMN ...      PUTREP   4
   9 6SN7 ...      TESTREP  OK,OPEN2 
   9 6SNL ...      ERROR    FAILMAG  
   9 6SNT ...      STOZ     AWORK2(2)
   8 6SP5 ...      BRN      TEND 
   4 6SPJ ...[   
   4 6SQ3 ...[   
  11 6SR6          MENDAREA 50,K99RENAMDIR   
   4 6T=Q    #END
   8 ____ ...36111552000300000000
