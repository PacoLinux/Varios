  15 22FL ...      SEG      RITEFAIL,863,FRANK BLUNDELL,BSTS 
   4 22_= ...#   
  11 23DW ...      SEGENTRY K1RITEFAIL,QK1RI 
  11 23YG ...      SEGENTRY K2RITEFAIL,QK2RI 
  11 24D6 ...      SEGENTRY K3RITEFAIL,QK3RI 
  11 24XQ ...      SEGENTRY K4RITEFAIL,QK4RI 
  13 25CB ...      FSHENTRY K5RITEFAIL,QK5RI,,QK5RI  
  14 25X2 ...      FSHENTRY K7RITEFAIL,QK7RI,QK7RI,QK7RI 
  14 26BL ...      FSHENTRY K8RITEFAIL,QK8RI,QK8RI,QK8RI 
  13 26W= ...      FSHENTRY K9RITEFAIL,QK9RI,,QK9RI  
   4 27*W ...#   
   5 27TG ...STYPE   
  10 28*6 ...#HAL           +BSTB+BQBLK,0
   4 28SQ ...#   
   9 29#B ...TERR1 GEOERR   0,WRNGBLK
  14 29S2 ...# ***************************************   
   4 2=?L ...#   
  11 2=R= ...#     ENTRY FOR LISTFAIL MACRO  
   4 2?=W ...#   
   6 2?QG ...QK1RI [*****
  19 2#=6 ...      CALL  0  XBSA                [CONVERT LFN TO BSA ADDRESS  
  21 2#PQ ...      CALL  0  XLIST               [CHANGE ENTRY FROM ONE BLOCK TO NEXT 
   5 2*9B ...      UP
  14 2*P2 ...# ***************************************** 
   4 2B8L ...#   
  10 2BN= ...# ENTRY FOR TIDYFAIL MACRO  
  21 2C7W ...# CHANGES BLOCK NUMBERS,REQUEUES APPROPRIAT Q BLOCKS, REMOVES ENTRY IN  
  17 2CMG ...# CURRENT FAIL LIST AND UNSETS FAIL BITS IF NECESSARY   
   4 2D76 ...#   
  11 2DLQ ...#     ACOMMUNE1  CONTAINS  LFN  
  21 2F6B ...#     ACOMMUNE2  CONTAINS  BLOCK NUMBER OF FAILED BLOCK (TO BE REPLACED)
  16 2FL2 ...#     ACOMMUNE3  CONTAINS  BLOCK NUMBER OF NEW BLOCK
   4 2G5L ...#   
   4 2GK= ...#   
   6 2H4W ...QK2RI [*****
   9 2HJG ...#SKI  K6RITEFAIL>99-99  
   4 2J46 ...(   
  12 2JHQ ...      TRACE    ACOMMUNE1(2),TIDYFAIL
   9 2K3B ...#SKI  K6RITEFAIL>199-199
   4 2KH2 ...(   
  10 2L2L ...      TRACE    ACOMMUNE2(2) 
  10 2LG= ...      TRACE    ACOMMUNE3(2) 
   4 2L_W ...)   
   4 2MFG ...)   
  10 2M_6 ...      LDX   5  ACOMMUNE1(2) 
  10 2NDQ ...      LDX   6  ACOMMUNE2(2) 
  10 2NYB ...      LDX   7  ACOMMUNE3(2) 
  10 2PD2 ...      LDX   1  ACOMMUNE1(2) 
  10 2PMS ...      STO   1  ACOMMUNE6(2) 
   7 2PXL ...      SLL   1  1
   8 2QC= ...      ADX   1  ALFTP
   8 2QWW ...      LDX   4  A1(1)
  20 2RBG ...      BNG   4  XNFILE              [JUMP IF THIS L.F DOES NOT EXIST 
   8 2RW6 ...      ANDN  4  #7777
  19 2S*Q ...      ADX   4  AFDTP               [POINTER TO FILE DESCRIPTION 
  11 2STB ...      DATUMBLK ACOMMUNE3(2),4   
  20 2T*2 ...      BZE   1  XNFILE              [JUMP IF BLOCK NUMBEROUT OF RANGE
   8 2TSL ...      CALL  0  XFAIL
  10 2T_S ...      LDX   0  ACOMMUNE6(2) 
  13 2W72 ...      STO   0  ACOMMUNE1(2)         [LFN
  19 2W#= ...      CALL  0  XBSA                [CONVERT LFN TO ADDRESS BSA  
  10 2WRW ...      LDX   4  ACOMMUNE3(2) 
  10 2X?G ...      STO   4  ACOMMUNE2(2) 
  19 2XR6 ...      CALL  0  XWAIT               [REQUEUE APPROPRIATE Q BLOCKS
  10 2Y=Q ...      LDX   0  ACOMMUNE4(2) 
  15 2YQB ...      BNZ   0  RNJ1                [J IF BLANKET
   8 2_=2 ...      CALL  0  XLIST
   9 2_PL ...RNJ2  LDX   1  AWORK1(2)
  17 329= ...      CALL  7  UNSTFB              [ CLEAR OUT FAIL BIT 
   5 32NW ...      UP
   8 338G ...RNJ1  LDN   0  RNJ2 
  13 33N6 ...      STO   0  AWORK4(2)           [LINK
  20 347Q ...      BRN      YLIST               [REDUCE COUNT OF CURRENT FAILS   
   4 34MB ...#   
   5 3572 ...XNFILE  
   9 35LL ...      GEOERR   0,WRNGLFN
  14 366= ...# ***************************************:  
   4 36KW ...#   
  10 375G ...# ENTRY FOR BADBLOCK MACRO  
  20 37K6 ...# UNSETS FAIL BITS AND REQUEUES ALL ONLINE Q BLOCKS FOR A GIVEN BSUN
   4 384Q ...#   
  10 38JB ...# ACOMMUNE1   CONTAINS BSUN 
   4 3942 ...#   
   4 39HL ...#   
   4 3=3= ...#   
   4 3=GW ...#   
   6 3?2G ...QK3RI [*****
   9 3?G6 ...#SKI  K6RITEFAIL>99-99  
  12 3?_Q ...      TRACE    ACOMMUNE1(2),BADBLOCK
  10 3#FB ...      LDX   1  ACOMMUNE1(2) 
  20 3#_2 ...      CALL  0  XBSB                [CONVERT BSUN TO  ADDRESS OF BSA 
  10 3*DL ...      STOZ     ACOMMUNE2(2) 
  18 3*Y= ...      CALL  0  XWAIT               [REQUE ONLINE Q BLOCKS   
   9 3BCW ...      SMO      AWORK1(2)
   9 3BXG ...      LDX   0  BSFAIL   
  19 3CC6 ...      ANDN  0  BOFB                [IF ONLINE FAIL BIT NOT SET  
  17 3CWQ ...      BZE   0  TUP                 [THEN DONT UNSET IT! 
  11 3DBB ...      UNSETFB  2,ACOMMUNE1(2)   
   5 3DW2 ...TUP   UP
  15 3F*L ...# ********************************************  
   4 3FT= ...#   
  15 3G#W ...#     SR TO REQUE  BLOCKS FROM  BSWQ TO BSA Q.  
  13 3GSG ...#     AWORK1    CONTAINS  ADDRESS OF BSA
  13 3H#6 ...#     ACOMMUNE1 CONTAINS  LFN OR BSUN   
  18 3HRQ ...#     ACOMMUNE2 CONTAINS  ZERO IF BSUN IN 1 OR  BLOCK NUMBER
  12 3J?B ...#     LINK  X0  STORED IN ACOMMUNE6 
   4 3JR2 ...#   
   4 3K=L ...#   
   5 3KQ= ...XWAIT   
  10 3L9W ...      STO   0  ACOMMUNE6(2) 
   9 3LPG ...      LDX   1  AWORK1(2)
   7 3M96 ...      LDN   7  0
  10 3MNQ ...      LDX   0  ACOMMUNE2(2) 
  16 3N8B ...      BNZ   0  SCA                 [JUMP IF OFFLINE 
   7 3NN2 ...      LDN   7  2
  16 3P7L ...SCA   LDN   6  BSQWCHAN(1)         [BASE OF WAIT Q  
   7 3PM= ...      LDX   3  6
  17 3Q6W ...SH    LDX   3  FPTR(3)             [ADDRESS OF Q BLOCK  
   8 3QLG ...SHA   LDX   2  FX2  
   7 3R66 ...      TXU   3  6
  16 3RKQ ...      BCC      SD                  [JUMP IF END OF Q
   9 3S5B ...      SBN   3  CHAINADD 
  21 3SK2 ...      LDN   4  BA1(3)              [RESCUE PARAMETERS BA AREA IF ACTIVIT
  18 3T4L ...      LDX   1  EXEC3(3)            [DATUMISED BLOCK NUMBER  
   9 3TJ= ...      LDX   0  ATYPE(3) 
   8 3W3W ...      SMO      FX1  
   8 3WHG ...      TXU   0  STYPE
   8 3X36 ...      BCS      SR1  
  10 3XGQ ...      LDX   1  ADBLKNO(3)   
   9 3Y2B ...      LDN   4  ATRAN(3) 
   9 3YG2 ...SR1   LDN   5  AWORK2(2)
   7 3Y_L ...      MOVE  4  3
   9 3_F= ...      LDX   0  AWORK2(2)
   7 3_YW ...      ANDN  0  2
   7 42DG ...      ERX   0  7
  20 42Y6 ...      BNZ   0  SG                  [JUMP IF WRONG TYPE OF TRANSFER  
  16 43CQ ...      BNZ   7  SY                  [JUMP IF ONLINE  
   9 43XB ...      LDX   0  AWORK3(2)
   8 44C2 ...      ANDN  0  #377 
  10 44WL ...      TXU   0  ACOMMUNE1(2) 
  17 45B= ...      BCS      SG                  [JUMP IF WRONG LFN   
   9 45TW ...      LDX   0  AWORK4(2)
   9 46*G ...      ANDX  0  BSTSMASK 
  10 46T6 ...      TXU   0  ACOMMUNE2(2) 
   8 47#Q ...      BCS      SG   
  10 47SB ...SY    LDX   4  CHAINADD(3)  
   9 48#2 ...#SKI  K6RITEFAIL>199-199
   4 48RL ...(   
   9 49?= ...      TRACE    3,REQUE  
   8 49QW ...      TRACE    6,   
   4 4==G ...)   
   9 4=Q6 ...      ADN   3  CHAINADD 
   7 4?9Q ...      OFFQUE   3
   9 4?PB ...      SBN   3  CHAINADD 
  10 4#92 ...      OPTRAN   3,1,AWORK1(2)
   7 4#NL ...      LDX   3  4
   8 4*8= ...      BRN      SHA  
  16 4*MW ...SG    ADN   3  CHAINADD            [ADJUST POINTER  
   8 4B7G ...      BRN      SH   
   8 4BM6 ...SD    LDX   1  FX1  
   8 4C6Q ...      LDX   2  FX2  
  10 4CLB ...      LDX   0  ACOMMUNE6(2) 
   7 4D62 ...      EXIT  0  0
  18 4DKL ...# **********************************************************
   4 4F5= ...#   
  21 4FJW ...#     SR TO  CONVERT LFN IN ACOMMUNE1  TO  ADDRESS OF APPROPRIATE  BSA  
  18 4G4G ...#     ADDRESS OF BSA LEFT IN AWORK1  X1,X2 RESET TO FX1,FX2 
   7 4GJ6 ...#     LINK X0   
   4 4H3Q ...#   
   4 4HHB ...#   
   4 4J32 ...XBSA
  10 4JGL ...      LDX   1  ACOMMUNE1(2) 
   7 4K2= ...      SLL   1  1
   8 4KFW ...      ADX   1  ALFTP
   8 4K_G ...      LDX   1  A1(1)
   8 4LF6 ...      SRL   1  12   
   9 4LYQ ...XBSB  MPY   1  ASBSAB   
   7 4MDB ...      NGX   2  2
   8 4MY2 ...      ADX   2  BBAC 
   9 4NCL ...      ADX   2  ASBSAB   
   8 4NX= ...      SMO      FX2  
   9 4PBW ...      STO   2  AWORK1   
   8 4PWG ...      LDX   2  FX2  
   8 4QB6 ...      LDX   1  FX1  
   7 4QTQ ...      EXIT  0  0
  15 4R*B ...# *******************************************   
   4 4RT2 ...#   
  18 4S#L ...# SR TO REMOVE ANENTRY FROM THE CFL AND PLACE IT IN THE FFL 
  10 4SS= ...# X0 LINK STORED IN AWORK4  
  14 4T?W ...# LFN IN ACOMMUNE1,BLOCK NUMBER IN ACOMMUNE2
  11 4TRG ...# USES ALL ACCS  X1=FX1 X2=FX2  
   4 4W?6 ...#   
   5 4WQQ ...XLIST   
   8 4X=B ...      SBX   0  FX1  
   9 4XQ2 ...      STO   0  AWORK4(2)
  10 4Y9L ...      LDX   6  ACOMMUNE1(2) 
  10 4YP= ...      LDX   7  ACOMMUNE2(2) 
  12 4_8W ...XDA   HUNT2    2,BSTB,BFRAIL,ATFAPTR
  19 4_NG ...      LDX   0  A1(2)               [CHECK IF LIST BEING ALTERED 
  15 5286 ...      BPZ   0  XDB                 [JUMP  IF OK 
   6 52MQ ...      COOR2 
   8 537B ...      BRN      XDA  
   9 53M2 ...XDB   LDN   1  A1-1(2)  
   7 546L ...      NGN   0  1
  21 54L= ...XD    ADN   1  2                   [CHECK IF ROOM IN LIST FOR NEW ENTRY 
   8 555W ...      TXU   0  0(1) 
   8 55KG ...      BCS      XD   
   9 5656 ...      SBN   1  A1-3(2)  
  10 56JQ ...      TXL   1  ALOGLEN(2)   
   8 574B ...      BCS      XE   
  10 57J2 ...      LDX   4  ALOGLEN(2)   
   9 583L ...      ADN   4  AFRAILEX 
  16 58H= ...      LDCT  0  #400                [LOCKOUT MARKER  
   8 592W ...      ORS   0  A1(2)
   7 59GG ...      LDX   3  2
   8 5=26 ...      ALTLEN   3,4  
  12 5=FQ ...      HUNT2    3,BSTB,BFRAIL,ATFAPTR
   8 5=_B ...      LDCT  0  #400 
   8 5?F2 ...      ERS   0  A1(3)
   7 5?YL ...      LDX   4  3
  13 5#D= ...XEA   FINDFAIL 3,7,6,CFL,XERR,AWORK1(2) 
   7 5#XW ...      LDX   2  4
   7 5*CG ...      NGN   0  1
   7 5*X6 ...XF    ADN   2  2
  19 5BBQ ...      TXU   0  A1-1(2)             [SEARCH FOR END OF F.F LIST  
   8 5BWB ...      BCS      XF   
  16 5CB2 ...      LDX   0  0(3)                [SET UP RECORD   
  15 5CTL ...      STO   0  A1-1(2)             [BLOCK NUMBER
   7 5D*= ...      LDX   0  6
  12 5DSW ...      SRC   0  6                   [
  17 5F#G ...      STO   0  A1(2)               [ L.F.N  IN  B0-B23  
   7 5FS6 ...      NGN   0  1
  17 5G?Q ...      STO   0  A1+1(2)             [END OF BLOCK MARKER 
   7 5GRB ...      SBN   3  1
  21 5H?2 ...PA    ADN   3  1                   [REMOVE RECORD FROM CFL  BY MOVING   
  15 5HQL ...      LDX   4  2(3)                [REMAINDER UP
   8 5J== ...      STO   4  0(3) 
  17 5JPW ...      TXU   4  0                   [TEST FOR END OF LIST
   8 5K9G ...      BCS      PA   
   9 5KP6 ...      FSHCODE  B,XIAMA  
   4 5L8Q ...(   
  14 5LNB ...      LDN   4  3                   [LENGTH  
  18 5M82 ...      SETUPCORE 4,3,FSH,FSHBDEL    [USED BY DELBLKS MACRO   
  16 5MML ...      STO   4  A1(3)               [RECORD HEADER   
  15 5N7= ...      STO   6  A1+1(3)             [RESIDENCE NO
  14 5NLW ...      STO   7  A1+2(3)             [BLOCK NO
  21 5P6G ...      TRANSBEG FSHTFID,RITEFAIL,5,FSH,FSHBDEL,ACOMMUNE1,YLIST,AUT,SUP   
   5 5PL6 ...XIAMA   
   4 5Q5Q ...)   
   8 5QKB ...      LDX   2  FX2  
  13 5R52 ...YLIST HUNT2    3,BSTB,BFAIL,AWORK1(2)   
   7 5RJL ...      LDN   0  1
  19 5S4= ...      SBS   0  A1(3)               [SUBTRACT ONE FROM CFL COUNT 
   8 5SHW ...      LDX   1  FX1  
   8 5T3G ...      LDX   2  FX2  
   9 5TH6 ...      LDX   0  AWORK4(2)
   8 5W2Q ...      ADX   0  FX1  
   7 5WGB ...      EXIT  0  0
   7 5X22 ...XE    LDX   4  2
   8 5XFL ...      LDX   2  FX2  
   8 5X_= ...      BRN      XEA  
  10 5YDW ...XERR  GEOERR   0,CFRECLST   
   8 5YYG ...XFAIL LDX   2  FX2  
   8 5_D6 ...      SBX   0  FX1  
   9 5_XQ ...      STO   0  AWORK4(2)
  15 62CB ...      STO   1  ACOMMUNE1(2)        [PRESERVE X1 
  10 62X2 ...      DOWN     RITEFAIL,4   
   9 63BL ...      LDX   0  AWORK4(2)
   8 63W= ...      ADX   0  FX1  
   7 64*W ...      EXIT  0  0
   3 64TG ...
  16 65*6 ...# ***********************************************   
   4 65SQ ...#   
  14 66#B ...#       SUBROUTINE TO UNSET OFFLINE FAIL BIT
   4 66S2 ...#   
   5 67?L ...UNSTFB  
   8 67R= ...      UNSETFB  1,1  
   7 68=W ...      EXIT  7  0
   4 68QG ...#   
  15 69=6 ...# ********************************************  
   4 69PQ ...#   
  12 6=9B ...#    ENTRY POINT FOR FAILURE MACRO  
  12 6=P2 ...#               X5 = RESIDENCE NO   
  14 6?8L ...#               X6 = BLOCK NO TO BE REPLACED
  14 6?N= ...#               X7 = BLOCK NO TO BE INSERTED
  17 6#7W ...#               X1 = DATUMISED B.S. ADDRESS OF NEW BLOCK
   4 6#MG ...#   
   5 6*76 ...QK4RI   
  10 6*LQ ...      LDX   1  ACOMMUNE1(2) 
   7 6B6B ...      FAILURE   
   5 6BL2 ...      UP
   4 6C5L ...#   
   9 6CK= ...      FSHCODE  A,USLSS  
   4 6D4W ...(   
  17 6DJG ...# ****************************************************  
   4 6F46 ...#   
  17 6FHQ ...#    ENTRY POINT IN M/C A TO UPDATE FSH/FHSBDEL BLOCK   
   4 6G3B ...#   
   5 6GH2 ...QK5RI   
   9 6H2L ...      DELBLKS  ALLOWFAIL
   7 6HG= ...      TRANSRET  
   4 6H_W ...#   
   5 6JFG ...USLSS   
   4 6J_6 ...)   
   8 6KDQ ...      FSHCODE  AORB 
   4 6KYB ...(   
   4 6LD2 ...#   
  16 6LXL ...# ***********************************************   
   4 6MC= ...#   
  17 6MWW ...#    ENTRY POINT TO EITHER UPDATE PFL ENTRY OR DELETE   
  16 6NBG ...#    PFL ENTRY CORRESPONDING TO THAT IN OTHER M/C.  
   4 6NW6 ...#   
   9 6P*Q ...XCERR GEOERR   0,NOPFL  
   5 6PTB ...QK7RI   
  20 6Q*2 ...      CALL  0  XBSA                 [GET ADDRESS OF BSA IN THIS M/C 
  15 6QSL ...      LDX   7  ACOMMUNE2(2)         [BLOCK NO   
  16 6R#= ...      ANDX  7  BSTSMASK             [ROUNDED DOWN   
  17 6RRW ...      FINDFAIL 3,7,ACOMMUNE1(2),PFL,XCERR,AWORK1(2)  [  
  19 6S?G ...                                    [GET POINTER TO PFL ENTRY   
  20 6SR6 ...      LDX   0  ACOMMUNE3(2)         [NEW ENTRY WITH UPDATED BLOCKLET
  19 6T=Q ...      BZE   0  REMVL                [J IF ENTRY TO BE REMOVED   
  17 6TQB ...      STO   0  1(3)                 [BITS TO PFL ENTRY  
   4 6W=2 ...RETN
  16 6WPL ...      TRANSFIN ,AUT                 [BACK TO HOME   
  20 6X9= ...      TRANSRET                      [LETS GET THIS SUPERVISORY GOING
   5 6XNW ...REMVL   
  17 6Y8G ...      NGN   1  1                    [BLOCK TERMINATOR   
   4 6YN6 ...RUP 
  19 6_7Q ...      LDX   0  2(3)                 [SHIFT LOWER ENTRIES UP 2   
   8 6_MB ...      STO   0  0(3) 
   7 7272 ...      TXU   0  1
  18 72LL ...      BCC      RETN                 [J IF SHIFT UP COMPLETE 
   7 736= ...      ADN   3  1
   8 73KW ...      BRN      RUP  
   4 745G ...#   
  16 74K6 ...# ************************************************  
  17 754Q ...#    ENTRY POINT TO CREATE NEW PFL ENTRY IN BFAIL BLOCK 
   4 75JB ...#   
  12 7642 ...#                X5 = RESIDENCE NO  
  16 76HL ...#                X6,X7 = PFL ENTRY TO BE INSERTED   
   4 773= ...#   
   5 77GW ...QK8RI   
  15 782G ...      STO   5  ACOMMUNE1(2)        [RESIDENCE NO
  19 78G6 ...      CALL  0  XBSA                [GET ADDRESS OF THIS M/CS BSA
   5 78_Q ...SHFS5   
  20 79FB ...      HUNT2    3,BSTB,BFAIL,AWORK1(2) [AND ASSOCIATED BFAIL BLOCK   
   8 79_2 ...      LDX   0  A1(3)
  21 7=DL ...      BPZ   0  SHFS6               [J IF BLOCK NOT BEING TAMPERED WITH  
  15 7=Y= ...      COOR2                        [ELSE WAIT   
   8 7?CW ...      BRN      SHFS5
   5 7?XG ...SHFS6   
   9 7#C6 ...      LDN   2  A1-1(3)  
   7 7#WQ ...      NGN   0  1
   5 7*BB ...SHFS7   
   7 7*W2 ...      ADN   2  2
   8 7B*L ...      TXU   0  0(2) 
  20 7BT= ...      BCS      SHFS7               [J IF TERMINATOR NOT FOUND YET   
   9 7C#W ...      SBN   2  A1-3(3)  
  20 7CSG ...      TXL   2  ALOGLEN(3)          [HAS BLOCK GOT TO BE LENGTHENED? 
  14 7D#6 ...      BCS      SHFS8               [J IF NOT
  10 7DRQ ...      LDX   4  ALOGLEN(3)   
  15 7F?B ...      ADN   4  AFAILEX             [NEW LENGTH  
   8 7FR2 ...      LDX   5  GSIGN
   8 7G=L ...      ORS   5  A1(3)
   8 7GQ= ...      ALTLEN   3,4  
  13 7H9W ...      HUNT2    3,BSTB,BFAIL,AWORK1(2) [ 
  20 7HPG ...      ERS   5  A1(3)               [CLEAR 'BEING TAMPERED WITH' BIT 
   7 7J96 ...      NGN   0  1
   5 7JNQ ...SHFS8   
   7 7K8B ...      ADN   3  2
  17 7KN2 ...      TXU   0  A1-1(3)             [END OF BLOCK YET?   
  14 7L7L ...      BCS      SHFS8               [J IF NOT
  21 7LM= ...      STO   0  A1+1(3)             [TERMINATOR FOUND - SO STORE AGAIN   
  18 7M6W ...      STO   6  A1-1(3)             [NEW ENTRY INSERTED HERE 
   8 7MLG ...      STO   7  A1(3)
   8 7N66 ...      BRN      RETN 
   4 7NKQ ...)   
  14 7P5B ...# ******************************************
   4 7PK2 ...#   
  13 7Q4L ...# ENTRY FROM READFAIL SEGMENT (OVERFLOW)
  17 7QJ= ...# FINDS FCB IN MACHINE A WITH FCB NUMBER SPECIFIED BY X6
  18 7R3W ...# SETS CLOSE ABANDON MARKER AND MARKS FOR FILE TO BE THROWN 
  11 7RHG ...#   OFFLINE IF OPENED CAREFULLY 
   4 7S36 ...#   
   5 7SGQ ...QK9RI   
  10 7T2B ...      FSHCODE  A,XFSHAFCB   
   4 7TG2 ...(   
  17 7T_L ...      GFCBC    6,2,XFCBERR         [FIND PTR TO FCB REQD
  18 7WF= ...      BS       2,BFCLOSEAB         [SET CLOSE ABANDON MARKER
  14 7WYW ...      LDX   3  2                   [ -> FCB 
  19 7XDG ...      JMBC   NOTCAREF,2,BFCARE     [ J-NOT CAREFULLY OPEN FILE  
  18 7XY6 ...      MAPBCH   4,3                 [ CHECKS BIT IN FMAPP BLK
  17 7YCQ ...      BNZ   0  NOTOFFLINE          [ J IF SWAPPED BLOCK 
   4 7YXB ...[   
  18 7_C2 ...[ IF THE FILE IS OPEN CAREFULLY AND THE CORRUPT OR FAILING  
  16 7_WL ...[ BLOCK IN THE FILE IS A SWAPPED BLOCK THEN WE ONLY 
  16 82B= ...[ CLOSEABANDON THE FILE AND DO NOT THROW IT OFFLINE.
   4 82TW ...[   
   5 83*G ...NOTCAREF
  17 83T6 ...      BS   3,BFCLOSEABOF           [ THROW FILE OFFLINE 
   6 84#Q ...NOTOFFLINE  
  19 84SB ...      TRANSRET                     [RETURN SUPERVISORY ACTIVITY 
   5 85#2 ...XFCBERR 
  19 85RL ...      GEOERR   1,FCB NOTA          [FILE NOT OPEN ON MACHINE A  
   5 86?= ...XFSHAFCB
   4 86QW ...)   
   4 87=G ...#END
   6 ____ ...330406100002
