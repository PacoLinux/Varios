  12 22#C ...      SEG   WSJOBSA,867,SECTION CENT
   4 22LS ...[   
   4 22_=    [   
   4 2357 ...[   
  16 2394 ...[  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982 
   4 23#_ ...[   
  17 23DW    [     THIS SEGMENT IS THE OVERFLOW SEGMENT FOR WSJOBS   
   4 23YG    [   
  14 243L    [     VERSION 866 WAS PRODUCED FOR DS8221   
  15 246Q    [     AN ENHANCEMENT TO PROVIDE DETAILS OF FILES
  14 249W    [     FOR WHICH JOBS ARE WAITING.   7.12.81 
   4 24*2    [   
  19 24D6          SEGENTRY K1WSJOBSA,Q1ENTRY  [O/P LIVE JOB INFO. TO WS FILE
  11 24XQ          SEGENTRY K2WSJOBSA,Q2ENTRY
  11 25CB          SEGENTRY K3WSJOBSA,Q3ENTRY
  11 25X2          SEGENTRY K4WSJOBSA,Q4ENTRY
  14 26BL          FSHENTRY K7WSJOBSA,,Q7ENTRY,Q7ENTRY   
  14 26W=          FSHENTRY K8WSJOBSA,,Q8ENTRY,Q8ENTRY   
  14 27*W          FSHENTRY K9WSJOBSA,,Q9ENTRY,Q9ENTRY   
  14 27H4          FSHENTRY K10WSJOBSA,Q10ENTRY,,Q10ENTRY
  14 27N=          FSHENTRY K11WSJOBSA,Q11ENTRY,,Q11ENTRY
  12 27TG          BITDEFS  CONTEXT,12,XFSTART   
  12 28*6          BITDEFS  CONTEXT,16,XREMOTE   
  12 28SQ    [     DEFINES THE CQPRINT/CQ1 BLOCK 
  17 29#B    #DEF  ZGA = A1                     [GROUP A INDICATORS  
  17 29S2    #DEF  ZGB = ZGA+1                  [GROUP B INDICATORS  
  17 2=?L    #DEF  ZGC = ZGB+1                  [GROUP C INDICATORS  
  17 2=R=    #DEF  ZGD = ZGC+1                  [GROUP D INDICATORS  
  17 2?=W    #DEF  ZGE = ZGD+1                  [GROUP E INDICATORS  
  17 2?QG    #DEF  ZGF = ZGE+1                  [GROUP F INDICATORS  
  17 2#=6    #DEF  ZGG = ZGF+1                  [GROUP G INDICATORS  
  17 2#PQ    #DEF  ZJCOUNT = ZGG                [JOB COUNT FOR 'WJ'  
  17 2*9B    #DEF  ZGH = ZGG+1                  [GROUP H INDICATORS  
  14 2*P2    #DEF  ZNAME =  ZGH+1               [JOB NAME
  15 2B8L    #DEF  ZUSER =  ZNAME+3             [USER NAME   
  16 2BN=    #DEF  ZPROP =  ZUSER+3             [PROPERTY NUMBER 
  18 2G5L    #DEF  ZLCOUNT= ZPROP+1             [COUNT OF JOB NOS. STORED
  15 2GK=    #DEF  ZLJOB =  ZLCOUNT+1           [JOB NUMBER  
  11 2H4W    [     DEFINES THE CQPRINT/CQ11  
  15 2HJG    #DEF  WJOBNO  = A1                 [JOB NUMBER  
   9 2J46    #DEF  WNAME   = WJOBNO+1
   9 2JHQ    #DEF  WUSER   = WNAME+3 
   9 2K3B    #DEF  WMISC   = WUSER+3 
   9 2KH2    #DEF  WJLSTAT = WMISC   
   9 2L2L    #DEF  WYONQ   = WMISC+1 
   9 2LG=    #DEF  WCONTEXT= WYONQ+1 
  10 2L_W    #DEF  WSOURCE1= WCONTEXT+1  
  10 2MFG    #DEF  WSOURCE2= WSOURCE1+1  
  10 2M_6    #DEF  WSOURCE3= WSOURCE2+1  
  10 2NDQ    #DEF  WURGE   = WSOURCE3+1  
   9 2NYB    #DEF  WCPI    = WURGE+1 
   9 2PD2    #DEF  WTIMEJ  = WCPI+1  
   9 2PXL    #DEF  WARRIVED= WTIMEJ  
  10 2QC=    #DEF  WPROGTIME =WTIMEJ+2   
  10 2QWW    #DEF  WARRIVET= WPROGTIME   
  10 2RBG    #DEF  WCSIZE  = WPROGTIME+2 
   9 2RW6    #DEF  WFWT    = WCSIZE+1
   8 2STB    #DEF  WJLSTATX=WFWT 
   9 2T*2    #DEF  WTLR    = WFWT+1  
   9 2TSL    #DEF  WTSWAPS = WTLR+1  
  10 2W#=    #DEF  WMAXSIZE= WTSWAPS+1   
  10 2WRW    #DEF  WWT1    = WMAXSIZE+1  
   9 2X?G    #DEF  WWT2    = WWT1+1  
   9 2XR6    #DEF  WWT3    = WWT2+1  
   9 2Y=Q    #DEF  WEXTRA  = WWT3+1  
   9 2YQB    #DEF  WPERPTR = WEXTRA+1
  10 329=    #DEF  WCOREPRI = WPERPTR+1  
  10 32NW    #DEF  WMILLPRI = WCOREPRI+1 
  11 338G    #DEF  SIZOCQ11 = WMILLPRI+1-A1  
   8 35LL    #DEF  OCOREBIT = #20
   8 366=    #DEF  OWAITBIT = #40
   4 36KW    [   
  12 375G          BITDEFS  WEXTRA,0,XWELL,XLIMBO
  11 384Q          BITDEFS  WEXTRA,2,XEXPR   
  11 38JB          BITDEFS  EXT+11,9,YLIMBO  
   8 3942    XK9            +K9  
   9 39HL    XQEND          +FLOWQ   
  17 3CWQ    XPRESS         #34631463              [CPI OF EXP'D JOB 
  16 3F*L    [  S/R TO GET NEXT HIGHEST JOB NO. FROM CQPRINT/CQ1 
  12 3FT=    [  LAST JOB NO. IS HELD IN AWORK3   
  10 3G#W    NXNO  MHUNTW   3,CQPRINT,CQ1
  15 3GSG          LDX   6  AWORK3(2)           [LAST JOB NO.
   7 3H#6          NGN   4  1
  16 3HRQ          LDX   0  ZLCOUNT(3)       [COUNT OF LIVE JOBS 
   8 3J?B          BZE   0  NXNO9
  18 3JR2    NXNO2 BXGE  6  ZLJOB(3),NXNO4             [J IF ALREADY O/P 
  20 3K=L          BXL   4  ZLJOB(3),NXNO4             [J IF NOT SMALLEST JOB NO.
   9 3KQ=          LDX   4  ZLJOB(3) 
   7 3L9W    NXNO4 ADN   3  1
   8 3LPG          BCT   0  NXNO2
   8 3M96          BNG   4  NXNO9
  17 3MNQ          STO   4  AWORK3(2)           [NEXT HIGHEST JOB NO.
   7 3N8B    NXNO7 EXIT  5  1
   7 3NN2    NXNO9 EXIT  5  0
  13 3PM=    [     O/P START JOB ONFO. TO 'WS' FILE  
  19 3Q6W    Q1ENTRY                        [MULTIPLE JOB O/P(JOBS IN CQ1)   
   7 3W3W          LDN   7  0
   8 3WHG          BRN      NJB  
   5 3YG2    Q3ENTRY 
  19 44C2          MHUNTW   3,CQPRINT,CQ11       [PTR TO CQPRINT/CQ11 BLOCK  
  16 44WL          ORX   7  GSIGN                [XCODE INDICATOR
  19 45B=          STO   7  WJOBNO(3)            [SAVE XCODE IND.& JOB NO.   
   8 45TW          ERX   7  GSIGN
   8 46*G          BRN      NJB4 
  16 47SB    Q2ENTRY                            [SINGLE JOB O/P  
  16 4?PB          LDX   7  ACOMMUNE4(2)        [JOB NO. FOR O/P 
   8 4#92          ORX   7  GSIGN
   9 4#NL    NJB   STO   7  AWORK3(2)
  15 4*8=    [     WRITE THE REQUIRED INFO TO THE 'WS' FILE  
  10 4*MW          LDX   0  ACOMMUNE1(2) 
  18 4B7G          STO   0  AWORK4(2)               [DEPTH OF 'WS' FILE  
  10 4BM6          MHUNTW   3,CQPRINT,CQ1
   9 4C6Q          LDX   4  ZGH(3)   
  18 4CLB          STO   4  AWORK1(2)              [O/P LEVEL INDICATORS 
  16 4D62          BNG   7  NJB2             [J IF SINGLE JOB O/P
  15 4DKL    NJB1  CALL  5  NXNO                [GET JOB NO. 
  18 4F5=          BRN      OPF                 [J IF NO MORE JOB NOS.   
  11 4FJW    NJB2  MHUNTW   3,CQPRINT,CQ11   
  17 4H3Q          LDXC  7  AWORK3(2)               [GET JOB NO.    .
  15 4HHB          BCC      /                       [AND    .
  17 4J32          STO   7  WJOBNO(3)               [STORE IN CQ11   
  16 4JGL    NJB4  STOZ     A1+1(3)                 [ZEROISE   . 
  16 4K2=          LDN   5  A1+1(3)                 [CQ11 BLOCK  
   9 4KFW          LDN   6  A1+2(3)  
  10 4K_G          MOVE  5  SIZOCQ11-2   
   4 4RT2    NUJ 
  14 4S#L    [     STORE BASIC 'RUNNING' JOB INFORMATION 
   8 4SS=          JOBLOCK 7,2   
  17 4T?W          BNG   2  NSJ                 [J IF NO JOB BLOCK   
  17 4TRG          HLSAV    2,SET,SAVING,JOBQ,NSJ [J IF BEING SAVED  
   9 5FS6          LDN   4  JNAME(2) 
   9 5G?Q          LDN   5  WNAME(3) 
  14 5GRB          MOVE  4  3                   [JOB NAME
   9 5H?2          LDN   4  JUSER(2) 
   9 5HQL          LDN   5  WUSER(3) 
  15 5J==          MOVE  4  3                   [USER NAME   
   9 5JPW          LDX   4  JMISC(2) 
  14 5K9G          STO   4  WMISC(3)            [JMISC   
   9 5KP6          LDX   4  JURGE(2) 
   9 5L8Q          STO   4  WURGE(3) 
  17 5MML          JBC      NUJ0,2,JBEXPREQ     [J IF NOT EXPRESSED  
   9 5N7=          BS       3,XEXPR  
   4 5NLW    NUJ0
   9 5PL6          LDX   4  JCPI(2)  
   9 5Q5Q          STO   4  WCPI(3)  
   9 5RJL          LDX   4  ACOREP(2)
  16 5S4=          STO   4  WCOREPRI(3)         [CORE PRIORITY   
   9 5SHW          LDEX  0  JMISC(2) 
  17 5T3G          BZE   0  TML7                [J IF NO CORE INFO.  
   8 5TH6          FPCAJO   1,2  
   9 5W2Q          LDX   0  ASU1(1)  
   8 5WGB          STO   0  GEN4 
   9 5X22          LDX   0  ASU2(1)  
   8 5XFL          STO   0  GEN3 
   9 5X_=          LDX   0  ASU3(1)  
   8 5YDW          STO   0  GEN2 
   9 5YYG          LDX   0  ASU4(1)  
   8 5_D6          STO   0  GEN1 
   7 5_XQ          LDN   4  4
   9 62CB    TML3  LDCH  0  AMILLP(2)
   8 62X2          BNZ   0  TML5 
   7 63BL          SMO      4
   8 63W=          LDX   0  GEN0 
  18 64*W          BNG   0  TML5                [J IF MEMBER NOT ACTIVE  
   8 64TG          LDN   0  30   
  10 65*6    TML5  DCH   0  WMILLPRI(3)  
   7 65SQ          BCHX  3  /
   7 66#B          BCHX  2  /
   8 66S2          BCT   4  TML3 
   8 67?L          LDX   1  FX1  
  13 67R=          SBN   2  1         [RESET POINTERS
   7 68=W          SBN   3  1
   4 68QG    TML7
   9 69PQ          LDX   4  HTIMEJ(2)
   9 6=9B          STO   4  WTIMEJ(3)
  10 6=P2          LDX   4  HTIMEJ+1(2)  
  10 6?8L          STO   4  WTIMEJ+1(3)  
  10 6?N=          LDX   4  APROGTIME(2) 
  10 6#7W          STO   4  WPROGTIME(3) 
  11 6#MG          LDX   4  APROGTIME+1(2)   
  11 6*76          STO   4  WPROGTIME+1(3)   
   9 6*LQ          LDX   4  JCSIZE(2)
   9 6B6B          STO   4  WCSIZE(3)
   9 6CK=          LDXC  0  WJOBNO(3)
  16 6D4W          BCS      NUJ1                [J IF HLS XCODE  
   9 6F46          LDN   0  OCOREBIT 
   8 6FHQ          SMO      FX2  
   9 6G3B          ANDX  0  AWORK1   
  19 6GH2          BZE   0  NUJ3                [J IF PROG INFO. NOT REQ'D   
   4 6HG=    NUJ1
   9 6H_W          LDEX  4  JMISC(2) 
  17 6JFG          BZE   4  NUJ3                [J IF NO CORE IMAGE  
  17 6J_6          NGN   4  1                   [FWT IF NOT YET RUN  
   9 6KDQ          STO   4  WFWT(3)  
  15 6KYB          LDX   4  CLEANCT             [GET TIME NOW
   9 6LD2          LDX   5  JCFWT(2) 
  16 6LXL          JBS      NUJ15,2,JBPLUG      [J IF PLUGGED IN 
  16 6MC=          BNG   5  NUJ3                [J IF NOT YET RUN
   9 6MWW          SBX   4  APROCTIME
   9 6NBG          ADX   4  JCTLR(2) 
   5 6NW6    NUJ15   
  16 6P*Q          STO   4  WTLR(3)             [TIME LAST RUN   
   8 6PTB          LDX   6  GSIGN
  19 6Q*2          BNG   5  NUJ2                [J IF FWT NOT AVAILABLE YET  
   8 6QSL          LDX   1  FX1  
   9 6R#=          MPY   5  XK9(1)   
  16 6RRW    NUJ2  STO   6  WFWT(3)             [FAIR WAIT TIME  
   5 6S?G    #SKI  G4
   4 6SR6    (   
  18 6T=Q          STO   2  4                   [SAVE ADDR. OF JOBBLOCK  
   7 6TQB          FPCAJO   2
  10 6W=2          HUNT2    2,BSTB,BSCB  
  10 6WPL          LDX   5  ATSWAPS(2)   
  10 6X9=          LDX   6  AMAXSIZE(2)  
  10 6XNW          STO   5  WTSWAPS(3)   
  10 6Y8G          STO   6  WMAXSIZE(3)  
  17 6YN6          LDX   2  4                   [ADDR. OF JOBBLOCK   
   4 6_7Q    )   
  16 6_MB    NUJ3  FCAJO    2                   [FIND JOB'S CPAT 
  10 7272          LDN   4  JSOURCE1(2)  
  10 72LL          LDN   5  WSOURCE1(3)  
  16 736=          MOVE  4  3                   [STORE IN CQ11   
  10 73KW          LDX   4  CONTEXT(2)   
  15 745G          STO   4  WCONTEXT(3)         [CONTEXT BITS
  16 773=          JBC      NUJ35,2,YLIMBO      [J IF NOT LIMBO  
  18 77GW          BS       3,XLIMBO             [SET LIMBO BIT IN CQ11  
   9 78_Q    NUJ35 LDXC  0  WJOBNO(3)
  16 79FB          BCS      NUJ37               [J IF HLS XCODE  
   9 79_2          LDN   0  OWAITBIT 
   8 7?XG          SMO      FX2  
   9 7#C6          ANDX  0  AWORK1   
  18 7#WQ          BZE   0  NUJ6                [NO WAITING INFO. REQ'D  
   5 7F?B    NUJ37   
   8 7FR2          LDN   6  FLOWQ
   7 7G=L    NUJ4  LDX   2  6
   9 7GQ=          LDX   2  FPTR(2)  
  16 7MLG          BXE   2  XQEND(1),NUJ6       [J IF NOT WAITING
   6 7N66    XFSHNBNUJWT 
   7 7P5B          LDX   6  2
   9 7PK2          SBN   2  CHAINADD 
  17 7Q4L          BXU   7  JOBNO(2),NUJ4       [J IF NOT REQD JOB   
   9 7QJ=          LDEX  0  ATYPE(2) 
  19 7R3W          STO   0  WWT1(3)             [STORE WAITING STYLE IN CQ11 
   8 7R4R    #UNSET  EWAITFILE   
   4 7R5N    (   
   8 7R6K    [     DS8221 CODE   
  14 7R7G    [     TEST FOR WAITING STYLES #72 & #136 AND
  15 7R8C    [     STORE CSN OF STORAGE UNIT IN CQ11 BLOCK   
  15 7R9#          LDX   5  ACOMMUNE9(2)   [PICK UP CSN IN X5
   8 7R=9          SBN   0  #72  
  16 7R?6          BZE   0  NUJ5           [#72 = TO ACCESS SCA  
   9 7R#3          SBN   0  #136-#72 
  18 7R#Y          BZE   0  NUJ5           [#136= TO OBTAIN SCA LOCKOUT  
   9 7R*T          LDX   5  ACC4(2)  
  15 7RBQ          ADN   0  #136-#35          [IF MAG TAPE   
   4 7RCM    )   
   8 7RDJ    #UNSET  EWAITFILE   
   5 7RFF    #SKIP   
   4 7RGB    (   
   9 7RHG          LDX   5  ACC6(2)  
  20 7S36          SBN   0  #6                  [IF WAITING FOR FILE AVAILABLE   
  15 7SGQ          BZE   0  NUJ5                [FCB IN X5   
   9 7T2B          LDX   5  ACC4(2)  
  15 7TG2          SBN   0  #35-#6              [IF MAG. TAPE
   4 7TPS    )   
  15 7T_L          BZE   0  NUJ5                [TSN IN X5   
   9 7XDG          LDXC  5  AWORK1(2)
   9 7XY6          SBN   0  #167-#35 
   8 7YCQ          BZE   0  NUJ5 
   9 7YXB          LDX   5  CLONG2(2)
   9 7_C2          ADN   0  #167-#24 
   8 85#2          BNZ   0  NUJ5 
  16 85RL          LDX   0  ACC5(2)             [FSTART OF FILE  
   9 86?=          STO   0  WWT3(3)  
  17 87Q6          LDX   0  ACC6(2)             [FILEFILESTORE NO.   
   9 889Q          DCH   0  WWT3(3)  
  16 8992          LDX   5  ACC4(2)             [FBLKS OF FILE   
   9 89NL    NUJ5  STO   5  WWT2(3)  
   4 8=8=    NUJ6
   8 8B5=          BRN      WRIT 
   4 8G_G    NSJ 
  16 8HF6    [     STORE BASIC INFORMATION ABOUT THE SAVED JOB   
  19 8HYQ          SHLSQ    7,2,WRITOFF         [J IF JOB NO LONGER EXISTS   
  10 8JDB          LDN   4  HLSJNAM(2)   
   9 8JY2          LDN   5  WNAME(3) 
  18 8KCL          MOVE  4  3                   [STORE JOBNAME IN CQ11   
  10 8KX=          LDN   4  HLSUNAM(2)   
   9 8LBW          LDN   5  WUSER(3) 
   7 8LWG          MOVE  4  3
  10 8MB6          LDX   4  HLSSTAT(2)   
   9 8MTQ          STO   4  WMISC(3) 
  10 8N*B          LDX   4  HLSYONQ(2)   
   9 8NT2          STO   4  WYONQ(3) 
   9 8P#L          LDX   4  HLSPCT(2)
  10 8PS=          STO   4  WSOURCE3(3)  
   4 8Q?W    WRIT
  13 8QRG    [      WRITE INFORMATION TO 'WS' FILE   
   9 8S=B          LDXC  0  WJOBNO(3)
  17 8SQ2          BCC      WRIT5               [J IF NOT HLS XCODE  
   9 8T9L          ACROSS   WSPERIS,2
   5 8TP=    WRIT5   
   8 8WNG          LDX   2  FX2  
   9 8X86          LDX   0  AWORK4(2)
  19 8XMQ          STO   0  ACOMMUNE1(2)               [DEPTH OF 'WS' FILE   
  18 8Y7B          DOWN     QPRINT,1            [WRITE INFO. TO 'WS' FILE
   8 8YM2          BRN      X2ER 
   8 8_6L          BRN      X2BRK
   9 8_L=          LDX   0  AWORK3(2)
  17 925W          BNG   0  OPF9                [J IF SINGLE JOB O/P 
  17 92KG          BRN      NJB1                 [GET NEXT JOB NO.   
   5 9356    WRITOFF 
   9 93JQ          LDXC  0  WJOBNO(3)
  17 944B          BCC      WRIT5               [J IF NOT HLS XCODE  
  10 94J2          MFREEW   CQPRINT,CQ11 
  10 98YL          ACROSS      HLSREAD,30
  14 99D=    X2ER  UP                           [ERROR   
  14 99XW    X2BRK UPPLUS   1                   [BREAK IN
  10 9=CG    OPF   MHUNTW   1,CQPRINT,CQ1
  10 9=X6          STOZ     ZLCOUNT(1)   
   9 9?BQ          STOZ     ZLJOB(1) 
  13 9?WB    OPF9  UPPLUS   2                   [OK  
   5 9#TL    #PAGE   
   5 9**=    STOJCT  
  10 9*SW    [     UPDATE MOP/BACK COUNT 
  12 9B#G    [     ON ENTRY     X1 LINK ADDRESS  
  16 9BS6    [                  X5 0 IF MOP     1 IF BACKGROUND  
  10 9C?Q          MHUNTW   2,CQPRINT,CQ1
   7 9CRB          LDN   0  1
   7 9D?2          SMO      5
  18 9DQL          ADS   0  ZJCOUNT(2)          [UPDATE MOP/BACK COUNT   
   8 9F==          LDX   2  FX2  
   7 9FPW          EXIT  1  0
   4 9G9G    [   
   5 9M5Q    #PAGE   
   5 9MKB    Q4ENTRY 
  14 9N52    [     S/R TO CHECK IF REQ'D 'STARTED JOB   .
  17 9NJL    [     AND STORE JOB IN X4 IF WS (X4=0 IF NOT REQ'D BY WS
  11 9P4=    [     OR UPDATEJOB COUNT IF 'WJ'
  12 9PHW    [   ON ENTRY & EXIT X1=FX1 & X2=FX2 
  14 9Q3G    [     X3 ADDR. OF JOB BLOCK ON ENTRY &EXIT  
   9 9QH6    [     X6 LINK ADDRESS   
   4 9R2Q    [   
  17 9RGB          FINDJOBQ 3,ACOMMUNE5(2),TNS90 [REFIND JOBQUE BLOCK
   9 9S22          LDX   5  AWORK1(2)
   7 9SFL          SLL   5  2
  18 9S_=          BPZ   5  TNS90               [J IF NO LIVE JOBS REQD  
   7 9TDW          SLL   5  1
  19 9TYG          BNG   5  TNS90               [J IF ONLY SAVED JOBS REQD   
  18 =4R=          BZE   5  TNS40               [J IF ALL LIVE JOBS REQ'D
   9 =PR6          LDX   4  JMISC(3) 
   7 =Q=Q          SLL   5  1
  16 =QQB          BPZ   5  TNS6                [ NO 'MOP'  CHECK
  15 =R=2          BNG   4  TNS90               [NOT  MOP JOB
   7 =RPL    TNS6  SLL   5  1
  16 =S9=          BPZ   5  TNS8                [NO 'BACK' CHECK 
  15 =SNW          BPZ   4  TNS90               [NOT BACK JOB
   7 =T8G    TNS8  SLL   5  1
  16 =TN6          BPZ   5  TNS10               [NO 'REAL' CHECK 
   8 =W7Q          LDN   0  #600 
   9 =WMB          ANDX  0  JMISC(3) 
  16 =X72          BZE   0  TNS90               [NOT 'REAL' JOB  
   7 =XLL    TNS10 SLL   5  1
  16 =Y6=          BPZ   5  TNS12               [NO 'CORE' CHECK 
   9 =YKW          LDEX  0  JMISC(3) 
  17 =_5G          BZE   0  TNS90               [J IF NO CORE IMAGE  
   7 =_K6    TNS12 SLL   5  1
  17 ?24Q          BPZ   5  TNS14               [NO 'EXPRESS' CHECK  
  17 ?342          JBC      TNS90,3,JBEXPREQ    [J IF NOT EXPRESSED  
   7 ?5_Q    TNS14 SLL   5  1
   9 ?6FB          LDX   4  JPRIV(3) 
   8 ?6_2          ANDN  4  #2   
  16 ?7DL          BPZ   5  TNS15               [NO 'SYS' CHECK  
  17 ?7Y=          BZE   4  TNS90               [J IF NOT SYS ISSUED 
   8 ?8CW          BRN      TNS16
   8 ?8XG    TNS15 BPZ   7  TNS16
   8 ?9C6          BNZ   4  TNS90
   7 ?9WQ    TNS16 SLL   5  1
  16 ?=BB          BPZ   5  TNS20               [NO 'USER' CHECK 
   7 ?=W2          LDN   0  3
   7 ??*L    TNS18 SMO      0
  10 ??T=          LDX   4  JUSER-1(3)   
   7 ?##W          SMO      0
  15 ?#SG          BXU   4  AWORK2-1(2),TNS90   [WRONG USER? 
   8 ?*#6          BCT   0  TNS18
   7 ?*RQ    TNS20 SLL   5  1
  17 ?B?B          BZE   5  TNS40               [NO FURTHER CHECKS   
  19 ?LK2          HLSAV    3,SET,SAVING,JOBQ,TNS90 [J IF JOB 'BEING SAVED'  
  15 ?M4L          FCAJO    2,3                 [FIND CPAT   
  16 ?MJ=          BPZ   5  TNS22               [NO FSTART CHECK 
  18 ?N3W          JBC      TNS90,2,XFSTART     [J IF NOT FULLY STARTED  
  10 ?NHG    TNS22 LDX   4  JSOURCE3(2)  
   9 ?P36          ANDX  4  BITS22LS 
   7 ?PGQ          SLL   5  1
  16 ?Q2B          BPZ   5  TNS24               [NO CLUSTER CHECK
  17 ?QG2          BZE   4  TNS90            [J IF NOT CLUSTER JOB   
   7 ?Q_L    TNS24 SLL   5  1
  16 ?RF=          BPZ   5  TNS26               [NO CENTRAL CHECK
  16 ?RYW          BNZ   4  TNS90        [J IF NOT CENTRAL JOB   
   7 ?SDG    TNS26 SLL   5  1
  15 ?SY6          BPZ   5  TNS28               [NO PR CHECK 
   7 ?TCQ          ERX   4  7
   9 ?TXB          ANDX  4  BITS22LS 
  16 ?WC2          BNZ   4  TNS90               [WRONG PROPERTY  
   7 ?WWL    TNS28 SLL   5  1
  16 ?XB=          BPZ   5  TNS40               [NO WAITING CHECK
   9 #BGL          LDX   4  JOBNO(2) 
   5 #C2=    TNS29L  
  17 #CFW          LDN   2  FLOWQ               [SEARCH WAITING LIST 
   9 #C_G    TNS29 LDX   2  FPTR(2)  
   9 #K*B          TXU   2  XQEND(1) 
  16 #KT2          BCC      TNS90               [J IF NOT WAITING
   6 #L#L    XFSHNBNOTWT 
  11 #M?W          TXU   4  JOBNO-CHAINADD(2)
   8 #MRG          BCS      TNS29
   5 #N?6    TNS40   
  13 #NQQ    [     UPDATE COUNT OF LIVE JOBS IF WJ   
  11 #P=B    [     OR PUT JOB NO. IN X4 IF WS
   4 #PQ2    UPD 
  16 #Q9L          BNG   7  UPD4                [J IF COUNTING   
   9 #QP=          LDX   4  JOBNUM(3)
   8 #R8W          BRN      TNS91
   9 #RNG    UPD4  LDX   5  JMISC(3) 
  17 #S86          SRL   5  23                  [   SET B23 IF BACK  
  18 #SMQ          CALL  1  STOJCT              [ADD 1 TO MOP/BACK COUNT 
  17 #T7B    TNS90 LDN   4  0                   [JOB NOT REQD BY WS  
   5 #TM2    TNS91   
   8 #_4B          SMO      FX2  
  18 #_J2          STO   4  ACOMMUNE4           [RETURN X4 VIA ACOMMUNE4 
   5 *23L          UP
   4 *2H=    #END
   6 ____ ...746306070001
