  15 22FL ...      SEG   SPERION,860,FRANK BLUNDELL,SPERION  
   4 22T* ...#   
  16 2394 ...#     COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982  
   4 23JR ...#   
   4 23YG    #   
  16 24D6    #     THIS SEGMENT DEALS WITH TRUSTED MODE 4,5 AND 6
   4 24XQ    #   
  12 25CB          SEGENTRY K1SPERION,QK1SPERION 
   4 25X2    #   
  14 26BL    #     MONITORING FILE MESSAGE IDENTIFIERS   
   4 26W=    #   
   9 27*W    ZRR1           +JYINVAL 
  16 27TG    ZRR2           +JYMODE             [MODE IN ERROR   
  19 28*6    ZRR3           +JYNOBUF            [BUFFER OUT OF RESERVATIONS  
  17 28SQ    ZRR4           +JYCOUNT            [COUNT OUT OF RANGE  
  15 29#B ...RFAIL          +EWOWNS             [BLOCK N FAIL
  16 2=?L    PERF           +JYPERFAIL          [ONLINE %A FAIL  
   4 2=R=    #   
   8 2?=W    #     LOCAL DEFINES 
   8 2#=6    #DEF  TAC1=EWORK6   
   8 2*P2    #DEF  TAC2=TAC1+1   
   8 2B8L    #DEF  TAC3=TAC2+1   
   8 2BN=    #DEF  TAC4=TAC3+1   
   8 2C7W    #DEF  TAC5=TAC4+1   
   8 2CMG    #DEF  TAC6=TAC5+1   
   8 2DLQ    #DEF  TAC7=ACOMMUNE7
   8 2G5L    #DEF  TAC8=TAC7+1   
   8 2GK=    #DEF  TAC9=TAC8+1   
   8 2H4W    #DEF   XCA=EVENT2   
  15 2HJG    #DEF  TREP=EXEC3                   [BSTS REPLY  
   9 2J46    #DEF  PB=BSET+BOBJ+BONL 
   4 2JHQ    #   
   7 2K3B    #     CONSTANTS 
   4 2KH2    #   
   8 2L2L    ZA    PAIR  PERION,1
   8 2LG=    TEN            +10  
   9 2L_W    RMASK          #100000  
   9 2MFG    SESS           +CHREAD  
   4 2M_6    #   
   9 2NDQ    #     DEVICE SUBTYPES   
   4 2NYB    #   
   8 2PD2    PEDS           +EDS 
   8 2PXL    PEMDS          +EMDS
   8 2QC=    PAFDS          +AFDS
   8 2QWW    PBFDS          +BFDS
   4 2RBG    #   
   7 2RW6    #     GEOERRS   
   4 2S*Q    #   
  16 2STB    RZR   GEOERR   1,EWMESS            [NO EWDAS/EWMESS 
  18 2T*2    RERR  GEOERR   1,RESVIOLA          [RESERVATION VIOLATION   
   4 2TSL    #   
   6 2W#=    QK1SPERION  
   9 2WRW    #SKI  K6SPERION>99-99   
   9 2X?G          TRACE    6,SPERION
  13 2XR6          LDX   0  6                   [MODE
   7 2Y=Q          ANDN  0  7
   9 2YQB          STO   0  AWORK3(2)
   7 2_=2          LDX   0  6
   7 2_PL          SRL   0  9
   8 329=          ANDN  0  #30  
  16 32NW          STO   0  AWORK1(2)           [ADDITIVE MODE   
  18 338G          ANDN  6  7                   [X6 CONTAINS BASIC MODE  
  16 33N6          LDX   4  XCA(2)              [CONTROL ADDRESS 
   9 347Q          STO   4  TAC6(2)  
  18 34MB          LDN   7  5                   [LENGTH OF CONTROL AREA  
  10 3572          LDX   3  APETADDR(2)  
   9 35LL          LDN   5  TAC1(2)  
  18 36KW          FOUTMOVE 3,4,5,7,HLOCK1,2    [MOVE CA TO ACOMMUNE1-5  
   9 384Q    #SKI  K6SPERION>199-199 
   4 38JB    (   
  11 3942          TRACE    TAC1(2),SPERICA1 
   9 39HL          TRACE    TAC2(2), 
   9 3=3=          TRACE    TAC3(2), 
   9 3=GW          TRACE    TAC4(2), 
   9 3?2G          TRACE    TAC5(2), 
   4 3?G6    )   
  17 3?_Q          STOZ     AWORK4(2)           [ZEROISE FOR REPLY   
  13 3#FB          LDX   7  TAC5(2)             [LBN 
  17 3#_2          BZE   7  RONGLBN             [OUT OF RANGE IF ZERO
  15 3*DL          BNG   7  RONGLBN             [OR NEGATIVE 
  19 3*Y=          MFINDEXOF 3,EXNUM(2)         [GET POINTER TO EXOF IN X3   
  10 3BCW          LDN   1  FDCELLS(3)   
   7 3BXG          SBN   1  7
  16 3CC6          LDEX  6  FIP+5(3)            [NO OF FILE AREAS
   9 3CWQ    NEXT  BZE   6  RONGLBN  
  18 3DBB          SBN   6  1                   [REDUCE COUNT OF AREAS   
  15 3DW2          ADN   1  6                   [NEXT CELL   
   8 3F*L          TXL   7  6(1) 
  17 3FT=          BCC      NEXT                [J NOT IN THIS CELL  
  20 3G#W          SBX   7  0(1)                [LBN RELATIVE TO START OF CELL   
   9 3GSG          LDX   4  FIP+8(3) 
  15 3H#6          ANDN  4  #77                 [BUCKET SIZE 
  17 3HRQ          MPY   7  4                   [NO OF BLOCKS IN X0  
  16 3J?B          DVD   7  5(1)                [CYL X0  BLOCK X7
  18 3JR2          ADX   0  2(1)                [ADD STCYL RELATIVE TO SU
   9 3K=L          STO   0  EWORK1(2)
  16 3KQ=          LDX   6  5(1)                [BLOCKS/SEEK AREA
  20 3L9W          SBX   6  7                   [X6=MAX NO. OF BLOCKS TO TRANSFER
   9 3LPG    #SKI  K6SPERION>199-199 
  10 3M96          TRACE    6,MAX BLKS   
  14 3MNQ          ADX   7  3(1)                [ST. SEG 
   9 3N8B          STO   7  EWORK2(2)
  15 3NN2          LDX   0  1(1)                [SERIAL NO.  
  15 3P7L          STO   0  EWORK3(2)           [AND STORE   
  16 3R66 ...      SUSESTX  1(1),NOTON          [SEE IF SU ONLINE
  16 3RKQ          LDX   0  ABSUN(1)            [BSUN FROM ASTUC 
   8 3S5B          LDX   2  FX2  
   9 3SK2          STO   0  TAC2(2)  
  15 3T4L          LDX   0  ASUBASE(1)          [ST CYL OF SU
   9 3TJ=          STO   0  EWORK5(2)
   9 3W3W          ADX   0  EWORK1(2)
   7 3WHG          LDX   4  0
   4 3WJX ...[   
   9 3WL# ...[     EMULATION CHANGE: 
  18 3WMP ...[           STORE CYL*BLOCKS PER CYL+BLOCK IN EWORK2 INTO X4
  20 3WP6 ...[           CONVERTX THEN RETURNS WITH B0 OF BLOCK SET FOR EMULATION
   4 3WQH ...[   
  18 3WRY ...      JENVNOT  NEMU,MEEP           [J IF NOT MEEP EMULATION 
  16 3WT* ...      LDX   5  EWORK2(2)           [GET BLOCK PART  
  20 3WWQ ...      MPA   4  HBLOCK(1)           [CALCULATE ABSOLUTE BLOCK ADDRESS
  17 3WY7 ...      STO   5  4                   [FOR CONVERTX MACRO  
   4 3W_J ...NEMU
  19 3X36          LDX   5  HTYPE(1)            [SUBTYPE AND TYPE FROM ASTUC 
   9 3XGQ          STO   5  EWORK4(2)
  16 3Y2B          ANDN  5  #77                 [ISOLATE SUBTYPE 
  18 3YG2          CONVERTX 4,EWORK2(2),EWORK4(2) [GET HARDWARE ADDRESS  
   8 3Y_L          LDX   1  FX1  
   8 3_F=          LDX   2  FX2  
   9 3_YW          STO   0  TAC5(2)  
  13 42DG          LDX   4  AWORK3(2)           [MODE
   7 42Y6          SBN   4  6
  16 43CQ          BNZ   4  NSIX                [J IF NOT MODE 6 
   9 43XB          TXU   5  PAFDS(1) 
  14 44C2          BCC      RX9A                [J IF 2A 
   9 44WL          TXU   5  PBFDS(1) 
  16 45B=          BCS      RX9                 [J IF NOT 2B FDS 
  16 45TW    RX9A  LDX   7  ZRR2(1)             [MODE IN ERROR   
   8 46*G          BRN      RX6  
   9 46T6    RX9   TXU   5  PEMDS(1) 
  16 472_ ...      BCS       PAB                [J IF NOT EMDS   
  19 478S ...      MFINDSU  1,EWORK3(2)         [GET ASTUC - CSN IN EWORK3   
  16 47BM ...      LPROP    HUNIT(1),0          [PROPERTIES IN X0
  18 47JG ...      ANDN  0  #100                [ISOLATE BIT 11 OF K52+1 
   8 47Q* ...      LDX   1  FX1  
  19 47Y8 ...      BNZ   0  PAB                 [INTEGRATED EDS30-60 BIT SET 
  19 4863 ...      LDN   0  1                   [ELSE COUNT OF 1 FOR  MODE 6 
   9 48*9 ...      STO   0  TAC3(2)  
   8 48JC ...      BRN      RX10A
   9 48RL    PAB   LDX   0  TAC3(2)  
  21 49?=          BZE   0  RONGLEN             [ZERO LENGTH ILLEGAL FOR EDS8 MODE #6
  17 49QW ...      BNG   0  RONGLEN             [ ALSO ILLEGAL IF -VE
  16 4==G ...      SBX   0  BIT11               [ BIT11 = 4096   
  17 4=Q6 ...      BPZ   0  RONGLEN             [ ILLEGAL IF > 4095  
  17 4#NL    RX10A STOZ     TAC4(2)             [ZERO CORE ADDRESS   
   7 4*8=          LDN   0  6
  16 4*MW          STO   0  TAC1(2)             [MODE FOR BSTS   
  15 4B7G          BRN      RX8                 [TO BACKTRAN 
  14 4BM6    NSIX  LDX   0  TAC3(2)             [LENGTH  
   9 4C6Q          TXL   0  RMASK(1) 
  20 4CLB          BCC      RONGLEN             [ILLEGAL LENGTH IF >#77777 OR -VE
   7 4D62          ADN   4  1
  15 4DKL          BZE   4  PHIVE               [J IF MODE 5 
  16 4F5=          LDX   7  TAC3(2)             [COUNT FOR CHECKA
   8 4FJW          BRN      NOT5 
  19 4G4G    PHIVE LDN   7  1                   [CHECK 1 WD ONLY FOR MODE 5  
  10 4GJ6    NOT5  LDX   3  APETADDR(2)  
  15 4H3Q          LDX   1  ANM(3)              [MEMBER NO.  
   8 4HHB          ANDN  1  #3   
   8 4J32          PMODES   1,3  
   8 4JGL          ANDN  1  #10  
  17 4K2=          BNZ   1  NP                  [22 BIT ADDRESSING   
  17 4KFW          LDX   0  BSP16               [15 BIT ADDRESSING   
   8 4K_G          BRN      NP1  
   9 4LF6    NP    LDX   0  BITS22LS 
   9 4LYQ    NP1   ANDS  0  TAC4(2)  
  18 4M58 ...#SKI  G4                     [RELEASE SU IN G4 IN CASE QUOTA
   4 4M9L ...(   
  18 4MB4 ...      RELSTX  EWORK3(2),NP8A        [NEEDS TO BE INCREASED  
   8 4MGG ...NP8A  LDX   2  FX2  
   4 4MLY ...)   
   8 4MRB ...      LDX   1  FX1  
  18 4MY2          LDX   3  SESS(1)             [READ ACCESS TO OBJ PROG 
  19 4N8G ...      CHECKB   TAC4(2),7,OUT,APETADDR(2),3,NP8,HLOCK2,,PERION   
  17 4NF2 ...#SKI  G4                     [GET SU BACK ONLINE IN G4  
   4 4NPG ...(   
  11 4P22 ...NP8   SUSESTX  EWORK3(2),NOTON  
   8 4P=G ...      LDX   1  FX1  
   8 4PH2 ...      LDX   2  FX2  
   4 4PRG ...)   
   5 4Q42 ...#SKI  G3
   4 4Q#G ...NP8 
  15 4QK2 ...      BNZ   4  RX1                 [J NOT MODE 5
  18 4QTQ          TXL   6  TAC3(2)             [MAX LENGTH NOT EXCEEDED 
   8 4R*B          BCC      RX11 
   9 4RT2          LDX   0  TAC3(2)  
   7 4S#L          SBX   0  6
  18 4SS=          STO   0  AWORK4(2)           [DIFFERENCE TO REPLY WD  
  17 4T?W          STO   6  TAC3(2)             [MAX LENGTH ALLOWED  
   8 4TRG          LDCT  0  #200 
  18 4W?6          ORS   0  AWORK4(2)           [END OF CYLINDER REPLY   
   9 4WQQ    RX11  TXU   5  PEMDS(1) 
  14 4X=B          BCS      RX12                [NOT EMDS
  17 4XQ2          STOZ     TAC4(2)             [ZERO CORE ADDRESS   
  13 4Y9L          LDN   0  5                   [MODE
  15 4YP=          STO   0  TAC1(2)             [FOR BACKTRAN
  16 4_8W          BRN      RX8                 [J TO BACKTRAN   
   9 4_NG    RX1   TXU   5  PAFDS(1) 
   8 5286          BCC      RX2  
   9 52MQ          TXU   5  PBFDS(1) 
  14 537B          BCS      RX3                 [J NOT 2B
  15 53M2          SLL   6  1                   [MAX BLOCK *2
   9 546L    RX2   TXL   6  TAC3(2)  
  16 54L=          BCC      RX3                 [J NOT OVER MAX  
   9 555W          LDX   0  TAC3(2)  
   7 55KG          SBX   0  6
  21 5656          STO   0  AWORK4(2)           [BLOCKS NOT TO BE DEALT WITH TO REPLY
  17 56JQ          STO   6  TAC3(2)             [MAX LENGTH ALLOWED  
   8 574B          LDCT  0  #10  
   9 57J2          ORS   0  AWORK4(2)
   9 583L    RX3   TXU   5  PEMDS(1) 
  15 58H=          BCS      RX4                 [J NOT EMDS  
   9 592W          LDX   0  TAC5(2)  
   8 59GG          ANDN  0  #7777
  20 5=26          BZE   0  RX5                 [ERROR IF BOTTOM 12 BITS NON-ZERO
   9 5=FQ    RX7   LDX   7  ZRR1(1)  
   8 5=_B          BRN      RX6  
   9 5?F2    RX4   TXU   5  PEDS(1)  
  15 5?YL          BCS      RX5                 [J NOT EDS   
   9 5#D=          LDX   0  TAC5(2)  
   8 5#XW          ANDN  0  #77  
  20 5*CG          BNZ   0  RX7                 [MAKE SURE BOTTOM 6 BITS ARE ZERO
   7 5*X6    RX5   LDN   0  7
  16 5BBQ          STO   0  TAC1(2)             [MODE FOR BSTS   
  19 5BH8 ...      LDX   0  AWORK1(2)           [GET ADDITIVE MODES IF ANY   
  20 5BML ...      ANDN  0  #30                 [SEE IF #20000 OR#30000 REQUIRED 
  18 5BS4 ...      SLL   0  9                   [ADD MODE OR ZERO IN X0  
  17 5BYG ...      STO   0  4                   [RETAIN FOR BACKTRAN 
  21 5C4Y ...      BACKTRAN TAC1(2),PB,TAC3(2),,TAC2(2),TAC5(2),TAC4(2),APETADDR(2),-
   9 5C9B ...                    EX4 
   8 5CB2          BRN      RX13 
  19 5CH8 ...RX8   LDX   0  AWORK1(2)           [ADDITIVE MODE SEQUENCE ...  
  16 5CNB ...      ANDN  0  #30                 [ ... AS ABOVE   
   7 5CTJ ...      SLL   0  9
   7 5D2Q ...      STO   0  4
  21 5D7Y ...      BACKTRAN TAC1(2),BONL+BSET,TAC3(2),,TAC2(2),TAC5(2),TAC4(2),,EX4  
  15 5D*=    RX13  LDX   0  TREP(2)             [BSTS REPLY  
  14 5DSW          STO   0  AWORK2(2)           [REMEMBER
  16 5F#G          CHTRAN   EWORK4(2),TFAIL,TFAIL [CHECK REPLY   
  15 5H?2 ...RX14  RELSTX   EWORK3(2),RX14B     [RELEASE UNIT
   8 5HQL          BRN      RX14A
   8 5J==    RX14B LDX   2  FX2  
  15 5JPW    RX14A LDX   3  APETADDR(2)         [PCA ADDRESS 
  15 5K9G          LDX   1  TAC6(2)             [CA ADDRESS  
  15 5KP6          ADN   1  1                   [REPLY WORD  
  18 5L8Q          FADDRESS 3,1,HLOCK1          [GEORGE ADDRESS OF REPLY 
  15 5LNB          LDX   0  AWORK4(2)           [ACTUAL REPLY
  14 5M82          STO   0  0(1)                [TO PROG 
   8 5MML          LDX   1  FX1  
  15 5N7=          OLPFIN1  ZA(1)               [TERMINATE   
   4 5NLW    #   
  21 5P6G    #     CHECK READ-NOT EMDS. MODE 5 NOT AVAILABLE ON DEVICES OTHER THAN   
  16 5PL6    #     EMDS SO SET UP BUFFER AND DO ORDINARY READ.   
   4 5Q5Q    #   
  16 5QKB    RX12  SETNCORE 128,3,EWDAS,FBUF    [SET UP BUFFER   
  14 5R52          LDX   6  TAC3(2)             [COUNT   
  15 5RJL          STOZ     TAC1(2)             [MODE TO READ
   8 5S4=          LDN   0  128  
  16 5SHW          STO   0  TAC3(2)             [COUNT FOR BSTS  
  15 5T3G          LDN   0  A1(3)               [CORE ADDRESS
  14 5TH6          STO   0  TAC4(2)             [FOR BSTS
  16 5W2Q          LOCK     3                   [LOCK DOWN BUFFER
   9 5WGB          LDX   4  EWORK1(2)
  13 5X22          ADX   4  EWORK5(2)           [CYL 
   4 5X3C ...[   
   9 5X4S ...[     EMULATION CHANGE: 
  20 5X69 ...[           STORE CYL*BLOCKS PER CYL+BLOCK PART IN EWORK2 INTO X4   
  21 5X7L ...[           CONVERTX THEN RETURNS IN X0 THE ABSOLUT BLOCK ADDRESS WITH B
   4 5X93 ...[   
  18 5X=D ...      JENVNOT  RX13A,MEEP          [J IF NOT MEEP EMULATION 
  10 5X?T ...      MFINDSU  3,EWORK3(2)  
  19 5X*= ...      LDX   5  EWORK2(2)           [GET BLOCK PART OF ADDRESS   
  20 5XBM ...      MPA   4  HBLOCK(3)           [CALCULATE ABSOLUTE BLOCKADDRESS 
  17 5XD4 ...      STO   5  4                   [FOR CONVERTX MACRO  
  20 5XFL    RX13A CONVERTX 4,EWORK2(2),EWORK4(2) [CONVERT TO HARDWARE BS ADDRESS
   8 5X_=          LDX   2  FX2  
  14 5YDW          STO   0  TAC5(2)             [FOR BSTS
  19 5YL4 ...      LDX   0  AWORK1(2)           [ADDITIVE MODE SEQUENCE ...  
  20 5YR= ...      ANDN  0  #30                 [ ... AS FOR PREVIOUS BACKTRANS  
   7 5YYD ...      SLL   0  9
   7 5_5M ...      LDX   7  0
  21 5_=W ...      BACKTRAN TAC1(2),BSET+BONL,TAC3(2),,TAC2(2),TAC5(2),TAC4(2),,EX7  
  15 5_D6          LDX   0  TREP(2)             [SAVE BSTS   
  14 5_XQ          STO   0  AWORK2(2)           [REPLY   
  16 62CB          CHTRAN   EWORK4(2),XFAIL,XFAIL [CHECK REPLY   
  20 62X2          SBN   6  1                   [REDUCE COUNT OF BLOCKS TO CHECK 
   7 63BL          ADN   0  1
  17 63W=          ADS   0  EWORK2(2)           [UPDATE BLOCK ADDRESS
  18 642Y ...      JNENVX   NEMU1,MEEP,1        [J IF NOT MEEP EMULATION 
  18 647L ...      ADX   4  0                   [UPDATE X4 FOR EMULATION 
   5 64=Q ...NEMU1   
  16 64*W          BNZ   6  RX13A               [J NOT LAST BLOCK
  10 64TG          HUNTW    1,EWDAS,FBUF 
  15 65*6          FREECORE 1                   [FREE BUFFER 
   8 65SQ          BRN      RX14 
   4 66#B    #   
   9 66S2    #     TRANFER FAILURES  
   4 67?L    #   
   4 67R=    #   
   9 68=W    TFAIL STOZ     AWORK4(2)
   8 68QG          BRN      RX15 
   9 69=6    XFAIL ORS   6  AWORK4(2)
  15 69PQ    RX15  MFINDSU  3,EWORK3(2)         [PTR TO SU   
   9 6=9B          LDX   7  EWORK1(2)
  10 6=P2          SBX   7  ASUBASE(3)   
   9 6?8L          MPY   7  HBLOCK(3)
   9 6?N=          ADX   0  EWORK2(2)
   7 6#7W          LDX   7  0
  15 6B6B ...      RELSTX   EWORK3(2),RX16      [RELEASE UNIT
   8 6BL2    RX16  LDX   2  FX2  
  15 6C5L          LDX   3  TAC6(2)             [CA ADDRESS  
   9 6CK=          BADBLOCK TAC2(2)  
  16 6D4W          KEEPMESS RFAIL(1),EWORK3(2),7 [BLOCK N FAIL   
   8 6DJG          LDCT  0  #40  
  15 6F46          ORS   0  AWORK4(2)           [FAILED REPLY
   8 6FHQ          LDCT  0  #100 
  19 6G3B          ANDX  0  AWORK2(2)           [TEST FOR B2 IN BSTS REPLY   
  19 6GH2          ORS   0  AWORK4(2)           [SET B2 IF B2 SET IN BSTS REP
  14 6H2L          BNZ   0  RX17                [B2 SET  
  15 6HG=          LDX   0  AWORK2(2)           [BSTS REPLY  
   8 6H_W          SRL   0  18   
   7 6JFG          SBN   0  4
  15 6J_6          BZE   0  RX17                [ONLY B3 SET 
   8 6KDQ          LDCT  0  #10  
  18 6KYB          ORS   0  AWORK4(2)           [B5=1 IF OTHER BITS SET  
  16 6LD2    RX17  LDX   4  AWORK1(2)           [ADDITIVE MODES  
  15 6LXL          LDX   5  EWCHAN(2)           [CHANNEL NO. 
  18 6MC=          LDX   6  AWORK4(2)           [REPLY TO OBJECT PROGRAM 
  11 6MWW          HUNTW    1,EWDAS,EWMESS   
   8 6NBG          BNG   1  RZR  
  16 6NW6          CHAIN    1,APETADDR(2)       [CHAIN BEHIND PCA
  16 6P*Q          TOPCA1   ZA(1)               [CHANGE TO PCA   
  16 6PTB          UNPLUG                       [FREE EXEC SOCKET
  21 6Q*2          CAPCA    EWDAS,EWMESS        [CHANGE TO CPA TAKING EWMESS WITH US 
  17 6QSL          EXMESS   ALL                 [OUTPUT BLOCK N FAIL 
  17 6R#=          LDN   7  0                   [RESTART O/P MARKER  
   8 6RRW          ANDN  4  #20  
  20 6S?G          BNZ   4  RX18                [J IF ADDITIVE MODE #20000 SET   
  20 6SR6    RX21  STO   6  AWORK1(2)           [REMEMBER OBJECT PROGRAM REPLY   
  17 6T=Q          NGN   7  1                   [DON'T RESTART PROG  
  16 6TQB ...XPAR  LDCT  6  #006                [ TYPE IN B0-8   
  17 6W=2 ...      ADX   6  EWCHAN(2)           [ CHANNEL IN B9-23   
  19 6WPL ...      OUTPACK  6,1,PERINAME,XPAR   [ ASSEMBKE PARAM FOR ILLEGALX
  17 72LL          LDX   6  AWORK1(2)           [OBJECT PROGRAM REPLY
   7 745G    RX18  ADN   3  1
   8 74K6          GETWORD  3,3  
  10 754Q          TESTREP  RESVIOL,RERR 
   8 75JB          STO   6  0(3) 
   7 7642          WORDFIN   
  20 7=Y=          BNG   7  RX19                [J IF PROG NOT TO BE RESTARTED   
  15 7?CW          RUNPROG                      [RESTART PROG
  19 7?XG    RX19  LDX   7  PERF(1)             [ONLINE *THING FAIL MESSAGE  
  19 7#C6    RX22  ILLEGALX 7                   [REPORT ILLEGAL AND FINISH   
   4 7#WQ    #   
  11 7*BB    #     STORAGE UNIT NOT ONLINE   
   4 7*W2    #   
   8 7B*L    NOTON LDX   2  FX2  
  20 7BT=          LDX   3  TAC6(2)             [REMEMBER CONTROL AREA ADDRESS   
  16 7C#W          LDX   5  EWCHAN(2)           [AND CHANNEL NO. 
  16 7CSG          LDX   6  EWORK3(2)           [AND SERIAL NO.  
   8 7D#6          LDX   1  FX1  
  16 7DRQ          TOPCA1   ZA(1)               [CHANGE TO PCA   
  15 7F?B          UNPLUG                       [FREE SOCKET 
  14 7FR2          CAPCA                        [TO CPA  
  16 7KN2          SMUSEST  6,NONL,REAKIN       [GET SU ONLINE   
  15 7L7L          RELSTX   6,RX20              [RELEASE UNIT
  17 7M6W    RX20  STEPBACK                     [STEP PROGS ONR BACK 
  15 7MLG          RUNPROG                      [TRY AGAIN   
   4 7N66    #   
  12 7NKQ    #     OPERATOR TYPES CANTDO LOAD SU 
   4 7P5B    #   
  14 7PK2    NONL  LDCT  6  #50                 [REPLY   
  17 7Q4L          BRN      RX21                [JOIN ILLEGAL PATH   
   4 7QJ=    #   
  13 7R3W    #     BREAKIN OCCURS WHILE WAIT FOR SU  
   4 7RHG    #   
   5 7S36    REAKIN  
  16 7SGQ          STEPBACK                     [STEP ONR BACK   
  16 7T2B          PROGBRKIN                    [REPORT BREAK IN 
   4 7TG2    #   
  13 7T_L    #     LOGICAL BUCKET NUMBER OUT OF RANGE
   4 7WF=    #   
   5 7WYW    RONGLBN 
  18 7XDG          LDCT  0  #10                 [LBN OUT OF RANGE REPLY  
   9 7XY6          STO   0  AWORK4(2)
  18 7YCQ          BRN      RX14A               [J TO PUT REPLY IN PROG  
   4 7YXB    #   
  11 7_C2    #     BUFFER OUT OF RESERVATIONS
   4 7_WL    #   
  21 82B=    OUT   LDX   7  ZRR3(1)             [BUFFER OUT OF RESERVATIONS MESSAGE  
   8 82TW          BRN      RX6  
   4 83*G    #   
   8 83T6    #     ILLEGAL LENGTH
   4 84#Q    #   
   5 84SB    RONGLEN 
  19 85#2          LDX   7  ZRR4(1)             [COUNT OUT OF RANGE MESSAGE  
  15 87=G ...RX6   RELSTX   EWORK3(2),RX23      [RELEASE SU  
   8 87Q6    RX23  LDX   1  FX1  
  20 889Q          TOPCA1   ZA(1)               [CHANGE TO PCA TO REPORT ILLEGAL 
  18 88PB          UNPLUG                       [FREE EXEC PROG SOCKET   
  16 8992          BRN      RX22                [J TO DO ILLEGAL 
   4 89NL    #   
  20 8=8=    #     RETURN TO START ALL OVER: (G4)-PROGRAM'S QOTA HAS BEEN CHANGED
   4 8=MW    #   
   5 8?7G    PERION  
   9 8?M6          ACROSS   PERION,2 
   4 8#6Q    #   
  10 8#LB          MENDAREA 40,K99SPERION
   4 8*62    #END
   8 ____ ...41265223000100000000
