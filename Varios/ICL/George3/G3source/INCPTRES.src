  13 22FL          SEG      INCPTRES,864,FILESTORE   
   4 22_=    #   
  20 23DW    #  THIS SEGMENT IS ENTERED DOWN AT EMS WHEN :DUMPER HAS BEEN PARTIAL
  20 23YG    #  RESTORED. ITS FUNCTION IS TO DETECT TAPES WHICH EXIST IN INCINDEX
  21 24D6    #  AND SYSTEM.SERIAL BUT WHICH HAVE HAD THEIR :DUMPER ENTRIES LOST DUE  
  21 24XQ    #  TO THE PARTIAL RESTORE. FOR EACH SUCH TAPE IT CONCOCTS A DIRECTORY   
  13 25CB    #  ENTRY AND INSERTS IT INTO :DUMPER.   
   4 25X2    #   
  10 26BL          SEGENTRY K1INCPTRES,X1
   4 26W=    #   
   8 27*W    #  SUNDRY CONSTANTS 
   4 27TG    #   
   9 28*6    MDUMPER        12HDUMPER
  11 28SQ    MGEO3PROC      12HGEO3PROCTAPE  
  11 29#B    MGEO3DUMP      12HGEO3DUMPTAPE  
   4 29S2    #   
  21 2=?L    #  ******************************************************************** 
   4 2=R=    #   
  20 2?=W    #  THE MAIN PATH STARTS HERE. ON ENTRY :SYSTEM.INCINDEX IS OPEN AT  
  21 2?QG    #  DEPTH -1 AND THE MISCELLANEOUS CHAIN CONTAINS THE FI/FTAPES BACKMAP  
  21 2#=6    #  BLOCK WHICH GIVES DETAILS OF WHAT TAPES HAVE WHICH DIRECTORY ENTRIES.
   4 2#PQ    #   
  20 2*9B    #  FIRST WE PREPARE FOR THE JOB. WE OPEN :DUMPER AND EXAMINE ITS FCB
  21 2*P2    #  TO SEE IF IT HAS BEEN PARTIAL RESTORED. IF IT HAS NOT WE JUMP STRAIGH
  20 2B8L    #  TO THE END OF THE SEGMENT AS THERE IS NOTHING FOR INCPTRES TO DO.
   4 2BN=    #   
   4 2C7W    X1  
  17 2CMG          SETNCORE 4,2,FILE,FABSNB     [X2-> 4 WORD FABS.   
  17 2D76          LDN   0  4                   [SET UP RECORD HEADER
   8 2DLQ          STO   0  A1(2)
  16 2F6B          ADN   1  MDUMPER             [X1->12HDUMPER   
  16 2FL2          ADN   2  A1+1                [X2->DESTINATION 
  15 2G5L          MOVE  1  3                   [MOVE NAME IN
  15 2GK=          OPEN     (GEOERR),GENERAL    [OPEN :DUMPER
  15 2H4W          MFREE    FILE,FABSNB         [FREE FABS.  
  17 2HJG          TOPFCB2  3                   [X3->:DUMPER'S FCB   
  19 2HQQ ...      JBS      X1A,3,BFPARTRES      [J IF NOT PARTIAL RESTORED  
   9 2H_2 ...      LDX   0  GINCTSN  
   8 2J7= ...      BZE   0  UP   
   4 2J*G ...X1A 
   4 2JHQ    #   
  20 2K3B    #  WE HAVE OPENED :DUMPER AND ESTABLISHED THAT THERE IS WORK FOR US 
  19 2KH2    #  TO DO. WE REWIND INCINDEX ( AT DEPTH -1) AND SET UP A FLOCNB,
  20 2L2L    #  ENT, AND FTRAP WHICH WILL COME IN USEFUL WHEN AN ENTRY HAS TO BE 
  20 2LG=    #  INSERTED INTO :DUMPER. THE ENT AND FLOCNB ARE SET UP WITH ALL THE
  21 2L_W    #  NECESSARY DETAILS EXCEPT LOCAL NAME AND TSN. WE HAVE TO ASSUME THAT  
  20 2MFG    #  THE TAPES HAVE REEL/GEN OF 0/0 AND WE SET THE TIMES AND DATES TO 
  11 2M_6    #  THE CURRENT TIME AND DATE.   
   4 2NDQ    #   
  16 2NYB          REWIND   -1                  [REWIND INCINDEX 
  18 2PD2          SETNCORE 8,3,FILE,FLOCNB     [SET UP TAPE TYPE FLOCNB 
  11 2PXL          MBS      3,BFABTSN,BFABLIB
  15 2QC=          SETNCORE FRDE,3,FILE,ENT     [SET UP ENT  
  16 2QWW          LDN   6  A1(3)               [X6->START OF ENT
  16 2RBG          LDN   7  A1+1(3)             [X7->NEXT WORD   
  16 2RW6          STOZ     A1(3)               [ZERO FIRST WORD 
  19 2S*Q          MOVE  6  FRDE-1              [AND MOVE ZEROES THRU BLOCK  
  17 2STB          LDN   0  FRDE                [SET UP REC HEADER   
   9 2T*2          STO   0  EREC(3)  
  14 2TSL          LDN   0  1                   [ONE TRAP
   9 2W#=          STO   0  ENUSE(3) 
  16 2WRW          LDCT  0  #405                [T/M FOR TAPES   
   9 2X?G          STO   0  ETM(3)   
  18 2XR6          BS       3,BELIB             [INDICATE TAPE ENTRANT   
  21 2Y=Q          LDX   0  EDATE               [INITIALISE DATE/TIME OF ACCESS ETC  
  14 2YQB          STO   0  EDLA(3)             [TO NOW  
  10 2_=2          STO   0  EWRITDAY(3)  
   9 2_PL          LDX   0  CLEANCT  
   9 329=          STO   0  ETLA(3)  
  10 32NW          STO   0  EWRITTIME(3) 
   9 338G          LDX   0  GMTNOW   
   9 33N6          STO   0  EGMTLA(3)
  17 347Q          SETNCORE ETRAP,3,FILE,FTRAP  [SET UP A TRAP BLOCK 
  17 34MB          LDN   0  ETRAP               [SET UP RECORD HEADER
  10 3572          STO   0  ETRAPRH(3)   
  17 35LL          LDN   0  CALL                [SET UP 'ALL' TRAP   
  10 366=          STO   0  ETRAPMODES(3)
  16 36KW          LDN   6  MDUMPER(1)          [X6->12HDUMPER   
  16 375G          LDN   7  ETRUSER1(3)         [X7->DESTINATION 
  19 37K6          MOVE  6  3                   [MOVE NAME INTO TRAP RECORD  
   4 384Q    #   
  16 38JB    #  NOW WE STEP THROUGH INCINDEX A RECORD AT A TIME. 
   4 3942    #   
  18 39HL    STEP  STEP     -1                  [X3->NEXT INCINDEX RECORD
  16 3=3=          BZE   3  YESEOF              [J IF FINISHED   
  19 3=GW          JBS      STEP,3,BIOBSR       [IGNORE OBSOLETE INCREMENTS  
  18 3?2G          LDX   5  AMTSR(3)            [AND INCS WITH NO TAPES  
   8 3?G6          BZE   5  STEP 
   4 3?_Q    #   
  20 3#FB    #  WE HAVE AN INCREMENT WITH TAPES. X5= NO OF TAPES. WE EXAMINE EACH
  10 3#_2    #  TAPE ENTRY IN THE RECORD.
   4 3*DL    #   
  20 3*Y=          LDN   4  0                   [OFFSET DOWN REC TO CURRENT ENTRY
   6 3BCW    NEXTENTRY   
  19 3BMN ...      STEPAGAIN     -1             [ RESET POINTER IN INCINDEX  
  19 3BXG          STO   4  AWORK1(2)           [SAVE OFFSET OVER REUSE OF X4
  18 3CC6          STO   5  AWORK2(2)           [DITTO: COUNT OF ENTRIES 
   7 3CWQ          SMO      4
  14 3DBB          LDX   6  AMAGR(3)            [X6=TSN  
  18 3DW2          BNG   6  NOTSN               [IGNORE IF DUMMY ENTRY   
   4 3F*L    #   
  21 3FT=    #  HAVING FOUND A TAPE WE HUNT FOR THE FI/FTAPE BLOCK SET UP WITH ALL   
  19 3G#W    #  SYSTEM.SERIAL ENTRIES BY BACKMAP. IT LIVES IN THE MISC CHAIN.
  20 3GSG    #  ONCE WE HAVE FOUND IT WE LOOK FOR AN ENTRY FOR THE TAPE USING A  
  21 3H#6    #  BINARY CHOP. IF NO ENTRY EXISTS WE IGNORE THIS ENTRY - THE DISCREPANC
  20 3HRQ    #  CANNOT BE RECTIFIED AND DIDN'T ARISE FROM THE PARTIAL RESTORE.   
   4 3J?B    #   
  15 3JR2          HUNTMISB 2,FI,FTAPE          [X2->FTAPE   
   8 3K=L          LDX   0  A1(2)
   7 3KQ=          LDX   4  2
   7 3L9W          ADX   4  0
  18 3LPG          ADN   4  A1+1                [DISTANCE MARKER FOR CHOP
  17 3M96          LDN   5  A1+1(2)             [HOME MARKER FOR CHOP
   5 3MNQ    NEXTCHOP
   7 3N8B          SRL   0  3
   7 3NN2          SLL   0  2
   7 3P7L          STO   0  2
  20 3PM=          NGNC  7  1                   [MASK OUT B0 OF INDICATED ENTRY  
   7 3Q6W          SMO      5
   8 3QLG          ANDX  7  0(2) 
  16 3R66          BXE   7  6,WEHAVEIT          [J IF ENTRY FOUND
  21 3RKQ          BZE   2  NOACTION            [[J IF NEXT CHOP WOULD GIVE SAME ENTR
   9 3S5B          BXL   7  6,MOREBIG
   7 3SK2          STO   5  4
   7 3T4L          ADX   4  0
   9 3TJ=          BRN      NEXTCHOP 
   5 3W3W    MOREBIG 
   7 3WHG          ADX   5  0
   7 3X36          LDX   0  4
   7 3XGQ          SBX   0  5
   9 3Y2B          BRN      NEXTCHOP 
   5 3YG2    WEHAVEIT
  15 3Y_L          ADX   2  5                   [X2->ENTRY   
   4 3_F=    #   
  20 3_YW    #  WE HAVE FOUND AN ENTRY. IF IT IS MARKED 'FOUND' BY BACKMAP (B0 OF
  20 42DG    #  TSN SET) WE TAKE NO ACTION - THERE IS A DIRECTORY ENTRY FOR IT.  
  19 42Y6    #  IF IT IS MARKED 'NOT FOUND' (B0 CLEAR) BUT DOES NOT BELONG TO
  21 43CQ    #  :DUMPER WE TAKE NO ACTION - THE DISCREPANCY CANNOT BE REPAIRED, AND  
  20 43XB    #  WASN'T CAUSED BY THE PARTIAL RESTORE ANYWAY. OTHERWISE WE SET UP 
  21 44C2    #  THE FLOCNB AND ENT TO INDICATE THE TAPE, LOCATE THE ENTRY POSITION   
  10 44WL    #  IN :DUMPER AND INSERT IT.
   4 45B=    #   
   8 45TW          LDXC  0  0(2) 
  17 46*G          BCS      NOACTION            [IGNORE IF 'FOUND'   
  19 46T6          TESTNAMX 3,1(2),MDUMPER(1),NOACTION[IGNORE IF NOT :DUMPER 
  19 47#Q ...      SETBIT   0,0(2)              [INDICATE ENTRY NOW 'FOUND'  
  15 47SB          MHUNT    2,FILE,FLOCNB       [X2->FLOCNB  
   9 48#2          STO   6  A1+7(2)  
   9 48RL          STOZ     A1+6(2)  
   9 49?=          STOZ     A1+5(2)  
   9 49QW          STOZ     A1+4(2)  
   9 4==G          STOZ     A1+3(2)  
  21 4=Q6          BSXD     0,BMPROCTR          [BIT IN MAGSTATE IDENTIFYING PROCTS  
   8 4=_Y ...      SMO      FX2  
   9 4?9Q ...      LDX   5  AWORK1   
   7 4?FJ ...      SMO      5
  10 4?PB          ANDX  0  AMAGSTATER(3)
  16 4#92          BNZ   0  PROCTAPE            [J IF A PROCTAPE 
  17 4#NL          LDN   4  MGEO3DUMP(1)        [X4->12HGEO3DUMPTAPE 
   9 4*8=          BRN      MOVENAME 
   5 4*MW    PROCTAPE
  17 4B7G          LDN   4  MGEO3PROC(1)        [X4->12HGEO3PROCTAPE 
   5 4BM6    MOVENAME
  18 4C6Q          LDN   5  A1(2)               [X5->DESTINATION OF NAME 
  15 4CLB          MOVE  4  3                   [MOVE IN NAME
  14 4D62          MHUNTW   2,FILE,ENT          [X2->ENT 
  18 4DKL          LDN   5  ELOC1(2)            [X5->LOCAL NAME POSITION 
  18 4F5=          MOVE  4  3                   [MOVE LOCAL NAME INTO ENT
  15 4FJW          STO   6  ESER(2)             [STORE TSN   
  14 4FLK ...      LDX   0  MGEO3DUMP+1(1)      ['DUMP'  
  17 4FN# ...      BXU   0  ELOC2(2),PROCTAPE2  [J IF NOT A DUMP TAPE
  21 4FQ3 ...      BS       2,BEUSEDUMP         [FOR DUMP TAPES ENSURE 'IN USE' BIT  
  13 4FRQ ...                                   [SET 
  10 4FTF ...      BRN      NPROCTAPE2   
   6 4FX8 ...PROCTAPE2   
  19 4FYX ...      BC        2,BEUSEDUMP        [BIT NEVER SET FOR PROC TAPES
   6 4G2L ...NPROCTAPE2  
  18 4G4G          NAME     2,FILE,FWB          [RENAME READY FOR INSERT 
  15 4GJ6          GETDIR   3                   [POSITION DIR
  17 4H3Q          TESTRPN2 NOFILE,(GEOERR)     [REPLY MUST BE NOFILE
  16 4HHB          INSERT                       [INSERT THE ENT  
  10 4J32          MHUNTW   3,FILE,FWB   
  16 4JGL          NAME     3,FILE,ENT          [RENAME ENT BACK 
  16 4K2=          MHUNTW   3,FILE,FTRAP        [TRAPS TURN NOW  
  10 4KFW          NAME     3,FILE,FWB   
  15 4K_G          INSERT                       [INSERT IT   
  10 4LF6          MHUNTW   3,FILE,FWB   
  16 4LYQ          NAME     3,FILE,FTRAP        [RENAME IT BACK  
   4 4MDB    #   
  11 4MY2    #  WE NOW MOVE ONTO A NEW ENTRY.
   4 4NCL    #   
   5 4NX=    NOTSN   
   5 4PBW    NOACTION
   8 4PWG          LDX   2  FX2  
  16 4QB6          LDX   4  AWORK1(2)           [OFFSET TO ENTRY 
  16 4QTQ          LDX   5  AWORK2(2)           [COUNT OF TSNS   
  16 4R*B          ADN   4  INCMAGLEN           [UPDATE OFFSET   
  20 4RT2          BCT   5  NEXTENTRY           [LOOP IF MORE ENTRIES IN RECORD  
  20 4S#L          BRN      STEP                [OTHERWISE LOOK FOR NEW RECORD   
   4 4SS=    #   
  15 4T?W    #  THAT'S THE LOT. WE CLOSE :DUMPER AND EXIT UP.
   4 4TRG    #   
   5 4W?6    YESEOF  
  10 4WQQ          MFREE    FILE,FLOCNB  
   9 4X=B          MFREEW   FILE,ENT 
  10 4XQ2          MFREEW   FILE,FTRAP   
   4 4Y9L    UP  
   7 4YP=          CLOSETOP  
   5 4_8W          UP
   4 4_NG    #   
   4 5286    #   
  11 52MQ          MENDAREA 30,K99INCPTRES   
   4 537B    #   
   4 53M2    #END
   6 ____ ...472642130001
