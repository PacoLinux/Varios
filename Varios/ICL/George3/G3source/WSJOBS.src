  15 22FL    #LIS     K0WSJOBS>K0ALLGEO>K0GREATGEO>K0COMMAND 
  12 22_= ...      SEG   WSJOBS,867,SECTION CENT 
   4 23YG    [   
   4 2435 ...[   
  16 245N ...[  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982 
   4 248? ...[   
  12 24=X    [     THIS SEGEMENT SELECTS THE JOBS
  11 24K#    [     SPECIFIED BY CQPRINT/CQ1  
   4 24XQ    [   
  11 257J          SEGENTRY K1WSJOBS,Q1ENTRY 
  13 25?F          FSHENTRY K2WSJOBS,Q2ENTRY,,Q2ENTRY
  12 25X2          BITDEFS  CONTEXT,12,XFSTART   
  12 266S          BITDEFS  CONTEXT,16,XREMOTE   
  12 26BL    [     DEFINES THE CQPRINT/CQ1 BLOCK 
  17 26W=    #DEF  ZGA = A1                     [GROUP A INDICATORS  
  17 27*W    #DEF  ZGB = ZGA+1                  [GROUP B INDICATORS  
  17 27TG    #DEF  ZGC = ZGB+1                  [GROUP C INDICATORS  
  17 28*6    #DEF  ZGD = ZGC+1                  [GROUP D INDICATORS  
  17 28SQ    #DEF  ZGE = ZGD+1                  [GROUP E INDICATORS  
  17 29#B    #DEF  ZGF = ZGE+1                  [GROUP F INDICATORS  
  17 29S2    #DEF  ZGG = ZGF+1                  [GROUP G INDICATORS  
  17 2=3S    #DEF  ZJCOUNT = ZGG                [JOB COUNT FOR 'WJ'  
  17 2=?L    #DEF  ZGH = ZGG+1                  [GROUP H INDICATORS  
  14 2=R=    #DEF  ZNAME =  ZGH+1               [JOB NAME
  15 2?=W    #DEF  ZUSER =  ZNAME+3             [USER NAME   
  16 2?QG    #DEF  ZPROP =  ZUSER+3             [PROPERTY NUMBER 
  18 2#=6    #DEF  ZLCOUNT= ZPROP+1             [COUNT OF JOB NOS. STORED
  15 2#PQ    #DEF  ZLJOB =  ZLCOUNT+1           [JOB NUMBER  
  11 2*9B    [     DEFINES THE CQPRINT/CQ11  
  15 2*P2    #DEF  WJOBNO  = A1                 [JOB NUMBER  
   9 2B8L    #DEF  WNAME   = WJOBNO+1
   9 2BN=    #DEF  WUSER   = WNAME+3 
   9 2C7W    #DEF  WMISC   = WUSER+3 
   9 2CCN    #DEF  WJLSTAT = WMISC   
   9 2CMG    #DEF  WYONQ   = WMISC+1 
   9 2D76    #DEF  WCONTEXT= WYONQ+1 
  10 2DLQ    #DEF  WSOURCE1= WCONTEXT+1  
  10 2F6B    #DEF  WSOURCE2= WSOURCE1+1  
  10 2FL2    #DEF  WSOURCE3= WSOURCE2+1  
  10 2G5L    #DEF  WURGE   = WSOURCE3+1  
   9 2GK=    #DEF  WCPI    = WURGE+1 
   9 2H4W    #DEF  WTIMEJ  = WCPI+1  
   9 2HJG    #DEF  WARRIVED= WTIMEJ  
  10 2J46    #DEF  WPROGTIME =WTIMEJ+2   
  10 2JHQ    #DEF  WARRIVET= WPROGTIME   
  10 2K3B    #DEF  WCSIZE  = WPROGTIME+2 
   9 2KH2    #DEF  WFWT    = WCSIZE+1
   7 2KN8    #UNS  FPARTRES  
   8 2KTB    #DEF  WJLSTATX=WFWT 
   9 2L2L    #DEF  WTLR    = WFWT+1  
   9 2Q4B    #DEF  WTSWAPS = WTLR+1  
  10 2Q5J    #DEF  WMAXSIZE= WTSWAPS+1   
  10 2Q6Q    #DEF  WWT1    = WMAXSIZE+1  
   9 2Q7Y    #DEF  WWT2    = WWT1+1  
   9 2Q96    #DEF  WWT3    = WWT2+1  
   9 2Q=#    #DEF  WEXTRA  = WWT3+1  
   9 2Q?G    #DEF  WPERPTR = WEXTRA+1
   6 2Q?L    #UNS  ECS   
   4 2Q?Q    (   
  10 2Q?W    #DEF  WCOREPRI = WPERPTR+1  
  10 2Q#2    #DEF  WMILLPRI = WCOREPRI+1 
  11 2Q#6    #DEF  SIZOCQ11 = WMILLPRI+1-A1  
   4 2Q#=    )   
   6 2Q#B    #UNS  ECS   
   4 2Q#G    #SKI
  11 2Q#N    #DEF  SIZOCQ11 = WPERPTR+1-A1   
   8 2QC=    #DEF  OCOREBIT = #20
   8 2QWW    #DEF  OWAITBIT = #40
   4 3KQ=    [   
  12 3KWN          BITDEFS  WEXTRA,0,XWELL,XLIMBO
   7 3KY7    #SKI  JWPHASE4  
  11 3K_L          BITDEFS  WEXTRA,2,XEXPR   
  11 3L36          BITDEFS  EXT+11,9,YLIMBO  
   8 3L7J    XK9            +K9  
   9 3L#2    XSAVED         #02750600
  17 3LDD    XUSER          #00020000             [USER IDENTIFIER   
  18 3LJW    XJOBL          #20000100             [WELL/NCAND IDENTIFIER 
   9 3LPG    PARMBITS       #00777777
   8 3M96    Z5             +5   
   9 3MNQ    NJLSTAT        #60000003
   8 3N8B    UJOBL          +10  
   8 3NN2                   12H  
  10 3P7L                   12HJOBLIST   
   7 3PM=                   1
   7 3Q6W                   1
   9 3QLG                   4H****   
   9 3R66    XQEND          +FLOWQ   
   4 3RKQ    [   
   9 3S5B    [      COMMAND IN ERROR 
   4 3SK2    [   
   5 3T4L    XEROUT  
   9 3TJ=          ERRORX   0(3),1(3)
   5 3W3W    XEROUT9 
   5 3WHG          UP
   4 3X36    [   
  10 3XGQ    SCQ1  MHUNTW   2,CQPRINT,CQ1
   7 3Y2B          EXIT  1  0
   4 3YG2    [   
  12 3Y_L    [     ALTER LENGTH OF CQPRINT-CQ1   
   4 3_F=    [   
   8 3_YW    SALT  CALL  1  SCQ1 
  10 42DG          LDX   4  ZLCOUNT(2)   
  17 42Y6          ADN   4  ZLJOB-A1        [REDUCE LOGICAL LENGTH   
   8 43XB    SALT4 SBX   6  FX1  
   8 4478          CALL  1  SCQ1 
  15 44C2          ALTLENG  2,4,SCQ1     [X4= LENGTH OF CQ1  
   8 44WL          ADX   6  FX1  
   8 453S    SALT9 LDX   1  FX1  
   8 4592          LDX   2  FX2  
   7 45B=          EXIT  6  0
   5 4K2=    OPENJOBS
  10 4KFW    [     S/R TO OPEN JOBLIST   
  17 4K_G    [     ON ENTRY     AWORK2-4  USERNAME/0    (****/SYSTEM)
  12 4LF6    [                  X6  LINK ADDRESS 
  10 4LYQ    [     EXIT         +0  ERROR
  11 4MDB    [                  +1  BREAK-IN 
  10 4MY2    [                  +2  OK   
   8 4NCL          SBX   6  FX1  
  18 4NX=          OPENSYS  OJL8,JOBLIST,READ,QUERY [OPEN SYSTEM.JOBLIST 
   8 4PBW          BRN      OJL6 
   5 4PWG    OPENJOBU
   8 4QB6          SBX   6  FX1  
  11 4QTQ          SETNCORE 10,3,FILE,FABSNB 
   9 4R*B          LDN   4  UJOBL(1) 
   8 4RT2          LDN   5  A1(3)
   8 4S#L          MOVE  4  10   
   9 4SS=          LDN   4  AWORK2(2)
   9 4T?W          LDN   5  A1+1(3)  
  16 4TRG          MOVE  4  3                   [SET UP USERNAME 
  17 4W5X          OPEN     OJL7,READ,QUERY     [OPEN JOBLIST(/****) 
  10 4WD#          MFREE    FILE,FABSNB  
  12 4WWL          TESTREP2 NOFILE,OJL9,NAME,OJL9
   7 4X#Y          ADN   6  1
   9 4XR=    OJL6  TESTRPN2 OK,OJL9  
   7 4Y9L    OJL7  ADN   6  1
  15 4YP=    OJL8  ADN   6  1                    [BREAK IN   
  14 4_8W    OJL9  ADX   6  FX1                  [ERROR  
   7 4_NG          EXIT  6  0
  12 4_PR    [     S/R TO SET UP JOB. NAME/NO.   
   5 4_R4    SETJOB  
  10 4_S*          MHUNTW   2,CQPRINT,CQ1
  16 4_TL          LDX   4  ZLJOB(2)               [GET JOB NO.  
  17 4_WX           BNZ   4  SETJOB9               [J IF NO. SUPPLIED
  19 4_Y8          LDX   4  ZNAME(2)               [OTHERWISE GET JOB NAME   
  10 4__F          LDX   5  ZNAME+1(2)   
  10 522Q          LDX   6  ZNAME+2(2)   
   5 5243    SETJOB9 
   8 525#          LDX   2  FX2  
   7 526K          EXIT  1  0
   5 5286    STOJNO  
  17 52MQ    [     S/R TO STORE A JOB NO. IN THE CQPRINT/CQ1 BLOCK   
  12 537B    [     ON ENTRY,    X6 LINK ADDRESS  
  10 53M2    [                  X4 JOB NO
   8 53YB    [     EXIT +0  OK   
  13 549Q    [            RESTART IF CQ1 TOO SMALL   
  10 54H6          MHUNTW   2,CQPRINT,CQ1
  10 54SG          LDX   0  ALOGLEN(2)   
  10 555W          SBN   0  ZLJOB-A1+1   
  10 55C=          SBX   0  ZLCOUNT(2)   
  19 55NL          BNG   0  RESTART             [J IF CQPRINT/CQ1 TOO SMALL  
  10 5622          SMO      ZLCOUNT(2)   
  16 56?B          STO   4  ZLJOB(2)            [STORE JOB NO.   
   7 56JQ          LDN   0  1
  19 574B          ADS   0  ZLCOUNT(2)          [UPDATE COUNT OF LIVE JOBS   
   8 57J2          LDX   2  FX2  
  16 583L          EXIT  6  0                   [BLOCK TO SMALL  
   5 6=9B    STOJCT  
  10 6=P2    [     UPDATE MOP/BACK COUNT 
  12 6?8L    [     ON ENTRY     X1 LINK ADDRESS  
  16 6?N=    [                  X5 0 IF MOP     1 IF BACKGROUND  
  10 6#7W          MHUNTW   2,CQPRINT,CQ1
   7 6#MG          LDN   0  1
   7 6*76          SMO      5
  18 6*FH          ADS   0  ZJCOUNT(2)          [UPDATE MOP/BACK COUNT   
   8 6*RY          LDX   2  FX2  
   7 6B6B          EXIT  1  0
   5 6BL2    WELLOUT 
  14 6C5L    [     S/R TO CHECK IF REQ'D WELL JOB AND....
  12 6CK=    [      TO WRITE AWAY 'WELL' DETAILS 
  12 6D4W    [     OR UPDATE JOB COUNT IF 'WJ'   
  10 6DJG    [     ON ENTRY     X2 FX2   
  17 6F46    [                  X3  JOBLIST ENTRY FOR RELEVANT JOB   
  12 6FHQ    [                  X6 LINK ADDRESS  
  15 6G3B    [                  X7 WSCOUNT INDICATOR/PROP. NO
  10 6GH2    [     EXIT         +0 ERROR 
  11 6H2L    [                  +1 BREAK IN  
   9 6HG=    [                  +2 OK
   8 6H_W          SBX   6  FX1  
  17 6JFG          LDX   5  AWORK1(2)           [SELECTION DETAILS   
  10 6J_6          SMO      JOBDATASIZE  
  10 6KDQ          LDX   4  FRH+JLSTAT(3)
   7 6KYB          SRC   4  1
   7 6LD2          SLL   5  4
  17 6LXL          BPZ   5  WO1                 [J IF NO MOP CHECK   
  16 6MC=          BPZ   4  WO7                 [J IF BACKGROUND 
   7 6MWW    WO1   SLL   5  1
  18 6NBG          BPZ   5  WO15                [J IF NO BACK CHEK REQ'D 
  14 6NW6          BNG   4  WO7                 [J IF MOP
   7 6P*Q    WO15  SLL   5  5
  17 6PTB          BPZ   5  WO17                 [J IF NO USER CHECK 
   7 6Q*2          LDN   0  3
   7 6QSL    WO16  SMO      0
  11 6R#=          LDX   4  FRH+JLUSER-1(3)  
   7 6RRW          SMO      0
  16 6S?G          BXU   4  AWORK2-1(2),WO7     [J IF WRONG USER 
   8 6SR6          BCT   0  WO16 
  16 6T=Q    WO17  LDX   4  FRH+JLPROPNO(3)     [GET PROPERTY NO.
   7 6TQB          SLL   5  2
  19 6W=2          BPZ   5  WO2                 [J IF NO CLUSTER CHECK REQ'D 
  17 6WPL          BZE   4  WO7                 [J IF    CENTRAL JOB 
   7 6X9=    WO2   SLL   5  1
  19 6XNW          BPZ   5  WO3                 [J IF NO CENTRAL CHECK REQ'D 
  17 6Y8G          BNZ   4  WO7                 [J IF NOT CENTRAL JOB
   7 6YN6    WO3   SLL   5  1
  19 6_7Q          BPZ   5  WO35                [J IF NO PROPERTY CHECK REQ'D
   7 6_MB          SMO      7
  15 7272          LDN   0  0                   [PROPERTY NO.
  17 72LL          BXU   4  0,WO7               [J IF WRONG PROPERTY 
   7 736=    WO35  SLL   5  3
  10 73KW          SMO      JOBDATASIZE  
   9 745G          LDN   1  FRH(3)   
  17 74K6          BNG   5  WO37                [J IF NCAND INCLUDED 
  16 754Q          JBS      WO7,1,JLBNOTCAND    [J IF NCAND JOB  
  17 75JB    WO37  LDX   5  AWORK1(2)           [SELECTION IDENTS.   
   7 7642          SLL   5  1
  17 76HL          BNG   5  WO4                 [J IF WELL INCLUDED  
  18 773=          JBC      WO7,1,JLBNOTCAND    [J IF WELL(IE. NOT NCAND)
  10 77GW    [     O/P WELL INFORMATION  
  10 782G    WO4   SMO      JOBDATASIZE  
  10 78G6          LDX   5  FRH+JLSTAT(3)
  11 78_Q          LDX   4  FRH+JLJOBNO(3)   
  17 79FB          BNG   7  WO6                 [J IF COUNTING JOBS  
  19 79_2    WO5   MHUNTW   2,CQPRINT,CQ11      [SET UP WELL INFO. IN CQ11   
   8 7=DL          STOZ     A1(2)
   8 7=Y=          LDN   1  A1(2)
   9 7?CW          LDN   2  A1+1(2)  
  15 7?XG          MOVE  1  SIZOCQ11-1          [ZEROIZE CQ11
   8 7#C6          SBN   2  A1+1 
  16 7#WQ          STO   4  WJOBNO(2)           [STORE JOB NO.   
  15 7*BB          STO   5  WJLSTAT(2)          [STORE JLSTAT
   7 7*DK    #UNS  FPARTRES  
   4 7*GS    (   
  10 7*K3          SMO      JOBDATASIZE  
  11 7*M=          LDX   0  FRH+JLSTATX(3)   
  10 7*PF          STO   0  WJLSTATX(2)  
   4 7*RN    )   
   7 7*W2          ANDN  5  1
  15 7B*L          BZE   5  WO58                [J IF NOT MOP
  16 7BT=          LDX   5  2                [SAVE ADDR OF CQ11  
   8 7C#W          LDX   2  FX2  
  13 7CSG    WO55  FINDACTF 2,2,CPAT,,(GEOERR),FPTR  
  17 7D#6          HUNT2J   1,JWELL,COPYSYS,2,WO55  [J IF NOT IN WELL
  17 7DRQ          BXU   4  A1+JLJOBNO(1),WO55      [J IF WRONG JOB  
  10 7F?B          LDN   4  JSOURCE1(2)  
  18 7FR2          LDX   2  5                     [RESTORE ADDR OF CQ11  
  10 7G=L          LDN   5  WSOURCE1(2)  
  18 7GQ=          MOVE  4  2                     [STORE JSOURCE WORDS   
  11 7H9W    WO58  LDX   4  FRH+JLPROPNO(3)  
  17 7HPG          STO   4  WSOURCE3(2)         [STORE PROPERTY NO.  
  10 7J96          LDN   4  FRH+JLUSER(3)
   9 7JNQ          LDN   5  WUSER(2) 
  16 7K8B          MOVE  4  3                   [STORE USERNAME  
  11 7KN2          LDN   4  FRH+JLJOBNAME(3) 
   9 7L7L          LDN   5  WNAME(2) 
  16 7LM=          MOVE  4  3                   [STORE JOB NAME  
  11 7M6W          LDX   4  FRH+JLARRIVED(3) 
  18 7MLG          STO   4  WARRIVED(2)         [STORE DATE OF ENWELLMENT
  11 7N66          LDX   4  FRH+JLARRIVET(3) 
  18 7NKQ          STO   4  WARRIVET(2)         [STORE TIME OF ENWELLMENT
   8 7P5B          LDX   4  GSIGN
  18 7PK2          STO   4  WEXTRA(2)           [SET 'WELL' JOB INDICATOR
   8 7Q4L          LDX   2  FX2  
   7 7QJ=          LDN   0  1
  16 7R3W          STO   0  ACOMMUNE1(2)        [DEPTH OF WS FILE
  18 7RHG          DOWN     QPRINT,1            [WRITE INFO. TO 'WS' FILE
  14 7S36          BRN      WO9                 [ERROR   
  14 7SGQ          BRN      WO8                 [BREAK IN
  13 7T2B          BRN      WO7                 [OK  
   7 7TG2    WO6   ANDN  5  1
  17 7T_L          ERN   5  1                   [MOP/BACK INDICATOR  
  15 7WF=          CALL  1  STOJCT              [UPDATE COUNT
  14 7WYW    WO7   ADN   6  1                   [OK  EXIT
  16 7XDG    WO8   ADN   6  1                   [BREAK IN  EXIT  
  16 7XY6    WO9   LDX   1  FX1                 [ ERROR    EXIT  
   8 7YCQ          ADX   6  FX1  
   7 7YXB          EXIT  6  0
   5 7_C2    STRTOUT 
  14 7_WL    [     S/R TO CHECK IF REQ'D 'STARTED JOB....
  17 82B=    [     AND STORE JOB IN X4 IF WS (X4=0 IF NOT REQ'D BY WS
  11 82TW    [     OR UPDATEJOB COUNT IF 'WJ'
  10 83*G    [   ON ENTRY & EXIT X2=FX2  
  14 83T6    [     X3 ADDR. OF JOB BLOCK ON ENTRY &EXIT  
   9 84#Q    [     X6 LINK ADDRESS   
  21 84SB          SBX   6  FX1                 [REL ADDRESS IN CASE OF COORDINATION 
  20 85#2          LDX   5  JOBNUM(3)           [JOB NUMBER OF JOB BEING CHECKED 
  16 85RL          STO   5  ACOMMUNE5(2)        [STORE JOB NUMBER
  18 86?=          DOWN     WSJOBSA,4           [CHECK IF JOB REQUIRED   
  17 86QW          LDX   4  ACOMMUNE4(2)        [RESET X4 AFTER UP   
  20 87=G          LDX   2  ACOMMUNE5(2)        [JOB NUMBER OF JOB BEING TESTED  
  21 87Q6          CALL  1  REFINDJOBQ          [RESET X3 TO APPROPRIATE PLACE IN JOB
   8 889Q          LDX   1  FX1  
   8 *F82          LDX   2  FX2  
  20 *FCS          ADX   6  FX1                 [ABSOLUTE LINK ADDRESS FOR EXIT  
   7 *FML          EXIT  6  0
   4 *FNK    [   
  13 *FPJ    [     S/R TO REFIND PLACE IN JOB QUEUE  
  21 *FQH    [     EITHER POINTING TO JOB WITH JOB NUMBER SPECIFIED BY X2 OR POINTING
  20 *FRG    [     TO JOB WITH HIGHEST JOB NUMBER LESS THAN THAT SPECIFIED BY X2 
   6 *FSF    REFINDJOBQ  
  17 *FTD          LDN   3  BJOBQ               [BASE OF JOB QUEUE   
   6 *FWC    REFINDLOOP  
  17 *FXB          LDX   3  FPTR(3)             [NEXT BLOCK ON QUEUE 
  20 *FY*          BXE   3  CXJO,REFINDPREV     [J IF BACK TO BASE OF JOB QUEUE  
  20 *F_#          BXE   2  JOBNUM(3),REFINDEXIT [J IF FOUND REQUIRED JOB BLOCK  
  20 *G2?          BXGE  2  JOBNUM(3),REFINDLOOP [J IF NOT FAR ENOUGH ALONG QUEUE
   6 *G3=    REFINDPREV  
  21 *G49          LDX   3  BPTR(3)             [BLOCK BEFORE WHERE REQD BLOCK WAS   
   6 *G58    REFINDEXIT  
   7 *G67          EXIT  1  0
  13 *G7=    [     S/R TO CHECK IF REQ'D SAVED JOB   
  12 *GLW    [     AND UPDATE JOB COUNT IF 'WJ'  
  12 *H6G    [     OR STORE JOB NO. IN X4 IF WS  
  11 *HL6    [     X4 =0 IF NOT REQ'D BY WS  
  10 *J5Q    [   ON ENTRY & EXIT X2=FX2  
  16 *JKB    [     ON ENTRY AND EXIT X3 HOLDS ADDR. OF HLS BLOCK 
   5 *K52    SAVEOUT 
   9 *KJL          LDX   5  AWORK1(2)
   9 *L4=          ANDX  5  XSAVED(1)
  18 *LHW          BNZ   5  TSA90               [J IF NO SAVED JOBS REQD 
   9 *M3G          LDX   5  AWORK1(2)
   7 *MH6          SLL   5  2
  17 *N2Q          BPZ   5  TSA90             [J IF NO LIVE JOBS REQD
   7 *NGB          SLL   5  8
  18 *P22          BZE   5  TSA9                [NO FURTHER CHECKS REQ'D 
  18 *PFL          BPZ   5  TSA5                [J IF NO USERNAME CHECK  
   7 *P_=          LDN   0  3
   7 *QDW    TSA4  SMO      0
  10 *RD6          LDX   4  HLSUNAM-1(3) 
   7 *RXQ          SMO      0
  16 *SCB          BXU   4  AWORK2-1(2),TSA90   [WRONG USERNAME  
   8 *SX2          BCT   0  TSA4 
  15 *TBL    TSA5  LDX   0  HLSPCT(3)           [PROP. NO.   
   9 *TLD          ANDX  0  BITS22LS 
   7 *TW=          SLL   5  2
  16 *W*W          BPZ   5  TSA6                [NO CLUSTER CHECK
  17 *WTG          BZE   0  TSA90               [NOT A CLUSTER JOB   
   7 *X*6    TSA6  SLL   5  1
  16 *XSQ          BPZ   5  TSA8                [NO CENTRAL CHECK
  16 *Y#B          BNZ   0  TSA90               [NOT ACENT JOB   
   7 *YS2    TSA8  SLL   5  1
  15 *_?L          BPZ   5  TSA9                [NO PR CHECK 
   7 *_R=          ERX   0  7
   9 B2=W          ANDX  0  BITS22LS 
  16 B2?L          BNZ   0  TSA90               [WRONG PROPERTY  
  17 B2#B    TSA9  BNG   7  TSA10               [J IF JOB COUNT REQ'D
  10 B2*6          LDX   4  HLSJONO(3)   
  13 B2*W          BRN      TSA91               [OK  
   7 B2BL    TSA10 LDN   5  1
  17 B2CB          CALL  1  STOJCT              [UPDATE 'BACK' COUNT 
  17 B2D6    TSA90 LDN   4  0                   [JOB NOT REQ'D BY WS 
   8 B2DW    TSA91 LDX   1  FX1  
   7 B2FL          EXIT  6  0
  13 B2GB    [     S/R TO TEST USERNAME IN AWORK2-4  
   5 B2H6    TESTUSER
   7 B2HW          LDN   0  3
   7 B2JL    TUS2  SMO      0
   8 B2KB          LDX   4  0(1) 
   7 B2L6          SMO      0
  11 B2LW          BXU   4  AWORK2-1(2),TUS9 
   8 B2ML          BCT   0  TUS2 
   7 B2NB          ADN   6  1
   8 B2P6    TUS9  LDX   1  FX1  
   7 B2PW          EXIT  6  0
  16 B2QL    [  S/R TO GET NEXT HIGHEST JOB NO. FROM CQPRINT/CQ1 
  12 B2RB    [  LAST JOB NO. IS HELD IN AWORK3   
  10 B2S6    NXNO  MHUNTW   3,CQPRINT,CQ1
  15 B2SW          LDX   6  AWORK3(2)           [LAST JOB NO.
   7 B2TL          NGN   4  1
  16 B2WB          LDX   0  ZLCOUNT(3)       [COUNT OF LIVE JOBS 
   8 B2X6          BZE   0  NXNO9
  18 B2XW    NXNO2 BXGE  6  ZLJOB(3),NXNO4             [J IF ALREADY O/P 
  20 B2YL          BXL   4  ZLJOB(3),NXNO4             [J IF NOT SMALLEST JOB NO.
   9 B2_B          LDX   4  ZLJOB(3) 
   7 B326    NXNO4 ADN   3  1
   8 B32W          BCT   0  NXNO2
   8 B33L          BNG   4  NXNO9
  17 B34B          STO   4  AWORK3(2)           [NEXT HIGHEST JOB NO.
   7 B356    NXNO7 EXIT  5  1
   7 B35W    NXNO9 EXIT  5  0
   4 B35_    [   
   5 B364    VOPSUB  
   9 B367          VOP      ,JWACCESS
   8 B36B          LDX   1  FX1  
   8 B36C          LDX   2  FX2  
   7 B36D          EXIT  6  0
  15 B36L    [  RESTART AT K1WSJOBS WITH LARGER CQPRINT/CQ1  
   5 B37B    RESTART 
  17 B386          STO 4  5                     [SAVE. MAY BE JOB NO.
  10 B38W          LDX   4  ALOGLEN(2)   
   7 B39L          SLL  4  1 
  17 B3=B          CALL  6  SALT4               [DOUBLE LOGLEN OF CQ1
   7 B3?6          LDX   4  5
   9 B3?W          LDX   0  AWORK1(2)
   9 B3#L          ANDX  0  XJOBL(1) 
   8 B3*B          BNZ   0  SWJ12
   8 B3B6          BRN      SWJ  
   5 B3BW    Q1ENTRY 
   4 B3CL    [   
  10 B3DB    [     COUNT THE NO. OF JOBS 
   5 B3F6    [     OR
  16 B3FW    [     O/P INFO. OF REQUIRED WELL JOBS AND ACCUMULATE
  12 B3GL    [     JOB NOS. OF REQUIRED LIVE JOBS
   4 B3HB    [   
   8 B3J6    [      ACOMMUNE1 :- 
  10 B3JW    [      B0 SET IF COUNTING   
  12 B3KL    [      B0 UNSET IF 'WS' INFO. IS O/P
   4 B3LB    [   
  10 B3M6          LDX   7  ACOMMUNE1(2) 
  15 B3MW          BNG   7  SWJ                 [J IF 'WJ'   
  13 B3NL          SETNCORE SIZOCQ11,3,CQPRINT,CQ11  
   7 B3NW    #SKI  JWPHASE4  
   4 B3NY    (   
  17 B3P2          BZE   7  SWJ                 [J IF NOT WS XCODE   
   5 B3P4    XCODE   
   9 B3P6          ACROSS   WSJOBSA,3
   4 B3P8    )   
  10 B3PB    SWJ   MHUNTW   3,CQPRINT,CQ1
  15 B3Q6          ORX   7  ZPROP(3)            [PROPERTY NO.
   9 B3QW          LDN   4  ZUSER(3) 
   9 B3RL          LDN   5  AWORK2(2)
  15 B3SB          MOVE  4  3                  [SAVE USERNAME
   9 B3T6          LDX   5  ZGA(3)   
  18 B3TW          STO   5  AWORK1(2)           [SELECTIONS INDICATORS   
  17 B3WL          BPZ   5  SID                 [J IF INDV. JOB SEL. 
  10 B3XB          STOZ     ZLCOUNT(3)   
   9 B3Y6          LDX   0  XJOBL(1) 
   7 B3YW          ANDX  0  5
  19 B3_L          BZE   0  SLJ                 [J IF ONLY LIVE JOBS REQ'D   
   9 B42B          LDX   4  AWORK2(2)
  18 B436          BZE   4  SWJ2                [J IF NO USERNAME GIVEN  
   8 B43W    [  GET USERS JOB NOS
  11 B44L    SWJ02 POP      SWJ02,,JWACCESS  
  17 B45B    SWJ01 CALL  6  OPENJOBU            [OPEN USER JOBLIST   
   9 B45Q          BRN      SWJ15A   
  14 B46#          BRN      XER95               [ERROR   
   8 B46W          BRN      XBRK3
   6 B47L    SWJ1  STEP  
  16 B48B          BZE   3  SWJ15               [J IF END OF FILE
  11 B496          LDX   4  FRH+JLJOBNO(3)   
  18 B49W    SWJ12 CALL  6  STOJNO               [STORE JOB NOS. IN CQ1  
   8 B4=L          SKIP     ,3   
   8 B4?B          BRN      SWJ1 
   6 B4#6    SWJ15 CLOSE 
   5 B4#G    SWJ15A  
   9 B4#W          LDX   0  XUSER(1) 
  19 B4*L          ERS   0  AWORK1(2)            [NO FURTHER USER CHKS REQD  
  17 B4BB    SWJ2  CALL  6  OPENJOBS            [OPEN SYSTEM.JOBLIST 
   8 B4C6          BRN      XER91
   8 B4CW          BRN      XBRK3
   9 B4DL          LDX   0  AWORK2(2)
  19 B4FB          BZE   0  SWJ25                 [J IF USERNAME NOT GIVEN   
  20 B4G6          STOZ     AWORK3(2)               [INITIALISE JOB NO. FOR NXNO 
   9 B4GW          CALL  6  VOPSUB   
   8 B4HL          BRN      SWJ3 
  19 B4JB    SWJ25 SKIP     ,1                    [TO START OF JOB ENTRIES   
   9 B4K6    SWJ3  LDX   0  AWORK2(2)
  18 B4KW          BZE   0  SWJ4               [J IF NO LIST OF JOB NOS. 
   8 B4LL          CALL  5  NXNO 
  18 B4MB          BRN      SWJ8                [J IF END OF JOB NO. LIST
  16 B4N6          GETJOB   AWORK3(2),SYSTEM    [GET JOB'S ENTRY 
  17 B4NW          TESTREPN2 OK,SWJ3            [J IF ENTRY NOT FOUND
  16 B4PL          STEPAGAIN                    [READ JOB'S ENTRY
   8 B4QB          BRN      SWJ45
   6 B4R6    SWJ4  STEP  
  16 B4RW          BZE   3  SWJ8                [END OF JOBLIST  
  10 B4SL    SWJ45 STO   3  ACOMMUNE3(2) 
  10 B4TB          SMO      JOBDATASIZE  
  10 B4W6          LDX   0  FRH+JLSTAT(3)
  16 B4WW          BPZ   0  SWJ68               [J IF WELL JOB   
  15 B4XL    SWJ5  LDX   6  FRH+JLJOBNO(3)      [GET JOB NO. 
   8 B4YB          LDN   3  BJOBQ
   9 B4_6    SWJ53 LDX   3  FPTR(3)  
  16 B4_W          BXE   3  CXJO,SWJ6           [J IF NOT IN JOBQ
  17 B52L          BXU   6  JOBNUM(3),SWJ53     [J IF NOT REQ'D JOB  
  11 B532          JBS      SWJ6,3,JBSAVING  
  17 B53B          CALL  6  STRTOUT             [STORE LIVE JOB INFO.
   8 B546          BRN      SWJ65
   8 B54W    SWJ6  LDN   3  BHLSQ
   9 B55L    SWJ63 LDX   3  FPTR(3)  
  16 B56B          BXE   3  CXHL,SWJ68          [J IF END OF HLSQ
  13 B56Y          HLSAV   3,SET,SAVING,HLSQ,SWJ64   
  13 B57G          HLSAV   3,CLEAR,SAFE,HLSQ,SWJ63   
   5 B584    SWJ64   
  17 B58L          BXU   6  HLSJONO(3),SWJ63    [J IF NOT REQ'D JOB  
  18 B59B          CALL  6  SAVEOUT             [STORE SAVED JOB INFO.   
  17 B5=6    SWJ65 BZE   4  SWJ3                [J IF NOT REQ'D BY WS
   7 B5=W          LDN   0  1
  16 B5?L          STO   0  ACOMMUNE1(2)        [DEPTH OF WS FILE
  10 B5#B          STO   4  ACOMMUNE4(2) 
   9 B5*6          DOWN     WSJOBSA,2
  15 B5*W          BRN      XER8                [J IF ERROR  
  16 B5BL          BRN      XBRK1               [J IF BREAK-IN   
   8 B5CB          BRN      SWJ3 
  10 B5D6    SWJ68 LDX   3  ACOMMUNE3(2) 
   9 B5DW          CALL  6  WELLOUT  
  16 B5FL          BRN      XER8                [ERROR REPLY SET 
   8 B5GB          BRN      XBRK1
   8 B5H6          BRN      SWJ3 
  16 B5HW    SWJ8  CLOSE                        [CLOSE JOBLIST   
   8 B5JL          BRN      Q2ND9
  11 B5KB    [     CHECK IF LIVE JOB IN JOBQ 
   4 B5L6    SID 
  11 B5LW          HUNT     3,ADATA,CSTORE   
   8 B5ML          BNG   3  SID1 
  19 B5NB          NAME     3,CPB,CUNI          [JOB NAME/NO.IN CASE OF ERROR
   8 B5P6    SID1  LDN   3  BJOBQ
   9 B5PW    SID2  CALL  1  SETJOB   
   8 B5QL          LDX   1  FX1  
  16 B5RB    SID4  LDX   3  FPTR(3)             [NEXT JOB IN JOBQ
  16 B5S6          BXE   3  CXJO,SIE            [J IF END OF JOBQ
  17 B5SG          HLSAV    3,SET,SAVING,JOBQ,SID4 [J IF BEING SAVED 
  17 B5SW          BPZ   4  SID6                [J IF JOB NO. GIVEN  
   9 B5TL          TXU   4  JNAME(3) 
  10 B5WB          TXU   5  JNAME+1(3)   
  10 B5X6          TXU   6  JNAME+2(3)   
  17 B5XW          BCS      SID4                [J IF WRONG JOB NAME 
  10 B5YL          LDN   1  JUSER-1(3)   
  17 B5_B          CALL  6  TESTUSER                   [CHECK USER   
  17 B626          BRN      SID2                        [WRONG USER  
   8 B62W          BRN      SID7 
  17 B63L    SID6  BXU   4  JOBNUM(3),SID4      [J IF WRONG JOB NO.  
   9 B64B    SID7  CALL  6  STRTOUT  
  17 B656          BZE   4  XER7                [USER/CLUSTER CHECK  
   8 B65W          BRN      SIE9 
   4 B66L    SIE 
  11 B67B    [     CHECK IF LIVE SAVED JOB   
   8 B686          LDN   3  BHLSQ
   9 B68W    SIE2  CALL  1  SETJOB   
   8 B69L          LDX   1  FX1  
   9 B6=B    SIE4  LDX   3  FPTR(3)  
  18 B6?6          BXE   3  CXHL,SIF            [J IF END OF SAVED JOBS  
  18 B6#=          HLSAV    3,CLEAR,SAFE,HLSQ,SIE4 [J IF NOT SAVED JOB   
  17 B6*B          BPZ   4  SIE6                [J IF JOB NO. GIVEN  
  10 B6B6          TXU   4  HLSJNAM(3)   
  10 B6BW          TXU   5  HLSJNAM+1(3) 
  10 B6CL          TXU   6  HLSJNAM+2(3) 
   8 B6DB          BCS      SIE4 
  10 B6F6          LDN   1  HLSUNAM-1(3) 
  15 B6FW          CALL  6  TESTUSER           [CHECK USER   
  15 B6GL          BRN      SIE2                [WRONG USER  
   8 B6HB          BRN      SIE7 
  17 B6J6    SIE6  BXU   4  HLSJONO(3),SIE4     [J IF WRONG JOB NO.  
   9 B6JW    SIE7  CALL  6  SAVEOUT  
  17 B6KL          BZE   4  XER7                [WRONG USER/CLUSTER  
  17 B6LB    SIE9  CALL  6  STOJNO              [STORE JOB NO. IN CQ1
   8 B6M6          BRN      Q2ND 
   4 B6MW    SIF 
  17 B6NL    [     GET JOB ENTRY FROM JOB LIST & O/P WELL INFORMATION
  19 B6PB          BNG   4  SIF3                [J IF JOB NO. NOT SUPPLIED   
  17 B6Q6          CALL  6  OPENJOBS            [OPEN SYSTEM.JOBLIST 
  15 B6QW          BRN      XER92               [CAN'T OPEN  
  14 B6RL          BRN      XBRK2               [BREAK IN
   9 B6SB          CALL  1  SETJOB   
  16 B6T6          GETJOB   4,SYSTEM            [GET JOB ENTRY   
   8 B6TW          BRN      SIF4 
  17 B6WL    SIF3  CALL  6  OPENJOBU            [OPEN JOBLIST(/****) 
   8 B6X6          BRN      XER6A
  14 B6XL          BRN      XER96               [ERROR   
  14 B6Y6          BRN      XBRK2               [BREAK IN
  10 B6YW          MHUNTW   3,CQPRINT,CQ1
   9 B6_L          LDN   4  ZNAME(3) 
  16 B72B          GETJOB   4,USER              [GET JOB'S ENTRY 
   9 B736    SIF4  TESTREPN2 OK,XER6 
   7 B73W          STEPAGAIN 
  11 B742          LDX   4  FRH+JLJOBNO(3)   
  10 B746          MHUNTW   2,CQPRINT,CQ1
   9 B74=          LDX   6  ZLJOB(2) 
  18 B74B          STO   4  ZLJOB(2)           [SET JOB NO. IF NAME GIVEN
   8 B74G          LDX   2  FX2  
  16 B74L          BNZ   6  SIF7           [J IF JOB N0. GIVEN   
   6 B74Q          CLOSE 
   8 B74W          BRN      SID1 
   9 B752    SIF7  LDX   0  XJOBL(1) 
  17 B75B          ORS   0  AWORK1(2)           [SET WELL IDENTIFIER 
  16 B766    SIF8  CALL  6  WELLOUT             [O/P WELL INFO.  
  14 B76W          BRN      XER8                [ERROR   
  15 B77L          BRN      XBRK1                [BREAK-IN   
   6 B78B    SIF9  CLOSE 
  11 B796          HUNT     3,ADATA,CSTORE   
  17 B79W          BPZ   3  Q2ND95              [J IF JOB NO. FOUND  
  15 B7=L           BRN       XER7               [NO SUCH JOB
   4 B7?B    SLJ 
  13 B7#6    [     ACCUMULATE JOB NOS. OF LIVE JOBS  
  11 B7#W    [     SELECT 'NON-SAVED' JOBS   
   9 B7*L          LDX   5  AWORK1(2)
   7 B7BB          SLL   5  2
  19 B7C6          BPZ   5  Q2ND                  [J IF LIVE JOBS NOT REQD   
   7 B7CW          SLL   5  1
  19 B7DL          BNG   5  SSA                 [J IF ONLY SAVED JOBS REQ'D  
   8 B7FB          LDN   3  BJOBQ
   9 B7G6    SNS3  LDX   3  FPTR(3)  
   8 B7GW          TXU   3  CXJO 
  15 B7HL          BCC      SSA                 [END OF JOBQ 
  17 B7J2          HLSAV    3,SET,SAVING,JOBQ,SNS3 [J IF BEING SAVED 
   9 B7JB          CALL  6  STRTOUT  
  18 B7K6          BZE   4  SNS3                [J IF JOB NOT REQ'D BY WS
   9 B7KW          CALL  6  STOJNO   
   8 B7LL          BRN      SNS3 
   4 B7MB    SSA 
  10 B7N6    [     SELECT 'SAVED' JOBS   
   9 B7NW          LDX   5  AWORK1(2)
   8 B7PL          LDX   1  FX1  
   9 B7QB          ANDX  5  XSAVED(1)
  18 B7R6          BNZ   5  Q2ND                [J IF SAVED JOBS EXCLUDED
   8 B7RW          LDN   3  BHLSQ
   9 B7SL    SSA3  LDX   3  FPTR(3)  
   8 B7TB          TXU   3  CXHL 
  17 B7W6          BCC      Q2ND                [NO MORE SAVED JOBS  
  17 B7X=          HLSAV    3,CLEAR,SAFE,HLSQ,SSA3 [J IF NOT SAVED   
   9 B7YB          CALL  6  SAVEOUT  
  17 B7_6          BZE   4  SSA3                [J IF NOT REQ'D BY WS
   9 B7_W          CALL  6  STOJNO   
   8 B82L          BRN      SSA3 
  16 B83B    Q2ND  BNG   7  Q2ND9               [J IF 'WJ' COUNT 
   8 B846          CALL  1  SCQ1 
  17 B84W          LDX   4  ZLCOUNT(2)          [COUNT OF LIVE JOBS  
  17 B85L          BZE   4  Q2ND9               [J IF NO FURTHER O/P 
   9 B86B          ADN   4  ZLJOB-A1 
  15 B8?J          CALL  6  SALT4               [REDUCE CQ1  
  17 B8DQ          STOZ     ACOMMUNE1(2)         [DEPTH OF WS FILE   
   9 B8KY          DOWN     WSJOBSA,1
  15 B8R6          BRN      XEROUT9             [J IF ERROR  
  16 B8Y#          BRN      XBRK2               [J IF BREAK-IN   
   8 B95L          BRN      Q2ND9
   6 B9K=    Q2ND7 CLOSE 
   5 B9KF    Q2ND9   
  15 B9KN          BNG   7  Q2ND99                [J IF 'WJ' 
  11 B9KX          HUNT     3,ADATA,CSTORE   
   9 B9L6          BPZ   3  Q2ND95   
   9 B9L*          DOWN     QPRINT,4 
   9 B9LJ          BRN      XEROUT9  
   8 B9LR          BRN      XBRK2
   9 B9M2          BRN      Q2ND99   
   5 B9M9    Q2ND95  
   7 B9MD          FREECORE 3
   5 B9MM    Q2ND99  
   7 B9MW          UPPLUS   2
   4 B9N9    [   
   8 B9N#    [     BREAK IN EXIT 
   4 B9NH    [   
   6 B9NQ    XBRK1 CLOSE 
   7 B9N_    XBRK2 UPPLUS 1  
  19 B9P2    XBRK3 LDX   0  AWORK2(2)            [B-IN WHILE OPENING JOBLIST 
  18 B9P3          BZE   0  XBRK2            [J IF NO USERNAME(IE.POP'D) 
   9 B9P4          CALL  6  VOPSUB   
   8 B9P5          BRN      XBRK2
   4 B9P8    [   
   8 B9PC    [     COMMAND ERRORS
   4 B9PL    [   
   6 B9PT    XER6  CLOSE 
  10 B9Q4    XER6A MHUNT    3,CPB,CUNI   
   9 B9Q?          LDX   4  APARA(3) 
  15 B9QG          BNG   4  XER62             [J IF JOB NAME 
  15 B9QP          CALL  3  XEROUT            [NO SUCH JOB   
   9 B9QY                   +FWHSTAT 
   8 B9R7                   -1   
   9 B9RB    XER62 CALL   3  XEROUT  
  16 B9RK                   +EWHSTAT          [NO SUCH JOB NAME  
   8 B9RS                   -1   
  17 B9S3    XER7  BZE   7  XER75               [J IF NO PROP CHECK  
  16 B9S=          CALL  3  XEROUT              [WRONG CLUSTER   
   9 B9SF                   +HWSCLUS 
   8 B9SN                   -1   
  15 B9SX    XER75 CALL  3  XEROUT               [WRONG USER 
   9 B9T6                   +HWSUSER 
   8 B9T*                   -1   
   6 B9TJ    XER8  CLOSE 
   9 B9TR          BRN      XEROUT9  
   9 B9W2    XER91 CALL  6  VOPSUB   
   9 B9W9    XER92 BRN      XEROUT9  
   5 B9W=    XER95   
   9 B9W?          CALL  6  VOPSUB   
   5 B9W#    XER96   
  11 B9W*          TESTRPN2 NOUSER,XEROUT9   
   9 B9WB          VFREE    CPB,CUNI 
   9 B9WC          CALL  3  XEROUT   
   9 B9WD                   +ERNOUSER
   8 B9WF                   -1   
   4 B9WG    [   
   4 B?46    #END
   6 ____ ...060345350001
