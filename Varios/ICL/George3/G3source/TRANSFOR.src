  12 22FL          SEG      TRANSFOR,1,BIDGOOD   
   4 22_=    #   
  19 23DW    #     THIS SEGMENT CONVERTS THE RECORD  FROM ADATA/ALINE BLOCK  
   9 23YG    #     TO SPECIFIED MODE.
   4 24D6    #   
  16 24XQ    #           AWORK1=IF ALPHA-PREVIOUS;SHIFT CHARACTER
  15 25CB    #           AWORK2=        ^        ;       ^   
  20 25X2    #           AWORK3=        ^        ;NO/UNUSED CHARS IN LAST WORD   
  16 26BL    #           AWORK1=IF SHIFT-GRAPHIC; TERMINAL SHIFT 
  17 26W=    #           AWORK2=        ^        ; PTR TO ADATA/AREST
  17 27*W    #           AWORK3=        ^       ; N/CH IN ADATA/AREST
  21 27TG    #     AWORK4: B7 SET INDICATES LAST CH. OF PREVIOUS TP PERI WAS A DELTA.
  16 28*6    #           ACOMMUNE1=OVERWRITTEN;SRMOVE SUBROUTINE 
   4 28SQ    #   
  12 29#B          SEGENTRY K1TRANSFOR,Z1TRANSFOR
   9 29S2    SPACE          #20202020
   9 2=?L    PLUS           #20000000
   8 2=R=    TEN            +10  
   8 2?=W    X37            +37  
   8 2?QG    X40            #40  
   8 2#=6    X64            #64  
   8 2#PQ    X74            #74  
   8 2*9B    XDELTA         #76  
   8 2*P2    X77            #77  
   8 2B8L    NORM           +5   
   8 2BN=    NOPM           +5   
   9 2C7W    TABLEREC       #00000420
   9 2CMG                   #24100000
   9 2D76                   #00000203
   9 2DLQ                   #05070000
   9 2F6B                   #04010004
   9 2FL2                   #03100000
   9 2G5L                   #20000200
   9 2GK=                   #02070000
   9 2H4W                   #24010001
   9 2HJG                   #00100000
   9 2J46                   #10060606
   9 2JHQ                   #06000000
   6 2K3B    Z1TRANSFOR  
  10 2KH2          LDX   4  ACOMMUNE1(2) 
  15 2L2L          ANDN  4  #7775               [IGNORE B22  
   8 2LG=          LDX   3  FX1  
  17 2L_W          LDX   5  NORM(1)             [NO OF RECORD MODES  
   7 2MFG    TESRM BCHX  3  /
  10 2M_6          LDCH  0  TABLEREC(3)  
  19 2NDQ          BXE   0  4,RMFOUND           [J IF REC.MODE HAS BEEN FOUND
   8 2NYB          BCT   5  TESRM
  17 2PD2          BRN      REPER               [J IF MODE IS ILLEGAL
   5 2PXL    RMFOUND 
  18 2QC=          SBX   3  1                   [PTR.RELATIVE TO TABLEREC
  15 2QWW          LDX   4  ACOMMUNE2(2)        [PERI MODE   
  15 2RBG          ANDN  4  #7775               [IGNORE B22  
  10 2RW6          LDN   2  TABLEREC(1)  
  16 2S*Q          LDX   5  NOPM(1)             [NO OF PERI MODES
   7 2STB    TESPM ADN   2  2
   8 2T*2          LDCH  0  0(2) 
  17 2TSL          BXE   0  4,PMFOUND           [J IF PERI MODE FOUND
  17 2W#=          BCT   5  TESPM               [J TO TEST NEXT MODE 
  17 2WRW          BRN      REPER               [J IF MODE IS ILLEGAL
   5 2X?G    PMFOUND 
   9 2XR6          SETREP   COORED   
  17 2Y=Q          ADX   2  3                   [MAKE PTR.TO TABLE   
   8 2YQB          LDCH  4  0(2) 
  17 2_=2          BZE   4  UP                  [J IF NO CONVERSION  
   9 2_PL          ADN   4  PTR(1)   
  10 329=          HUNT     3,ADATA,ACONV
   8 32NW          BNG   3  UP   
   8 338G          ADN   3  A1   
  17 33N6          CALL  6  RECLEN              [GET LENGTH OF RECORD
   4 347Q    #   
  12 34MB    #              EXIT:X3=PTR TO RECORD
  15 3572    #                   X5=RECORD LENGTH IN CHARS.  
  13 35LL    #                   X7=N/CH IN RECORD   
   4 366=    #   
   8 36KW          STO   4  GEN0 
   7 375G          SBN   3  1
  21 37K6          LDCT  4  #006                [TEST IF BITS SET INDICATING 'DELTA L
  21 384Q          ANDX  4  0(3)                [CH. IN 2000 CH. RECORD OR LAST CH. O
  21 38JB          ERS   4  0(3)                [TP PERI. CLEAR ANY BIT THAT IS SET. 
   7 3942          ADN   3  1
  20 39HL          LDX   0  4                   [X0 INDICATES STATE OF BOTH BITS.
   7 3=3=          SLL   0  7
  17 3=GW          SRL   0  7                   [GET RID OF BIT 6.   
   8 3?2G          LDX   2  FX2  
  20 3?G6          STO   0  AWORK4(2)           [PRESERVE STATE OF B7 IN AWORK4. 
   4 3?_Q    PTR 
   9 3#FB          BRN      (GEN0)   
  14 3#_2          BRN      XALPR               [ALP-PREV
  14 3*DL          BRN      XPRAL               [PREV-ALP
  15 3*Y=          BRN      XACNM               [ALLC-NORM   
  16 3BCW          BRN      XAANP               [ALLCALP-NORMPREV
  16 3BXG          BRN      XAPNA               [ALLCPREV-NORMALP
  15 3CC6          BRN      XSHGR               [SHIFT-GRAP  
  16 3CWQ          BRN      XGRAL               [GRAP-SHIFT,ALPH 
  16 3DBB          BRN      XGRPR               [GRAP-SHIFT,PREV 
   4 3DW2    #   
   9 3F*L    REPER SETREP   ILLMODE  
   5 3FT=    UP    UP
   4 3G#W    #   
  11 3GSG    #              ALLCHAR-NORMAL   
   4 3H#6    #   
   5 3HRQ    XACNM   
  17 3J?B          CALL  4  SRACNM              [CONVERSION ROUTINE  
   5 3JR2          UP
   4 3K=L    #   
   5 3KQ=    SRACNM  
  15 3L9W          BZE   5  UP                  [EMPTY RECORD
  18 3LPG          LDX   6  3                   [REMEMBER PTR TO RECORD  
   8 3LWN          LDCT  7  #002 
   9 3M3W          ANDX  7  AWORK4(2)
  15 3M96          LDX   2  3                   [INPUT PTR   
  20 3MDY          BNZ   7  TNCH                [J IF LAST CH OF PREVIOUS WAS A _
  15 3MNQ    NEXT  LDCH  0  0(2)                [LOAD CHAR   
   7 3N8B          BCHX  2  /
   8 3NN2          SBN   0  #76  
  15 3P7L          BZE   0  RUND                [J IF DELTA  
   8 3PM=          ADN   0  #76  
  15 3Q6W    SUDEP DCH   0  0(3)                [DEPOSIT CHAR
   7 3QLG          BCHX  3  /
  17 3R66    RUNDL BCT   5  NEXT                [J IF MORE TO TEST   
  15 3RKQ    UPRT  CALL  5  NEWRT               [UPDATE R/T  
   7 3S5B          EXIT  4  0
   4 3SK2    #   
  17 3T4L    RUND  BCT   5  TNCH                [J IF MORE TO TEST   
   8 3TJ=          LDN   0  #76  
  16 3W3W          DCH   0  0(3)                [DEPOSIT DELTA   
   7 3WHG          BCHX  3  /
   8 3X36          BRN      UPRT 
   4 3XGQ    #   
  16 3Y2B    TNCH  LDCH  0  0(2)                [LOAD SUCCESSOR  
   7 3YG2          BCHX  2  /
   8 3Y_L          SBN   0  #20  
  15 3_F=          BZE   0  RUNDL1               [J IF RUNOUT
   8 3_YW          SBN   0  #53  
  15 42DG          BZE   0  RUNDL1               [J IF DELETE
  15 42Y6          ADN   0  #73                 [SUCCESSOR   
  20 433=          BZE   7  TNORM               [J IF LAST CH OF PREVIOUS NOT A _
   7 436B          STOZ     7
   8 439G          BRN      SUDEP
   5 43#L    TNORM   
   8 43CQ          LDN   1  #76  
  16 43XB          DCH   1  0(3)                [DEPOSIT DELTA   
   7 44C2          BCHX  3  /
   8 44WL          LDX   1  FX1  
   8 45B=          BRN      SUDEP
   5 45CM    RUNDL1  
   8 45F4          BZE   7  RUNDL
  21 45GF          SMO      FX2                 [UNSET SWITCH TO INDICATE WE HAVE TAK
  21 45HW ...      ERS   7  AWORK4              [ACCOUNT OF _ SPLIT OVER TWO PERIS.  
   7 45K?          STOZ     7
  10 45LN          MHUNT    1,ADATA,ALINE
  21 45N5          LDN   0  1                   [REMOVE _ FROM PREVIOUS PERI SINCE   
  20 45PG          SBS   0  A1(1)               [IT IT PART OF ALLCHAR CHARACTER 
  18 45QX          LDX   1  FX1                 [WHICH MUST BE REMOVED.  
   8 45S#          BRN      RUNDL
   4 45TW    #   
  10 46*G    #              SHIFT-GRAPHIC
   4 46T6    #   
   5 47#Q    XSHGR   
   9 47SB          STOZ     AWORK4(2)
  18 48#2          LDX   6  3                   [REMEMBER PTR TO RECORD  
   7 48RL          SLC   3  2
   7 49?=          SBN   3  2
   7 49QW          SRC   3  2
  16 4==G          LDCH  0  0(3)                [TERMINAL SHIFT  
   9 4=Q6          STO   0  AWORK1(2)
   7 4?9Q          LDN   0  0
  14 4?PB          DCH   0  0(3)                [MODE=0  
  15 4#92          BZE   5  UP                  [EMPTY RECORD
  15 4#NL          LDX   1  6                   [INPUT PTR   
  15 4*8=          LDX   3  6                   [OUTPUT PTR  
  17 4*MW          BNZ   4  SUCLO               [J IF DELTA AT 2000  
  16 4B7G    NEXT1 LDCH  0  0(1)                [LOAD CHARACTER  
   7 4BM6          BCHX  1  /
   8 4C6Q          SBN   0  #74  
  15 4CLB          BPZ   0  XFUCH               [J IF SHIFT  
   5 4D62    PUTCH   
   8 4DKL          ADN   0  #74  
  15 4F5=          DCH   0  0(3)                [DEPOSIT CHAR
   7 4FJW          BCHX  3  /
  16 4G4G    TMOR  BCT   5  NEXT1               [J IF MORE CHARS 
  15 4GJ6    RTUP  CALL  5  NEWRT               [UPDATE R/T  
   5 4H3Q          UP
   5 4HHB    XFUCH   
   7 4J32          SBN   0  2
  16 4JGL          BNZ   0  TMOR                [IGNORE IF $ OR ]
  16 4K2=          BCT   5  SUCLO               [J IF MORE CHARS 
   8 4KFW          BRN      RTUP 
  16 4K_G    SUCLO LDCH  0  0(1)                [LOAD SUCCESSOR  
   7 4LF6          BCHX  1  /
   8 4LYQ          SBN   0  #64  
  16 4MDB          BNG   0  STMAY               [J IF MAYBE STOP 
   7 4MY2          SBN   0  4
  17 4NCL          BPZ   0  TMOR               [J IF NATIONAL CHAR   
   7 4NX=          ADN   0  4
   8 4PBW          BRN      PUTCH
   5 4PWG    STMAY   
   8 4QB6          ADN   0  #43  
  17 4QTQ          BNG   0  TMOR                [J IF TO BE IGNORED  
   7 4R*B          LDN   0  2
  10 4RT2          ANDX  0  ACOMMUNE2(2) 
  17 4S#L          BZE   0  TMOR                [J IF READ N CHARS   
  16 4SS=          BCT   5  ONCAR               [J IF MORE CHARS 
   8 4T?W          SETREP2  STOP 
   8 4TRG          BRN      RTUP 
  17 4W?6    ONCAR SBX   1  3                   [INPUT PTR-OUTPUT PTR
  14 4WQQ          STO   1  AWORK2(2)           [REMEMBER
  17 4X=B          LDX   4  5                   [N/CH NOT CONVERTED  
  14 4XQ2          ADN   4  15                  [ORUND UP
  13 4Y9L          SRL   4  2                   [N/W 
  10 4YP=          MHUNT    2,ADATA,AREST
  15 4_8W          BPZ   2  PRESB               [J IF EXISTS 
  11 4_NG          SETUPCORE 4,2,ADATA,AREST 
   8 5286          BRN      OKLEN
  15 52MQ    PRESB BXL   4  ALOGLEN(2),OKBL     [LENGHT OK   
  15 537B          LDX   7  2                   [PTR TO AREST
  17 53M2          ALTLEN   7,4                 [ALTER AREST'S LENGTH
  10 546L          MHUNT    2,ADATA,AREST
  10 54L=    OKLEN MHUNT    1,ADATA,ACONV
  16 555W          ADN   1  A1+2                [PTR TO RECORD   
  17 55KG          BXE   1  6,OKBL              [ACONV DID NOT MOVE  
  15 5656          SBX   3  6                   [RELATIVE PTR
  16 56JQ          LDX   6  1                   [PTR TO RECORD   
  16 574B          ADX   3  1                   [NEW OUTPUT PTR  
  18 57J2    OKBL  LDX   7  2                   [REMEMBER PTR TO AREST   
   8 583L          LDX   1  FX2  
  16 58H=          STO   5  AWORK3(1)           [N/CH IN AREST   
   8 592W          ADN   2  A1+2 
  17 59GG          LDX   1  AWORK2(1)           [RELATIVE INPUT PTR  
  17 5=26          ADX   1  3                   [ABSOLUTE INPUT PTR  
  16 5=FQ          CALL  0  SRMOVE              [RESIDUE TO AREST
  17 5=_B          CALL  5  NEWRT               [UPDATE R/T IN ACONV 
   8 5?F2          LDX   2  FX2  
  16 5?YL          LDX   0  AWORK1(2)           [TERMINAL SHIFT  
  15 5#D=          LDX   1  7                   [PTR TO AREST
   7 5#XW          SLL   0  6
  13 5*CG          ORN   0  #41                 [PFCC
   9 5*X6          STO   0  A1+1(1)  
  16 5BBQ          LDN   6  A1+2(1)             [PTR TO RECORD   
  16 5BWB          LDX   3  AWORK3(2)           [N/CH IN AREST   
  17 5CB2          CALL  5  NEWR1               [UPDATE R/T IN AREST 
   8 5CTL          SETREP   STOP 
   5 5D*=          UP
   4 5DSW    #   
  11 5F#G    #              PREVIOUS-ALPHA   
   4 5FS6    #   
   5 5G?Q    XPRAL   
   8 5GRB          LDX   2  FX2  
   8 5H?2          LDN   0  #74  
  15 5HQL          STO   0  AWORK1(2)           [STORE ALPHA 
   8 5J==          LDN   0  #75  
  15 5JPW          STO   0  AWORK2(2)           [STORE BETA  
   8 5K9G          BRN      XCONV
   4 5KP6    #   
  11 5L8Q    #              ALPHA-PREVIOUS   
   4 5LNB    #   
   5 5M82    XALPR   
   8 5MML          LDX   2  FX2  
   8 5N7=          LDN   0  #75  
  15 5NLW          STO   0  AWORK1(2)           [STORE BETA  
   8 5P6G          LDN   0  #74  
  15 5PL6          STO   0  AWORK2(2)           [STORE ALPHA 
   4 5Q5Q    #   
   5 5QKB    XCONV   
  15 5R52          BZE   5  UP                  [EMPTY RECORD
  18 5RJL          LDX   6  3                   [REMEMBER PTR TO RECORD  
  15 5S4=          SBN   3  2                   [PTR TO N/W  
  13 5SHW          LDX   0  0(3)                [N/W 
  15 5T3G          SBN   0  2                   [- RED TAPE  
  13 5TH6          SLL   0  2                   [N/CH
  19 5W2Q          SBX   0  5                   [NO/UNUSED CHARS IN LAST WORD
   9 5WGB          STO   0  AWORK3(2)
   7 5X22          SLC   3  2
   7 5XFL          ADN   3  6
   7 5X_=          SRC   3  2
  20 5YDW          LDCH  0  0(3)                [END SHIFT OF PRECEDING RECORD   
   8 5YYG          SBN   0  #75  
  15 5_D6          BNZ   0  SHUP                [JIF ALPHA   
   9 5_XQ          LDX   0  AWORK1(2)
  21 62CB          DCH   0  0(3)                [STOR ALPHA IF CONVERSION TO ALPHA   
  17 62X2          LDX   3  6                   [RESTORE PTR TO RCD  
  15 63BL    NEXT2 LDCH  0  0(3)                [LOAD CHAR   
   7 63W=          BCHX  3  /
  20 64*W          BXGE  0  X40(1),SHCH         [J IF SHIFT OR SENSITIVE CHAR.   
   8 64TG    TRCH  BCT   5  NEXT2
  17 65*6          BRN      UP                  [J IF NO CONVERSION  
  17 65SQ    SHCH  BXU   0  AWORK1(2),NOTAL     [J IF IT ISN'T ALPHA 
   7 66#B          LDX   1  3
   7 66S2          SLC   3  2
   7 67?L          SBN   3  1
   7 67R=          SRC   3  2
   7 68=W          LDX   2  3
  18 68QG          CALL  0  SRMOVE              [DELETE ALPHA,CLOSE GAP  
  16 69=6          LDX   3  7                   [N/CH IN RECORD  
   7 69PQ          SBN   3  1
  16 6=9B    SETRP CALL  5  NEWR1               [UPDATE RED TAPE 
   8 6=P2    SHUP  SETREP   SHIFT
   5 6?8L          UP
   8 6?N=    NOTAL LDX   2  FX2  
  14 6#7W ...      BXE   0  AWORK2(2),UP      [J IF BETA 
  18 6#MG          BXU   0  XDELTA(1),SENCH     [J IF SHIFT-SENS.CHAR.   
   8 6*76          BCT   5  SKISC
   8 6*LQ          BRN      UP   
  19 6B6B    SKISC BCHX  3  /                   [IF DELTA,SKIP ITS SUCCESOR  
   8 6BL2          BCT   5  NEXT2
   8 6C5L          BRN      UP   
   7 6CK=    SENCH LDX   1  6
  18 6D4W          SBN   1  2                   [PTR TO RECORD RED TAPE  
  15 6DJG          LDX   4  7                   [N/USED CHARS
  15 6F46          LDX   7  0(1)                [N/W IN RCD  
  17 6FHQ          ADX   7  1                   [ADDR OF LAST WORD   
   8 6G3B          LDX   2  FX2  
   8 6GH2    NCHTE LDX   1  FX1  
  19 6H2L          BXL   5  X37(1),RESTM        [J TO SHIFT REST OF RECORD   
  16 6HG=          SBN   7  9                   [UPDATE END ADDR.
  10 6H_W          LDN   0  ACOMMUNE1(2) 
  18 6JFG          MOVE  7  9                   [MOVE 9 WORDS TO BUFFER  
   7 6J_6          LDX   1  7
   7 6KDQ          BCHX  1  /
  20 6KYB          MVCH  0  36                  [SHIFT 9 WORDS 1 CHAR.POS.ONWARDS
  19 6LD2          SBN   5  36                  [UPDATE REMAINDER OF CHARS   
  18 6LXL          ADX   5  AWORK3(2)           [ADD N/CH IN LAST WORD   
   9 6MC=          STOZ  AWORK3(2)   
  16 6MWW          BRN      NCHTE               [J TO SHIFT IT   
   7 6NBG    RESTM SLC   3  2
   7 6NW6          SBN   3  1
   7 6P*Q          SRC   3  2
  17 6PTB          BZE   5  SHINS               [J TO INSERT SHIFT   
   7 6Q*2          LDX   7  3
  10 6QSL          LDN   0  ACOMMUNE1(2) 
   7 6R#=          SMO      5
  19 6RRW          MVCH  7  0                   [MOVE REST OF RCD TO BUFFER  
   7 6S?G          LDX   1  3
   7 6SR6          BCHX  1  /
  10 6T=Q          LDN   0  ACOMMUNE1(2) 
  21 6TQB          SMO      5                   [SHIFT REST OF RCD.1 CHAR.POS.ONWARDS
   7 6W=2          MVCH  0  0
   9 6WPL    SHINS LDX   0  AWORK2(2)
  17 6X9=          DCH   0  0(3)                [INSERT SHIFT CHAR   
  13 6XNW          LDX   3  4                   [N/CH
  15 6Y8G          ADN   3  1                   [UPDATE N/CH 
   8 6YN6          BRN      SETRP
   4 6_7Q    #   
  14 6_MB    #              ALLCHAR,ALPHA-NORMAL,PREVIOUS
   4 7272    #   
   5 72LL    XAANP   
  16 736=          CALL  4  SRACNM              [ALLCHAR-NORMAL  
  16 73KW          LDX   3  2                   [PTR TO RED TAPE 
   8 745G          LDX   2  FX2  
   8 74K6          LDN   0  #75  
  15 754Q          STO   0  AWORK1(2)           [STORE BETA  
   8 75JB          LDN   0  #74  
   8 7642          BRN      STAW 
   4 76HL    #   
  14 773=    #              ALLCHAR,PREVIOUS-NORMAL,ALPHA
   4 77GW    #   
   5 782G    XAPNA   
   9 78G6          CALL  4  SRACNM   
  16 78_Q          LDX   3  2                   [PTR TO RED TAPE 
   8 79FB          LDX   2  FX2  
   8 79_2          LDN   0  #74  
  15 7=DL          STO   0  AWORK1(2)           [STORE ALPHA 
   8 7=Y=          LDN   0  #75  
   9 7?CW    STAW  STO   0  AWORK2(2)
  15 7?XG          CALL  6  RECLEN              [RCD LENGTH  
  16 7#C6          BRN      XCONV               [PREVIOUS-ALPHA  
   4 7#WQ    #   
  13 7*BB    #              GRAPHIC-SHIFT,PREVIOUS   
   4 7*W2    #   
   5 7B*L    XGRPR   
   7 7BT=          SBN   3  1
   8 7C#W          LDCT  2  #400 
   7 7CSG          ORX   2  3
  16 7D#6          LDCH  0  0(2)                [TERMINAL SHIFT  
   8 7DRQ          SBN   0  #75  
  15 7F?B          BZE   0  XB                  [J IF BETA   
  15 7FR2          BRN      XAL                 [J IF ALPHA  
   4 7G=L    #   
  12 7GQ=    #              GRAPHIC-SHIFT,ALPHA  
   4 7H9W    #   
   5 7HPG    XGRAL   
   7 7J96          SBN   3  1
   8 7JNQ          LDCT  2  #400 
   7 7K8B          ORX   2  3
   4 7KN2    #   
  14 7L7L    XAL   LDN   0  #74                 [ALPHA   
  16 7LM=          DCH   0  0(2)                [LAST SHIFT=ALPHA
  15 7M6W    XB    BZE   5  UP                  [EMPTY RECORD
   8 7MLG          SBN   3  A1+1 
  15 7N66          LDX   0  ALOGLEN(3)          [BLOCK LENGTH
  16 7NKQ          LDX   4  A1(3)               [N/W IN RECORD   
  16 7P5B          SLL   4  1                   [MULTIPLY BY 2   
  19 7PK2          BXGE  0  4,OK                [J IF BLOCK IS LONG ENOUGH   
  18 7Q4L          ALTLEN   3,4                 [ALTER LENGTH OF BLOCK   
  10 7QJ=          HUNT     3,ADATA,ACONV
  16 7R3W    OK    LDX   4  A1(3)               [N/W IN RECORD   
  17 7RHG          SBN   4  2                   [SUBTRACT RED TAPE   
   8 7S36          ADN   3  A1+2 
  15 7SGQ          LDX   6  3                   [PTR TO RCD  
  17 7T2B          LDX   2  3                   [OLD PTR TO RECORD   
  16 7TG2          ADX   3  4                   [NEW PTR TO RCD  
   7 7T_L          SMO      4
  17 7WF=          MOVE  2  0                   [MOVE WHOLE RECORD   
  15 7WYW    NEXT3 LDCH  0  0(3)                [PICK UP CHAR
   7 7XDG          BCHX  3  /
   8 7XY6          SBN   0  #74  
  15 7YCQ          BPZ   0  SHEXP               [J IF A SHIFT
  15 7YXB          ADN   0  #74                 [RESTORE CHAR
   8 7_C2          DCH   0  0(2) 
   7 7_WL          BCHX  2  /
  17 82B=          BCT   5  NEXT3               [J IF MORE TO TEST   
   8 82TW          BRN      URT  
   8 83*G    SHEXP LDN   1  #76  
  16 83T6          DCH   1  0(2)                [TRANSFER DELTA  
   7 84#Q          BCHX  2  /
   8 84SB          ADN   0  #64  
  17 85#2          DCH   0  0(2)                [DEPOSIT SUCCESSOR   
   7 85RL          BCHX  2  /
  17 86?=          BCT   5  NEXT3               [J IF MORE TO TEST   
  19 86QW    URT   LDX   3  2                   [PTR TO THE END OF THE RECORD
  16 87=G          CALL  5  NEWRT               [UPDATE RED TAPE 
  17 87Q6          SBN   6  A1+2                [PTR TO ADATA/ALINE  
   7 889Q          ADN   3  1
  17 88PB          ALTLEN   6,3                 [SHORTEN ADATA BLOCK 
  10 8992          HUNT     3,ADATA,ACONV
   8 89NL          ADN   3  A1   
  15 8=8=          LDX   0  1(3)                [2ND WORD RT 
   7 8=MW          SRL   0  6
   8 8?7G          ANDN  0  #77  
   8 8?M6          SBN   0  #76  
  19 8#6Q          BNZ   0  UP                  [J IF TERMINAL SHIFT IS ALPHA
  16 8#LB          CALL  6  RECLEN              [RECORD LENGTH   
  17 8*62          BRN      XALPR               [CONVERT TO PREVIOUS 
   4 8*KL    #   
  15 8B5=    #                      S U B R O U T I N E S :  
   4 8BJW    #   
  18 8C4G    #     SUBROUTINE TO UPDATE RED TAPE AND SPACEFILL LAST WORD 
   4 8CJ6    #   
  16 8D3Q    #              ENTRY:X3=PTR TO THE END OF THE RECORD
  18 8DHB    #                    X6=PTR TO THE FIRST CHAR OF THE RECORD 
  16 8F32    #                    X7=N/CH BEFORE THE CONVERSION  
  10 8FGL    #                    X5=LINK
  13 8G2=    #              EXIT :X2=PTR TO RED TAPE 
  15 8GFW    NEWRT SBX   3  6                   [REC LENGTH  
  16 8G_G          SLC   3  2                   [N/CH IN NEW RCD 
   9 8HF6          BXU   3  7,NEWR1  
   7 8HYQ          LDX   2  6
  16 8JDB          SBN   2  2                   [PTR TO RED TAPE 
  13 8JY2          BRN      SREX                [EXIT
  13 8KCL    #              ENTRY:X3=N/CH IN NEW RCD 
   7 8KX=    NEWR1 LDX   2  6
  18 8LBW          SBN   2  2                   [PTR TO RECORD RED TAPE  
   7 8LWG          NGX   0  3
  17 8MB6          BNG   3  NEGNCH              [J IF NEGATIVE N/CH  
   7 8MTQ          SRC   3  2
   8 8N*B          LDN   1  #7777
  15 8NT2          ANDS  1  1(2)                [CLEAR B0,B1 
   8 8P#L          LDCT  1  #600 
   7 8PS=          ANDX  1  3
  18 8Q?W          ORS   1  1(2)                [UPDATE N/CH IN LAST WORD
   8 8QRG          ANDN  0  #3   
  18 8R?6          BZE   0  X2                  [J IF LAST WORD IS FULL  
   8 8RQQ          LDN   1  #20  
   7 8S=B    STOR  SMO      2
  15 8SQ2          DCH   1  2(3)                [SPACEFILL   
   7 8T9L          BCHX  3  /
   8 8TP=          BCT   0  STOR 
   7 8W8W    X2    ADN   3  2
   8 8WNG          STO   3  0(2) 
   8 8X86          LDX   1  FX1  
   4 8XMQ    SREX
   8 8Y7B          SMO      FX2  
  21 8YM2          LDX   0  AWORK4              [RESTORE STATE OF B7 INTO RED TAPE OF
  21 8_6L          ORS   0  1(2)                [FOR TPOBJ AS TRANSFOR MAY NOT HAVE  
  16 8_L=          EXIT  5  0                   [DEALT WITH IT.  
   4 925W    #   
  13 92KG    #     SUBROUTINE TO GET LENGTH OF RECORD
  13 9356    #              ENTRY:X3=PTR TO RED TAPE 
  10 93JQ    #                    X6=LINK
  13 944B    #              EXIT: X5,7=N/CH IN RECORD
  12 94J2    #                    X0=OVERWRITTEN 
  13 953L    #                    X3=PTR TO RECORD   
   4 95H=    #   
   5 962W    RECLEN  
  16 96GG          LDX   5  0(3)                [N/W IN RECORD   
   7 9726          SBN   5  2
   8 97FQ          BZE   5  NUCH 
   7 97_B          SLL   5  2
   8 98F2          LDCH  0  1(3) 
  16 98YL          SRL   0  4                   [NO OF USED CHARS
   8 99D=          BZE   0  NUCH 
   7 99XW          SBN   5  4
  18 9=CG          ADX   5  0                   [ADD N/CH IN LAST WORD   
   7 9=X6    NUCH  LDX   7  5
  15 9?BQ          ADN   3  2                   [ADD RED TAPE
   7 9?WB          EXIT  6  0
   4 9#B2    #   
  11 9#TL    #     SUBROUTINE TO MOVE RECORD 
  10 9**=    #              ENTRY:X0=LINK
  14 9*SW    #                    X1=START CHAR ADDRESS  
  13 9B#G    #                    X2=END CHAR ADDRESS
  12 9BS6    #                    X5=N/CH TO MOVE
   4 9C?Q    #   
   5 9CRB    SRMOVE  
   8 9D?2          SMO      FX2  
  14 9DQL          STO   5  ACOMMUNE1           [STORE X5
   8 9F==    TEST5 SBN   5  512  
  17 9FPW          BNG   5  SHALL               [J TO MOVE THE REST  
  16 9G9G          MVCH  1  0                   [MOVE 512 CHARS  
   8 9GP6          BNZ   5  TEST5
   8 9H8Q          BRN      RESX5
   8 9HNB    SHALL ADN   5  512  
   7 9J82          SMO      5
  16 9JML          MVCH  1  0                   [MOVE THE REST   
   8 9K7=    RESX5 SMO      FX2  
  15 9KLW          LDX   5  ACOMMUNE1           [RESTORE X5  
   7 9L6G          EXIT  0  0
   4 9LL6    #   
   5 9M5Q    NEGNCH  
  10 9MKB          GEOERR   1,NEG N/CH   
   4 9N52    #   
  11 9NJL          MENDAREA 100,K99TRANSFOR  
   4 9P4=    #END
   8 ____ ...65662612000800000000
