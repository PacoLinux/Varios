  14 22FL ...      SEG      MOPSTART,,G. CONSTANTINIDES. 
  15 22_=    #LIS  K0MOPSTART>K0MULTIPLX>K0ALLGEO>K0GREATGEO 
   7 23YG    # ENTRY POINTS  
  12 24D6          SEGENTRY K2MOPSTART,Z2MOPSTART
  12 24XQ          SEGENTRY K3MOPSTART,Z3MOPSTART
  11 25X2          MENDAREA 30,K99MOPSTART   
   4 26BL    [   
  17 26W=    [K2MOPSTART IS ENTERED FROM SETIDF. IT CREATES AN AMSCAN
  17 27*W    [ACTIVITY FOR EVERY AMOP WHOSE DEVICE IS NOT WRONGED.   
   4 27TG    [   
  19 28*6    [K3MOPSTART IS OBEYED FROM THE AMOP/IMOPQ ACTIVITY. IT SCANS ALL
  19 28SQ    [AMXOR/ALINES BLOCKS TO SEE WHETHER ANY LINES NEED CPATS TO BE  
  20 29#B    [CREATED TO RUN MOP, IF SO IT SETS THEM UP, THEN GOES BACK TO SLEEP 
   4 29S2    [   
   9 2=?L    PAIR1 PAIR     PROFLAG,1
   9 2=R=    PAIR2 PAIR     SCAN,1   
   9 2?=W    PAIR3 PAIR     SCAN,3   
   4 2?QG    [   
   5 2#=6    SAOUT   
   8 2#PQ    #HAL   AMXOR+AOUT,0 
   6 2*9B    SBI      +8 
   7 2*P2          8HBREAK IN
   8 2B8L    SECBIT     #10000000
   5 2BN=    XLINE   
  11 2C7W    #HAL           AMXOR+ALINES,0   
   5 2C7Y ...REFIND  
   7 2C82 ...      LDX   2  3
  13 2C84 ...      HUNT2J   2,AMXOR,ALINES,,REFIND   
   7 2C86 ...      ADX   2  4
  10 2C88 ...      LDX   2  A1+BPTR(2)   
   8 2C8= ...      SBN   2  A1   
   7 2C8# ...      EXIT  1  0
   6 2C8G ...#SKI JSKI14 
   4 2C96 ...(   
  17 2C9Q ...RSTPT ADN   2  A1                  [RESET KTAB ENTRIES  
  18 2C=B ...                                   [AND LEAVE CHAP. UNRINGED
   8 2C?2 ...      ORX   2  GSIGN
   9 2C?L ...      SMO      BACK1(2) 
   8 2C#= ...      STO   2  KTAB 
   8 2C#W ...      ERX   2  GSIGN
   9 2C*G ...      STO   2  FPTR(2)  
   7 2CB6 ...      EXIT  0  0
  11 2CBQ ...SEEK  MHUNTW   2,AMXOR,CHAPCOPY 
   7 2CCB ...      EXIT  1  0
   7 2CD2 ...RMV   LDEX  3  4
   8 2CDL ...      BZE   3  ZEMV 
   7 2CF= ...      SMO      3
   7 2CFW ...NMV   MOVE  1  0
   7 2CGG ...      ADX   1  3
   7 2CH6 ...      ADX   2  3
   7 2CHQ ...      SBX   4  3
   8 2CJB ...ZEMV  LDN   3  512  
   8 2CK2 ...      BNZ   4  NMV  
   7 2CKL ...      EXIT  0  0
   4 2CL= ...)   
   4 2CMG    [   
   6 2D76    Z2MOPSTART  
  17 2DLQ          LDX   3  ADMULTACT           [BASE OF MOP LIST.   
  18 2F6B          BZE   3  T7                  [J. IF NO MOP ACTIVITIES.
  10 2FL2    T3    JWRONG   CPPTR(3),T6  
  17 2G5L          LINKSET  3,PAIR1(1)          [LINK TO K1PROFLAG   
  18 2GK=          GETACT   AMSCAN              [SET UP SCANNING ACTIVITY
   9 2H4W          LDX   2  BPTR(2)  
  16 2HJG          LINKSET  2,PAIR2(1)          [LINK TO K1SCAN  
  10 2J46          LNLI     CPPTR(3),6   
   7 2JHQ          ADN   6  1
   7 2K3B          LDX   5  6
   7 2KH2          SLL   5  2
  10 2L2L          STO   3  AMOPADDR(2)  
  21 2LG=          FWAIT    #102                [SET AMSCAN WAITING FOR END OF E.M.S.
  12 2L_W          SETXCORE 5,1,AMXOR,ALINES,,6,4
  18 2MFG          LDX   2  1                   [INITIALISE THE BLOCK.   
   8 2M_6          NGN   4  A1   
   8 2NDQ          LDCT  0  #40  
  18 2NYB    T4    STOZ     A1+BPTR(2)          [NULLIFY RING ELEMENT.   
  21 2PD2          STO   4  A1+BSTART(2)        [DISTANCE OF ELT. FROM START OF BLOCK
  19 2PXL          STO   0  A1+FOURTHWD(2)      [SET ^WAITING FOR SCAN^ BIT. 
  16 2QC=          SBN   4  4                   [STEP POINTERS.  
   7 2QWW          ADN   2  4
  17 2RBG          BCT   6  T4                  [J. IF MORE LINES.   
   7 2RW6          SBN   5  4
  15 2S*Q          BZE   5  T5                  [J. IF UPX.  
  19 2STB          STOZ     A1+FOURTHWD(1)      [DUMMY ENTRY FOR LINE ZERO.  
  20 2T*2    T5    CHAIN    1,3                 [CHAIN ALINES BLOCK AFTER AMOP.  
   6 2TG8 ...#SKI  ICT   
  21 2TMB ...      FON      ADEVLIN                 [START ACTIVITIES WAITING FOR ADE
  18 2TSL    T6    LDX   3  ADMP(3)             [POINTER TO NEXT AMOP.   
  18 2W#=          BNZ   3  T3                  [DROP THROUGH IF NONE.   
  14 2WRW    T7    UP                           [FINISH. 
   4 2X?G    [   
  10 2XR6 ...      SEGENTRY K50MOPSTART  
  10 2Y=Q    MOPCRUNCH      2*60000/K9   
  11 2YQB    ONEDAY         3600*24/K9*1000  
   6 2_=2    Z3MOPSTART  
  11 2_PL          LONGSET  #125,Z3MOPSTART  
  21 329=    T8    OUTPAUSE T8,TEVENT,,1,TCORE  [WAIT FOR 1 MINUTE OR CORE REQUESTS  
   6 329J ...#SKI JSKI14 
   4 329W ...(   
  11 32=8 ...      HUNTW    2,AMXOR,CHAPCOPY 
   8 32=G ...      BNG   2  NOBLK
   7 32=S ...      LDX   5  2
   8 32?6 ...      SMO      FX2  
   9 32?D ...      LDX   0  ICONCEP  
  18 32?Q ...      BNG   0  NFREE               [J IF BLOCK NOT IN USE   
   7 32#B ...      LDN   6  2
   9 32*2 ...      ANDX  6  AFLAG(2) 
   8 32*# ...      BZE   6  NOBLK
  10 32*L ...REQ   LDX   4  ALOGLEN(2)   
   7 32*Y ...      LDX   6  4
   8 32B= ...      GETCORE  4,0,1
  16 32BJ ...      BRN      SCORE               [J IF HAVE CORE  
  15 32BW ...      BRN      NOBLK               [J IF NO CORE
   8 32C8 ...NFREE ADN   2  A1   
  18 32CG ...      LDX   1  BACK2(2)            [GET DATA FOR KTAB ENTRY 
   9 32CS ...      SMO      BACK1(2) 
  17 32D6 ...      STO   1  KTAB                [STORE IN CH. TABLE  
  19 32DD ...      ADX   2  ALOGLEN(2)          [NOW REPEAT FOR NEXT CHAP.   
   8 32DQ ...      ADN   2  A1   
   9 32F4 ...      LDX   1  BACK2(2) 
   9 32FB ...      SMO      BACK1(2) 
   8 32FN ...      STO   1  KTAB 
   8 32G2 ...      SMO      FX2  
  18 32G# ...      STOZ     ICONCEP             [MARK ^NO CHAP. BLOCK^   
   7 32GL ...      FREECORE 5
   8 32GY ...      BRN      NOBLK
   5 32H= ...SCORE   
   7 32HJ ...      FINDCORE 2
  11 32HW ...      NAME     2,AMXOR,CHAPCOPY 
   8 32J8 ...      ADN   2  A1   
   7 32JG ...      LDX   1  5
   8 32JS ...      ADN   1  A1   
   8 32K6 ...      CALL  0  RMV  
   7 32KD ...      SBX   2  6
   8 32KK ...      SBN   2  A1   
   9 32KQ ...      LOCKC    2,SEEK   
   8 32L4 ...      CALL  1  SEEK 
   8 32LB ...      CALL  0  RSTPT
  10 32LN ...      ADX   2  ALOGLEN(2)   
   8 32M2 ...      CALL  0  RSTPT
   7 32M# ...      FREECORE 5
   8 32ML ...NOBLK LDX   1  FX1  
   8 32MY ...      LDX   2  FX2  
   4 32N= ...)   
   9 32NW          LDX   3  ADMULTACT
   7 338G    T9    LDX   2  3
   9 33N6    T10   LDX   2  FPTR(2)  
   9 347Q          LDX   0  ATYPE(2) 
  18 34MB          BXGE  0  CACT,T14            [J. IF NO ALINES BLOCK   
  10 3572          BXU   0  XLINE(1),T10 
  10 35LL          LNLI     CPPTR(3),4   
   7 366=          LDX   6  4
   7 36KW          SLL   6  2
   7 375G          ADX   2  6
   7 37K6          LDX   5  2
   7 384Q    T12   SMO      5
   9 38JB          LDX   2  A1+BPTR  
  15 3942          BZE   2  T13                 [J. IF NO LCB
   7 39HL          SMO      5
  10 3=3=          LDX   0  A1+FOURTHWD  
  16 3=GW          BNG   0  T13                 [J IF CONCEPTUAL 
   8 3?2G          SBN   2  A1   
   9 3?G6          LDX   0  GTIM(2)  
  20 3?_Q          BNG   0  T13                 [J. UNLESS UNTERMINATED TRANSFER 
   7 3#FB          NGX   0  0
   8 3#_2          ADX   0  JTIME
   8 3*DL          BPZ   0  T12A 
   9 3*Y=          ADX   0  ONEDAY(1)
  10 3BCW    T12A  SBX   0  MOPCRUNCH(1) 
  21 3BXG          BNG   0  T13                 [J. IF 1 MINUTE SINCE TRANSFER ISSUED
  10 3CC6          LDX   0  GUNITNO(2)   
   8 3CWQ          SLL   0  15   
   7 3DBB          ADN   0  4
  12 3DW2          DCA      CPPTR(3),STO,0,MODE  
   9 3F*L          GLPERI   CPPTR(3) 
   7 3FT=    T13   SBN   5  4
   8 3G#W          BZE   6  T14  
  19 3GSG          BCT   4  T12                 [J. IF MORE LINES TO LOOK AT 
   9 3H#6    T14   LDX   3  ADMP(3)  
   8 3HKG ...      BNZ   3  T9   
   6 3HNL ...#SKI JSKI14 
   4 3HRQ ...(   
   8 3HWW ...      SMO      FX2  
   8 3J8= ...      LDX   3  IWAKE
   4 3J#Y ...)   
   8 3JFL ...      BZE   3  T8   
   5 3JR2    TEVENT  
   9 3K=L          LDX   3  ADMULTACT
   7 3KQ=    T17   LDX   2  3
   9 3L9W    T18   LDX   2  FPTR(2)  
   9 3LPG          LDX   0  ATYPE(2) 
  18 3M96          BXGE  0  CACT,T20A           [J. IF NO ALINES BLOCK   
  10 3MNQ          BXU   0  XLINE(1),T18 
  10 3N8B          LNLI     CPPTR(3),4   
   7 3NN2          LDX   6  4
   8 3P7L    T19   SMO      FX2  
   8 3PM=          LDX   0  IWAKE
  20 3Q6W          BZE   0  T20B                [J. IF CORE REQUESTS SATISFIED   
   7 3QLG          LDCT  0  4
   7 3R66          SLL   4  2
   7 3RKQ          SMO      4
  11 3S5B          ANDX  0  A1+FOURTHWD(2)   
  20 3SK2          BZE   0  T20                 [J. IF NOTHI-G TO DO ON THIS LINE
   7 3T4L          SMO      4
  10 3TJ=          LDX   7  A1+BPTR(2)   
   8 3W3W          BNZ   7  XBRI 
   8 3WHG          LDCT  7  #100 
   7 3X36          SMO      4
  11 3XGQ          ANDX  7  A1+FOURTHWD(2)   
  17 3Y2B          BNZ   7  T21                 [J. IF MOPPING OFF   
   7 3YG2          SMO      4
  11 3Y_L          ERS   0  A1+FOURTHWD(2)   
   8 3_F=          GETACT   CPAT 
   9 3_YW          LDX   2  BPTR(2)  
  15 42DG          STO   3  AWORK4(2)           [AMOP ADDRESS
  16 42Y6          LINKSET  2,PAIR3(1)          [LINK TO K3SCAN  
   7 43CQ          LDX   7  4
   7 43XB          SRL   7  2
   9 44C2          STO   7  ACC7(2)  
   6 44WL          FPUT  
   7 45B=    TFNLN LDN   0  1
   9 45TW          SBS   0  IWAKE(2) 
  18 46*G          LDX   2  3                   [HUNT FOR AMXOR/ALINES   
   9 46T6    T19A  LDX   2  FPTR(2)  
   9 47#Q          LDX   0  ATYPE(2) 
  15 47SB          BXGE  0  CACT,T20A           [NOT FOUND   
  17 48#2          BXU   0  XLINE(1),T19A       [LOOK AT NEXT BLOCK  
   8 48RL    T20   BZE   6  T20A 
   7 49?=          SRL   4  2
  19 49QW          BCT   4  T19                 [J. IF MORE LI-ES TO LOOK AT 
   9 4==G    T20A  LDX   3  ADMP(3)  
  19 4=Q6          BNZ   3  T17                 [J. UNLESS NO MORE ACTIVITIES
   8 4?9Q          SMO      FX2  
   8 4?PB          LDX   0  IWAKE
  21 4#92          BNZ   0  TEVENT              [J. UNLESS CORE REQUESTS SATISFIED   
   9 4#NL ...T20B  LONGSET  #125,T8  
   8 4*8=          BRN      TCORE
   8 4*MW    T21   LDCT  0  #24  
   7 4B7G          SMO      4
  18 4BM6          ERS   0  A1+FOURTHWD(2)      [UNSET ^SETTING UP^ BITS 
   7 4C6Q          LDN   0  1
   8 4CLB          SMO      FX2  
  18 4D62          SBS   0  IWAKE               [DECREASE REQUESTS BY 1  
   8 4DKL          BRN      T20  
   8 4F5=    XBRI  LDX   2  FX2  
   9 4FJW          STO   4  AWORK3(2)
   9 4G4G          STO   6  AWORK2(2)
   9 4GJ6          STO   3  AWORK4(2)
   8 4H3Q          SBN   7  A1   
   7 4H5N ...      SMO      7
   9 4H7L ...      LDX   0  GLASTM   
   8 4H9J ...      BPZ   0  TRBI 
   9 4H?G ...      LOCKC    7,REFIND 
   9 4H*D ...      CALL  1  REFIND   
   7 4HCB ...      LDX   7  2
   4 4HM8 ...TRBI
   9 4HX2 ...      HUNTACT  CPAT,2,7 
   7 4HY? ...#UNSET ENBREAKIN
  21 4H_J ...      NBREAKIN 2,3,(GEOERR)        [J GEOERR REJECTED - CONTINUE ACCEPT 
   7 4J2T ...#UNSET ENBREAKIN
   4 4J46 ...#SKI
   4 4J5C ...(   
   8 4J6S ...      NBREAKIN 2,3  
   9 4JBL ...      BRN      (GEOERR) 
   4 4JW= ...)   
   9 4MDB          LDX   7  AWORK3(2)
   9 4MY2          LDX   2  AWORK4(2)
   7 4NCL          LDX   3  2
  12 4NX=          HUNT2J   3,AMXOR,ALINES,,ZR   
   7 4PBW          LDCT  0  4
   7 4PWG          ADX   3  7
  11 4QB6          ERS   0  A1+FOURTHWD(3)   
   8 4QTQ          LDN   0  #1000
  11 4R*B          ANDX  0  A1+FOURTHWD(3)   
  11 4RT2          ERS   0  A1+FOURTHWD(3)   
  10 4S#L          LDX   3  A1+FPTR(3)   
   8 4SS=          SBN   3  A1   
   7 4T?W          SLL   0  2
   9 4TRG ...      ORX   0  SECBIT(1)
   9 4W?6          ORS   0  GWAIT(3) 
   9 4WQQ          LDX   0  GLASTM(3)
   8 4X=B          SBN   0  #13  
  16 4XQ2          BNZ   0  N3                  [J IF NOT MODE 3 
  18 4Y9L          LDN   0  1                   [SET B.I. BIT FORMOPOUT  
   9 4YP=          ORS   0  GWAIT(3) 
   8 4_8W          BRN      XOUT 
  18 4_NG    N3    LDX   4  SAOUT(1)            [AMXOR/AOUT BLOCK TYPE.  
  21 5286          LDX   1  3                   [HUNT BACK FROM THE LCB TO THE CPAT  
  21 52MQ          LDN   5  0                   [TO DISCOVER THE LAST BLOCK IN THE   
  16 537B    NACT  LDX   1  BPTR(1)             [OUTPUT QUEUE.   
   9 53M2          LDX   0  ATYPE(1) 
   9 546L          BXU   4  0,NOUT   
   7 54L=          LDX   5  1
   9 555W    NOUT  BXL   0  CACT,NACT
  17 55KG          BZE   5  XOUT                [J. IF NONE FOUND.   
   7 5656          LDX   1  5
   9 56JQ          LDXC  0  AMOPTY(1)
  20 574B          ADXC  0  0                   [J. UNLESS NO NL TERMINATOR ON   
  20 57J2          BCC      XOUT                [THE END OF THIS MESSAGE BLOCK.  
  20 583L          LDX   5  AMOPCHS(1)          [INSERT NL (SO THAT ^BROKEN IN^  
  20 58H=          LDCH  0  CHARS(3)            [MESSAGE COMES OUT ON NEW LINE). 
   9 592W          SBX   5  GNOCHS(3)
   9 59GG          LDX   4  BITS22LS 
   7 5=26          ADX   5  0
  21 5=FQ          ANDS  4  AMOPTY(1)           [UNSET THE ^NL SUPPRESSED^ MARKER.   
   7 5=_B          SRC   5  2
  10 5?F2          LDN   4  GNLBUFF(3)   
  10 5?YL          ADN   5  AMOPOUT(1)   
  19 5#D=          ADS   0  AMOPCHS(1)          [UPDATE THE CHARACTER COUNT. 
   9 5#XW          SMO      GNOCHS(3)
   7 5*CG          MVCH  4  0
   8 5*X6    XOUT  SMO      FX1  
  20 5BBQ          LDN   1  SBI                 [SET UP THE ^BREAK IN^ MESSAGE.  
  17 5BWB    XOP   LDN   4  GNLBUFF(3)          [SET UP NL MESSAGE NL
   9 5CB2          LDN   5  BUFFER(3)
   9 5CTL          LDCH  0  CHARS(3) 
   7 5D*=          SMO      0
   7 5DSW          MVCH  4  0
   8 5F#G          LDN   4  1(1) 
   8 5FS6          SMO      0(1) 
   7 5G?Q          MVCH  4  0
  10 5GRB          LDN   4  GNLBUFF(3)   
   9 5H?2          SMO      GNOCHS(3)
   7 5HQL          MVCH  4  0
   8 5J==          ADX   0  0(1) 
   9 5JPW          ADX   0  GNOCHS(3)
   7 5K9G          LDX   4  0
   7 5KP6          SLC   5  2
   7 5L8Q          SBN   5  2
  19 5LNB          SRC   5  2                   [POINT TO LAST BUT ONE CHAR  
   7 5M82          LDX   1  5
  18 5MML          LDN   0  #10                 [REPLACE EOM WITH CANCEL 
   8 5N7=          DCH   0  0(1) 
   9 5NLW          LDN   5  BUFFER(3)
   8 5P6G          LDX   1  FX1  
  10 5PL6          LDX   6  GUNITNO(3)   
   8 5Q5Q          FINDLCA  ,6   
   8 5QKB          JLBUSY   1,REJ
   8 5R52          SLL   6  15   
  10 5RJL          DLCA     1,DLA,4,COUNT
  10 5S4=          DLCA     1,STO,5,CTSA 
   8 5SHW          LDN   5  #11  
   9 5T3G          STO   5  GLASTM(3)
  10 5TH6          DLCA     1,DLA,5,MODE 
   9 5W2Q          LDN   5  GREP1(3) 
  10 5WGB          DLCA     1,STO,5,CREP 
  10 5X22          DCA      ,STO,6,MODE  
  18 5XFL          DLCA     1,LDX,4,FLAG        [NEXT PERI MIGHT FINISH  
  18 5X_=          ANDN  4  #77                 [BEFORE EXEC RETURNS TO G
   6 5YDW          GLPERI
  20 5YYG          LDX   0  JTIME               [REMEMBER WHEN TRANSFER STARTED  
  21 5_D6          STO   0  GTIM(3)             [IN CASE IT NEEDS TO BE TERMINATED   
   4 5_XQ    REJ 
   8 62CB    PERY3 LDX   2  FX2  
   8 62X2          LDX   1  FX1  
   9 63BL          LDX   3  AWORK4(2)
   9 63W=          LDX   6  AWORK2(2)
   9 64*W          LDX   4  AWORK3(2)
   8 64TG          BRN      TFNLN
   9 65*6    ZR    GEOERR   1,NOAL   
   4 65SQ    #END
   8 ____ ...47071657000100000000
