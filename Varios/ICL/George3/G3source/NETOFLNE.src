  14 22FL          SEG    NETOFLNE,867,SECT.FILE,,G550   
   7 22_=    #LIS K0NETOFLNE 
   7 23DW    #OPT G550 = 0   
   6 23YG    #SKI G550&1 
  20 24D6    #  FOR G3UG MEND M550 (LINE PRINTER OUTPUT DIRECT TO MAGNETIC TAPE) 
  16 24XQ          SEGENTRY K1NETOFLNE,Z1NETOFLNE[ZOFFLINE(ZF)   
  16 25CB          SEGENTRY K2NETOFLNE,Z2NETOFLNE[PERI HANDLING  
  16 25X2          SEGENTRY K3NETOFLNE,Z3NETOFLNE[RELEASE ENTRY  
  16 26BL          SEGENTRY K4NETOFLNE,Z4NETOFLNE['ILLEGAL' ENTRY
   4 26W=    [   
   4 27*W    [   
  15 27TG    [         THIS SEGMENT WRITTEN BY N.E.T.R.H.A.  
   4 28*6    [   
  17 28SQ    [         IT IMPLEMENTS THE ZOFFLINE COMMAND TO SPOOL   
  17 29#B    [         PRINT DIRECTLY TO TAPE, AND THE OTHER LINKED  
   8 29S2    [         FUNCTIONS.
   4 2=?L    [   
  18 2=R=    [         NOTE: IT WILL ONLY IMPLEMENT 2/#0 OR 2/#4 PERIS   
  18 2?=W    [               THE 2/#2 AUTOWRITE WILL BE FLAGED AS ILLEGAL
   4 2?QG    [   
   4 2#=6    [   
   8 2#PQ    XFIVE          +5   
   8 2*9B    XLP            +2   
   9 2*P2    XMASK          #77777771
   9 2B8L    XMASK1         #60000000
   9 2BN=    XMASK2         #60077777
   9 2C7W    XMASK3         #17777777
   9 2CMG    XMASK4         #04000000
   9 2D76    XMASK5         #02000000
   8 2DLQ    XHASH11        #11  
   8 2F6B    XHASH51        #51  
   8 2FL2    XWRITE         #40  
  12 2G5L    XTRA           2,4096,2,4096,2,4096 
   8 2GK=    MAX1           +162 
   8 2H4W    MAX2           +513 
   9 2HJG    ZJYMODE        +JYMODE  
   9 2J46    ZJYCHAR        +JYCHAR  
   9 2JHQ    ZJYCOUNT       +JYCOUNT 
  10 2K3B    ZJYRESVIOL     +JYRESVIOL   
  10 2KH2    ZJYOUTFULL     +JYOUTFULL   
  10 2L2L    ZJYPERFAIL     +JYPERFAIL   
  10 2LG=    PAIR2 PAIR     NETOFLNE,2   
  10 2L_W    PAIR4 PAIR     NETOFLNE,4   
   4 2MFG    #   
   4 2M_6    #   
  10 2NDQ    #         ZOFFLINE COMMAND  
   4 2NYB    #   
   4 2PD2    #   
   6 2PXL    Z1NETOFLNE  
  11 2QC=    #     DECODE COMMAND PARAMETERS 
  18 2QWW          PERCON   1                   [DECODE PERIPHERAL NAME  
  15 2RBG          TESTREPNOT OK,TERR           [J IF ERROR  
  17 2RW6          MHUNT    3,CPAR,GPERCON      [LOOK FOR REPLY BLOCK
   8 2S*Q          LDX   0  A1(3)
   8 2STB          SRL   0  15   
   9 2T*2          TXU   0  XLP(1)   
  14 2TSL          BCC      P1                  [J IF LP 
  17 2W#=          SPARANOT 1                   [PASS FIRST PARAMETER
  18 2WRW          ERROR    JPNAL,JNOTLP        [OTHERWISE REPORT ERROR  
  17 2X?G    TERR  ENDCOM                       [TERMINATE IF ERROR  
  12 2XR6    #     RELEASE ANY PREVIOUS CHANNEL  
  16 2Y=Q    P1    LDX   5  A1(3)               [TYPE/UNIT NO.   
   9 2YQB          STO   5  AWORK4(2)
   7 2_=2    #UNS  JWPHASE4  
   4 2_PL    (   
   8 329=          LDCT  6  #3   
   7 32NW          ADX   6  5
  11 338G          HLSXONCH XBRKIN,6,PHLSER  
   4 33N6    )   
  20 347Q          RELPERENT 5                  [RELEASE ANY PREVIOUS PERIPHERAL 
  12 34MB    #     ENTRANT DESCRIPTION PARAMETER 
  19 3572    P4    SPARANOT 2                   [UNPACK ENTRANT DESCRIPTION  
  10 35LL          MHUNT    3,CPB,CUNI   
   9 366=          LDX   0  ANUM(3)  
  19 36KW          BPZ   0  P2                  [J UNLESS PARAMETER MISSING  
  11 375G          COMERR   JPARMIS,JFENTRANT
   7 37K6    #UNS  JWPHASE4  
   4 384Q    (   
   5 38JB    PHLSER  
   9 3942          COMERR   ERHLSONL 
   4 39HL    )   
   5 3=3=    PNOTEMP 
   9 3=GW          COMERR   ERTEMPDIR
  10 3?2G    P2    NAMETOP  3,FILE,FNAME 
   6 3?G6          FNORM 
   7 3?_Q          PICKREP2 7
  10 3#FB          MHUNT    3,FILE,FNAME 
  10 3#_2          NAMETOP  3,CPB,CUNI   
   7 3*DL          PUTREP2  7
   7 3*Y=          NGN   7  1
  10 3BCW          TESTREP2 NAMEFORM,TERR
   9 3BXG          TESTRPN2 OK,TERR  
   9 3CC6    P5    MFREE    CPB,CUNI 
  10 3CWQ          MHUNT    3,FILE,FABSNB
   9 3DBB          LDX   0  ATYPE(3) 
   8 3DW2          ANDN  0  #200 
   9 3F*L          BNZ   0  PNOTEMP  
   9 3FT=          LDX   0  HDREC(3) 
   8 3G#W          SBN   0  10   
   8 3GSG          BNG   0  PE1  
   7 3H#6          ADN   0  4
  18 3HRQ          STO   0  HDREC(3)            [INSERT REDUCED LENGTH   
   8 3J?B          LDX   0  ACES 
   9 3JR2          SMO      HDREC(3) 
   9 3K=L          STO   0  A1+5(3)  
   8 3KQ=          NGN   0  #101 
  17 3L9W          ANDS  0  ATYPE(3)            [REMOVE LANG CODE BIT
  10 3LPG          FSHCODE  B,XFSHBOPEN  
   4 3M96    ([  
  16 3MNQ          OPEN     PG4,GENERAL,QUERY   [OPEN DIRECTORY  
   6 3N8B    XFSHBOPEN   
   4 3NN2    )[  
  10 3P7L          FSHSKIP  B,XFSHNBOPEN 
   4 3PM=    ([  
  11 3Q6W          OPEN     PG4,READ,QUERY   
   6 3QLG    XFSHNBOPEN  
   4 3R66    )[  
   8 3RKQ          TESTREP2 OK,P6
   9 3S5B          TESTERR  NAME,TERR
  10 3SK2          TESTREP2 NOFILE,PE4   
  10 3T4L          TESTREP2 VRYWRONG,PE4 
  10 3TJ=          TESTREP2 NOUSER,PE4   
   9 3W3W          GEOERR   1,REPLY  
   4 3WHG    P6  
  19 3X36          MHUNT    2,FILE,FABSNB       [THE FABSNB NEEDS TO BE SET  
  20 3XGQ          LDN   0  6                   [TO CONTAIN :USER.LOCALNAME  ONLY
  19 3Y2B          ADS   0  A1(2)               [THIS IS FOR LATER USE WHEN  
  16 3YG2          LDX   0  A1(2)               [UPDATING BUDGETS
   8 3Y_L          SBN   0  10   
   8 3_F=          BZE   0  P02  
   7 3_YW          LDX   3  2
   7 42DG          TOPFCB   1
   8 42Y6          ADN   1  FME1 
   8 43CQ          ADN   2  A1+1 
   7 43XB          MOVE  1  3
   8 44C2          SMO      A1(3)
   9 44WL          LDN   1  A1-6(3)  
   9 45B=          LDN   2  A1+4(3)  
  10 45TW          LDX   5  ALOGLEN(3)   
   8 46*G          SBX   5  A1(3)
   7 46T6          ADN   5  6
   7 47#Q          SMO      5
  19 47SB          MOVE  1  0                   [MOVE LOCAL NAME UP FABSNB   
   7 48#2          ADN   5  4
   8 48RL          LDN   1  10   
   8 49?=          STO   1  A1(3)
  11 49QW          ALTLEN   3,5,FILE,FABSNB  
   4 4==G    P02 
   6 4=Q6    #UNS  FTS1  
   4 4?9Q    (   
  19 4?PB          TOPFCB   3                   [X3 POINTS TO DIRS FILE-FCB  
  21 4#92          JFREZFCB 3,PERR2             [TEST FOR FROZEN DIR & FREEZE PRIVILE
   4 4#NL    )   
  19 4*8=          OWNERCHECK                   [CHECK OUT OWNERSHIP OF TAPE 
   7 4*MW          PICKREP2 3
  16 4B7G          CLOSETOP                     [CLOSE DIRECTORY 
   7 4BM6          PUTREP2  3
   6 4C6Q    #UNS  FTS1  
   4 4CLB    (   
  16 4D62          TESTREP2 OK,P7               [OWNERSHIP OK?   
   7 4DKL          REPALLER  
   8 4F5=          BRN      TERR 
   4 4FJW    )   
  10 4G4G    P7    MHUNT    3,FILE,FABSNB
   6 4GJ6    #UNS  FTS1  
  10 4H3Q          VFREE    FILE,ADJUNCTS
   7 4HHB          LDN   0  1
  20 4J32          ORS   0  ATYPE(3)            [NON-FILESTORE ENTRANT REQUIRED  
  10 4JGL          STOZ     ACOMMUNE1(2) 
   9 4K2=          TRACE    2,GETONZF
  18 4KFW          DOWN     GET,3               [GO DOWN TO GET A TAPE   
   9 4K_G          BRN      XBRKIN   
  19 4LF6          TESTREPNOT OK,PE             [J IF FAILED TO GET A TAPE   
   9 4LYQ          OUTPAR   TIMENOW  
   8 4MDB          LGEOG    ,3   
  10 4MY2    P8    OUTPACK  3,1,GEOPER,P8
   9 4NCL    P9    LDX   5  AWORK4(2)
  11 4NX=          OUTPACK  5,1,PERINAME,P9  
   9 4PBW          DFLOAD   ,TSN,3   
  10 4PWG    P10   OUTPACK  3,1,TSN,P10  
  10 4QB6    P11   MHUNT    3,FILE,FABSNB
   9 4QTQ          ADX   3  HDREC(3) 
  12 4R*B          OUTPACK  A1-6(3),3,LOCNAME,P11
  10 4RT2    P12   MHUNT    3,FILE,FABSNB
   9 4S#L          ADX   3  HDREC(3) 
  12 4SS=          OUTPACK  A1-2(3),1,GENNUM,P12 
  10 4T?W    P13   MHUNT    3,FILE,FABSNB
   9 4TRG          ADX   3  HDREC(3) 
  12 4W?6          OUTPACK  A1-3(3),1,REELNUM,P13
   8 4WQQ          OUTPAR   SKIP 
   8 4X=B          MONOUT   JPER 
  10 4XQ2          MHUNT    3,FILE,FABSNB
   7 4Y9L          FREECORE 3
  11 4YP=          LDN   3  AOLPTLEN+AZFLEN  
  15 4_8W          LDCT  5  AOLPT/8             [AOLPT TYPE  
   8 4_NG          GETACTX  5,3,1
   9 5286          LDX   3  BPTR(2)  
   9 52MQ          LDX   0  JOBNO(2) 
   9 537B          STO   0  JOBNO(3) 
   8 53M2          LDCT  0  #400 
  10 546L          STO   0  FRECHADD(3)  
  17 54L=          ORS   0  GIVE(3)             [SET BIT FOR ADLOT   
   9 555W          LDX   0  AWORK4(2)
   9 55KG          STO   0  FTYPNO(3)
   9 5656          STOZ     AWORK1(3)
   9 56JQ          STOZ     AWORK2(3)
   9 574B          STOZ     AWORK3(3)
   9 57J2          STOZ     AWORK4(3)
   8 583L          LDX   1  FX1  
  10 58H=          LINKSET  3,PAIR2(1)   
  18 592W          CHANGEPER 3                  [TRANSFER TAPE TO AOLPT  
   9 59GG          LDX   5  AWORK4(2)
  17 5=26          PUTPEREN 5,GSIGN             [INSERT PERIPH. ENTRY
  18 5=FQ          ROLPA                        [CHAIN AOLPT IN PCA CHAIN
   6 5=_B          ENDCOM
   4 5?F2    #   
  15 5?YL    #         EXECPTION CONDITIONS FOR ZOFFLINE(ZF) 
   4 5#D=    #   
  10 5#XW    PE    TESTREP2 NOLOCN,PE1   
   9 5*CG          TESTREP2 TSN,PE2  
   9 5*X6          TESTREP2 NOTAV,PE3
  10 5BBQ          TESTRPN2 ODRAWN,PQ1   
  11 5BWB          ERROR    BUDGODR,BUDGSPA  
   8 5CB2          BRN      TERR 
  10 5CTL    PQ1   TESTRPN2 INCREASE,PQ2 
  11 5D*=          ERROR    BUDGINC,BUDGSPA  
   8 5DSW          BRN      TERR 
  10 5F#G    PQ2   TESTREP2 ALREADY,TERR 
   9 5FS6          TESTREP2 NAME,TERR
  10 5G?Q          TESTREP2 WRGEN,TERR   
  10 5GRB          TESTREP2 VRYWRONG,TERR
   9 5H?2          TESTREP2 TEMP,TERR
  10 5HQL          TESTREP2 NODECKS,PE5  
   9 5J==          GEOERR   1,GETREP 
  17 5JPW    PE1   COMERR   AMTDESC,JLOCN       [SERIAL NUMBER GIVEN 
  17 5K9G    PE2   COMERR   AMTDESC,JNTSN       [LOCAL NAME NOT GIVEN
  18 5KP6    PE3   COMERR   JNPT                [POOL TAPE NOT AVAILABLE 
   9 5L8Q    PE4   COMERR   ERNAME   
   9 5LNB    PE5   COMERR   ERNODECKS
  10 5M82    PERR  TESTRPN2 ILLCOMB,PERR1
   9 5MML          COMERR   ERILLCOMB
   9 5N7=    PERR1 COMERR   ERILLMODE
   6 5NLW    #UNS  FTS1  
   4 5P6G    (   
   7 5PL6    PERR2 CLOSETOP  
  21 5Q5Q          ERRTEST  3,TERR,FROZEN,NOFPRIV [ERROR MESS FOR FROZEN & NO-F-PR   
  17 5QKB          GEOERR   1,GLFRREP           [ELSE GEORGE ERROR   
   4 5R52    )   
   8 5RJL    #     BREAKIN EXITS 
   7 5S4=    XBRKIN COMBRKIN 
   8 5SHW    #     GEORGE ERRORS 
  11 5T3G    PG4   GEOERR   BRIEFPM,BREAKIN  
   4 5TH6    #   
   4 5W2Q    #   
  11 5WGB    #         PERI HANDLING ROUTINE 
   4 5X22    #   
   4 5XFL    #   
   6 5X_=    Z2NETOFLNE  
   9 5YDW          LDX   0  AWORK1(2)
  17 5YYG          BNG   0  Z2FAIL              [J IF TAPE HAS FAILED
  17 5_D6          BNZ   0  Z2RENTRY            [J IF NOT FIRST ENTRY
   9 5_XQ          LDX   0  AWORK2(2)
  17 62CB          BNZ   0  Z2RENTRY            [J IF NOT FIRST ENTRY
   8 62X2          NGN   0  62   
   8 63BL          LDN   5  512  
  19 63W=          CALL  6  Z2WRITES            [WRITE START OF DATA SENTINEL
  16 64*W          BNZ   4  Z2FAIL              [J IF WRITE ERROR
   5 64TG    Z2RENTRY
  10 65*6          LDX   7  APETADDR(2)  
   7 65SQ          LDX   3  7
  17 66#B          LOCKCA   3,EVENT2(2)         [CHECK CONTROL AREA  
   9 66S2          LDX   4  EVENT2(2)
   9 67?L          LDN   5  ACA1(2)  
   7 67R=          LDN   6  4
  18 68=W          FOUTMOVE 3,4,5,6,HLOCK1      [MOVE OUT CONTROL AREA   
  15 68QG          LDEX  0  ACA1(2)             [TEST MODE   
  14 69=6          BZE   0  Z2CAD               [OK IF #0
   9 69PQ          ANDX  0  XMASK(1) 
   9 6=9B          BNZ   0  Z2MILL   
   9 6=P2          LDEX  0  ACA1(2)  
   8 6?8L          ANDN  0  #6   
   8 6?N=          ERN   0  #6   
   9 6#7W          BZE   0  Z2MILL   
   7 6#MG          SRL   0  2
  17 6*76          BNZ   0  Z2MILL              [ILLEGAL IF MODE #2  
   8 6*LQ    Z2CAD LDCT  5  #600 
  15 6B6B          ANDX  5  ACA4(2)             [TEST ADDRESS
   9 6BL2          ERX   5  XMASK1(1)
  18 6C5L          BNZ   5  Z2CADILL            [ILLEGAL IF NOT CHAR 3   
   5 6CK=    Z2CADOK 
  15 6D4W          LDX   6  ACA3(2)             [TEST COUNT  
   9 6DJG          BZE   6  Z2CILL   
   9 6F46          TXL   6  MAX1(1)  
  17 6FHQ          BCC      Z2CILL              [ILLEGAL IF > 2000   
   7 6G3B          ADN   6  6
  18 6GH2          SRL   6  2                   [CONVERT COUNT TO WORDS  
   9 6H2L          LDX   5  ACA4(2)  
  17 6HG=          PMODES   3,7                 [CHECK ADDRESS MODE  
   8 6H_W          ANDN  3  #10  
  15 6JFG          BNZ   3  Z2A                 [J IF 22AM   
   9 6J_6          ANDX  5  XMASK2(1)
  16 6KDQ    Z2A   LDN   4  CHREAD              [SET MODE = READ 
  15 6KYB          STO   5  ACA4(2)             [SAVE ADDRESS
  18 6LD2          CHECKB   5,6,Z2RESV,7,4,Z2RESOK [CHECK RESERVATIONS   
   4 6LXL    #   
   4 6MC=    #   
  13 6MWW    #         PERI VALID - NOW PROCESS IT   
   4 6NBG    #   
   4 6NW6    #   
   5 6P*Q    Z2RESOK 
   7 6PTB          LDX   2  7
   8 6Q*2          SMO      FX2  
  18 6QSL          LDX   3  ACA4                [PROGRAM BUFFER ADDRESS  
  15 6R#=          FADDRESS 2,3,HLOCK1          [ADD DATUM   
   8 6RRW          LDX   2  FX2  
  15 6S?G          LDCH  5  0(3)                [LOAD PFCC   
   9 6SR6          TXL   5  XWRITE(1)
  17 6T=Q          BCC      Z2RESOKP            [J IF 'PRINT' MODE   
  16 6TQB          LDN   6  1                   [SET LENGTH = 1  
   5 6W=2    Z2RESOKP
   7 6WPL          ADN   6  1
  18 6X9=          ADX   6  AWORK1(2)           [ADD BUFFER SIZE SO FAR  
   9 6XNW          TXL   6  MAX2(1)  
  16 6Y8G          BCC      Z2BUFFUL            [J IF BUFFER FULL
  17 6YN6          SBX   6  AWORK1(2)           [RESTORE RECORD SIZE 
  20 6_7Q          BRN      Z2BUFFOK            [AND J TO ADD RECORD TO BUFFER   
   4 6_MB    #   
   4 7272    #   
  13 72LL    #         BUFFER FULL - WRITE IT TO TAPE
   4 736=    #   
   4 73KW    #   
   5 745G    Z2BUFFUL
   9 74K6          LDX   5  AWORK1(2)
  17 754Q          STOZ     AWORK1(2)           [MARK BUFFER EMPTY   
   7 75JB          LDN   0  1
  18 7642          ADS   0  AWORK2(2)           [UPDATE MT BLOCK COUNT   
  18 76HL          CALL  6  Z2WRITE             [WRITE THE BLOCK TO TAPE 
  16 773=          BNZ   4  Z2FAIL              [J IF WRITE ERROR
   9 77GW          LDX   6  ACA3(2)  
   8 782G          ADN   6  10   
  17 78G6          SRL   6  2                   [RESET RECORD LENGTH 
   9 78_Q          LDX   4  CPRW1(2) 
   9 79FB          ANDX  4  XMASK5(1)
  17 79_2          BZE   4  Z2BUFFOK            [J IF NOT END OF TAPE
   9 7=DL          LDX   4  AWORK4(2)
  18 7=Y=          BZE   4  Z2EOR               [J IF EOT JUST REACHED   
  17 7?CW          BNG   4  Z2EOT               [J IF OK TO WRITE EOR
   9 7?XG          ANDX  4  XMASK3(1)
  17 7#C6          SBN   4  1                   [DECREMENT END COUNT 
  17 7#WQ          BZE   4  Z2EOT               [J IF COUNT EXHAUSTED
  17 7*BB          BNG   4  Z2EOT               [J IF COUNT EXHAUSTED
   9 7*W2          DSA   4  AWORK4(2)
  17 7B*L          BRN      Z2BUFFOK            [J TO PROCESS PERI   
   8 7BT=    Z2EOR LDCT  4  #200 
  19 7C#W          ADN   4  10                  [SET COUNT OF 10 MORE BLOCKS 
  20 7CSG          STO   4  AWORK4(2)           [SO THAT WE MAY WAIT FOR A HOF   
  17 7D#6          BRN      Z2BUFFOK            [J TO PROCESS PERI   
   4 7DRQ    #   
   4 7F?B    #   
  11 7FR2    #         WRITE EOR TO FULL REEL
  15 7G=L    #         AND THEN FAIL OUTPUT *LPN FILE FULL   
   4 7GQ=    #   
   4 7H9W    #   
   8 7HPG    Z2EOT LDCT  0  #600 
   9 7J96          LDX   5  AWORK2(2)
  16 7JNQ          CALL  6  Z2WRITES            [WRITE EOR LABEL 
  16 7K8B          BNZ   4  Z2FAIL              [J IF TAPE FAIL  
   8 7KN2          LDCT  0  #400 
   9 7L7L          STO   0  AWORK1(2)
   8 7LM=          LDCT  5  #50  
   9 7M6W          LDX   0  ACA1(2)  
   7 7MLG          SRL   0  3
   8 7N66          ANDN  0  #7777
   8 7NKQ          SBN   0  #3000
   8 7P5B          BZE   0  Z2REP
  16 7PK2          LDX   5  ZJYOUTFULL(1)       [OUTPUT FILE FULL
   8 7Q4L          BRN      Z2ILL
   4 7QJ=    #   
   4 7R3W    #   
  11 7RHG    #         ADD RECORD TO BUFFER  
   4 7S36    #   
   4 7SGQ    #   
   5 7T2B    Z2BUFFOK
   7 7TG2          LDX   2  7
   8 7T_L          SMO      FX2  
  18 7WF=          LDX   3  ACA4                [PROGRAM BUFFER ADDRESS  
  15 7WYW          FADDRESS 2,3,HLOCK1          [ADD DATUM   
  15 7XDG          LDCH  4  0(3)                [LOAD PFCC   
   8 7XY6          LDX   2  FX2  
  10 7YCQ          TXU   4  XHASH51(1)   
  17 7YXB          BCC      Z2THROW             [J IF WRITE AFTER HOF
  10 7_C2          TXU   4  XHASH11(1)   
  18 7_WL          BCS      Z2NOTHROW           [J IF NOT THROW TO HOF   
   5 82B=    Z2THROW 
   9 82TW          LDX   4  AWORK4(2)
  17 83*G          BZE   4  Z2NOEOT             [J IF EOT NOT REACHED
   8 83T6          LDCT  4  #400 
  18 84#Q          STO   4  AWORK4(2)           [MARK OK TO CLOSE TAPE   
   5 84SB    Z2NOEOT 
   9 85#2          LDX   3  AWORK1(2)
  17 85RL          BNZ   3  Z2BUFFUL            [J IF DATA IN BUFFER 
  16 86?=          BNG   4  Z2EOT               [J TO CLOSE TAPE 
   6 86QW    Z2NOTHROW   
   7 87=G          LDX   2  7
   8 87Q6          SMO      FX2  
  18 889Q          LDX   3  ACA4                [PROGRAM BUFFER ADDRESS  
  15 88PB          FADDRESS 2,3,HLOCK1          [ADD DATUM   
   8 8992          LDX   2  FX2  
  15 89NL          LDCH  5  0(3)                [LOAD PFCC   
   9 8=8=          TXL   5  XWRITE(1)
  17 8=MW          BCC      Z2PRINT             [J IF 'PRINT' MODE   
   9 8?7G          LDX   3  AWORK1(2)
   8 8?M6          ADX   3  FX2  
   7 8#6Q          LDN   6  2
  16 8#LB          STO   6  ACA5(3)             [WORD COUNT = 2  
   9 8*62          ADS   6  AWORK1(2)
   8 8*KL          SLL   5  18   
   9 8B5=          LDEX  4  ACA1(2)  
  17 8BJW          SRL   45 6                   [ADD MODE INTO WORD  
  16 8C4G          LDN   4  1                   [NO. CHARS = 1   
   9 8CJ6          BRN      Z2ENDPERI
   5 8D3Q    Z2PRINT 
   9 8DHB          LDX   3  AWORK1(2)
   8 8F32          ADX   3  FX2  
  15 8FGL          STO   6  ACA5(3)             [WORD COUNT  
   9 8G2=          ADS   6  AWORK1(2)
  18 8GFW          SBN   6  1                   [RESET TO LENGTH OF DATA 
   9 8G_G          LDX   4  ACA4(2)  
   9 8HF6          ANDX  4  XMASK3(1)
   9 8HYQ          LDN   5  ACA6(3)  
  18 8JDB          FOUTMOVE 7,4,5,6,HLOCK1      [MOVE DATA INTO BUFFER   
  15 8JY2          LDX   5  ACA6(3)             [LOAD PFCC   
   8 8KCL          SLL   5  18   
   9 8KX=          LDEX  4  ACA1(2)  
  17 8LBW          SRL   45 6                   [ADD MODE INTO WORD  
   9 8LWG          LDX   4  ACA3(2)  
   6 8MB6    Z2ENDPERI   
  19 8MTQ          SRL   45 12                  [ADD CHAR. COUNT INTO WORD   
  20 8N*B          STO   5  ACA6(3)             [STORE LENGTH/MODE/PFCC IN RECORD
   7 8NT2          LDN   5  0
  18 8P#L    Z2REP LINKSET  FX2,PAIR2(1)        [SET LINK FOR NEXT ENTRY 
   7 8PS=          LDX   2  7
   8 8Q?W          SMO      FX2  
  18 8QRG          LDX   3  EVENT2              [ADDRESS OF CONTROL AREA 
   7 8R?6          ADN   3  1
  10 8RQQ          FADDRESS 2,3,HLOCK1   
  20 8S=B          STO   5  0(3)                [SET 'OK' REPLY TO OBJECT PROGRAM
  17 8SQ2          OLPFINX  7                   [SIGNAL PERI FINISHED
  17 8T9L          BRN      Z2NETOFLNE          [J FOR NEXT TRANSFER 
   4 8TP=    #   
   4 8W8W    #   
  15 8WNG    #         VARIOUS ILLEGAL CONDITIONS COME HERE  
   4 8X86    #   
   4 8XMQ    #   
   5 8Y7B    Z2FAIL  
   8 8YM2          LDCT  5  #50  
   9 8_6L          LDX   0  ACA1(2)  
   7 8_L=          SRL   0  3
   8 925W          ANDN  0  #7777
   8 92KG          SBN   0  #3000
   8 9356          BZE   0  Z2REP
   8 93JQ          LDCT  5  #400 
   9 944B          STO   5  AWORK1(2)
  16 94J2          LDX   5  ZJYPERFAIL(1)       [ONLINE PERI FAIL
   8 953L          BRN      Z2ILL
   5 95H=    Z2MILL  
  16 962W          LDX   5  ZJYMODE(1)          [MODE IN ERROR   
   8 96GG          BRN      Z2ILL
   5 9726    Z2CADILL
  17 97FQ          LDX   5  ZJYCHAR(1)          [ILLEGAL CHAR. ADDR. 
   8 97_B          BRN      Z2ILL
   5 98F2    Z2CILL  
  17 98YL          LDX   5  ZJYCOUNT(1)         [COUNT OUT OF RANGE  
   8 99D=          BRN      Z2ILL
   5 99XW    Z2RESV  
  18 9=CG          LDX   5  ZJYRESVIOL(1)       [RESERVATION VIOLATION   
  15 9=X6    Z2ILL LDX   4  FTYPNO(2)           [TYPE/UNIT   
   9 9?BQ          TOPCA    PAIR4(1) 
   6 9?WB          COOR1Y
  17 9#B2          BRN      Z2NETOFLNE          [J FOR NEXT TRANSFER 
   4 9#TL    #   
   4 9**=    #   
  11 9*SW    #         TAPE WRITE SUBROUTINE 
   4 9B#G    #   
   4 9BS6    #   
  17 9C?Q    Z2WRITES                           [WRITE SENTINEL ENTRY
   8 9CRB          SBX   6  FX1  
  16 9D?2          STO   0  ACA5(2)             [SENTINEL TYPE   
  18 9DQL          STO   5  ACA6(2)             [SUBSIDIARY INFORMATION  
   9 9F==          STOZ     ACA7(2)  
   9 9FPW          LDN   4  ACA7(2)  
   9 9G9G          LDN   5  ACA8(2)  
  18 9GP6          MOVE  4  17                  [SET REMAINDER TO ZERO   
   9 9H8Q          LDX   3  CPPTR(2) 
   7 9HNB          LDN   5  5
  10 9J82          DCA      3,STO,5,MODE 
  17 9JML          GPERI    ,2                  [WRITE A TAPE MARK   
  19 9K7=          COOR1                        [WAIT FOR TRANSFER TO FINISH 
  20 9KLW          LDN   5  20                  [QUALIFIER BLOCK 20 WORDS LONG   
   8 9L6G          BRN      Z2WRS
  17 9LL6    Z2WRITE                            [WRITE BLOCK ENTRY   
   8 9M5Q          SBX   6  FX1  
   9 9MKB          TXL   5  XFIVE(1) 
  18 9N52          BCC      Z2WRITEOK           [J IF BLOCK >= 5 WORDS   
   9 9NJL          LDN   4  ACA5(2)  
   7 9P4=          ADX   4  5
   9 9PHW          LDN   3  XTRA(1)  
  19 9Q3G          MOVE  3  6                   [ADD 3 NULL 2 WORD RECORDS   
  18 9QH6          ADN   5  6                   [INCREMENT BLOCK LENGTH  
   6 9R2Q    Z2WRITEOK   
   9 9RGB          LDX   3  CPPTR(2) 
  10 9S22    Z2WRS DCA      3,STO,5,COUNT
   7 9SFL          LDN   5  1
  10 9S_=          DCA      3,STO,5,MODE 
   9 9TDW          LDN   5  ACA5(2)  
  10 9TYG          DCA      3,STO,5,CTSA 
  16 9WD6          GPERI    ,2                  [WRITE THE BLOCK 
  17 9WXQ          COOR1                        [WAIT FOR TRANSFER   
  15 9XCB          LDX   4  CPRW1(2)            [LOAD REPLY  
  16 9XX2          ANDX  4  XMASK4(1)           [MASK ERROR BIT  
   8 9YBL          ADX   6  FX1  
   7 9YW=          EXIT  6  0
   4 9_*W    #   
   4 9_TG    #   
   9 =2*6    #         RELEASE ENTRY 
   4 =2SQ    #   
   4 =3#B    #   
   6 =3S2    Z3NETOFLNE  
  19 =4?L    #     *MT FAILS IN CLOSING ARE NOTED IN ACOMMUNE1 FOR OFFREL.   
  10 =4R=          STOZ     ACOMMUNE1(2) 
   9 =5=W          LDX   5  AWORK1(2)
  17 =5QG          BNG   5  Z3NOWRITE           [J IF TAPE HAS FAILED
  18 =6=6          BZE   5  Z3BUFFEMPTY         [J IF NO DATA IN BUFFER  
   7 =6PQ          LDN   0  1
  18 =79B          ADS   0  AWORK2(2)           [UPDATE MT BLOCK COUNT   
  19 =7P2          CALL  6  Z2WRITE             [WRITE THE REMAINING BLOCK   
  10 =88L          STO   4  ACOMMUNE1(2) 
   9 =8N=          BNZ   4  Z3NOWRITE
   4 =97W    #   
   4 =9MG    #   
  15 ==76    #         WRITE TRAILER LABEL AND RELEASE TAPE  
   4 ==LQ    #   
   4 =?6B    #   
   6 =?L2    Z3BUFFEMPTY 
   8 =#5L          LDCT  0  #400 
  15 =#K=          LDX   5  AWORK2(2)           [BLOCK COUNT 
  17 =*4W          CALL  6  Z2WRITES            [WRITE EOF SENTINEL  
  10 =*JG          STO   4  ACOMMUNE1(2) 
   6 =B46    Z3NOWRITE   
  18 =BHQ    #     OFFREL WILL REWIND THE TAPE AUTONOMOUSLY AND FREE IT. 
  16 =C3B          UP                           [RETURN TO OFFREL
   4 =CH2    #   
   4 =D2L    #   
  10 =DG=    #         'ILLEGAL' ENTRY   
   4 =D_W    #   
   4 =FFG    #   
   6 =F_6    Z4NETOFLNE  
  21 =GDQ          UNPLUG                       [UNPLUG THE PROGRAM FROM EXECUTIVE   
  10 =GYB          OUTPACK  4,1,PERINAME 
  19 =HD2          ILLEGALX 5                   [SEND THE PROGRAM 'ILLEGAL'  
   4 =HXL    #END
   2 =JC=
   6 ____ ...455552450001
