  15 22FL          SEG      FILELOG,,GEORGE PORTER,FILESTORE 
  11 22_=          SEGENTRY K1FILELOG,START  
  19 23DW    SLAN           4HC1                [TEMPORARY DIRECTORY LANGUAGE
  18 23YG                   4HB1B0              [MONITORING FILE LANGUAGE
  20 24D6                   4HB5B1              [JOB-DESCRIPTION-FILE LANGUAGE   
   8 24XQ                   +0   
  20 25CB    # THIS SEGMENT ERASES THE THE TEMP FILE STACK & THE MONITORING FILE.
  21 25X2    #     IF FILES CANT BE ERASED BECAUSE THEY ARE OPEN, A TO BE ERASED BIT 
  14 26BL    #     BE LEFT IN THEIR DIRECTORY ENTRIES.   
   4 26W=    [   
  15 27*W    START                              [ENTRY POINT 
  15 27TG          TRACEIF  K6FILELOG,99,299,JOBNO(2),FILELOG
  16 28*6          ERALLWF                      [ERASE WORKFILES 
  11 28SQ          SETNCORE 6,1,FILE,FLOCNB  
  11 29#B          SETNCORE 10,3,FILE,FABSNB 
  16 29S2          LDN   0  4                   [RECORD HEADER   
  14 2=?L          STO   0  A1(3)               [HEADER  
  17 2=R=          FJOCA    1,2                 [GET X1->JOB BLOCK   
   9 2?=W          LDN   2  JUSER(1) 
   8 2?QG          ADN   3  A1+1 
  17 2#=6          MOVE  2  3                   [USERNAME TO FABSNB  
  10 2#PQ          MHUNTW   3,FILE,FLOCNB
   9 2*9B          LDN   2  JNAME(1) 
   8 2*P2          ADN   3  A1   
  17 2B8L          MOVE  2  3                   [LOCAL NAME TO FLOCNB
   7 2BN=          LDCT  4  2
  21 2C7W          ANDX  4  JMISC(1)            [BIT 7 OF JMISC SET IF TEMP DIR CREAT
  21 2CMG          SLC   4  8                   [SET X4 =0 OR 1              [SEE NEW
  18 2D76          ERN   4  1                   [=0 IF TEMP DIR EXISTS   
  17 2DLQ          OPEN     NO,GENERAL          [OPEN USERS DIRECTORY
   8 2F6B          TESTRPN2 OK,NO
   4 2FL2    NEXT
  19 2G5L          SMO      4                   [MODIFIER FOR LANGUAGE WORD  
  18 2GK=          LDX   3  SLAN(1)             [GET NEXT LANGUAGE WORD  
  10 2H4W          MHUNT    1,FILE,FLOCNB
  18 2HJG          BZE   3  XITA                [J IF NO MORE TEMP FILES 
  14 2J46          STO   3  A1+5(1)             [LANGUAGE
  20 2JHQ          ADN   4  1                   [INCREASE LANGUAGE WORD MODIFIER 
  14 2K3B ...      STOZ     A1+4(1)             [GEN 1   
  13 2L2L          STOZ     A1+3(1)             [REEL
  21 2LG=          GDE                          [FIND DIRECTORY ENTRY FOR THIS FILE  
  17 2L_W          TESTRPN2 OK,NEXT             [JDF MAY NOT EXIST   
  10 2MFG          MHUNTW   1,FILE,ENT   
  20 2M_6          JBC      XFREE,1,BETEMP      [IGNORE UNLESS TEMPORARY FILE (?)
  10 2NDQ          MHUNT    2,FILE,FABSNB
  10 2NYB          LDX   0  ALOGLEN(2)   
  20 2PD2          STO   0  A1(2)               [SET REC HEADER = LOGICAL LENGTH 
   7 2PXL          ADX   2  0
   8 2QC=          ADN   2  A1-6 
  16 2QWW          MHUNT    1,FILE,FLOCNB       [FIND LOCAL NAME 
   8 2RBG          ADN   1  A1   
  18 2RW6          MOVE  1  6                   [MOVE INTO END OF FABSNB 
  17 2S*Q          ERASEND                      [ERASE NEXT TEMP FILE
   8 2STB          BRN      NEXT 
   5 2T*2    XFREE   
  13 2TSL          FREECORE 1                   [ENT 
   8 2W#=          BRN      NEXT 
   4 2WRW    XITA
  14 2X?G          FREECORE 1                   [FLOCNB  
  10 2XR6          MFREE    FILE,FABSNB  
  18 2Y=Q          CLOSE                        [CLOSE USER'S DIRECTORY  
   5 2YQB          UP
  20 2_=2    NO    GEOERR   BRIEFPM,FILELOG     [UNABLE TO OPEN USER'S DIRECTORY 
   4 2_PL    #END
   8 ____ ...71446730000300000000
