  15 22FL          SEG  BRADSPLT,860,FRANK BLUNDELL,BRADSPLT 
  16 22_=    #     (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23DW    #   
  13 23YG    #     ********************************  
  13 24D6    #     *BRADFORD UNIVERSITY SCF SYSTEM*  
  13 24XQ    #     ********************************  
   4 25CB    #   
  12 25X2          SEGENTRY K1BRADSPLT,Q1BRADSPLT
  10 26BL          SEGENTRY K98BRADSPLT  
  13 26W=    BRADSPLTINF    +0,+0,+0         [NAME   
  14 27*W                   +0               [TYPE B0-5  
  17 27TG                   +0               [CSN OF DISC IF GIVEN   
   5 28*6    ZCCTABLE
   8 28SQ    #HAL BSTB+CCTABLE,0 
   6 29#B    ZALFTABLE   
   8 29S2    #HAL BSTB+ALFTABLE,0
   5 2=?L    ZFDTABLE
   8 2=R=    #HAL BSTB+FDTABLE,0 
   9 2?=W    XN1            #77000000
   8 2?QG    Y              #71  
   8 2#=6    N              #56  
  15 2#PQ    XSUITA         +FIFSFAS         [ >1 FILE AREA  
  19 2*9B    XSUITB         +FIFSFIC         [FIC SEPARATED FROM FILE AREA   
  19 2*P2    XSUITC         +FIFSRET         [RETENTION PEROOD NOT EXPIRED   
  15 2B8L    XSUITD         +FIFSSYF         [SYFLE BIT SET  
  16 2BN=    XSUITE         +FIFSBUCK        [BUCKET > 1 BLOCK   
  17 2C7W    WANT           +10,36HDO YOU WISH TO USE THE SCF SYSTEM 
  15 2CMG    NAME           +7,24HGIVE NAME OF FILE TO USE   
  20 2D76    TYPE           +13,48HGIVE DEVICE TYPE(DA=UDAS,DR=1964 DRUM,HD=HSD) 
  10 2DLQ    ZTPE           #41,#62,#44  
   9 2F6B    ZTPEX          #44,#50  
   8 2FL2    ZDEV           +0   
   6 2G5L    #FID 0,5,+6 
   8 2GK=                   +0   
   6 2H4W    #FID 0,5,+9 
   8 2HJG                   +0   
   6 2J46    #FID 0,5,+25
   4 2JHQ    #   
   4 2K3B    #   
   6 2KH2    Q1BRADSPLT  
  10 2L2L          SEGENTRY K95BRADSPLT  
   8 2LG=          BRN      XHE  
  12 2L_W          SETNCORE GEXDLEN,3,EWDAS,GEXD 
  13 2MFG          SETNCORE FIRLEN+FIRSS,1,BSTB,FSTAB
   8 2M_6          SMO      FX1  
  10 2NDQ          LDX   0  BRADSPLTINF  
   9 2NYB          TRACE 0,FILENAME  
  18 2PD2          BZE   0  ZASKNAME         [INFO NOT GIVEN IN CHAPTER  
   8 2PXL          SMO      FX1  
  18 2QC=          LDN   0  BRADSPLTINF      [DATA FOR FILE IN CHAPTER   
  17 2QWW          ADN   1  FIRSS            [MOVE TO DATA ADDRESS   
  17 2RBG          MOVE  0  5                [MOVE IT INTO DATA BLOCK
  20 2RW6          LDN   7  0                   [REQUIRED BECAUSE OF COMMON ERROR
  19 2S*Q                                       [PATHS IN FSERR. USED IN SMO 
   5 2STB    ZRETRY  
  10 2T*2          MHUNTW 1,BSTB,FSTAB   
  10 2TSL          MHUNTW 3,EWDAS,GEXD   
  14 2W#=          LDCH  0  FIRSS+3(1)       [GET TYPE   
   7 2WRW          SBN   0  6
   8 2X?G          BZE   0  T1   
   7 2XR6          SBN   0  7
  15 2Y=Q          BZE   0  T1               [JUMP IF DISC   
   4 2YQB    #   
   4 2_=2    #   
   7 2_PL    #     DRUM  PART
   4 329=    #   
  14 32NW          SETNCORE BBUFF+129-A1,3,EWDAS,FDRUM   
  10 338G          MHUNTW   1,BSTB,FSTAB 
  10 33N6          LDX   0  FIRSS+3(1)   
   8 347Q          SMO      FX1  
   8 34MB          ANDX  0  XN1  
   9 3572          STO   0  AGTYPE(3)
   8 35LL          ADN   3  ANAME
   9 366=          LDN   2  FIRSS(1) 
  16 36KW          MOVE  2  3                [MOVE IN FILENAME   
  15 375G    U9    DOWN     FINDRUM,1        [FIND THE FILE  
  15 37K6          TESTREPNOT   OK,S1        [JUMP IF ERROR  
   4 384Q    #   
  12 38JB    #              DRUM FILE NOW OPEN   
  12 3942    #              STORE INFO IN CCTABLE
   4 39HL    #   
  10 3=3=          MHUNTW   3,EWDAS,FDRUM
  11 3=GW          TRACE EXBSP+2(3),DRUMOPEN 
  10 3?2G          MHUNTW   1,BSTB,FSTAB 
  16 3?G6          LDX   2  BINDEX           [POINTER TO CCTABLE 
  14 3?_Q          LDX   0  ATYPE(2)         [BLOCK TYPE 
   8 3#FB          SMO      FX1  
   9 3#_2          TXU   0  ZCCTABLE 
   9 3*DL          BCC      ZCCTBLOCK
  16 3*Y=          GEOERR 1,SPLITF03         [NOT CORRECT BLOCK  
   6 3BCW    ZCCTBLOCK   
  14 3BXG          LDX   0  ALOGLEN(2)       [WORK SIZE  
  16 3CC6          SBN   0  12               [2*DISC INFO SIZE   
   9 3CWQ          BPZ   0  ZCCTSIZE 
  16 3DBB          GEOERR 1,SPLITF04         [TOO SMALL A BLOCK  
   5 3DW2    ZCCTSIZE
  19 3F*L          LDN   6  6                [OFFSET TO SECOND BLOCK OF INFO 
   9 3FT=          LDX   0  AGTYPE(3)
   7 3G#W          SMO      6
  13 3GSG          STO   0  A1(2)            [TYPE   
  10 3H#6          LDX   0  EXBSP+1(3)   
   7 3HRQ          SMO      6
  13 3J?B          STO   0  A1+1(2)          [DATUM  
  10 3JR2          LDX   0  EXBSP+2(3)   
   7 3K=L          SMO      6
  13 3KQ=          STO   0  A1+2(2)          [LENGTH 
  20 3L9W          ADX   6  2                [X6 POINTS TO SECOND PART O  CTABLE 
  16 3LPG          LDX   1  FPTR(2)          [SHOULD GET ALFTABLE
  14 3M96          LDX   0  ATYPE(1)         [BLOCK TYPE 
   8 3MNQ          SMO      FX1  
   9 3N8B          TXU   0  ZALFTABLE
   9 3NN2          BCC      ZALFBLOCK
  16 3P7L          GEOERR 1,SPLITF05         [NOT ALFTABLE BLOCK 
   6 3PM=    ZALFBLOCK   
  16 3Q6W          LDX   2  FPTR(1)          [SHOULD GET FDTABLE 
  14 3QLG          LDX   0  ATYPE(2)         [BLOCK TYPE 
   8 3R66          SMO      FX1  
   9 3RKQ          TXU   0  ZFDTABLE 
   9 3S5B          BCC      ZFDBLOCK 
  16 3SK2          GEOERR 1,SPLIT06          [NOT FDTABLE BLOCK  
   5 3T4L    ZFDBLOCK
  15 3TJ=          LDX   4  BLMP             [HIGHEST RES + 1
  17 3W3W          SBN   4  1                [NUMBER OF HIGHEST RES  
   8 3WHG    R2    LDX   5  A1(1)
  15 3X36          BNG   5  R1               [NON EXISTANT   
   8 3XGQ          ANDN  5  #7777
   7 3Y2B          SMO      5
   9 3YG2          LDX   0  A1+1(2)  
   7 3Y_L          SMO      6
   8 3_F=          TXU   0  A1+1 
  17 3_YW          BCS      R1               [JUMP IF DATUM DIFFERENT
   8 42DG          LDX   0  A1(1)
   8 42Y6          SRL   0  12   
  14 43CQ          ANDN  0  #1777            [CHECK  BSUN
   9 43XB          TXU   0  EXBSP(3) 
  17 44C2          BCC      S4               [JUMP - FILE USED BEFORE
   7 44WL    R1    ADN   1  2
   8 45B=          BCT   4  R2   
  13 45TW          LDX   4  EXBSP(3)         [BSUN   
   9 46*G          TRACE 4,DRUMBSUN  
  17 46T6          STO   4  BRADSPLTBSU      [SET 2ND BSUN FOR GEORGE
  16 47#Q          SETALLOC ADRDLIST(3)      [MARK DEVICE IN USE 
  10 47SB          LDX   4  ADRDLIST(3)  
   9 48#2          STO   4  GEOSCFPTR
  16 48RL          FREECORE 3                [RELEASE EWDAS-FDRUM
   9 49?=          BRN      ZCOMPL   
   4 49QW    #   
   7 4==G    #     DISC  PART
   4 4=Q6    #   
  13 4?9Q    T1    SETNCORE FDCELLS-A1,3,EWDAS,EXOF  
  10 4?PB          MHUNTW   1,BSTB,FSTAB 
  10 4#92          LDN   0  BOPEN+BSPEC  
  16 4#NL          STO   0  EXMARK(3)        [SET OPEN MARKERS   
  16 4*8=          STOZ     FIP+4(3)         [GENERATION NUMBER  
   9 4*MW          LDN   5  FIP(3)   
   9 4B7G          LDN   4  FIRSS(1) 
  16 4BM6          MOVE  4  3                [MOVE IN FILENAME   
  10 4C6Q          LDX   4  FIRSS+4(1)   
  17 4CLB          BZE   4  T2               [JUMP IF NOT SPECIFIED  
   9 4D62          STO   4  ASUSN(3) 
  21 4DKL          LDN   0  BSERL+BSUPP      [SERIAL NO. GIVEN-SUPPRES REQUEST IF NOT
   9 4F5=          ORS   0  EXMARK(3)
  14 4FJW    T2    DOWN     EXOPEN,1         [OPEN FILE  
   9 4G4G          TESTREPNOT   OK,S2
   4 4GJ6    #   
  12 4H3Q    #              DISC FILE NOW OPEN   
  12 4HHB    #              STORE INFO IN CCTABLE
   4 4J32    #   
  10 4JGL          MHUNTW   3,EWDAS,EXOF 
  17 4K2=                                    [START CHECKS ON FILE   
  11 4KFW          TRACE ASUSN(3),DISCFILE   
   9 4K_G          LDEX  0  FIP+5(3) 
   7 4LF6          SBN   0  1
  18 4LYQ          BNZ   0  S3FAREA          [MUST BE ONLY ONE FILE AREA 
  10 4MDB          LDX   0  FDCELLS(3)   
   9 4MY2          TXU   0  ASUSN(3) 
  21 4NCL          BCS      S3ASFIC          [FILE AREA MUST BE ON SAME UNIT AS FIC  
   9 4NX=          LDX   0  FIP+7(3) 
  16 4PBW          SRL   0  12               [RETENTION PERIOD   
  15 4PWG          ADX   0  FIP+6(3)         [DATE WRITTEN   
   8 4QB6          LDX   4  EDATE
   7 4QTQ          TXL   4  0
  21 4R*B          TXL   4  FIP+6(3)            [IS DATE WRITTEN LATER THAN CURRENT  
  20 4RT2          BCS      T9                  [DATE? J IF SO TO MISS RP CHECKS 
  19 4S#L          BCS      S3RETN           [RETENTION PERIOD NOT EXPIRED   
   8 4SS=    T9    LDCT  0  #100 
   9 4T?W          ANDX  0  FIP+5(3) 
  15 4TRG          BNZ   0  S3SYFLE          [SYFLE BIT SET  
   9 4W?6          LDX   0  FIP+8(3) 
  14 4WQQ          ANDN  0  #77              [BUCKET SIZE
   7 4X=B          SBN   0  1
  15 4XQ2          BNZ   0  S3BUCK           [BUCKET NOT ONE.
  20 4Y9L          LDN   1  BCCB             [CHECK IF THIS FILE SPECIFIED BEFORE
   9 4YP=    T4    LDX   1  BPTR(1)  
   8 4_8W          TXU   1  CXCC 
  15 4_NG          BCC      T3               [END OF CHAIN   
   9 5286          LDX   0  BACK2(1) 
  15 52MQ          BZE   0  T3               [NO1MORE EXOFS  
   9 537B          LDX   0  ASUSN(1) 
   9 53M2          TXU   0  ASUSN(3) 
  20 546L          BCS      T4               [JUMP IF SERIAL NUMBERS DIFFERENT   
   9 54L=          LDX   0  HOME(1)  
   9 555W          TXU   0  HOME(3)  
  18 55KG          BCS      T4               [JUMP IF BS HOMES DIFFERENT 
  15 5656          BRN      S4               [FILE USED TWICE
  14 56JQ    T3    LDX   0  EDATE            [UPDATE  FIP
  15 574B          STO   0  FIP+6(3)         [DATE WRITTEN   
   9 57J2          LDX   0  FIP+4(3) 
  15 583L          ANDN  0  #7777            [VERSION NUMBER 
   7 58H=          ADN   0  1
   9 592W          DSA   0  FIP+4(3) 
   7 59GG          LDX   2  3
  10 5=26          MHUNTW   3,EWDAS,GEXD 
  14 5=FQ          OPENST   ASUSN(2)         [OPEN UNIT  
  16 5=_B          READST   HOME(2),T5       [READ DOWN FIP CELL 
  14 5?F2          TESTREP  OK,T7            [JUMP IF OK 
  13 5?YL          TESTREP  EXFAIL,S5        [OR FAIL
  10 5#D=    T8    GEOERR   1,SPLITF07   
   9 5#XW    T7    LDX   1  ECOR(3)  
  10 5*CG    T5    MHUNTW   2,EWDAS,EXOF 
   7 5*X6          ADN   1  2
   9 5BBQ          LDN   0  FIP(2)   
  15 5BWB          MOVE  0  15               [MOVE IN NEW FIP
  19 5CB2          CLOSEWR  W,HOME(2)        [REWRITE FIP CELL AND CLOSE UNIT
  14 5CTL          TESTREP  OK,T6            [JUMP IF  OK
  10 5D*=          TESTREPNOT   EXFAIL,T8
   8 5DSW          BRN      S5   
  10 5F#G    T6    MHUNTW   3,EWDAS,EXOF 
   7 5FS6          LDN   0  1
  16 5G?Q          STO   0  COPEN(3)         [COUNT OF OPENS=1   
   8 5GRB          LDCT  0  #500 
  20 5H?2          STO   0  EXMARK(3)        [OPEN FOR WRITING AND SYSTEM BITS   
  10 5HQL          MFINDSU  1,ASUSN(3)   
  18 5J==                                    [START REORGANISING FDCELL  
  16 5JPW          LDX   5  ABSUN(1)         [GET BSUN FOR LATER 
   9 5K9G          TRACE 5,DISCBSUN  
  17 5KP6          STO   5  BRADSPLTBSU      [SET 2ND BSU FOR GEORGE 
  15 5L8Q          LDX   4  HUNIT(1)         [AND D.L. PTR   
  16 5LNB          LDX   2  BINDEX           [POINTER TO CCTABLE 
  14 5M82          LDX   0  ATYPE(2)         [BLOCK TYPE 
   8 5MML          SMO      FX1  
   9 5N7=          TXU   0  ZCCTABLE 
   9 5NLW          BCC      YCCTBLOCK
  14 5P6G          GEOERR 1,SPLITF08         [NOT CCTABLE
   6 5PL6    YCCTBLOCK   
  14 5Q5Q          LDX   0  ALOGLEN(2)       [WORK SIZE  
  16 5QKB          SBN   0  12               [2*DISC INFO SIZE   
   9 5R52          BPZ   0  YCCSIZE  
  15 5RJL          GEOERR 1,SPLITF09         [BLOCK TOO SMALL
   5 5S4=    YCCSIZE 
  16 5SHW          LDN   6  6                [OFFESET FOR INNFO  
   9 5T3G          LDX   0  HTYPE(1) 
   7 5TH6          SMO      6
   8 5W2Q          STO   0  A1(2)
  10 5WGB          LDX   0  FDCELLS+1(3) 
  15 5X22          ADX   0  ASUBASE(1)       [START OF UNIT  
   7 5XFL          SMO      6
   9 5X_=          STO   0  A1+1(2)  
  10 5YDW          LDN   0  FDCELLS+2(3) 
   7 5YYG          SMO      6
   9 5_D6          LDN   1  A1+2(2)  
   7 5_XQ          MOVE  0  3
   7 62CB          NGNC  0  1
   7 62X2          SMO      6
   9 63BL          ANDS  0  A1+3(2)  
  10 63W=          LDX   0  FDCELLS(3)   
   7 64*W          SMO      6
   9 64TG          STO   0  A1+5(2)  
  13 65*6          LDX   2  4               [D.L. PTR
   9 65SQ          STO   2  GEOSCFPTR
   8 66#B          LDCT  0  #400 
  18 66S2          LADDP 2,1                 [ADDITIONAL DEVICE INFO AREA
  18 67?L          ORS   0  BSUNIT2(1)       [MARK IN USE BY TH SYSTEM   
  17 67R=          EXOFILE 3                 [CHAIN INTO EXOFILE LIST
   9 68=W          LDX   4  ASUSN(3) 
  18 68QG          UNLOCKSU 4         [UNLOCK SO OTHER ACTIVITIES CAN USE
   7 69=6          RELST    4
   5 69PQ    ZCOMPL  
   9 6=9B          MHUNT 3,EWDAS,GEXD
   7 6=P2          FREECORE 3
   9 6?8L          MHUNT 1,BSTB,FSTAB
   7 6?N=          FREECORE 1
  10 6#7W          HUNT 3,EWDAS,EWMESS   
   9 6#MG          BNG   3  OPENCOMPL
   7 6*76          FREECORE 3
   6 6*LQ    OPENCOMPL   
   4 6B6B    #   
  13 6BL2    #     OPENING OF THE SCF IS NOW COMPLETE
   4 6C5L    #   
   4 6CK=    #   
   4 6D4W    #   
   9 6DJG          ACROSS BRADDATA,1 
   4 6F46    #   
  12 6FHQ    #     NO NAME GIVEN OR FILE INVALID 
   4 6G3B    #   
   5 6GH2    ZASKNAME
   6 6H2L    #SKI  EMSJNL
   9 6HG=          DEMMESS  FIFSWANT 
   7 6H_W    #SKI  EMSJNL<1$1
  16 6JFG          DEMMESS WANT              [ASK IF ANOTHER FILE
  14 6J_6          HUNT 3,CONBUFF,EMSIN      [FIND REPLY 
   8 6KDQ          BPZ   3  ZOK  
  15 6KYB          GEOERR 1,SPLITF18         [NO REPLY BLOCK 
  14 6LD2    ZOK   LDCH  4  A1+1(3)          [REPLY CHAR 
  16 6LXL          FREECORE 3                [FREE REPLY BLOCK   
   8 6MC=          TXU   4  N(1) 
  17 6MWW          BCC      TIDY             [RELPY NO SO TERMINATE  
   8 6NBG          TXU   4  Y(1) 
  18 6NW6          BCS      ZASKNAME         [REPLY NOT N OR Y SO REPEAT 
   6 6P*Q    #SKI  EMSJNL
   9 6PTB          DEMMESS  FIFSNAME 
   7 6Q*2    #SKI  EMSJNL<1$1
  16 6QSL          DEMMESS NAME              [ASK FOR FILE NAME  
  15 6R#=          HUNT 3,CONBUFF,EMSIN      [GET REPLY BLOCK
   8 6RRW          BPZ   3  NOKY 
  15 6S?G          GEOERR 1,SPLITF19         [NOT REPLY BLOCK
  15 6SR6    NOKY  HUNT 1,BSTB,FSTAB         [OPEN INFO BLOCK
   8 6T=Q          BPZ   1  YOKY 
  15 6TQB          GEOERR 1,SPLITF20         [LOST THE BLOCK 
   8 6W=2    YOKY  LDX   6  ACES 
   9 6WPL          STO   6  FIRSS(1) 
  10 6X9=          STO   6  FIRSS+1(1)   
  15 6XNW          STO   6  FIRSS+2(1)       [SPACE FILL AREA
  14 6Y8G          LDN   4  A1+1(3)          [START ADD  
  13 6YN6          LDN   5  FIRSS(1)         [DEP ADD
  15 6_7Q          LDX   2  A1(3)            [LENGTH IN CHARS
   8 6_MB          MVCH  4  0(2) 
   7 7272          FREECORE 3
   6 72LL    #SKI  EMSJNL
   9 736=    ZATPE DEMMESS  FIFSTYPE 
   7 73KW    #SKI  EMSJNL<1$1
  16 745G    ZATPE DEMMESS TYPE              [ASK FOR DEVICE TYPE
  14 74K6          HUNT 3,CONBUFF,EMSIN      [FIND REPLY 
   8 754Q          BPZ   3  XOKY 
  14 75JB          GEOERR 1,SPLITF21         [LOST REPLY 
  14 7642    XOKY  HUNT 1,BSTB,FSTAB         [OPEN BLOCK 
   8 76HL          BPZ   1  POKY 
  15 773=          GEOERR 1,SPLITF22         [LOST THE BLOCK 
  14 77GW    POKY  LDCH  4  A1+1(3)          [REPLY CHAR 
   7 782G          LDX   6  3
   8 78G6          SMO      FX1  
   8 78_Q          TXU   4  ZTPEX
   8 79FB          BCC      ZCKY 
   8 79_2          SMO      FX1  
   9 7=DL          TXU   4  ZTPEX+1  
   8 7=Y=          BCS      ZATPE
   7 7?CW    ZCKY  BCHX  3  /
   9 7?XG          LDCH  4  A1+1(3)  
  17 7#C6          LDN   2  3                [NUMBER OF VALID REPLIES
   8 7#WQ    NXTT  SMO      FX1  
  13 7*BB          TXU   4  ZTPE-1(2)        [U,S,D  
  15 7*W2          BCC      PFND             [JUMP IF A MATCH
  14 7B*L          BCT   2  NXTT             [TRY NEXT   
  18 7BT=          BRN      ZATPE            [NOT A VALID REPLY ASK AGAIN
   8 7C#W    PFND  SMO      FX1  
  16 7CSG          LDX   4  ZDEV-1(2)        [DEVICE TYPE IN B0:5
  14 7D#6          STO   4  FIRSS+3(1)       [STORE TYPE 
  15 7DRQ          STOZ     FIRSS+4(1)       [NO CSN GIVEN   
  16 7F?B          FREECORE 6                [LOSE REPLY BLOCK   
  17 7FR2          BRN      ZRETRY           [TRY AGAIN TO OPEN SCF  
   4 7G=L    #   
   7 7GQ=    #     TIDY UP   
   4 7H9W    #   
   9 7HPG    TIDY  HUNT 3,EWDAS,GEXD 
   8 7J96          BNG   3  XHA  
   7 7JNQ          FREECORE 3
   9 7K8B    XHA   HUNT 3,BSTB,FSTAB 
   8 7KN2          BNG   3  XHB  
   7 7L7L          FREECORE 3
   9 7LM=    XHB   HUNT 3,EWDAS,EXOF 
   8 7M6W          BNG   3  XHC  
   7 7MLG          FREECORE 3
  10 7N66    XHC   HUNT 3,EWDAS,EWMESS   
   8 7NKQ          BNG   3  XHD  
   7 7P5B          FREECORE 3
   9 7PK2    XHD   HUNT 3,EWDAS,FDRUM
   8 7Q4L          BNG   3  XHE  
   7 7QJ=          FREECORE 3
  10 7R3W    XHE   INFORM 1,BRADSPT1,1   
  16 7RHG          UP                        [NO SCF IN USE NOW  
  13 7S36    #     FILE  NOT SUITABLE FOR FILESTORE  
   9 7SGQ    #     OR FILE USED TWICE
  18 7T2B    #     OUTPUT APPROPRIATE MESSAGE AND GO BACK FOR NEXT FIE   
   4 7TG2    #   
   5 7T_L    S3FAREA 
   8 7WF=          SMO      FX1  
   9 7WYW          LDX   3  XSUITA   
   8 7XDG          BRN      S3AB 
   5 7XY6    S3ASFIC 
   8 7YCQ          SMO      FX1  
   9 7YXB          LDX   3  XSUITB   
   8 7_C2          BRN      S3AB 
   5 7_WL    S3RETN  
   8 82B=          SMO      FX1  
   9 82TW          LDX   3  XSUITC   
   7 83*G          BRN   S3AB
   5 83T6    S3SYFLE 
   8 84#Q          SMO      FX1  
   9 84SB          LDX   3  XSUITD   
   8 85#2          BRN      S3AB 
   5 85RL    S3BUCK  
   8 86?=          SMO      FX1  
   9 86QW          LDX   3  XSUITE   
   4 87=G    #   
   9 87Q6    S3AB  STO   3  AWORK2(2)
   9 889Q          DOWN     FSERR,1  
   9 88PB          BRN      ZASKNAME 
   4 8992    #   
   9 89NL    S4    DOWN     FSERR,2  
   9 8=8=          BRN      ZASKNAME 
   4 8=MW    #   
   4 8?7G    #   
  17 8?M6    #     FAILURE  TO OPEN FILE,OR FAIL WHILST REWRITING FIP
   4 8#6Q    #   
   4 8#LB    #   
   4 8*62    #   
   4 8*KL    #   
   9 8B5=    S5    DOWN     FSERR,3  
  18 8BJW          BRN      ZASKNAME            [UP  FILE NOT AVAILABLE  
  17 8C4G          BRN      U8                  [UPPLUS 1  AVAILABLE 
   4 8CJ6    #   
  11 8D3Q    #     NOT OK REPLY FROM FINDRUM 
   4 8DHB    #   
  11 8F32    S1    TESTREPNOT NOTFOUND,S2A   
   9 8FGL          DOWN     FSERR,8  
  18 8G2=          BRN      ZASKNAME            [UP FILE NOT AVAILABLE   
  17 8GFW          BRN      U8                  [UPPLUS 1 AVAILABLE  
  10 8G_G    S2A   GEOERR   1,SPLITF01   
  11 8HF6    S2    TESTREPNOT   EXFAIL,S2A   
   9 8HYQ          DOWN     FSERR,5  
  18 8JDB          BRN      ZASKNAME            [UP FILE NOT AVAILABLE   
   4 8JY2    #   
  14 8KCL    #     UPPLUS 1 FROM FSERR FILE IS AVAILABLE 
   4 8KX=    #   
   4 8LBW    U8  
  10 8LWG          MHUNTW   1,BSTB,FSTAB 
  10 8MB6          LDX   0  FIRSS+3(1)   
   8 8MTQ          SRL   0  18   
  12 8N*B          SBN   0  6                [EDS
   8 8NT2          BZE   0  T2   
  12 8P#L          SBN   0  7                [EDS
   8 8PS=          BZE   0  T2   
  13 8Q?W          BRN      U9               [DRUMS  
   4 8QRG    #END
   6 ____ ...630165140001
