  16 22FL ...      SEG      SETPG,,CENT(INSTALLATION HOOKS),,G400
   4 22_=    [   
  15 23DW    [ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23YG    [   
   4 24D6    #   
  13 24XQ    #     THIS CHAPTER IS FOR G3PLUS-IH MK2 
   4 25CB    #   
  11 25X2    #          OVERFLOW FROM SETPC  
   4 26BL    #   
  17 26W=          SEGENTRY K1SETPG,XCMS        [SP COMMAND SOURCE   
  16 27*W          SEGENTRY K2SETPG,XGCO        [SP GEORGE CORE  
  17 27TG          SEGENTRY K3SETPG,XGLV        [SP GLOBAL VARIABLE  
  16 28*6          SEGENTRY K4SETPG,XJBV        [SP JOB VARIABLE 
   4 28SQ    #   
  10 29#B    #     SP - COMMAND SOURCE   
   4 29S2    #   
  10 2=?L    XOPSM          12H*OPERATORS
  10 2=DS ...XPROG          8H*PROGRAM   
  10 2=L2 ...XCOM           8H*COMMAND   
   9 2=R=    TEN4           +10000   
   9 2?=W    XMOP           4H*MOP   
   8 2?QG    XCI            4H*CI
   8 2#=6    XCR            4H*CR
   8 2#PQ    XTR            4H*TR
   8 2*9B    X13            #13  
   8 2*P2    X30            #30  
   8 2B8L    XO74           #74  
   8 2BN=    XO77           #77  
   4 2C7W    #   
   4 2CMG    #   
  21 2D76    #     ##################################################################
   4 2DLQ    #   
  11 2F6B    #           K    K            1 
  11 2FL2    #           K   K            11 
  11 2G5L    #           K  K              1 
  11 2GK=    #           K K               1 
  11 2H4W    #           KKK               1 
  11 2HJG    #           K  K              1 
  11 2J46    #           K   K             1 
  11 2JHQ    #           K    K            1 
  11 2K3B    #           K     K           1 
   4 2KH2    #   
  21 2L2L    #     ##################################################################
   4 2LG=    #   
   4 2L_W    XCMS
   4 2MFG    #   
  20 2M_6    #  THIS ROUTINE SETS THE PARAMETER TO THE SOURCE FROM WHICH COMMANDS
  20 2NDQ    #  ARE ISSUED.  IF THE COMAND IS IN A HOOK THE PARAMETER IS SET TO  
  14 2NYB    #  THE SOURCE FROM WHICH THE HOOK WAS ISSUED
   4 2PD2    #   
  19 2PXL                                       [COMDAT TO HOLD 'NEW' PARAM  
  14 2QC=          SETNCORE APARA-A1+10,3,COMDAT,CSETP   
  17 2QWW          STO   3  AWORK1(2)           [SAVE PTR TO COMDAT  
   9 2Q_F ...      LDX   5  EXT+10(2)
  18 2R44 ...      ANDN  5  #7777               [X5 - TOP BREAK-IN LEVEL 
   9 2R6M ...      SEGENTRY K80SETPG 
  17 2R9= ...      TESTHOOK XCMS0               [JUMP IF IN A HOOK   
   9 2R?T ...      SEGENTRY K81SETPG 
   9 2RBG          LDX   0  CPLEV(2) 
   7 2RJQ ...      SBN   0  1
  19 2RR2 ...      BZE   0  XCMS1               [BR IF LEVEL ABOVE CURRENT   
  18 2R_= ...      TXU   5  0                   [LEVEL IS CP LEVEL 0 OR  
  17 2S7G ...      BCC      XCMS1               [TOP BREAK-IN LEVEL  
   8 2S*Q          LDX   3  FX2  
  18 2SG8 ...      HUNT2J   3,CPB,CALAS,,XCMSERR [CALAS FOR SP COMMAND   
  19 2SLL ...      HUNT2J   3,CPB,CALAS,,XCMSERR [CALAS FOR LEVEL ABOVE SP   
   9 2SR4 ...      BRN      XCMS01   
   5 2SXG ...XCMS0   
  10 2T3Y ...      HUNT2   3,CPB,CALAS   
  10 2T8B ...      LDX   7  CPBCPLEV(3)  
   7 2T#S ...      SLL   7  2
   7 2TF= ...      SRL   7  2
   8 2TKN ...      BZE   7  XCMS1
  18 2TQ6 ...      TXU   7  5                    [BR IF NOW AT CP LEVEL 0
  17 2TWJ ...      BCC      XCMS1              [OR TOP BREAKIN LEVEL 
   9 2W32 ...      TESTHKL  XCMS0,3  
   5 2W7D ...XCMS01  
   9 2W#=          LDX   7  CPBFSD(3)
  19 2WRW          BPZ   7  XCMS2A              [J IF SOURCE ANOTHER MACRO   
  10 2W_P ...      LDX   7  CPBCPLEV(3)  
  19 2X7J ...      BPZ   7  XCMSERR              [SOURCE NOT COMMAND OR PROG 
  10 2X*C ...      LDX   7  CONTEXT(2)   
   8 2XH= ...      SLL   7  11   
   9 2XP5 ...      LDN   6  XPROG(1) 
  17 2XWY ...      BNG   7  XCMS02               [PROG ISSUED COMMAND
  18 2Y4R ...      LDN   6  XCOM(1)              [COMMAND ISSUED COMMAND 
   5 2Y=L ...XCMS02  
   8 2YDF ...      LDN   7  APARA
  19 2YL# ...      LDX   3  AWORK1(2)           [X3 POINTS TO COMDAT-CSETP   
   7 2YS7 ...      ADX   7  3
   7 2_22 ...      MOVE  6  2
   7 2_7T ...      LDN   7  8
   9 2_*N ...      STO   7  ANUM(3)  
   8 2_HH ...      BRN      XITA 
   5 2_PL    XCMS2A  
   9 329=          LDX   7  CPBFSD(3)
  17 32NW          ANDN  7  #77                 [ISOLATE FILE HEIGHT 
   8 338G          SMO      FX2  
  17 33N6          LDN   3  FILERING            [FPTR FOR FILERING   
   5 347Q    XCMS2   
  19 34MB          LDX   3  1(3)                [GET NEXT ELEMENT (BACKWARD) 
  20 3572          BCT   7  XCMS2               [GET ELEMENT FOR FILE HEIGHT 1   
   5 35LL    XCMS3   
  17 366=          LDX   2  AWORK1(2)           [LOOK AT CSETP BLOCK 
   8 36KW          ADX   3  2(3) 
  16 375G          LDX   3  BPTR(3)             [X3 POINTS TO FCB
  17 37K6          LDX   1  3                   [X1 HOLDS COPY OF X3 
  13 384Q          LDN   6  #12                 [:   
   9 38JB          DCH   6  APARA(2) 
   7 3942          BCHX  2  /
   9 39HL          LDN   6  FUSER1   
  21 3=3=          CALL  0  XCMV                [MOVE USER TO COMDAT BUT NOT END SPAC
  13 3=GW          LDN   6  #36                 [.   
   9 3?2G          DCH   6  APARA(2) 
   7 3?G6          BCHX  2  /
   8 3?_Q          LDN   6  FLOC1
  21 3#FB          CALL  0  XCMV                [MOVE LOC. NAME TO COMDAT BUT NOT END
   5 3#_2    XCMS7   
  13 3*DL          LDN   6  #30                 [(   
   9 3*Y=          DCH   6  APARA(2) 
   7 3BCW          BCHX  2  /
  13 3BXG          LDX   5  FGEN(3)             [FGN 
   7 3CC6          LDN   6  0
   8 3CWQ          SMO      FX1  
   8 3DBB          DVR   5  TEN4 
  14 3DW2          LDCT  7  #200                [ROUND UP
   7 3F*L          MODE     1
   7 3FT=          LDN   4  4
   5 3G#W    XCMS8   
  19 3GSG          CBD   6  APARA(2)            [STORE FGN(DECIMAL) IN APARA 
   7 3H#6          BCHX  2  /
   8 3HRQ          BCT   4  XCMS8
   7 3J?B          LDN   4  4
  13 3JR2          LDN   6  #37                 [/   
   9 3K=L          DCH   6  APARA(2) 
   7 3KQ=          BCHX  2  /
   5 3L9W    XCMS11  
   9 3LPG          LDCH  6  FLAN(3)  
   9 3M96          DCH   6  APARA(2) 
   7 3MNQ          BCHX  3  /
   7 3N8B          BCHX  2  /
   9 3NN2          BCT   4  XCMS11   
   5 3P7L    XCMS12  
  13 3PM=          LDN   6  #31                 [)   
   9 3Q6W          DCH   6  APARA(2) 
  20 3QLG          LDN   1  37                  [SET ANUM TO NO OF CHARS IN PARAM
   8 3R66          SMO      FX2  
   9 3RKQ          LDX   2  AWORK1   
  17 3S5B          STO   1  ANUM(2)             [STORE CHAR. COUNT   
   8 3SK2          BRN      XITA 
   5 3T4L    XCMS1   
   8 3TJ=          LDX   1  FX1  
   8 3W3W          LDX   2  FX2  
  17 3WHG          LDX   3  AWORK1(2)           [X3 POINTS TO COMDAT 
  10 3X36          LDX   7  JSOURCE1(2)  
  16 3XGQ          SRL   7  18                  [JOBSOURCE=OPS?  
   9 3Y2B          BNZ   7  XCMS1A   
  15 3YG2          LDX   7  XTR(1)              [SOURCE = *TR
   9 3Y_L          BRN      XCMS1X   
   5 3_F=    XCMS1A  
   9 3_YW          TXU   7  JTHREE   
   9 42DG          BCS      XCMS1B   
  13 42Y6          LDX   7  XCR(1)              [*CR 
   9 43CQ          BRN      XCMS1X   
   5 43XB    XCMS1B  
  15 44C2          TXU   7  X13(1)              [MOP ON CBS  
   9 44WL          BCC      XCMS1C   
  14 45B=          TXU   7  X30(1)              [OR MBS  
   9 45TW          BCS      XCMS1D   
   5 46*G    XCMS1C  
   9 46T6          LDX   7  XMOP(1)  
   9 47#Q          BRN      XCMS1X   
   5 47SB    XCMS1D  
   9 48#2          TXU   7  XO74(1)  
   9 48RL          BCS      XCMS1E   
  13 49?=          LDX   7  XCI(1)              [*CI 
   9 49QW          BRN      XCMS1X   
   5 4==G    XCMS1E  
  14 4=Q6          TXU   7  XO77(1)             [J IF YES
   8 4?9Q          BCC      XCOPS
  19 4?PB          LDX   7  JSOURCE1(2)         [ERROR SHOULD NOT GET HERE   
   5 4#92    XCMS1X  
   9 4#NL          STO   7  APARA(3) 
   7 4*8=          LDN   7  4
  17 4*MW          STO   7  ANUM(3)             [SET PARAM COUNT TO 4
  17 4B7G          BRN      XITA                [RETURN TO SETPARAM  
   5 4BM6    XCOPS   
   9 4C6Q          LDN   6  XOPSM(1) 
   8 4CLB          LDN   7  APARA
   7 4D62          ADX   7  3
  19 4DKL          MOVE  6  3                   [MOVE *OPERATORS TO COMDAT   
   8 4F5=          LDN   7  10   
  18 4FJW          STO   7  ANUM(3)             [SET PARAM COUNT TO 1O   
  17 4G4G          BRN      XITA                [RETURN TO SETPARAM  
   4 4GJ6    #   
  12 4H3Q    # THIS ROUTINE MOVES NAMES TO COMDAT
   4 4HHB    #   
   4 4J32    XCMV
   8 4JGL          LDN   4  12   
  19 4K2=          ADX   3  6                   [X3 NOW POINTS TO NAME SOURCE
   5 4KFW    XCMV1   
   8 4K_G          LDCH  6  0(3) 
   9 4LF6          DCH   6  APARA(2) 
   7 4LYQ          BCHX  2  /
   7 4MDB          BCHX  3  /
   8 4MY2          BCT   4  XCMV1
  15 4NCL          LDX   3  1                   [RESTORE X3  
   7 4NX=          EXIT  0  0
   5 4PBW    XCMSERR 
  10 4PWG          GEOERR   1,CMSERROR   
   4 4QB6    #   
   4 4QTQ    #   
   4 4R*B    #   
   4 4RT2    #   
   5 4S#L    #PAGE   
   9 4SS=    #     SP - GEORGE CORE  
   4 4T?W    #   
   9 4TRG    MAGIC          +7036875 
   8 4W?6    MINUS          #35  
  10 4WQQ    SPEC           8H-8388608   
   4 4X=B    #   
   4 4XQ2    #   
   4 4Y9L    XDEC
   8 4YP=          SBX   5  FX1  
   8 4_8W          STO   5  GEN0 
   8 4_NG          SMO      FX2  
  16 5286          LDN   2  AWORK1              [RESULT ADDRESS  
   8 52MQ          STOZ     0(2) 
   8 537B          STOZ     1(2) 
  16 53M2          LDN   7  1                   [INITIALISE COUNT
  17 546L          BPZ   4  POZ                 [J IF RESULT NON-NEG 
  17 54L=          ADN   7  1                   [ELSE UP CT FOR MINUS
   7 555W          NGX   4  4
  16 55KG          BNG   4  SPECIAL             [TREAT #40000000#
   9 5656          LDX   6  MINUS(1) 
  15 56JQ          DCH   6  0(2)                [INSERT MINUS
  14 574B          BCHX  2  /                   [STEP PTR
   4 57J2    POZ 
  17 583L          MPY   4  MAGIC(1)            [CONV TO DL FRACTION 
   7 58H=          LDN   0  7
   7 592W          MODE     0
   4 59GG    XCDR
   8 5=26          CBD   4  0(2) 
   8 5=FQ          LDCH  6  0(2) 
   8 5=_B          BNZ   6  YOKO 
  14 5?F2          BCT   0  XCDR                [DO NEXT 
   8 5?YL          BRN      WEND 
   7 5#D=    ONO   BCHX  2  /
   8 5#XW          CBD   4  0(2) 
  16 5*CG          ADN   7  1                   [UPDATE CHAR CT  
   4 5*X6    YOKO
   8 5BBQ          BCT   0  ONO  
   4 5BWB    WEND
   7 5CB2          LDX   4  7
   7 5CTL          ADN   4  3
   7 5D*=          SRL   4  2
   8 5DSW          BRN      XITDC
   5 5F#G    SPECIAL 
  14 5FS6          LDN   1  SPEC(1)             [-8388608
   7 5G?Q          MOVE  1  2
   7 5GRB          LDN   7  8
   7 5H?2          LDN   4  2
   5 5HQL    XITDC   
   8 5J==          LDX   5  GEN0 
   8 5JPW          ADX   5  FX1  
   7 5K9G          EXIT  5  0
   5 5KP6    XCONV   
   8 5L8Q          SBX   5  FX1  
   8 5LNB          CHNUMCOD 1,3  
  11 5M82          TESTREP  CHNUMERR,XCONV1  
   8 5MML          ADX   5  FX1  
  15 5N7=          EXIT  5  1                   [NORMAL EXIT 
   5 5NLW    XCONV1  
   8 5P6G          ADX   5  FX1  
   7 5PL6          EXIT  5  0
   4 5Q5Q    #   
  20 5QKB    #     ############################################################# 
   4 5R52    #   
  11 5RJL    #           K    K          222 
  11 5S4=    #           K   K          2   2
  11 5SHW    #           K  K               2
  11 5T3G    #           K K                2
  11 5TH6    #           KKK               2 
  11 5W2Q    #           K  K             2  
  11 5WGB    #           K   K           2   
  10 5X22    #           K    K         2
  12 5XFL    #           K     K       2222222   
   4 5X_=    #   
  20 5YDW    #     ############################################################# 
   4 5YYG    #   
   4 5_D6    XGCO
  11 5_XQ          SETNCORE 64,3,COMDAT,CSETP
   9 62CB          STOZ     ANUM(3)  
   7 62X2          LDN   7  2
  19 63BL          PARALYSE #34,,7              [SPLIT PARAM FOLLOWING GCO   
   5 63W=    XGCO1   
  17 64*W          PARANEXT                     [GET NEXT LOCATION   
  10 64TG          MHUNT    3,CPB,CUNI   
   9 65*6          LDX   7  ANUM(3)  
   8 65SQ          BNG   7  XITA 
  16 66#B          BZE   7  XERR                [ERROR IF NULL   
  17 66S2          CALL  5  XCONV               [CONV START TO BIN.  
  15 67?L          BRN      XERR1               [ERROR RETURN
  10 67R=          LDX   7  ACOMMUNE1(2) 
   9 68=W          STO   7  AWORK3(2)
   7 68QG          PARAPASS  
  10 69=6          MHUNT    3,CPB,CUNI   
   9 69PQ          LDX   7  ANUM(3)  
   8 6=9B          BNG   7  XONLY
  16 6=P2          BZE   7  XERR                [ERROR IF NULL   
  17 6?8L          CALL  5  XCONV               [NO. OF LOCS. TO BIN 
  15 6?N=          BRN      XERR1               [ERROR RETURN
  10 6#7W          LDX   7  ACOMMUNE1(2) 
   4 6#MG    #   
  10 6*76    #     X7 HOLDS NO. OF LOCS. 
   4 6*LQ    #   
   8 6B6B          BNG   7  XERR 
   9 6BL2          BRN      XRANGE   
   5 6C5L    XONLY   
   7 6CK=          LDN   7  1
  10 6D4W          STO   7  ACOMMUNE1(2) 
   5 6DJG    XRANGE  
  17 6F46          ADX   7  AWORK3(2)           [ADD IN START LOC.   
   7 6FHQ          SBN   7  2
   8 6G3B          SBX   7  CORES
  18 6GH2          BPZ   7  XERR                [ERROR IF OUT OF RANGE   
   9 6H2L          LDX   7  AWORK3(2)
  18 6HG=          BNG   7  XERR                [ERROR IF START LOC, -VE 
  11 6H_W          HUNT     3,COMDAT,CSETP   
   7 6JFG          NGN   6  1
  17 6J_6          STO   6  AWORK4(2)           [INITIALISE SUPP. MOD
   5 6KDQ    XGCO2   
   8 6KYB          LDX   2  FX2  
  15 6LD2          LDX   6  ACOMMUNE1(2)        [NO. OF LOCS.
   7 6LXL          SBN   6  1
  14 6MC=          BNG   6  XGCO5               [NO MORE 
  10 6MWW          STO   6  ACOMMUNE1(2) 
   9 6NBG          LDX   6  AWORK4(2)
   7 6NW6          ADN   6  1
   9 6P*Q          STO   6  AWORK4(2)
  18 6PTB          ADX   6  AWORK3(2)           [NOW X6 IS ADDR REQUIRED 
   7 6Q*2          SMO      6
   7 6QSL          LDX   4  0
  18 6R#=          CALL  5  XDEC                [GEORGE CORE LOC. IN DEC.
   8 6RRW          LDX   2  FX2  
   7 6S?G          LDX   0  7
   9 6SR6          ADX   0  ANUM(3)  
   7 6T=Q          ADN   0  3
  15 6TQB          SRL   0  2                   [ROUND TO WDS
  15 6W=2          TXL   0  ALOGLEN(3)          [ANY ROOM?   
  13 6WPL          BCS      XGCO3               [Y   
   8 6X9=          ADN   0  32   
  11 6XNW          ALTLEN   3,0,COMDAT,CSETP 
   5 6Y8G    XGCO3   
   9 6YN6          LDX   5  ANUM(3)  
  16 6_7Q          BZE   5  XGCO4               [J IF 1ST TIME   
   7 6_MB          LDN   6  1
   9 7272          ADS   6  ANUM(3)  
   8 72LL          LDN   6  #34  
   7 736=          SRC   5  2
  16 73KW          ADX   3  5                   [BUILD CHAR MOD  
  16 745G          DCH   6  APARA(3)            [SEPERATING COMMA
   7 74K6          BCHX  3  /
   5 754Q    XGCO4   
   9 75JB          ADN   2  AWORK1   
   8 7642          ADN   3  APARA
   7 76HL          SMO      7
   7 773=          MVCH  2  0
  11 77GW          MHUNT    3,COMDAT,CSETP   
   9 782G          ADS   7  ANUM(3)  
   8 78G6          BRN      XGCO2
   5 78_Q    XGCO5   
  10 79FB          MFREE    CPB,CMULTI   
   9 79_2          MFREE    CPB,CUNI 
   9 7=DL          VFREE    CPB,CUNI 
   8 7=Y=          BRN      XGCO1
   4 7?CW    #   
   4 7?XG    #   
   5 7#C6    #PAGE   
   4 7#WQ    #   
  12 7*BB    #      SP --- GLOBAL & JOB VARIABLES
   4 7*W2    #   
  15 7B*L    #DEF VLOCK=A1+1                    [LOCKOUT WORD
  21 7BT=    #DEF VDATALEN=A1+2                 [POINTER TO END OF USED SPACE,RELATIV
  13 7C#W    #                         ENTRY FORMAT  
  15 7CSG    #DEF VHEADER=0                     [ENTRY HEADER
  17 7D#6    #DEF REDTAPE2=1                    [2ND WORD OF RED TAPE
  17 7DRQ    #DEF VNAME=2                       [3 WORD VARIABLE NAME
  15 7F?B    #DEF VDATA=5                       [VALUE DATA  
   4 7FR2    #   
   4 7G=L    #   
  20 7GQ=    #     ############################################################# 
   4 7H9W    #   
  11 7HPG    #           K    K         3333 
  11 7J96    #           K   K         3    3
  11 7JNQ    #           K  K               3
  11 7K8B    #           K K                3
  11 7KN2    #           KKK              33 
  11 7L7L    #           K  K               3
  11 7LM=    #           K   K              3
  11 7M6W    #           K    K        3    3
  11 7MLG    #           K     K        3333 
   4 7N66    #   
  20 7NKQ    #     ############################################################# 
   4 7P5B    #   
   4 7PK2    XGLV
   7 7Q4L          LDN   4  2
  15 7QJ=          CALL  7  XHNT1               [DECODE NAME 
  15 7R3W          CHECKLFN2 XERR,XERR,3        [CHECK FORMAT
  10 7RHG          MHUNT    3,CPB,CUNI   
  18 7S36          LDX   0  ACES                [SPACE-FILL AWORK AREA   
   9 7SGQ          STO   0  AWORK1(2)
   9 7T2B          STO   0  AWORK2(2)
   9 7TG2          STO   0  AWORK3(2)
  15 7T_L          LDN   5  APARA(3)            [FROM ADDRESS
  15 7WF=          LDN   6  AWORK1(2)           [TO ADDRESS  
   9 7WYW          SMO      ANUM(3)  
  15 7XDG          MVCH  5  0                   [MOVE IN NAME
   5 7XY6    XGLV3   
  15 7YCQ          CALL  7  SLOCGV              [LOCATE ENTRY
  15 7YXB          BNG   3  XITN                [NULL RESULT 
  14 7_C2          LDX   5  VHEADER(3)          [HEADER  
  17 7_WL          SBN   5  VDATA-VHEADER       [-RED TAPE+NAME AREA 
  17 82B=          BZE   5  XITN                [NO DATA-NULL RESULT 
   9 82TW          ADN   5  APARA-A1 
  11 83*G          SETUPCORE 5,1,COMDAT,CSETP
   9 83T6          SBN   5  APARA-A1 
  16 84#Q          CALL  7  SLOCGV              [RELOCATE ENTRY  
  16 84SB          BPZ   2  XGLV1               [J IF NOT NULL   
  15 85#2          STOZ     ANUM(1)             [SET O/P NULL
  14 85RL          BRN      XITA                [AND EXIT
   5 86?=    XGLV1   
  10 86QW          LDX   4  VHEADER(3)   
  16 87=G          SBN   4  VDATA-VHEADER       [LENGTH OF DATA  
   7 87Q6          TXL   5  4
  18 889Q          BCC      XGLV2               [J IF BLOCK BIG ENOUGH   
  15 88PB          FREECORE 1                   [ELSE FREE IT
  16 8992          BRN      XGLV3               [AND TRY AGAIN   
   5 89NL    XGLV2   
  16 8=8=          SLL   4  2                   [LENGTH IN WORDS 
  15 8=MW          LDCH  0  REDTAPE2(3)         [SPARE CHARS 
   7 8?7G          SBX   4  0
  15 8?M6          STO   4  ANUM(1)             [STORE COUNT 
  16 8#6Q          LDN   2  VDATA(3)            [START OF DATA   
  17 8#LB          LDN   3  APARA(1)            [O/P BLOCK ADDRESS   
   7 8*62          SMO      4
  15 8*KL          MVCH  2  0                   [MOVE IN DATA
  16 8B5=          BZE   0  XGLV4               [J IF NO SPARE   
   8 8BJW          LDN   2  ACES 
   7 8C4G          SMO      0
  16 8CJ6          MVCH  2  0                   [FILL WITH SPACES
   5 8D3Q    XGLV4   
  14 8DHB          BRN      XITA                [AND EXIT
   5 8F32    SLOCGV  
  15 8FGL          DOFINDGV AWORK1(2),3         [LOCATE ENTRY
   7 8G2=          EXIT  7  0
   5 8GFW    #PAGE   
   4 8G_G    #   
  20 8HF6    #     ############################################################# 
   4 8HYQ    #   
  11 8JDB    #           K    K            4 
  11 8JY2    #           K   K            44 
  11 8KCL    #           K  K            4 4 
  11 8KX=    #           K K            4  4 
  11 8LBW    #           KKK           4   4 
  11 8LWG    #           K  K         4444444
  11 8MB6    #           K   K             4 
  11 8MTQ    #           K    K            4 
  11 8N*B    #           K     K           4 
   4 8NT2    #   
  20 8P#L    #     ############################################################# 
   4 8PS=    #   
   4 8Q?W    XJBV
   7 8QRG          LDN   4  2
  15 8R?6          CALL  7  XHNT1               [DECODE NAME 
  15 8RQQ          CHECKLFN2 XERR,XERR,3        [CHECK FORMAT
  10 8S=B          MHUNT    3,CPB,CUNI   
  18 8SQ2          LDX   0  ACES                [SPACE-FILL AWORK AREA   
   9 8T9L          STO   0  AWORK1(2)
   9 8TP=          STO   0  AWORK2(2)
   9 8W8W          STO   0  AWORK3(2)
  15 8WNG          LDN   5  APARA(3)            [FROM ADDRESS
  15 8X86          LDN   6  AWORK1(2)           [TO ADDRESS  
   9 8XMQ          SMO      ANUM(3)  
  15 8Y7B          MVCH  5  0                   [MOVE IN NAME
   5 8YM2    XJBV3   
  15 8_6L          CALL  7  SLOCJV              [LOCATE ENTRY
  15 8_L=          BNG   3  XITN                [NULL RESULT 
  14 925W          LDX   5  VHEADER(3)          [HEADER  
  17 92KG          SBN   5  VDATA-VHEADER       [-RED TAPE+NAME AREA 
  17 9356          BZE   5  XITN                [NO DATA-NULL RESULT 
   9 93JQ          ADN   5  APARA-A1 
  11 944B          SETUPCORE 5,1,COMDAT,CSETP
   9 94J2          SBN   5  APARA-A1 
  16 953L          CALL  7  SLOCJV              [RELOCATE ENTRY  
  16 95H=          BPZ   2  XJBV1               [J IF NOT NULL   
  15 962W          STOZ     ANUM(1)             [SET O/P NULL
  14 96GG          BRN      XITA                [AND EXIT
   5 9726    XJBV1   
  10 97FQ          LDX   4  VHEADER(3)   
  16 97_B          SBN   4  VDATA-VHEADER       [LENGTH OF DATA  
   7 98F2          TXL   5  4
  18 98YL          BCC      XJBV2               [J IF BLOCK BIG ENOUGH   
  15 99D=          FREECORE 1                   [ELSE FREE IT
  16 99XW          BRN      XJBV3               [AND TRY AGAIN   
   5 9=CG    XJBV2   
  16 9=X6          SLL   4  2                   [LENGTH IN WORDS 
  15 9?BQ          LDCH  0  REDTAPE2(3)         [SPARE CHARS 
   7 9?WB          SBX   4  0
  15 9#B2          STO   4  ANUM(1)             [STORE COUNT 
  16 9#TL          LDN   2  VDATA(3)            [START OF DATA   
  17 9**=          LDN   3  APARA(1)            [O/P BLOCK ADDRESS   
   7 9*SW          SMO      4
  15 9B#G          MVCH  2  0                   [MOVE IN DATA
  16 9BS6          BZE   0  XJBV4               [J IF NO SPARE   
   8 9C?Q          LDN   2  ACES 
   7 9CRB          SMO      0
  16 9D?2          MVCH  2  0                   [FILL WITH SPACES
   5 9DQL    XJBV4   
  14 9F==          BRN      XITA                [AND EXIT
   5 9FPW    SLOCJV  
  15 9G9G          DOFINDJV AWORK1(2),3         [LOCATE ENTRY
   7 9GP6          EXIT  7  0
   4 9H8Q    #   
   5 9HNB    XHNT1   
   8 9J82          SBX   7  FX1  
   8 9JML          SPARANXT ,,4  
   8 9K7=          BRN      XHNT2
   4 9KLW    XHNT
   8 9L6G          SBX   7  FX1  
   8 9LL6          PARANEXT ,,4  
   5 9M5Q    XHNT2   
  10 9MKB          TESTREP   UNPAIR,XERR 
   8 9N52          ADX   7  FX1  
  10 9NJL          HUNT     3,CPB,CUNI   
   7 9P4=          EXIT  7  0
   4 9PHW    XITN
   7 9Q3G          LDN   4  0
   7 9QH6          LDN   7  0
   9 9R2Q          LDN   5  AWORK1   
   4 9RGB    XITC
   7 9S22          ADN   4  2
  15 9SFL          SETUPCORE 4,3,COMDAT,CSETP   [PARAM BLK   
   9 9S_=          STO   7  ANUM(3)  
   8 9TDW          BZE   7  XITA 
   7 9TYG          SMO      5
   8 9WD6          LDN   1  0(2) 
   9 9WXQ          LDN   2  APARA(3) 
   7 9XCB          SMO      7
  17 9XX2          MVCH  1  0                   [PARAM TO BE INSERTED
   7 9YBL          SBN   4  2
   7 9YW=          SLL   4  2
   7 9_*W          SBX   4  7
   8 9_TG          BZE   4  XITA 
   8 =2*6          LDN   1  ACES 
   7 =2SQ          SMO      4
   7 =3#B          MVCH  1  0
   4 =3S2    XITA
  13 =4?L          UP                           [OK  
   4 =4R=    #   
   4 =5=W    XERR
  18 =5QG          UPPLUS   1                   [ PARAMETER FORMAT ERROR 
   4 =6=6    #   
   5 =6PQ    XERR1   
  17 =79B          UPPLUS   2                   [NUMBER FORMAT ERROR 
   4 =7P2    #   
   4 =88L    #   
   4 =8N=    #END
   6 ____ ...253767110004
