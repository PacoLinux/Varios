  14 22FL ...      SEG SCHEDMTA,860,C.R.JOHNSON,SCHEDULER
   4 22_= ...[   
   4 23DW ...[   
  12 23YG ...      SEGENTRY K1SCHEDMTA,X1SCHEDMTA
  12 24D6 ...      SEGENTRY K2SCHEDMTA,X2SCHEDMTA
  12 24XQ ...      SEGENTRY K3SCHEDMTA,X3SCHEDMTA
  12 25CB ...      SEGENTRY K4SCHEDMTA,X4SCHEDMTA
   4 25X2 ...[   
   4 26BL ...[   
  19 26W= ...[     THIS SEGMENT IS AN OVERFLOW FOR SCHEDMTX (THE RETRIEVAL   
  19 27*W ...[     SYSTEM SCHEDULING SEGMENT) AND DEALS WITH CERTAIN ASPECTS 
  18 27TG ...[     OF RETRIEVEING FILES FROM ALIEN FILESTORE DUMP TAPES. 
   4 28*6 ...[   
   4 28SQ ...[   
  18 29#B ...      SEGENTRY K99SCHEDMTA         [THIS ENTRY POINT IS USED
  18 29S2 ...               +7                  [BY THE 'ALIENDIR' MACRO.
  16 2=?L ...               12HALIENFS          [DEFAULT USERNAME
  17 2=R= ...               12HINCINDEX         [MUST BE 'INCINDEX'  
  15 2?=W ...               +1                  [REEL NUMBER 
  17 2?QG ...               +0                  [ALLOW ANY GENERATION
  16 2#=6 ...               +0                  [AND LANGUAGE.   
   4 2#PQ ...[   
   4 2*9B ...[   
  20 2*P2 ...TEN            +10                 [USED TO CONVERT BINARY TO DEC.  
  20 2B8L ...XINCMISS       +JINCMISS           [MESSAGE PARAMETER IDENTIFIERS   
  12 2BN= ...XINCOFF        +JINCOFF            [
  12 2C7W ...XINCCORR       +JINCCORR           [
  12 2CMG ...XINCWRO        +JINCWRO            [
  12 2D76 ...XINCMSO        +JINCMSO            [
   4 2DLQ ...[   
  21 2F6B ...[   ******************************************************************  
   4 2FL2 ...[   
  21 2G5L ...[     THIS SUBROUTINE SETS UP PARAMETERS FOR AND OUTPUTS THE MESSAGE:-  
  16 2GK= ...[     'SOME RETRIEVES ARE FAILING BECAUSE %B IS %A' 
  15 2H4W ...[          X6 CONTAINS THE %A MESSAGE IDENTIFIER
   4 2HJG ...[   
   5 2J46 ...RVFAIL  
  16 2JHQ ...      SBX   7  FX1                 [RELATIVIZE LINK 
  20 2K3B ...      JBS      XFABFREE,BOLC,BIFDTRVF [J IF MESSAGE ALREADY OUTPUT  
  15 2KH2 ...      OUTMESSX 6                   [OUTPUT %A   
  17 2L2L ...      SETNCORE 11,3,ADATA,CREADL   [SET UP FOR UNNORM   
  19 2LG= ...      STOZ     A1(3)               [CHARACTER COUNT ZREO AS YET 
  17 2L_W ...      UNNORM   TWO                 [CONVERT ENTRANT NAME
  17 2MFG ...      MHUNT    3,ADATA,CREADL      [UNNORM COORDINATES  
  19 2M_6 ...      OUTPARAM A1(3),CPDATA,ADATA,CREADL [OUTPUT %B OF MESSAGE  
  16 2NDQ ...      MFREE    ADATA,CREADL        [NO LONGER NEEDED
  16 2NYB ...      MONOUT   JRVFAIL             [OUTPUT MESSAGE  
  18 2PD2 ...      SEGENTRY K98SCHEDMTA         [USED BY TSRPALL MACRO   
  21 2PXL ...      BS       BOLC,BIFDTRVF       [SET 'RV FAILED MESSAGE OUTPUT' BIT  
  18 2QC= ...      SEGENTRY K97SCHEDMTA         [USED BY TSRPALL MACRO   
  12 2QWW ...XFABFREE                           [
  15 2RBG ...      MFREE    FILE,FABSNB         [FREE FABSNB 
  15 2RW6 ...      ADX   7  FX1                 [RESET LINK  
  13 2S*Q ...      EXIT  7  0                   [EXIT
   4 2STB ...[   
  21 2T*2 ...[   ******************************************************************* 
   4 2TSL ...[   
  18 2W#= ...[     THIS SUBROUTINE CHECKS THAT THE TSN CONTAINED IN X6 IS
  19 2WRW ...[     SECURE (IN :SYSTEM.SERIAL). IF NOT REPLY 'OPWRONG' IS SET.
   4 2X?G ...[   
  12 2XR6 ...TESTSECURE                         [
  16 2Y=Q ...      SBX   7  FX1                 [RELATIVIZE LINK 
  17 2YQB ...      OPENSYS  ,SERIAL,READ        [OPEN :SYTEM.SERIAL  
  17 2_=2 ...      SSEARCH  XNOTSECURE,6        [J IF TSN NOT SECURE 
  15 2_PL ...      CLOSETOP                     [CLOSE SERIAL
  15 329= ...      ADX   7  FX1                 [RESET LINK  
  15 32NW ...      EXIT  7  1                   [   SECURE   
  12 338G ...XNOTSECURE                         [
  15 33N6 ...      CLOSETOP                     [CLOSE SERIAL
  20 347Q ...      SETREP2  OPWRONG             [SET REPLY FOR SCHEDMTX & DTSTART
  15 34MB ...      ADX   7  FX1                 [RESET LINK  
  15 3572 ...      EXIT  7  0                   [   INSECURE 
   4 35LL ...[   
  21 366= ...[   ******************************************************************* 
   4 36KW ...[   
  19 375G ...[     THIS SUBROUTINE CHECKS THAT THE RECORD POINTED TO BY X3   
  20 37K6 ...[     IS A VALID INCINDEX RECORD, I.E. THAT THE FOLLOWING IS TRUE:  
   4 384Q ...[   
  14 38JB ...[        [AINCRH] = AMAGR + [AMTS]*INCMAGLEN
   4 3942 ...[   
   7 39HL ...[     LINK IS X7
   4 3=3= ...[   
  14 3=GW ...[     X3 -> INCINDEX RECORD(RECORD HEADER)  
   4 3?2G ...[   
  12 3?G6 ...RECCHECK                           [
  17 3?_Q ...      LDX   5  AMTSR(3)            [X5 => NO OF TAPES   
  19 3#FB ...      LDN   4  INCMAGLEN           [X4 => LENGTH OF TAPE ENTRY  
  19 3#_2 ...      MPY   5  4                   [X6 => LENGTH OF TAPE ENTRIES
  18 3*DL ...      ADN   6  AMAGR               [X6 => LENGTH OF RECORD? 
  20 3*Y= ...      LDEX  5  AINCRHR(3)          [X5 => LENGTH OF RECORD HEADER   
  21 3BCW ...      BXU   5  6,EXIT1             [J IF TAPE ENTRIES OVERFLOW RECORD   
  18 3BXG ...      SBN   5  AMAGR               [RECORD MUST BE > AMAGR-1
  18 3CC6 ...      BNG   5  EXIT1               [J IF RECORD TOO SHORT   
  15 3CWQ ...      EXIT  7  1                   [VALID RECORD
  12 3DBB ...EXIT1                              [
  16 3DW2 ...      EXIT  7  0                   [INVALID RECORD  
   4 3F*L ...[   
   4 3FT= ...[   
  21 3G#W ...[   ******************************************************************  
   4 3GSG ...[   
  19 3H#6 ...[     THIS ENTRY POINT IS ENTERED DOWN FROM SCHEDMTX IN ORDER   
  19 3HRQ ...[     TO ATTEMPT TO OPEN THE ALIEN FILESTORE INCINDEX (FILESTORE
  17 3J?B ...[     NUMBER SPECIFIED IN X6), POSITION ON THE RELEVENT 
  18 3JR2 ...[     INCREMENT (SPECIFIED IN X4) AND TO PERFORM SUFFICIENT 
  18 3K=L ...[     VALIDITY CHECKING SO THAT THE RETRIEVAL SYSTEM WILL   
   8 3KQ= ...[     NOT BREAK!!   
   4 3L9W ...[   
  12 3LPG ...X1SCHEDMTA                         [
  20 3M96 ...      DVS   5  TEN(1)              [CONVERT FILESTORE NUMBER TO DEC.
  19 3MNQ ...      LDN   7  2                   [X7 => COUNT OF REL. CHARS.  
  19 3N8B ...      SLL   6  18                  [QUOTIENT IN TOP CHAR POSIT. 
  19 3NN2 ...      SLL   5  12                  [REMAINDER IN SECOND POSIT.  
  16 3P7L ...      BNZ   6  SETUPFAB            [J IF TWO CHARS  
  17 3PM= ...      SBN   7  1                   [ONLY ONE CHARACTER  
  19 3Q6W ...      SLL   5  6                   [REMAINDER IN TOP POSITION   
  12 3QLG ...SETUPFAB                           [
  18 3R66 ...      ORX   6  5                   [X6 => FILESTORE IN CHARS
  15 3RKQ ...      BZE   6  (GEOERR)            [X6 ILLEGAL  
  16 3S5B ...      SETNCORE 10,3,FILE,FABSNB    [FOR OPEN MACRO  
  16 3SK2 ...      STO   6  ACOMMUNE1(2)        [FILESTORE NUMBER
  16 3T4L ...      LDN   0  10                  [RECORD HEADER   
  14 3TJ= ...      STO   0  A1(3)               [STORE IT
  16 3W3W ...      LDX   0  ACES                [X0 => #20202020 
  17 3WHG ...      STO   0  A1+1(3)             [PUT SPACES IN FABSNB
  16 3X36 ...      LDN   0  A1+1(3)             [NOW MOVE SPACES 
  15 3XGQ ...      LDN   1  A1+2(3)             [DOWN FABSNB 
  15 3Y2B ...      MOVE  0  5                   [ONLY FIVE   
  14 3YG2 ...      LDX   1  FX1                 [RESET X1
  18 3Y_L ...      LDX   0  K99SCHEDMTA(1)      [X0 => LENGTH OF USERNAME
  16 3_F= ...      BZE   0  (GEOERR)            [MUSN'T BE ZERO  
  16 3_YW ...      LDN   1  K99SCHEDMTA+1(1)    [X1 -> USERNAME  
  19 42DG ...      LDN   2  A1+1(3)             [A1+1 -> USERNAME IN FABSNB  
  16 42Y6 ...      SMO      0                   [LENGTH OF MOVE  
  17 43CQ ...      MVCH  1  0                   [USERNAME MOVED IN   
  12 43XB ...      LDX   1  FX2                 [
  18 44C2 ...      LDN   1  ACOMMUNE1(1)        [X1 -> FILESTORE NUMBER  
  15 44WL ...      SMO      7                   [X7 => 1 OR 2
  18 45B= ...      MVCH  1  0                   [MOVE IN FILESTORE NUMBER
  14 45TW ...      LDX   1  FX1                 [RESET X1
  16 46*G ...      LDN   2  K99SCHEDMTA+4(1)    [X2 -> LOCAL NAME
  19 46T6 ...      LDN   3  A1+4(3)             [X3 -> LOCAL NAME IN FABSNB  
  18 47#Q ...      MOVE  2  6                   [MOVE IN 'INCINDEX' ETC. 
   4 47SB ...[   
   9 48#2 ...[     NOW OPEN INCINDEX,
   4 48RL ...[   
  13 49?= ...      OPEN  (GEOERR),READ,QUERY,NOWAIT  
   4 49QW ...[   
   9 4==G ...[     AND TEST REPLIES. 
   4 4=Q6 ...[   
  16 4?9Q ...      TESTREP2 OK,XCHECK,LOST,XMISSING,MAG,XOFFLINE 
  19 4?PB ...      TESTREP2 NOFILE,XMISSING,NOUSER,XMISSING,VRYWRONG,XWRONG  
  10 4#92 ...      TESTREP2 WRONG,XWRONG 
  16 4#NL ...      GEOERR   1,ALIENFS           [UNEXPECTED REPLY
  12 4*8= ...XCHECK                             [
   4 4*MW ...[   
  12 4B7G ...[     X4 CONTAINS INCREMENT NUMBER  
  13 4BM6 ...[     SEARCH FOR THIS RECORD IN INCINDEX
   4 4C6Q ...[   
  19 4CLB ...      BS       BOLC,BIFDTOPEN      [SET THE 'INCINDEX OPEN' BIT 
  18 4D62 ...      FINDREC ,AINCNOR,4,XCORRUPT  [J IF RECORD NOT FOUND   
  18 4DKL ...      READAGAIN                    [INCINDEX RECORD FOUND   
  18 4F5= ...      MHUNTW   3,FILE,FRB          [X3 -> INCINDEX RECORD   
  17 4FJW ...      ADN   3  A1                  [SET UP FOR RECCHECK 
  21 4G4G ...      CALL  7  RECCHECK            [CHECK FOR INVALID INCINDEX RECORD   
  16 4GJ6 ...      BRN      XCORRUPT            [INVALID RECORD  
  17 4H3Q ...      LDX   5  AMTSR(3)            [X5 => NO OF TAPES   
  16 4HHB ...      BZE   5  XCORRUPT            [J IF NO TAPES   
  18 4J32 ...      LDEX  2  AINCRHR(3)          [X2 => LENGTH OF RECORD  
  19 4JGL ...      SBN   2  AMAGR               [X2 => LENGTH OF TAPE ENTRIES
  17 4K2= ...      BZE   2  XCORRUPT            [J IF NO TAPE ENTRIES
  12 4KFW ...XLOOP                              [
  14 4K_G ...      SBN   2  INCMAGLEN           [SET X2  
  12 4LF6 ...      SMO      2                   [
  15 4LYQ ...      LDX   0  AMAGR(3)            [X0 => TSN   
  18 4MDB ...      BPZ   0  UP                  [J IF POSITIVE TSN FOUND 
  20 4MY2 ...      BNG   2  XCORRUPT            [J IF NONEVEN TAPE ENTRY LENGTH  
  18 4NCL ...      BNZ   2  XLOOP               [J IF MORE TAPE ENTRIES  
  19 4NX= ...      BRN      XCORRUPT            [J IF ALL TSNS ARE NEGATIVE  
  12 4PBW ...UP                                 [
  14 4PWG ...      UP                           [GO UP   
  19 4QB6 ...XMISSING                           [ALIEN INCINDEX IS MISSING   
  21 4QTQ ...      LDX   6  XINCMISS(1)         [X6 => MESSAGE IDENTIFIER FOR RVFAIL 
  16 4R*B ...      CALL  7  RVFAIL              [OUTPUT MESSAGE  
  12 4RT2 ...      BRN      XPERMLST            [
  20 4S#L ...XWRONG                             [ALIEN INCINDEX IS OF WRONG TYPE 
  21 4SS= ...      LDX   6  XINCWRO(1)          [X6 => MESSAGE IDENTIFIER FOR RVFAIL 
  16 4T?W ...      CALL  7  RVFAIL              [OUTPUT MESSAGE  
  12 4TRG ...      BRN      XPERMLST            [
  20 4W?6 ...XMISSORDER                         [ALIEN INCINDEX IS MISSORDERED   
  12 4WQQ ...      MFREEW   FILE,FDINCS         [
  21 4X=B ...      LDX   6  XINCMSO(1)          [X6 => MESSAGE IDENTIFIER FOR RVFAIL 
  16 4XQ2 ...      CALL  7  RVFAIL              [OUTPUT MESSAGE  
  12 4Y9L ...      BRN      XPERMLST            [
  12 4YP= ...XFREEFDINC                         [
  17 4_8W ...      MFREEW   FILE,FDINCS         [FREE FDINCS BLOCK   
  19 4_NG ...XCORRUPT                           [ALIEN INCINDEX IS CORRUPT   
  21 5286 ...      LDX   6  XINCCORR(1)         [X6 => MESSAGE IDENTIFIER FOR RVFAIL 
  16 52MQ ...      CALL  7  RVFAIL              [OUTPUT MESSAGE  
  18 537B ...XPERMLST                           [FILE IS PERMENTLY LOST  
  12 53M2 ...      LDX   2  FX2                 [
  21 546L ...      MBSXD    7,BPERLOST,BTERMST  [MASK FOR PERMENTLY LOST INCREMENT   
  17 54L= ...      STO   7  ACOMMUNE1(2)        [STORE FOR SCHEDMTX  
  12 555W ...      BRN      UPPLUS1             [
  19 55KG ...XOFFLINE                           [ALIEN INCINDEX IS OFFLINE   
  21 5656 ...      LDX   6  XINCOFF(1)          [X6 => MESSAGE IDENTIFIER FOR RVFAIL 
  16 56JQ ...      CALL  7  RVFAIL              [OUTPUT MESSAGE  
  18 574B ...XTEMPLST                           [FILE IS TEMPORARILY LOST
  12 57J2 ...      LDX   2  FX2                 [
  20 583L ...      MBSXD    7,BTEMLOST,BTERMST  [MASK FOR TEMPORARILY LOST FILE  
  17 58H= ...      STO   7  ACOMMUNE1(2)        [STOR FOR SCHEDMTX   
  12 592W ...UPPLUS1                            [
  15 59GG ...      UPPLUS   1                   [GO UP + 1   
   4 5=26 ...[   
  21 5=FQ ...[   ******************************************************************  
   4 5=_B ...[   
  18 5?F2 ...[    THIS ENTRY POINT SETS UP A FDINCS BLOCK FOR AN ALIEN   
  19 5?YL ...[     TAPE WHOSE TSN IS SPECIFIED IN AWORK2 (FILESTORE NUMBER   
   7 5#D= ...[     IS IN X6).
   4 5#XW ...[   
  12 5*CG ...X2SCHEDMTA                         [
  17 5*X6 ...      SETNCORE 7,3,FILE,FDINCS     [X3 -> FDINCS BLOCK  
  17 5BBQ ...      STOZ     AWORK1(2)           [SET FIRST INC ZERO  
  19 5BWB ...      STOZ     FDINCNO(3)          [SET COUNT OF ENTRIES ZERO   
  15 5CB2 ...      LDX   0  AWORK2(2)           [X0 => TSN   
  17 5CTL ...      STO   0  FDINCTSN(3)         [STORE TSN IN FDINCS 
  17 5D*= ...      BZE   6  (GEOERR)            [X6 SHOULDN'T BE ZERO
  19 5DSW ...      STOZ     FDINCWOR(3)         [SET FDINC STATE WORD ZERO   
  18 5F#G ...      STF      3,FDINCFSNO,6       [STORE FILESTORE NUMBER  
  20 5FS6 ...      REWIND                       [POSITION AT START OF INCINDEX   
  12 5G?Q ...T1MTA                              [
  17 5GRB ...      STEP                         [X3 -> NEXT RECORD   
  17 5H?2 ...      BZE   3  T4MTA               [J IF END OF INCINDEX
  20 5HQL ...      CALL  7  RECCHECK            [CHECK VALIDITY OF THIS RECORD   
  17 5J== ...      BRN      XFREEFDINC          [J IF INVALID RECORD 
  18 5JPW ...      LDEX  2  AINCRHR(3)          [X2 => LENGTH OF RECORD  
  19 5K9G ...      SBN   2  AMAGR               [X2 => LENGTH OF TAPE ENTRIES
  17 5KP6 ...      BZE   2  T1MTA               [J IF NO TAPE ENTRIES
  16 5L8Q ...      SMO      FX2                 [X2 IS CORRUPT   
  15 5LNB ...      LDX   0  AWORK2              [X0 => TSN   
  12 5M82 ...T2MTA                              [
  14 5MML ...      SBN   2  INCMAGLEN           [SET X2  
  12 5N7= ...      SMO      2                   [
  21 5NLW ...      BXE   0  AMAGR(3),T3MTA      [J IF TSN FOUND IN INCINDEX RECORD   
  20 5P6G ...      BNG   2  XCORRUPT            [J IF NONEVEN TAPE ENTRY LENGTH  
  17 5PL6 ...      BNZ   2  T2MTA               [J IF MORE ENTRIES   
  16 5Q5Q ...      BRN      T1MTA               [READ NEXT RECORD
  16 5QKB ...T3MTA                              [TAPE ENTRY FOUND
  16 5R52 ...      ADX   2  3                   [X2 -> TAPE ENTRY
  18 5RJL ...      LDX   7  AINCNOR(3)          [X7 => INCREMENT NUMBER  
  16 5S4= ...      BNG   7  XFREEFDINC          [J IF NEG INC NO 
  12 5SHW ...      SMO      FX2                 [
  17 5T3G ...      LDX   5  AWORK1              [X5 => PREVIOS INC   
  19 5TH6 ...      BXL   7  5,XMISSORDER        [J IF INCINDEX MISSORDERED   
  17 5W2Q ...      BXE   7  5,XFREEFDINC        [J IF INC NOS EQAL   
  12 5WGB ...      SMO      FX2                 [
  19 5X22 ...      STO   7  AWORK1              [STORE THIS INCREMENT NMBER  
  15 5XFL ...      MHUNTW   2,FILE,FDINCS       [X2 -> FDINCS
  20 5X_= ...      LDX   3  FDINCNO(2)          [X3 => NUMBER OF ENTRIES SO FAR  
  12 5YDW ...      LDN   0  1                   [
  19 5YYG ...      ADS   0  FDINCNO(2)          [INCREASE ENTRY COUNT BY ONE 
  12 5_D6 ...      SMO      2                   [
  20 5_XQ ...      STO   7  FDINCINC(3)         [INCREMENT NUMBER FOR THIS ENTRY 
  16 62CB ...      ADN   3  4                   [INCREASE LENGTH?
  17 62X2 ...      BXL   3  ALOGLEN(2),T1MTA    [J IF BLOCKSIZE OK   
  12 63BL ...      LDX   6  2                   [
  19 63W= ...      ADN   3  8                   [INCREASE LENGTH BY 8 ENTRIES
  17 64*W ...      ALTLEN   6,3,FILE,FDINCS     [INCREASE BLOCKSIZE  
  15 64TG ...      BRN      T1MTA               [START AGAIN 
  16 65*6 ...T4MTA                              [FDINCS SET UP OK
  21 65SQ ...      MFREE    FILE,FABSNB         [KEPT TILL NOW FOR ERROR REPORTING   
  12 66#B ...      BRN      UP                  [
   4 66S2 ...[   
  21 67?L ...[   ******************************************************************  
   4 67R= ...[   
  19 68=W ...[     THIS ENTRY POINT IS USED TO OPEN AN ALIEN FILESTORE TAPE  
  19 68QG ...[     IF IT IS FOUND TO BE SECURE - ENTERED DOWN FROM SCHEDMTX  
   4 69=6 ...[   
  10 69PQ ...[          X6 => TSN OF TAPE
   4 6=9B ...[   
  12 6=P2 ...X3SCHEDMTA                         [
  19 6?8L ...      CALL  7  TESTSECURE          [IS TSN SECURE (IN SERIAL)?  
  16 6?N= ...      BRN      UPPLUS1             [J IF INSECURE   
  15 6#7W ...      TAPEOPEN (GEOERR),6,,INFORM,NOTOL,REPLY   
  14 6#MG ...      BRN      UP                  [GO UP   
   4 6*76 ...[   
  21 6*LQ ...[   ******************************************************************  
   4 6B6B ...[   
  19 6BL2 ...[     THIS ENTRY POINT IS USED TO OPEN AN ALIEN FILESTORE TAPE  
  19 6C5L ...[     IF IT IS FOUND TO BE SECURE - ENTERED DOWN FROM DTSTART   
   4 6CK= ...[   
  10 6D4W ...[          X4 => TSN OF TAPE
   4 6DJG ...[   
  12 6F46 ...X4SCHEDMTA                         [
  15 6FHQ ...      LDX   6  4                   [X6 => TSN   
  19 6G3B ...      CALL  7  TESTSECURE          [IS TSN SECURE (IN SERIAL)?  
  16 6GH2 ...      BRN      UPPLUS1             [J IF INSECURE   
  13 6H2L ...      TAPEOPEN (GEOERR),6,,NOTOL,REPLY  
  12 6HG= ...      BRN      UP                  [
   4 6H_W ...#END
   6 ____ ...347720260001
