  16 22FL ...      SEG      SETPN,,CENT(INSTALLATION HOOKS),,G400
   4 22_=    [   
  15 23DW    [ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23YG    [   
   4 24D6    #   
  16 24XQ ...      SEGENTRY K1SETPN,SENTRY1     [ENTRY FOR SP NOR
  16 25CB ...      SEGENTRY K3SETPN,SENTRY3     [ENTRY FOR SP PID
  16 25X2 ...      SEGENTRY K4SETPN,SENTRY4     [ENTRY FOR SP CLB
   4 26BL    #   
  13 26W=    #     THIS CHAPTER IS FOR G3PLUS-IH MK2 
   4 27*W    #   
   9 27TG    RGCH           4H0+-0   
  14 28*6    SDEFLOC        24H000000000000000-00000000  
  13 28SQ    ZEROS          20H00000000000000000000  
   8 29#B    TENS           +0   
   8 29S2                   +10  
   8 2=?L                   +100 
   8 2=R=                   +1000
   9 2?=W                   +10000   
   9 2?QG                   +100000  
   9 2#=6                   +1000000 
   8 2#PQ                   -1   
   4 2*9B    #   
  20 2*P2    #     ############################################################# 
   4 2B8L    #   
  11 2BN=    #           K    K            1 
  11 2C7W    #           K   K            11 
  11 2CMG    #           K  K              1 
  11 2D76    #           K K               1 
  11 2DLQ    #           KKK               1 
  11 2F6B    #           K  K              1 
  11 2FL2    #           K   K             1 
  11 2G5L    #           K    K            1 
  11 2GK=    #           K     K           1 
   4 2H4W    #   
  20 2HJG    #     ############################################################# 
   4 2J46    #   
   5 2JHQ    SENTRY1 
   4 2K3B    #   
  10 2KH2    #     NORMALISE PARAMETER   
  15 2L2L    #     1    -    FNORM THE ENTRANT DESCRIPTION   
   4 2LG=    #   
   7 2L_W          LDN   4  2
  18 2MFG          CALL  7  XHNT1               [LOCATE PARAMETER BLOCK  
   9 2M_6          LDX   4  ANUM(3)  
  19 2NDQ          BNG   4  XERR                [ERROR NONEXISTENT PARAMETER 
  17 2NYB          BZE   4  XERR                [ERROR NULL PARAMETER
  10 2PD2          NAMETOP  3,FILE,FNAME 
  14 2PXL          SETNCORE APARA-A1+18,3,COMDAT,CSETP   
   9 2QC=          BRUSEN   XNORA,FX2
   8 2QWW          BRN      XNORB
   5 2RBG    XNORA   
  10 2RW6          STOZ     CPREFIX(2)   
  10 2S*Q          STOZ     CPREFIX+1(2) 
  10 2STB          STOZ     CPREFIX+2(2) 
  10 2T*2    #     ZERO-FILL OUTPUT BLOCK
   4 2TSL    #   
   5 2W#=    XNORB   
   8 2WRW          STOZ     A1(3)
   8 2X?G          LDN   4  A1(3)
   9 2XR6          LDN   5  A1+1(3)  
  10 2Y=Q          MOVE  4  APARA-A1+17  
  18 2YQB          FNORM    9                   [NORMALISE,WITH ADJUNCTS 
  17 2_=2          TESTREP  NAMEFORM,XERR       [REPORT FORMAT ERROR 
  10 2_PL          MHUNT    2,FILE,FABSNB
  11 329=          MHUNT    3,COMDAT,CSETP   
   9 32NW          LDN   7  APARA(3) 
   4 338G    #   
   6 33N6    # USERNAME  
   4 347Q    #   
   9 34MB          LDN   6  A1+1(2)  
   7 3572          MOVE  6  3
  16 35LL          ADN   7  3                   [UPDATE POINTER  
   4 366=    #   
   6 36KW    # LOCALNAME 
   4 375G    #   
   7 37K6    # 1 - FILENAME  
   4 384Q    #   
  16 38JB          LDX   5  HDREC(2)            [RECORD HEADER   
   8 3942          SBN   5  10   
  15 39HL          BNG   5  XNORL1              [NOT GIVEN   
  16 3=3=          LDX   6  2                   [FPTR OF FABSNB  
   7 3=GW          ADX   6  5
  18 3?2G          ADN   6  A1+4                [POINTS TO A1+[HDREC]-6  
  16 3?G6          MOVE  6  3                   [INSERT FILENAME 
  15 3?_Q          ADN   7  3                   [UPDATE PTR  
   4 3#FB    #   
   7 3#_2    # 2 - REEL NO   
   4 3*DL    #   
   7 3*Y=          SMO      6
   7 3BCW          LDX   5  3
  18 3BXG          ANDN  5  #7777               [CLEAR BIT 2 IN ZERO CASE
  14 3CC6          LDN   4  3                   [COUNT   
  17 3CWQ          CALL  0  XDECCON             [CONVERT TO DECIMAL  
  15 3DBB          BRN      XERR                [FORMAT ERROR
   8 3DW2          SMO      FX2  
   9 3F*L          LDN   6  AWORK1   
  17 3FT=          MVCH  6  3                   [INSER DEC REEL NO   
   4 3G#W    #   
   8 3GSG    # 3 - GENERATION NO 
   4 3H#6    #   
   9 3HRQ          SMO      HDREC(2) 
  15 3J?B          LDX   5  A1-2(2)             [LOAD GEN NO 
  17 3JR2          LDCT  1  #600                [B0&1 SET FOR REL GEN
  17 3K=L          ANDX  1  5                   [MASK REL GEN BITS   
   8 3KQ=          SMO      FX1  
  17 3L9W          LDCH  0  RGCH(1)             [LOAD REL GEN CHAR   
  14 3LPG          LDX   1  7                   [POINTER 
  15 3M96          DCH   0  0(1)                [STORE CHAR  
  15 3MNQ          BCHX  7  /                   [STEP POINTER
  17 3N8B          ANDN  5  #7777               [MASK NUMERICAL PART 
  14 3NN2          LDN   4  4                   [COUNT   
   9 3P7L          CALL  0  XDECCON  
  15 3PM=          BRN      XERR                [FORMAT ERROR
   8 3Q6W          SMO      FX2  
   9 3QLG          LDN   6  AWORK1   
  14 3R66          MVCH  6  4                   [INSERT  
   4 3RKQ    #   
   8 3S5B    # 4 - LANGUAGE CODE 
   4 3SK2    #   
   9 3T4L          SMO      HDREC(2) 
  16 3TJ=          LDN   6  A1-1(2)             [LANGUAGE ADDRESS
  14 3W3W          MVCH  6  4                   [INSERT  
   9 3WHG          BRN      XNORLE   
   4 3X36    #   
   8 3XGQ    # DEFAULT LOCAL NAME
   4 3Y2B    #   
   5 3YG2    XNORL1  
   8 3Y_L          SMO      FX1  
   9 3_F=          LDN   6  SDEFLOC  
   8 3_YW          MVCH  6  24   
   4 42DG    #   
   8 42Y6    #     SERIAL NUMBER 
   4 43CQ    #   
   5 43XB    XNORLE  
  15 44C2          LDX   0  ALOGLEN(2)          [BLOCK LENGTH
   7 44WL          SBN   0  2
  16 45B=          BXL   0  HDREC(2),XNORSN1    [JIF NO SERIAL NO
   7 45TW          SMO      0
  16 46*G          LDXC  5  A1+1(2)             [LOAD SERIAL NO  
   7 46T6          BCS      /
  14 47#Q          LDN   4  8                   [COUNT   
  14 47SB          CALL  0  XOCTCON             [CONVERT 
   8 48#2          BRN      XERR 
   8 48RL          SMO      FX2  
  15 49?=          LDN   6  AWORK1              [ADDRESS FROM
  14 49QW          MVCH  6  8                   [INSERT  
  16 4==G          BRN      XNORADJ             [ADJUNCTS PART   
   5 4=Q6    XNORSN1 
   8 4?9Q          SMO      FX1  
  15 4?PB          LDN   6  ZEROS               [ZERO DEFAULT
  14 4#92          MVCH  6  8                   [INSERT  
   4 4#NL    #   
  13 4*8=    #     INFORMATION FROM ADJUNCTS BLOCK   
   4 4*MW    #   
   5 4B7G    XNORADJ 
  16 4BM6          TESTREPN ADJUNCTS,XDEFADJ    [JIF NO ADJUNCTS 
  15 4C6Q          MHUNT    2,FILE,ADJUNCTS     [LOCATE BLOCK
   4 4CLB    #   
   7 4D62    #     MAIN MODE 
   4 4DKL    #   
  14 4F5=          LDX   0  A1+1(2)             [MODE WD 
  14 4FJW          ANDN  0  #7000               [B12-14  
  16 4G4G          SRL   0  9                   [RIGHT JUSTIFY   
  15 4GJ6          LDN   1  0                   [MANIPULATE  
  17 4H3Q          SRL   01 2                   [0=>0,4=>1,3=>2,6=>3 
   7 4HHB          SLC   1  2
   7 4J32          ANDN  1  2
  13 4JGL          ADX   0  1                   [!!  
   7 4K2=          LDX   1  7
  15 4KFW          DCH   0  0(1)                [STORE RESULT
  14 4K_G          BCHX  7  /                   [STEP PTR
   4 4LF6    #   
   8 4LYQ    #      ADDITIVE MODE
   4 4MDB    #   
   9 4MY2          LDX   0  A1+1(2)  
   7 4NCL          SRL   0  2
  13 4NX=          ANDN  0  #75                 [MASK
   9 4PBW          LDX   1  A1+1(2)  
   7 4PWG          SRL   1  6
   8 4QB6          ANDN  1  #100 
   7 4QTQ          ADX   0  1
   9 4R*B          LDX   1  A1+1(2)  
   7 4RT2          ANDN  1  3
   7 4S#L          SLL   1  7
   7 4SS=          ADX   0  1
   9 4T?W          LDX   1  A1+1(2)  
   7 4TRG          SLC   1  7
   7 4W?6          ANDN  1  2
   7 4WQQ          ADX   0  1
   7 4X=B          LDX   5  0
  14 4XQ2          LDN   4  3                   [COUNT   
   9 4Y9L          CALL  0  XOCTCON  
  15 4YP=          BRN      XERR                [REPORT ERROR
   8 4_8W          SMO      FX2  
   9 4_NG          LDN   6  AWORK1   
  14 5286          MVCH  6  3                   [INSERT  
   4 52MQ    #   
   7 537B    # DEVICE TYPE   
   4 53M2    #   
   9 546L          LDX   5  A1+2(2)  
   8 54L=          SRL   5  15   
   8 555W          ANDN  5  #77  
   9 55KG          BNZ   5  XNORDEV1 
   9 5656          LDX   5  A1+2(2)  
   7 56JQ          SRL   5  3
   7 574B          ANDN  5  3
   9 57J2          BZE   5  XNORDEV1 
   7 583L          ADX   5  5
   7 58H=          ADN   5  1
   7 592W          ANDN  5  3
   5 59GG    XNORDEV1
   7 5=26          LDN   4  2
  17 5=FQ          CALL  0  XDECCON             [CONVERT TO DECIMAL  
   8 5=_B          BRN      XERR 
   8 5?F2          SMO      FX2  
   9 5?YL          LDN   6  AWORK1   
  14 5#D=          MVCH  6  2                   [INSERT  
   4 5#XW    #   
  13 5*CG    #     BACK TO FABSNB FOR PARAMETER TYPE 
   4 5*X6    #   
   4 5BBQ    XNPT
  15 5BWB          MHUNT    2,FILE,FABSNB       [TYPE WORD   
   8 5CB2          SMO      FX2  
   9 5CTL          STOZ     AWORK1   
   9 5D*=          LDX   0  ATYPE(2) 
   8 5DSW          ANDN  0  #1000
   7 5F#G          SRL   0  9
   8 5FS6          SMO      FX2  
  15 5G?Q          ORS   0  AWORK1              [STORE BIT   
   9 5GRB          LDX   0  ATYPE(2) 
   8 5H?2          ANDN  0  #200 
   7 5HQL          SRL   0  6
   8 5J==          SMO      FX2  
   9 5JPW          ORS   0  AWORK1   
  10 5K9G          LDX   1  ALOGLEN(2)   
   9 5KP6          SBX   1  HDREC(2) 
   7 5L8Q          SBN   1  2
  15 5LNB          BNG   1  XNPT1               [JIF NO TSN  
   9 5M82          SMO      HDREC(2) 
  13 5MML          LDXC  1  A1+1(2)             [TSN 
  15 5N7=          BCC      XNPT1               [J NOT XENO  
   7 5NLW          ORN   0  4
   8 5P6G          SMO      FX2  
  16 5PL6          ORX   0  AWORK1              [GET ANOTHER BITS
   5 5Q5Q    XNPT2   
   7 5QKB          LDX   1  7
  14 5R52          DCH   0  0(1)                [INSERT1 
   7 5RJL          BCHX  7  /
  15 5S4=          BRN      XNORT               [FOR TRAPS   
   5 5SHW    XNPT1   
   8 5T3G          SMO      FX2  
   9 5TH6          LDX   0  AWORK1   
   8 5W2Q          BRN      XNPT2
   5 5WGB    XDEFADJ 
   7 5X22          SLC   7  2
   7 5XFL          ADN   7  6
   7 5X_=          SRC   7  2
   8 5YDW          BRN      XNPT 
   4 5YYG    #   
  10 5_D6    #     FINALLY...TRAP RECORDS
   4 5_XQ    #   
   5 62CB    XNORT   
  10 62X2          HUNT     2,FILE,FTRAP 
  16 63BL          BNG   2  XNORE               [NO MORE TRAPS   
   5 63W=    XNTR1   
  10 64*W          LDN   6  ETRUSER1(2)  
  14 64TG          MVCH  6  12                  [INSERT  
  10 65*6          LDX   5  ETRAPMODES(2)
   7 65SQ          SLC   5  1
  15 66#B          ANDN  5  #77                 [MASK BITS   
   7 66S2          LDN   4  2
  16 67?L          CALL  0  XOCTCON             [CONVERT TO OCTAL
  14 67R=          BRN      XERR                [TOO BIG 
   8 68=W          SMO      FX2  
   9 68QG          LDN   6  AWORK1   
  17 69=6          MVCH  6  2                   [INSERT TRAP MOC=DES 
  16 69PQ          FREECORE 2                   [FREE TRAP BLOCK 
   7 6=9B          SLC   3  2
   7 6=P2          SLC   7  2
   7 6?8L          SBX   7  3
   8 6?N=          SMO      FX2  
  15 6#7W          STO   7  AWORK1              [SAVE COUNT  
  16 6#MG          SRC   3  2                   [RESTORE ADDRESS 
   8 6*76          ADN   7  19   
   7 6*LQ          SRL   7  2
  16 6B6B          ALTLEN   3,7,COMDAT,CSETP    [LENGTHEN BLOCK  
  11 6BL2          MHUNT    3,COMDAT,CSETP   
   7 6C5L          SLC   3  2
   9 6CK=          LDX   7  AWORK1(2)
   7 6D4W          ADX   7  3
   7 6DJG          SRC   3  2
  16 6F46          SRC   7  2                   [RESTORE POINTER 
  17 6FHQ          BRN      XNORT               [TRY NEXT TRAP BLK   
   4 6G3B    #   
  11 6GH2    #      CLEAR UP READY FOR OUTPUT
   4 6H2L    #   
   5 6HG=    XNORE   
   7 6H_W          SLC   7  2
   7 6JFG          SLC   3  2
   7 6J_6          SBX   7  3
   9 6KDQ          SBN   7  APARA*4  
  14 6KYB          SRC   3  2                   [ADDRESS 
  15 6LD2          STO   7  ANUM(3)             [STORE COUNT 
   9 6LXL ...      ACROSS SETPARAM,4 
   4 6MC=    #   
  11 6MWW    #     END OF NORMALISE MAIN PATH
   4 6NBG    #   
   4 6NW6    #   
   9 6P*Q    #    OCTAL SUBROUTINE   
  11 6PTB    #    ON ENTRY,X4 = CHAR COUNT   
  11 6Q*2    #             X5 = NO IN BINARY 
  18 6QSL    #    ON EXIT,AWORK1&AWORK2 OF THE CURRENT ACTIVITY CONTAIN  
   7 6R#=    #    THE RESULT 
   4 6RRW    #   
  12 6S?G    #      EXIT + 0,IF VALUE TOOLARGE   
   9 6SR6    #      EXIT + 1,IF OK   
   7 6T=Q    #     LINK X0   
   4 6TQB    #   
   5 6W=2    XOCTCON 
  15 6WPL          STO   0  GEN1                [SAVE LINK   
  14 6X9=          STO   4  GEN0                [SAVE X4 
   7 6XNW          LDN   0  8
  17 6Y8G          SBX   0  GEN0                [NO OF LEADING ZEROES
  15 6YN6          BZE   0  XOCT1               [NONE LEFT   
   5 6_7Q    XOCT2   
  14 6_MB          STOZ     4                   [CLEAR X4
  18 7272          SLL   45 3                   [SHIFT FIRST LEADING ZERO
  18 72LL          BNZ   4  XOCTER              [ERROR EXIT IF NOT ZERO  
   8 736=          BCT   0  XOCT2
   5 73KW    XOCT1   
  14 745G          LDX   0  GEN0                [COUNT   
  14 74K6          STO   2  GEN0                [SAVE X2 
  19 754Q          LDX   2  FX2                 [SET TO FX2 TO ACCESS AWORK1 
   5 75JB    XOCT3   
   7 7642          STOZ     4
   7 76HL          SLL   45 3
  16 773=          DCH   4  AWORK1(2)           [STORE OCTAL CHAR
   7 77GW          BCHX  2  /
   8 782G          BCT   0  XOCT3
   8 78G6          LDX   2  GEN0 
   8 78_Q          LDX   0  GEN1 
  13 79FB          EXIT  0  1                   [EXIT
   5 79_2    XOCTER  
   8 7=DL          LDX   0  GEN1 
  15 7=Y=          EXIT  0  0                   [ERROR EXIT  
   4 7?CW    #   
   9 7?XG    #     DECIMAL SUBROUTINE
   4 7#C6    #   
   5 7#WQ    XDECCON 
   8 7*BB          LDX   1  FX1  
   7 7*W2          SMO      4
  15 7B*L          TXL   5  TENS(1)             [MAX ALLOWED 
  16 7BT=          BCC      XDECER              [ERROR TOO BIG   
   8 7C#W          STO   6  GEN0 
  14 7CSG          STO   7  GEN1                [SAVE X7 
   7 7D#6          LDN   6  0
   7 7DRQ          SMO      4
  16 7F?B          DVR   5  TENS(1)             [DIVIDE BY TENS  
  14 7FR2          LDCT  7  #200                [ROUND UP
   8 7G=L          SMO      FX2  
  15 7GQ=          LDN   1  AWORK1              [DESTINATION 
   5 7H9W    XDEC1   
   8 7HPG          CBD   6  0(1) 
   7 7J96          BCHX  1  /
   8 7JNQ          BCT   4  XDEC1
  15 7K8B          LDX   6  GEN0                [RETRIEVE X6 
  15 7KN2          LDX   7  GEN1                [RETRIEVE X7 
  14 7L7L          LDX   1  FX1                 [RESET X1
   7 7LM=          EXIT  0  1
   5 7M6W    XDECER  
   7 7MLG          EXIT  0  0
   4 7N66    QNUM
   8 7NKQ          SBX   5  FX1  
  15 7P5B          PARAPASS                     [PASS PARAM  
  10 7PK2          HUNT     3,CPB,CUNI   
   9 7Q4L          LDX   4  ANUM(3)  
   8 7QJ=          BNG   4  QNUM1
   8 7R3W          BZE   4  QNUM1
  17 7RHG          CHNUMCOD 1,3,XBREAK          [CONVERT TO BINARY   
  11 7S36          TESTREP  CHNUMERR,XERR1   
  10 7SGQ          LDX   4  ACOMMUNE1(2) 
  17 7T2B          BNG   4  XERR                [ERROR IF NEGATIVE   
   5 7TG2    QNUM2   
   8 7T_L          ADX   5  FX1  
   7 7WF=          EXIT  5  0
   5 7WYW    QNUM1   
  15 7XDG          LDN   4  0                   [DEFAULT ZERO
   8 7XY6          BRN      QNUM2
   5 7YCQ    XHNT1   
   8 7YXB          SBX   7  FX1  
   8 7_C2          SPARANXT ,,4  
   8 7_WL          BRN      XHNT2
   4 82B=    XHNT
   8 82TW          SBX   7  FX1  
   8 83*G          PARANEXT ,,4  
   5 83T6    XHNT2   
  10 84#Q          TESTREP  UNPAIR,XERR  
   8 84SB          ADX   7  FX1  
  10 85#2          HUNT     3,CPB,CUNI   
   7 85RL          EXIT  7  0
   4 86?=    SPAR
   8 86QW          SBX   7  FX1  
   7 87=G          SPARAPAS  
  10 87Q6          MHUNT    3,CPB,CUNI   
   8 889Q          ADX   7  FX1  
   7 88PB          EXIT  7  0
   5 8992    XERR1   
  16 89NL          LDX   7  PCHNUMERR(1)        [ERROR CODE ...  
   8 8=8=          BRN      XER1 
   4 8=MW    XERR
   8 8?7G          LDX   1  FX1  
  10 8?M6          LDX   7  PAPFERR(1)   
   4 8#6Q    XER1
   8 8#LB          SMO      FX2  
   9 8*62          LDX   4  AWORK4   
   7 8*KL          NGN   6  1
   8 8B5=          BZE   4  XER2 
   7 8BJW          PARAFREE  
   7 8C4G          UNIFREE   
   8 8CJ6          LDX   2  FX2  
   9 8D3Q          LDX   0  AWORK3(2)
  10 8DHB          STO   0  JOBEVENTS(2) 
   5 8F32          UP
   4 8FGL    XER2
   8 8G2=          COMERRX  7,6  
   9 8GFW    PCHNUMERR      +CHNUMERR
   9 8G_G    PAPFERR        +APFERR  
   4 8HF6    #   
   5 8HYQ    XBREAK  
   7 8JDB          COMBRKIN  
   4 8JY2    #   
  20 8KCL    #     ############################################################# 
   4 8KX=    #   
  11 8LBW    #           K    K         3333 
  11 8LWG    #           K   K         3    3
  11 8MB6    #           K  K               3
  11 8MTQ    #           K K                3
  11 8N*B    #           KKK              33 
  11 8NT2    #           K  K               3
  11 8P#L    #           K   K              3
  11 8PS=    #           K    K        3    3
  11 8Q?W    #           K     K        3333 
   4 8QRG    #   
  20 8R?6    #     ############################################################# 
   4 8RQQ    #   
   5 8S=B    SENTRY3 
   4 8SQ2    XPID
  13 8T9L          SETNCORE 7+APARA-A1,2,COMDAT,CSETP
  10 8TP=          HUNT     1,IUSER,BADGE
   8 8W8W          BNG   1  XPID1
   9 8WNG          LDX   0  ANUM(1)  
   9 8X86          STO   0  ANUM(2)  
   8 8XMQ          ADN   1  APARA
   5 8Y7B    XPID2   
   8 8YM2          ADN   2  APARA
   7 8_6L          MOVE  1  7
  10 8_L= ...      ACROSS   SETPARAM,4   
   5 925W    XPID1   
   8 92KG          LDN   0  25   
   9 9356          STO   0  ANUM(2)  
   8 93JQ          SMO      FX1  
   8 944B          LDN   1  XPID3
   8 94J2          BRN      XPID2
  14 953L    XPID3          28HNO IDENTIFICATION PRESENT 
   4 95H=    #   
  20 962W    #     ############################################################# 
   4 96GG    #   
  11 9726    #           K    K            4 
  11 97FQ    #           K   K            44 
  11 97_B    #           K  K            4 4 
  11 98F2    #           K K            4  4 
  11 98YL    #           KKK           4   4 
  11 99D=    #           K  K         4444444
  11 99XW    #           K   K             4 
  11 9=CG    #           K    K            4 
  11 9=X6    #           K     K           4 
   4 9?BQ    #   
  20 9?WB    #     ############################################################# 
   4 9#B2    #   
   5 9#TL    SENTRY4 
  10 9**=          HUNT     2,JWELL,ACLB 
  16 9*SW          BNG   2  XCLB1               [NO LINE BUFFER  
  15 9B#G          LDEX  4  A1(2)               [WORD COUNT  
  16 9BS6          ADN   4  APARA-A1            [ADD FOR RED TAPE
  16 9C?Q          SETUPCORE 4,3,COMDAT,CSETP   [BLOCK FOR BUFFER
  16 9CRB          MHUNT    2,JWELL,ACLB        [FIND LINE BUFFER
  16 9D?2          LDN   5  A1+2(2)             [START OF COMMAND
   9 9DQL          LDN   6  APARA(3) 
  18 9F==          SBN   4  APARA-A1+2          [SUBTRACT FOR RED TAPE   
   7 9FPW          SMO      4
  16 9G9G          MOVE  5  0                   [MOVE IN COMMAND 
   7 9GP6          SLL   4  2
   5 9H8Q    XCLB2   
  15 9HNB          STO   4  ANUM(3)             [CHAR COUNT  
  16 9J82 ...      ACROSS   SETPARAM,4          [ALL IN PARAMETER
   5 9JML    XCLB1   
  13 9K7=          SETNCORE 1+APARA-A1,3,COMDAT,CSETP
  15 9KLW          LDN   4  0                   [NULL REPLY  
   8 9L6G          BRN      XCLB2
   4 9LL6    #END
   6 ____ ...411620740001
