  14 22FL    #SEG  READBULK8                    [R.WEYAND
  16 22PD ...[   (C)   COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  11 22_=    #LIS  K0READBULK>K0IPB>K0ALLGEO 
  10 23DW                   8HREADBULK   
   4 23YG    #   
  13 24D6          SEGENTRY K1READBULK,SK1READBULK   
  13 24XQ          SEGENTRY K2READBULK,SK2READBULK   
  13 253M ...      SEGENTRY  K3READBULK,SK3READBULK  
  13 257J ...      SEGENTRY  K4READBULK,SK4READBULK  
  13 25?F ...      SEGENTRY  K5READBULK,SK5READBULK  
   9 25CB    NBLANK         #76322020
   8 25X2    RUNOUT         #20  
   7 26BL ...SHIFT        #76
   9 27*W    SUPSEG         #01270000
   8 27TG    T              #64  
   8 28SQ    XDEL           #73  
   8 29#B    XIS1           #17  
  10 29B5 ...XERR             +CCNOT80CHS
  10 29CS ...               +CCWRONGILL  
   5 29S2    XIDATSG 
  11 2=?L    #HAL           AMXOR+IDATSG,0   
   4 2=R=    #   
  19 2?=W    #     SUBROUTINE TO RE-FIND BLOCK BEING LENGTHENED BY ALTLENG   
   4 2?QG    #   
  11 2#=6    REH   MHUNTW   2,ADATA,CREADL   
   7 2#PQ          EXIT  1  0
   4 2#R7 ...#   
  21 2#SJ ...#     SUBROUTINES TO TEMPORARILY PRESERVE(OVER A DEMAND) AND TO RESTORE 
  21 2#T_ ...#     THE CONTENTS OF ACOMMUNE3 AND ACOMMUNE6 IN X6 AND X7 RESPECTIVELY 
   4 2#XB ...#   
  19 2#YR ...PACOM LDX   6  ACOMMUNE3(2)        [SHIFT AT START OF DATA BLOCK
  21 2*28 ...      LDX   7  ACOMMUNE6(2)        [RELATIVE (ADATA/CREADL) DCH POINTER 
   7 2*3K ...      EXIT  0  0
  10 2*52 ...RACOM STO   6  ACOMMUNE3(2) 
  10 2*6C ...      STO   7  ACOMMUNE6(2) 
   7 2*7S ...      EXIT  0  0
   4 2*9B    #   
  20 2*HR ...#     SEND STOP INPUT IF NOT ALREADY SENT (AFTER UNSETTING ONE SHOT 
  10 2*W8 ...#     MARKER) ; CALL ON X7  
   4 2B8L    #   
  10 2BN=    STOP  MHUNTW   3,AMXOR,ADCB 
   8 2C7W          LDCT  0  #10  
  16 2CMG          LDN   5  #123                [STOP INPUT CODE 
   9 2D76          ORS   0  IWAIT(3) 
  20 2DLQ          LDCT  6  #400                [DON'T WAIT FOR REPLY IN SUPER   
   9 2F6B          ERS   0  IWAIT(3) 
   7 2F=? ...      SLL   0  2
  10 2FB8 ...      ANDX  0  ISTATUS(3)   
  19 2FG5 ...      BNZ   0  (7)                 [EXIT IF ALREADY STOPPED (B3)
   8 2FL2          BRN      SUPER
   4 2G5L    #   
  21 2GK=    #     SEND ONE SHOT INPUT IF NOT ALREADY SENT ; DROPS THROUGH TO 'SUPER'
   4 2H4W    #   
  10 2HJG    ONE   MHUNTW   3,AMXOR,ADCB 
   8 2J46          LDCT  0  #10  
   9 2JHQ          ANDX  0  IWAIT(3) 
  18 2K3B          BNZ   0  (7)                 [ALREADY SENT ONE SHOT   
   8 2K7? ...      LDCT  0  #40  
  10 2K?8 ...      ANDX  0  ISTATUS(3)   
  21 2KC5 ...      BNZ   0  (7)                 [STOPPED-CLEARING DATA:SKIP ONE SHOT 
  18 2KH2          LDN   5  #122                [CODE FOR ONE SHOT INPUT 
   8 2L2L          LDCT  0  #10  
  17 2LG=          LDCT  6  #400                [DONT WAIT IN SUPER  
  18 2L_W          ORS   0  IWAIT(3)            [INDICATE ONE SHOT SENT  
   4 2MFG    #   
  21 2M_6    #     SEND SUPERVISORY SEGMENT AND AWAIT REPLY ; CODE IN X5 , REPLY X6  
   4 2NDQ    #   
   8 2NYB    SUPER SBX   7  FX1  
  10 2PD2          MHUNTW   3,AMXOR,ADCB 
  21 2PXL          LDCT  0  #100                [SKIP SUPER IF IDENTIFIER IS CLOSED :
  18 2QC=          ANDX  0  ISTATUS(3)          [MOP OFF OR FINISH NOW   
   8 2QWW          BZE   0  SOUT 
   8 2RBG          ERN   5  #120 
  21 2RW6          BNZ   5  SEX                 [UNLESS SENDING OPEN SUPERVISORY SEG 
  14 2S5Y ...      ORN   5  #120                [RESET X5
   4 2S*Q    SOUT
   7 2STB          LDX   3  5
   7 2T*2          SRL   3  6
  14 2TSL          SETNCORE IPBIDE-A1(3),1,AMXOR,IOUT,1  
  10 2W#=          MHUNTW   3,AMXOR,ADCB 
   8 2WRW          LDX   0  GSIGN
  21 2X?G          SLL   5  12                  [WORD COUNT (=1) + SUPERVISORY CODE  
  16 2XR6          STO   0  IBSW(1)             [INDICATE NO DATA
  10 2Y=Q          ADX   5  IDENTNO(3)   
  18 2YQB          STOZ     IMOPTY(1)           [DON'T WAKE AFTER OUTPUT 
   9 2_=2          STO   5  IPBIDE(1)
  16 2_PL          BNZ   6  NOSI                [NOT START INPUT 
   9 329=          LDX   0  AWORK2(2)
  20 32NW          STO   0  IPBIDE+1(1)         [START INPUT UP TO TERMINATOR.   
  19 338G    NOSI  LDX   2  IADIN(3)            [ADDRESS OF IPBIN ACTIVITY   
  10 33N6          LDX   2  IPBRING(2)   
  19 347Q          SBN   2  IPBRING             [ADDRESS OF IPBOUT ACTIVITY  
   7 34MB          LDX   4  2
  10 3572          LDX   2  ACTRING(2)   
   9 35LL          SBN   2  ACTRING  
   9 366=          LDX   2  BPTR(2)  
  19 36KW          CHAIN    1,2                 [PUT ON END OF IPBOUT'S QUEUE
   7 375G          LDX   2  4
   8 37K6          LDCT  0  #100 
   9 384Q          ANDX  0  IPBSW(2) 
   8 38JB          BZE   0  WAKAL
   9 3942          ERS   0  IPBSW(2) 
   6 39HL          FPUT  
   4 3=3=    #   
  18 3=GW    WAKAL LDN   4  0                   [IN CASE JUMP TO PBNOP   
   9 3?2G          SMO      IADIN(3) 
   8 3?G6          LDXC  0  IPBSW
  16 3?_Q          BCS      PBNOP               [IPB INOPERABLE  
  19 3#FB          BZE   6  SEX                 [DON'T WAIT AFTER START INPUT
  17 3#_2          BNG   6  SEX                 [OR ONE SHOT INPUT   
  21 3*DL          LDCT  0  #401                [SET B8 TO RECEIVE SUPERVISORY INPUT 
  16 3*Y=          ORS   0  IWAIT(3)            [INDICATE ASLEEP 
   6 3BCW          COOR1 
  10 3BXG          MHUNTW   3,AMXOR,ADCB 
   7 3CC6          LDN   4  1
  11 3CWQ    YSUG  HUNTW    2,AMXOR,IDATSG   
   9 3DBB ...      BNG   2  WAKALA   
   9 3DW2          LDX   0  A1+1(2)  
  19 3F*L          BNG   0  SSE                 [J IF A SUPERVISORY SEGMENT  
   7 3FT=          LDX   0  6
   8 3G#W          SBN   0  52   
  21 3GSG          BNZ   0  NOSSE               [J IF NOT AWAITING INPUT CLEARED REP.
  19 3H#6          NAME     2,AMXOR,#77         [PRESERVE DATA BEING CLEARED 
   8 3HRQ          BRN      YSUG 
   5 3HSP ...WAKALA  
  10 3HTN ...      LDX   2  A1+FPTR(3)   
  18 3HWM ...      LDX   0  FOURTHWD(2)            [LD FOURTHWD OF@ADEVS 
  14 3HXL ...      ORX   0  FINISH           [PLUS FINISH
   7 3HYK ...      SLL   0  2
  19 3H_J ...      BPZ   0  WAKAL               [J. NOT FINISH OR MOPPING OFF
   7 3J2H ...      LDX   0  6
   8 3J3G ...      SBN   0  54   
  19 3J4F ...      BNG   0  OPOF                [J. NOT CRASH CLOSING SUP.   
   9 3J5D ...      SMO      IADIN(3) 
  17 3J6C ...      LDX   2  IPBRING             [PTR TO AMOP IPBOUT  
   8 3J7B ...      LDCT  0  #420 
  19 3J8* ...      ANDX  0  IPBSW(2)            [TEST B4 AMOP - MOP OFF PM   
  18 3J9# ...      BZE   0  WAKAL               [J. BIT NOT SET - WAIT   
  15 3J=* ...      BRN      SCCX                [J. BIT SET  
   4 3J?B    SSE 
   7 3JR2          BCHX  2  /
  18 3K=L          LDCH  4  A1+2(2)             [PICK UP SUPERVISORY CODE
   9 3KQ=          ANDX  2  BITS22LS 
   8 3KW7 ...      SBN   4  #45  
   8 3L24 ...      BZE   4  XILL 
   8 3L5_ ...      ADN   4  #45  
   7 3L9W          SBX   4  6
   7 3LPG    NOSSE FREECORE 2
   8 3M96          BNZ   4  YSUG 
  11 3MNQ    PBNOP HUNTW    2,AMXOR,IDATSG   
   8 3N8B          BPZ   2  NOSSE
   8 3NN2    SEX   ADX   7  FX1  
  19 3P7L          LDX   2  FX2                 [ENSURE X2-> TO CURRENT ACT. 
   7 3PM=          EXIT  7  0
   4 3PP8 ...[   
  13 3PR6 ...[ ILLEGAL SUPERVISORY RECEIVED IN ERROR 
   4 3PT4 ...[   
   4 3PX2 ...XILL
  19 3PYY ...      LDX   5  XERR+1(1)             [LD. 8CHS TO RECONISE ERR  
  18 3Q2W ...      LDX   6  A1+2(3)              [LD. DATA INFO FOR ILL  
   8 3Q4S ...      BRN      XCERR
   4 3Q6W    #   
   4 3QLG    #   
  20 3R66    #     ENTRY POINT FROM COMMAND PROCESSOR VIA READLINE MACRO FOR NEW 
  20 3RKQ    #     BULK INPUT FROM IPB ; INPUT WILL BE IN AN AMXOR/IDATSG BLOCK  
   4 3S5B    #   
   6 3SK2    SK1READBULK 
  18 3T4L          LDCT  6  #420                [READ TAPE IN NORMAL MODE
  18 3TJ=          STO   6  AWORK4(2)           [INDICATE READLINE ENTRY 
  20 3W3W          CALL  7  ONE                 [SEND ONE SHOT INPUT IF NECESSARY
   8 3WHG          BRN      NEWEN
   4 3X36    #   
  20 3XGQ    #     ENTRY FROM INPUT COMMAND FOR REMOTE BULK INPUT FROM THE IPB   
  20 3Y2B    #     CONVERT AND APPEND INPUT RECORDS TO FILE(OPENED BY PREPREAD)  
   4 3YG2    #   
   6 3Y_L    SK2READBULK 
  15 3_F=    #              SET B0-5 OF AWORK4 TO FILE'S MODE
   9 3_YW          STOZ     AWORK4(2)
  18 42DG          TOPFCB2  3                   [GET PTR TO FILE'S FCB   
   9 42Y6          LDEX  0  FETM(3)  
   9 43CQ          DCH   0  AWORK4(2)
   4 43XB    #   
   8 44C2          BRN      NEWIN
   4 44WL    #   
  11 45B=    SLOOP MHUNTW   3,ADATA,CREADL   
  18 45TW          LDX   6  A1(3)               [COUNT OF CHARS IN OUTPUT
   8 46*G          LDX   2  FX2  
   9 46T6          ADN   3  CPDATA   
   8 47#Q          LDCH  0  0(3) 
   8 47SB          SBN   0  #74  
   8 48#2          BNZ   0  NOL  
  21 48RL          BCHX  3  /                   [FIRST CHAR = #74 : TEST NEXT FOUR   
   7 49?=    NOL   LDN   4  0
   7 49QW          MVCH  3  4
   9 4==G          TXU   0  AWORK2(2)
  16 4=Q6          BCC      TERMF               [TERMINATOR FOUND
   7 4=QQ ...#UNS  JPGLTER   
   4 4=RB ...(   
  18 4=XQ ...      TXU   0  GTERM               [UNIVERSAL TERMINATOR?   
   8 4=YB ...      BCS      NOTUT
  10 4=_2 ...      MHUNTW   2,AMXOR,ADCB 
   9 4=_L ...      SMO      IADIN(2) 
   8 4?2= ...      LDX   0  IPBSW
  15 4?2W ...      ANDN  0  4                    [APPLICABLE?
  19 4?3G ...      BNZ   0  TFJP                 [YES, TREAT AS TERMINATOR   
   8 4?46 ...      LDX   2  FX2  
  20 4?4Q ...      LDN   7  JSOURCE1(2)          [NO, OUTPUT INFORMATORY MESSAGE 
  10 4?5B ...      LDN   0  ACOMMUNE1(2) 
   7 4?62 ...      MOVE  7  2
   7 4?6L ...      OUTPERA   
  13 4?7= ...      OUTPARAM ACOMMUNE1(2),ACOMMUNE2   
  20 4?7W ...      MONOUT   JPUTIGN              [UNIVERSAL TERMINATOR IGNORED   
   5 4?8G ...NOTUT   
   4 4?96 ...)   
   9 4?9Q          LDX   0  AWORK1(2)
  19 4?PB          BNG   0  STBIG               [J IF FILE FULL MESSAGE SENT 
  17 4#92          ADN   6  11                  [RED TAPE + ROUNDING 
   7 4#NL          LDN   7  0
   7 4*8=          SRL   67 2
  19 4*MW          BCHX  7  /                   [TO GET POSITION OF LAST CHAR
  21 4B7G          LDX   0  ACOMMUNE3(2)        [SHIFT AT START OF THIS DATA BLOCK   
   7 4BM6          SLL   0  6
  20 4C6Q          DSA   0  7                   [NOT ADX AS BCHX MAY HAVE SET B23
   7 4CLB    ROK   LDX   2  6
   8 4D62          STEP     ,0(2)
  14 4DKL          TESTREP2 FILEFULL,XFULL,APPWAIT,XWAIT 
  13 4F5=          ADN   7  #41                 [PFCC
  15 4FJW          STO   6  0(3)                [WORD COUNT  
  21 4G4G          STO   7  1(3)                [LAST CHAR POS. AND STARTING SHIFT   
  11 4GJ6          MHUNTW   2,ADATA,CREADL   
   7 4H3Q          SBN   6  2
  17 4HHB          BZE   6  NULEN               [NO DATA : NULL ENTRY
   9 4J32          ADN   2  CPDATA   
   7 4JGL          ADN   3  2
   7 4K2=          SMO   6   
  16 4KFW          MOVE  2  0                   [INSERT RECORD   
   9 4K_G          SBN   2  CPDATA   
  19 4LF6    NULEN LDN   3  CPDATA-A1+32        [MINIMUM TAPE BLOCK LENGTH   
   8 4LYQ          LDN   0  128  
  16 4MDB          STO   0  A1(2)               [TAPE CHAR COUNT 
  21 4MY2          TXL   3  ALOGLEN(2)          [LESS ONLY IF HAD LONG TAPE RECORD   
  17 4NCL          BCC      RILAL               [RIGHT LENGTH ALREADY
  18 4NX=          ALTLEN   2,3                 [REDUCE TO INITIAL SIZE  
   8 4PBW    RILAL LDX   2  FX2  
   8 4PWG          BRN      STBIG
   4 4QB6    #   
  19 4QTQ    #     FILE FULL : IF MULTIFILING OPEN ANOTHER STREAM COMPONENT  
   4 4R*B    #   
   5 4RP5 ...XFULL   
  20 4S4S ...      INPUTFUL XBRK1,ROK,JOVER     [OUTPUT ^FILE %A OVERFLOWED...^  
  20 4S#L    #              IF ISSUED BY JOB,FILE IS CLOSEDABANDONED AND SETERR  
  15 4SS=    #              OBEYED ELSE FILE IS JUST CLOSED  
   8 4S_D ...      LDCT  0  #400 
  18 4T6L ...      ORS   0  AWORK1(2)           [SET FILE FULL FLAG AND  
  19 4T?W          BRN      STBIG               [J TO SEARCH FOR TERMINATOR  
  19 4TRG    XWAIT LONGSTOP XBRK                [AWAIT FILE SPACE FROM GDR   
  16 4W?6          BRN      ROK                 [TRY APPEND AGAIN
   6 4WQQ    XBRK  CLOSE 
   9 4X=B    XBRK1 CALL  5  SINFIN   
   7 4XQ2          ABANDCOM  
   5 4Y9L    SINFIN  
  19 4YP=          INFINB   [FLIP ^INPUT IN PROGRESS^ BIT FOR FINISH COMMAND 
   7 4_8W          EXIT  5  0
   4 4_NG    #   
  21 5286    #     CLOSE FILE WHEN TERMINATOR FOUND , HAVING APPENDED IT AND A DUMMY 
  20 52MQ    #     RECORD (BLANK IF CARDS , NEWLINE IF TAPE) IF S-TYPE TERMINATOR
   4 537B    #   
  21 53M2    TFOUN CALL  7  STOP                [PLEASE ENGAGE TERMINATED:STOP INPUT 
   4 53WS ...TFJP
  18 546L          MHUNTW   3,ADATA,CREADL      [SIMULATE TERMINATOR READ
   7 54L=          LDN   0  4
   8 555W          STO   0  A1(3)
   9 55KG          LDX   0  AWORK2(2)
   9 5656          STO   0  CPDATA(3)
   8 56JQ    TERMF LDX   1  FX2  
  15 574B          LDX   6  AWORK2(1)           [TERMINATOR  
  19 57J2          LDXC  4  AWORK1(1)           [TYPE OF TERMINATOR : S OR T 
  18 57WC ...      BCS      NCLOS               [J IF FILE FULL FLAG UP  
   8 58H=          SBN   4  #64  
  17 592W          BZE   4  NSTO                [T-TYPE : DON'T STORE
  14 59GG    # S-TYPE TERMINATOR : STORE ENTIRE RECORD   
  11 5=26          MHUNTW   3,ADATA,CREADL   
  17 5=FQ          LDX   6  A1(3)               [RECORD CHAR COUNT   
  18 5=_B          ADN   6  11                  [RED TAPE+ROUNDING(8+3)  
  19 5?F2          LDN   7  0                   [GET RECORD WORD COUNT IN X6 
   7 5?YL          SRL   67 2
  19 5#D=          BCHX  7  /                   [& POS OF LAST CHAR+1 IN X7  
  18 5#XW          LDX   5  ACOMMUNE3(1)        [SHIFT AT START OF BLOCK 
   7 5*CG          SLL  5  6 
  17 5*M# ...      ORN   5  #41                 [SET PFCC IN B18-23  
  17 5*X6          DSA   5  7                   [SET SHIFT IN B12-17 
  16 5BBQ          LDX   3  6                   [RECORD LENGTH   
  19 5BWB          STEP     ,0(3)               [GET PTR FOR APPENDING RECORD
  18 5CTL          STO   6  0(3)                [INSERT RECORD WORD COUNT
  21 5D*=          STO   7  1(3)                [INSERT LAST CHAR POS+1 AND STARTING 
  11 5DSW          MHUNTW   2,ADATA,CREADL   
  15 5F#G          ADN   2  CPDATA              [RECORD PTR  
  20 5FS6          ADN   3  2                   [MOVE APPEND PTR PASS RED TAPE   
  15 5G?Q          SBN   6  2                   [FOR RED TAPE
   7 5GRB          SMO      6
  16 5H?2          MOVE  2  0                   [APPEND RECORD   
   4 5HQL    #   
   8 5J==          STEP     ,2   
  15 5JPW          LDN   0  2                   [WORD COUNT  
  19 5K9G          STO   0  0(3)                [INSERT BLANK RECORD AFTER   
  17 5KP6                                       [TERMINATOR FOR CARDS
  21 5L8Q          STO   5  1(3)                [SECOND WORD OF DUMMY RECORD HEADER  
   8 5LNB    NSTO  ISITJOB  NOTIN
  18 5M82          BRN      NCLOS               [INPUT FROM WITHIN JOB   
   8 5MML    NOTIN TESTMULT STEM 
   6 5N7=          CLOSE 
   8 5NLW          BRN      NCLOS
   6 5P6G    STEM  CLOSE 
  19 5PL6          CLOSEMULT                    [MULTIFILING : CLOSE ELEMENT 
   9 5Q5Q    NCLOS CALL  5  SINFIN   
   6 5QKB          ENDCOM
   4 5R52    #   
  18 5RJL    #     RESTART REQUEST RECEIVED 'CANT DO' REPLY : ERASE FILE 
   4 5S4=    #   
  15 5SHW    NORES CALL  7  STOP                [STOP INPUT  
   9 5SRN ...      LDX   4  AWORK1(2)
  20 5T3G ...      BNG   4  NCLOS               [FILE FULL FLAG UP : FILE CLOSED 
   8 5T?# ...      TESTMULT STREM
  20 5TH6          CLOSEABANDON                 [CATERS FOR THE APPEND QUALIFIER 
   8 5W2Q          BRN      TISIS
   6 5WGB    STREM CLOSE 
  19 5X22          ERASEMULT                    [MULTIFILING : ERASE ELEMENT 
   8 5XFL    TISIS ISITJOB  NCLOS
  21 5X_=          SETERR                       [INDICATE ERROR TO COMMAND PROCESSOR 
   8 5YDW          BRN      NCLOS
   4 5YYG    #   
  20 5_D6    #     COMMON ROUTINES TO FIND AN AMXOR/IDATSG BLOCK CONTAINING INPUT
  21 5_XQ    #     FROM IDENTIFIER DEFINED IN THE AMXOR/ADCB BELONGING TO THIS CPA   
  19 62CB    #     AND CONVERT IT INTO LOCAL FORMAT IN ADATA/INPUSG BLOCKS   
   4 62X2    #   
  21 63BL    #     WAIT FOR INPUT BLOCK AND CONVERT IF VALID AND IDENTIFIER OPERABLE 
   4 63W=    #   
  10 64*W    WAIT  MHUNTW   3,AMXOR,ADCB 
   8 64KN ...      LDX   2  FX2  
   9 64L_ ...      LDX   0  AWORK4(2)
  16 64N= ...      BPZ   0  WAIT1               [J. ENTRY POINT 2
   9 64PH ...      LDX   0  FINISH   
  15 64QS ...      BNZ   0  OPOF                [J. FINISH   
   5 64S5 ...WAIT1   
   7 64TG          LDCT  0  4
  10 65*6          ANDX  0  ISTATUS(3)   
  21 65SQ          BNZ   0  STEND               [END OF DATA STREAM ON PREVIOUS INPUT
  20 668F ...      LDCT  4  #450                [IF IDENTIFIER IS INOPERABLE OR  
  20 66J8 ...      ANDX  4  ISTATUS(3)          [STOPPED BY STOP INPUT & CLEAR,  
  15 66XX ...      BNZ   4  TINOP               [J TO TINOP  
   8 67?L          LDCT  0  #400 
  20 67R=          ADN   0  #2000               [WAKE UP IF UNAVAILABLE RECEIVED 
  19 68=W          ORS   0  IWAIT(3)            [INDICATE 'ACTIVITY ASLEEP'  
  18 68QG          COOR1                        [WAIT FOR BLOCK TO APPEAR
   8 69=6          BRN      START
   4 69PQ    #   
  10 69WY ...NEWIN MHUNTW   3,AMXOR,ADCB 
   8 6=46 ...      LDCT  0  #40  
  10 6=9# ...      ANDX  0  ISTATUS(3)   
  20 6=BG ...      BNZ   0  NEWEN               [STOPPED-CLEARING DATA:SKIP START
  21 6=HN ...      LDN   5  #221                [START INPUT : TERMINATOR IN 2ND WORD
   7 6=P2          LDN   6  0
   8 6?8L          CALL  7  SUPER
  15 6?N=    NEWEN STOZ     AWORK3(2)           [NEW ENTRY   
   8 6#7W          LDN   3  20   
  10 6#MG          LDCH  7  JSOURCE1(2)  
  17 6*76          BNZ   7  SETU                [J IF INPUT FROM CR  
   8 6*LQ          ADN   3  12   
   8 6B6B          LDCT  0  #100 
   9 6BL2          ANDX  0  AWORK4(2)
  15 6C5L          BNZ   0  SETU                [GRAPHIC MODE
   8 6CK=          LDN   0  #74  
  21 6D4W          STO   0  AWORK3(2)           [INITIAL SHIFT FOR NORMAL OR ALLCHARS
  14 6DJG    SETU  SETNCORE CPDATA-A1(3),1,ADATA,CREADL  
  17 6F46          STOZ     CPDATA-1(1)         [FOR ROUTINE AT TRAIL
   7 6FHQ          SLL   3  2
  17 6G3B          STO   3  A1(1)               [INITIAL CHAR COUNT  
  20 6GH2    STBIG LDX   7  AWORK3(2)           [CURRENT SHIFT IS PREVIOUS SHIFT 
   8 6H2L          LDCT  4  #200 
  20 6HG=          ANDX  4  7                   [B1 SET IF JUST STORED 2000 CHARS
   9 6H_W          LDN   0  CPDATA   
  20 6JFG          STO   7  ACOMMUNE3(2)        [FOR NEXT RECORD APPENDED TO FILE
   9 6J_6          ERS   4  AWORK3(2)
   8 6KDQ          SBN   7  #75  
  18 6KYB          STO   0  ACOMMUNE6(2)        [INITIAL CREADL POINTER  
  21 6LD2          BNZ   4  START               [B0 WILL ALREADY BE SET IF NECESSARY 
  18 6LXL          BNZ   7  START               [CURRENT SHIFT NOT BETA  
   8 6MC=          LDCT  0  #400 
  21 6MWW          ORS   0  AWORK3(2)           [STORE ALPHA IF SHIFT-SENS CHAR. MET 
  17 6NBG    START MHUNTW   3,AMXOR,ADCB        [DEVICE CONTROL BLOCK
  19 6NW6          LDX   2  A1+FPTR(3)          [POINT TO AMXOR/ADEVS ENTRY  
  18 6P*Q          LDX   0  FOURTHWD(2)         [BIT 2 SET IF MOPPING OFF
  18 6PTB          ORX   0  FINISH              [B2 SET IF 'FINISH NOW'  
   7 6Q*2          SLL   0  2
   8 6QSL          BNG   0  OPOF 
  21 6QXQ ...      LDCT  0  #10                 [IF LINE/TERMINAL FAIL EVENT OCCURRED
  20 6R2W ...      ANDX  0  ISTATUS(3)          [(B5), UNSET FLAG & DEAL W/ EVENT
  10 6R62 ...      ERS   0  ISTATUS(3)   
   9 6R96 ...      BNZ   0  TLFAIL   
  16 6R#=          LDX   4  IMDNPU(3)           [MODE OF INPUT   
  11 6RRW          HUNTW    2,ADATA,INPUSG   
  21 6S?G          BNG   2  SEA                 [NO PARTIALLY DEALT WITH INPUT BLOCK 
   8 6SR6          LDX   1  FX2  
  21 6T=Q          LDX   6  A1(2)               [NO. OF CHARS STILL TO BE CONVERTED  
   8 6TQB          ANDN  6  #7777
   9 6W=2          LDX   0  A1+1(2)  
  10 6WPL          STO   0  ACOMMUNE4(1) 
   8 6X9=          BRN      USGAL
   4 6X#B ...[   
   8 6XCG ...[     K5  READBULK  
   4 6XGL ...[   
   6 6XKQ ...SK5READBULK 
  18 6XNW    SEA   HUNTW    2,AMXOR,IDATSG      [LOOK FOR ORDINARY DATA  
  17 6Y8G          BNG   2  WAIT                [INPUT BLOCK MISSING 
  10 6YN6          MHUNTW   3,AMXOR,ADCB 
   8 6_7Q          LDCT  6  #10  
   8 6_MB          LDCH  0  A1(2)
   9 7272          ANDX  6  IWAIT(3) 
   7 72LL          SRC   0  3
  20 736=          ERS   6  IWAIT(3)            [UNSET ONE SHOT MARKER , IF SET  
   8 73KW          BPZ   0  NOTER
  21 745G          FREECORE 2                   [B3 SET : INPUT DATA ENDED WITH SUB  
  19 74K6          BZE   6  SEA                 [NOT SET : MUST BE K2 ENTRY  
  20 754Q          CALL  7  ONE                 [WAS ONE SHOT : REQUEST ONE MORE 
   8 75JB          BRN      SEA  
   4 7642    #   
   8 76HL    NOTER LDX   6  A1(2)
   7 773=          SRC   0  1
  20 77GW          ANDN  6  #7777               [NO. OF CHARS IN INPUT SEGMENT   
  20 782G          BPZ   0  YES                 [NOT MARKED 'END OF DATA STREAM' 
  17 78G6          BNZ   6  NONUL               [NOT A NULL TRANSFER 
  17 78_Q          FREECORE 2                   [NULL TRANSFER BLOCK 
   8 79FB          BRN      STOVE
  16 79_2    STEND ERS   0  ISTATUS(3)          [UNSET B6 IF SET 
  20 7=DL ...#     END OF DATA STREAM : IF K2 ENTRY,DEMAND 'PLEASE ENGAGE' ELSE  
  10 7=Y= ...#     INDICATE END TO COMPRO
   9 7?CW ...STOVE BRN      RESTART  
   7 7*W2    NONUL LDCT  0  4
  21 7B*L          ORS   0  ISTATUS(3)          [MARK 'END OF DATA' FOR NEXT ENTRY   
   4 7BT=    #   
  21 7C#W    #     CONVERT INPUT INTO ADATA/CREADL BLOCK(S) ACCORDING TO DEVICE TYPE 
  11 7CSG    #     AND THE MODE OF IPB INPUT 
   4 7D#6    #   
  19 7DRQ    YES   BNZ   6  NOTND               [J IF NOT NULL DATA TRANSFER 
   7 7F?B          FREECORE 2
   8 7FR2          BRN      SEA  
   8 7G=L    NOTND LDN   0  A1+2 
   8 7GQ=          LDX   1  FX2  
  19 7H9W          STO   0  ACOMMUNE4(1)        [START OF AMXOR/IDATSG BLOCK 
  11 7HPG          NAME     2,ADATA,INPUSG   
  11 7J96    USGAL MHUNTW   3,ADATA,CREADL   
  10 7JNQ          STO   3  ACOMMUNE5(1) 
  21 7K8B          LDEX  5  A1(3)               [ROOM LEFT IF PARTIALLY FULL OR 128  
  21 7KN2          ADX   3  ACOMMUNE6(1)        [CPDATA OR DCH POINTER IF PART FULL  
  21 7L7L          ADX   2  ACOMMUNE4(1)        [LDCH POINTER IF PART USED OR A1+2   
  10 7LM=          SBS   2  ACOMMUNE4(1) 
  10 7M6W          LDCH  0  JSOURCE1(1)  
  18 7M#4 ...      BNZ   0  XCARD                [INPUT FROM CARD READER 
   5 7MF= ...TAPE1   
   4 7MLD ...TAPE
  19 7MRL ...      ACROSS  IPBTAPE,1           [DO CONVERSION FOR PAPER TAPE 
   5 7MYS ...XCARD   
   4 7N66    #   
  17 7NKQ    #     INPUT FROM REMOTE CARD READER ; NPU IS ONE CARD ; 
  21 7P5B    #     CONVERSION COMPLETE AFTER 80 CONVERTED CHARS OR INPUT EXHAUSTED   
  21 7PK2    #     COMPLETE LAST WORD WITH SPACES & REMOVE TRAILING WORDS OF SPACES  
   4 7Q4L    #   
   7 7QJ=          SLL   4  7
  18 7R3W          BNG   4  REMOT               [INPUT MODE 2 : REMOTE   
  18 7RHG          MOVE  2  20                  [LOCAL CARD READER FORMAT
  17 7S36          ADN   3  19                  [POINT TO LAST WORD  
  18 7SGQ          LDN   5  0                   [INITIAL REMAINDER COUNT 
   8 7T2B    #SKI  K6IPB>100-100 
   4 7TG2    (   
   8 7T_L          SBN   6  80   
  19 7WF=          BNZ   6  NBIG                [TP DID NOT PRODUCE 80 CHARS 
   4 7WYW    )   
   7 7XDG          LDN   6  0
  21 7XY6    TRAIL LDX   0  0(3)                [REMOVE TRAILING SPACE-FILLED WORDS  
   8 7YCQ          TXU   0  ACES 
  18 7YXB          BCS      TSCS                [WORD NOT SPACE FILLED   
   7 7_C2          ADN   5  4
  16 7_WL          BCT   3  TRAIL               [STEP BACK A WORD
  21 82B=    #              UPDATE REM COUNT FOR TRAILING SPACE CHARS IN LAST WORD   
  15 82TW    TSCS  ORX   3  CACT                [-> CHAR 3   
   8 83*G    NSCH  LDCH  0  0(3) 
   8 83T6          SBN   0  #20  
  16 84#Q          BNZ   0  TEND                [NOT SPACE CHAR  
  18 84SB          ADN   5  1                   [UPDATE REMAINDER COUNT  
   8 85#2          LDCT  0  #200 
  16 85RL          SBX   3  0                   [STEP BACK 1 CHAR
   9 86?=          TXL   3  BITS22LS 
  16 86QW          BCC      NSCH                [NOT -> CHAR 0   
   8 87=G          BRN      TEND 
   4 87Q6    #   
  19 889Q    REMOT LDX   1  FX1                 [REMOTE CARD READER FORMAT   
   8 88PB          LDN   5  80   
  15 8992          BZE   6  TEND                [BLANK CARD  
   8 89NL          LDN   4  #67  
   8 8=8=    RLOOP LDCH  0  0(2) 
   9 8=MW          TXU   0  SHIFT(1) 
  14 8?7G          BCS      STOR                [NOT #76 
   7 8?M6          BCHX  2  /
  20 8#6Q          SBN   6  1                   [COUNT OF CHARS IN IDATSG IN X6  
  18 8#LB          BZE   6  SPCH                [BLOCK FINISHED WITH #76 
   8 8*62          LDCH  0  0(2) 
  13 8*KL          TXL   0  T(1)                [#64 
   8 8B5=          BCS      NILL 
  13 8BJW          TXL   4  0                   [#67 
  20 8C4G          BCC      NADJ                [#63<X0<#70 : ADJUST CHARACTER   
  21 8CJ6    NILL  LDN   0  #10                 [IS1 (SUPPRESSED BLANK) OR ILL. CHAR 
   8 8D3Q    NADJ  ADN   0  #10  
   8 8DHB    STOR  DCH   0  0(3) 
   7 8F32          BCHX  3  /
   7 8FGL          BCHX  2  /
   8 8G2=          BCT   6  RCT  
   8 8GFW    SPCH  LDN   0  #20  
  21 8G_G          SBN   3  1                   [POINT TO LAST WORD AT END OF LOOP   
   7 8HF6 ...SPACE SBN   5  1
   9 8HYQ ...PLAST TXL   3  BITS22LS 
  20 8JDB ...      BCS      TRAIL               [REMOVE COUNT OF TRAILING SPACES 
  21 8JY2          DCH   0  1(3)                [SPACE FILL REMAINDER OF LAST WORD   
   8 8KCL          BCHX  3  SPACE
   8 8KPJ ...RCT   BCT   5  RLOOP
  10 8L3G ...#              > 80 CHARS   
  21 8L7Q ...      ORX   6  GSIGN               [SET B0 TO INDICATE REMOTE MODE 2 IN 
  21 8L#2 ...                                   [USE : EXTRA INFO FOR CCERROR MACRO  
   4 8LD= ...NBIG
  18 8LDT ...      LDX   5  XERR(1)        [LD 8CH RECON. FOR NOT 80 CHS 
   4 8LFD ...[   
   4 8LG3 ...[   
   8 8LGL ...[   COMERR ROUTINE  
   4 8LH9 ...[   
   5 8LHS ...XCERR   
   6 8LJG ...#UNS CCERROR
   8 8LKX ...      LDN   5  #17  
   9 8LM# ...      TRACE   5,CCERROR 
  16 8LNQ ...      CCERROR  5,6        [REPORT DCP PROTOCOL ERROR
  21 8LT2 ...                                   [EXTRA INFO : B0 SET&B1-10 UNSET =   
  20 8L_= ...                                   [MODE 2 IN USE;B11-23=NO OF CHARS
  15 8M5G ...                                   [> 80 (+OR-) 
  18 8M6X ...                                [IF WRONGILL EXTRA INFO :-  
  19 8M8# ...                                       [     REASON FOR ILLEGAL 
  21 8MMY ...      BRN      TLFAIL               [CD,MP OFF,OR FN NOW EVENT OCCURRED 
   4 #3QW    #   
  18 #4=G    #     END OF MESSAGE : STORE CHARACTER COUNT IN ADATA/CREADL
   4 #4Q6    #   
   4 #4T= ...[   
   8 #4YB ...[     K3  READBULK  
   4 #53G ...[   
   6 #56L ...SK3READBULK 
  21 #59Q    TMAR  LDCT  0  #200                [ENTRY FOR STORING 2000 CHAR RECORD  
  21 #5PB          ORS   0  AWORK3(1)           [INHIBIT REVERSION TO ALPHA FOR NEXT 
   4 #5R_ ...[   
   4 #5WJ ...[   
   8 #5_7 ...[     K4   READBULK 
   4 #63Q ...[   
   6 #66* ...SK4READBULK 
   8 #692    TEND  LDX   2  FX2  
  20 #6NL          LDX   3  ACOMMUNE5(2)        [START ADDRESS OF ADATA/CREADL   
  10 #78=          LDX   0  ALOGLEN(3)   
   9 #7MW          SBN   0  CPDATA-A1
   7 #87G          SLL   0  2
  18 #8M6          SBX   0  5                   [REMAINDER COUNT IN X5   
   8 #96Q          STO   0  A1(3)
  11 #9LB          MHUNTW   1,ADATA,INPUSG   
  19 #=62          BNZ   6 NOTOV                [INPUSG NOT FINISHED WITH YET
   7 #=KL          FREECORE 1
   8 #?5=          BRN      NOFIN
  10 #?JW    NOTOV LDX   0  ACOMMUNE4(2) 
  20 ##4G          STO   0  A1+1(1)             [PRESERVE POINTER THROUGH COMPRO 
   8 ##J6    NOFIN LDX   2  FX2  
   9 #*3Q          LDX   5  AWORK4(2)
   8 #*HB          ERN   5  #6000
  16 #B32          ANDS  5  AWORK4(2)           [UNSET B12 IF SET
  19 #BGL          BPZ   5  SLOOP               [K2 ENTRY : APPEND TO FILE   
  19 #C2=          UP                           [K1 ENTRY : RETURN TO COMPRO 
   4 #CFW    #   
  21 #C_G    #     MOP OFF IN PROGRESS : CRASH CLOSE IDENTIFIER , FREE RECENT INPUT  
   4 #DF6    #   
  10 #DS9 ...XMPOF MHUNTW   3,AMXOR,ADCB 
   7 #DYQ    OPOF  LDCT  0  4
  19 #FDB          ANDX  0  ISTATUS(3)          [B6 SET IF END OF DATA STREAM
  18 #FY2          BNZ   0  PREC                [CRASH CLOSE SENT ALREADY
   9 #GCL          SMO      IADIN(3) 
   9 #GX=          LDX   2  IPBRING  
  19 #HBW          LDCT  0  #420                [B0 : INOP ; B4 : MOP OFF,PM 
  11 #HWG          ANDX  0  IPBSW-IPBRING(2) 
  19 #JB6          BZE   0  SCC                 [J IF CRASH CLOSE NECESSARY  
   4 #JKY ...SCCX
   8 #JTQ          LDCT  0  #100 
  19 #K*B          ORS   0  ISTATUS(3)          [SIMULATE IDENTIFIER CLOSED  
   8 #KK8 ...      BRN      REDS 
   4 #KT2    SCC 
   8 #L#L          LDN   5  #127 
   8 #LS=          LDN   6  54   
  16 #M?W          CALL  7  SUPER               [SEND CRASH CLOSE
   7 #MRG    REDS  LDCT  0  4
  19 #N?6          ORS   0  ISTATUS(3)          [INDICATE END OF DATA STREAM 
   8 #NQQ ...PREC  LDX   2  FX2  
   9 #P=B ...      LDX   5  AWORK4(2)
   8 #PQ2          BRN      REPCD
   4 #Q9L    #   
  20 #QP=    #     EITHER IDENTIFIER INOPERABLE BECAUSE PERIPHERAL NOT ENGAGED OR
  21 #R3M ...#     ERROR RESTART NEEDED : CLEAR ANY REMAINING INPUT (EXCEPT IF LINE  
  18 #RB4 ...#     FAIL) THEN OUTPUT MESSAGE TO REMOTE CONSOLE AND TAKE  
  13 #RNG    #     ACTION APPROPRIATE TO THE REPLY   
   4 #S86    #   
  21 #S=H ...TINOP SLC   4  5                   [B5 SET IF LINE OR TERMINAL FAILURE  
   9 #S#Y ...      BNG   4  TLFAIL   
   7 #SC* ...      SRC   4  2
  21 #SFQ ...      BNG   4  RESTART             [IDEN ALREADY STOP INPUT & CLEARED   
   8 #SJ7 ...      LDCT  0  #10  
   9 #SLJ ...      ORS   0  IWAIT(3) 
  21 #SN_ ...      ERS   0  IWAIT(3)            [UNSET^ONE SHOT INPUT SENT^BIT IF SET
  19 #SRB ...      LDN   5  #124                [SEND STOP INPUT AND CLEAR   
  20 #STR ...      LDN   6  52                  [AND WAIT FOR INPUT CLEARED REPLY
   8 #SY8 ...      CALL  7  SUPER
   8 #T2K ...      LDCT  4  #40  
  21 #T52 ...      ORS   4  ISTATUS(3)          [SET IDEN STOPPED-CLEARING DATA FLAG 
  10 #T7C ...      HUNTW    3,AMXOR,#77  
  17 #T9S ...      BNG   3  NODATA              [J IF NO DATA CLEARED
  20 #T#9 ...RENAM NAME     3,AMXOR,IDATSG      [UNPRESERVE DATA BLOCKS CLEARED  
  10 #TBL ...      HUNTW    3,AMXOR,#77  
   8 #TF3 ...      BPZ   3  RENAM
  19 #THD ...      BRN      START               [J TO DEAL WITH DATA CLEARED 
   5 #TKT ...NODATA  
  10 #TN= ...      MHUNTW   3,AMXOR,ADCB 
  21 #TQM ...RESTART                            [RESET IDENTIFIER TO STARTED STATE   
   8 #TT4 ...      LDCT  4  #40  
  10 #TXF ...      ANDX  4  ISTATUS(3)   
  18 #T_W ...      BZE   4  XSTREAM             [IDENTIFIER NOT STOPPED  
  20 #W4? ...      ERS   4  ISTATUS(3)          [UNSET IDENTIFIER STOPPED FLAG   
   9 #W6N ...      LDX   5  AWORK4(2)
  20 #W95 ...      BNG   5  SOSI                [K1 ENTRY : SEND ONE SHOT INPUT  
  19 #W?G ...      LDN   5  #221                [K2 ENTRY : SEND START INPUT 
   7 #W*X ...      LDN   6  0
   8 #WD# ...      CALL  7  SUPER
   9 #WGP ...      BRN      XSTREAM  
   8 #WK6 ...SOSI  CALL  7  ONE  
   5 #WMH ...XSTREAM 
  10 #WPY ...      LDXC  4  ISTATUS(3)   
  20 #WS* ...      BCC      START               [IDENTIFIER HAS BECOME AVAILABLE 
  10 #WWQ ...      LDX   7  IDENTNO(3)   
  19 #W_7 ...      LONGSET  #36,SET,7           [PUT IDENTIFIER NO. IN CLONG2
   7 #X3J ...      SLL   4  4
  18 #X4Q ...      LDX   5  AWORK4(2)           [SET X5 FOR REPCD & TERM 
  21 #X5_ ...      BNG   4  RESET               [DEV. FAILURE: ERROR RESTART REQUIRED
  20 #X=R ...      BNG   5  RNGN                [END OF DATA AFTER COMPRO ENTRY  
  20 #X*N ...      CALL  0  PACOM               [PRESERVE ACOMMUNE3 & ACOMMUNE6  
   6 #XKG          OUTPER
  13 #Y56          DEMAND   0,JMARY,REPCD,TERM,YTER,2
  10 #Y?B ...      MFREEW   ADATA,ADEMAND
   9 #YFL ...      VFREEW   GMON,AOUT
   8 #YMW ...      CALL  0  RACOM
   8 #YW6 ...      BRN      START
  18 #_4B ...TLFAIL                             [LINE OR TERMINAL FAILURE
   8 *2H=          LDN   5  #127 
   8 *32W          LDN   6  54   
  15 *3GG          CALL  7  SUPER               [CRASH CLOSE 
   8 *426          LDN   0  #40  
   9 *4FQ          ANDX  0  IWAIT(3) 
  20 *4_B          BNZ   0  REDS                [ON-TEST REQUESTED : LEAVE CLOSED
   8 *5F2          LDN   5  #120 
   8 *5YL          LDN   6  48   
  13 *6D=          CALL  7  SUPER               [OPEN
   8 *6FH ...      LDCT  0  #40  
  21 *6GS ...      ORS   0  ISTATUS(3)          [IDENTIFIER NOW STOPPED:FLAG FACT(B3)
   4 *6J5 ...#   
  21 *6KB ...                                   [TO AID TERM OPERATOR WITH RESTART : 
  21 *6LM ...      SETERR                       [INDICATE ERROR TO COMMAND PROCESSOR 
  21 *6MY ...                                   [SO THAT THE IDENTIFIER IS WRONGED   
  20 *6P9 ...                                   [(SOON AFTER ENDCOM) IF K2 ENTRY 
   8 *6QG ...      LDN   0  #1000
  21 *6RR ...      ORS   0  IWAIT(3)            [SET WAITING TO BE WRONGED FLAG (B14)
  21 *6T4 ...                                   [SO THAT THE IDENTIFIER IS WRONGED IF
  14 *6W* ...                                   [K1 ENTRY
   8 *6XW          BRN      REDS 
   4 *7CG    #   
   6 *7X6    RESET OUTPER
  11 *8BQ          MHUNTW   1,ADATA,CREADL   
   8 *8WB          LDN   4  128  
  10 *9B2          LDN   0  32+CPDATA-A1 
  18 *9TL          STO   4  A1(1)               [START THIS BLOCK AGAIN  
   9 *=*=          LDN   3  CPDATA   
  10 *=SW ...      TXL   0  ALOGLEN(1)   
  20 *?#G          BCC      SHRAL               [CREADL HAS NOT BEEN LENGTHENED  
  11 *?S6          ALTLEND  1,32+CPDATA-A1   
  20 *#?Q    SHRAL STO   3  ACOMMUNE6(2)        [OPERATOR SHOULD REPOSITION TAPE 
  20 *#HJ ...      CALL  0  PACOM               [PRESERVE ACOMMUNE3 & ACOMMUNE6  
  21 *#RB          DEMAND   0,JRESET,REPCD,TERM,RETY,2  ['ENGAGE FOR ERROR RESTART'  
  10 **?2    STRID MFREEW   ADATA,ADEMAND
   9 **QL          VFREEW   GMON,AOUT
  20 *B2D ...      CALL  0  RACOM               [RESTORE ACOMMUNE3 & ACOMMUNE6   
   8 *B== ...      BNG   5  START
   8 *BG4 ...      BRN      NORES
   4 *BPW    #   
  16 *B_N ...TERM                               [INPUT TERMINATED
  20 *C9G ...      CALL  0  RACOM               [RESTORE ACOMMUNE3 & ACOMMUNE6   
   7 *CF# ...      LDN   4  0
  21 *CP6          BPZ   5  TFOUN               [K2 ENTRY : AS IF TERMINATOR FOUND   
   8 *D8Q          BRN      SDUM 
   4 *DNB ...#   
  18 *FML    REPCD BPZ   5  NORES               [K2 ENTRY : NO RESTART   
   4 *FXD ...RNGN
  16 *G7= ...      LONGOFF                      [IF NECESSARY.   
  16 *GC4 ...      NGN   4  1                   [REPLY 'CANT DO' 
  11 *GLW    SDUM  MHUNTW   3,ADATA,CREADL   
  18 *H6G          ALTLEND  3,2                 [SHORTEN FOR DUMMY BLOCK 
   8 *HL6          STO   4  A1(3)
  10 *TBL          MHUNTW   3,AMXOR,ADCB 
   8 *TW=          LDN   0  #40  
  19 *W*W          ANDX  0  IWAIT(3)            [B18 SET IF ON-TEST REQUESTED
   8 *WTG          BZE   0  NOTST
   8 *X*6          LDN   5  #127 
   8 *XSQ          LDN   6  54   
   8 *Y#B          CALL  7  SUPER
   8 *YS2          BRN      NOCC 
  19 *_?L    NOTST CALL  7  ONE                 [SEND ONE SHOT IF NECESSARY  
  19 *_R=    NOCC  UP                         [RETURN TO COMMAND PROCESSOR   
   4 B2=W    #   
  17 B3PQ    SET   GEOERR   1,ALRDYSET          [LONGSET GIVEN TWICE 
  11 B49B          MENDAREA 50,K99READBULK   
   4 B4P2    #END
   8 ____ ...66135113000100000000
