  15 22FL    #SEG  PAGES8                       [A C PUTMAN  
  15 22PD ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   7 22_=    #OPT  K0PAGES=0 
  14 23DW    #LIS  K0PAGES>K0PERI>K0COMMAND>K0GREATGEO   
   4 23YG    #   
   9 24D6                   8HPAGES  
   4 24XQ    #   
  20 25CB    #     THIS SEGMENT SERVICES THE PAGES COMMAND FOR ERROR RESTARTS ON 
  20 25X2    #     LINEPRINTERS DURING LISTFILES. THE ACTLF OWNING THE DEVICE IS 
  20 26BL    #     LOCATED BY SEARCHING ROUND THE LISTFILE SCHEDULER'S JOB-RING. 
  20 26W=    #     IF THIS ACTLF HAS AN EXTANT ^PLEASE GIVE PAGES.... ^ REQUEST  
  20 27*W    #     THE NUMBER OF PAGES GIVEN TO THE COMMAND IS CHECKED AGAINST   
  20 27TG    #     ACOMMUNE6 OF THE ACTLF, AND, IF GREATER, A COMMAND ERROR IS   
  18 28*6    #     REPORTED. OTHERWISE THE NUMBER IS STORED IN ACOMMUNE6.
  20 28SQ    #     THE PERIPHERAL DESCRIPTION PARAMETER IS ANALYSED BY PARSORTX  
   4 29#B    #   
   4 29S2    #   
   4 2=?L    #   
  11 2=R=          SEGENTRY K1PAGES,X1PAGES  
   4 2?=W    #   
   4 2?QG    #   
   9 2#=6    XNUMB          8HNUMBER 
  17 2#PQ    XREQ           36HPLEASEGIVEPAGESFORERRORRESTARTONUNIT  
  11 2*9B    #     ERROR MESSAGE IDENTIFIERS 
  17 2*P2    XJPARMIS       +JPARMIS            [PARAMETER MISSING   
  17 2B8L    XJTOOMANY      +JTOOMANY           [TOO MANY PARAMETERS 
  14 2BN=    XJNUMBER1      +JNUMBER1           [NUMBER  
  18 2C7W    XJEXT          +JEXT               [NO SUCH SYSTEM REQUEST  
  17 2CMG    XJPAGE1        +JPAGE1             [TOO MANY PAGES GIVEN
  16 2D76    XJMIN6         +JMIN6              [NO OF PAGES < 1 
  19 2DLQ    XJMULT         +JMULT              [PERIPHERAL TYPE NOT ALLOWED 
  16 2F6B    XJNOPAR        +JNOPAR             [NO PARAMETERS   
   4 2FL2    #   
  14 2G5L    #                                  *  *   * 
  14 2GK=    #                                  * *   ** 
  14 2H4W    #                                  **     * 
  14 2HJG    #                                  * *    * 
  14 2J46    #                                  *  *  ***
   4 2JHQ    #   
  17 2K3B    X1PAGES                            [THE ONLY ENTRY POINT
  18 2KH2          LDN   5  0                   [PARAMETER ANALYSIS LEVEL
  19 2L2L          PARANUMB 4,5                 [FIND NO OF PARAMETERS GIVEN 
   9 2LG=          BZE   4  NOPARAMS 
   7 2L_W          SBN   4  3
  20 2MFG          BPZ   4  TOOMANY             [ERROR IF MORE THAN 2 PARAMETERS 
  17 2M_6          LDN   3  1                   [1ST PARAMETER WANTED
  18 2NDQ          LDN   7  10                  [LOOK FOR ^NUMBER^ FIRST 
   5 2NYB    NUMBER  
   7 2PD2          SBN   7  4
  18 2PXL          PARABEGX 3,7,XNUMB(1)        [PASS NUMBER PARAMETER   
  10 2QC=          MHUNT    2,CPB,CUNI   
   9 2QWW          NGX   0  ANUM(2)  
  15 2RBG          BNG   0  YNUMB               [BR IF FOUND 
   9 2RW6          BCT   7  NUMBER   
  19 2S*Q          BRN      NONUMBER            [NO NUMBER PARAMETER - ERROR 
   5 2STB    YNUMB   
  16 2T*2          LDX   6  JPARNUM(2)          [PARAMETER NUMBER
  17 2TSL          CHNUMCOD                     [CONVERT PARAMETER   
  15 2W#=          TESTREP2 CHNUMERR,TEND       [BR IF FAILED
  10 2WRW          NGX   0  ACOMMUNE1(2) 
  17 2X?G          BPZ   0  TERROR              [ERROR IF -VE OR ZERO
  18 2XR6          NGS   0  AWORK1(2)           [STORE NUMBER OF PAGES   
   8 2Y=Q    #SKI  K6PERI>99$99  
  12 2YQB          TRACE    AWORK1(2),PAGENUMB   
  20 2_=2          ERN   6  3                   [X6 = PERIPHERAL PARAMETER NUMBER
  20 2_PL          PARSORTX XBRK,6              [ANALYSE PERIPHERAL DESCRIPTION  
  10 329=          MHUNTW   3,CPAR,JSCE  
  17 32NW          BNG   3  TEND                [BR IF IT WAS WRONG  
  15 338G          LDCH  0  JSCEA(3)            [DEVICE TYPE 
   7 33N6          SBN   0  2
  18 347Q          BNZ   0  NOTYPE              [ERROR IF NOT LINEPRINTER
  18 34MB          GEOPACK  4,JSCEA(3)          [UNIQUE DEVICE IDENTIFIER
   8 3572    #SKI  K6PERI>99$99  
   9 35LL          TRACE    4,GEOPER 
   4 35N* ...#   
   5 35Q4 ...TRYAGAIN
   4 366=    #   
  16 36KW    #     AWORK1 NOW CONTAINS THE NUMBER OF PAGES GIVEN 
  18 375G    #     X4 CONTAINS THE GEOPACKED JSOURCE WORDS FOR THE DEVICE
  20 37K6    #     NOW TO LOCATE THE ACTLF OWNING THE DEVICE BY SEARCHING ROUND  
  19 384Q    #     THE LISTFILE SCHEDULER'S JOB RING VIA THE LFPACTLF MACRO  
   4 38JB    #   
  15 39HL          LFPACTLF 4,2                 [LOCATE ACTLF
  16 3=3=          BNG   2  NOMORE              [BR IF NOT FOUND 
   4 3=3W ...#   
   5 3=4G ...XNOTB1  
   4 3=GW    #   
  20 3?2G    #     X2 NOW POINTS TO THE ACTLF OWNING THE PERIPHERAL SPECIFIED IN 
  20 3?G6    #     THE ORIGINAL PAGES COMMAND. WE NOW HAVE TO SEE WHETHER OR NOT 
  11 3?_Q    #     IT IS INTERESTED IN US.   
   4 3#FB    #   
   9 3#_2          LDEX  0  CLONG1(2)
  19 3*DL          SBN   0  #104                [BR IF ACTLF NOT LOOKING FOR 
  15 3*Y=          BNZ   0  NOTRIGHT            [PAGE RESTART
  14 3BCW          LDX   6  ACOMMUNE6(2)        [IPAGE?  
   8 3BXG          SMO      FX2  
  17 3CC6          LDX   5  AWORK1              [NO OF PAGES GIVEN   
   7 3CWQ          TXL   6  5
  16 3DBB          BCS      SURFEIT             [BR IF TOO MANY  
  20 3DW2          STO   5  ACOMMUNE6(2)        [ELSE STORE NUMBER OF PAGES GIVEN
   7 3F*L    #SKI  APAGESMK7 
  17 3FT=          LONGON1  #104,4              [AND WAKE UP ACTIVITY
   8 3G#W    #SKI  APAGESMK7<1$1 
   4 3GSG    (   
   5 3H#6    TRYNWAKE
  10 3HRQ          LONGCLEA 2,#104,WAIT  
   8 3J?B          BRN      WOKEN
   4 3JR2    WAIT
  16 3K=L          COOR3    #62                 [WAIT 4 SECONDS  
  16 3KQ=          BRN      TRYAGAIN            [THEN TRX AGAIN  
   4 3L9W    )   
   5 3LPG    WOKEN   
   4 3M96    TEND
  15 3MNQ          ENDCOM                       [THEN PACK UP
   4 3N8B    #   
  13 3NN2    #     BELOW WE HAVE VARIOUS ERROR LABELS
   4 3P7L    #   
  17 3PM=    SURFEIT                            [TOO MANY PAGES GIVEN
  16 3Q6W          OUTPACK  6,1,NUMA            [OUTPUT PARAMETER
  10 3QLG          LDX   6  XJPAGE1(1)   
   7 3R66          NGN   7  1
  16 3RKQ          BRN      REPORT              [NO SUB-MESSAGE  
   4 3S5B    #   
  18 3SK2    NOPARAMS                           [NO PARAMETERS SPECIFIED 
  16 3T4L          LDX   6  XJNOPAR(1)          [PRIMARY MESSAGE 
  16 3TJ=          NGN   7  1                   [NO SUB-MESSAGE  
   9 3W3W          BRN      REPORT   
   4 3WHG    #   
  17 3X36    TOOMANY                            [TOO MANY PARAMETERS 
  10 3XGQ          LDX   6  XJTOOMANY(1) 
  16 3Y2B          NGN   7  1                   [NO SUB-MESSAGE  
   9 3YG2          BRN      REPORT   
   4 3Y_L    #   
  18 3_F=    NONUMBER                           [NUMBER PARAMETER MISSING
  10 3_YW          LDX   6  XJPARMIS(1)  
  10 42DG          LDX   7  XJNUMBER1(1) 
   9 42Y6          BRN      REPORT   
   4 43CQ    #   
  18 43XB    TERROR                             [NO OF PAGES GIVEN < 1   
   9 44C2          LDX   6  XJMIN6(1)
  18 44WL          NGN   7  1                   [SUB-MESSAGE IN CPB,CUNI 
   9 45B=          BRN      REPORT   
   4 45TW    #   
  18 46*G    NOTYPE                             [INVALID PERIPHERAL TYPE 
   9 46T6          LDX   6  XJMULT(1)
  19 47#Q          LDX   4  JSCEQ(2)            [PERIPHERAL PARAMETER NUMBER 
  16 47SB          LDN   5  0                   [ANALYSIS LEVEL 0
  17 48#2          SPARANOX 4,5                 [REACCES PARAMETER   
   7 48RL          NGN   7  1
   9 49?=          BRN      REPORT   
   4 49QW    #   
  16 4==G    NOMORE                             [NO ACTLF FOUND  
   5 4=Q6    NOTRIGHT
   9 4?9Q          LDX   6  XJEXT(1) 
   7 4?PB          NGN   7  1
   5 4#92    REPORT  
  15 4#NL          COMERRX  6,7                 [REPORT ERROR
   4 4*8=    #   
   4 4*MW    XBRK
  10 4B7G          GEOERR   1,PA BREAK   
   4 4BM6    #   
   5 4C6Q    #PAGE   
  10 4CLB          MENDAREA 40,K100PAGES 
   4 4D62    #END
   8 ____ ...43650474000100000000
