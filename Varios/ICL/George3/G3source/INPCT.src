  16 22FL ...#SEG  INPCT84                       [PHILIP TURNER  
  10 22_=    #LIS  K0INPCT>K0PCT>K0ALLGEO
   9 23DW                   8HINPCT  
   4 23YG    #   
  11 24D6          SEGENTRY K1INPCT,SK1INPCT 
  11 24XQ          SEGENTRY K2INPCT,SK2INPCT 
   4 25CB    #   
   9 25X2    NEWLINE        #76322020
   6 26BL    X76   #76   
   4 26W=    #   
  19 27*W    #     UPDATE BUFFER NUMBER , ALLOWING FOR OVERFLOW ; CALL ON X0 
   4 27TG    #   
   7 28*6    PUTUP ADN   4  1
  19 28SQ          TXU   4  BUFFNUM(2)          [CONTAINS NUMBER OF BUFFERS  
   8 29#B          BCS      (0)  
   7 29S2          LDN   4  0
   7 2=?L          EXIT  0  0
   4 2=R=    #   
  19 2?=W    #     SUBROUTINE TO RE-FIND BLOCK BEING LENGTHENED BY ALTLENG   
   4 2?QG    #   
  11 2#=6    REH   MHUNTW   2,ADATA,CREADL   
   7 2#PQ          EXIT  1  0
   4 2*9B    #   
  21 2*P2    # SUBROUTINE TO SLEEP, AND FLIP FOR ^PLEASE ENGAGE^ ACTION IF DC4 READ. 
   4 2B8L    #   
   8 2BN=    SLEEP SBX   6  FX1  
   8 2C7W    XSLP  SMO      FX2  
  14 2CMG          LDX   5  ACOMMUNE4           [SAVE IT 
  12 2D76          SLEEP    IN,XBRKI,XCANT,XFIN  
  10 2DLQ          STO   5  ACOMMUNE4(2) 
   7 2F6B          ADX   6  1
   7 2FL2          EXIT  6  0
   6 2G5L    XFIN  FLIP  
   8 2GK=          BRN      XSLP 
   4 2H4W    #   
  18 2HJG    #     ENTRY POINT FROM COMMAND PROCESSOR VIA READLINE MACRO 
   4 2J46    #   
   5 2JHQ    SK1INPCT
  18 2K3B ...      LDCT  0  #520                [READ TAPE IN NORMAL MODE
  10 2KH2          LDCH  4  JSOURCE1(2)  
   8 2L2L          BNZ   4  NAC  
   8 2LG= ...      LDCT  0  #400 
  19 2L_W    NAC   STO   0  AWORK3(2)           [B0 INDICATES READLINE ENTRY 
   8 2MFG          BRN      Z1   
   4 2M_6    #   
  20 2NDQ    #     ENTRY FROM PREPREAD : CONVERT INPUT RECORDS AND APPEND TO FILE
   4 2NYB    #   
   5 2PD2    SK2INPCT
   9 2PXL          STOZ     AWORK3(2)
   7 2QC=          TOPFCB2  3
   9 2QWW          LDEX  0  FETM(3)  
   9 2RBG          DCH   0  AWORK3(2)
  10 2RW6          MHUNTW   2,A7020,INPUT
   8 2S*Q          LDCT  4  #200 
  18 2STB          ERS   4  EWAIT(2)            [UNSET B1 FOR BURST MODE 
   4 2T*2    #   
  14 2TSL    Z1    SETNCORE CPDATA-A1+34,3,ADATA,CREADL  
   9 2W#=          STOZ     AWORK4(2)
  17 2WRW          STOZ     CPDATA-1(3)         [FOR ROUTINE AT TRAIL
  10 2X?G          STOZ     ACOMMUNE4(2) 
   8 2XR6          LDCT  0  #100 
   9 2Y=Q          ANDX  0  AWORK3(2)
  20 2YQB          BNZ   0  NEWREC              [B2 SET IF CARDS OR GRAPHIC TAPE 
   8 2_=2          LDN   0  #74  
  21 2_PL          STO   0  AWORK4(2)           [INITIAL SHIFT FOR NORMAL & ALLCHARS 
   8 329=          LDN   0  #7400
  21 32NW          STO   0  ACOMMUNE4(2)        ['PREVIOUS SHIFT' FOR FIRST APPEND   
   4 338G    #   
   5 33N6    NEWREC  
  10 347Q          MHUNTW   2,A7020,INPUT
  19 34MB          LDXC  4  COMMON1(2)          [TEST IF PRESENT BUFFER NOT  
  16 3572          BCC      USED                [COMPLETELY USED 
   8 35LL          CALL  0  PUTUP
   7 366=          SMO   4   
  10 36KW          LDXC  6  COMMON2(2)   
   8 375G          BCC      XERR1
   7 37K6          LDN   5  0
   7 384Q          DSA   6  5
  20 38JB          SRL   6  12                  [COUNT OF CHARS TRANSFERRED IN X6
  20 3942          SBX   5  6                   [COUNT OF CHARS TO TRANSFER IN X5
  18 39HL          BZE   5  XEBUF             [J IF END OF THIS BUFFER   
   7 3=3=          SRC   6  2
   7 3=GW          LDX   1  6
   8 3?2G          BRN      XON  
   4 3?G6    #   
  16 3?_Q    XEBUF CALL  0  PUTUP             [GET TO NEXT BUFFER
   7 3#FB          LDN   1  0
   8 3#_2          BRN      XON  
   7 3*DL    USED  LDN   1  0
  17 3*Y=          CALL  0  PUTUP               [UPDATE BUFFER NUMBER
   7 3BCW          SMO   4   
  19 3BXG          LDXC  5  COMMON2(2)          [INPUT BUFFER COUNT IF FILLED
  20 3CC6          BCC      SLEP                [B0 SET IF FILLED : SLEEP IF NOT 
   4 3CWQ    XON 
   7 3DBB          SLL   4  5
   7 3DW2          SMO   4   
  10 3F*L          ADN   1  COMMON2(2)   
  10 3FT=          ADX   1  BUFFNUM(2)   
   8 3G#W          LDN   7  128  
  11 3GSG          MHUNTW   3,ADATA,CREADL   
   9 3H#6          ADN   3  CPDATA   
   8 3HRQ          LDN   6  #6000
  20 3J?B          SMO      FX2                 [BIT 12 IF DISCARDING THIS BLOCK 
  19 3JR2          ANDX  6  AWORK3              [BECAUSE MET TAB OUT OF RANGE
   8 3K=L          SLL   6  12   
   9 3K*Q ...      LDX   0  EWAIT(2) 
   8 3KDW ...      ANDX  0  BIT10
   9 3KJ2 ...      ERS   0  EWAIT(2) 
   8 3KM6 ...      BRN      XPVRN
   4 3KQ=    #   
  12 3L9W    #     START OF MAIN TRANSFER LOOP   
   4 3LPG    #   
   7 3M3X ...XOOP  LDN  0  0 
   8 3MB# ...XPVRN LDX   2  FX2  
   7 3MNQ          LDCT  4  1
   9 3MSM ...      ANDX  4  AWORK3(2)
   9 3MYJ ...      ERS   4  AWORK3(2)
   8 3N4F ...      BNZ   0  XDC4 
  20 3N8B          LDCT  0  #100                [B2 SET IF CARDS OR GRAPHIC TAPE 
   9 3P7L          ANDX  0  AWORK3(2)
   9 3Q6W          BNZ   0  XGRAPHIC 
   8 3QLG          BZE   4  XOK  
   8 3R66          BZE   5  PCTX1
   8 3RKQ          BRN      XLASD
   4 3S5B    XOK 
   8 3SK2    XN6   LDCH  4  0(1) 
   8 3T4L    XN7   SMO      FX1  
   8 3TJ=          TXU   4  X76  
   8 3W3W          BCC      XDELT
   8 3WHG          LDN   0  #74  
   7 3X36          TXL   4  0
  15 3XGQ          BCS      XN4                 [CHAR < #74  
  16 3Y2B          LDEX  0  AWORK4(2)           [CURRENT SHIFT   
  20 3YG2          STO   4  AWORK4(2)           [CANCEL B0 IF SET OR CHANGE SHIFT
  20 3Y_L          BNG   6  XNOF                [DISCARD SHIFT : TAB OUT OF RANGE
   7 3_F=          TXU   4  0
  18 3_YW          BCC      XNOF                [= CURRENT SHIFT : IGNORE
   8 42DG          BRN      XN5  
   8 42Y6    XN4   BNG   6  XNOF 
  16 43CQ          ADN   6  1                   [TAB-SIG COUNT   
  21 43XB          LDXC  0  AWORK4(2)           [B0 SET IF LAST BLOCK ENDED IN BETA  
  21 44C2          BCC      XN5                 [SHIFT AND NO SHIFT SENS CHAR SINCE  
   8 44WL          LDN   0  #41  
  19 45B=          TXL   4  0                   [NOT SHIFT-SENSITIVE IF LESS 
   8 45TW          BCS      XN5  
   8 46*G          LDN   0  #74  
  21 46T6          STO   0  AWORK4(2)           [CHANGE TO ALPHA SHIFT AND UNSET B0  
  19 47#Q ...      DCH   0  0(3)                [ENTRY TO STORE DELTA PAIR   
   7 47SB          BCHX  3  /
   8 48#2          BCT   7  XN5  
  18 48RL          SBN   6  1                   [COUNTERACT ADN ON RETURN
  21 49?=          LDN   0  1                   [MOVE X1 BACK 1 CHAR IF JUMP TO PTENT
   8 49QW          BRN      PTENT
   4 4==G    XN5 
  17 4=Q6          DCH   4  0(3)                [IF NO,DUMP IN BUFFER
   7 4?9Q    XN2   SBN   7  1
   7 4?PB          BCHX  3  /
  21 4#92          BZE   7  PTLINE              [J IF 128 CHARS HAVE BEEN INSERTED   
   7 4#NL    XNOF  SBN   5  1
  19 4*8=          BZE   5  PCTX                [J IF PCT BUFFER EXHAUSTED   
   8 4*MW          BCHX  1  XOK  
   4 4B7G    #   
  15 4BM6    #     DELTA PAIR IN NORMAL OR ALLCHARS TAPE MODE
   4 4C6Q    #   
   4 4CLB    #   
  20 4D62    XDELT SBN   5  1                   [J IF #76 LAST CH I.N PCT BUFFER 
   8 4DKL          BZE   5  PCTX1
  19 4F5=          BCHX  1  /                   [P.U. 2ND CHAR OF #76 PAIR   
   8 4FJW    XLASD LDCH  4  0(1) 
  21 4G4G ...      LDCT  0  #200                [B1 SET IF ALLCHAR ; B2 IF GRAPHIC   
   9 4GJ6          ANDX  0  AWORK3(2)
  15 4H3Q          BZE   0  NOTAC               [NORMAL TAPE 
   7 4HHB          LDX   0  4
   8 4J32          SBN   0  #20  
  20 4JGL          BZE   0  XNFIR               [STORE RUNOUTS IN ALLCHARS MODE  
   8 4K2=          SBN   0  #53  
  20 4KFW          BZE   0  XNFIR               [STORE DELETES IN ALLCHARS MODE  
   8 4K_G    NOTAC LDN   0  #64  
   7 4LF6          TXL   4  0
   8 4LYQ          BCS      XN1  
   8 4MDB          LDN   0  #73  
   7 4MY2          TXU   4  0
  18 4NCL          BCC      XNOF                [IGNORE #7673 = DELETE   
   8 4NX=          LDN   0  #76  
   7 4PBW          TXU   4  0
  19 4PWG          BCC      XDELT               [#7676 : IGNORE SECOND #76   
   7 4QB6          ADN   6  1
   8 4QTQ    XNFIR LDN   0  #76  
  21 4R*B          BNG   6  XNOF                [DISCARD DELTA PAIR IF LARGE TAB MET 
  15 4RF? ...      DCH   0  0(3)                [DEPOSIT #76 
   7 4RK8 ...      BCHX  3  /
  21 4RP5 ...      BCT   7  XN5                 [STORE RES OF PAIR UNLESS CREAD FULL 
   8 4RT2 ...      SMO      FX2  
  21 4RYX ...      STO   4  ACOMMUNE9           [REMEMBER STORE REST OF PAIR LATER   
  18 4S4S ...      BRN      NEWCREAD            [GO TO LENGTHEN CREADL   
  21 4S8P ...[                                  [X5 LEFT SO REFIND LAST CHAR ON RETUR
   4 4S#L    #   
  15 4SS=    #     COMMON ROUTINES FOR ANALYSING DELTA PAIRS 
   4 4T?W    #   
   7 4TRG    XN1   SBN   4  7
  18 4W?6          BNG   4  XDC4                [J IF 7604 : END OF DATA 
  20 4WQQ          BZE   4  XETB                [J IF 7607 : END OF THIS BLOCK   
   8 4X=B          SBN   4  #10  
  21 4XQ2          BZE   4  XIS1                [J IF IS1 : SUPPRESSED BLANK IF CARDS
  18 4Y9L          BNG   4  XISX                [J IF OTHER IS CHARACTER 
   7 4YP=          SBN   4  1
  18 4_8W          BZE   4  NULL                [IGNORE IF NULL CHARACTER
   7 4_NG          SBN   4  3
  21 5286          BZE   4  XETB                [J IF 7623 = ETX : END OF THIS DATA  
  19 52MQ          BNG   4  STX                 [J IF 7622 : START OF MESSAGE
   7 537B          SBN   4  1
  21 53M2          BZE   4  XETB                [J IF 7624 = EOT : END OF DATA STREAM
   7 546L          SBN   4  4
  18 54L=          BZE   4  TBACK               [J IF 7630 : BACKSPACE   
   7 555W          SBN   4  1
  19 55KG          BZE   4  TABEX               [J IF 7631 : HORIZONTAL TAB  
   7 5656          SBN   4  4
  19 56JQ          BZE   4  TCARR               [J IF 7635 : CARRIAGE RETURN 
  18 574B          BRN      XIS                 [NEWLINE = END OF RECORD 
   4 57J2    #   
  15 583L    STX   SBN   5  1                   [IGNORE STX  
  19 58H=          BZE   5  XERR1               [ERROR IF END BUFFER ON STX  
   8 592W          BCHX  1  XOOP 
  18 59GG    TBACK SBN   6  1                   [DECREMENT TAB-SIG COUNT 
   8 5=26          ADN   4  #30  
   8 5=FQ          BRN      XNFIR
  18 5=_B    TCARR LDN   6  0                   [ZEROISE TAB-SIG COUNT   
   8 5?F2          ADN   4  #35  
   8 5?YL          BRN      XNFIR
  10 5#D=    XIS1  LDCH  0  JSOURCE1(2)  
  18 5#XW          BZE   0  XIS1X               [STORE IS CHARS IF TAPE  
   8 5*CG ...      LDN   4  #20  
  18 5*X6          BRN      XG5                 [CARD : SUPPRESSED BLANK 
   4 5BBQ    #   
   5 5BWB    XGRAPHIC
  20 5CB2          BZE   4  XOK1                [LAST BUFFER DID NOT END WITH #76
   8 5CTL          BZE   5  PCTX1
  20 5D*=          BRN      XG6                 [LAST CHAR OF PREV BUFFER WAS #76
   4 5DSW    XOK1
   8 5F#G          LDCH  4  0(1) 
   8 5FS6          SMO      FX1  
   8 5G?Q          TXU   4  X76  
   8 5GRB          BCC      XG1  
   8 5H?2          LDN   0  #74  
  18 5HQL          TXL   4  0                   [IGNORE #74,#75 OR #77   
   8 5J==          BCC      XG2  
   8 5JPW    XG5   BNG   6  XG2  
   8 5K9G          DCH   4  0(3) 
  20 5KP6          ADN   6  1                   [COUNT OF TAB-SIG CHARS FOR TAPE 
   7 5L8Q          SBN   7  1
   7 5LNB          BCHX  3  /
   9 5M82          BZE   7  PTLINE   
   7 5MML    XG2   SBN   5  1
   8 5N7=          BZE   5  PCTX 
   8 5NLW          BCHX  1  XOK1 
   7 5P6G    XG1   SBN   5  1
   8 5PL6          BZE   5  PCTX1
   7 5Q5Q          BCHX  1  /
   8 5QKB    XG6   LDCH  4  0(1) 
   8 5R52          LDN   0  #64  
   7 5RJL          TXL   4  0
   8 5S4=          BCS      XN1  
   8 5SHW          ADN   4  #10  
   8 5T3G          LDN   0  #100 
   8 5TH6          BXL   4  0,XG5
   7 5W2Q    NULL  SBN   5  1
   8 5WGB          BZE   5  PCTX 
   8 5X22          BCHX  1  XOOP 
   4 5XFL    #   
  21 5X_=    #     EXPAND #7631 = HORIZ TAB INTO SPACES ACCORDING TO TAB FIELDS SPEC.
   4 5YDW    #   
  18 5YYG    TABEX STO   1  ACOMMUNE7(2)        [PRESERVE LDCH POINTER   
  10 5_D6          HUNTW    2,AINPAR,ATB 
  18 5_XQ          LDN   4  #31                 [FOR DCH IF JUMP TO XNOT 
  20 62CB          BNG   2  XNOT                [NO TAB PARAMETER : STORE PAIR   
   7 62X2          ADN   6  1
  17 63BL          LDX   1  A1(2)               [NO. OF TAB FIELDS   
   9 63W=    TABAG TXL   6  A1+1(2)  
  19 64*W          BCS      THIST               [THIS IS THE RELEVANT FIELD  
   7 64TG          ADN   2  1
   8 65*6          BCT   1  TABAG
   7 65SQ          ADN   7  1
   8 66#B          LDN   0  #6000
   8 66S2          SMO      FX2  
  20 67?L          ORS   0  AWORK3              [SET DISCARD REST OF BLOCK MARKER
  21 67R=          LDCT  6  #600                [COUNT TOO BIG : IGNORE REST OF BLOCK
   8 68=W          BRN      ONLY1
   9 68QG    THIST LDX   1  A1+1(2)  
   7 69=6          SBX   1  6
  17 69PQ          LDX   6  A1+1(2)             [UPDATE TAB POINTER  
   7 6=9B          SBN   6  1
  21 6=P2    RINRE TXL   7  1                   [REENTRY PT AFTER CREADL LENGTHENED  
  20 6?8L          BCC      ROMOK               [ENOUGH ROOM IN CREADL FOR SPACES
   7 6?N=          SBX   1  7
   8 6#7W          SMO      FX2  
  21 6#MG          DEX   1  AWORK3              [SPACES TO BE INSERTED AFTER ALTLEN  
  20 6*76          LDX   1  7                   [STORE SPACES TO END OF CREADL   
   8 6*LQ    ROMOK LDN   0  #20  
   7 6B6B          SBN   1  1
   8 6BL2          DCH   0  0(3) 
   7 6C5L          LDX   2  3
   7 6CK=          BCHX  3  /
  17 6D4W          BZE   1  ONLY1               [STORE ONLY ONE SPACE
  18 6DJG          MVCH  2  0(1)                [STORE REQUISITE SPACES  
   7 6F46          SBX   7  1
   8 6FHQ    ONLY1 LDX   2  FX2  
  18 6G3B          LDX   1  ACOMMUNE7(2)        [RETRIEVE LDCH POINTER   
   8 6GH2          BCT   7  XNOF 
   9 6H2L          BRN      PTLINE   
   8 6HG=    XNOT  LDX   2  FX2  
   8 6H_W          BRN      XNFIR
   4 6JFG    #   
  21 6J_6    #     END OF MESSAGE : IF ENTERED FROM COMPRO GO UP ; IF ENTERED FROM   
  18 6KDQ    #     PREPREAD STORE RECORD IN FILE AND LOOK FOR NEXT INPUT 
   4 6KYB    #   
  16 6LD2    XISX  LDCH  0  JSOURCE1(2)         [STORE IS IF TAPE
   8 6LXL          BNZ   0  XIS  
   8 6MC=    XIS1X LDCT  0  #100 
  17 6MWW          ANDX  0  AWORK3(2)           [AND NOT GRAPHIC MODE
   8 6NBG          BNZ   0  XIS  
   8 6NW6          ADN   4  #17  
   8 6P*Q          BRN      XNFIR
   4 6PTB    XIS 
   8 6Q*2          LDCT  6  #600 
  19 6QSL          ANDX  6  3                   [POSITION OF NEXT CHARACTER  
   7 6R#=          SBN   5  1
   8 6RRW          LDN   0  #20  
   7 6S?G          SBN   3  1
   9 6SR6    SPACE TXL   3  BITS22LS 
  18 6T=Q          BCS      YG                  [J IF X3 POINTS TO CHAR 0
  21 6TQB          DCH   0  1(3)                [SPACE-FILL REMAINDER OF LAST WORD   
   7 6W=2          SBN   7  1
   8 6WPL          BCHX  3  SPACE
   8 6X9=    YG    LDX   2  FX2  
  10 6XNW          LDCH  0  JSOURCE1(2)  
   8 6Y8G          BZE   0  SENDA
   8 6YN6          LDX   0  ACES 
   7 6_7Q          LDN   6  0
  21 6_MB    TRAIL TXU   0  0(3)                [REMOVE TRAILING SPACE-FILLED WORDS  
   8 7272 ...      BCS      SECHR
   7 72LL          ADN   7  4
  16 736=          BCT   3  TRAIL               [STEP BACK A WORD
  18 73KW    SEND  LDN   6  0                   [FOR MAX RECORD OR CARDS 
   8 73LP ...      BRN      SENDA
   4 73MJ ...[   
   4 73NC ...[   
  18 73P= ...[  NOW GO THROUGH LAST WORD TO FIND LAST NON SPACE CHARACTER
  12 73Q5 ...[  AND SET X6 TO CHAR POSN AFTER IT 
   4 73QY ...[   
   4 73RR ...[   
   7 73SL ...SECHR SMO      3
  17 73TF ...      ERX   0  0               [SET SPACE CHARS TO ZERO 
   7 73W# ...      LDN   6  4
   5 73X7 ...NEXSPA  
   7 73Y2 ...      LDCH  2  0
   8 73YT ...      BNZ   2  NSECH
   7 73_N ...      SRL   0  6
  14 742H ...      BCT   6  NEXSPA     [TEST NEXT CHAR   
  16 743B ...NSECH ANDN  6  3          [NOW MAKE X6 CHAR POINTER 
   7 7449 ...      SRC   6  2
   5 745G    SENDA   
  10 74K6          MHUNTW   2,A7020,INPUT
   8 754Q          LDX   4  GSIGN
  21 75JB          ORS   4  COMMON1(2)          [INDICATE BUFFER NOT COMPLETELY USED 
  10 7642          ERX   4  COMMON1(2)   
   8 76HL          CALL  0  PUTUP
   7 773=          SMO   4   
  10 77GW          LDX   3  COMMON2(2)   
  18 782G          ANDN  3  #7777               [COUNT OF CHARS IN BUFFER
   7 78G6          LDX   0  3
  20 78_Q          SBX   3  5                   [GET COUNT OF CHARS TRANSFERRED  
   8 796Y ...      BPZ   3  ZCTOK
   8 79#6 ...      LDX   3  BIT10
   9 79F# ...      ORS   3  EWAIT(2) 
   7 79LG ...      LDN   3  1
   8 79RN ...ZCTOK SLL   3  12   
   7 79_2          ADX   3  0
   8 7=DL          ORX   3  GSIGN
   7 7=Y=          SMO   4   
  10 7?CW          STO   3  COMMON2(2)   
   4 7?XG    #   
  11 7#C6          MHUNTW   2,ADATA,CREADL   
  19 7#WQ          LDX   4  ALOGLEN(2)          [CPDATA-A1+2 + MULTIPLE OF 32
  20 7*BB          SBN   4  CPDATA-A1+2         [GET DATA WORD COUNT ROUNDED UP  
   7 7*W2          SLL   4  2
   8 7B*L          BNG   7  RFULL
   7 7BT=          SBX   4  7
   5 7C#W    RFULL   
   8 7CSG    #SKI  K6PCT>99-99   
   4 7D#6    (   
   8 7DRQ          LDX   1  FX2  
  10 7F?B          LDCH  0  JSOURCE1(1)  
  15 7FR2          BZE   0  OKCARDS             [TAPE INPUT  
   8 7G=L          LDN   0  80   
   7 7GQ=          TXL   0  4
  18 7H9W          BCC      OKCARDS             [NOT MORE THAN 80 COLUMNS
  17 7HPG          GEOERR   1,LONGCARD          [MORE THAN 80 COLUMNS
   5 7J96    OKCARDS 
   4 7JNQ    )   
   8 7K8B          SMO      FX2  
   9 7KN2          LDXC  0  AWORK3   
  19 7L7L          BCC      PREP                [K2 ENTRY : APPEND TO FILE   
  21 7LM=          STO   4  A1(2)               [K1 ENTRY : STORE CHAR CT. AND GO UP 
   5 7M6W          UP
  17 7MLG    PREP  ADN   4  11                  [RED TAPE + ROUNDING 
   8 7N66          ANDN  0  #6000
   8 7NKQ          SMO      FX2  
  21 7P5B          ERS   0  AWORK3              [UNSET 'DISCARD REST OF BLOCK' MARKER
  15 7PK2          SRL   4  2                   [WORD COUNT  
   9 7Q4L          ADN   2  CPDATA   
   8 7QJ=          LDCH  0  0(2) 
   8 7R3W          SBN   0  #74  
   8 7RHG          BNZ   0  NOL  
   7 7S36          BCHX  2  /
  20 7SGQ    NOL   LDN   3  5                   [MOVE FIRST FOUR CHARS INTO X5   
   7 7T2B          MVCH  2  4
   8 7TG2          LDX   2  FX2  
   9 7T_L          TXU   5  AWORK2(2)
   7 7W49 ...#UNS  JPGLTER   
   4 7W6S ...(   
  16 7W9C ...      BCC      TERMF               [TERMINATOR FOUND
  15 7W#2 ...      TXU   5  GTERM               [UNIVERSAL?  
   4 7WBK ...)   
  16 7WF=          BCS      TERX                [TERMINATOR FOUND
   6 7WLD ...#UNS JPGLTER
   5 7WRL ...TERMF   
   9 7WYW          LDEX  7  AWORK1(2)
   8 7XDG          SBN   7  #64  
  19 7XY6          BZE   7  NSTO                [J IF TERM NOT TO BE STORED  
   8 7YCQ          BRN      TX   
   7 7YXB    TERX  LDN   7  0
  21 7_C2    TX    ADX   6  ACOMMUNE4(2)        [SHIFT AT START OF BLOCK IN CHAR 2   
   5 7_WL    XFULL   
   7 82B=          LDX   2  4
   8 82TW          STEP     ,0(2)
  14 83*G          TESTREP2 FILEFULL,XERR,APPWAIT,WAIT   
  13 83T6          ADN   6  #41                 [PFCC
  16 84#Q          LDEX  0  AWORK4(2)           [CURRENT SHIFT   
   8 84SB          STO   6  1(3) 
   7 85#2          SLL   0  6
  15 85RL          STO   4  0(3)                [WORD COUNT  
  20 86?=          STO   0  ACOMMUNE4(2)        [WILL BE PREVIOUS SHIFT NEXT TIME
   7 86QW          SBN   4  2
  11 87=G          MHUNTW   2,ADATA,CREADL   
  21 87Q6          BZE   4  NULEN               [NULL ENTRY : NEWLINE OR BLANK CARD  
   9 889Q          ADN   2  CPDATA   
   7 88PB          ADN   3  2
   7 8992          SMO      4
   7 89NL          MOVE  2  0
   9 8=8=          SBN   2  CPDATA   
   5 8=MW    NULEN   
  18 8?7G          BNZ   7  TY                  [J IF THIS IS TERMINATOR 
  18 8?M6          ALTLEND  2,CPDATA-A1+34      [SHORTEN FOR NEXT RECORD 
  16 8#6Q          BRN      NEWREC              [GET NEXT RECORD 
   4 8#LB    #   
  16 8*62    #     FILE FULL : OPEN ANOTHER STREAM IF MULTIFILE  
   4 8*KL    #   
  12 8B5=    XERR  INPUTFULL XBRK1,XFULL,JOVER   
   9 8BJW          CALL  5  SINFIN   
   6 8C4G          ENDCOM
   8 8CJ6    WAIT  LONGSTOP XBRK 
   8 8D3Q          BRN      XFULL
   6 8DHB    XBRK  CLOSE 
   9 8F32    XBRK1 CALL  5  SINFIN   
   7 8FGL          ABANDCOM  
   5 8G2=    SINFIN  
   6 8GFW          INFINB
   7 8G_G          EXIT  5  0
   4 8HF6    #   
  21 8HYQ    #     READER DEALLOCATED : IF K1 ENTRY INDICATE END OF DATA TO COMPRO ; 
  12 8JDB    #     IF K2 ENTRY REQUEST MORE INPUT
   4 8JY2    #   
  11 8KCL    XDC4  MHUNTW   2,ADATA,CREADL   
  10 8KX=          LDX   0  ALOGLEN(2)   
  10 8LBW          SBN   0  CPDATA-A1+2  
   7 8LWG          SLL   0  2
  21 8MB6          ADN   5  2                   [START WITH DC4 NEXT TIME IF J TO XIS
  18 8MTQ          TXU   0  7                   [EQUAL IF CREADL EMPTY   
  19 8N*B          BCS      XIS                 [DEAL WITH RECORD UP TO DC4  
  18 8NT2          MHUNTW   2,A7020,INPUT       [IF FILE OPEN : REQUEST  
  18 8P#L          LDXC  4  COMMON1(2)          [MORE INPUT.FIRST UPDATE 
   7 8PS=          LDN   0  0
   8 8Q?W          CALL  0  PUTUP
  10 8QRG          STO   4  COMMON1(2)   
  16 8R?6          SMO      4                   [FREE THE BUFFER.
  10 8RQQ          STOZ     COMMON2(2)   
   7 8S=B          LDN   0  1
  10 8SQ2          ADS   0  FREBUFF(2)   
   8 8T9L          SMO      FX2  
   9 8TP=          LDXC  0  AWORK3   
  18 8W8W          BCS      XCD                 [K1 ENTRY : END OF DATA  
   7 8WNG    SLEP  LDX   3  2
  17 8X86          CALL  6  SLEEP               [WAIT FOR MORE INPUT.
   9 8XMQ          BRN      NEWREC   
   4 8Y7B    #   
  21 8YM2    #     TERMINATOR FOUND : IF S-TYPE APPEND IT AND DUMMY (NEWLINE) RECORD 
  13 8_6L    #     THEN CLOSE FILE AND END COMMAND   
   4 8_L=    #   
  10 925W    TY    MFREEW   ADATA,CREADL 
  14 92KG          LDX   5  ACOMMUNE4(2)        [SHIFT   
   8 9356          ADN   5  #41  
   8 93JQ          BRN      TZ   
  10 944B    XBRKI STO   5  ACOMMUNE4(2) 
   8 94J2    XBRKIN SMO     FX2  
   9 953L          LDXC  0  AWORK3   
  19 95H=          BCS      XTERM               [K1 ENTRY : INPUT TERMINATED 
   5 962W    TFOUND  
   8 96GG          LDX   1  FX2  
  18 9726          LDXC  4  AWORK1(1)           [REMOVE TOP BIT IF SET   
  20 97FQ          LDN   0  0                   [TEST IF S OR T TYPE TERMINATOR  
   8 97_B          SBN   4  #64  
   8 98F2          BZE   4  NSTO 
  19 98YL          LDX   6  AWORK2(1)           [S TYPE : STORE TERMINATOR.  
  18 99D=          LDX   5  ACOMMUNE4(1)        [SHIFT AT START OF BLOCK 
  15 99XW          LDN   4  3                   [WORD COUNT  
  13 9=CG          ADN   5  #41                 [PFCC
   8 9=X6          STEP     ,3   
   7 9?BQ          LDN   2  4
  17 9?WB          MOVE  2  3                   [INSERT TERMINATOR   
   7 9#B2    TZ    LDN   4  2
   8 9#TL          STEP     ,2   
  21 9**=          STO   4  0(3)                [BLANK RECORD FOR CARDS OR GRAP TAPE 
   8 9*SW          STO   5  1(3) 
   8 9B#G    NSTO  ISITJOB  NX4  
   8 9BS6          BRN      XJOB 
   7 9C?Q    NX4   LDN   4  0
   8 9CRB          TESTMULT STREM
   7 9D?2          LDN   4  1
   6 9DQL    STREM CLOSE 
   8 9F==          BNZ   4  XJOB 
   7 9FPW          CLOSEMULT 
   4 9G9G    XJOB
  18 9GP6          HUNTW    2,A7020,INPUT       [UPDATE REMEMBERANCES.   
   8 9H8Q          LDCT  0  #200 
  18 9HNB          ORS   0  EWAIT(2)            [RESET SINGLE-SHOT BIT   
   9 9J82          CALL  5  SINFIN   
  14 9JML          ENDCOM                       [FINISH  
   4 9K7=    #   
  17 9KLW    #     PCT BUFFER EXHAUSTED : FIND REST OF THIS RECORD   
   4 9L6G    #   
   8 9LL6    XETB  LDN   0  #75  
   9 9M5Q          TXU   0  AWORK4(2)
  19 9MKB          BCS      PCTX                [NOT CURRENTLY IN BETA SHIFT 
   8 9N52          LDCT  0  #400 
  21 9NJL          ORS   0  AWORK4(2)           [TEST SHIFT ON FIRST CHAR OF NEXT BLK
   8 9P4=          BRN      PCTX 
  17 9PHW    PCTX1                              [END BUFFER ON #76   
   7 9Q3G          LDCT  0  1
   8 9QH6          SMO      FX2  
  20 9R2Q          ORS   0  AWORK3              [INDICATE BUFFER ENDED WITH #76  
  10 9RGB    PCTX  HUNTW    2,A7020,INPUT
  10 9S22          LDXC  4  COMMON1(2)   
   7 9SFL          LDX   0  0
   8 9S_=          CALL  0  PUTUP
  18 9TDW          STO   4  COMMON1(2)          [MOVE ON TO NEXT BUFFER  
  18 9TYG          SMO      FX2                 [STORE NON-TAB-SIG COUNT 
  17 9WD6          STO   6  ACOMMUNE6           [IN CASE CALL SLEEP  
  17 9WXQ          LDX   6  3                   [SAVE DCH POINTER.   
  16 9XCB          LDX   3  2                   [BUFFER BLOCK.   
  20 9XX2          WAKEUP   IN                  [WAKE UP PCT ACTIVITY IF WAITING.
  17 9YBL          LDX   2  3                   [RESTORE POINTERS.   
   7 9YW=          LDX   3  6
   7 9_*W          SMO      4
  10 9_TG          STOZ     COMMON2(2)   
   8 =2*6          CALL  0  PUTUP
   4 =2SQ    Y12 
   7 =3#B          SMO   4   
  10 =3S2          LDXC  5  COMMON2(2)   
  19 =4?L          BCS      XOOP2               [NEXT BUFFER FILLED ALREADY  
   7 =4R=          LDX   3  2
   8 =5=W          LDCT  0  #100 
   9 =5QG          ANDX  0  EWAIT(2) 
  19 =6=6          BZE   0  SL                  [NO ERROR RESTART REQUIRED   
  11 =6PQ          MHUNTW   2,ADATA,CREADL   
  10 =79B          LDN   0  CPDATA-A1+34 
  18 =7P2          LDN   7  128                 [START THIS BLOCK AGAIN  
  10 =88L          TXU   0  ALOGLEN(2)   
  20 =8N=          BCC      SL                  [CREADL HAS NOT BEEN LENGTHENED  
  11 =97W          ALTLEND  2,CPDATA-A1+34   
   8 =9MG    SL    CALL  6  SLEEP
  11 ==76          MHUNTW   3,ADATA,CREADL   
  20 ==LQ          ADX   3  ALOGLEN(3)          [RESET POINTER IN OUTPUT BLOCK   
   9 =?6B          ADN   3  CPDATA-4 
   7 =?L2          SLC   3  2
   7 =#5L          SBX   3  7
   7 =#K=          SRC   3  2
  10 =*4W          MHUNTW   2,A7020,INPUT
   8 =*JG          BRN      Y12  
   5 =B46    XOOP2   
   7 =BHQ          SLL   4  5
   7 =C3B          SMO      4
  10 =CH2          LDN   1  COMMON2(2)   
  19 =D2L          ADX   1  BUFFNUM(2)          [GET START ADDRESS OF BUFFER 
   8 =DG=          SMO      FX2  
  21 =D_W          LDX   6  ACOMMUNE6           [NON-TAB-SIGNIFICANT COUNT IF TAPE.  
   8 =FFG          BRN      XOOP 
   4 =F_6    #   
  21 =GDQ    #     ADATA/CREADL FULL BEFORE NEWLINE OR OTHER TRANSMISSION CHAR. FOUND
  20 =GYB    #     INCREASE ADATA/CREADL BY 32 WORDS AND CONTINUE CONVERSION ;   
  20 =HD2    #     MAX. SIZE 504 WORDS : WHEN REACHED , STEP AND INSERT RECORD   
   4 =HXL    #   
  20 =JC=    PTENT ADX   5  0                   [NO. OF CHARS TO MOVE BACK IN X0 
   7 =JWW          SLC   1  2
   7 =KBG          SBX   1  0
   7 =KW6          SRC   1  2
   5 =L*Q    PTLINE  
   8 =LFM ...      LDX   2  FX2  
  20 =LKJ ...      NGS   2  ACOMMUNE9(2)        [SHOW NOT IN MIDDLE OF DELTA PAIR
   5 =LPF ...NEWCREAD
  10 =LTB          MHUNTW   2,A7020,INPUT
  16 =M*2          SBX   1  2                   [RELATIVISE AND  
  19 =MSL          LDX   4  1                   [PRESERVE A7020/INPUT POINTER
  11 =N#=          MHUNTW   1,ADATA,CREADL   
   7 =NRW          SBX   3  1
  10 =P?G          LDX   2  ALOGLEN(1)   
  10 =PR6          LDN   0  CPDATA+502-A1
   7 =Q=Q          TXL   2  0
  21 =QQB          BCC      SEND                [MAX SIZE REACHED : STEP AND INSERT  
   8 =R=2          ADN   2  32   
   8 =RPL          LDN   7  128  
   7 =S9=          TXL   2  0
  19 =SNW          BCS      SALT                [NOT YET 504 : INCREASE BY 32
  19 =T8G          LDN   2  CPDATA+502-A1       [ALREADY 480 WORDS INSERTED  
  15 =TN6          LDN   7  80                  [500-480*4   
   8 =W7Q    SALT  SMO      FX2  
   9 =WMB          STO   2  ACOMMUNE8
   8 =X72          LDX   2  FX2  
  12 =XLL          ALTLENG  1,ACOMMUNE8(2),REH   
  11 =Y6=          MHUNTW   2,ADATA,CREADL   
   7 =YKW          ADX   3  2
  20 =_5G          MHUNTW   1,A7020,INPUT       [RESET POINTER IN BUFFER BLOCK.  
   7 =_K6          ADX   1  4
   8 ?24Q          LDX   2  FX2  
  21 ?2JB          LDEX  0  AWORK3(2)           [REMAINDER COUNT IF CAME VIA TABEX   
  19 ?2MG ...      BNZ   0  XRSPA               [J IF SPACES LEFT FROM TABEX 
  10 ?2NH ...      LDX   4  ACOMMUNE9(2) 
  20 ?2PJ ...      BPZ   4  XN5                 [J IF NEED TO FINISH DELTA PAIR  
   8 ?2QL ...      LDCT  0  #100 
   9 ?2TQ ...      ANDX  0  AWORK3(2)
   8 ?2YW ...      BZE   0  XNOF 
  16 ?342 ...[                                   J IF NOT GRAPHIC
   7 ?376 ...      SBN   5  1
   8 ?3== ...      BZE   5  PCTX 
  16 ?3*B ...      BCHX  1  XOK1                [J FOR NEXT CHAR 
   5 ?3DG ...XRSPA   
  16 ?3HL          STO   1  ACOMMUNE7(2)        [FOR LDX AT ONLY1
   7 ?43=          LDX   1  0
   7 ?4GW          LDN   0  0
   9 ?52G          DEX   0  AWORK3(2)
  19 ?5G6          BRN      RINRE               [INSERT REST OF THE SPACES   
   4 ?5_Q    #   
   8 ?6FB    XCANT SMO      FX2  
   9 ?6_2          LDXC  0  AWORK3   
  18 ?7DL          BCS      XCD                 [K1 ENTRY : END OF DATA  
  17 ?7Y=          TESTMULT STEM                [TEST IF STREAMS OPEN
   8 ?8CW          CLOSEABANDON  
   8 ?8XG          BRN      STIS 
   6 ?9C6    STEM  CLOSE 
   7 ?9WQ          ERASEMULT 
   4 ?=BB    STIS
  21 ?=W2          ISITJOB  XJOB                [J IF NOT ISSUED INTERNALLY BY JOB   
   6 ??*L          SETERR
  14 ??T=          BRN      XJOB                [ENDCOM  
   4 ?##W    #   
  16 ?#SG    XTERM LDN   4  0                   [INPUT TERMINATED
   8 ?*#6          BRN      SDUM 
  19 ?*RQ    XCD   NGN   4  1                   [INDICATE END OF DATA STREAM 
  11 ?B?B    SDUM  MHUNTW   3,ADATA,CREADL   
  18 ?BR2          ALTLEND  3,2                 [SHORTEN FOR DUMMY BLOCK 
   8 ?C=L          STO   4  A1(3)
   5 ?CQ=          UP
   4 ?D9W    #   
  10 ?DPG    XERR1 GEOERR   1,BUFMIXUP   
   4 ?F96    #   
  11 ?FNQ          MENDAREA APCTGAP,K100INPCT
   4 ?G8B    #END
   2 ?GN2
   8 ____ ...65107151000900000000
