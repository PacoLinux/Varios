  10 22FL ...      SEG      WRITEFIP,867 
   4 22_=    #   
  15 23DW    #   COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982
   4 23YG    #   
   4 25CB    #   
  12 25X2    # SEGMENT TO REWRITE OR CREATE FIP  
  17 26BL    # ACTION  DETERMINED BY MARKERS IN EXMARK OF EXOF BLOCK 
  19 26W=    #     ENTERED VIA DOWN FROM CLOSBAC/RENAMEX TO REWRITE FIP -WITH
  13 27*W    #     PARAMS BRITE+BSERL+BSPEC+BLOCK,   
  17 27TG    #     OR VIA ACROSS FROM SOUP TO CREATE FIP -WITH PARAMS
  17 28*6    #     BCREATE+BSPEC+(OPTIONALLY)BSERL+(OPTIONALLY)BSUPP 
  20 28SQ    # OUTPUT IS REPLY OK OR EXFAIL ,IF EXFAIL MARKERS FOR REASON SET IN 
  12 29#B    # EMARK OF GEXD BLOCK AS FOLLOWS:   
  18 29S2    #      ABREAK = BREAK-IN WHILST WAITING FOR SU TO BE LOADED 
  13 2=?L    #               OR SCALO TO BE CLAIMED  
  17 2=R=    #      AFAIL  = TRANSFER FAIL WHILST READING FIP CELLS  
  16 2?=W    #               OR EXOFILE SECURITY CHECK FAILURE   
  16 2?QG    #      APLINKF= IPB FAILURE - SCALO NOT AVAILABLE   
  18 2#=6    #      ANONL  = UNIT NOT ONLINE(PLEASE LOAD MESS SUPPRESSED)
  15 2#PQ    #      ARITE  = CD REPLY TO PLEASE LOAD REQUEST 
  14 2*9B    #      ANFOUND= FILE NOT FOUND(BRITE ONLY)  
  13 2*P2    #      ANOROOM= NO ROOM TO CREATE FIP   
  16 2B8L    #      ANINUSE= FILE ALREADY EXISTS(BCREATE ONLY)   
   4 2BN=    #   
   4 2C7W    #   
  19 2CMG          SEGENTRY K1WRITEFIP,QK1WRITEFIP  [REWRITE FIP (VIA DOWN)  
  21 2D76          SEGENTRY K2WRITEFIP,QK2WRITEFIP  [CREATE FIP (ACROSS FROM SOUP)   
   4 2DLQ    #   
   4 2F6B    #   
   9 2FL2    XEWR           +EWFULS  
  16 2G5L    XEWN           +EWNOL              [UNIT NOT ONLINE 
   9 2GK=    XEWI           +EWINUSE 
  18 2H4W    XEWCS          +EWCONSEC           [CANT CREATE ON SECURE SU
  19 2HJG    XEWCI          +EWCONINS           [CANT CREATE ON INSECURE SU  
  20 2J46    XEWTC          +EWTCEE             [TRAPS CLOSED FOR EXOFILE ENTRY  
  20 2JHQ    XEWNE          +EWSUNE             [SU UNSUITABLE - NO EXOF ENTRY   
  19 2K3B    XEWNT          +EWSUTC             [SU UNSUITABLE - TRAPS CLOSED
   4 2KH2    #   
   9 2L2L    NX5            #37777777
   4 2LG=    #   
   9 2L_W    XER1  GEOERR   1,EXOTRAN
   4 2MFG    #   
  14 2M_6    #     S/R TO SET X2 POINTING TO EWDAS/EXOF  
  14 2NDQ    #            AND X3 POINTING TO EWDAS/GEXD  
   4 2NYB    #   
  10 2PD2    XHUNT MHUNTW   2,EWDAS,EXOF 
  10 2PXL          MHUNTW   3,EWDAS,GEXD 
   7 2QC=          EXIT  5  0
   4 2QWW    #   
   4 2RBG    #   
  14 2RW6    #              S/R TO FIND AND GET NEXT S.U.
   9 2S*Q    #              LINK X7  
  20 2STB    #              ON  ENTRY AND EXIT   X1  POINTS TO CURRENT SU  ASTUC 
  16 2T*2    #                                   X2  EXOF BLOCK  
  16 2TSL    #                                   X3  GEXD BLOCK  
   4 2W#=    #   
  10 2WRW    #              USES X4,X5   
   4 2X?G    #   
   5 2XR6    NEXTSU  
   8 2Y=Q          SBX   7  FX1  
   7 2YQB          LDN   4  0
   9 2_=2    NX2   LDX   1  FPTR(1)  
   8 2_PL          TXU   1  CXCC 
  17 329=          BCC      NX1                 [JUMP IF END OF CHAIN
   9 32NW          LDX   0  BACK1(1) 
  18 338G          BZE   0  NX1                 [JUMP IF NO MORE ASTUCS  
  17 33N6          BNG   0  NX2                 [JUMP IF DEAD ASTUC  
   8 347Q          LDCT  0  #200 
   9 34MB          ANDX  0  ASMARK(1)
  16 3572          BNZ   0  NX2                 [OR DISENGAGED   
   9 35LL          LDX   0  EXMARK(2)
   9 366=          ANDN  0  BCREATE  
  19 36KW          BZE   0  NX3                 [JUMP IF NOT CREATING FILE   
  19 375G          LDCT  0  #100                [CHECK SCRATCH INHIBIT BIT   
   9 37K6          ANDX  0  HSTUN(1) 
  18 384Q          BNZ   0  NX2                 [JUMP IF SCRATCH INHIBIT 
  16 38JB    NX3   LDX   5  BACK1(1)            [SAVE SERIAL NO  
  21 3942          USESTX   5,NX9,,TBREAK,NX9   [ADD ONE TO COUNT OF USERS AND GET SC
  10 39HL          MHUNTW   3,EWDAS,EXOF 
   9 3=3=          LDX   0  EXMARK(3)
   9 3=GW          ANDN  0  BCREATE  
  19 3?2G          BZE   0  NXES9               [J IF FILE NOT BEING CREATED 
  14 3?G6          LDN   0  CAPPEND             [APPEND  
  18 3?_Q          STO   0  AWORK2(2)           [TRAPS TO BE CHECKED FOR 
  21 3#FB          EXCHOFE  ICL,5,AWORK2(2),NXES9   [DETERMINE IF EXOF ENTRY HAS TRAP
  16 3#_2          PICKREP  6                   [PRESERVE REPLY  
  16 3*DL          TESTREP2 OK,NXES1,NOFILE,NXES2,NOTRAP,NXES3   
   8 3*Y=          BRN      XER1 
   5 3BCW    NXES2   
   8 3BXG          LDX   0  EDSQ 
  19 3CC6          BNG   0  NXES7               [J IF FULL SECURITY IN USE   
   5 3CWQ    NXES1   
  14 3DBB          LDN   0  CWRITE              [WRITE   
  18 3DW2          STO   0  AWORK2(2)           [TRAPS TO BE CHECKED FOR 
  20 3F*L          EXCHOFE  EXOF,5,AWORK2(2),NXES9  [DETERMINE IF EXOF ENTRY HAS 
  20 3FT=          TESTREP2 OK,NXES4,NOFILE,NXES5,NOTRAP,NXES6  [CORRECT TRAPS   
   8 3G#W          BRN      XER1 
   5 3GSG    NXES4   
  15 3H#6          PUTREP   6                   [OLD REPLY   
  18 3HRQ          TESTREP2 OK,NXES4A           [OK+OK MEANS OK TO GO ON 
  21 3J?B          LDX   6  XEWCI(1)            [CANT CREATE SECURE ON INSECURE SU   
   8 3JR2          BRN      NXES8
   5 3K=L    NXES4A  
  21 3KQ=          EXCHOFE  ICL,5,,NXES9,AW     [D&T WRITTEN+ACCESSED FOR ICLSCAFILE 
  21 3L9W          EXCHOFE  EXOF,5,,NXES9,AW    [ ^     ^   ^   ^      ^  EXOF ENTRY 
   8 3LPG          BRN      NXES9
   5 3M96    NXES5   
   7 3MNQ          PUTREP   6
  20 3N8B          TESTREP2 NOFILE,NXES9        [NOFILE+NOFILE MEANS OK TO GO ON 
  21 3NN2          LDX   6  XEWCS(1)            [CANT CREATE INSECURE ON SECURE SU   
   8 3P7L          BRN      NXES8
   5 3PM=    NXES6   
  19 3Q6W          LDX   6  XEWTC(1)            [EXOFILE ENTRY'S TRAPS CLOSED
   8 3QLG          BRN      NXES8
   5 3R66    NXES3   
  19 3RKQ          LDX   6  XEWNT(1)            [SU UNSUITABLE - TRAPS CLOSED
   8 3S5B          BRN      NXES8
   5 3SK2    NXES7   
  20 3T4L          LDX   6  XEWNE(1)            [SU UNSUITABLE - NO EXOF ENTRY   
   5 3TJ=    NXES8   
   8 3W3W          OPTMESS  6,5  
  16 3WHG          UNLOCKSU 5                   [UNLOCK THIS ONE 
   8 3X36          BRN      NX9  
   5 3XGQ    NXES9   
  18 3Y2B          ADN   7  1                   [UPDATE LINK TO EXIT OK  
  10 3YG2          MHUNTW   3,EWDAS,GEXD 
  20 3Y_L          STO   5  ESERL(3)            [SAVE SERIAL NUMBER OF THIS UNIT 
  18 3_F=    NX1   BZE   4  NX10                [JUMP IF NO UNIT SAVED   
  17 3_YW          RELST    4                   [OTHERWISE RELEASE IT
   8 42DG    NX10  CALL  5  XHUNT
   8 42Y6          ADX   7  FX1  
   7 43CQ          EXIT  7  0
   4 43XB    #   
  15 44C2    #     SCALO NOT AVAILABLE OR IN USE BY S-TRUSTED
   4 44WL    #   
  18 45B=    NX9   BZE   4  NX11                [JUMP IF NO UNIT SAVED   
   7 45TW          RELST    4
  18 46*G    NX11  LDX   4  5                   [SAVE NEW UNIT SERIAL NO.
  15 46T6          MFINDSU  1,4                 [REFIND ASTUC
   8 47#Q          CALL  5  XHUNT
  18 47SB          BRN      NX2                 [JUMP TO GET NEXT UNIT   
   4 48#2    #   
  19 48RL    #     BREAKIN WHILE WAITING FOR LOCK-OUT FROM OTHER PROCESSOR   
   4 49?=    #   
   5 49QW    TBREAK  
   8 4==G          CALL  5  XHUNT
   9 4=Q6          LDN   0  ABREAK   
  17 4?9Q          ORS   0  EMARK(3)            [SET BREAK IN MARKER 
   8 4?PB          BRN      NX1  
   4 4#92    #   
   4 4#NL    #   
   4 4*8=    #   
  12 4*MW    #     READ  S/R LINK X5 ADDRESS X1  
   4 4B7G    #   
   5 4BM6    XREAD   
  14 4C6Q          SBX   5  FX1                 [READ S/R
   9 4CLB          READST   1,XRD1   
  10 4D62          TESTREPNOT   OK,XRD2  
   9 4DKL          LDX   1  ECOR(3)  
  10 4F5=    XRD1  MHUNTW   2,EWDAS,EXOF 
   8 4FJW          ADX   5  FX1  
   7 4G4G          EXIT  5  1
  11 4GJ6    XRD2  TESTREPNOT   EXFAIL,XER1  
   8 4H3Q          ADX   5  FX1  
   7 4HHB          EXIT  5  0
   4 4J32    #   
   4 4JGL    #   
  16 4K2=    #     UNLOCK UNIT S/R   LINK X7  SERIAL NUMBER X1   
   4 4KFW    #   
   5 4K_G    XUNLOCK 
   8 4LF6          SBX   7  FX1  
   7 4LYQ          UNLOCKSU 1
   8 4MDB          CALL  5  XHUNT
   8 4MY2          ADX   7  FX1  
   7 4NCL          EXIT  7  0
   4 4NX=    #   
  16 4PBW    #     OPEN  UNIT  S/R   LINK X7 SERIAL NO. ESERL(3) 
   4 4PWG    #   
   5 4QB6    XOPEN   
   6 4QTQ          OPENST
   7 4R*B          EXIT  7  0
   4 4RT2    #   
   4 4S#L    #   
   7 4SS=    #     START HERE
  13 4T?W    #     THROUGHOUT X2 POINTS TO EXOF BLOCK
  13 4TRG    #                X3 POINTS TO GEXD BLOCK
   4 4W?6    #   
   6 4WQQ    QK2WRITEFIP 
   7 4X=B          NGNC  0  1
   6 4XQ2    QK1WRITEFIP 
   7 4Y9L          LDN   0  0
   9 4YP=          STO   0  AWORK1(2)
   8 4_8W          CALL  5  XHUNT
  18 4_NG          STOZ     EMARK(3)            [ZEROISE RESULT MARKER   
  11 5286 ...      TRACE    EXMARK(2),EXOPEN 
   9 52MQ ...      TRACE    ASUSN(2),
   4 537B    #   
  11 53M2    #     CHECK VALID ENTRY PARAMS  
   4 546L    #   
   9 54L=          LDX   0  EXMARK(2)
  17 555W          LDN   4  BRITE+BSPEC+BSERL+BLOCK   [CHECK SENSIBLE
  21 55KG          ANDX  0  4                         [COMBINATION OF BRITE PARAMS   
   7 5656          TXU   0  4
  16 56JQ          BCC      XROK                      [_ IF OK   
   9 574B          LDX   0  EXMARK(2)
  17 57J2          LDN   4  BCREATE+BSPEC             [CHECK SENSIBLE
  21 583L          ANDX  0  4                         [COMBINATION OF BCREATE PARAMS 
   7 58H=          TXU   0  4
   8 592W          BCC      XROK 
  18 59GG    XMARK GEOERR   1,MARKERR                 [OTHERWISE ERROR   
   9 5=26    XROK  LDX   0  EXMARK(2)
  21 5=FQ          ANDX  0  BOPEN+BANY+BHIGH+BREPQUAL [CHECK NO OTHER INVALID MARKERS
   8 5=_B          BNZ   0  XMARK
   4 5?F2    #   
   9 5?YL          LDX   0  EXMARK(2)
   8 5#D=          ANDN  0  BRITE
  19 5#XW          BNZ   0  XRITE               [JUMP IF FIP TO BE REWRITTEN 
   9 5*CG          LDX   0  EXMARK(2)
   8 5*X6          ANDN  0  BSERL
  21 5BBQ          BZE   0  SERCH               [JUMP IF SERIAL NUMBER NOT SPECIFIED 
   4 5BWB    #   
  11 5CB2    #     SERIAL NUMBER SPECIFIED   
  12 5CTL    #     GET UNIT ON LINE IF NECCESSARY
   4 5D*=    #   
   9 5DSW          LDX   0  EXMARK(2)
   9 5F#G          ANDN  0  BCREATE  
  20 5FS6          BZE   0  PES10               [J IF EXOFILE NOT BEING CREATED  
   9 5G?Q          LDX   7  ASUSN(2) 
  14 5GRB          LDN   5  CAPPEND             [APPEND  
  18 5H?2          EXCHOFE  ICL,7,5,PES10       [TRAPS TO BE CHECKED FOR 
  16 5HQL          PICKREP  6                   [PRESERVE REPLY  
  15 5J==          TESTREP2 OK,PES1,NOFILE,PES2,NOTRAP,PES3  
   8 5JPW          BRN      XER1 
   4 5K9G    PES2
   8 5KP6          LDX   0  EDSQ 
  19 5L8Q          BNG   0  PES7                [J IF FULL SECURITY IN USE   
   4 5LNB    PES1
  18 5M82          LDN   5  CWRITE              [CHECK FOR WRITE TRAPS   
  16 5MML          EXCHOFE  EXOF,,5,PES9        [TO EXOFILE ENTRY
  15 5N7=          TESTREP2 OK,PES4,NOFILE,PES5,NOTRAP,PES6  
   8 5NLW          BRN      XER1 
   4 5P6G    PES4
   7 5PL6          PUTREP   6
  18 5Q5Q          TESTREP  OK,PES4A            [OK+OK MEANS OK TO,GO ON 
  19 5QKB          LDX   5  XEWCI(1)            [CANT CREATE ON INSECURE SU  
   8 5R52          BRN      PES8 
   5 5RJL    PES4A   
  21 5S4=          EXCHOFE  ICL,7,,PES9,AW      [D&T WRITTEN+ACESSED FOR ICLSCAFILE  
  21 5SHW          EXCHOFE  EXOF,,,PES9,AW      [ ^     ^   ^   ^      ^  EXOF ENTRY 
   8 5T3G          BRN      PES9 
   4 5TH6    PES5
   7 5W2Q          PUTREP   6
  20 5WGB          TESTREP  NOFILE,PES9         [NOFILE+NOFILE MEANS OK TO GO ON 
  18 5X22          LDX   5  XEWCS(1)            [CANT CREATE ON SECURE SU
   8 5XFL          BRN      PES8 
   4 5X_=    PES6
  20 5YDW          LDX   5  XEWTC(1)            [TRAPS CLOSED FOR EXOFILE ENTRY  
   8 5YYG          BRN      PES8 
   4 5_D6    PES7
  20 5_XQ          LDX   5  XEWNE(1)            [SU UNSUITABLE - NO EXOFILE ENTRY
   8 62CB          BRN      PES8 
   4 62X2    PES3
  19 63BL          LDX   5  XEWNT(1)            [SU UNSUITABLE - TRAPS CLOSED
   4 63W=    PES8
   8 64*W          KEEPMESS 5,7  
  19 64TG          LDN   4  AFAIL               [SUPPRESS FURTHER MESSAGE +  
  20 65*6          BRN      P2D                 [IT GIVES OL *DA0 FAIL IN CHINERR
   4 65SQ    PES9
   8 66#B          CALL  5  XHUNT
   5 66S2    PES10   
  12 67?L          USEST    ASUSN(2),P2A,,P2F,P2H
   4 67R=    #   
  18 68=W    #     UNIT NOW MARKED ^TO BE KEPT ONLINE^ + SCALO CLAIMED   
   4 68QG    #   
   8 69=6    P2G   CALL  5  XHUNT
   9 69PQ          LDX   0  ASUSN(2) 
  19 6=9B          STO   0  ESERL(3)            [PUT SERIAL NUMBER IN GEXD   
  17 6=P2          BRN      PS1            [GO TO SEARCH FIP CELLS   
   4 6?8L    #   
  14 6?N=    #     UNIT NOT ONLINE OR IN USE BY S-TRUSTED
   4 6#7W    #   
   8 6#MG    P2A   CALL  5  XHUNT
   9 6*76          LDX   0  EXMARK(2)
  21 6*LQ          ANDN  0  BSUPP               [CHECK IF TO SUPPRESS LOAD MESSAGE   
  15 6B6B          BZE   0  P2C                 [JUMP IF NOT 
  16 6BL2          KEEPMESS XEWN(1),ASUSN(2)    [UNIT NOT ONLINE 
   8 6C5L          LDN   4  ANONL
   8 6CK=          BRN      P2D  
   4 6D4W    #   
  13 6DJG    #     SCALO NOT AVAILABLE - IPB FAILURE 
   4 6F46    #   
  14 6FHQ    P2H   LDN   4  APLINKF             [IPB FAIL
   8 6G3B          BRN      P2D  
   4 6GH2    #   
  15 6H2L    #     OUTPUT REQUEST FOR UNIT TO BE PUT ONLINE  
   4 6HG=    #   
  12 6H_W    P2C   MUSEST   ASUSN(2),P2E,P2F,,P2H
   8 6JFG          BRN      P2G  
   4 6J_6    #   
   9 6KDQ    #     CANTDO TO REQUEST 
   4 6KYB    #   
   8 6LD2    P2E   LDN   4  ARITE
   8 6LXL          BRN      P2D  
   4 6MC=    #   
  17 6MWW    #     BREAK-IN WHILST WAITING FOR UNIT TO BE PUT ONLINE 
   4 6NBG    #   
  14 6NW6    P2F   LDN   4  ABREAK              [BREAKIN 
   8 6P*Q    P2D   CALL  5  XHUNT
   9 6PTB          ORS   4  EMARK(3) 
   8 6Q*2          BRN      XERR 
   4 6QSL    #   
  18 6R#=    #     BSERL NOT SPECIFIED - START SEARCH OF ALL ONLINE UNITS
   4 6RRW    #   
   9 6S?G    SERCH STOZ     ASUSN(2) 
   8 6SR6          LDN   1  BCCB 
  17 6T=Q          CALL  7  NEXTSU              [GET NEXT UNIT ONLINE
  18 6TQB          BRN      TF4                 [NO SUITABLE UNIT FOUND  
   4 6W=2    #   
  16 6WPL    #     SEARCH FIP CELLS OF SPECIFIED OR CURRENT UNIT 
   4 6X9=    #   
  15 6XNW    PS1   CALL  7  XOPEN               [OPEN UNIT   
   8 6Y8G    PS1A  LDN   0  147  
  17 6YN6          STO   0  HOME(2)             [START OF FIP CELLS  
   9 6_7Q    PS1B  LDX   1  HOME(2)  
  16 6_MB          CALL  5  XREAD               [READ NEXT CELL  
  16 7272          BRN      TF1                 [TRANSFER ERROR  
   8 72LL    PS1C  LDX   0  0(1) 
  17 736=          BNG   0  US1                 [CHECK IF CELL UNUSED
   8 73KW          LDX   5  2(1) 
   8 745G          LDX   6  3(1) 
  16 74K6          LDX   7  4(1)                [CHECK FILENAMES 
   9 754Q          TXU   5  FIP(2)   
   9 75JB          TXU   6  FIP+1(2) 
   9 7642          TXU   7  FIP+2(2) 
  17 76HL          BCS      P5                  [JUMP IF DIFFERENT   
  16 773=          LDX   0  6(1)                [GEN NUMBER CHECK
   8 77GW          SRL   0  12   
   9 782G          LDX   4  FIP+4(2) 
   8 78G6          SRL   4  12   
   7 78_Q          TXU   0  4
  17 79FB          BCS      P5                  [JUMP IF DIFFERENT   
   9 79_2          LDX   0  EXMARK(2)
   9 7=DL          ANDN  0  BCREATE  
  17 7=Y=          BNZ   0  TF5                 [JUMP IF CREATING FIP
   4 7?CW    #   
   4 7?XG    #   
   9 7#C6    #     EXISTING FIP FOUND
   4 7#WQ    #   
   9 7*BB          LDX   4  HOME(2)  
   9 7*W2          LDX   5  BACK2(2) 
  18 7B*L          MFINDEXOF 2,5                [FIND EXOF IN CCB CHAIN  
  16 7BT=          STO   4  HOME(2)             [RESET B S HOME  
  16 7C#W          BRN      XUPDF               [JUMP TO REWRITE 
   4 7CSG    #   
  11 7D#6    #     FILENAME OR GEN DIFFERENT 
   9 7DRQ    #     GET NEXT FIP CELL 
   4 7F?B    #   
  17 7FR2    P5    LDX   0  0(1)                [POINTER TO NEXT CELL
  21 7G=L          BNZ   0  P5A                 [JUMP IF NEXT CELL NOT CONSECUTIVE   
   8 7GQ=          LDN   1  17   
  17 7H9W          ADS   1  ECOR(3)             [UPDATE CORE POINTER 
  16 7HPG          ADS   1  HOME(2)             [ UPDATE B.S HOME
   9 7J96          LDX   1  ECOR(3)  
   8 7JNQ          BRN      PS1C 
   8 7K8B    P5A   SMO      FX1  
  18 7KN2          TXU   0  NX5                 [CHECK IF END OF CHAIN   
  16 7L7L          BCC      TF6                 [JUMP IF IT IS   
   9 7LM=          STO   0  HOME(2)  
  16 7M6W          BRN      PS1B                [CONTINUE SEARCH 
   4 7MLG    #   
   9 7N66    #     NO MORE USED CELLS
   4 7NKQ    #   
   9 7P5B    US1   LDX   0  EXMARK(2)
   9 7PK2          ANDN  0  BCREATE  
   8 7Q4L          BNZ   0  XCRE 
  16 7QJ=          LDN   0  ANFOUND              [FILE NOT FOUND 
   9 7R3W          ORS   0  EMARK(3) 
  18 7RHG          CLOSEST  XERR                [GO TO SEARCH FIP CELLS  
   8 7S36          BRN      XERR 
   4 7SGQ    #   
  11 7T2B    #     ROOM FOUND TO CREATE FILE 
   4 7TG2    #   
   7 7T_L    XCRE  NGNC  0  1
  16 7WF=          ANDS  0  0(1)                [UNSET UNUSED BIT
  16 7WYW          STOZ     1(1)                [FDCELL POINTER  
   7 7XDG          LDN   0  1
   9 7XY6          STO   0  FBUK(2)  
   9 7YCQ          LDX   0  ESERL(3) 
  18 7YXB          STO   0  ASUSN(2)            [PUT SERIAL NO IN EXOF   
   8 7_C2          BRN      XUPDF
   4 7_WL    #   
   4 82B=    #   
   8 82TW    #     REWRITE FIP.  
   4 83*G    #   
  15 83T6    XRITE CALL  7  XOPEN               [OPEN UNIT   
   9 84#Q          LDX   1  HOME(2)  
   8 84SB          CALL  5  XREAD
  16 85#2          BRN      TF2                 [TRANSFER ERROR  
  16 85RL          LDX   5  2(1)                [CHECK FILENAME  
   8 86?=          LDX   6  3(1) 
   8 86QW          LDX   7  4(1) 
   9 87=G          TXU   5  FIP(2)   
   9 87Q6          TXU   6  FIP+1(2) 
   9 889Q          TXU   7  FIP+2(2) 
  17 88PB          BCS      PS1A                [JUMP IF NOT THE SAME
  16 8992          LDX   4  6(1)                [CHECK GEN NUMBER
   8 89NL          SRL   4  12   
   9 8=8=          LDX   0  FIP+4(2) 
   8 8=MW          SRL   0  12   
   7 8?7G          TXU   0  4
  17 8?M6          BCS      PS1A                [JUMP IF NOT THE SAME
   9 8#6Q          LDX   4  BACK2(2) 
   8 8#LB          MFINDEXOF 2,4 
   4 8*62    #   
   9 8*KL    #     HERE TO UPDATE FIP
   4 8B5=    #   
   7 8BJW    XUPDF ADN   1  2
   9 8C4G          LDN   0  FIP(2)   
   8 8CJ6          MOVE  0  15   
   9 8D3Q          CLOSEWR  W,HOME(2)
  10 8DHB          TESTREPNOT   OK,TF3   
   8 8F32          CALL  5  XHUNT
   9 8FGL          STOZ     EMARK(3) 
   4 8G2=    #   
   8 8GFW    XEND  SETREP   OK   
   8 8G_G          SMO      FX2  
   9 8HF6          LDX   0  AWORK1   
   8 8HYQ          BNZ   0  XEND1
   5 8JDB          UP
   4 8JY2    #   
  17 8KCL    #     FIP SUCCESSFULLY CREATED - PREPARE TO OPEN FILE   
   4 8KX=    #   
   5 8LBW    XEND1   
  18 8LWG          STOZ     COPEN(2)            [CLEAR COPEN AND BACK2   
   9 8MB6          STOZ     BACK2(2) 
   8 8MTQ          LDN   0  #7777
  17 8N*B          ANDS  0  EXMARK(2)           [ALSO B0-11 OF EXMARK
  14 8NT2          LDN   0  1                   [CREATE:-
  16 8P#L          STO   0  COPEN(2)            [COUNT OF OPENS=1
   8 8PS=          LDCT  0  #400 
  16 8Q?W          STO   0  EXMARK(2)           [SET WRITE MARKER
   7 8QRG          LDX   7  2
  20 8R?6          EXOFILE  7                   [CHAIN IN EXOF & GIVE IT A NUMBER
   7 8RQQ          LDX   2  7
   9 8S=B          LDX   7  BACK2(2) 
  14 8SQ2          STO   7  CEFN(3)             [SAVE EFN
  17 8T9L          LDX   6  ASUSN(2)            [PICK UP SERIAL NO   
  19 8TP=    #     AS THE FILE IS BEING OPENED FOR WRITING WE HAVE TO INFORM 
  13 8W8W    #     THE CAFS SUBSYSTEM OF THE EVENT   
  11 8WNG          CAFSINFO OPENFILE,CEFN(3) 
   6 8X86          EXMESS
  15 8XMQ          ACROSS   ANTIC,1             [CONNECT FILE
   4 8Y7B    #   
   4 8YM2    #   
   8 8_6L    #     FAILURE CASES 
   4 8_L=    #   
  14 925W    #     TRANSFER FAIL WHILE READING FIP CELLS 
   4 92KG    #   
   8 9356    TF1   LDN   4  AFAIL
   8 93JQ    TF1C  CALL  5  XHUNT
  15 944B    TF1B  ORS   4  EMARK(3)            [SET FAIL BIT
   9 94J2          LDX   0  EXMARK(2)
   8 953L          ANDN  0  BSERL
  20 95H=          BZE   0  TF1A                [_ IF SERIAL NUMBER NOT SPECIFIED
   8 962W          LDN   0  BLOCK
   9 96GG          ANDX  0  EXMARK(2)
  17 9726          BNZ   0  XERR                [_ IF LOCKED ON ENTRY
   8 97FQ          BRN      XREL 
  20 97_B    TF1A  LDX   6  ESERL(3)            [OTHERWISE (BCREATE ONLY) ATTEMPT
  19 98F2          SMO      FX2                 [TO SEARCH NEXT ONLINE UNIT  
  15 98YL          STO   6  AWORK4              [PRESERVE X6 
   8 99D=          MFINDSU  1,6  
  17 99XW          CALL  7  NEXTSU              [GET NEXT S.U,IF ANY 
  16 9=CG          BRN      XREL                [JUMP IF NO MORE 
   8 9=X6          SMO      FX2  
   9 9?BQ          LDX   6  AWORK4   
   8 9?WB          LDCT  0  #400 
   9 9#B2          ANDX  0  EMARK(3) 
  14 9#TL          ERS   0  EMARK(3)            [UNSET   
  20 9**=          BNZ   0  PS1                 [J IF NOT TO UNLOCK PREVIOUS UNIT
   7 9*SW          LDX   1  6
  15 9B#G          CALL  7  XUNLOCK             [UNLOCK UNIT 
   7 9BS6          RELST    6
   8 9C?Q          CALL  5  XHUNT
  18 9CRB          BRN      PS1                 [BACK TO SEARCH NEXT UNIT
   4 9D?2    #   
   4 9DQL    #   
  11 9F==    #     READ FAIL FOR FIP REWRITE 
   4 9FPW    #   
   8 9G9G    TF2   LDN   4  AFAIL
   8 9GP6          BRN      TF1C 
   4 9H8Q    #   
  13 9HNB    #     TRANSFER FAIL WHILST WRITING FIP  
   4 9J82    #   
   8 9JML    TF3   LDN   4  ARITE
   8 9K7=          BRN      TF1C 
   4 9KLW    #   
   4 9L6G    #   
  14 9LL6    #     NO SUITABLE UNIT FOUND (CREATE ONLY)  
   4 9M5Q    #   
   8 9MKB    TF4   CALL  5  XHUNT
   9 9N52          LDX   0  EMARK(3) 
   9 9NJL          ANDN  0  ABREAK   
   8 9P4=          BNZ   0  XERR 
   8 9PHW          LDN   0  ANONL
   9 9Q3G          ORS   0  EMARK(3) 
   8 9QH6          BRN      XERR 
   4 9R2Q    #   
   4 9RGB    #   
  12 9S22    #     FILE FOUND WHILST CREATING.   
   4 9SFL    #   
   8 9S_=    TF5   LDX   1  FX1  
  16 9TDW          OPTMESS  XEWI(1),ESERL(3)    [FILENAME IN USE 
   8 9TYG          CALL  5  XHUNT
   9 9WD6          LDN   0  AINUSE   
   9 9WXQ          ORS   0  EMARK(3) 
   8 9XCB          CLOSEST  TF5A 
  10 9XX2          TESTREPNOT   OK,XER1  
   8 9YBL          CALL  5  XHUNT
   8 9YW=    TF5A  LDN   0  BSERL
   9 9_*W          ANDX  0  EXMARK(2)
   8 9_TG          BNZ   0  XREL 
   8 =2*6          BRN      TF1A 
   4 =2SQ    #   
   4 =3#B    #   
  13 =3S2    #     NO ROOM TO CREATE FIP ON THIS SU  
   4 =4?L    #   
   4 =4R=    #   
   8 =5=W    TF6   LDX   1  FX1  
  19 =5QG          OPTMESS  XEWR(1),ESERL(3)    [OPTIONALLY OUTPUT NO ROOM   
   9 =6=6          LDN   4  ANOROOM  
   8 =6PQ          BRN      TF1C 
   4 =79B    #   
  18 =7P2    #     IN SOME FAILURE CASES UNIT MUST BE UNLOCKED + RELEASED
   4 =88L    #   
   9 =8N=    XREL  LDX   1  ESERL(3) 
   9 =97W          CALL  7  XUNLOCK  
   9 =9MG          RELST    ESERL(3) 
   4 ==76    #   
  18 ==LQ    #     FAILURE CASE TERMINATION IS UP OR ACROSS TO K1CHINERR 
   4 =?6B    #   
   9 =?L2    XERR  SETREP   EXFAIL   
   8 =#5L          SMO      FX2  
   9 =#K=          LDX   0  AWORK1   
   8 =*4W          BZE   0  XERR1
   9 =*JG          ACROSS   CHINERR,1
   5 =B46    XERR1 UP
   4 =BHQ    #   
  11 =C3B          MENDAREA 30,K99WRITEFIP   
   4 =CH2    #END
   6 ____ ...321744250003
