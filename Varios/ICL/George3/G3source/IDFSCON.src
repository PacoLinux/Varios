  15 22FL    #SEG  IDFSCON8                     [A C PUTMAN  
  13 22PD ...# COPYRIGHT INTERNATIONAL COMPUTERS 1982
   8 22_=    #OPT  K0IDFSCON=0   
  16 23DW    #LIS  K0IDFSCON>K0ALLGEO>K0GREATGEO>K0COMMAND>K0IDF 
   7 23YG          8HIDFSCON 
  11 24D6          SEGENTRY K20IDFSCON,XIDF20
  11 24XQ          SEGENTRY K1IDFSCON,XIDF1  
  11 25CB          SEGENTRY K4IDFSCON,XIDF4  
  11 25X2          SEGENTRY K3IDFSCON,XIDF3  
  11 26BL          SEGENTRY K30IDFSCON,XIDF30
  11 26W=          SEGENTRY K35IDFSCON,XIDF35
  13 27*W    ZMSEARCH           [SEARCH FOR A CELL   
   8 27TG          SBX   7  FX1  
   8 28*6          MSEARCH  2,3,4
   8 28SQ          ADX   7  FX1  
   7 29#B          EXIT  7  0
  20 29S2    OPEN           [OPEN IDF AND SEE IF UNIT IN ACOMMUNE2 EXISTS:X2=FX2 
   8 2=?L          SBX   5  FX1  
  15 2=R=          LGEOG    ACOMMUNE2(2),4      [LOAD GEO.NO.
   8 2?CJ ...      IDFOPEN  XBRK 
  18 2?QG          CALL  7  ZMSEARCH            [SEARCH FOR UNIT CHAIN   
   8 2#=6          SMO      FX2  
   9 2#PQ          LDX   6  ACOMMUNE1
  16 2*9B          BNZ   6  P1                  [UNIT FOUND OKAY 
  19 2*P2          GEOERR   1,UNITILL           [WRONG UNIT NO IN %B OF MACRO
   8 2B8L    P1    LDX   1  FX2  
  18 2BN=          STO   3  AWORK2(1)           [STORE UNIT CELL POINTER 
   9 2C7W          LDX   6  BRECNO(2)
  17 2CMG          STO   6  AWORK1(1)           [STORE UNIT RECORD NO
   7 2D76          ADX   3  2
  16 2DLQ          LDX   6  BNUMB(3)            [FOR USE ON EXIT 
   8 2F6B          ADX   5  FX1  
   7 2FL2          EXIT  5  0
   5 2FTS ...XBRK  UP
  21 2G5L    ZCLOSE                             [FREE FILE/FRRB BLOCKS & CLOSE IDF   
   8 2GK=          SBX   5  FX1  
   9 2H4W          MFREEALL FILE,FRRB
   6 2HJG          CLOSE 
   8 2J46          ADX   5  FX1  
   7 2JHQ          EXIT  5  0
   5 2K3B    XWRITE  
   8 2KH2          SBX   5  FX1  
   7 2L2L          IDFWRITE  
   8 2LG=          ADX   5  FX1  
   7 2L_W          EXIT  5  0
  13 2MFG    S7071              [SET UP B7071 BLOCK  
   8 2M_6          SBX   5  FX1  
  20 2NDQ          ANDN  6  #7777               [GET NO OF LINES-X6 ALREADY OKAY 
  20 2NYB          LDCH  2  BTYPE(3)            [PICK UP DEVICE TYPE(#12 OR #13) 
  20 2PD2          SBN   2  #12                 [MAKE X2 ZERO FOR UPX:ONE FOR MPX
  20 2PXL          LDX   4  2                   [AND KEEP IN X4 AS FIRST LINE NO 
  21 2QC=          ADX   2  6                   [MAKE X2 LENGTH OF B7071 BLOCK:MPX-  
  16 2QWW          SETUPCORE 2,1,IDF,B7071      [HAS DUMMY LINE 0
   8 2RBG          ADX   5  FX1  
   7 2RW6          EXIT  5  0
  21 2S*Q    YREC           [X3 CONTAINS FORWARD POINTER:X2 POINTS TO CURRENT RECORD 
  20 2STB                   [ON EXIT X2 IS RECORD POINTER & X3 IS CELL POINTER   
   8 2T*2          BPZ   3  SAME 
   5 2TSL    YREC1   
   8 2W#=          SBX   6  FX1  
   7 2WRW          LDX   0  3
   7 2X?G          SRL   0  9
   8 2XR6          ANDN  0  #7777
   8 2Y=Q          LDX   2  FX2  
  19 2YQB          STO   0  ACOMMUNE3(2)        [STORE FORWARD REC.NO FOR MXB
   7 2_=2          MXB      2
   8 2_PL          ADX   6  FX1  
   8 329=    SAME  ANDN  3  #777 
   7 32NW          EXIT  6  0
  21 338G    RECFIND        [HUNT-RECORD NO OF FILE/FRRB IN X3-FINAL POINTER IN X2   
   8 33N6          LDX   2  FX2  
   4 347Q    REC 
  10 34MB          HUNT2    2,FILE,FRRB  
  10 3572          TXU   3  BRECNO+A1(2) 
   8 35LL          BCS      REC  
   8 366=          ADN   2  A1   
   7 36KW          EXIT  6  0
  17 375G    SETUP              [X4 & X6 MUST BE PRESERVED:X4=LINE NO
  21 37K6                       [ON EXIT:-CONNAME OR B7020 BLOCKS MAY HAVE BEEN MADE 
  14 384Q                       [X7=ENTRY FOR B7071 BLOCK
  16 38JB                       [X3=ABSOLUTE POINTER TO LINE CELL
   8 3942          SBX   5  FX1  
   8 39HL          SLL   6  12   
  18 3=3=          ORX   4  6                   [^PACK^ X4 & X6 INTO X4  
   8 3=GW          LDX   1  FX2  
  18 3?2G          LDX   3  AWORK1(1)           [RECORD NO OF UNIT CELL  
  15 3?G6          CALL  6  RECFIND             [FIND RECORD 
   9 3?_Q          LDX   3  AWORK2(1)
   9 3#FB          LDN   3  BLINE(3) 
  16 3#_2          CALL  7  ZMSEARCH            [SEARCH FOR LINE 
   8 3*DL          SMO      FX2  
   9 3*Y=          LDX   7  ACOMMUNE1
   8 3BCW          BNZ   7  SET2 
  19 3BXG          GEOERR   1,LINEILL           [ERROR IF LINE NON-EXISTENT  
   8 3CC6    SET2  LDX   1  FX2  
  19 3CWQ          STO   3  AWORK4(1)           [LINE CELL POINTER INTO STORE
   9 3DBB          LDX   7  BRECNO(2)
  19 3DW2          STO   7  AWORK3(1)           [LINE RECORD NO INTO STORE   
  21 3F*L          ADX   3  2                   [ABSOLUTE POINTER TO LINE CELL IN X3 
   8 3FT=          LDCT  7  #770 
   9 3G#W          ANDX  7  BTYPE(3) 
  10 3GSG          LDX   0  CONTCEP(3)   
  17 3H#6          BNZ   7  SET3                [J.IF NOT NULL DEVICE
  16 3HRQ          BZE   0  SET4                [J NOT ATTACHED  
  16 3J?B          LDCT  7  #200                [SET ATTACHED BIT
  16 3JR2    SET4  ORX   7  GSIGN               [SET UNKNOWN BIT 
   5 3K=L    SETXIT  
  16 3KQ=          LDX   6  4                   [^UNPACK^ X4 & X6
   8 3L9W          SRL   6  12   
   8 3LPG          ANDN  4  #7777
   8 3M96          ADX   5  FX1  
   7 3MNQ          EXIT  5  0
  19 3N8B    SET3  SLC   7  6                   [MOVE DEVICE TYPE IETO B18-23
  17 3NN2          BNZ   0  SET5                [J.IF LINE ATTATCHED 
   7 3P7L          LDN   0  2
   7 3PM=          TXU   7  0
  16 3Q6W          BCS      SETXIT              [NOT 7020 SO EXIT
  21 3QLG          LDX   6  BNUMB(3)            [NO OF 7020'S:X6 HAS BEEN ^PACKED^   
   8 3R66          ANDN  6  #7777
   7 3RKQ          STO   6  0
  15 3S5B          SLL   6  3                   [MULT BY 8   
  17 3SK2          SBX   6  0                   [REDUCE TO MULT BY 7 
  19 3T4L          ADN   6  2                   [LENGTH OF B7020 BLOCK IN X6 
  11 3TJ=          SETUPCORE  6,1,IDF,B7020  
  21 3W3W          LDN   7  A1(1)               [X7 CONTAINED 2-WILL REPLACE LATER   
   9 3WHG          LDN   0  A1+1(1)  
   8 3X36          STOZ     A1(1)
   7 3XGQ          SBN   6  1
   7 3Y2B          SMO      6
  21 3YG2          MOVE  7  0                   [ZEROIZE B7020 BLOCK-FOR NULL ENTRIES
  15 3Y_L          LDX   3  AWORK3(2)           [LINE REC. NO
   9 3_F=          CALL  6  RECFIND  
   8 3_YW          SMO      FX2  
  17 42DG          ADX   2  AWORK4              [X2=POINTER TO CELL  
   9 42Y6          LDX   6  BNO(2)   
   8 43CQ          SLL   6  12   
  14 43XB          STO   6  APCTLIN(1)          [LINE NO 
   9 44C2          LDX   6  BTYPE(2) 
   8 44WL          ANDN  6  #7777
  14 45B=          ORS   6  APCTLIN(1)          [UNIT NO 
   9 45TW          LDX   6  BNUMB(2) 
   8 46*G          ANDN  6  #7777
  15 46T6          STO   6  APCTNO(1)           [NO OF PCT'S 
  19 47#Q          LDX   3  BLINE(2)            [FORWARD POINTER TO 7020 CELL
   8 47SB          SMO      FX2  
  17 48#2          SBX   2  AWORK4              [X2=RECORD POINTER   
  21 48RL          LDN   7  APCTLS              [X7 CONTAINED 2,SO CAN PUT IT BACK   
  16 49?=    SET10 CALL  6  YREC                [FIND 7020 CELL  
  10 49QW          MHUNTW   1,IDF,B7020  
  18 4==G          ADX   3  2                   [FOR FUTURE CONVENIENCE  
  19 4=Q6          LDX   6  BNO(3)              [PICK UP IDENT/POLLING ADD   
   7 4?9Q          SMO      7
  20 4?PB          STO   6  0(1)                [STORE IDENT/POLLING ADD IN B7020
   8 4#92          BRN      SET7 
  19 4#NL    SET9  CALL  6  YREC                [FIND CELL FOR NEXT INTERFACE
  10 4*8=          MHUNTW   1,IDF,B7020  
   7 4*MW          ADX   3  2
  21 4B7G    SET7  LDCT  6  #400    [MAKE SURE 7020 CELLS ARE MARKED MOPPED ON:FOR-  
  20 4BM6                           [^SCAN^ SHOULD ALREADY BE,BUT NOT FOR ^MOPON^
  10 4C6Q          ORS   6  BTYPE+1(3)   
   7 4C?Y ...#SKI  JSKI17<1$1
   4 4CF6 ...(   
  18 4CLB          LDX   6  BTYPE(3)            [GET DEVICE/INTERFACE NO.
   8 4D62          LDCT  0  #770 
   7 4DKL          ANDX  0  6
  16 4F5=          ANDN  6  #77                 [X6=INTERFACE NO 
  16 4FJW          SLC   0  6                   [X0=DEVICE TYPE  
  20 4G4G          ADX   6  7                   [SO X6 IS POSITION IN B7020 BLOCK
   7 4GJ6          SMO      6
  20 4H3Q          STO   0  0(1)                [STORE DEVICE TYPE IN B7020 BLOCK
   4 4H4D ...)   
   6 4H56 ...#SKI  JSKI17
   4 4H5S ...(   
   7 4H6G ...      LDN   0  0
  10 4H78 ...      LDX   6  BTYPE+1(3)   
   7 4H7W ...      SLL   6  4
  17 4H8J ...      BPZ   6  XSPOOLABLE          [J IF NOT SPOOLABLE  
   7 4H9= ...      SLL   6  1
  21 4H9Y ...      ANDX  6  CACT                [PRESERVE SPOOLING,UNATTENDED BITS   
   7 4H=L ...      ORX   0  6
   6 4H?# ...XSPOOLABLE  
   9 4H#2 ...      LDX   6  BTYPE(3) 
   7 4H#N ...      SLC   6  6
  17 4H*B ...      ANDN  6  #77                 [INTERNAL DEVICE TYPE
   7 4HB4 ...      ORX   0  6
   9 4HBQ ...      LDX   6  BTYPE(3) 
  16 4HCD ...      ANDN  6  #77                 [INTERFACE NUMBER
  19 4HD6 ...      ADX   6  7                   [ADD PTR TO FIRST INTERFACE  
   7 4HDS ...      SMO      6
  18 4HFG ...      STO   0  0(1)                [STORE IN IDF/B7020 BLK  
   4 4HG8 ...)   
  10 4HHB          LDX   6  BTYPE+1(3)   
   8 4J32          ANDN  6  #77  
  17 4JGL          BZE   6  SET8                [J NO CONSOLE IN IT  
   7 4JQD ...#SKI  JSKI17<1$1
  17 4K2=          LDN   0  3                   [CONSOLE DEVICE TYPE 
   7 4KFW          ADX   6  7
   7 4K_G          SMO      6
  17 4LF6          STO   0  0(1)                [STORE IN B7020 BLOCK
  10 4LYQ    SET8  LDX   6  BTYPE+2(3)   
   8 4MDB          LDX   3  0(3) 
  17 4MY2          BPZ   6  SET9                [J.FOR NEXT INTERFACE
  15 4NCL          ADN   7  7                   [UPDATE X7   
  18 4NX=          BNZ   3  SET10               [J.FOR NEXT 7020 TERMINAL
  20 4PBW          LDN   7  2                   [PUT BACK 7020 DEVICE TYPE IN X7 
   8 4PWG    TIDY  LDX   1  FX2  
  16 4QB6          LDX   3  AWORK3(1)           [REC NO FOR LINE 
   9 4QTQ          CALL  6  RECFIND  
  15 4R*B          LDX   3  AWORK4(1)           [CELL POINTER
   7 4RT2          ADX   3  2
   9 4S#L          BRN      SETXIT   
   4 4SS=    SET5
  21 4T?W          SETNCORE 4,1,IDF,CONNAME     [SET UP BLOCK TO CONTAIN CONCEPTUAL. 
   9 4TRG          LDX   3  AWORK3(2)
   9 4W?6          LDX   1  AWORK4(2)
  16 4WQQ          CALL  6  RECFIND             [HUNT LINE CELL  
   7 4X=B          SMO      2
  18 4XQ2          LDX   3  CONTCEP(1)          [LD. PTR. TO CONCEPTUAL. 
  16 4Y9L          CALL  6  YREC                [HUNT CONCEPTUAL.
   8 4YP=          ADN   2  1(3) 
  16 4_8W          MHUNTW   3,IDF,CONNAME       [RE-HUNT BLOCK.  
   8 4_NG          ADN   3  A1+1 
  18 5286          MOVE  2  3                   [MOVE IN CONCEPTUAL NAME 
  18 52MQ          LDX   4  3(2)                [LD.CT. OF NO. OF LNS.   
   8 537B          ANDN  4  #7777
   8 53M2          SMO      FX2  
   9 546L          STOZ     AWORK1   
   8 54L=          LDX   3  6(2) 
  17 555W          CALL  6  YREC1               [HUNT USER NAME CELL 
   7 55KG          SMO      2
  17 5656          LDX   3  7(3)                [PTR. TO LINE CELL.  
   8 56JQ          SMO      FX2  
   9 574B          LDX   7  AWORK3   
   7 57J2          SLL   7  9
   8 583L          SMO      FX2  
  20 58H=          ADX   7  AWORK4              [X7 = REC.NO. & CELL PTR. OF LINE
  16 592W    SET50 CALL  6  YREC                [HUNT LINE CELL. 
   5 59GG    SET52   
   7 5=26          LDN   0  1
   8 5=FQ          SMO      FX2  
  19 5=_B          ADS   0  AWORK1              [AWORK1= CONCEPTUAL LINE NO. 
   7 5?F2          SMO      2
   8 5?YL          LDX   0  0(3) 
  19 5#D=          ANDX  0  BITS22LS            [MASK OUT REC.NO.&CELL PTR.  
  15 5#XW          BXE   0  7,SET55             [J. IF EQUAL.
   7 5*CG          LDX   0  4
   8 5*X6          ANDN  4  #7777
   7 5BBQ          SBN   4  1
   8 5BWB          BZE   4  SET54
   7 5CB2          SLC   0  3
   8 5CTL          ANDN  0  #7   
   7 5D*=          ADN   0  1
   7 5DSW          LDN   6  7
   7 5F#G          SBX   6  0
   8 5FS6          BZE   6  SET53
   7 5G?Q          SRC   0  3
   7 5GRB          ADN   3  1
   7 5H?2          ADX   4  0
   8 5HQL          BRN      SET52
   7 5J==    SET53 SMO      3
  20 5JPW          LDX   3  1(2)                [LD. PTR.  TO&NEXT CONCEPT. CELL 
   8 5K9G          BRN      SET50
  10 5KP6    SET54 GEOERR   1,LOSTLINE   
  19 5L8Q    SET55 MHUNTW   1,IDF,CONNAME       [HUNT CONCEPTUAL NAME BLK.   
   8 5LNB          LDX   2  FX2  
   9 5M82          LDX   0  AWORK1(2)
  19 5MML          STO   0  A1(1)               [ST. CONCEPTUAL LINE IN BLOCK
   9 5N7=          LDX   3  AWORK3(2)
   9 5NLW          CALL  6  RECFIND  
   8 5P6G          SMO      FX2  
   9 5PL6          LDX   3  AWORK4   
   8 5Q5Q          LDCT  7  #770 
   7 5QKB          ADX   3  2
   9 5R52          ANDX  7  BTYPE(3) 
   7 5RJL          SLC   7  6
   8 5S4=          LDCT  0  #200 
   7 5SHW          ORX   7  0
   9 5T3G          BRN      SETXIT   
   5 5TH6    XIDF20  
  20 5W2Q    #     THIS ENTRY POINT IS FOR SETTING INFORMATION ABOUT 7900'S. THE 
  16 5WGB    #     INFORMATION WILL BE HELD IN IDF/IPBSCAN BLOCK.
  10 5X22          LDX   5  ACOMMUNE3(2) 
   9 5XFL          STOZ     AWORK4(2)
  17 5X_=          STO   5  AWORK3(2)           [FOR USE AFTER OPEN  
  16 5YDW          LDN   3  BIPB                [LD. BASE CHAIN  
   8 5YYG          CALL  5  OPEN 
   8 5_D6          BPZ   6  NOGO 
   8 5_XQ          ANDN  6  #777 
   8 62CB          BZE   6  SET  
   7 62X2    XJUM  SBX   3  2
   8 63BL          SMO      FX2  
  18 63W=          LDX   4  AWORK3              [CONTAINS ZERO=ALL LINES 
  21 64*W          BNZ   4  XONE                [CONTAINS A NO. MEANS INFO. ON THIS  
   7 64TG          SMO      3
   8 65*6          LDX   4  5(2) 
   7 65SQ          SMO      3
  19 66#B          LDX   3  BLINE(2)            [LD. PTR. TO IDENTIFIER CHAIN
   8 66S2          SMO      FX2  
   9 67?L          STO   3  AWORK3   
  16 67R=          CALL  6  YREC                [HUNT FOR CELL.  
   7 68=W          SMO      3
  21 68QG          SBX   4  BNO(2)              [X4=DIFF. BETWEEN LARGE & SMALL IDNT 
  14 69=6          BZE   4  XADD                [ONLY ONE
   7 69PQ          ADN   4  2
   5 6=9B    XSETU   
  21 6=P2          SETUPCORE 4,1,IDF,IDENTIFIERS [SET UP BLOCK TO CONTAIN 7900 INFO .
   9 6?8L          LDX   3  AWORK3(2)
  19 6?N=          CALL  6  YREC1               [HUNT RECORY MAY HAVE COOR.  
   7 6#7W          ADX   2  3
   9 6#MG          LDX   5  BNO(2)   
  19 6*76          LDX   4  5                   [ST. SMALLEST IDENTIFIER NO. 
   7 6*LQ          SBN   4  1
  11 6B6B          MHUNTW   1,IDF,IDENTIFIERS
  20 6BL2          STO   4  A1(1)               [ST. SIN -1 INTO THE BLOCK SET UP
   8 6C5L          LDN   4  A1+1 
  18 6CK=    STTYP ADX   1  4                   [ADD. IN POSITION DOWN   
   7 6D4W          LDN   7  0
   9 6DJG          LDX   6  BTYPE(2) 
  21 6F46          SRL   6  15                  [SOMETIME PUT IN HERE ABOUT CONCEPTUA
   7 6FHQ          SRL   67 2
  18 6G3B          BPZ   7  XJB                 [J. IF R.J.E MOD NOT SET 
   8 6GH2          ORN   6  #2000
   7 6H2L    XJB   SRL   67 1
  20 6HG=          BPZ   7  XJA                 [J. IF UNKNOWN BITS IS NOT SET   
   8 6H_W          LDCT  0  #004 
  17 6JFG          ERS   0  BTYPE(2)            [UNSET UNKNOWN BIT   
   8 6J_6    XJA   STO   6  0(1) 
  10 6K3P ...      LDX   0  CONTCEP(2)   
  17 6K6# ...      BZE   0  NOTAH               [J IF NOT ATTACHED   
   8 6K8X ...      LDCT  0  #200 
  16 6K?G ...      ORS   0  0(1)                [SET ATTACHED BIT
   5 6KB5 ...NOTAH   
   8 6KDQ          SMO      FX2  
   9 6KYB          LDX   0  AWORK4   
  21 6LD2          BZE   0  XNSET               [J. IF MOPPON BIT NOT REQUIRE SETTING
   8 6LXL          LDCT  0  #400 
   9 6MC=          ORS   0  BNUMB(2) 
   7 6MWW    XNSET SBX   2  3
   7 6NBG          SMO      2
  18 6NW6          LDX   3  0(3)                [LD. FPTR. TO NEXT CELL  
   8 6P*Q          BZE   3  XEN1 
  20 6PTB          CALL  6  YREC                [HUNT FOR NEXT IDENTIFIER CELL   
   7 6Q*2          ADX   2  3
  15 6QSL    NXIDN MHUNTW   1,IDF,IDENTIFIERS   [HUNT BLOCK. 
  19 6R#=          ADN   4  1                   [INCREASE POSITION DOWN BLK. 
  18 6RRW          ADN   5  1                   [INCREASE IDENTIFIER NO. 
  17 6S?G          TXU   5  BNO(2)              [TEST IDENTIFIER NO. 
  20 6SR6          BCC      STTYP               [IDENTIFIERS EQUAL J BACK ST TYPE
  15 6T=Q          LDX   6  GSIGN               [SET B0 -VE  
   7 6TQB          ADX   1  4
  21 6W=2          STO   6  0(1)                [ST. -VE TO INDICATE NO IDENTIFIER.  
   8 6WPL          BRN      NXIDN
   4 6X9=    XEN1
   8 6XNW          SMO      FX2  
   9 6Y8G          LDX   0  AWORK4   
   8 6YN6          BNZ   0  XWR  
   8 6_7Q          BRN      XEN2 
   4 6_MB    XADD
   7 7272          LDN   4  2
   8 72LL          BRN      XSETU
   4 736=    #   
  21 73KW    #     THIS PIECE DEALS WITH THE CASE WHEN INFORMATION HAS BEEN REQUESTED
  21 745G    #     ABOUT ONE PARTICULAR IDENTIFIER. THE IDENTIFIER NO. COMES IN HELD 
  21 74K6    #     IN ACOMMUNE3 THIS HAS BEEN TRANSFERED TO AWORK3 IN CASE OF OVER-  
   7 754Q    #     WRITING.  
   4 75JB    #   
   7 7642    XONE  SMO      2
   9 76HL          LDEX  6  BTYPE(3) 
  18 773=          ADN   3  BLINE               [BASE OF IDENTIFIER CHAIN
  17 77GW          CALL  7  ZMSEARCH            [SEARCH FOR A CELL   
   8 782G          SMO      FX2  
  15 78G6          LDX   7  ACOMMUNE1           [TEST TO SEE 
  18 78_Q          BZE   7  XNOIDE              [IDENT.NO. DOES NOT EXIST
   4 79FB    XOK 
   7 79_2          SMO      2
  21 7=DL          LDX   0  CONTCEP(3)          [J. IF THE IDENTIFIER IS NOT ATTACHED
   8 7=Y=          BZE   0  XF   
   9 7?CW          LDX   7  BRECNO(2)
   7 7?XG          SLL   7  9
   7 7#C6          ADX   7  3
   8 7#WQ          SMO      FX2  
  19 7*BB          STO   7  AWORK2              [PRESERVE PTR. TO IDENTIFIER.
   7 7*W2          LDX   3  0
  21 7B*L          SETNCORE 4,1,IDF,CONNAME     [SET UP BLOCK TO CONTAIN CONCEP. NAME
  20 7BT=          CALL  6  YREC1               [HUNT FOR THE CONCEPTUAL CELL.   
   8 7C#W          ADN   2  1(3) 
  10 7CSG          MHUNTW   3,IDF,CONNAME
   8 7D#6          ADN   3  A1+1 
  20 7DRQ          MOVE  2  3                   [MOVE CONCEPTUAL NAME INTO BLOCK.
   8 7F?B          LDX   4  3(2) 
   8 7FR2          ANDN  4  #7777
  19 7G=L          LDX   0  5(2)                [LD. LOWEST IDENTIFIER NO.   
   7 7GQ=          SBN   0  1
   8 7H9W          SMO      FX2  
   9 7HPG          STO   0  AWORK1   
  19 7J96          LDX   3  6(2)                [LD. PTR. TO USER NAME CELL. 
   9 7JNQ          CALL   6  YREC1   
   7 7K8B          SMO      2
  18 7KN2          LDX   3  7(3)                [LD. PTR. TO LINE CELL.  
   8 7L7L          SMO      FX2  
   9 7LM=          LDX   7  AWORK2   
   8 7M6W    XA    CALL  6  YREC 
   7 7MLG    XB    LDN   0  1
   8 7N66          SMO      FX2  
   9 7NKQ          ADS   0  AWORK1   
   7 7P5B          SMO      2
   8 7PK2          LDX   0  0(3) 
   9 7Q4L          ANDX  0  BITS22LS 
   8 7QJ=          BXE   0  7,XE 
   7 7R3W          LDX   0  4
   8 7RHG          LDCT  6  #700 
   7 7S36          ANDX  6  0
   8 7SGQ          ANDN  4  #7777
   7 7T2B          SBN   4  1
   8 7TG2          BZE   4  XD   
   7 7T_L          SLC   6  3
   7 7WF=          ADN   6  1
   7 7WYW          LDN   0  7
   7 7XDG          SBX   0  6
   8 7XY6          BZE   0  XC   
   7 7YCQ          SRC   6  3
   7 7YXB          ADX   4  6
   7 7_C2          ADN   3  1
   8 7_WL          BRN      XB   
   7 82B=    XC    SMO      2
   8 82TW          LDX   3  1(3) 
   8 83*G          BRN      XA   
  10 83T6    XD    GEOERR   1,LOSTLINE   
  10 84#Q    XE    MHUNTW   1,IDF,CONNAME
   8 84SB          LDX   2  FX2  
   9 85#2          LDX   0  AWORK1(2)
   8 85RL          ANDN  0  #7777
   8 86?=          STO   0  A1(1)
   9 86QW          LDX   3  AWORK2(2)
   8 87=G          CALL  6  YREC1
   7 87Q6    XF    SMO      2
  18 87TM ...      LDX   4  BTYPE(3)            [DEVICE TYPE AND GEOG NO 
  15 87WH ...      LDEX  0  4                   [LD. GEOG.NO.
   8 87XC ...      SMO      FX2  
  17 87Y? ...      STO   0  AWORK1              [AWORK1 = GEOG. NO.  
   4 89C5 ...XJ  
   7 89GL ...      SMO      2
  20 89L7 ...      LDX   7  BLINE(3)            [MODE/NPUSIZE IN CORRECT FORMAT  
   7 89PN ...      SMO      2
   9 89T9 ...      LDX   6  BNUMB(3) 
  15 89YQ ...      ANDN  6  #7777               [LINE NUMBER 
   4 8=4? ...XM  
   7 8=8=          LDN   5  0
   8 8?M6          SRL   4  15   
   7 8?WY ...#SKI  JSKI17<1$1
   7 8#6Q          SRL   45 2
   6 8#7Y ...#SKI  JSKI17
   4 8#96 ...(   
   7 8#=# ...      SRL   45 1
  17 8#?G ...      BPZ   5  XSPOOL              [J IF NOT SPOOLABLE  
   8 8##N ...      LDCT  0  #30  
   7 8#*W ...      SMO      2
  18 8#C4 ...      ANDX  0  BNUMB(3)            [SPOOLING,UNATTENDED BITS
  17 8#D= ...      SRL   0  6                   [MOVE TO BITS 10,11  
  18 8#FD ...      ORX   4  0                   [PRESERVE FOR ACOMMUNE2  
   5 8#GL ...XSPOOL  
   6 8#GQ ...#UNS FL7502 
   4 8#GW ...(   
  10 8#H2 ...      BSXD     0,IDF7502LP  
   7 8#H6 ...      SMO      2
  10 8#H= ...      ANDX  0  BNUMB+1(3)   
  12 8#HB ...      SRL   0  14-(IDF7502LP&#37)   
   7 8#HG ...      ORX   4  0
   4 8#HL ...)   
   7 8#HS ...      SRL   45 1
   4 8#K2 ...)   
  18 8#LB          BPZ   5  XNOKB               [J. IF R.J.E MOD NOT SET 
   8 8*62          ORN   4  #2000
   7 8*KL    XNOKB SRL   45 1
  19 8B5=          BPZ   5  XNOK                [J. IF UNKNOWN BIT NOT SET   
   8 8BJW          ORN   4  #400 
   7 8C4G ...XNOK  LDCH  0  4
   7 8CJ6          SBN   0  3
  16 8D3Q          BZE   0  XPRCON              [J.DEVICE IS A LP
   7 8DHB          SBN   0  4
  17 8F32          BZE   0  XPRCON              [J.DEVICE IS A TP.   
  15 8FGL    XNOKA CALL  5  ZCLOSE              [CLOSE IDF   
  16 8G2=          STO   4  ACOMMUNE2(2)        [ST. DEVICE NO.  
  15 8GFW          STO   7  ACOMMUNE1(2)        [ST. MODE.   
  15 8G_G          STO   6  ACOMMUNE3(2)        [ST. LINE NO.
  19 8HF6          STO   3  ACOMMUNE4(2)        [ST. CONS.PROP. IF LP OR TP. 
  15 8HYQ          BRN      SC3                 [J. TO EXIT. 
   5 8JDB    XPRCON  
   7 8JY2          LDX   5  6
   7 8KCL          SMO      2
  19 8KX=          LDX   3  BATT(3)             [LD. PTR. TO ATTRIBUTION CELL
   8 8L2P ...      BNZ   3  XPRA 
   8 8L68 ...      LDX   2  FX2  
  10 8L9M ...      STOZ     ACOMMUNE1(2) 
  10 8L*6 ...      LDN   0  ACOMMUNE1(2) 
  21 8LDK ...      LDN   1  ACOMMUNE2(2)        [STO ZERO IN ACOMMUNE WDS. NO ATTRI. 
   9 8LJ4 ...      MOVE  0  ATTMAX-1 
   8 8LMH ...      BRN      XPRB 
   8 8LR2 ...XPRA  CALL  6  YREC 
   8 8LWG          SMO      FX2  
   9 8MB6          LDN   1  ACOMMUNE1
   7 8MTQ          SMO      2
  20 8N*B          LDN   0  BTYPE(3)            [COPY ATTRIBUTIONS INTO ACOMMUNE 
  14 8NT2          MOVE  0  ATTMAX              [WORDS   
  13 8P#L ...XPRB  SETNCORE ATTMAX,1,GOUT,GPROPNOS   
   9 8PS=          ADN   1  GPROPNUM 
  10 8Q?W          LDN   0  ACOMMUNE1(2) 
  19 8QRG          MOVE  0  ATTMAX              [... THEN INTO GPROPNOS BLOCK
  16 8R?6          LDX   3  ACOMMUNE1(2)        [CONSOLE PROPERTY
   9 8RGY ...      ANDX  3  BITS22LS 
  16 8RHX ...      LDCH  0  4                   [LD. DEVICE TYPE 
   7 8RJW ...      SRC   0  6
  17 8RKT ...      ADX   0  AWORK3(2)           [ADD, IN IDENTIFIER  
  19 8RLS ...      STO   0  JSOURCE1(2)         [ST DEVICE TYPE - IDENTIFIER 
  15 8RMR ...      LDX   0  CACT                [SET B0 - B1 
  16 8RNQ ...      ADX   0  AWORK1(2)           [ADD IN GEOG NO. 
  10 8RPP ...      STO   0  JSOURCE2(2)  
  16 8RQQ          LFBITSET G,2                 [SET FLAG G...   
  15 8S=B          LFBITSET F,2                 [... AND F   
  19 8SQ2          POINTACT OUT,2               [LOCATE SCHEDULER - RING INTO
  14 8T9L          LDX   1  FX2                 [JOBRING 
   9 8TP=          ADN   2  JOBRING  
  10 8W8W          RERING   JOBRING(1),2 
   7 8WNG          LDX   6  5
   8 8X86          BRN      XNOKA
   5 8XMQ    XNOIDE  
   7 8Y7B          OUTBLOCN 4
   8 8YM2          OUTNUM   6,0  
  10 8_6L          OUTNUM   AWORK3(2),0  
  10 8_L=          INFORM   1,IDEWRONG,1 
   7 925W          LDN   3  0
   8 92KG          LDN   4  #400 
   7 9356          LDN   6  0
   7 93JQ          LDN   7  0
   8 944B          BRN      XNOKA
   5 94J2    XIDF1   
  10 953L          LDX   5  ACOMMUNE3(2) 
  17 95H=          STO   5  AWORK3(2)           [FOR USE AFTER OPEN  
   8 962W          LDN   3  BUNIT
   8 96GG          CALL  5  OPEN 
   8 9726          BPZ   6  NOGO 
   8 97FQ          SMO      FX2  
  16 97_B          LDX   4  AWORK3              [LINE NO OR ZERO 
   8 98F2          BPZ   4  SC1  
  18 98YL          CALL  5  S7071               [TO SET UP B7071 BLOCK   
  18 99D=    SC2   CALL  5  SETUP               [GET DEVICE DATA IN X7   
  10 99XW          MHUNTW   1,IDF,B7071  
   7 9=CG          SMO      4
  17 9=X6          STO   7  A1(1)               [STORE DEVICE DATA   
  15 9?BQ          ADN   4  1                   [NEXT LINE   
  20 9?WB          BCT   6  SC2                 [BRANCH ON COUNT OF NO OF LINES  
   4 9#B2    XEN2
  15 9#TL          SETREP   OK                  [SETREP OK.  
   9 9**=    XEN   CALL  5  ZCLOSE   
   7 9*K4 ...SC3   UPPLUS   2
   8 9B#G    SC1   CALL  5  SETUP
  15 9BS6          CALL  5  ZCLOSE              [CLOSE IDF   
  17 9C?Q          STO   7  ACOMMUNE2(2)        [STO. DEVICE DATA.   
   8 9CRB          BRN      SC3  
   9 9D?2    ZNOGO CALL  5  XWRITE   
  17 9DQL    NOGO  CALL  5  ZCLOSE              [ERROR-CLOSE IDF ETC.
   7 9F2D ...      UPPLUS   1
   5 9FPW    XIDF4   
   8 9G9G          LDN   3  BIPB 
   9 9GP6          STOZ     AWORK3(2)
  10 9GW# ...      LDX   0  ACOMMUNE3(2) 
   9 9H3G ...      STO   0  AWORK4(2)
   8 9H8Q          BRN      XCONT
   5 9HNB    XIDF3   
   8 9J82          LDN   3  BUNIT
   9 9JCS ...      STOZ     AWORK4(2)
   5 9JML    XCONT   
   8 9K7=          CALL  5  OPEN 
   8 9K=B ...      SMO      FX2  
  21 9K*G ...      LDX   0  AWORK4              [J. IF BIT5 NOT REQUIRED TO BE TESTED
   8 9KDL ...      BZE   0  XUN  
   9 9KHQ ...      FMOPG    1,4,IPB  
  16 9KLW ...      BNG   1  XUN                 [GEOG NO. NOT IPB
   8 9KQ2 ...      LDCT  0  #10  
  19 9KT6 ...      ANDX  0  IPBSW(1)            [TEST BIT 5 - BEING MOPPED ON
  17 9KY= ...      BNZ   0  NOGO                [J. IF BIT SET ERROR 
   8 9L3B ...XUN   LDCT  5  #700 
   7 9L6G          ANDX  5  6
   8 9LL6          ANDN  6  #7777
  20 9LTY ...      BNZ   5  NOGO                [J IF MOPPED ON,ONLINE OR WRONGED
   6 9M5Q ...#UNS  CRAMP 
   4 9M*J ...#SKI
  18 9MKB ...      BZE   6  SET                 [J IF NO IDENTS ON 7900  
   4 9MT8 ...RPEY
   8 9N52          LDCT  5  #400 
  21 9NJL          ORS   5  BNUMB(3)            [SET MOPON BIT-X3 POINTS TO UNIT CELL
   6 9NPS ...#UNS  CRAMP 
  18 9NX2 ...      BZE   6  SETT                [J IF NO IDENTS ON 7900  
   9 9P4=          LDCH  5  BTYPE(3) 
   8 9PHW          SBN   5  #30  
  16 9Q3G          BZE   5  XJUMA               [J. IF IPB CHAIN 
  17 9QH6          CALL  5  S7071               [SET UP B7071 BLOCK  
   8 9R2Q    MN1   CALL  5  SETUP
  10 9RGB          MHUNTW   1,IDF,B7071  
   7 9S22          SMO      4
   8 9SFL          STO   7  A1(1)
  15 9S_=          ADN   4  1                   [NEXT LINE   
   8 9TDW          LDCT  5  #400 
  17 9TYG          ORS   5  BNUMB(3)            [MARK LINE MOPPED ON 
  16 9WD6          BCT   6  MN1                 [FOR NEXT LINE   
   9 9WXQ    XWR   CALL  5  XWRITE   
   8 9XCB          BRN      XEN2 
   5 9XX2    XJUMA   
   7 9YBL          LDN   6  1
   8 9YW=          SMO      FX2  
   9 9_*W          STO   6  AWORK4   
   8 9_TG          BRN      XJUM 
   6 9_Y5 ...#UNS  CRAMP 
   4 =22N ...(   
   4 =25? ...SETT
  18 =27W ...      CALL  5  XWRITE              [WRITE AWAY MOPPED ON BIT
   4 =2=F ...)   
   4 =2*6    SET 
  21 =2SQ          SETREP   NULL                [SET REPLY NULL = NO LINES ON 7900.  
   8 =3#B          BRN      NOGO 
  21 =3S2    #     THIS PART DEALS WITH THE CASE WHEN UNKOWN BIT REQUIRES SETTING.   
   5 =4?L    XIDF30  
  16 =4R=          LDX   4  ACOMMUNE1(2)        [IDENTIFIER NO.  
  13 =5=W          STO   4  AWORK3(2)           [SAVE
   8 =5QG          LDN   3  BIPB 
  15 =6=6          CALL  5  OPEN                [OPEN IDF.   
   8 =6PQ          SMO      FX2  
  20 =79B          LDX   4  AWORK3              [IDENTIFIER NO. TO BE SEARCH FOR.
   8 =7P2          ADN   3  BLINE
   7 =88L          SBX   3  2
  18 =8N=          CALL  7  ZMSEARCH            [SEARCH FOR IDENTIFIER.  
   8 =97W          SMO      FX2  
   9 =9MG          LDX   0  ACOMMUNE1
   8 ==76          BNZ   0  PU2  
  16 ==LQ    PU3   GEOERR   1,IDENTILL          [IDENTIFIER ILL  
   4 =?6B    PU2 
   8 =?L2          LDCT  4  #4   
   7 =#5L          SMO      2
  16 =#K=          ORS   4  BTYPE(3)            [ORS IN MARKER.  
   8 =*4W          BRN      ZNOGO
   4 =*JG    #   
  16 =B46    #     THIS BIT DEALS WITH THE CASE WHEN A NEW MODE  
   4 =BHQ    #   
   5 =C3B    XIDF35  
  18 =CH2          LDX   4  ACOMMUNE3(2)        [LD. MODE TO BE STORED   
   9 =D2L          STO   4  AWORK4(2)
  16 =DG=          LDX   0  ACOMMUNE1(2)        [IDENTIFIER NO.  
   9 =D_W          STO   0  AWORK3(2)
   8 =FFG          LDN   3  BIPB 
  14 =F_6          CALL  5  OPEN                [OPEN IDF
   8 =GDQ          SMO      FX2  
  15 =GYB          LDX   5  AWORK4              [MODE WORD.  
   8 =HD2          SMO      FX2  
  16 =HXL          LDX   4  AWORK3              [IDENTIFIER NO.  
   7 =JC=          SBX   3  2
   8 =JWW          ADN   3  BLINE
  19 =KBG          CALL  7  ZMSEARCH            [SEARCH FOR IDENTIFIER CELL  
   8 =KW6          SMO      FX2  
   9 =L*Q          LDX   0  ACOMMUNE1
   8 =LTB          BZE   0  PU3  
   7 =LX_ ...      ADX   3  2
  20 =N9F ...      STO   5  BLINE(3)            [MODE IN B0-8,NPUSIZE IN B9-23   
   8 =N#=          BRN      ZNOGO
  11 =NRW          MENDAREA 50,K100IDFSCON   
   4 =P?G    #END
   8 ____ ...20225530000100000000
