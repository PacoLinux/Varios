   7 228= ...#SEG  SCEXEND867
   4 22BG ...#   
  16 22JQ ...#     COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982  
   4 22R2 ...#   
   7 22_=    #LIS  K0SCEXEND 
   7 23DW          8HSCEXEND 
  21 23YG    #              THIS SEGMENT CONTINUES AND COMPLETES EXTEND OR SCRATCH,  
  17 24D6    #              FROM THE POINT WHERE THE AREA UNIT IS GOT
  13 24XQ    #              ENTRY IS FROM NEXT-UNIT  
  16 25CB    #                  AREA UNIT IS ONLINE AND LOCKED   
   4 25X2    #   
  12 26BL          SEGENTRY K1SCEXEND,QK1SCEXEND 
  12 26W=          SEGENTRY K2SCEXEND,QK2SCEXEND 
  12 27*W          SEGENTRY K3SCEXEND,QK3SCEXEND 
  12 27TG          SEGENTRY K4SCEXEND,QK4SCEXEND 
   4 28*6    #   
   6 28SQ    QK1SCEXEND  
   4 29#B    UGOT
  10 29S2          MHUNTW   3,EWDAS,GEXD 
   8 2=?L    #SKI  K6SCEX>199-199
  12 2=R=          TRACE    ARUNIT(3),AREAUNIT   
   8 2?=W    #SKI  K6SCEX>299-299
   4 2?QG    (   
   9 2#=6          LDX   0  ARUNIT(3)
   8 2#PQ          BZE   0  UG1  
   8 2*9B          SRL   0  18   
   8 2*P2          BZE   0  UG2  
   9 2B8L    UG1   GEOERR   1,ARUNIT 
   4 2BN=    UG2 
   4 2C7W    )   
  10 2CMG          MFINDEXOF 2,CEFN(3)   
  21 2D76          NGS   3  EOFCYL(3)           [SET EOFCYL -VE,EOF AREA IS NOT ON   
  19 2DLQ          STOZ     CELLD(3)            [CLEAR CELLD    /THIS UNIT   
   9 2F6B          LDX   0  ARUNIT(3)
  10 2FL2          TXU   0  EOFUNIT(3)   
  17 2G5L          BCS      START               [J IF NOT EOF UNIT   
   9 2GK=          LDEX  0  FIP+5(2) 
   7 2H4W          SBN   0  2
  16 2HJG          BNG   0  START               [J IF NO EOF AREA
   7 2HS# ...      ADN   0  2
   7 2J46          SLL   0  1
   7 2JHQ          LDX   1  0
   7 2K3B          SLL   1  1
   7 2KH2          ADX   1  0
  17 2L2L          ADN   2  FDCELLS(1)          [POINTER TO EOF CELL 
  16 2LG=          LDX   0  3(2)                [NR OF CYLINDERS 
  21 2L_W          BPZ   0  WFD                 [J IF FULL DEPTH,TAKE END CYLINDER+1,
   7 2MFG          SLL   0  1
   7 2M_6          SBN   0  2
  20 2NDQ          BNZ   0  START               [J IF MORE THAN 1 CYLINDER WIDE, 
  19 2NYB    WFD   ADX   0  1(2)                [ELSE TAKE START CYLINDER,   
  16 2PD2          STO   0  EOFCYL(3)           [AS EOF CYLINDER 
   8 2PXL    #SKI  K6SCEX>299-299
   9 2QC=          TRACE    0,EOFCYL 
   8 2QWW          LDX   0  2(2) 
  18 2RBG          STO   0  EOFBLOCK(3)         [START BLOCK=EOF BLOCK   
   4 2RW6    #   
  18 2S*Q    START                              [START SEARCH FOR AREA   
  21 2STB    #                  OPEN AREA UNIT,READ CONTROL HEADER AND FIRST UNAL CEL
  10 2T*2          MHUNTW   3,EWDAS,GEXD 
  19 2TSL          OPENST   ARUNIT(3)           [OPEN AREA UNIT FOR TRANSFERS
   8 2W#=          LDN   7  128  
  19 2WRW          READSAVE 7,SC1               [READ & SAVE CONTROL HEADER  
  15 2X?G          TESTREPNOT OK,XN15           [J IF FAIL   
   9 2XR6          LDX   1  ECOR(3)  
   8 2Y=Q    SC1   SMO      FX2  
  21 2YQB          STO   1  AWORK4              [CORE ADDRESS OF CH IN AWORK4 (FOR JB
   8 2_=2          LDN   1  10(1)
  21 2_PL          STO   1  CELLA+5(3)          [COREAD OF HEAD OF UNALLOCATED CHAIN 
  16 329=          STOZ     CELLB+5(3)          [CLEAR CELL(B)   
  19 32NW          SAVEST   C,CELLA+5(3)        [SAVE CELL(A)= HEAD OF CHAIN 
   8 338G          BRN      SC2  
   4 33N6    #   
  18 347Q    #                  NEW CELL REQUIRED FROM UNALLOCATED CHAIN 
   5 34MB    NCELL   
  10 3572          MHUNTW   3,EWDAS,GEXD 
  19 35LL          CHUCK    C,CELLP(3)          [CHUCK OLD PREVIOUS CELL (P) 
  17 366=    SC2   LDX   1  CELLA+5(3)          [CORE ADDRESS CELL(A)
  19 36KW          LDX   6  0(1)                [WORD ADDRESS OF NEXT CELL   
  18 375G          BZE   6  NOCELL              [J IF NO MORE IN CHAIN   
  18 37K6          STO   1  CELLP(3)            [SET CELL(P)= OLD CELL(A)
  10 384Q          LDX   0  CELLB+5(3)   
  17 38JB          BZE   0  SC3                 [J IF CELL(B)NOT READ
   8 3942    #SKI  K6SCEX>399-399
   4 39HL    (   
  10 3=3=          TXU   6  CELLB+4(3)   
   8 3=GW          BCC      SC2A 
   9 3?2G          GEOERR   1,CELLB? 
   4 3?G6    SC2A
   4 3?_Q    )   
  20 3#FB          LDN   4  CELLB(3)            [ELSE COPY DETAILS OF CELLB TO   
  14 3#_2          LDN   5  CELLA(3)            [CELLA   
   7 3*DL          MOVE  4  6
  15 3*Y=          STOZ     CELLB+5(3)          [CLEAR CELLB 
   8 3BCW          BRN      SC4  
  18 3BXG    SC3   READSAVE 6,SC5               [READ NEXT CELL & SAVE   
  15 3CC6          TESTREPNOT OK,XN15           [J IF FAIL   
   9 3CWQ          LDX   1  ECOR(3)  
  19 3DBB    SC5   CALL  7  SETC                [SET UP NEW CELL AS CELL(A)  
  20 3DW2    SC4   CALL  6  SCRX                [CHECK IF IN USE BY SCRATCH FILE 
  15 3F*L    #                  ELSE WE HAVE A NEW CELL(A)   
  14 3FT=    #                  FENCE BREAK INTO AREA(A) 
  18 3G#W          ACROSS   EXINTWO,2          [COMES BACK TO K2SCEXEND  
   4 3GSG    #   
  18 3H#6    #                  S/R TO SET UP CELLA FROM BS COPY OF CELL 
   4 3HRQ    SETC
  17 3J?B          STO   1  CELLA+5(3)          [STORE CORE ADDRESS, 
  16 3JR2          STO   6  CELLA+4(3)          [BS WORD ADDRESS,
  16 3K=L          LDX   5  1(1)                [(START BLOCK)   
   9 3KQ=          DVS   4  EDEPF(3) 
  16 3L9W          STO   5  CELLA(3)            [START CYLINDER, 
  15 3LPG          STO   4  CELLA+2(3)          [START BLOCK,
   8 3M96          LDXC  5  2(1) 
   7 3MNQ          BCS      /
  15 3N8B          SBN   5  1                   [(END BLOCK) 
   9 3NN2          DVS   4  EDEPF(3) 
  16 3P7L          STO   5  CELLA+1(3)          [END CYLINDER,   
  15 3PM=          STO   4  CELLA+3(3)          [END BLOCK   
   7 3Q6W          EXIT  7  0
   4 3QLG    #   
  15 3R66    #                  RE-ENTRY AFTER FENCE BREAKER,
   6 3RKQ    QK2SCEXEND  
   4 3S5B    #   
  10 3SK2          MHUNTW   3,EWDAS,GEXD 
   8 3T4L    #SKI  K6SCEX>199-199
  11 3TJ=          TRACE    CELLA+4(3),CELLA 
   8 3W3W    #SKI  K6SCEX>399-399
   4 3WHG    (   
   7 3X36          LDN   0  4
   9 3XGQ    STT1  TRACE    CELLA(3) 
   7 3Y2B          ADN   3  1
   8 3YG2          BCT   0  STT1 
   7 3Y_L          SBN   3  4
   4 3_F=    )   
   9 3_YW          LDX   0  CELLD(3) 
  19 42DG          BNZ   0  SH1                 [J IF WE HAVE AN AREA SAVED  
  18 42Y6          CALL  7  SUIT                [CHECK AREA IS SUITABLE  
   9 43CQ          LDX   0  EOFCYL(3)
  21 43XB          BNG   0  XGOT                [TAKE THIS AREA IF NOT EOF AREA,ELSE 
   9 44C2          TXL   0  CELLA(3) 
  21 44WL          BCS      XGOT                [TAKE IT IF WE HAVE PASSED EOF AREA  
  18 45B=          TXU   0  CELLA(3)            [J IF NOT EOF CYLINDER,  
   8 45TW          BCS      SH2  
  10 46*G          LDX   4  CELLA+2(3)   
  18 46T6          TXL   4  EOFBLOCK(3)         [ELSE TAKE THIS AREA IF  
  18 47#Q          BCC      XGOT                [WE HAVE PASSED EOF BLOCK
  21 47SB    SH2   LDN   4  CELLP(3)            [EOF CYLINDER/BLOCK NOT YET FOUND,   
  18 48#2          LDN   5  CELLD(3)            [SAVE THIS SUITABLE AREA 
  14 48RL          MOVE  4  7                   [IN CELLD
   8 49?=    #SKI  K6SCEX>399-399
  11 49QW          TRACE    CELLD(3),SAVECELL
  17 4==G          SAVEST   C,CELLP(3)          [SAVE CELLS A AND P  
  10 4=Q6          SAVEST   C,CELLA+5(3) 
   8 4?9Q          BRN      NCELL
  17 4?PB    SH1   LDX   0  EOFCYL(3)           [LOOKING FOR EOF AREA
   9 4#92          TXL   0  CELLA(3) 
  18 4#NL          BCS      SH3                 [J IF PAST EOF CYLINDER  
  10 4*8=          LDX   4  CELLA+2(3)   
  10 4*MW          TXL   4  EOFBLOCK(3)  
  16 4B7G          TXU   0  CELLA(3)            [GET NEXT AREA IF
  18 4BM6          BCS      NCELL               [NOT YET UP TO EOF AREA, 
  19 4C6Q          CALL  7  SUIT                [ELSE CHECK AREA IS SUITABLE 
  18 4CLB          CHUCK    C,CELLD(3)          [IF SO,CHUCK SAVED AREA  
  10 4D62          CHUCK    C,CELLD+6(3) 
  21 4DKL          BRN      XGOT                [AND USE THIS AREA (NEXT TO EOF AREA)
   4 4F5=    #   
  17 4FJW    NOCELL         [=D7,NO MORE CELLS IN UNALLOCATED CHAIN  
  10 4G4G          MHUNTW   3,EWDAS,GEXD 
   9 4GJ6          LDX   0  CELLD(3) 
  21 4H3Q          BZE   0  XN14                [J IF NO SAVED AREA,NO ROOM ON THIS  
  15 4HHB          BRN      SH4                 [UNIT,ELSE,  
   4 4J32    #   
  19 4JGL    SH3   CHUCK    C,CELLP(3)          [NO AREA NEXT TO EOF,SO CHUCK
  17 4K2=    SH4   CHUCK    C,CELLA+5(3)        [CURRENT CELLS AND   
  17 4KFW          LDN   4  CELLD(3)            [RESTORE SAVED AREA  
   9 4K_G          LDN   5  CELLP(3) 
   7 4LF6          MOVE  4  7
   8 4LYQ    #SKI  K6SCEX>399-399
  12 4MDB          TRACE    CELLA+5(3),RESRCELL  
   4 4MY2    #   
  21 4NCL    XGOT                               [WE HAVE AN AREA:-IS IT TO BE SPLIT? 
   8 4NX=    #SKI  K6SCEX>299-299
  11 4PBW          TRACE    CELLA(3),GOTAREA 
  10 4PWG          LDX   4  CELLB+5(3)   
  16 4QB6          BZE   4  XG1                 [J IF NO CELL B  
  16 4QTQ          CHUCK    C,4                 [ELSE CHUCK IT   
  10 4R*B    XG1   LDX   1  CELLA+5(3)   
   8 4RT2          LDX   0  1(1) 
  18 4S#L          STO   0  ARQS(3)             [SAVE START BLOCK OF AREA
  17 4SS=          LDN   4  CELLA(3)            [MOVE DETAILS OF AREA
  14 4T?W          LDN   5  AREQT(3)            [TO AREQT
   7 4TRG          MOVE  4  4
  20 4W?6          LDX   4  CELLA+1(3)          [SET UP SOME VALUES IN CELLB:-   
   7 4WQQ          ADN   4  1
   9 4X=B          SBX   4  CELLA(3) 
  18 4XQ2          STO   4  CELLB+1(3)          [CELLB+1=WIDTH OF AREA   
  10 4Y9L          LDX   0  CELLA+3(3)   
   7 4YP=          ADN   0  1
  10 4_8W          SBX   0  CELLA+2(3)   
  18 4_NG          STO   0  CELLB+2(3)          [CELLB+2=DEPTH OF AREA   
   7 5286          MPY   4  0
  17 52MQ          STO   5  CELLB(3)            [CELLB=SIZE OF AREA  
  16 537B          MFINDSU  2,ARUNIT(3)         [FIND AREA ASTUC 
   9 53M2          LDX   4  HCYL(2)  
   8 546L    #SKI  K6SCEX>399-399
   4 54L=    (   
  10 555W          TXL   4  CELLB+1(3)   
   8 55KG          BCC      XG2  
   9 5656    XG3   GEOERR   1,CELLA? 
   9 56JQ    XG2   LDX   5  EDEPF(3) 
   9 574B          TXU   5  HBLOCK(2)
   8 57J2          BCS      XG3  
   4 583L    )   
  15 58H=          MPY   4  HBLOCK(2)           [DEPTH*WIDTH 
  13 592W          SRL   5  4                   [/16 
  19 59GG          STO   5  CELLB+3(3)          [CELLB+3=SIZE OF AREA UNIT/16
  18 5=26          LDX   7  HCYL(2)             [X7=WIDTH OF AREA UNIT   
  18 5=FQ          LDN   6  0                   [SET COUNT AND MARKER=0  
  19 5=_B          LDX   0  AREQT(3)            [DO WE SPLIT OFF CYLINDERS?  
  10 5?F2          TXU   0  AREQT+1(3)   
  19 5?YL          BCC      SP1                 [J IF AREA 1 CYLINDER WIDE   
   9 5#D=          LDX   5  BREQ(3)  
  19 5#XW          SBN   5  1                   [REQUEST SIZE/DEPTH (ROUND-  
  18 5*CG          DVS   4  CELLB+2(3)          [ED UP) TO GET MIN NR OF 
  18 5*X6          ADN   5  1                   [CYLINDERS NEEDED (X5)   
  10 5BBQ          TXU   5  CELLB+1(3)   
  21 5BWB          BCC      TAKE                [J IF NO CYLINDERS TO BE SPLIT OFF   
   8 5CB2    #SKI  K6SCEX>399-399
   4 5CTL    (   
  10 5D*=          TXL   5  CELLB+1(3)   
   8 5DSW          BCC      XG3  
   4 5F#G    )   
  10 5FS6          LDX   4  CELLB+1(3)   
  19 5G?Q          SBX   4  5                   [NR OF CYLINDERS TO SPLIT OFF
   7 5GRB          TXL   4  5
  21 5H?2          BCC      SP2                 [SPLIT IF AREA TAKEN<OR= REMAINDER   
  13 5HQL          SRL   7  4                   [ELSE
  17 5J==          TXL   7  4                   [DONT SPLIT UNLESS   
  19 5JPW          BCC      TAKE                [REMAINING AREA>1/16 OF UNIT 
  20 5K9G    SP2   SBS   4  AREQT+1(3)          [RESET END CYLINDER TO SHOW SPLIT
  19 5KP6          LDCT  6  #200                [SET CYLINDER-SPLIT MARKER   
  16 5L8Q          ADN   6  1                   [AND COUNT OF 1  
   7 5LNB          SBN   5  1
  21 5M82          BNZ   5  TAKE                [J UNLESS AREA NOW 1 CYLINDER WIDE   
   8 5MML    #SKI  K6SCEX>399-399
   4 5N7=    (   
   9 5NLW          LDX   5  AREQT(3) 
  10 5P6G          TXU   5  AREQT+1(3)   
   8 5PL6          BCS      XG3  
   4 5Q5Q    )   
  14 5QKB    SP1   LDX   5  CELLB+2(3)          [WIDTH   
  18 5R52          SBX   5  BREQ(3)             [BLOCKS TO BE SPLIT OFF  
  19 5RJL          BZE   5  TAKE                [J IF AREA EXACTLY RIGHT SIZE
   8 5S4=    #SKI  K6SCEX>399-399
   8 5SHW          BNG   5  XG3  
  19 5T3G          TXL   5  BREQ(3)             [SPLIT IF AREA SPLIT OFF IS  
  16 5TH6          BCC      SP3                 [NOT < AREA LEFT 
  13 5W2Q          LDX   0  CELLB+3(3)          [ELSE
  18 5WGB          TXL   0  5                   [TAKE WHOLE AREA UNLESS  
  20 5X22          BCC      TAKE                [AREA SPLIT OFF>OR=1/16 OF UNIT  
  20 5XFL    SP3   SBS   5  AREQT+3(3)          [ADJUST END BLOCK TO SHOW SPLIT  
   8 5X_=          LDCT  0  #400 
  18 5YDW          ORX   6  0                   [SET BLOCK-SPLIT MARKER  
  18 5YYG          ADN   6  1                   [ADD 1 TO COUNT OF SPLITS
   4 5_D6    #   
  20 5_XQ    TAKE                   [SPLIT IF NECESSARY,RECHAIN CELLS,ETC,ETC,   
   8 62CB    #SKI  K6SCEX>99-99  
  11 62X2          TRACE    AREQT(3),AREAUSED
   8 63BL    #SKI  K6SCEX>299-299
   4 63W=    (   
   7 64*W          LDN   0  3
  10 64TG    TK1   TRACE    AREQT+1(3)   
   7 65*6          ADN   3  1
   8 65SQ          BCT   0  TK1  
   7 66#B          SBN   3  3
   7 66S2          TRACE    6
   4 67?L    )   
  12 67R=    #                  AS FOR MODE *1500
  16 68=W          ACROSS   EXINTO,3            [RETURNS TO...   
   4 68QG    #   
   4 69=6    #   
   6 69PQ    QK4SCEXEND  
  16 6=9B    #                  CONNECTED TO FILE,EXOF IS UPDATED
   4 6=P2    #   
  17 6?8L          EXMESS                       [OUTPUT ANY MESSAGES 
  10 6?N=          MHUNTW   3,EWDAS,GEXD 
   8 6#7W          LDCT  0  #40  
   9 6#MG          ANDX  0  EMARK(3) 
  20 6*76          BZE   0  NOPE                [J UNLESS OPENING SCRATCH FILE   
  21 6*#Y ...      HUNTW    3,EWDAS,ASCOL       [SET UP IF IPB DOWN FOR OL SCRATCH   
   8 6*BW ...      BNG   3  NTH  
  16 6*DS ...      FREECORE 3                   [FREE IF PRESENT 
   4 6*GQ ...NTH 
  16 6*LQ          ACROSS   ANTIC,1             [GO TO CONNECT   
   4 6B6B    #   
  20 6BL2    NOPE                               [SET REPLIES TO SUCCESFUL EXTEND 
  10 6C5L          MFINDEXOF 1,CEFN(3)   
   9 6CK=          LDEX  2  FIP+5(1) 
   7 6D4W          SBN   2  1
   8 6DJG    #SKI  K6SCEX>399-399
   4 6F46    (   
   8 6FHQ          BPZ   2  WK1  
   9 6G3B    WK2   GEOERR   1,NEWAREA
   4 6GH2    WK1 
   4 6H2L    )   
  18 6HG=          SLL   2  1                   [GET REPLIES FROM EXOF   
   7 6H_W          LDX   5  2
   7 6JFG          SLL   2  1
   7 6J_6          ADX   2  5
  21 6KDQ          ADN   2  FBUK(1)             [POINTER TO LAST WORD OF 2ND-TO-LAST 
  21 6KYB          LDX   5  0(2)                [FD CELL. GET LAST LBN BEFORE (+1)   
   8 6LD2          LDX   6  6(2) 
  20 6LXL          SBX   6  5                   [NUMBER OF BUCKETS EXTENDED BY   
  16 6MC=          SBN   5  1                   [LAST LBN BEFORE 
  10 6MWW          LDX   0  EOPMODE(3)   
   8 6NBG          SBN   0  #400 
  19 6NW6          BNZ   0  WK3                 [J UNLESS OPEN IN EOF MODE   
  18 6P*Q          SBX   5  FIP+10(1)           [ELSE SET LBN RELATIVE   
  16 6PTB          ADN   5  1                   [TO END OF FILE, 
   8 6Q*2          BPZ   5  WK3  
  18 6QSL          LDN   5  0                   [OR ZERO IF OUT OF RANGE 
   4 6R#=    WK3 
  18 6RRW          LDX   7  1(2)                [SERIAL NR FOR NEW AREA  
   8 6S?G    #SKI  K6SCEX>399-399
   4 6SR6    (   
   9 6T=Q          TXU   7  ARUNIT(3)
   8 6TQB          BCS      WK2  
   4 6W=2    )   
  15 6WPL          LDX   4  CNR(3)              [CA ADDRESS  
  15 6X9=          FREECORE 3                   [FREE /GEXD  
   8 6XNW          CALL  3  WORD 
  21 6Y8G          STO   5  0(2)                [SET REPLY WORD 1  (LAST LBN BEFORE) 
   8 6YN6          CALL  3  WORD 
  21 6_7Q          STO   6  0(2)                [SET REPLY WORD 2  (NR OF BUCKETS)   
   7 6_MB          ADN   4  1
   8 7272          CALL  3  WORD 
  21 72LL          STO   7  0(2)                [SET REPLY WORD 4  (SERIAL NR NEWAR) 
   7 736=          WORDFIN   
  16 73KW          RUNPROG                      [RESTART PROGRAM 
  13 745G    #                                  [****
   4 74K6    #   
  21 754Q    WORD                               [S/R TO GET ADDRESS OF OBJECT PROGRAM
  20 75JB          SBX   3  FX1                 [WORD([X4]+1),IN X2,USING GETWORD
   7 7642          ADN   4  1
   9 76HL          GETWORD  4,2,WRITE
   8 773=    #SKI  K6SCEX>399-399
   4 77GW    (   
   9 782G          TESTREP  OK,WOK   
  18 78G6          GEOERR   1,SCEXPROG          [REPLY RESVIOL OR NOCORE 
   4 78_Q    WOK 
   4 79FB    )   
   8 79_2          ADX   3  FX1  
   7 7=DL          EXIT  3  0
   4 7=Y=    #   
   4 7?CW    #   
   4 7?XG    #   
   4 7#C6    #   
  19 7#WQ    #                  S/R TO CHECK IF AREA(A) USED BY SCRATCH FILE 
   4 7*BB    SCRX
  10 7BT=          SMO      CELLA+5(3)   
  19 7C#W          LDX   4  1                   [GET START BLOCK OF CELL A   
  15 7CSG          MFINDSU  1,ARUNIT(3)         [FIND  ASTUC 
  18 7D#6          LDX   0  CSCRATCH(1)         [CHECK AREA A NOT SCRATCH
   8 7DRQ    #SKI  K6SCEX>299-299
   4 7F?B    (   
   8 7FR2          BPZ   0  SU2  
  10 7G=L          GEOERR   1,SCRTCHCT   
   4 7GQ=    )   
  18 7H9W    SU2   BZE   0  SU1                 [J NO MORE SCRATCH AREAS 
   7 7HPG          SBN   0  1
   7 7J96          SMO      0
  10 7JNQ          TXU   4  HSCRATCH(1)  
  20 7K8B          BCC      NCELL               [J IF SAME START BLOCK,AREA A IS 
  17 7KN2          BRN      SU2                 [SCRATCH AND NO GOOD 
  21 7LG2 ...SU1   CHAREA   ARUNIT(3),4,NCELL   [CHECK IF FILE IN USE IN OTHER PROCES
  14 7QJ=          EXIT  6  0                   [AREA OK 
   4 7RHG    #   
  19 7S36    #                  S/R TO CHECK IF AREA(A SUITABLE FOR EXTEND   
   4 7SGQ    SUIT
  20 7T_L          CALL  6  SCRX                [CHECK NOT USED BY SCRATCH FILE, 
  14 7WF=          LDX   0  CELLA+1(3)          [ELSE,   
   9 7WYW          TXU   0  CELLA(3) 
  18 7XDG          BCC      SU3                 [J IF ONLY 1 CYLINDER,   
  10 7XY6          SMO      CELLA+5(3)   
  21 7YCQ          LDX   4  2                   [ELSE CHECK THAT IT IS FULL-DEPTH,   
  16 7YXB          BNG   4  NCELL               [NO GOOD IF NOT  
  17 7_C2    SU3   SBX   0  CELLA(3)            [AREA IS SUITABLE,   
   7 7_WL          ADN   0  1
  17 82B=          LDX   4  CELLA+3(3)          [IS IT BIG ENOUGH?   
  10 82TW          SBX   4  CELLA+2(3)   
   7 83*G          ADN   4  1
  17 83T6          MPY   0  4                   [TOTAL SIZE OF AREA  
   9 84#Q          TXL   1  BREQ(3)  
  16 84SB          BCS      SU4                 [J IF TOO SMALL  
  17 86?=          EXIT  7  0                   [ELSE EXIT,AREA OK   
   9 86QW    SU4   TXL   1  BAVAIL(3)
  19 87=G          BCS      NCELL               [J IF SMALLER THAN REPLY AREA
  20 87Q6          STO   1  BAVAIL(3)           [ELSE REMEMBER AS BIGGEST AREA   
  20 889Q          BRN      NCELL               [FOUND SO FAR AND GO FOR NEXT.   
   4 88PB    #   
   4 8992    #   
   4 89NL    #   
  16 8=8=    #                  AREA NOT FOUND ON CURRENT UNIT   
   4 8=MW    XN14
  15 8?7G          LDN   6  #40                 [REPLYBIT=NO 
  18 8?M6          LDN   5  XNM1                [MESSAGE=AREA NOT FOUND  
  10 8#6Q    XN18  MHUNTW   3,EWDAS,GEXD 
  17 8#LB          CLOSEST  XN19                [CLOSE CURRENT UNIT  
  15 8*62          TESTREPNOT OK,XN15           [J IF FAIL   
  10 8*KL    XN19  MHUNTW   3,EWDAS,GEXD 
  15 8B5=          ORS   6  EMARK(3)            [SET REPBIT  
   8 8BJW    #SKI  K6SCEX>299-299
  11 8C4G          TRACE    EMARK(3),NOGOOD  
   9 8CJ6          LDX   7  ARUNIT(3)
  16 8D3Q          UNLOCKSU 7                   [UNLOCK AREA UNIT
   8 8DHB          LDX   1  FX1  
   7 8F32          ADX   1  5
  18 8FGL          OPTMESS  0(1),7              [OPTIONALLY STORE MESSAGE
   5 8G2=    NUNIT   
  16 8GFW          ACROSS   NEXTUNIT,2          [TRY NEXT UNIT   
   4 8G_G    #   
  16 8HF6    XNM1           +EWNAREA            [MESSAGE,NO AREA 
  15 8HYQ    #                  TRANSFER FAIL ON AREA UNIT   
   4 8JDB    XN15
  10 8JY2          MHUNTW   3,EWDAS,GEXD 
   8 8KCL          LDN   0  #10  
  16 8KX=          ORS   0  EMARK(3)            [SET REPBIT=FAIL 
   9 8LBW          LDX   6  HMUNIT(3)
   9 8LWG          LDX   7  ARUNIT(3)
  16 8MB6          UNLOCKSU 7                   [UNLOCK AREA UNIT
  18 8MTQ          TXU   6  7                   [GO FOR NEXT UNIT IF FAIL
  16 8N*B          BCS      NUNIT               [NOT ON HOME UNIT
  14 8NT2          BRN      XEND                [AND END 
   4 8P#L    #   
  18 8PS=    #                  ENTRY FROM EXINTO IF ERROR,MARKER IN X7  
   6 8Q?W    QK3SCEXEND  
  16 8QRG          BPZ   7  XN16                [J IF NOT SCAFULL
  15 8R?6          LDN   5  XNM2                [GET MESSAGE 
  15 8RQQ          LDN   6  #20                 [AND REPBIT  
  12 8S=B          BRN      XN19                [
   4 8SQ2    #   
  16 8T9L    XNM2           +EWFULS             [MESSAGE SCAFULL 
   4 8TP=    #   
  18 8W8W    XN16  BNZ   7  XN15                [J IF FAIL ON AREA UNIT  
  18 8WNG          MHUNTW   3,EWDAS,GEXD        [ELSE FAIL ON HOME UNIT  
   8 8X86          LDN   0  #10  
   7 8XMQ          SMO      3
  18 8Y7B          DCH   0  EMARK               [SET REPBIT FAIL (ONLY)  
  16 8YM2          UNLOCKSU HMUNIT(3)           [UNLOCK HOME UNIT
   4 8_6L    XEND
  18 8_L=          ACROSS   NEXTUNIT,3          [TO GENERAL ERROR ROUTINE
   4 925W    #   
   4 92KG    #   
   4 9356    #   
  10 93JQ          MENDAREA 30,K99SCEXEND
   4 944B    #END
   8 ____ ...71770513000100000000
