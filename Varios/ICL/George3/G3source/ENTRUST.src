  12 22FL          SEG      ENTRUST,,ALMANDRAS   
   4 22_=    #   
  12 23DW          SEGENTRY K1ENTRUST,XK1ENTRUST 
  12 23YG          SEGENTRY K2ENTRUST,XK2ENTRUST 
   8 24D6    TPQ            4HQ  
   8 24XQ    TPR            4HR  
   7 25CB    TPS         4HS 
   9 25X2    PRIV           8HTRUSTED
   4 26BL    #   
   4 26W=    #   
   6 27*W    XK2ENTRUST  
   4 27TG    #   
  19 28*6          LDN   4  #7777               [SET FLAG TO CANCEL ENTRUST  
   8 28SQ          BRN      TPPAR
   4 29#B    #   
   6 29S2    XK1ENTRUST  
   4 2=?L    #   
  16 2=R=          STOZ  4                      [FLAG FOR ENTRUST
  19 2?=W    TPPAR PARAPASS                     [COPY COMMAND PARAMETER INTO 
  16 2?QG    #                                   CPB/CUNI BLOCK  
  19 2#=6          MHUNT    3,CPB,CUNI          [X3=POINTER TO CPB/CUNI BLOCK
   9 2#PQ          LDX   5  ANUM(3)  
  18 2*9B          BZE   5  PERR1               [JUMP IF PARAMETER NULL  
   7 2*P2          ADN   5  1
  20 2B8L          BZE   5  PERR1               [JUMP IF PARAMETER NON EXISTENT  
  18 2BN=          PARANUMB 5                   [CHECK TO SEE THAT THERE 
  18 2C7W          SBN   5  1                   [IS ONLY ONE PARAMETER   
  19 2CMG          BNZ   5  PERR4               [JUMP IF MORE THAN ONE PARAM 
  18 2D76          BNZ   4  TENT                [JUMP IF CANCEL ENTRUST  
  21 2DLQ          CHEKPRIV 0,PRIV,PERR3        [CHECK IF PROGRAM HAS TRUSTED PRIVILE
  17 2F6B    TENT  MHUNT    3,CPB,CUNI          [RELOCATE CPB/CUNI   
  14 2FL2          LDX   6  TPQ(1)              [X6='Q'  
  20 2G5L          LDX   7  APARA(3)            [X7=PARAMETER FROM CPB/CUNI BLOCK
  19 2GK=          TXU   6  7                   [SET C IF PARAMETER IS NOT Q 
  18 2H4W          BCC      TPQSET              [JUMP IF PARAMETER IS Q  
  14 2HJG          LDX   6  TPR(1)              [X6='R'  
  19 2J46          TXU   6  7                   [SET C IF PARAMETER IS NOT R 
  18 2JHQ          BCC      TPRSET              [JUMP IF PARAMETER IS R  
  14 2K3B          LDX   6  TPS(1)              [X6= 'S' 
  19 2KH2          TXU   6  7                   [SET C IF PARAMETER IS NOT S 
  19 2L2L          BCS      PERR2               [JUMP IF PARAMETER IS NOT S  
   4 2LG=    #   
   5 2L_W    TPSSET  
  21 2MFG          CALL  1  TPSUB               [CALL SUBR TO PROCESS 'S' PRIVILEGE  
  21 2M_6                   #75777777           [MASK FOR CANCELLING 'S' PRIVILEGE   
  20 2NDQ                   #02000000           [MASK FOR GIVING 'S' PRIVILEGE   
   8 2NYB          BRN      TPEND
   4 2PD2    #   
   5 2PXL    TPRSET  
  21 2QC=          CALL  1  TPSUB               [CALL SUBR TO PROCESS 'R' PRIVILEGE  
  21 2QWW                   #73777777           [MASK FOR CANCELLING 'R' PRIVILEGE   
  20 2RBG                   #04000000           [MASK FOR GIVING 'R' PRIVILEGE   
   8 2RW6          BRN      TPEND
   4 2S*Q    #   
   5 2STB    TPQSET  
  21 2T*2          CALL  1  TPSUB               [CALL SUBR TO PROCESS 'Q' PRIVILEGE  
  21 2TSL                   #67777777           [MASK FOR CANCELLING 'Q' PRIVILEGE   
  20 2W#=                   #10000000           [MASK FOR GIVING 'Q' PRIVILEGE   
   6 2WRW    TPEND CCOFF 
   6 2X?G          ENDCOM
   4 2XR6    #   
   5 2Y=Q    TPSUB   
  17 2YQB          FPCACA   3,2                 [X3=POINTER TO PCA   
  19 2_=2          LDX   6  ATRUST(3)           [X6=TRUSTED STATUS IN B2-4   
  16 2_PL          BZE   4  TPENTR1             [JUMP IF ENTRUST 
  18 329=    #                                   OTHERWISE CANCEL ENTRUST
  19 32NW          ANDX  6  0(1)                [ZERO APPROPRIATE TRUST BIT  
   8 338G          BRN      TPGOR
   5 33N6    TPENTR1 
  19 347Q          ORX   6  1(1)                [SET APPROPRIATE TRUST BIT   
  17 34MB    TPGOR STO   6  ATRUST(3)           [STORE RESULT IN PCA 
  19 3572          HUNT2    3,AOBRES,0          [X3=POINTER TO REQUEST SLIP  
  19 35LL          LDX   6  A1+2(3)             [X6=TRUSTED STATUS IN B2-4   
  16 366=          BZE   4  TPENTR2             [JUMP IF ENTRUST 
  18 36KW    #                                   OTHERWISE CANCEL ENTRUST
  19 375G          ANDX  6  0(1)                [ZERO APPROPRIATE TRUST BIT  
   8 37K6          BRN      SFIN 
   5 384Q    TPENTR2 
  19 38JB          ORX   6  1(1)                [SET APPROPRIATE TRUST BIT   
  19 3942    SFIN  STO   6  A1+2(3)             [STORE RESULT IN REQUEST SLIP
   7 39HL          EXIT  1  2
  20 3=3=    PERR1 COMERR   JPARMIS,JNEW2       [ERROR MESSAGE - TRUSTED STATUS  
  17 3=GW    #                                   PARAMETER MISSING   
  20 3?2G    PERR2 COMERR   ERMCH               [ERROR MESSAGE - PARAMETER NOT   
  15 3?G6    #                                   RECOGNIZED  
  20 3?_Q    PERR3 COMERR   ERB4,JNEW1          [ERROR MESSAGE - YOU DO NOT HOLD 
  17 3#FB    #                                   TRUSTED PRIVILEGE   
  18 3#_2    PERR4 COMERR   JONEPAR             [ERROR MESSAGE - ONLY ONE
  17 3*DL                                       [PARAMETER ALLOWED   
   5 3*Y=    #GAP  20
   4 3BCW    #END
   8 ____ ...50230364000100000000
