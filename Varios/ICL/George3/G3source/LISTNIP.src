   8 22FL    #OPT  K0LISTNIP=0   
  12 22_=    #LIS  K0LISTNIP>K0LISTFILE>K0ALLGEO 
  16 23DW    #SEG  LISTNIP                      [H.CAMPBELL-GRANT
   9 23YG                   8HLISTNIP
  16 24D6          SEGENTRY K1LISTNIP,POINT1    [NIPSTART MACRO  
  16 24XQ          SEGENTRY K2LISTNIP,POINT2    [NIPRPERT MACRO  
  16 25CB          SEGENTRY K3LISTNIP,POINT3    [NIPLINES MACRO  
  16 25X2          SEGENTRY K4LISTNIP,POINT4    [NIPENDLF MACRO  
  15 26BL ...      SEGENTRY K5LISTNIP,POINT5    [NIPREL MACRO
  21 26W=    [THIS SEGMENT CONTAINS ALL THE CODE OBEYED IN THE CPAT BY THE LF MACROS 
  14 27*W    [ISSUED BY THE JOB RUNNING THE NIP PROGRAM  
  21 27TG    [THE JOB ACCESSES THE LISTFILE ROUTINES USING A FORM OF THE ACCESS CMMAN
  17 28*6    [       NIPSTART IS ISSUED AFTER A MODE #201 EXTRACODE  
  17 28SQ    [       NIPRPERT IS ISSUED AFTER A MODE #202 EXTRACODE  
  17 29#B    [       NIPLINES IS ISSUED AFTER A MODE #203 EXTRACODE  
  17 29S2    [       NIPENDLF IS ISSUED AFTER A MODE #204 EXTRACODE  
  21 2=?L    [       NIPREL IS ISSUED AFTER A REL EXTRACODE OR WHEN THE PROGRAM IS DE
   4 2=R=    [   
  21 2?=W    [***********************************************************************
   4 2?K? ...[   
   9 2?XN ...[              DATA AREA
   4 2#=5 ...[   
  20 2#JG ...[****************************************************************** 
   4 2#WX ...[   
   8 2*9B    XA             #41  
   8 2*P2    XZPLUS         #73  
   8 2B8L    X48            48   
   8 2BN=    X64            64   
   8 2C7W    X96            96   
   9 2CCN ...XHALF           #10000  
  20 2CMG    XINIT PAIR     LISTINIT,8          [NIPLINES - AT START OF LISTING  
  14 2D4M ...XNXT2 PAIR     LISTNEXT,11         [NIPREL  
   4 2DFS ...[   
  20 2DW_ ...[****************************************************************   
   4 2F#6 ...[   
  11 2FP? ...[             SUBROUTINE AREA   
   4 2G6D ...[   
  21 2GHK ...[*********************************************************************  
   4 2GYQ ...[   
   4 2H*X ...[   
  21 2HR4 ...[***********************************************************************
   4 2J89 ...[   
  16 2JKB ...[           SUBROUTINE TO SEARCH FOR ACTLF/ALPS14   
  21 2K2H ...[           CALLED ON X6    - USES ALL ACCUMULATORS EXCEPT X7 AND AWORK2
  18 2KCN ...[           EXIT   +1   IF ACTLF FOUND - X3 REFERENCES ACTLF
  14 2KST ...[                  +0   IF ACTLF NOT FOUND  
  10 2L=2 ...[           NO COORDINATION 
   4 2LM7 ...[   
   4 2M4# ...[   
   5 2MFG    SFINDACT
  16 2M_6          POINTACT OUT,3               [GET ACTLF/BSCHED
  17 2NDQ          LDX   5  JOBNO(2)            [JOB NUMBER OF ACTLF 
  19 2NYB          LDX   4  AWORK1(2)           [TYPE/UNIT NUMBER IN ACTLF   
  18 2PD2          STO   3  AWORK2(2)           [KEEP ADDRESS OF BSCHED  
  10 2PXL    SLOOP LDX   3  JOBRING(3)   
   9 2QC=          SBN   3  JOBRING  
   9 2QWW          TXU   5  JOBNO(3) 
  10 2RBG          TXU   4  JSOURCE1(3)  
  17 2RW6          BCC      SFND                [FOUND ACTLF/ALPS14  
   9 2S*Q          TXU   3  AWORK2(2)
   8 2STB          BCS      SLOOP
   7 2T*2          EXIT  6  0
   4 2TSL ...[   
   7 2W#=    SFND  EXIT  6  1
   4 2WRW ...[   
   4 2X?G ...[   
  21 2XR6 ...[              SUBROUTINE TO FIND THE ACTLF/ALPS14 AND CHECK THE BREAKIN
   8 2Y=Q ...[              BIT. 
  16 2YQB ...[              CALLED ON X7 - USES ALL ACCUMULATORS 
  15 2_=2 ...[              EXIT    +0   IF ACTLF NOT FOUND  
  17 2_PL ...[                      +1   IF BREAKIN DURING LONGWAIT  
  19 329= ...[                      +2   IF ACTLF FOUND - X3 REFERENCES ACTLF
  10 32NW ...[              USES SFINDACT
   4 338G ...[   
   6 33N6    SCHKBREAK   
   9 347Q          CALL  6  SFINDACT 
  16 34MB          EXIT  7  0                   [ACTLF NOT FOUND 
   4 3535 ...[   
   4 35BS ...[   
  12 35QH ...      LONGSET  ANIPBRK,SJMP,JOBNO(2)
  18 366=    SACTF LFBBUS   B,3,SNSET           [CHECK THE BREAKIN BIT   
   8 36KW          SBX   7  FX1  
   9 36WR ...      LDX   0  AWORK2(2)
  16 377N ...      BZE   0  STOP                [J IF NOT NIPREL 
  18 37DK ...      LONGCOOR                     [DO NOT BRKIN IF NIPREL  
   8 37PG ...      BRN      SOUT 
  20 382C ...STOP  LONGSTOP SBRKN               [WAIT FOR BREAKIN BIT TO CLEAR   
   8 38?# ...SOUT  ADX   7  FX1  
   9 38JB          CALL  6  SFINDACT 
  20 3942          BRN      SNFND               [ACTLF NO LONGER THERE - GEOERR  
  19 39HL          BRN      SACTF               [GO TO RECHECK BREAKIN BIT   
   7 3=3=    SNSET LONGOFF   
   7 3=GW          EXIT  7  2
   7 3?2G    SBRKN LONGOFF   
   8 3?G6          ADX   7  FX1  
   7 3?_Q          EXIT  7  1
   4 3#FB ...[   
   4 3#_2 ...[   
   9 3*DL    SNFND GEOERR   0,ALPS14 
   4 3*Y= ...[   
   4 3BCW ...[   
   9 3BXG    SJMP  GEOERR   0,LONGSET
   4 3CC6 ...[   
   4 3CWQ ...[   
  19 3DBB ...[              SUBROUTINE TO LONGWAIT FOR ACTLF TO FINISH WORK  
  16 3DW2 ...[              CALLED ON X7 - USES ALL ACCUMULATORS 
  12 3F*L ...[              EXIT +0 IF ALL OKAY  
  14 3FT= ...[                   GOES UP IF BROKEN IN ON 
   4 3G#W ...[   
   4 3GSG ...[   
   4 3H#6 ...[   
   5 3HRQ    SWAITACT
   8 3J?B          SBX   7  FX1  
  13 3JR2          LONGSET  AWAITNIP,SJMP,JOBNO(2)   
   9 3J_N ...      LDX   0  AWORK2(2)
  16 3K8B ...      BZE   0  STOP2               [J IF NOT NIPREL 
  18 3KC4 ...      LONGCOOR                     [DO NOT BKIN IF NIPREL   
   8 3KKQ ...      BRN      SOUT2
  20 3KSD ...STOP2 LONGSTOP SBRK2               [WAIT FOR COMPLETION OF ACTION   
   7 3L36 ...SOUT2 LONGOFF   
   8 3L9W          ADX   7  FX1  
   7 3LPG          EXIT  7  0
   4 3M96 ...[   
   9 3MNQ    SBRK2 CALL  6  SFINDACT 
  18 3N8B          BRN      SNFND               [ACTLF NOT THERE - GEOERR
  16 3NN2          LFBITSET B,3                 [SET BREAKIN BIT 
   7 3P7L          LONGOFF   
  20 3PM=          UP                           [GO TO JUMP TO BREAKIN LABEL OF  
  16 3Q6W                                       [CALLING ROUTINE 
   4 3QLG ...[   
   4 3R66 ...[   
   4 3RKQ ...[   
   4 3S5B ...[   
   4 3SK2 ...[   
  12 3T4L ...[              MAIN BODY OF SEGMENT 
   4 3TJ= ...[   
   4 3W3W ...[   
  19 3WHG ...[              THIS IS THE ENTRY POINT FOR THE NIPSTART MACRO   
  21 3X36 ...[              EXPECTS THE TYPE/UNIT NUMBER OF THE PROGRAM CHANNEL IN   
   9 3XGQ ...[              ACOMMUNE1
  21 3Y2B ...[              THERE MAY BE AN APERI/APROPS BLOCK IN THE ACTIVITY CHAIN 
   4 3YG2 ...[   
  21 3Y_L ...[              CONVERTS THE APERI/APROPS BLOCK TO A GOUT/GPROPNOS BLOCK 
  20 3_F= ...[              WITH THE HELP OF THE PROPNO MACRO OR CREATES A ZERO  
  12 3_YW ...[              GOUT/GPROPNOS BLOCK. 
  20 42DG ...[              NEXT FINDS ACTLF/ALPS14 ACTIVITY BLOCK FOR CHANNEL   
  21 42Y6 ...[              IF IT DOES NOT EXIST,CREATES ONE AND CHAINS IT IN ACTLF  
   9 43CQ ...[              JOBRING. 
  21 43XB ...[              IF IT DOES EXIST,CHECKS THE BREAKIN BIT AND GOES WAITING 
   9 44C2 ...[              IF SET.  
  21 44WL ...[              THEN THE GOUT/GPROPNOS BLOCK IS CHAINED AFTER THE ACTLF, 
  21 45B= ...[              FLAG G IS SET IN THE ACTLF,THE LF SCHEDULER IS KICKED OFF
  13 45TW ...[              AND THE CPAT LONGWAITS.  
  19 46*G ...[              EXPECTS TO FIND THE ACTLF NON-EXISTENT OR BETWEEN
   9 46T6 ...[              LISTINGS 
  17 47#Q ...[              LEAVES ACTLF BEFORE FIRST LINE OF LISTING
   4 47SB ...[   
   5 48#2    POINT1  
  10 48RL          LDX   0  ACOMMUNE1(2) 
  17 49?=          STO   0  AWORK1(2)           [KEEP FOR FUTURE USE 
   9 49H4 ...      STOZ     AWORK2(2)
   4 49QW ...[   
  13 4==G ...      SETNCORE ATTMAX,3,GOUT,GPROPNOS   
  10 4=Q6          STOZ     GPROPNUM(3)  
   9 4?9Q          ADN   3  GPROPNUM 
   7 4?PB          LDX   4  3
   7 4#92          ADN   4  1
  18 4#NL ...      MOVE  3  ATTMAX-1            [ZEROISE GPROPNOS BLOCK  
  11 4*8=          HUNTW    3,APERI,APROPS   
  21 4*MW          BNG   3  XSACT               [NO MORE TO DO-GO TO SEARCH FOR ACTLF
   9 4B7G          LDX   7  A1+1(3)  
   8 4BM6          LDN   6  A1+2 
   7 4C6Q          LDN   5  0
   9 4CLB ...      SBN   7  ATTMAX   
  17 4D62          BPZ   7  TOMNY               [TOO MANY PROPERTIES 
   9 4DKL ...      ADN   7  ATTMAX   
  10 4F5=          SETNCORE 5,3,CPB,CUNI 
  10 4FJW          STOZ     JPARNUM(3)   
  12 4G4G          OPENSYS  XBRK,PROPERTY,READ   
   4 4GJ6 ...[   
  17 4H3Q ...[              LOOP TO CONVERT PROPERTY NAMES TO NUMBERS
   4 4HHB ...[   
  11 4J32    XLOOP MHUNTW   2,APERI,APROPS   
  10 4JGL          MHUNTW   3,CPB,CUNI   
   7 4K2=          ADX   2  6
   8 4KFW          ADN   3  ANUM 
  18 4K_G          MOVE  2  4                   [MOVE IN PROPERTY NAME   
   7 4LF6          ADN   6  4
   4 4LYQ ...[   
  11 4MDB          CHEKLFN2 (GEOERR),XFORM   
  10 4MY2 ...      PROPNO   3,0,XNFND,4  
  16 4NCL          BNG   3  XCONS               [CONSOLE PROPERTY
   8 4NX= ...      ANDX  4  GSIGN
  21 4QB6          ORX   4  3                   [GET PROP NO IN FORMAT FOR GOUT/GPROP
  11 4QTQ          MHUNTW   3,GOUT,GPROPNOS  
   7 4R*B          SMO      5
  10 4RT2          STO   4  GPROPNUM+1(3)
   7 4S#L          ADN   5  1
  19 4SS=          BCT   7  XLOOP               [GO TO GET NEXT PROPERTY NAME
   4 4T?W ...[   
   6 4TRG          CLOSE 
   9 4W?6          MFREEW   CPB,CUNI 
  10 4WQQ          MFREEW   APERI,APROPS 
   4 4X=B ...[   
  14 4XQ2 ...[              GO TO SEARCH FOR ACTLF/ALPS14
   4 4Y9L ...[   
   9 4YP=    XSACT CALL  7  SCHKBREAK
  17 4_8W          BRN      XCRET               [GO TO CREATE ALPS14 
  15 4_NG          BRN      XBRKIN              [BROKEN IN ON
   4 5286 ...[   
  17 52BS ...      LFBBUS   S,3,XNREP           [NOT BETWEEN LISTINGS
   9 52KG ...      LFBBUS   G,3,XLPOK
  11 52S8 ...[ REMOVE OLD GOUT,GPROPNOS BLOCK
  13 532W ...      HUNT2J   2,GOUT,GPROPNOS,3,XLPOK  
   7 539J ...      FREECORE 2
   8 53D= ...      BRN      XLPOK
   4 53M2 ...[   
   4 546L ...[   
  13 54L= ...[              CREATE AN ACTLF/ALPS14   
   4 555W ...[   
  18 55KG    XCRET GETACT   ACTLF,ALPS14        [WILL NOT BE LONG LOCKED 
   9 5656          LDX   2  BPTR(2)  
   9 56JQ          ADN   2  JOBRING  
   8 574B          POINTACT OUT,3
   9 57J2          ADN   3  JOBRING  
  18 583L          ENRING   2,3                 [RING ACTLF ON JOBRING   
   9 58H=          LDX   3  BPTR(2)  
   9 592W          LDX   0  JOBNO(2) 
   9 59GG          STO   0  JOBNO(3) 
   9 5=26          LDX   0  AWORK1(2)
  10 5=FQ          STO   0  JSOURCE1(3)  
   7 5=LY ...      LDN   0  1
  10 5=S6 ...      STO   0  JSOURCE2(3)  
  16 5=_B          LFBITSET S,3                 [BETWEEN LISTINGS
   8 5?F2          LDN   0  #72  
  18 5?YL          DCH   0  JSOURCE3(3)         [PUT Z IN DEBATCHING CHAR
   4 5#D= ...[   
   4 5#XW ...[   
  19 5*CG    XLPOK LFBITSET G,3                 [ACTIVITY HAS GOUT/GPROPNOS  
  11 5*X6          MHUNTW   2,GOUT,GPROPNOS  
   8 5BBQ          CHAIN    2,3  
   8 5BLJ ...      LFBITCLR F,3  
   8 5BWB          STARTACT OUT  
  18 5CB2          CALL  7  SWAITACT            [WAIT FOR ACTLF TO FINISH
   7 5F#G    XFIN2 UPPLUS   1
   4 5FS6 ...[   
   4 5G?Q ...[   
  19 5GRB ...[              THIS IS THE ENTRY POINT FOR THE NIPRPERT MACRO   
  21 5H?2 ...[              EXPECTS THE TYPE/UNIT NUMBER OF THE PROGRAM CHANNEL IN   
   9 5HQL ...[              ACOMMUNE1
  21 5J== ...[              THERE MUST BE AN APERI/ARPERT BLOCK IN THE ACTIVITY CHAIN
   4 5JPW ...[   
  21 5K9G ...[              THE INFORMATION IN THE BLOCK IS VALIDATED AND NON-ZERO   
  13 5KP6 ...[              ITEMS MOVED TO THE ACTLF 
  19 5L8Q ...[              EXPECTS ACTLF TO BE BEFORE FIRST LINE OF LISTING 
  12 5LNB ...[              LEAVES ACTLF THE SAME
   4 5M82 ...[   
   4 5MML ...[   
   5 5N7=    POINT2  
  10 5NLW          LDX   0  ACOMMUNE1(2) 
  17 5P6G          STO   0  AWORK1(2)           [KEEP FOR FUTURE USE 
   9 5PB# ...      STOZ    AWORK2(2) 
  11 5PL6          MHUNTW   3,APERI,ARPERT   
  15 5Q5Q          LDX   0  A1(3)               [PAGE WIDTH  
  16 5QKB          BZE   0  XNWDT               [NONE SPECIFIED  
   8 5R52 ...      SBN   0  512  
  18 5RJL          BPZ   0  XTBIG               [TOO MANY PRINT POSITIONS
   8 5S4= ...      ADN   0  512  
  16 5SHW    XNWDT STO   0  AWORK3(2)           [STORE IN ACTLF  
   4 5T3G ...[   
  17 5TH6 ...      LDCH  0  A1+1(3)             [DEBATCHING CHARACTER
  16 5W2Q          BZE   0  XNDBC               [NONE SPECIFIED  
   8 5WGB          TXL   0  XA(1)
  15 5X22          BCS      XNALF               [NOT ALPHA   
   9 5XFL          TXL   0  XZPLUS(1)
  15 5X_=          BCC      XNALF               [NOT ALPHA   
  21 5YDW          SBN   0  1                   [UPDATE DEBATCHING CHAR BEFORE LISTIN
   8 5YYG          TXL   0  XA(1)
   8 5_D6          BCC      XDBC 
  17 5_XQ          LDN   0  #72                 [IF WAS A SET TO Z   
   9 62CB    XDBC  DCH   0  AWORK3(2)
   4 62HL ...[   
  15 62MW ...XNDBC LDX   0  A1+1(3)             [PAGE LENGTH 
   7 62S6 ...      SLL   0  6
   7 62YB ...      SRL   0  6
  16 634L ...      BZE   0  XNPGL               [NONE SPECIFIED  
   9 638W ...      TXL   0  XHALF(1) 
  17 63*6 ...      BCC      XERPL               [TOO HIGH PAGE LENGTH
   9 63FB ...      STO   0  AWORK4(2)
   4 63KL ...[   
  16 63PW ...XNPGL LDX   0  A1+2(3)             [CHARACTER SET   
  16 643W ...      BZE   0  XCHSX               [NONE SPECIFIED  
   8 64*W          LDN   4  #4000
   9 64TG ...      TXU   0  X48(1)   
  15 65*6          BCC      XCHS                [48 CHAR SET 
   7 65SQ          SRC   4  1
   9 66#B ...      TXU   0  X96(1)   
  15 66S2 ...      BCC      XCHS                [96 CHAR SET 
   7 67?L ...      LDN   4  0
   9 67R= ...      TXU   0  X64(1)   
  18 68=W          BCS      XERCH               [CHAR SET NOT 48,64 OR 96
   9 68QG ...XCHS  ORS   4  AWORK3(2)
  10 692# ...XCHSX MFREEW   APERI,ARPERT 
   4 69=6 ...[   
  16 69PQ          CALL  7  SCHKBREAK           [FIND THE ACTLF  
  18 6=9B          BRN      SNFND               [ACTLF NOT FOUND-GEOERR  
  15 6=P2 ...      BRN      XBRK2               [BROKEN IN ON
   4 6?8L ...[   
  21 6?N=          LFBBUS   Q,3,XNREP           [EXTRACODE ISSUED IN THE WRONG ORDER 
  17 6#7W          LDCH  0  AWORK3(2)           [MOVE DATA INTO ACTLF
   8 6#MG          BZE   0  XNDEP
  10 6*76          DCH   0  JSOURCE3(3)  
   9 6*LQ    XNDEP LDEX  0  AWORK3(2)
   8 6B6B          BZE   0  XNDEX
  10 6BL2          DEX   0  JSOURCE3(3)  
   9 6B_P ...XNDEX LDX   0  AWORK3(2)
   8 6C*D ...      ANDN  0  #7000
   8 6CP7 ...      BZE   0  XNPL 
   8 6D4W          LDN   4  #7000
  10 6DJG          ANDX  4  JSOURCE3(3)  
  10 6F46          ERS   4  JSOURCE3(3)  
  10 6FHQ          ORS   0  JSOURCE3(3)  
   9 6FXW ...XNPL  LDX   0  AWORK4(2)
   8 6G#2 ...      BZE   0  XFIN 
  10 6GN6 ...      STO   0  JSOURCE2(3)  
   4 6H4= ...[   
   8 6HDB ...XFIN  SETREP   OK   
   8 6HSG ...      BRN      XFIN2
   4 6J8L ...[   
  19 6JJQ ...[              THIS IS THE ENTRY POINT FOR THE NIPLINES MACRO   
  21 6JYW ...[              EXPECTS THE TYPE/UNIT NUMBER OF THE PROGRAM CHANNEL IN   
   9 6K*2 ...[              ACOMMUNE1
  20 6KP6 ...[              EXPECTS ACTLF TO BE BEFORE FIRST LINE OF A LISTING OR
  20 6L5= ...[              IN THE MIDDLE OF A LISTING(NO BITS SET IN FLAG WORD) 
  20 6LFB ...[              LEAVES ACTLF IN THE MIDDLE OF A LISTING OR AT END OF 
   9 6LTG ...[              LISTING  
   4 6M9L ...[   
  21 6MKQ ...[              WILL GIVE AN ERROR REPLY IF REPERTOIRE INFO NOT COMPLETE 
   4 6M_W ...[   
   5 6NBG    POINT3  
  10 6NW6          LDX   0  ACOMMUNE1(2) 
  17 6P*Q          STO   0  AWORK1(2)           [KEEP FOR FUTURE USE 
   9 6PKJ ...      STOZ    AWORK2(2) 
   9 6PTB          CALL  7  SCHKBREAK
  18 6Q*2          BRN      SNFND               [ACTLF NOT FOUND-GEOERR  
  15 6QSL          BRN      XBRK2               [BROKEN IN ON
   4 6R#= ...[   
  14 6RRW ...      LFBBS    R,3,XNREP           [MODES IN
  15 6S?G ...      LFBBS    S,3,XNREP           [WRONG ORDER 
   4 6SR6 ...[   
  15 6T=Q ...[              CHECK REPERTOIRE INFO COMPLETE   
  10 6TQB          LDEX  0  JSOURCE3(3)  
  16 6W=2          BZE   0  XNORP               [INFO INCOMPLETE 
   8 6WPL ...      LDN   0  #6000
  10 6X9=          ANDX  0  JSOURCE3(3)  
   8 6X#K ...      BZE   0  XCHOK
   8 6XCY ...      LDN   4  #1000
   9 6XH? ...      BXE   4  0,XCHOK  
   7 6XLL ...      SLL   4  1
  16 6XP_ ...      BXU   4  0,XNORP              [INFO INCOMPLETE
   5 6XT# ...XCHOK   
  10 6XYN ...      LDX   0  JSOURCE2(3)  
   8 6Y8G ...      BZE   0  XNORP
   4 6YD# ...[   
  19 6YN6          LFBBUS   Q,3,XFPUT           [J IF IN MIDDLE OF LISTING   
  10 6_7Q          LINKSET  3,XINIT(1)   
   7 6_MB    XFPUT LDX   2  3
  15 7272          FPUT                         [FPUT ACTLF  
  18 72LL          CALL  7  SWAITACT            [WAIT FOR ACTLF TO FINISH
  20 736= ...      BRN      XFIN2               [GO TO SET OK REPLY AND UPPLUS1  
   4 73KW ...[   
  19 745G ...[              THIS IS THE ENTRY POINT FOR THE NIPENDLF MACRO   
  21 74K6 ...[              EXPECTS THE TYPE/UNIT NUMBER OF THE PROGRAM CHANNEL IN   
   9 754Q ...[              ACOMMUNE1
   4 75JB ...[   
  21 7642 ...[              EXPECTS ACTLF TO BE AT END OF LISTING OR BETWEEN LISTINGS
  14 76HL ...[              LEAVES ACTLF BETWEEN LISTINGS
   4 773= ...[   
   4 77GW ...[   
   4 782G ...[   
   5 78G6    POINT4  
  10 78_Q          LDX   0  ACOMMUNE1(2) 
  17 79FB          STO   0  AWORK1(2)           [KEEP FOR FUTURE USE 
   9 79P8 ...      STOZ    AWORK2(2) 
   9 79_2          CALL  7  SCHKBREAK
  18 7=DL          BRN      SNFND               [ACTLF NOT FOUND-GEOERR  
  15 7=Y=          BRN      XBRK2               [BROKEN IN ON
   4 7?CW ...[   
  19 7?XG          LFBBS    S,3,XFIN            [BETWEEN LISTINGS-OK REPLY   
  17 7#C6 ...      LFBBUS   R,3,XNREP           [NOT END OF LISTING  
   7 7*BB          LDX   2  3
   6 7*W2          FPUT  
   9 7B*L          CALL  7  SWAITACT 
   8 7BT=          BRN      XFIN 
   4 7C#W ...[   
   4 7CSG ...[   
  18 7D#6 ...[              THIS IS THE ENTRY POINT FOR THE NIPREL MACRO 
  21 7DRQ ...[              EXPECTS THE TYPE/UNIT NUMBER OF THE PROGRAM CHANNEL IN   
   9 7F?B ...[              ACOMMUNE1
   4 7FR2 ...[   
  18 7G=L ...[              ACTLF MAY BE IN ANY STATE(EVEN NON EXISTENT) 
  15 7GQ= ...[              CLOSES DOWN ACTLF AND MURDERS IT 
   4 7H9W ...[   
  16 7HC4 ...[                THIS MACRO CANNOT BE BROKEN IN ON  
  16 7HJ= ...[                - SEE USE OF AWORK2 IN SUBROUTINES 
   4 7HPG ...[   
   4 7J96 ...[   
   5 7JNQ    POINT5  
  10 7K8B          LDX   0  ACOMMUNE1(2) 
  17 7KN2          STO   0  AWORK1(2)           [KEEP FOR FUTURE USE 
   9 7KXS ...      NGS   2  AWORK2(2)
   9 7L7L          CALL  7  SCHKBREAK
  18 7LM=          BRN      XFIN2               [ACTLF DOES NOT EXIST END
  15 7M6W          BRN      XBRK2               [BROKEN IN ON
   4 7MLG ...[   
  21 7N66          LFBBS    S,3,XBETL           [BETWEEN LISTINGS-GO TO MURDER ACTLF 
   9 7NCG ...      LFBBS    R,3,XREND
   9 7NNW ...      LFBBUS   Q,3,XNFST
  10 7P2= ...      LINKSET  3,XNXT2(1)   
   7 7P?L ...XENDL LDX   2  3
  17 7PK2          FPUT                         [GO TO CANTDO LISTING
  21 7Q4L          CALL  7  SWAITACT            [WAIT FOR ACTLF TO CLOSE DOWN LISTING
  17 7QJ=          CALL  6  SFINDACT            [FIND ACTIVITY AGAIN 
  19 7R3W          BRN      SNFND               [ACTLF SHOULD STILL BE THERE 
   4 7RHG ...[   
   7 7S36    XBETL LOCK     3
  15 7SGQ          MURDER   3                   [MURDER ACTLF
   8 7T2B          BRN      XFIN2
   4 7T3S ...[   
  15 7T5= ...[ IF FLAG R SET FORCE CANTDO BY CLEARING FLAG R 
   4 7T6N ...[   
   8 7T86 ...XREND LFBITCLR R,3  
   8 7T9J ...      BRN      XENDL
   4 7T?2 ...[   
  15 7T#D ...[ IF FLAG R CLEAR FORCE CANTDO BY SETTING FLAG R
   4 7TB5 ...[   
   8 7TPS ...XNFST LFBITSET R,3  
   8 7W5H ...      BRN      XENDL
   4 7WF= ...[   
   4 7WS_ ...[   
  12 7X8N ...[              BREAKIN AND ALL THAT 
   4 7XJC ...[   
  21 7XY6    XBRK  MFREEW   CPB,CUNI            [OPENING :SYSTEM.PROPERTY IN NIPSTART
  10 7YCQ          MFREEW   APERI,APROPS 
  21 7YXB    XBRKIN                             [DURING LW FOR BREAKIN BIT IN NIPSTAR
  10 7_C2          MFREEW   GOUT,GPROPNOS
   5 7_WL    XBRK2 UP
   4 84#Q ...[   
   9 84SB ...[              ERRORS   
   4 85#2 ...[   
  17 85RL    TOMNY SETREP   MAXATT              [TOO MANY PROPERTIES 
   4 86?= ...[   
  10 86QW    TOMN2 MFREEW   APERI,APROPS 
  10 87=G          MFREEW   GOUT,GPROPNOS
   7 87Q6    TOMN3 UPPLUS   1
   4 889Q ...[   
  16 88PB    XNFND SETREP   XPROP               [NO SUCH PROPERTY
   6 8992    XNF2  CLOSE 
   8 89NL          BRN      TOMN2
   4 8=8= ...[   
  16 8=MW    XCONS SETREP   XCPROP              [CONSOLE PROPERTY
   8 8?7G          BRN      XNF2 
   4 8?M6 ...[   
  15 8#6Q    XFORM SETREP   FORMAT              [FORMAT ERROR
   8 8#LB          BRN      XNF2 
   4 8BJW ...[   
  16 8C4G ...XNREP GEOERR   0,ILLMODE             [ILLEGAL MODE  
  10 8CJ6    XNR2  MFREEW   APERI,ARPERT 
   8 8D3Q ...      BRN      TOMN3
   4 8DHB ...[   
  17 8F32    XTBIG SETREP   LONG                [PAGE WIDTH TOO BIG  
   8 8FGL          BRN      XNR2 
   4 8G2= ...[   
  19 8GFW    XNALF SETREP   FORMAT              [DEBATCHING CHAR NOT ALPHA   
   8 8G_G          BRN      XNR2 
   4 8HF6 ...[   
  18 8HYQ    XERCH SETREP   WRONG               [CHAR SET NOT 48,64 OR 96
   8 8JDB          BRN      XNR2 
   4 8JY2 ...[   
   9 8KCL ...XERPL SETREP   TOOHIGH  
   8 8KX= ...      BRN      XNR2 
   4 8LBW ...[   
  21 8LWG    XNORP SETREP   NOPROP              [REPERTOIRE INFO INCOMPLETE-NIPLINES 
   8 8MB6 ...      BRN      TOMN3
   4 8MTQ ...[   
   4 8N*B ...[   
   4 8NT2 ...[   
   4 8P#L    #END
   6 ____ ...242123540001
