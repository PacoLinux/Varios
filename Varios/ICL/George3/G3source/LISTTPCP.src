   8 22FL    #OPT  K0LISTTPCP=0  
   7 22PD ...#OPT JMIXEDTP=0 
  18 22_=    #LIS  K0LISTTPCP>K0LISTFILE>K0ALLGEO>K0GREATGEO>K0COMMAND   
   7 23DW    #SEG  LISTTPCP  
  10 23YG                   8HLISTTPCP   
  15 248# ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
  11 24D6          SETENTRY LISTTPCP,POINT,1 
  18 25CB    [THIS SEGMENT IS ENTERED FROM LISTINTC TO IMPLEMENT CENTRAL 
  13 25X2    [CARD PUNCH OR TAPE PUNCH FILE LISTINGS 
   4 26BL    [   
   4 26W=    [   
  21 27*W    [***********************************************************************
   6 27TG    [DATA AREA  
  18 28*6    NUMCA          +10000              [CARD MAXIMUM LINE NUMBER
  20 28SQ    XCONT          4H  -               [CONTINUATION LINE MARK-OUTPUT AT
  20 29#B                                       [END OF EACH LINE(EXCEPT LAST) OF
  16 29S2                                       [MULTILINE RECORD
   8 2=R=    XFULLREC       +2000
  21 2?=W                                       [MAX NUMBER OF CHARACTERS IN RECORD  
  12 2?QG    TPLEN          +128                [
  15 2#=6    NEWRUN         #76327620           [NEWLINE AND 
  16 2#PQ    RUN            #76207620           [RUNOUTS FOR TP  
  18 2*9B    SMLP           +7036875            [MAGIC CONVERSION NUMBER 
   4 2*P2    [   
  21 2B8L    [***********************************************************************
   7 2BN=    [SUBROUTINE AREA
   4 2C7W    [   
  16 2CMG    [THIS SUBROUTINE TIDIES UP WHEN A LISTING IS TO END 
   4 2D76    [   
   5 2DLQ    TIDYUP  
  18 2F6B          LFBBUS   K,2,NOADATA         [J IF NO CONVERSION BLOCK
  10 2FL2          MHUNTW   1,ADATA,ACONV
   7 2G5L          FREECORE 1
  18 2GK=          LFBITCLR K,2                 [CLEAR CONVERSION FLAG   
   5 2H4W    NOADATA 
  11 2HJG ...      MHUNTW   1,GOUT,INFOLIST  
   9 2J46          LDX   4  GREST(1) 
   7 2JHQ          FREECORE 1
   7 2K3B          EXIT  3  0
   4 2KH2    [   
  17 2L2L    [THIS SUBROUTINE IS USED BY THE LOCK MACRO TO SET X2=FX2
   4 2LG=    [   
   5 2L_W    SLOCKC  
   8 2MFG          LDX   2  FX2  
   7 2M_6          EXIT  1  0
   4 2NDQ    [   
  18 2NYB    [THIS SUBROUTINE CONVERTS THE BINARY NUMBER IN X5 TO DECIMAL
  21 2PD2    [CHARACTERS STORING THE RESULT IN THE LOCATION WHOSE ADDRESS IS IN X2   
   4 2PXL    [   
   5 2QC=    SEQUENCE
  16 2QWW          MODE     1                   [ZERO SUPPRESS   
   8 2RBG    SCBD  CBD   4  0(2) 
   7 2RW6          BCHX  2  /
   8 2S*Q          BCT   3  SCBD 
  18 2STB          MODE     0                   [DON'T SUPPRESS ZEROES   
  17 2T*2          CBD   4  0(2)                [FOR LAST CHARACTER  
   7 2TSL          BCHX  2  /
   8 2W#=          DCH   1  0(2) 
   8 2WRW          LDX   1  FX1  
   8 2X?G          LDX   2  FX2  
   7 2XR6          EXIT  0  0
   4 2Y=Q    [   
  18 2YQB    [THIS SUBROUTINE PERFORMS THE PERI ON THE DEVICE AND CHECKS 
  15 2_=2    [THE REPLY FOR DISENGAGED AND INOPERABLE STATUS 
   4 2_PL    [   
   4 329=    PERI
   8 32NW          SBX   5  FX1  
   4 338G    REP 
  10 33N6          DCA      ,LDX,4,COUNT 
   8 347Q          BZE   4  NOT  
   8 34MB          LDX   1  FX1  
   9 3572          LOCKC    2,SLOCKC 
  17 35LL          LDN   4  GZDATA+1(2)         [STORE BUFFER ADDRESS
  16 366=          DCA      ,STO,4,CTSA         [IN CONTROL AREA 
   8 36KW          GPERI    ,2   
   6 375G          COOR1 
   7 37K6          UNLOCK 2  
   9 384Q          LDX   4  FINISH   
   7 38JB          SLL   4  2
  15 3942          BNG   4  STOP                ['FINISH NOW'
   8 39HL          LDCT  4  #50  
   9 3=3=          ANDX  4  CPRW1(2) 
  18 3=GW          BNZ   4  NOTENG              [J IF FAIL OR DISENGAGED 
   7 3?2G          LDN   0  1
  19 3?G6          ADS   0  ALFTRANS(2)         [UPDATE COUNT OF TRANSFERS   
   9 3?_Q          DCA   ,LDX,4,COUNT
   7 3#FB          SRL   4  2
  18 3#_2          ADS   4  ALFWORDS(2)         [UPDATE COUNT OF WORDS   
   4 3*DL    NOT 
   8 3*Y=          ADX   5  FX1  
   7 3BCW          EXIT  5  0
   4 3BXG    [   
  16 3CC6    [THIS SUBROUTINE TRANSFERS DATA FROM FILE TO BUFFER 
   4 3CWQ    [   
   5 3DBB    TRANSFER
   7 3DW2          SLL   3  2
  10 3F*L          ADX   3  GZPOINT(2)   
   7 3FT=          SRC   3  2
  10 3G#W          LDN   4  GZDATA+1(2)  
   7 3GSG          SLL   4  2
   9 3H#6          ADX   4  GZDUMP(2)
   7 3HRQ          SRC   4  2
  10 3J?B          LDX   2  GZTRANS(2)   
   9 3JR2          BZE   2  NODATA   
   7 3K=L          SMO      2
   7 3KQ=          MVCH  3  0
   5 3L9W    NODATA  
   8 3LPG          LDX   2  FX2  
   7 3M96          EXIT  0  0
   4 3MNQ    [   
  18 3N8B    [THIS CODE IS USED BY THE PERI SUBROUTINE WHEN PERIPHERAL   
  13 3NN2    [FAIL OR DISENGAGED STATUS IS DETECTED  
   4 3P7L    [   
   5 3PM=    NOTENG  
   8 3Q6W          LDCT  0  #40  
   9 3QLG          ANDX  0  CPRW1(2) 
  17 3R66          BNZ   0  XERREST             [J IF TRANSFER ERROR 
  10 3RKQ          ENGPER   SBRKIN,,STOP 
   8 3S5B          BRN      REP  
   9 3SK2    STOP  CALL  3  TIDYUP   
   8 3T4L          LDCT  0  #600 
   7 3TJ=          ANDX  0  4
   9 3W3W          BNZ   0  NOTLINER 
  15 3WHG          SBN   4  1                   [GREST=GNUM  
   5 3X36    NOTLINER
   9 3XGQ          ANDX  4  BITS22LS 
   8 3Y2B          LFEND    CD,4 
   5 3YG2    XERREST 
  10 3Y_L          ENGPER   SBRKIN,,STOP 
   9 3_F=          TRACE    5,ENGAGE 
  11 3_YW ...      MHUNTW   1,GOUT,INFOLIST  
   8 42DG          LDCT  4  #200 
   8 42Y6          ADX   5  FX1  
   9 43CQ          ANDX  4  GREST(1) 
  17 43XB          BZE   4  PERI                [J IF LINE RESTART   
  17 44C2          BRN      STOP                [ALL RESTART=CANTDO  
   5 44WL    SBRKIN  
   9 45B=          CALL  3  TIDYUP   
   8 45TW          LFEND    TE   
   4 46*G    [   
   4 46C2    [   
  17 46CG    [  THE FOLL. S/R IS PART OF THE LISTFILE ENHANCEMENT TO 
  10 46D2    [  READ ERROR RESILIENCE.   
  17 46DG    [  THE S/R IS CALLED IF STEP ENCOUNTERS A CORRUPT BLOCK 
  16 46F2    [  (INDICATED BY FLAG 'M' BEING SET IN THE ACTLF)   
  15 46FG    [  THE DEVICE IS DISENGAGED WITH THE MESSAGE :- 
  20 46G2    [  'PSE ENGAGE UNIT N IF COMPLETION OF CORRUPT LISTING REQUIRED '   
   4 46GG    [   
   4 46H2    [   
   4 46HG    SRER
   8 46J2          SBX   5  FX1  
  19 46JG    XBSY  JUNBUSY  ,NOTBSY             [WAIT UNTIL DEVICE NOT BUSY  
   8 46K2          BRN      XBSY 
   5 46KG    NOTBSY  
  17 46L2          DISENGPER                    [THEN DISENGAGE IT   
  17 46LG          OUTPER                       [PSE ENGAGE MESSAGE  
  19 46M2          ENGPER   SBRKIN,,SCDE,5      [APP. JUMPS IF TERM OR CDE   
   8 46MG          ADX   5  FX1  
   7 46N2          EXIT  5  0
   4 46NG    [   
  19 46P2    [  IF CANTDO REPLY GIVEN WE RE-SCHEDULE REQUEST FROM BEGINNING  
   4 46PG    [   
   4 46Q2    SCDE
  16 46QG          CALL  3  TIDYUP              [FREE BLOCKS ETC 
   8 46R2          LFEND    CD   
   4 46RG    [   
  21 46T6    [***********************************************************************
   9 47#Q    [MAIN BODY OF SEGMENT   
  21 47SB    [***********************************************************************
  20 48#2    [THIS IS THE ENTRY POINT FOR LISTING A FILE TO A TAPE PUNCH(X7=0)   
   9 48RL    [OR A CARD PUNCH(X7=2)  
   4 49?=    [   
   5 49QW    POINT1  
  11 4=34 ...      MHUNTW   3,GOUT,INFOLIST  
  15 4=?=    [  CODE FOR RER ENHANCEMENT...B11 SET IN GMODE  
  16 4=?G    [  MEANS CORR. WAS DETECTED DURING CONTEXT SEARCH   
  13 4=?Q    [  IF SO WE MUST DISENGAGE DEVICE ETC...
   9 4=#2          LDX   0  GQUAL(3) 
   8 4=#=          ANDX  0  BIT11
  17 4=#G          BZE   0  NOERR               [J IF NO READ ERROR  
   8 4=#Q          CALL  5  SRER 
   5 4=*2    NOERR   
   9 4=Q6          LDX   0  GQUAL(3) 
   9 4?9Q          STO   0  AWORK1(2)
  18 4?PB          LDCT  6  #177                [SET UP X6 WITH MAXIMUM  
  20 4#92          ANDX  6  AWORK1(2)           [NUMBER OF CHARACTERS PERMITTED  
  16 4#NL          SRL   6  15                  [FOR ONE TRANSFER
  17 4*8=          BNZ   7  TUPT                [J IF NOT PAPER TAPE 
   8 4*MW          LDN   6  48   
   8 4B7G    TUPT  ADN   6  80   
   7 4C_#          STEPAGAIN 
  13 4MDB    [WE ARE NOW POSITIONED ON CORRECT RECORD
   9 4MY2          LDX   4  FRH+1(3) 
   9 4NCL          STOZ     GZDUMP(2)
  15 4NX=          BNZ   7  NOTP1               [J IF NOT PT 
  19 4PBW          LDN   5  #10                 [INITIALISE MODE TO GRAPHIC  
  16 4PWG          LDX   0  AWORK1(2)           [COPY OF GMODE   
   8 4QB6 ...      ANDN  0  #300 
   9 4QTQ ...      BZE   0  XDMODE   
   7 4R*B ...      LDN   5  4
   5 53M2    XDMODE  
  10 546L          DCA      ,DSA,5,MODE  
   5 54L=    NOTP1   
   9 55KG          BRN      STARTED  
   5 5656    START   
   6 56JQ          STEP  
  17 56PX          LFBBUS   M,2,NOCOR3          [J IF NO READ ERROR  
   8 56RL          LFBITCLR M,2  
   8 56T*          CALL  5  SRER 
   7 56X4          STEPAGAIN 
   5 56YR    NOCOR3  
  14 574B          BZE   3  TEND                [J IF EOF
   5 57J2    STARTED 
  21 583L          LDEX  5  FRH(3)              [PICK UP NUMBER OF WORDS IN RECORD   
  19 58H=          LDN   4  4                   [INITIALISE PICKUP ADDRESS   
   7 58ND ...#UNS  JMIXEDTP  
   4 58TL ...#SKI
   8 592S ...      LDN   0  #40  
   7 5982 ...#UNS  JMIXEDTP  
   8 59*8 ...      LDN   0  #440 
   9 59GG          ANDX  0  AWORK1(2)
  20 5=26          BZE   0  OK1                 [J IF NOT BASIC PERIPHERAL FILE  
   8 5=FQ          LDN   0  #700 
   9 5=_B          ANDX  0  AWORK1(2)
  17 5?F2          BZE   0  NOCONV              [J IF NOT SHIFT FILE 
  14 5?GF ...      BNZ   7  RU3               [J IF *CP  
   8 5?HY ...      LDN   4  #7700
   9 5?KC ...      ANDX  4  FRH+1(3) 
   7 5?LW ...      SRL   4  6
   7 5?N* ...#UNS  JMIXEDTP  
   4 5?PS ...(   
  16 5?R? ...      ANDN  0  #400                [THESE 4 LINES   
  15 5?SQ ...      BZE   0  RU1                 [IMPLEMENT   
  15 5?W9 ...      DCA      ,DSA,4,MODE         [OLD-STYLE   
  16 5?XN ...      BRN      NOCONV              [MIXED *TP FILES 
   4 5?_7 ...RU1 
   4 5#2L ...)   
  19 5#45 ...      LDN   0  #75                 [IF PREVIOUS SHIFT BETA LEAVE
  19 5#5J ...      TXU   0  4                   [ALONE,ELSE RESET TO ALPHA   
  19 5#73 ...      SBN   0  0                   [(#74) IN CASE IT WAS INVALID
  10 5#8G ...      DCH   0  GZDATA+1(2)  
   7 5#9_ ...      LDN   0  1
   9 5#?D ...      STO   0  GZDUMP(2)
   9 5##X ...      BRN      NOCONV   
   4 5#BB ...RU3 
  19 5#D=          LFBBS    K,2,OLDCONV         [J IF ALREADY AN ACONV BLOCK 
  19 5#XW          SETUPCORE 5,3,ADATA,ACONV    [BLOCK FOR SHIFT CONVERSION  
   8 5*CG          LFBITSET K,2  
   7 5*X6          PHOTO    7
   8 5BBQ          LDN   4  A1(3)
   7 5BWB          STEPAGAIN 
   9 5CB2          BRN      TMOVEMENT
  19 5CTL    OLDCONV                            [HISTORIC CODE HERE I GUESS! 
  10 5D*=          MHUNTW   3,ADATA,ACONV
   8 5DSW          LDN   4  A1(3)
   7 5F#G          PHOTO    7
  11 5FS6          ALTLEN   3,5,ADATA,ACONV  
   7 5G?Q          STEPAGAIN 
  16 5GRB    TMOVEMENT                          [MUST WE REHUNT? 
  14 5H?2          TESTMOVE 7,NOMOVE1           [J IF NOT
  10 5HQL          MHUNTW   1,ADATA,ACONV
   8 5J==          LDN   4  A1(1)
   5 5JPW    NOMOVE1 
   7 5K9G          SMO      5
  15 5KP6          MOVE  3  0                   [MOVE IN DATA
  17 5L8Q          LDN   7  1                   [RESET TO INDICATE CP
  18 5LNB          LDN   1  #26                 [SET MODES FOR ALLCHAR TO
   9 5M82          LDX   0  AWORK1(2)
   8 5MML          ANDN  0  #100 
   9 5N7=          BZE   0  NOTNORM  
   7 5NLW          LDN   1  6
   5 5P6G    NOTNORM 
  17 5PL6          LDN   2  #10                 [GRAPHIC CONVERSION  
   8 5Q5Q          TRANSFOR 1,2  
  10 5QKB          MHUNTW   3,ADATA,ACONV
  17 5R52          ADN   3  A1-FRH              [X3->RECORD HEADER   
  18 5RJL          LDEX  5  FRH(3)              [RESET LENGTH OF RECORD  
   5 5S4=    NOCONV  
  17 5SHW          LDN   4  8                   [SET PICKUP ADDRESS  
  18 5T3G          LDEX  0  FRH(3)              [CHECK FOR EMPTY RECORD  
   7 5TH6          SBN   0  2
  16 5W2Q          BZE   0  OK2                 [AND J IF EMPTY  
  20 5WGB          LDX   0  FRH+1(3)            [CALCULATE NUMBER OF CHARACTERS  
  18 5X22          SLC   0  2                   [IN LAST WORD OF RECORD  
   7 5XFL          ANDN  0  3
   8 5X_=          BZE   0  OK2  
  18 5YDW          SBN   5  1                   [ADJUST CHARACTER COUNT  
   8 5YYG          BRN      OK2  
   4 5_D6    OK1 
  18 5_XQ          LDN   0  0                   [NO ADJUSTMENT NECESSARY 
  18 62CB    OK2                                [CONVERT TO CHARACTERS   
   7 62X2          SLL   5  2
  17 63BL          ADX   5  0                   [ADJUST IF NECESSARY 
  20 63W=          SBX   5  4                   [SUBTRACT PICKUP ADDRESS TO GIVE 
  21 64*W                                       [NUMBER OF CHARACTERS OF DATA IN X5  
  15 64TG          STO   5  GZLENGTH(2)         [DATA LENGTH 
  16 65*6          STO   4  GZPOINT(2)          [PICKUP ADDRESS  
  15 65SQ          BZE   7  TOTP                [J IF TO TP  
   4 66#B    [   
   4 66S2    [   
  15 67?L    [THIS CODE DEALS WITH PERIS TO THE CARD PUNCH   
   4 67R=    [   
   4 68=W    TWCP
  14 68QG          LDN   4  80                  [COUNT   
  18 69=6          TXL   4  GZLENGTH(2)         [IF WE ARE DEALING WITH A
  18 69PQ          BCS      TOBI                [MULTILINE RECORD,JUMP   
  10 6=9B          LDX   4  GZLENGTH(2)  
   4 6=P2    TOBI
  19 6?8L          STO   4  GZTRANS(2)          [COUNT OF CHARACTERS FOR PERI
  13 6?N=          LDX   0  ACES                [NOW 
  15 6#7W          STO   0  GZDATA+1(2)         [SPACEFILL   
  14 6#MG          LDN   0  GZDATA+1(2)         [ACTLF   
  13 6*76          LDN   1  GZDATA+2(2)         [DATA
  14 6*LQ          MOVE  0  19                  [BUFFER  
   8 6B6B          LDX   1  FX1  
  21 6BL2          CALL  0  TRANSFER            [TRANSFER DATA FROM FILE TO BUFFER   
   7 6C5L          LDN   0  2
   9 6CK=          ANDX  0  AWORK1(2)
  17 6D4W          BZE   0  NONUM               [J IF NO SEQUENCING  
  11 6DJG ...      MHUNTW   3,GOUT,INFOLIST  
   9 6F46          LDX   4  GNUM(3)  
   9 6FHQ          TXL   4  NUMCA(1) 
  19 6G3B          BCC      NONUM               [J IF LINE NO EXCEEDS MAXIMUM
   7 6GH2          LDN   3  3
   9 6H2L          MPY   4  SMLP(1)  
   7 6HG=          CBD   4  1
   7 6H_W          CBD   4  1
   7 6JFG          CBD   4  1
   7 6J_6          LDN   1  0
   8 6KDQ          LDCT  2  384  
   9 6KYB          ADN   2  GZDATA+19
   8 6LD2          ADX   2  FX2  
   9 6LXL          CALL  0  SEQUENCE 
   5 6MC=    NONUM   
   8 6MWW          CALL  5  PERI 
  13 6NBG ...REPT           MHUNTW 3,GOUT,INFOLIST   
   7 6NW6          LDN   4  1
   9 6P*Q          ADS   4  GNUM(3)  
  20 6PTB          LDX   4  GLNO(3)             [TEST AND J IF LAST LINE OUTPUT  
   8 6Q*2          BNG   4  TOEND
   7 6QSL          SBN   4  1
   9 6R#=          STO   4  GLNO(3)  
  16 6RRW          BZE   4  TEND                [LAST LINE OUTPUT
   5 6S?G    TOEND   
  13 6SR6          LDCT  4  #200                [J IF
  13 6T=Q          ANDX  4  GREST(3)            [ALL 
  14 6TQB          BNZ   4  SPLIT               [RESTART 
  17 6W=2          LDX   4  GNUM(3)             [ELSE SET LINE NUMBER
  15 6WPL          STO   4  GREST(3)            [FOR RESTART 
   8 6X9=    SPLIT BRN      START
   4 6XNW    [   
  17 6Y8G    [THIS PART OF THE CODE DEALS WITH PERIS TO A TAPE PUNCH 
  19 6YN6    [THE AMOUNT OF SUPERVISORY COUNTROL DEPENDS ON THE TYPE OF DATA 
  18 6_7Q    [IF THE DATA IS ALLCHAR WE DO NOT SUPPLY RECORD TERMINATIORS
  20 6_MB    [IF-THE DATA IS GRAPHIC OR NORMAL,THEN NULL  AND THREE RUNOUTS ARE  
   9 7272    [APPENDED TO EACH RECORD
  20 72LL    [IF THE FILE IS AN OLD FASHIONED 'TAPE PUNCH FILE' THEN THERE IS NO 
   9 736=    [TERMINATION OF RECORDS 
  21 73KW    [IF THE RECORD CONSISTS OF EXACTLY 2000 CHARACTERS THEN IT WAS CREATED  
  18 745G    [BY INPUTTING A RECORD OF EITHER A.EXACTLY 2000 CHARACTERS  
  18 74K6    [                             OR B.MORE THAN 2000 CHARACTERS
  19 754Q    [IN CASE A.'INPUT'WILL HAVE CREATED A SUBSEQUENT DUMMY RECORD   
  18 75JB    [SO IN EITHER CASE NO RECORD TERMINATORS ARE SUPPLIED BY US 
  20 7642    [THE FLAG 'L' IN THE ACTLF IS SET WHEN A COUNT OF 2000 IS DETECTED  
   4 76HL    TOTP
   8 76RD          LDX   1  FX1  
  16 773=          LDN   5  1                   [NON ZERO SWITCH 
  18 77GW          LDN   6  0                   [INITIALISE RUNOUT MARKER
  16 782G          LDX   0  GZLENGTH(2)         [RECORD LENGTH   
  18 78G6          TXU   0  XFULLREC(1)         [IS IT 2000 CHARACTERS?  
  14 78_Q          BCS      NOT2000             [J IF NOT
  16 79FB          LFBITSET L,2                 [ELSE SET FLAG   
  17 79_2          BRN      TPF1                [AND TREAT AS GRAPHIC
   5 7=DL    NOT2000 
  10 7=Y=          DCA      ,LDEX,0,MODE 
   8 7?CW          SBN   0  #10  
  15 7?XG          BZE   0  TPF1                [J IF GRAPHIC
   9 7#C6          LDX   0  AWORK1(2)
   8 7#WQ          SLL   0  15   
   8 7*BB          BNG   0  TPF1 
   7 7*W2          SLL   0  1
  17 7B*L          BNG   0  YALCH               [J IF ALLCHAR DATA   
  15 7BT=          ADN   6  6                   [FOR RUNOUT  
  15 7C#W    YALCH ADN   6  2                   [FOR NEWLINE 
  17 7CSG    TPF1  LDX   0  TPLEN(1)            [MAX TRANSFER COUNT  
  18 7D#6          SBX   0  GZDUMP(2)           [SET UP X0 WITH MAXIMUM  
  16 7DRQ          SBX   0  6                   [ROOM FOR DATA   
  18 7F?B          TXL   0  GZLENGTH(2)         [CAN WE ACCOMODATE ALL?  
  14 7FR2          BCS      XLTP                [IF NOT,J
  15 7G=L          LDX   0  GZLENGTH(2)         [YES WE CAN  
  16 7GQ=          NGX   5  2                   [RUNOUT SWITCH   
   8 7H9W          BRN      TRANS
  19 7HPG    XLTP  ADX   0  6                   [IS THERE ROOM IF WE FORGET  
  17 7J96          TXL   0  GZLENGTH(2)         [NEWLINE AND RUNOUT  
  14 7JNQ          BCS      TRANS               [J IF NOT
  18 7K8B          LDX   0  GZLENGTH(2)         [ELSE TRANSFER ALL DATA  
  18 7KN2          LDN   5  0                   [ALL WE NEED IS RUNOUT   
   5 7L7L    TRANS   
  10 7LM=          STO   0  GZTRANS(2)   
   9 7LX4 ...      ADX   0  GZDUMP(2)
  10 7M6W          DCA      ,STO,0,COUNT 
  17 7MLG          CALL  0  TRANSFER            [MOVE DATA TO BUFFER 
  18 7N66          BZE   6  NOXT                [NO N/L OR R/O THIS TIME 
   8 7NKQ          BPZ   5  NOXT 
   9 7P5B    XGN   LDN   3  NEWRUN   
   8 7PK2          ADX   3  FX1  
   7 7Q4L          SMO      6
  18 7QJ=          MVCH  3  0                   [MOVE IN EXTRA CHARACTERS
  15 7R3W          DCA      ,ADSC,6,COUNT       [IGNORE BO   
   7 7RHG          BCS      /
   9 7S36    NOXT  STO   5  AWORK3(2)
   8 7SGQ          CALL  5  PERI 
  10 7T2B          DCA      ,STOZ,,COUNT 
   9 7TG2          STOZ     GZDUMP(2)
  10 7T_L          LDX   0  GZTRANS(2)   
  10 7WF=          SBS   0  GZLENGTH(2)  
  10 7WYW          ADS   0  GZPOINT(2)   
   9 7XDG          LDX   5  AWORK3(2)
   8 7XY6          BZE   5  XGNA 
   9 7YCQ          BNG   5  XFINIS   
   7 7YXB          STEPAGAIN 
   8 7_C2          BRN      TPF1 
   5 7_WL    XFINIS  
  21 82B=          LDXC  6  6                   [TEST IF WE HAVE JUST DONE 'NORMAL'  
  20 82TW    STEST BCC      NOGRUN              [PERI FOR RUNOUT IN GRAPHIC DATA 
  19 83*G          DCA      ,STO,6,MODE         [IF SO,RESET MODE TO GRAPHIC 
  15 83T6          BRN      REPT                [NEXT RECORD 
   5 84#Q    NOGRUN  
  18 84SB          LFBBS    L,2,XFINL           [J IF 2000 CHAR RECORD   
  19 85#2          DCA   ,LDEX,0,MODE           [IF GRAPHIC THEN WE MUST DO  
  17 85RL          SBN   0  #10                 [SHIFT MODE PERI FOR 
  15 86?=          BNZ   0  REPT                [R/O AND N/L 
  16 86QW          LDN   0  4                   [PREVIOUS MODE   
  10 87=G          DCA      ,STO,0,MODE  
   7 87Q6          LDN   6  8
  16 889Q          ORX   6  GSIGN               [TEST AT STEST   
  14 88PB    XGNA  NGN   5  1                   [SWITCH  
  16 8992          LDN   4  GZDATA+1(2)         [BUFFER ADDRESS  
   8 89NL          BRN      XGN  
   4 8=8=    [   
   4 8=MW    [   
   4 8BJW    TEND
   9 8C4G          CALL  3  TIDYUP   
  14 8CJ6          STO   2  AWORK1(2)           [SWITCH  
  15 8D3Q          DOWN     LISTINTC,3          [TERMINATORS 
   6 8DHB          LFEND 
   5 8F32    XGEOR   
  10 8FGL          GEOERR   1,BREAK IN   
   4 8G2=    [   
  13 8GFW    [THIS CODE DEALS WITH MONFILE PARAMETER 
  19 8G_G    [THIS CODE MAY NEVER BE USED,BUT IT IS INCLUDED IN CASE LOGANAL 
  21 8HF6    [WISHES TO LIST MONITORING FILES TO OTHER SLOW PERIPHERALS BESIDES PRINT
   4 8HYQ    [   
  18 8JDB          ADN   4  4                   [ALLOW FOR CATEGORY WORD 
  18 8JY2          LDX   0  FRH+1(3)            [PICK UP CATEGORY WORD   
   9 8KCL          ANDX  0  AWORK3(2)
  20 8KX=          BNZ   0  OK1                 [J IF THIS CATEGORY TO BE LISTED 
  11 8LBW ...      MHUNTW   1,GOUT,INFOLIST  
   7 8LWG          LDN   0  1
  19 8MB6          ADS   0  GNUM(1)             [UPDATE CURRENT LINE NUMBER  
   9 8MTQ          LDX   2  GLNO(1)  
  16 8N*B          BNG   2  START               [J IF LIST TO EOF
   9 8NT2          SBS   0  GLNO(1)  
  16 8P#L          BRN      START               [TRY NEXT RECORD 
   4 8PS=    [   
   8 8Q?W    [LONG RECORD PATH   
   4 8QRG    [   
   5 8R?6    XFINL   
  20 8RQQ          LFBITCLR L,2                 [CLEAR 2000 CHAR RECORD MARKER   
  18 8S=B          LFBITCLR H,2                 [UNSET LONG RECORD MARKER
   8 8SQ2          BRN      REPT 
   4 8W8W    #END
   8 ____ ...31772643000100000000
