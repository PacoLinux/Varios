  14 2259 ...      SEG   CLOSEND,863,SECTION FILE,CLOSE  
   4 228D ...#   
  15 22=3 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   4 22?M ...#   
   7 22BW ...#     USES OF X7
   7 22G5 ...#     ----------
  15 22K# ...      BITDEFS  7,0,XBSHORT         [CLOSESHORT  
  17 22NH ...      BITDEFS  7,1,XBTOP           [CLOSETOP/CLOSEDIR   
  15 22RQ ...      BITDEFS  7,2,XBFULL          [CLOSE (FULL)
  15 22W_ ...      BITDEFS  7,3,XBFRBS          [CLOSEFRBS   
  19 23?T ...      BITDEFS  7,10,XBFRITEALL     [FRITE WITH 'ALL' PARAMETER  
  21 23*_ ...      BITDEFS  7,11,XBCORUNJWR     [ADATA/CORUNJAM BUFFER BEING (FIXED) 
  19 23D5 ...                                   [ WRITTEN BY CORE UNJAMMER   
  15 23G? ...      BITDEFS  7,12,XBTHROW        [CLOSETHROW  
  15 23KG ...      BITDEFS  7,14,XBABAND        [CLOSEABANDON
  20 23QT ...      BITDEFS  7,15,XBNONAUTOWR    [NON-AUTONOMOUS WRITING OF WRITE 
  18 23Y8 ...                                   [ BUFFER BLOCKS REQUIRED 
  18 245K ...      BITDEFS  7,20,XBCOPY         [CLOSING FROM COPY MODE  
  15 24#3 ...      BITDEFS  7,22,XBAUTO         [AUTONOMOUS  
  14 24C= ...      BITDEFS  7,23,XBSET          [CLOSESET
   4 24GF ...#   
   4 24KN ...#   
   8 24NX ...#     ENTRY POINTS. 
   4 24S6 ...#   
  11 24XQ          SEGENTRY K1CLOSEND,XENT1  
  17 25CB ...      SEGENTRY K2CLOSEND,XENT2     [ENTRY FROM CLOSEMAS 
   4 26DT ...#   
   4 26S2 ...#   
   4 2777 ...#   
   4 27G# ...#   
  19 27TG    #     CLOSEND CLOSES THE FILE IN THAT IT ADJUSTS CTOPEN, UNSETS 
  20 28*6 ...#     BFMCOP (THE OPEN IN COPY MODE BIT) AND RELEASES ANY WAITERS.  
  21 28SQ    #     IT DECIDES WHETHER AN AUTONOMOUS CLOSE SHOULD BE SET UP TO UPDATE 
  21 29#B ...#     THE DIRECTORY, AND IF SO, GOES ACROSS TO CLOSEONE TO SET ONE GOING
   4 29S2    [   
   4 2B8L ...#   
   4 2BN= ...#   
   5 2G5L    SFINDEXF
   8 2GK=    #HAL  FI+FINDEXF,0  
   9 2HJG    MCOMMUNE       #57777777
   9 2J46    MASKV          #47577737
   4 2KH2    #   
   4 2KLX ...#   
  10 2KQS ...#              SUBROUTINES  
  10 2KWP ...#              -----------  
   4 2L2L    #   
   4 2LG=    #   
  21 2LQ4 ...#     FREES THE FSTACK ELEMENT POINTED AT BY X2. ON EXIT, X1=FX1, X2=FX2
   5 2L_W    SUBRELT 
   7 2MFG          SUBRELT  2
   7 2M_6          EXIT  6  0
   4 2N8Y ...#   
   4 2N=G ...#   
  10 2N#4 ...#     LOCATES A FMAPP BLOCK.
  17 2N*L ...#     ON ENTRY, X2->FCB OR FSTACK.    X1 IS S/R LINK.   
  15 2NC8 ...#     ON EXIT, X2->FMAPP.      X0 OVERWRITTEN.  
   5 2NDQ    SFMAPP  
   9 2NYB ...      LDX   0  FFSFMAPP 
   5 2PXL    MAPFIND 
   9 2QC=          LDX   2  FPTR(2)  
  11 2QWW ...      BXU   0  ATYPE(2),MAPFIND 
   7 2RW6          EXIT  1  0
   4 2S5Y ...#   
   4 2S9T ...#   
   4 2S*Q    TAB 
  16 2STB          TOPFCAB  3,2                 [X2->FCB  X3->FCA
   7 2T*2          EXIT  6  0
   4 2TJS ...#   
   4 2TNP ...#   
   4 2TSL    TB  
  14 2W#= ...      TOPFCB   2                   [X2-> FCB
   7 2WRW          EXIT  6  0
   4 2X?G    #   
   4 2XR6 ...[   
  21 2Y=Q ...[ADJUST ACTIVITY PRIORITY WHEN CLOSING VITAL SYS FILE:ASF REQUIREMENT   
   8 2YQB ...[ ON ENTRY X2->FCB  
   5 2_=2 ...PRIORITY
  19 2_PL ...      JMBAC    (5),2,BFDIR,BFVSF    [EXIT IF NOT VITAL SYS FILE 
  15 329= ...      APVSF    DOWN           [ADJUST ACT PRI   
  14 32NW ...      CALL  6  TB             [X2->FCB AGAIN
   7 338G ...      EXIT  5  0
   4 375G    #   
  18 37K6    #     FIND THE ACTIVITY ATTACHED TO THE FCA ADDRESSED BY X2 
   5 384Q    SFACT   
   9 38JB          LDX   2  BPTRF(2) 
  10 3942          SMO      FBACKPOINT(2)
   9 39HL          LDX   0  ATYPE(2) 
   8 3=3=          TXL   0  CACT 
   8 3=GW          BCS      SFACT
  10 3?2G          ADX   2  FBACKPOINT(2)
  15 3?G6 ...      TRACEIF  K6CLOSEND,99,299,ACNUM(2),ACTOUT 
   7 3?_Q          EXIT  1  0
   4 3#FB    #   
   4 3G#W    #   
  21 3GSG ...#     CONVERT TO A FULL CLOSE - CHANGE X7 B0-8 TO HAVE JUST XBFULL SET  
   5 3H#6    XCONVERT
  14 3HRQ ...      TRACEIF  K6CLOSEND,99,299,7,CLOSE???  
  19 3J?B ...      ANDX  7  BSP16               [PRESERVE L.S. 15 BITS OF X7 
   9 3JR2 ...      BS       ,XBFULL  
   7 3M96          EXIT  5  0
   4 3MNQ    #   
   4 3MYJ ...#   
  21 3N8B    #     RELEASE ANY AUTOCLOSE ON THE FILE WHICH IS WAITING FOR THE FILE TO
  21 3ND8 ...#     BE FREE (IT WILL ONLY BE WAITING IF THE FILE IS OPEN IN GENERAL,  
  11 3NN2 ...#      WRITE OR APPEND MODE).   
  15 3NXS ...#     ON ENTRY, X2-> FCB.        X5 IS S/R LINK.
  21 3P?H ...#     ON EXIT, X2-> FCB, X0,X1 & X6 MAY BE OVERWRITTEN.  NO COORDINATION
   5 3PM=    RELCLFR 
  19 3QLG ...      JBCC     (5),2,BFCLEANW      [EXIT IF NO WAITING AUTOCLOSE
  16 3R66 ...      TRACEIF  K6CLOSEND,99,299,FLOC1(2),REL CLFR   
  12 3XGQ          LONGON   CLOSEFREE,BACK2(2)   
  14 3Y2B ...      CALL  6  TB                  [X2->FCB 
   7 3YG2 ...      EXIT  5  0
   4 3Y_L    #   
   4 3_9D ...#   
  18 3_F= ...#     THIS SUBROUTINE WAKES WAITERS IN STYLE 6 ON THIS FILE,
  20 3_P4 ...#     I.E. ANY FWAITCOUNT TYPE WAITERS OR ANYONE WAITING TO EMPTY.  
  15 3_YW ...#     ON ENTRY, X2-> FCB.       X5 IS S/R LINK. 
  21 428N ...#     ON EXIT, X0,X1 & X6 MAY BE OVERWRITTEN,  X2-> FCB. NO COORDINATION
   5 42DG    RELWAIT 
  10 48RL          LDX   0  FWAITCOUNT(2)
  17 49?=          BNZ   0  WAITER              [J IF SOMEONE WAITING
  20 49QW ...      JBC      (5),2,BFEMPTY       [EXIT IF NO-ONE WAITING TO EMPTY 
   5 4?9Q    WAITER  
  15 4?PB ...      TRACEIF  K6CLOSEND,99,299,FLOC1(2),RL STY6
  10 4#92          LONGON   6,BACK2(2)   
  14 4#NL ...      CALL  6  TB                  [X2-> FCB
   7 4*8= ...      EXIT  5  0
   4 4HKD ...#   
   4 4HLF ...#   
  20 4HN= ...[     THIS SUBROUTINE CLEARS THE BFTIDY SEMAPHORE WHICH IS SET UP IN
  20 4HQ3 ...[     SEGMENT CLOSEDIR - THE SEMAPHORE IS USED TO PREVENT ONE CLOSE 
  16 4HRS ...[     OVERTAKING ANOTHER IN CRITICAL CIRCUMSTANCES. 
  16 4HTN ...[     ON ENTRY, X2->FCB.          X5 IS S/R LINK.   
  21 4HXQ ...[     ON EXIT , X0, X1 & X6 MAY BE DESTROYED, X2->FCB.  NO COORDINATION.
   5 4H_S ...SBFTIDY 
  18 4J5Y ...      VOP      2,BFTIDY            [CLEAR BFTIDY SEMAPHORE  
  14 4J82 ...      CALL  6  TB                  [X2->FCB 
   7 4J=4 ...      EXIT  5  0
   4 4J#6 ...[   
   4 4J#8 ...#   
  19 4J#9 ...#     SUBROUTINE TO ISSUE A LFCLOSE MACRO, IF NECESSARY, TO TELL
  18 4J#= ...#     LISTFILE THAT THIS FILE IS NO LONGER OPEN FOR WRITING.
  21 4J#? ...#     ON ENTRY, TOP FILE RINGED IN IS ONE FOR WHICH LFCLOSE TO BE ISSUED
  17 4J## ...#               X2->FCB, X3->FCA.      X5 IS S/R LINK.  
  19 4J#* ...#     ON EXIT, X0,X1 & X6 MAY BE OVERWRITTEN, X2->FCB, X3->FCA. 
  13 4J#B ...#                      MAY COORDINATE.  
   5 4J#K ...XLFCLOSE
  20 4J#M ...      JFZ      (5),2,FFFREZCT      [J IF FILE NOT LISTFILE-FROZEN   
  10 4J#P ...      LDX   0  FSAVECOUNT(2)
   9 4J#R ...      ANDX  0  HALFTOP  
  18 4J#T ...      BNZ   0  (5)                 [J IF HLS WRITE FROZEN   
  21 4J#X ...      TRACEIF  K6CLOSEND,99,299,FLOC1(2),LFCLOS  [LFCLOSE MACRO ISSUED  
   8 4J#_ ...      SBX   5  FX1  
  19 4J*3 ...      GETFNAME                     [SET UP FABSNB FOR THIS FILE 
  14 4J*5 ...      CALL  6  TB                  [X2-> FCB
   6 4J*6 ...#UNS  AWLSZ 
   4 4J*7 ...(   
   9 4J*8 ...      LDX   6  FBLMOD(2)
  17 4J*9 ...      SBN   6  FBLKS-A1            [PICK UP FILE SIZE   
   4 4J*= ...)   
  10 4J*? ...      LF       2,FFFREZCT,0 
   6 4J*# ...#UNS  AWLSZ 
  21 4J** ...      LFCLOSE  0,6                 [INFORM LF THAT FILE CAN BE LISTED   
   6 4J*B ...#UNS AWLSZ  
   4 4J*C ...#SKI
  21 4J*D ...      LFCLOSE  0                   [INFORM LISTFILE THAT FILE CAN NOW BE
  14 4J*F ...                                   [ LISTED 
  17 4J*G ...      CALL  6  TAB                 [X2-> FCB, X3->FCA   
   8 4J*H ...      ADX   5  FX1  
   7 4J*J ...      EXIT  5  0
   4 4J*K ...#   
   4 4J*L ...#   
   6 4J*M ...TICKLEDRM   
   8 4J*P ...      SBX   5  FX1  
   7 4J*S ...      FDRMAUTX 6
  16 4J*T ...      CALL  6  TAB                 [X2->FCB, X3->FCA
   8 4J*X ...      ADX   5  FX1  
   7 4JB2 ...      EXIT  5  0
   4 4JB5 ...#   
   4 4JBG ...#   
   9 4JBX ...#              MAIN CODE
   9 4JC# ...#              ---- ----
   4 4JCP ...[   
   4 4JD= ...[   
  14 4JGL    #     THIS IS THE MAIN ENTRY FROM CLOSEDIR  
  19 4K2= ...#     THE FILE IS STILL OPEN & ATTACHED TO THE CLOSING ACTIVITY.
  12 4KFW ...#     AUTOCLOSES COULD BE RINGED IN.
   5 4LF6    XENT1   
   5 4LMK ...MERGEAB 
  17 4LN4 ...      CALL  6  TAB                 [X2 -> FCB X3 -> FCA 
  17 4LX2 ...      JBC      NOFFWF,2,BFWORK     [J IF NOT WORK FILE  
  12 4LY? ...      JBC      NOTERALL,2,BFERALLWF 
  19 4L_J ...      COOR3    #41                 [WAIT FOR ERALLWF TO FINISH  
  19 4M2T ...      BRN      MERGEAB              [AND MOVE FCB TO FILE CHAIN 
   5 4M46 ...NOTERALL
  20 4M5C ...      BS      2,BFERALLWF          [TO LOCK OUT ERALLWF DURING FRBS 
  21 4M6Q ...      ANDX  7  BSP16               [PRESERVE L.S. 15 BITS OF X7 (ESP.   
  19 4MBG ...                                   [ CLOSEABANDON BIT IF SET)   
  17 4ML= ...      BS       ,XBFRBS             [CONVERT TO CLOSEFRBS
   5 4M_W ...NOFFWF  
  21 4N5J ...      JBC      NOTAB,2,BFCLOSEAB   [J IF FILE NOT TO BE CLOSEABANDONED  
   9 4N9= ...      BS       ,XBABAND 
   5 4N#_ ...NOTAB   
  17 4NSK ...      JBS      XABAND,,XBABAND     [J IF CLOSEABANDON   
  21 4P#9 ...      JBS      NOTOPSTILL,,XBAUTO  [IGNORE OTHER OPENERS IF  AUTOCLOSE  
  19 4PRT ...      LDX   0  CTOPEN(2)           [X0= NO. OF TIMES FILES OPEN 
  20 4Q?F ...      SBX   0  4                   [ALLOW FOR MODE THIS ACT HAS FILE
  14 4QR5 ...                                   [ OPEN IN
  10 4XQ2          ANDX  0  MCOMMUNE(1)  
  16 4XY= ...      BZE   0  NOTOPSTILL           [J IF NOT OPEN  
  12 4Y6G ...      JBC      STILLOPEN,2,BFWORK   
  21 4Y#Q ...      BC       2,BFERALLWF          [CLEAR IN CASE SET ABOVE FOR FRBS   
   9 4YH2 ...      BRN      STILLOPEN
  21 4YP= ...                                   [IGNORE COPY MODE AT THIS POINT SO WE
  18 4_8W ...                                   [ RELEASE RIGHT WAITERS  
   6 52MQ    NOTOPSTILL  
  16 537B ...      JBS      TESTMAS,,XBFULL     [J IF FULL CLOSE 
  21 53M2 ...      JMBAS    WORKERASE,2,BFWORK,BFERASE [J IF TO-BE-ERASED WORKFILE   
  20 546L ...      JMBS     START,,XBSHORT,XBFRBS [J IF CLOSESHORT OR CLOSEFRBS  
   4 6KDQ    #   
  20 6KYB    #     CHECK TO SEE IF WE SHOULD CONVERT TO A FULL CLOSE. CONVERT IF 
  16 6LD2 ...#           BLOCKS ALT. OR UWB OR BFDIRUPDATE SET   
   4 6LLB ...#   
  15 6M36 ...      JMBAC    MFULL,2,BFALTB,BFUWB,BFDIRUPDATE 
  18 6M6L ...      CALL  5  XCONVERT            [CONVERT TO FULL CLOSE   
   5 6M=6 ...TESTMAS 
  16 6M*L ...      BXU   2  BFILE,MFULL         [J IF NOT MASTER 
   4 6MF6 ...#   
   4 6MJL ...#   
   4 6MN6 ...#   
  15 6MRL ...#     FULL CLOSE ON MASTER & NO OTHER OPENERS.  
  15 6MX6 ...#      GO ACROSS TO CLOSEMAS TO REWRITE MASTER'S
  21 6N2L ...#     DIRECTORY ENTRY. COME BACK TO K2CLOSEND IF NOT AN AUTOCLOSE. IF IT
  21 6N66 ...#     IS AN AUTOCLOSE, CLOSEMAS GOES ACROSS TO CLOSEALT TO WIND UP THE  
   8 6N9L ...#     CAREFUL UPDATE
   4 6N*6 ...#   
   8 6NDL ...      LDCT  0  #774 
  10 6NJ6 ...      ANDX  0  FGENERAL1(3) 
  18 6NML ...      ERS   0  FGENERAL1(3)        [CLEAR MODE OPEN IN FCA  
  19 6NR6 ...      BS       3,BAMGEN            [SET 'GENERAL MODE' IN FCA   
   8 6NWL ...      LDCT  0  #400 
  21 6P26 ...      STO   0  CTOPEN(2)           [PRETEND MASTER OPEN IN GENERAL MODE 
  18 6P5L ...      CALL  5  SBFTIDY             [CLEAR 'BFTIDY' SEMAPHORE
  10 6P96 ...      ACROSS   CLOSEMAS,1   
   5 6P#L ...XENT2   
  17 6PD6 ...      CALL  6  TAB                 [X2-> FCB, X3-> FCA  
  18 6PHL ...      STOZ     CTOPEN(2)           [CLEAR WAY MASTER IS OPEN
   9 6PKB ...      CALL  5  PRIORITY 
  19 6PM6 ...      CALL  5  RELWAIT             [RELEASE WAITERS IN STYLE 6  
  18 6PQL ...      BRN      TORIDFCB            [GO TO FREE FSTACK BLOCK 
   4 6PW6 ...#   
   4 6P_L ...#   
   4 6Q56 ...#   
   8 6Q8L ...#     CLOSEABANDON  
  13 6Q#6 ...#     FOR A WORKFILE, CARRY ON CLOSING  
  21 6QCL ...#     OTHERWISE WE GO DOWN TO CLOSEAB TO RETURN THE FILE TO THE STATE   
  18 6QH6 ...#           IT WAS IN BEFORE IT WAS WRITTEN TO OR OPENED.   
   4 6QLL ...#   
   5 6QQ6 ...XABAND  
  16 6QTL ...      JBS      WFAB,2,BFWORK       [J IF  WORK FILE 
  17 6Q_6 ...      DOWN     CLOSEAB,1           [CLOSEABANDON FILE   
  21 6R4L ...      BRN      OTHEROPE            [OTHER OPENERS (DO ABSOLUTE MINIMUM) 
  16 6R86 ...      CALL  6  TAB                 [X2->FCB, X3->FCA
   9 6R?L ...      BRN      XCTOPEN  
   5 6RC6 ...OTHEROPE
  16 6RGL ...      CALL  6  TAB                 [X2->FCB, X3->FCA
  21 6RJY ...      JBS      XCTOPEN,,XBCOPY     [J IF COPIER(MUSTN'T GO TO STILLOPEN 
  20 6RM= ...                                   [ - ROUTE ALL COPIERS BY XCTOPEN)
   9 6RPL ...      LDX   0  CTOPEN(2)
  10 6RT6 ...      ANDX  0  MCOMMUNE(1)  
   7 6RYL ...      SBX   0  4
  20 6S46 ...      BZE   0  XCTOPEN             [J IF STILLOPEN ONLY IN COPY MODE
   9 6S7L ...      BRN      STILLOPEN
   4 6S?6 ...#   
   4 6SBL ...#   
   4 6SG6 ...#   
  17 6SKL ...WORKERASE                          [TO-BE-ERASED ! FILE 
  21 6SP6 ...      JFNZ     START,2,FFFREZCT    [J IF LF-FROZEN (CAN'T BE HLS FROZEN)
   5 6SSL ...WFAB1   
   9 6SY6 ...      LDX   6  FBLMOD(2)
   9 6T3L ...      SBN   6  FBLKS-A1 
   8 6T76 ...      SMO      FX2  
  21 6T9D ...      STOZ     AWORK1              [REMEMBER IN AWORK1 NO. OF BLOCKS IN 
  18 6T?Q ...                                   [ FILE TO GIVE TO DELWORK
   9 6TB6 ...      LDN   0  FBLKS-A1 
  15 6TFL ...      STO   0  FBLMOD(2)           [EMPTY FILE  
  14 6TLW ...      SUBCUBS  3,6,JOB       [REDUCE CUBS   
  14 6TS6 ...      CALL  6  TB                  [X2-> FCB
   8 6TXL ...      BRN      START
  18 6W36 ...WFAB                               [TO-BE-ABANDONED ! FILE  
   9 6W6L ...      LDX   0  CTOPEN(2)
  10 6W=6 ...      ANDX  0  MCOMMUNE(1)  
   7 6W*L ...      SBX   0  4
  17 6WF6 ...      BZE   0  WFAB1               [J IF NOT STILL OPEN 
  21 6WJL ...      BS       2,BFCLOSEAB         [SET 'TO-BE-CLOSEABANDON' BIT IN FCB 
   4 6WPL    #   
   4 6X4R ...#   
   4 6XCY ...#   
  21 6XN? ...#     IT'S CLOSESHORT OR CLOSEFRBS. IF THERE ARE ANY SPARE BLOCKS IN THE
  21 6XYL ...#     FCB, PUT THEM IN A FULLB. IF CAREFUL, LOCATE ANY FULLBS AFTER THE 
  13 6Y8_ ...#     FMAPP AND CHAIN THESE TO THE ACT..
  16 6YFC ...#     IF CLOSEFRBS, FREEBAX ALL THIS BACKING STORE. 
  20 6YSJ ...#     IF CLOSESHORT, CHAIN THE FULLBS AFTER THE DIRECTORY'S FMAPP   
  16 6_7Q    #           - THE DIRECTORY MUST BE OPEN AT LEVEL 1 
   5 6_MB    START   
   9 7272          LDX   3  FUSEBL(2)
  21 72LL ...      SBX   3  FBLMOD(2)           [X3= NO. OF BLOCKS WHICH NEED FREEING
  19 736= ...      LDN   5  0                   [INITIALISE 'NO. OF FULLBS'  
  18 73KW ...      BZE   3  MAPCHX              [J IF NO BLOCKS TO FREE  
  15 745G ...      SETNCORE 2(3),2,BSTB,FULLB   [X2-> FULLB  
  14 74K6 ...      TOPFCB   1                   [X1-> FCB
   9 754Q ...      LDX   6  FUSEBL(1)
  21 75DF0...      SBX   6  FBLMOD(1)           [X6= NEW NO. OF BLKS NEEDING FREEING 
  21 75S8 ...      BXL   3  6,ZBSERR            [GEOERR IF NO. OF SPARE BLOCKS HAS   
  15 767X ...                                   [ INCREASED  
  18 76HL ...      LDX   3  6                   [X3= NO. OF SPARE BLOCKS 
  19 773= ...      SBS   3  FUSEBL(1)           [ADJUST NO. OF BLOCKS IN FCB 
  17 77GW ...      SBS   3  ALOGLEN(1)          [ADJUST LENGTH OF FCB
   7 782G ...      ADN   3  2
  20 78G6 ...      STO   3  A1(2)               [SET UP RECORD HEADER FOR FULLB  
   9 7G=L          LDX   0  BSPRE(1) 
  19 7GK3 ...      STO   0  A1+1(2)             [FILE RESIDENCE INTO FULLB   
  17 7GXD ...      BZE   3  NBLKS               [J IF NOW NO BLOCKS  
   9 7H9W          ADX   1  FBLMOD(1)
   8 7HPG          ADN   1  A1   
   8 7J96          ADN   2  A1+2 
  19 7JNQ ...      MOVE  1  510(3)              [MOVE SPARE BLOCKS TO FULLB  
   5 7K8B ...NBLKS   
  18 7KN2 ...      ADN   5  1                   [UPDATE COUNT OF FULLBS  
   5 7LM=    MAPCHX  
  16 7LX4 ...      CALL  6  TAB                 [X2->FCB, X3->FCA
  11 7M6W          JBC      NOCARE,2,BFCARE  
  15 7MLG ...      CALL  1  SFMAPP              [X2->FMAPP   
   7 7PK2          LDX   3  2
   9 7Q4L ...      LDX   6  FFSFULLB 
   5 7RHG    MOVENEXT
   9 7S36          LDX   1  FPTR(3)  
  17 7SGQ ...      BXU   6  ATYPE(1),MOVEDALL   [FINISH IF NOT FULLB 
  21 7T2B ...      CHAIN    1,FX2               [RECHAIN THE FULLBS AFTER THE ACT.   
  15 7T_L          BUX   5  MOVENEXT            [COUNT THEM  
   5 7WF=    MOVEDALL
  16 7WYW ...      CALL  6  TAB                 [X2->FCB, X3->FCA
   5 7XDG    NOCARE  
  16 7XY6 ...      BZE   5  NOSPARE             [J IF NO FULLBS  
  16 7YXB ...      JBS      TOFREEBS,,XBFRBS    [J IF CLOSEFRBS  
  17 82B=          LDX   2  FPTRF(3)            [X3->DIRECTORY'S FCA 
  10 82TW          ADX   2  FBACKPOINT(2)
  18 83*G ...      CALL  1  SFMAPP              [X2->DIRECTORY'S FMAPP   
  21 85#2    #     WHEN TRANSFERRING FULLB'S THEY SHOULD BE PUT AFTER THE FIRST FULLB
  17 85RL ...#     OF THE TRANSFEREE, OR AFTER THE FMAPP IF NO FULLB 
   9 86QW          SMO      FPTR(2)  
   8 87=G          LDX   0  ATYPE
  21 87Q6 ...      BXU   0  FFSFULLB,NODIRFUL   [J IF NO FULLB AFTER DIRECTORY'S FMAP
  16 889Q ...      LDX   2  FPTR(2)             [X2-> FIRST FULLB
   5 89NL    NODIRFUL
  20 8=8= ...      LDX   3  2                   [X3-> BLOCK TO CHAIN FULLBS TO   
   5 8=MW    NEXTMV  
  10 8?7G          MHUNTW   1,BSTB,FULLB 
  20 8?M6          CHAIN    1,3                 [CHAIN FULLB'S AFTER DIRECTORY   
   9 8#6Q          BCT   5  NEXTMV   
   9 8#LB          BRN      NOSPARE  
  18 8*62    TOFREEBS       [CLOSEFRBS FREES BACKING STORE NON-CAREFULLY 
   7 8*KL          FREEBAX   
  10 8B5=          MFREEW   BSTB,EMPTYB  
   9 8BJW          BCT   5  TOFREEBS 
   5 8C4G    NOSPARE 
  16 8CJ6 ...      CALL  6  TAB                 [X2->FCB, X3->FCA
  11 8D3Q ...      JBC      NOTWOR,2,BFWORK  
  19 8DHB ...      BC       2,BFERALLWF          [CLEAR BIT SET DURING FRBS  
   5 8F32 ...NOTWOR  
   9 8FGL          LDX   0  CTOPEN(2)
   8 8G2=          SMO      FX1  
   9 8GFW          ANDX  0  MCOMMUNE 
   7 8G_G          SBX   0  4
  21 8HF6 ...      BNZ   0  STILLOPEN           [J IF FILE NOW STILL OPEN (CAN BE IF 
  15 8HYQ ...                                   [ WORKFILE)  
   5 8LWG    MFULL   
  20 8MB6 ...      STOZ     FGENERAL1(3)        [CLEAR OUT FCA MARKERS SINCE ON  
  21 8MTQ ...      STOZ     FGENERAL2(3)        [ UPDATING CTOPEN WE ENTER NEW PHASE 
  18 8N*B ...                                   [ - MARK THE FCA AS SPARE
   4 953L    #   
  12 95H= ...#     CLOSE THE FILE - CLEAR CTOPEN 
   5 9726 ...XCTOPEN 
   9 979Y ...      CALL  5  PRIORITY 
  19 97FQ ...      JBS      YCOPY,,XBCOPY       [J IF CLOSING FROM COPY MODE 
   5 97PJ ...YCTOPEN 
  18 97_B ...      STOZ     CTOPEN(2)           [CLEAR 'WAY FILE IS OPEN'
  16 98F2 ...      MBC      2,BFDCF,BFCORE,BFSTEPWAIT,BFAPPWAIT  
  19 98YL ...      CALL  5  RELWAIT             [RELEASE ANY STYLE 6 WAITERS 
  20 99D= ...      CALL  5  RELCLFR             [RELEASE ANY AUTOCLOSE WAITING   
  14 99XW ...      JBSC     TLF,2,BFGDR         [J IF GDR
   7 9=CG ...      LDX   0  4
   7 9=X6 ...      ANDN  0  1
  19 9?BQ ...      BNZ   0  YREAD               [J IF CLOSING FROM READING   
  21 9?WB ...                                   [SINCE LISTFILE MAY SHARE WITH READ  
   4 9#B2 ...TLF 
  21 9#NC ...      CALL  5  XLFCLOSE            [INFORM LISTFILE (IF FROZEN) FILE NOW
  14 9*2S ...                                   [ FREE   
   5 9**= ...YREAD   
  18 9*SW ...      CALL  5  SBFTIDY             [CLEAR BFTIDY SEMAPHORE  
  15 9B#G ...      JMBS     SAVETEST,,XBABAND,XBSHORT,XBFRBS 
   5 9BS6 ...WTFILE  
  18 9C?Q ...      JBS      ZAUTO,2,BFSOLE      [J IF AUTOCLOSE ON FILE  
  17 9CRB ...      JBC      NFULL,,XBFULL       [J IF NOT FULL CLOSE 
   4 *NGB    #   
   4 *NSR ...#   
   4 *P78 ...#   
  13 *PFK ...#     THE DIRECTORY IS TO BE UPDATED.   
  16 *PS2 ...#     SET THE DIRECTORY-BEING-UPDATED BIT (BFSOLE). 
  18 *Q6C ...#     MARK THE FCA AS BELONGING TO AN UNCONVERTED AUTOCLOSE.
  21 *QDW    #     PRESERVE THE FCB NO. IN X5 FOR CHECKING IF THE FILE STILL EXISTS  
  20 *QR? ...#           AFTER A COORDINATION - SOMEONE MAY HAVE CLOSESHORTED OR 
  12 *R5N ...#           CLOSEABANDONED IT !!!   
  21 *RD6    #     SET UP AN AUTONOMOUS CLOSE ACTIVITY TO DO THE UPDATE, UNLESS THIS 
  21 *RXQ ...#           IS AN AUTOCLOSE CLOSE - THEN THE CURRENT ACTIVITY MUST DO   
  12 *SCB ...#           THE DIRECTORY UPDATE.   
  20 *SX2 ...      BS       2,BFSOLE            [SET 'WE ARE GOING TO UPDATE THE 
  16 *TBL ...                                   [ DIRECTORY' BIT 
   4 *TW=    [   
  21 *W*W ...[     NOW FIND THE LATEST AUTOCLOSE (IF ANY) AND UNMARK IT, MARK OUR FCA
  14 *WTG ...[     AS THE LATEST, CONVERTED SOLECLOSE.   
   4 *X*6    [   
  20 *XC* ...      LF       2,FFAUTCLCT,6       [X6= COUNT OF AUTOCLOSES ON FILE 
   7 *XFJ ...      ADN   6  1
  20 *XHR ...      STF      2,FFAUTCLCT,6       [UPDATE COUNT OF AUTOCLOSES BY 1 
   8 *XL2 ...      SBN   6  4095 
  21 *XN9 ...      BPZ   6  ZAUTTOOBIG          [J IF FIELD FFAUTCLCT HAS OVERFLOWED 
  21 *XQD ...      LDX   5  BACK2(2)            [REMEMBER FCB NO. OF TOP FILE OPEN   
  17 *XSQ          PSTAC    2,3                 [X2 -> FSTACK BLOCK  
  10 *Y#B ...      LDEX  6  ARINGNO(2)   
  19 *YS2 ...      ADN   2  A1                  [X2-> 1ST ELEMNT IN FSTACK   
   4 *_?L    NEXT
  21 *_L3 ...      JBSC     XLATEST,2,BALATEST  [J IF FOUND LATEST AUTOCLOSE (& CLEAR
  14 *_YD ...                                   [  BIT)  
   9 B2=W ...      ADN   2  FELLEN   
  20 B2QG ...      BCT   6  NEXT                [J IF MORE ELEMENTS TO LOOK AT   
   5 B3=6    XLATEST 
  13 B67W          MBS      3,BACONV,BASOLE,BALATEST 
  19 B776 ...      JBC      NOTCLAUT,,XBAUTO    [J IF NOT AN AUTONOMOUS CLOSE
  10 B9K=          ACROSS   CLOSEONE,1   
   5 B=4W    NOTCLAUT
  20 B=C? ...      ACROSS   CLOSEONE,4          [TO SET UP AUTONOMOUS CLOSE WHICH
  18 B=PN ...                                   [ THEN GOES TO K1CLOSEONE
   4 B?46    #   
   4 B?4C ...#   
   4 B?4N ...#   
   5 B?4_ ...YCOPY   
  21 B?53 ...      BXE   2  BFILE,YCTOPEN       [J IF :MASTER(OPEN IN GENERAL MODE   
  21 B?55 ...                                   [ FOR COPYING - FULL CLOSE SINCE FILE
  18 B?57 ...                                   [ NOT ACTUALLY COPIED)   
  20 B?5= ...      BC       2,BFMCOP            [CLEAR 'OPEN IN COPY MODE' BIT   
  10 B?5B ...      BRN      NOCOPYTEST   
   8 B?5H ...      SMO      FX2  
   9 B?5S ...      LDX   0  FILERING 
   8 B?65 ...      SMO      FX2  
  20 B?69 ...      BXU   0  FILEBRING,XCOPYERR2 [GEOERR IF EXTRA FILE RINGED INTO
  15 B?6* ...                                   [ AUTOCOPY   
   6 B?6G ...NOCOPYTEST  
  12 B?6M ...      JBCC     NOWCLOSED,2,BFFRITEW 
  16 B?6Y ...      TRACEIF  K6CLOSEND,99,299,FLOC1(2),RL FRITE   
  21 B?76 ...      FON      FRITEWAIT           [RELEASE ANY FRITES WAITING FOR COPY 
  15 B?7# ...                                   [ TO FINISH  
  14 B?7G ...      CALL  6  TB                  [X2->FCB 
   6 B?7R ...NOWCLOSED   
  21 B?84 ...      CALL  5  RELWAIT             [RELEASE ANY (COPY) WAITER IN STYLE 6
   9 B?88 ...      CALL  5  RELCLFR  
  18 B?8* ...      CALL  5  SBFTIDY             [CLEAR BFTIDY SEMAPHORE  
  18 B?8L ...      JBC      WTFILE,,XBABAND     [J IF NOT CLOSEABANDON   
   9 B?8X ...      LDX   0  CTOPEN(2)
  16 B?98 ...      BNZ   0  NOSET               [J IF STILL OPEN 
   9 B?9F ...      BRN      SAVETEST 
   4 B?9Q ...#   
   4 B?=3 ...#   
   4 B?=# ...#   
  20 B?=K ...ZAUTO                              [AUTOCLOSE ON FILE (BFSOLE SET)  
  19 B?=W ...      JBC      XJBRN,,XBAUTO       [J IF THIS NOT AN AUTOCLOSE  
  21 B??4 ...      JBC      XWAITAUTO,2,BFAUTO  [J IF AUTOCLOSE BEING SET UP BUT IS  
  16 B??= ...                                   [ NOT QUITE THERE
  10 B??D ...      SMO      FBACKPOINT(3)
  15 B??P ...      LDN   2  0(3)                [X2-> FSTACK 
  10 B?#2 ...      LDEX  5  ARINGNO(2)   
   8 B?#? ...      ADN   2  A1   
   4 B?#J ...SFCA
  17 B?#T ...      BXE   2  3,OURS              [J IF THIS IS OUR FCA
  10 B?*6 ...      BSXD     6,BALATEST   
  10 B?*C ...      ANDX  6  FGENERAL1(2) 
  18 B?*N ...      BNZ   6  XFOUND              [J IF LATEST FCA FOUND   
   4 B?*_ ...OURS
   9 B?B= ...      ADN   2  FELLEN   
  20 B?BH ...      BCT   5  SFCA                [J IF MORE ELEMENTS TO LOOK AT   
  11 B?BS ...      GEOERR   BRIEFPM,NO FCA   
  19 B?C5 ...XFOUND                             [X2-> FCA OF LATEST AUTOCLOSE
  19 B?CB ...      CALL  1  SFACT               [X2-> FILE'S AUTOCLOSE ACT.  
   8 B?CM ...      SMO      FX2  
  21 B?CY ...      STO   2  AWORK1              [REMEMBER POINTER TO OTHER AUTOCLOSE 
  15 B?D9 ...[              MERGE TO SOLECLOSE OR COPYFILE   
   4 B?DG ...#   
  21 B?DR ...#     RERING OUR FILE RING, EXCEPT OUR FCA, AT THE BOTTOM OF THE OTHER'S
   4 B?F4 ...#   
   8 B?F* ...      LDX   2  FX2  
  16 B?FL ...      LDX   3  FILERING(2)         [X3-> OUR TOP FCA
   7 B?FX ...      LDX   4  3
  21 B?G4 ...      BXE   3  FILEBRING(2),OURSUB [J IF OUR (TOP) FCA IS THE ONLY FCA  
  17 B?G9 ...                                   [ RINGED TO OUR ACT. 
  21 B?GB ...                                   [NO NEED FOR RERINGING SINCE OTHER   
  21 B?GH ...                                   [ AUTOCLOSE ALREADY RINGED TO FILE   
  15 B?GQ ...      LDX   2  AWORK1(2)           [OTHER ACT   
  20 B?H3 ...      LDX   5  FILEBRING(2)        [X5-> BOTTOM FCA OF OTHER ACT.   
  16 B?H# ...      LDX   3  FPTRF(3)            [X3-> OUR 2ND FCA
   4 B?HK ...NXX1
   8 B?HW ...      RERING   3,5  
   7 B?J7 ...      SMO      4
  19 B?JD ...      LDX   3  FPTRF               [X3-> NEXT FCA IN OUR RING   
  10 B?JP ...      SMO      FBACKPOINT(3)
  20 B?JX ...      LDN   2  0(3)                [X2-> BLOCK CONTAINING THIS RING 
  14 B?K5 ...                                   [ ELEMENT
  20 B?K? ...      BXU   2  FX2,NXX1            [J IF NOT BACK TO OUR ACT. BLOCK 
   5 B?KJ ...OURSUB  
   7 B?KT ...      LDX   2  4
  17 B?L6 ...      CALL  6  SUBRELT             [DERING OUR (TOP) FCA
   9 B?LC ...      BRN      SUICIDE  
   4 B?LN ...#   
   4 B?L_ ...#   
   4 B?M= ...#   
   6 B?MH ...XWAITAUTO   
  16 B?MS ...[     WAIT FOR THE FILE TO BE RUNG TO THE AUTOCLOSE 
   9 B?N5 ...      BS       2,BFAUTOW
  16 B?NB ...      TRACEIF  K6CLOSEND,99,299,FLOC1(2),WT CLWT1   
   9 B?NM ...      LDX   5  BACK2(2) 
  12 B?NY ...      LONGSET  CLOSEWAIT,(GEOERR),5 
   7 B?P9 ...      LONGCOOR  
  17 B?PG ...      CALL  6  TAB                 [X2-> FCB, X3->FCA   
  18 B?PR ...      BXE   5  BACK2(2),WTFILE     [J IF FILE STILL THERE   
  14 B?Q4 ...      TRACEIF  K6CLOSEND,99,299,FX2,CLWTGONE
   7 B?Q* ...      SUICIDE   
   4 B?QL ...#   
   4 B?QX ...#   
   4 B?R8 ...#   
   5 B?RF ...NFULL   
  20 B?RM ...      JBC      NCOPY,,XBCOPY       [J IF NOT COPY (CAN BE NO OTHER  
  14 B?RT ...                                   [ OPENER)
   9 B?S3 ...      LDX   0  CTOPEN(2)
  17 B?S# ...      BNZ   0  NOSET               [J IF FILE STILL OPEN
   5 B?SK ...NCOPY   
  20 B?SR ...      JFZ      TORIDFCB,2,FFAUTCLCT [J IF NO AUTOCLOSES OPERATING ON
  14 B?S_ ...                                    [ FILE  
  17 B?T7 ...      JBC      NOSET,,XBSET        [J IF NOT CLOSESET   
  21 B?TD ...#     CLOSESET SO MAKE THE CLOSING ACTIVITY WAIT UNTIL THE OTHER CLOSE  
  11 B?TP ...#     FINISHES AND RELEASES US. 
   5 B?W2 ...WAITSET 
  10 B?W? ...      STOZ     FGENERAL1(3) 
  20 B?WF ...      BS       3,BASET             [THIS ACT. IS ABOUT TO WAIT ON A 
  15 B?WM ...                                   [ CLOSESET   
  18 B?WT ...      BS       2,BFCLOSESET        [SET WAIT MARKER IN FCB  
  15 B?X6 ...      TRACEIF  K6CLOSEND,99,299,FLOC1(2),WT CLST
   6 B?XC ...      COOR1 
   5 B?XN ...      UP
   4 B?X_ ...#   
   4 B?Y= ...#   
   4 B?YH ...#   
  20 B?YS ...#     CLOSEABANDON CLOSESHORT OR CLOSEFRBS SO THERE ISN'T MUCH POINT
  21 B?_5 ...#     LETTING OTHER CLOSES CONTINUE. LOOK AT THE FCA'S IN THE FSTACK,   
  21 B?_B ...#     RELEASING ANY CLOSESET WAITERS BEFORE GOING ON TO FREE THE FSTACK.
   5 B?_M ...SAVETEST
  19 B?_Y ...      JBC      NOCLOSESET,2,BFCLOSESET [J IF NO CLOSESET ON FILE
  20 B#26 ...      LDN   6  0                   [INITIALISE COUNT OF CLOSESETS   
  15 B#2# ...                                   [ RELEASED   
  10 B#2G ...      SMO      FBACKPOINT(3)
  15 B#2R ...      LDN   5  0(3)                [X5-> FSTACK 
   7 B#34 ...      SMO      5
  18 B#3* ...      LDEX  4  ARINGNO             [X4= NO. OF RING ELEMENTS
  18 B#3L ...      ADN   5  A1                  [SET UP TO SEARCH FSTACK 
   5 B#3X ...SFCAPT  
  17 B#48 ...      BXE   5  3,OURFCA            [J IF THIS IS OUR FCA
  16 B#4F ...      LDX   2  5                   [X2-> NEXT FCA   
  20 B#4Q ...      JBC      OURFCA,2,BASET      [J IF THIS FCA IS ON A CLOSESET  
  20 B#53 ...      CALL  1  SFACT               [X2->ACTIVITY ATTACHED TO THE FCA
  16 B#5# ...      TRACEIF  K6CLOSEND,99,299,ACNUM(2),CLOSESET   
  19 B#5K ...      FPUT                         [PUT CLOSESET BACK ON LIST   
  21 B#5W ...      ADN   6  1                   [UPDATE COUNT OF CLOSESETS RELEASED  
   5 B#67 ...OURFCA  
  16 B#6D ...      ADN   5  FELLEN              [LOCATE NEXT FCA 
  20 B#6P ...      BCT   4  SFCAPT              [J IF MORE ELEMENTS TO LOOK AT   
  20 B#6X ...      BZE   6  ZCLOSESET           [GEOERR IF NO CLOSESET WAITER &  
  16 B#75 ...                                   [ BFCLOSESET SET 
  14 B#7? ...      CALL  6  TB                  [X2-> FCB
   6 B#7J ...NOCLOSESET  
  21 B#7T ...      FC       2,FFAUTCLCT         [CLEAR COUNT OF AUTOCLOSES ON FILE   
  16 B#86 ...      TRACEIF  K6CLOSEND,99,299,FLOC1(2),SH/FR/AB   
   4 B#8C ...#   
   4 B#8N ...#   
   4 B#8_ ...#   
  21 B#9= ...#     TRY TO FREE THE REMAINING BLOCKS ASSOCIATED WITH THE FILE BEFORE. 
   8 B#9H ...#           GOING UP
  18 B#9S ...#           FSTACK  FREE IT UNLESS BEING OPENED IN COPY MODE
  21 B#=5 ...#           FMAPP   FREE UNLESS CLOSESAVE - THEN CHAIN AFTER CURRENT ACT
  21 B#=B ...#           FINDEXF GOES WITH THE FCB - FREED OR PUT IN HALF-OPEN CHAIN 
  20 B#=M ...#           FULLB   ONLY HERE IF CLOSESAVE - CHAIN AFTER CURRENT ACT
  21 B#=Y ...#           FEXTRA  FREE IT IF WF IS TO-BE-ERASED, NOT FROZEN, NOT BEING
  17 B#?9 ...#                   UPDATED BY ERALLWF AND NOT CLOSESAVE
  21 B#?G ...#           FCB     FREE IT OR PUT DIRECTORY IN HALF-OPEN CHAIN UNLESS  
  17 B#?R ...#                      MARKED KEEP HALF-OPEN (COMM B14) 
  18 B##4 ...#                      WAITERS, OR SOMEONE WAITING TO EMPTY 
  21 B##* ...#                      CONVERTED AUTOCLOSE ON THE FILE I.E. CLOSECT SET 
  15 B##L ...#                      BEING OPENED IN COPY MODE
  14 B##X ...#                      WF NOT TO-BE-ERASED  
  21 B#*8 ...#                      WF TO-BE-ERASED BUT FROZEN OR BEING UPDATED BY   
  12 B#*F ...#                          ERALLWF  
   5 B#*Q ...TORIDFCB
  14 B#B3 ...      MBC      2,BFSOLE,BFCLOSESET,BFAUTO   
  21 B#B8 ...      JMBS     NOSET,2,BFMCOP,BFFCB [J IF FILE BEING COPIED (NEED THIS  
  21 B#B* ...                                   [ SINCE STILL OPEN IN COPY MODE SENT 
  19 B#BG ...                                   [ THROUGH SOLE OPENER PATH)  
  21 B#BM ...      JBS      TOFREEFCB,,XBABAND  [J IF CLOSEABANDON (IGNORE EMPTIERS &
  15 B#BS ...                                   [ WAITERS)   
  21 B#B_ ...      JMBS     TOFREEFSTAC,2,BFHALF,BFEMPTY  [J IF EMPTY OR TO KEEP FCB 
  20 B#C6 ...                                   [ IN FILE CHAIN (:MASTER ONLY)   
  10 B#CD ...      LDX   0  FWAITCOUNT(2)
  16 B#CP ...      BNZ   0  TOFREEFSTAC         [J IF WAITED FOR 
   6 B#D2 ...TOFREEFCB   
  16 B#D? ...      JBS      TOWF,2,BFWORK       [J IF WORKFILE   
  14 B#DJ ...      LDX   3  2                   [X3->FCB 
  16 B#DT ...      FREECORE FPTR(2)             [FREE FILE/FSTACK
  18 B#F6 ...      LDN   6  0                   [SET 'NO  FINDEXF' MARKER
   5 B#FC ...TRYNEXT 
  19 B#FN ...      LDX   2  FPTR(3)             [X2->  BLOCK CHAINED TO FCB  
   4 B#F_ ...TRYX
  18 B#G= ...      BXE   2  CXFI,OVERCHECK      [J IF BASE OF FILE CHAIN 
   9 B#GH ...      LDX   0  ATYPE(2) 
  16 B#GS ...      BXE   0  FILEPLUSFCB,OVERCHECK [J IF NEXT FCB 
  17 B#H5 ...      BXU   0  SFINDEXF(1),TRYFMAPP [J IF NOT FINDEXF   
   8 B#HB ...      LDN   6  #77  
   9 B#HM ...      ANDX  6  FINFC(3) 
  21 B#HY ...      BZE   6  ODDEND              [GEOERR IF FINDEXF BUT NOT INDEXED   
  20 B#J9 ...      LDX   2  FPTR(2)             [X2-> NEXT BLOCK IN FILE CHAIN   
   8 B#JG ...      BRN      TRYX 
   5 B#JR ...TRYFMAPP
  17 B#K4 ...      BXU   0  FFSFMAPP,ODDEND     [GEOERR IF NOT FMAPP 
  20 B#K* ...      JBCC     ODDEND,3,BFCARE     [GEOERR IF FMAPP BUT NOT CAREFUL 
  16 B#KL ...      FREECORE 2                   [FREE FILE/FMAPP 
   9 B#KX ...      BRN      TRYNEXT  
   5 B#L8 ...ODDEND  
  11 B#LF ...      GEOERR   BRIEFPM,ODD END  
   6 B#LQ ...OVERCHECK   
  14 B#M3 ...      JMBS     SFDIR,,XBSHORT,XBFRBS,XBTHROW
  16 B#M# ...      JBS      YDIRECTORY,3,BFDIR  [J IF DIRECTORY  
   5 B#MK ...SFDIR   
  18 B#MW ...      BZE   6  NOFINDEXF           [J IF NO FINDEXF BLOCK   
  15 B#N7 ...      FREECORE FPTR(3)             [THE FINDEXF 
   6 B#ND ...NOFINDEXF   
  14 B#NP ...      DELFCB   3,FILE              [FREE FCB
  18 B#P2 ...      JBC      UPA,,XBABAND        [J IF NOT CLOSEABANDON   
  21 B#P? ...      CLOSETOP                     [CLOSE DIRECTORY (OPENED IN CLOSEAB) 
   8 B#PJ ...      BRN      UPA  
   6 B#PT ...YDIRECTORY  
   9 B#Q6 ...      BZE   6  NOINDEX  
  10 B#QC ...      CHAIND   FPTR(3),BHALF
   5 B#QN ...NOINDEX 
  21 B#QW ...      TRANSFCB 3,FILE,HALF         [TRANSFER FCB FROM FILE TO HALFOPEN  
  14 B#R4 ...                                   [ CHAIN  
   8 B#R= ...      BRN      UPA  
   6 B#RH ...TOFREEFSTAC 
  15 B#RS ...      FREECORE FPTR(2)             [THE FSTACK  
   8 B#S5 ...      BRN      UPA  
   4 B#SB ...TOWF
  20 B#SM ...      TRACEIF  K6CLOSEND,99,299,FLOC3(2),CL FFWF [SOLE CLOSE OF A WF
   8 B#SY ...      SMO      FX1  
   8 B#T9 ...      LDX   0  MASKV
   9 B#TG ...      ANDS  0  COMM(2)  
  18 B#TR ...      JBC      TOFREEFSTAC,2,BFERASE [J IF NOT TO-BE-ERASED 
  19 B#W4 ...      JBS      TOFREEFSTAC,2,BFERALLWF [J IF ERALLWF OPERATING  
  16 B#W* ...      JFNZ     TOFREEFSTAC,2,FFFREZCT [J IF FROZEN  
  15 B#WL ...      TRACEIF  K6CLOSEND,99,299,FLOC3(2),ER FFWF
   7 B#WX ...      DELWORK   
   8 B#X8 ...      BRN      UPA  
   4 B#XF ...#   
   6 B*2L ...STILLOPEN   
  20 B*G= ...      TRACEIF  K6CLOSEND,99,299,FLOC1(2),STILOPEN [FILE STILL OPEN  
   9 B*_W ...      CALL  5  PRIORITY 
  16 BJSL          SBS   4  CTOPEN(2)           [ADJUST CTOPEN   
  21 BL*8 ...      STOZ     FGENERAL1(3)        [CLEAR OUT FCA MARKERS SINCE ENTERING
  15 BLDM ...      STOZ     FGENERAL2(3)        [ NEW PHASE  
  20 BLJ6 ...      JBS      YCOPYERR,,XBAUTO    [GEOERR IF AUTOCLOSE OR AUTOCOPY 
  19 BLMK ...                                   [ (THEY GO DOWN XCTOPEN PATH)
   7 BLR6 ...      LDX   0  4
   7 BM=Q ...      ANDN  0  1
  21 BMM= ...      BZE   0  XCLOSAPP            [J IF CLOSING FROM APPENDING (READING
  15 BN3Q ...                                   [ OTHERWISE) 
  10 BND= ...      LDX   0  FCOMMCT(2)   
  20 BP9= ...      BZE   0  XBFTIDY             [J IF NOT STILL OPEN COMMUNALLY  
  21 BQ5B ...      CALL  5  RELWAIT             [RELEASE WAITING COMMUNAL APPENDER IF
  20 BQBQ ...                                   [ ANY (MUST BE A COMMUNAL READER)
  10 BQN6 ...      LDN   6  FCBAPPWAIT   
  17 BR7Q ...      CALL  5  TICKLEDRM           [TICKLE ANY DRMERS   
  21 BRMB ...      JBCC     XBFTIDY,2,BFFREEW   [WAKE UP ANY DESTRUCTIVE APPENDERS   
  21 BR_R ...      LONGON   IWTDEST,BACK2(2)    [  WAITING FOR BLOCK TO BE FREED (MAY
  21 BS#8 ...                                   [  BE COOR4 AS WELL AS LONG WAITERS) 
  14 BSLL ...      CALL  6  TB                  [X2-> FCB
   5 BT6= ...XBFTIDY 
  18 BTKW ...      CALL  5  SBFTIDY             [CLEAR BFTIDY SEMAPHORE  
   4 BW5G ...#   
   4 BWK6 ...#   
   4 BX4Q ...#   
   5 CH5B    XJBRN   
  17 CJ4L ...      JBC      NOSET,,XBSET        [J IF NOT CLOSESET   
  21 CJJ= ...      JBS      WAITSET,,XBFULL     [J TO WAIT IF CLOSESET CONVERTED FROM
  18 CK3W ...                                   [ 'TOP' TO 'FULL' CLOSE  
  20 CKHG ...      JFNZ     WAITSET,2,FFAUTCLCT [J IF AUTOCLOSE STILL GOING ON ON
  14 CL36 ...                                   [ FILE   
   5 CM2B    NOSET   
   7 CMG2          LDX   2  3
   9 CM_L          CALL  6  SUBRELT  
   4 CNF= ...UPA 
  19 CNYW ...      JBC      UP,,XBCOPY          [J IF NOT CLOSING FROM COPY  
   5 CPY6    SUICIDE 
  19 CQCQ          SUICIDE                      [ONLY IF AUTOCOPY ACTIVITY   
   4 CQXB    UP  
   5 CRC2          UP
   4 CRC3 ...#   
   4 CRC4 ...#   
   4 CRD9 ...#   
   4 CRFD ...#   
   4 CRGM ...#   
   4 CRHW ...#   
  21 CRK5 ...XCLOSAPP                           [SINCE STILL OPEN MUST BE COMMUNAL   
   9 CRL# ...      LDX   0  CTOPEN(2)
   9 CRMH ...      ANDX  0  COMUNI   
  17 CRNQ ...      BNZ   0  XBFTIDY             [J IF OTHER APPENDERS
  21 CRPJ ...      CALL  5  RELWAIT             [LAST APPENDER - SO ALLOW ANY WAITING
  18 CRQB ...                                   [ NON-COMMUNAL READERS IN
  20 CRR8 ...      CALL  5  RELCLFR             [RELEASE ANY AUTOCLOSE WAITING   
  21 CRS2 ...      CALL  5  XLFCLOSE            [TELL LISTFILE FILE CAN NOW BE LISTED
  15 CRSS ...                                   [ (IF FROZEN)
  10 CRTL ...      LDN   6  FCBSTEPWAIT  
  21 CRWT ...      CALL  5  TICKLEDRM           [TICKLE ANY DRMERS WAITING FOR RECORD
  21 CRXD ...      JBCC     YBFTIDY,2,BFAPPC    [J IF STYLE 5 WAITER EXISTS (CLEAR   
  15 CRY3 ...                                   [ BIT TOO)   
  19 CRYL ...      BC       2,BFAPPLW           [THIS BIT MAY BE SET TOO !   
  10 CR_? ...      LONGON1  5,BACK2(2)   
  14 CS2G ...      CALL  6  TB                  [X2-> FCB
   5 CS3P ...YBFTIDY 
  18 CS4Y ...      CALL  5  SBFTIDY             [CLEAR BFTIDY SEMAPHORE  
  20 CS5M ...      JMBS     XJBRN,,XBABAND,XBFRBS [NO DIR. UPDATE IF FILE WILL BE
  20 CS6B ...                                   [ CLOSEABANDONED OR IS A ! FILE  
  20 CS75 ...      CALL  5  XCONVERT            [CONVERT TO FULL CLOSE TO ENSURE 
  20 CS7S ...                                   [ ATTEMPT TO UPDATE DIR. ENTRY   
  16 CS8K ...      BRN      WTFILE              [GO & UPDATE DIR.
   4 CS9S ...#   
   4 CS?3 ...#   
   4 CS#= ...#   
   4 CS*F ...#   
   4 CSBN ...#   
   4 CSCP ...#   
   4 CSCS ...#   
   5 CSCX ...ZBSERR  
  20 CSDP ...      GEOERR   BRIEFPM,BS INC      [CLOSESHORT OR CLOSEFRBS - NO. OF
  19 CSFH ...                                   [ SPARE BLOCKS HAS INCREASED 
   5 CSG* ...YCOPYERR
  21 CSH7 ...      GEOERR   BRIEFPM,STILAUTO    [AUTOCLOSE OR AUTOCOPY HAS GONE DOWN 
  16 CSH_ ...                                   [ STILLOPEN PATH 
   6 CSJR ...XCOPYERR2   
  21 CSL2 ...      GEOERR   BRIEFPM,COPYRING    [FILE RINGED INTO AUTOCOPY (TOO EARLY
   6 CSM9 ...ZCLOSESET   
  20 CSN3 ...      GEOERR   BRIEFPM,NO SET      [BFCLOSESET SET BUT NO CLOSESET  
  14 CSNT ...                                   [ WAITING
   6 CSPM ...ZAUTTOOBIG  
  20 CSQ6 ...      GEOERR   BRIEFPM,AUT OVR     [COUNT OF AUTOCLOSES ON FILE HAS 
  20 CSQK ...                                   [ OVERFLOWED (BEYOND 12 BITS!!)  
   4 CSRH ...#   
   4 CSS5 ...#   
   4 CSTW    #END
   8 ____ ...00504311000100000000
