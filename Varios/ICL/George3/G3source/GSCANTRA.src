   8 22FL    #OPT  K0GSCANTRA=0  
  14 22_=    #LIS  K0GSCANTRA>K0POST>K0KERNEL>K0ALLGEO   
  16 23DW    #SEG  GSCANTRA                     [DICK RUSBRIDGE  
   7 23YG          8HGSCANTRA
  13 24D6    #     FIXED STORE CODE OF GSCAN MACRO   
   9 24XQ          LABFIX   JJSCAN   
  16 25CB          STO   0  BATEMAN1            [REMEMBER LINK   
   9 25X2          SMO      BATEMAN1 
  19 26BL          OBEY     2                   [LOAD AND STORE ADDRESS OF   
  17 26W=          STO   0  BATEMAN2            [AREA TO BE SCANNED  
   9 27*W          SMO      BATEMAN1 
  18 27TG          OBEY     3                   [LOAD AND STORE LENGTH OF
   7 28*6          LDCH  0  0
  17 28SQ          STO   0  BATEMAN3            [AREA TO BE SCANNED  
  21 29#B          ADN   0  4                   [ADD 4 FOR REC HDR, NAME AND ACT NO. 
  21 29S2          TXL   0  GSCANLEFT           [TEST AND J IF THIS SCAN WILL FIT IN 
  14 2=?L          BCS      PMOVE               [BUFFER  
   9 2=R=          LDX   1  GSCANPNTR
  21 2?=W          STOZ     0(1)                [STOZ NEXT WORD IN BUFFER-INDICATES  
  19 2?QG                                       [NEXT SCAN AT START OF BUFFER
   9 2#=6          LDX   1  GSCANBUFF
  21 2#PQ          STO   1  GSCANPNTR           [RE-SET CURRENT SCAN AND LAST VALID  
  21 2*9B          STO   1  GSCANVAL            [SCAN POINTERS = TO START OF BUFFER  
  20 2*P2          LDX   1  GSCANLEN            [ALSO RESET 'SPACE LEFT TO END OF
  19 2B8L          STO   1  GSCANLEFT           [BUFFER' = LENGTH OF BUFFER  
  21 2BN=    PMOVE ADX   0  GSCANPNTR           [X0=CURRENT SCAN ADD. IN BUFFER PLUS 
  16 2C7W                                       [LENGTH OF SCAN  
  21 2CMG          LDX   1  GSCANVAL            [BRANCH IF NOT NECESSARY TO UPDATE   
  14 2D76    X     BZE   1  SOK                 [GSCANVAL
  21 2DLQ          TXL   0  GSCANVAL            [TEST AND J IF FIRST VALID SCAN IS   
  19 2F6B          BCS      SOK                 [NOT GOING TO BE OVERWRITTEN 
  21 2FL2          LDX   1  0(1)                [GET LENGTH OF FIRST VALID SCAN AND  
  19 2G5L          BZE   1  Z                   [BRANCH IF AT START OF BUFFER
   9 2GK=          ADX   1  GSCANVAL 
  21 2H4W    Z     STO   1  GSCANVAL            [RESET ADDRESS OF FIRST VALID SCAN   
   7 2HJG          BRN      X
  21 2J46    SOK   SBX   0  GSCANPNTR           [RESET X0 TO LENGTH OF CURRENT SCAN  
  20 2JHQ          LDX   1  GSCANPNTR           [LOAD CURRENT POSITION IN BUFFER 
  17 2K3B          STO   0  0(1)                [STORE RECORD HEADER 
  14 2KH2          LDX   2  BATEMAN1            [GET LINK
  16 2L2L          LDX   0  0(2)                [STORE SCAN NAME 
   8 2LG=          STO   0  1(1) 
   8 2L_W          LDX   0  1(2) 
   8 2MFG          STO   0  2(1) 
   8 2M_6          LDX   2  FX2  
  18 2NDQ          LDX   0  ACTNUM(2)           [STORE ACTIVITY NUMBER   
   8 2NYB          STO   0  3(1) 
  17 2PD2          ADN   1  4                   [STEP BUFFER POINTER 
   9 2PXL          LDX   0  BATEMAN3 
  20 2QC=          BZE   0  SOK1                [BRANCH IF NOTHING TO BE SCANNED 
  21 2QWW          LDX   0  BATEMAN2            [GET START ADD. OF AREA TO BE SCANNED
   9 2RBG          SMO      BATEMAN3 
  16 2RW6          MOVE  0  0                   [COPY TO BUFFER  
  20 2S*Q    SOK1  LDX   1  GSCANPNTR           [RESET POINTER TO START OF THIS  
  20 2STB          LDX   0  0(1)                [RECORD AND LOAD RECORD LENGTH   
  17 2T*2          ADS   0  GSCANPNTR           [RESET BUFFER POINTER
  19 2TSL          SBS   0  GSCANLEFT           [AND LENGTH LEFT IN BUFFER   
  15 2W#=          LDN   1  4                   [UPDATE LINK 
   9 2WRW          ADS   1  BATEMAN1 
  14 2X?G          LDX   1  FX1                 [X1 AND  
  13 2XR6          BRN      (BATEMAN1)          [EXIT
   2 2Y=Q
   2 2YQB
   8 2_=2    #SKI  K6TRACE>11-11 
   4 2_PL    (   
   4 329=    #   
  21 32NW    # THIS ROUTINE, ENTERED FROM THE TRACE MACRO, PLANTS INFO IN TRACE BUFFE
   9 338G          LABFIX   JJTRACEX 
  21 33N6          BVCI     /                   [SET V IF TRACE GENERATED BY GSCAN   
   9 347Q          LABFIX   JJTRACE  
  15 34MB          STO   1  BATEMAN3            [STORE LINK  
   9 3572          STO   2  BATEMAN2 
  15 35LL          LDX   1  BATEMAN1            [RESET  X1   
   9 366=          SMO      BATEMAN3 
   7 36KW          OBEY     2
  18 375G          LDX   2  JTRAPNTR            [CURRENT BUFFER POINTER  
  15 37K6          STO   1  2(2)                [%A TO BUFFER
   8 384Q          BZE   1  T1   
  19 38JB          BXGE  1  JTHREE,T1           [SPECIAL ACTION IF X1 OR X2  
  10 3942          LDN   1  BATEMAN1-1(1)
  16 39HL    T1    LDX   1  0(1)                [CONTENTS OF %A  
   8 3=3=          STO   1  3(2) 
  14 3=GW          LDX   1  BATEMAN3            [EGT LINK
  16 3?2G          MOVE  1  2                   [8H%B TO BUFFER  
   7 3?G6          LDN   1  0
   8 3?_Q          BVCR     T3   
  18 3#FB          LDCT  1  256                 [SET SIGN BIT IF V SET   
  17 3#_2    T3    ADS   1  2(2)                [STORE GSCAN MARKER  
   8 3*DL          LDX   1  FX2  
  16 3*Y=          LDX   1  ACTNUM(1)           [ACTIVITY NUMBER 
   8 3BCW          STO   1  4(2) 
  16 3BXG    T2    ADN   2  JTRAENT             [STEP ON POINTER 
  10 3CC6          BXL   2  JTRATOP,XIT  
  18 3CWQ          LDX   2  JTRABUFF            [REST IF END OF BUFFER   
  19 3DBB          STOZ     JTRAIND             [SET BUFFER FULL INDICATOR   
  19 3DW2    XIT   STO   2  JTRAPNTR            [STORE POINTER TO NEXT ENTERY
  15 3F*L          LDX   1  BATEMAN1            [RESTORE X1  
  14 3FT=          LDX   2  BATEMAN2            [  AND X2
   9 3G#W          SMO      JTHREE   
  13 3GSG          BRN      (BATEMAN3)          [EXIT
   4 3H#6    )   
   4 3HRQ    #END
   5 3J?B    #LIS  0 
   8 ____ ...64624757000200000000
