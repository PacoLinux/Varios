  13 22FL ...      SEG      JWELLONE,865,SECTION CENT
   4 22KH ...[   
  16 22PD ...[   (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982
   4 22T* ...[   
   4 22_=    #   
   4 23DW    #   
  21 23YG    #     THIS SEGMENT CONTAINS CODE THAT HAS OVERFLOWED FROM OTHER PARTS   
  20 24D6    #     OF THE JOBWELL SYSTEM.   THE SEGMENTS CATERED FOR HERE ARE:   
   4 24XQ    #   
  10 25CB ...#               SCHEDULE.   
   4 25X2    #   
   4 26BL    #   
   4 26W=    #   
  12 27*W ...      SEGENTRY K1JWELLONE,(GEOERR)  
  12 27TG ...      SEGENTRY K2JWELLONE,(GEOERR)  
  10 28*6 ...      FSHENTRY K3JWELLONE   
  17 28SQ          SEGENTRY K4JWELLONE,XK4      [ENTRY FROM SCHEDULE 
  18 29#B          SEGENTRY K5JWELLONE,XK5      [RE-ENTRY FOR SCHEDJOB   
  17 29S2          SEGENTRY K7JWELLONE,XK7      [ENTRY FROM EXPRESS  
  10 2=3S ...      FSHENTRY K8JWELLONE   
   4 2=?L    #   
   4 2=R=    #   
   8 2?=W    #DEF  SIZELIST=10   
   4 2?QG    #   
   4 2#=6    #   
   9 2#PQ    XJOBLIST       +SIZELIST
   8 2*9B                   12H  
  10 2*P2                   12HJOBLIST   
   8 2B8L                   +1   
   8 2BN=                   +1   
   9 2C7W                   4H****   
   4 2CMG ...[   
   4 2D76 ...[   
  16 2DLQ ...#           [*****  OVERFLOW FROM SCHEDULE  *****   
   4 5N7=    #   
  10 5NLW          BITDEFS  AWORK3,6,SCOM
   6 5NS4 ...#UNS  ICT   
  10 5N_= ...      BITDEFS  AWORK3,12,XAP
  11 5QKB          BITDEFS  AWORK3,14,XCP,XMP
  12 5R52          BITDEFS  AWORK3,16,XCC,XST,XEP
  13 5S4=          BITDEFS  AWORK3,20,XGP,XGB,XGR,XGQ
  19 5SHW          BITDEFS  AWORK4,0,NUM,OPENJL,USER,XME,NULL,WELL,OPENUSER  
  12 5T3G ...      BITDEFS  AWORK4,7,XBACK,POPPED
   4 5XFL    [   
  19 5X_=    [     SUBROUTINE TO REWRITE JOBLIST ENTRY WITH NEW BIT SETTINGS.
   5 5YDW    SETBITS 
   8 5YYG          SBX   7  FX1  
   7 5_D6          STEPAGAIN 
  10 5_XQ          ADX   3  JOBDATASIZE  
   7 62CB          LDN   5  0
  10 62X2          JBC      SET1,2,XGB   
  10 63BL          BSXD     4,JLBBREAKGR 
   7 63W=          ORX   5  4
  10 64*W    SET1  JBC      SET2,2,XGR   
  10 64TG          BSXD     4,JLBRERUN   
   7 65*6          ORX   5  4
  10 65SQ    SET2  JBC      SET3,2,XGQ   
  10 66#B          BSXD     4,JLBGRQUERY 
   7 66S2          ORX   5  4
  10 67?L    SET3  JBC      SET3A,2,XGP  
  10 67R=          BSXD     4,JLBRUNDUMP 
   7 68=W          ORX   5  4
   9 68QG    SET3A ORS   5  JLSTAT(3)
  11 69=6          JMBAC    SET4,2,XGQ,XCC   
  10 69PQ          JBC      SET5,2,XCC   
   9 6=9B          ERS   5  JLSTAT(3)
   8 6=P2          BRN      SET5 
  18 6?8L    SET4  BC       3,JLBGRQUERY        [CLEAR GQ IF GB OR GR SET
  10 6?N=    SET5  SBX   3  JOBDATASIZE  
  10 6#7W          LDX   6  JLJOBNO(3)   
   8 6#MG          STEPREWRITE   
   8 6*76          ADX   7  FX1  
   7 6*LQ          EXIT  7  0
   4 6B6B    [   
   4 7?XG    #   
   4 7#C6    #   
   6 7#WQ    XCLOSETOP   
   4 7*BB    #   
  20 7*W2    #     THERE WILL BE OCCASIONS WHEN BOTH USER AND SYSTEM JOBLIST WILL
  21 7B*L    #     BE OPEN TOGETHER.   THIS BIT OF CODE CLOSES USER JOBLIST IF BOTH  
   9 7BT=    #     FILES ARE OPEN>   
   4 7C#W    #   
  19 7CSG          JBCC     XCLOSETOPX,2,OPENUSER [JIF USER JOBLIST NOT OPEN 
  16 7D#6          SBX   7  FX1                   [PROCESS LINK  
  18 7DRQ          CLOSETOP                       [CLOSE TOP FILE OPEN   
   8 7F?B          ADX   7  FX1  
   4 7FR2    #   
   6 7G=L    XCLOSETOPX  
  17 7GQ=          EXIT  7  0                     [RETURN TO CALLER  
   4 7H9W    #   
   9 7K8B    #     ENTRY FROM EXPRESS
   4 7KN2    XK7 
   9 7L7L          STOZ     AWORK3(2)
   9 7LM=          STOZ     AWORK4(2)
   8 7M6W          BS       2,XEP
   9 7MLG          BRN      XEPENT   
   4 7NKQ    XK4 
  10 7P5B          JBC      SDN,2,WELL   
   4 7PK2    [   
   9 7Q4L    [     GETJOBS JOB NUMBER
   4 7QJ=    [   
   7 7R3W          STEPAGAIN 
  10 7RHG          LDX   6  JLJOBNO(3)   
  10 7S36          ADX   3  JOBDATASIZE  
  11 7SGQ          JBS      WELLMOP,3,JLBMOP 
   9 7T2B          BS       2,XBACK  
   5 7TG2    WELLMOP 
   9 7T_L    SDN   LDX   6  AWORK1(2)
  10 7WF=    XR    JMBAS    XS,2,WELL,XEP
  12 7_C2          JMBS     XWL,2,XGB,XGR,XGQ,XGP
   8 82B=          BRN      XT   
   8 82TW    XS    BS       2,XST
  10 83*G    XT    JBC      TELLV,2,XST  
   4 83T6    XWL 
  10 84#Q          JBC      STNOW,2,WELL 
  13 85RL          JMBAC    XSTC,2,XGB,XGR,XGQ,XGP   
  12 86?=          JBC      XNODG,3,JLBNOTCAND   
  11 86QW ...      JBS      OP1,2,OPENUSER   
  17 87=G          CALL  6  NOJOB               [OPEN USER JOBLIST   
   9 87Q6    OP1   CALL  7  SETBITS  
   9 889Q ...      CALL  7  XCLOSETOP
  10 88PB ...      JBSS     OP2,2,OPENJL 
  15 8992 ...      CALL  7  XOPENJL    [ OPEN SYSTEM JOBLIST 
   9 89NL          GETJOB   6,SYSTEM 
   9 8=8=    OP2   CALL  7  SETBITS  
   8 8=MW          BRN      TELLV
   4 8?7G    XSTC
  12 9#TL          JBS      STARTDG,3,JLBNOTCAND 
  20 =F_6          CALL  7  XCLOSETOP           [CLOSE JOBLIST IF WE OPENED IT!!!
  10 =GDQ ...      JBSS     SOK,2,OPENJL 
  15 =GYB ...      CALL  7  XOPENJL   [ OPEN SYSTEM JOBLIST  
   9 =HXL          GETJOB   6,SYSTEM 
   9 =JC=          TESTRPN2 OK,NONX  
   4 =KW6    SOK 
   7 =L*Q          STEPAGAIN 
  10 =LTB          ADX   3  JOBDATASIZE  
  16 =TN6          JBC      STBACK,3,JLBMOP     [J IF BACKGROUND 
  10 =W7Q          JBS      TELLV,,HLSMS 
   8 =WMB          BRN      START
   5 =X72    STBACK  
  10 =XLL          JBS      TELLV,,HLSBS 
   8 =Y6=    START BC       2,XST
   7 =YKW          READAGAIN 
   4 ?2JB    [   
  15 ?342    [     REWRITE SYSTEM JOBLIST ENTRY AS RUNNING   
   4 ?3HL    [   
  10 ?43=          MHUNT    3,FILE,FRB   
  10 ?4GW          NAME     3,FILE,FWB   
   7 ?52G          JLADJUST 3
  10 ?5G6          BS       3,JLBRUNNING 
   7 ?9C6          REWRITE   
  10 ?9WQ          MHUNT    3,FILE,FWB   
  10 ?=97 ...      LDX   4  ALOGLEN(3)   
  12 ?=HJ ...      SETUPCORE 4,2,JWELL,COPYSYS   
  10 ?=W2          MHUNTW   3,FILE,FWB   
   8 ??*L          LDN   4  A1(3)
   8 ??T=          LDN   5  A1(2)
  10 ?##W          SMO      ALOGLEN(3)   
   7 ?#SG          MOVE  4  0
  11 ?*#6          NAME     3,JWELL,COPYSYS  
   9 ?B?B          DOWN     SETJOBQ,2
   8 ?CQ=          JWELLADD LIVE 
   8 ?JLG          JWELLSUB WELL 
   8 ?K66          BRN      TELL 
   4 ?KKQ    [   
   4 ?L5B    [   
   4 ?LK2    [   
   9 ?M4L    TELLV CALL  5  VOPCLOS  
   4 ?MJ=    TELL
  10 ?RF=          JBS      XTQ,2,NULL   
   5 ?SY6    XEPENT  
  10 ?TCQ          FINDJOBQ 3,6,STSTATE  
   9 ?TXB          LDX   0  JMISC(3) 
  16 ?WC2          BNG   0  TJB1                [J IF BACK JOB   
  18 ?WWL          JBS      NINT,,HLSMFS        [J IF HLS IS FS-ING MOP  
   8 ?XB=          BRN      TJB2 
  18 ?XTW    TJB1  JBS      NINT,,HLSBFS        [J IF HLS IS FS-ING BACK 
  10 ?Y*G    TJB2  ACROSS   SCHDJOBA,2   
   5 ?YT6    STSTATE 
   6 ?_2# ...#UNS  ICT   
   4 ?_7G ...#SKI
  17 ?_#Q          JMBAC    NINT,2,XCP,XMP      [IF IF NOT CP OR MP  
   6 ?_FY ...#UNS  ICT   
  17 ?_M6 ...      JMBAC    NINT,2,XCP,XMP,XAP  [IF NOT CP,MP, OR AP 
  19 ?_SB          SHLSQ     6,3,NOTST           [ERROR IF JOB IS NOT STARTED
   4 #2#2    XK5 
  16 #2RL    NINT  JBC      XTQ,2,SCOM          [J IF EP COMMAND 
  10 #CFW          JBS      SNUM,2,NUM   
   9 #C_G          CALL  7  SNAMES   
   7 #DF6          OUTBLOCN 8
  12 #DYQ          OUTPARAM AWORK1(2),ACOMMUNE1  
  12 #FDB          OUTPARAM AWORK2(2),ACOMMUNE4  
  18 #FY2          LDX   0  Z2(1)               [LOAD MESSAGE IDENTIFIER 
   8 #GCL          BRN      X2   
   8 #GX=    SNUM  OUTNUM   6,0  
   8 #HBW          LDX   0  Z1(1)
   4 #HWG    X2  
  16 #JB6          MONOUTX  0                   [OUTPUT MESSAGE  
   6 #MXC ...#UNS  ICT   
   5 #N3# ...#SKIP   
  14 #NQQ    XTQ   JMBAC    THEND,2,XEP,XME,XST,XCP,XMP  
   6 #PTD ...#UNS  ICT   
  15 #Q2K ...XTQ   JMBAC    THEND,2,XEP,XME,XST,XCP,XMP,XAP  
  11 #T7B          MBC      2,XGB,XGR,XGQ,XGP
   7 #TM2          LDN   7  1
  11 #W6L          JBC      WELLMOP1,2,XBACK 
   7 #WL=          LDN   7  2
   5 #X5W    WELLMOP1
   7 #YJQ          LDN   5  0
  10 #_4B          HUNT     3,ADATA,ASUP 
  16 #_J2          BNG   3  NOM3                [J IF NO MP/CP   
  13 *23L          LDX   5  A1+1(3)             [CP  
   7 *2H=          SLL   5  6
   6 *2ND ...#UNS  ICT   
   4 *2PX ...(   
   9 *2RB ...      LDX   0  A1+2(3)  
  16 *2ST ...      SRL   0  12                  [SET AP IN B18,19
   7 *2W# ...      ORX   5  0
  14 *2XR ...[  SHIFTED INTO B6,7 OF HLSYONQA BY HLSEVENT
   4 *2_= ...)   
  10 *32W          JBC      NOM1,2,XMP   
   6 *384 ...#UNS  ICT   
   5 *3*= ...#SKIP   
   8 *3GG          ALTLENGD 3,1  
   6 *3LC ...#UNS  ICT   
   8 *3Q# ...      ALTLENGD 3,2  
   4 *3W9 ...#   
   8 *426          BRN      NOM3 
   7 *4FQ    NOM1  FREECORE 3
   4 *4_B    NOM3
  13 *9TL          HLSINFORM THEND,SCHEDULE,6,5,,7   
   4 *MH6    #   
   5 *N2Q    THEND   
  21 *NGB          ACROSS   SCHEDULE,2          [END OF COMMAND RETURN TO SCHEDULE   
   4 *P22    #   
   4 *PFL    XBRK
  19 *P_=          ACROSS   SCHEDULE,3          [BREAK-IN RETURN TO SCHEDULE 
   4 *QDW    [   
   4 *QYG    [   
   5 *RD6    VOPCLOS 
   8 *RXQ          SBX   5  FX1  
  12 *SCB          JMBAC    PEX,2,OPENJL,OPENUSER
   7 *SX2          CLOSETOP  
   8 *TBL    PEX   ADX   5  FX1  
  10 *TP3 ...VOP   JBCC    (5),2,POPPED  
   9 *W3D ...      VOP      ,JWACCESS
   7 *W*W          EXIT  5  0
   4 *WTG    [   
   4 *X*6    [   
   4 *XSQ    [   
   5 *Y#B    SNAMES  
  10 *YS2          HUNT     3,CPB,CUNI   
  15 *_?L          LDX   4  ACES                [LOAD SPACES 
  10 *_R=          STO   4  ACOMMUNE1(2) 
  10 B2=W          LDN   4  ACOMMUNE1(2) 
  17 B2QG          LDN   5  ACOMMUNE2(2)        [ SPACEFILL  ACOMMUNE
  14 B3=6          MOVE  4  5                   [ AREA   
  17 B3PQ          LDEX  0  ANUM(3)             [NO. CHARS IN JOBNAME
  16 B49B          LDN   4  APARA(3)            [PTR TO JOBNAME  
  10 B4P2          LDN   5  ACOMMUNE1(2) 
   7 B58L          SMO      0
  16 B5N=          MVCH  4  0                   [MOVE IN JOBNAME 
  19 B67W          STO   0  AWORK1(2)           [SAVE COUNT OF JOBNAME CHARS 
  20 B6MG          HUNT2    3,CPB,CUNI          [FIND /CUNI CONTAINING USERNAME  
   9 B776          LDEX  0  ANUM(3)  
   9 B7LQ          LDN   4  APARA(3) 
  10 B86B          LDN   5  ACOMMUNE4(2) 
   7 B8L2          SMO      0
  20 B95L          MVCH  4  0                   [MOVE USERNAME INTO ACOM. AREA   
   9 B9K=          STO   0  AWORK2(2)
   7 B=4W          EXIT  7  0
   4 B=JG    [   
   5 B?46    NOJOB   
   8 B?HQ          SBX   6  FX1  
  13 B#3B          SETNCORE SIZELIST,3,FILE,FABSNB   
   8 B#H2          LDN   5  A1(3)
  10 B*2L          LDN   4  XJOBLIST(1)  
   9 B*G=          MOVE  4  SIZELIST 
  11 BB_6          JBC      NOJOB1,2,OPENJL  
   7 BCDQ          STEPAGAIN 
   9 BCYB          LDN   4  JLUSER(3)
   9 BDD2          BRN      NOJOB2   
   5 BDXL    NOJOB1  
   9 BFWW          CALL  7  SNAMES   
  10 BGBG          LDN   4  ACOMMUNE4(2) 
   5 BGW6    NOJOB2  
  10 BH*Q          HUNT     3,FILE,FABSNB
   9 BHTB          LDN   5  A1+1(3)  
   7 BK#=          MOVE  4  3
  14 BL?G          USEROPEN XBRK,GENERAL,UNTRAP,FROZEN   
  10 BQ*N ...[ TEST FOR CLUDGE - B2032   
  10 BQGW ...      TESTREP  OK,OPENUSEROK
  18 BQN4 ...      TESTREP  CLUDGE,XNOJL      [ FILE ALREADY OPEN TO JOB 
   8 BQT= ...      BRN      NONX 
   6 BR2D ...OPENUSEROK  
  11 BR7Q          MBS      2,WELL,OPENUSER  
  11 BSLL          JBC      NOJOB3,2,OPENJL  
   8 BT6=          STEPAGAIN 1   
  10 BTKW          LDN   4  JLJOBNAME(3) 
   9 BW5G          BRN      NOJOB4   
   5 BWK6    NOJOB3  
   9 BXJB          CALL  7  SNAMES   
  10 BY42          LDN   4  ACOMMUNE1(2) 
   5 BYHL    NOJOB4  
   9 B_3=          GETJOB   4,USER   
   9 B_GW          TESTRPN2 OK,NONX  
   7 C22G          STEPAGAIN 
  10 C2G6          LDX   4  JLJOBNO(3)   
   8 C2_Q          ADX   6  FX1  
   7 C3FB          EXIT  6  0
   4 C3G3 ...[   
   4 C3GN ...[   
   5 C3H* ...XOPENJL 
  14 C3J2 ...[ USE SUPPLEMENTARY MODE 'QUERY' TO PREVENT 
  14 C3JM ...[ GEOERR IF FILE ALREADY OPEN TO JOB - B1511
   6 C3K# ...[ 04.03.82  
   8 C3K_ ...      SBX   7  FX1  
  15 C3LL ...      OPENSYS  (GEOERR),JOBLIST,GENERAL,QUERY   
   6 C3M? ...[ TEST REPLY
   9 C3MY ...      TESTREP  OK,OPENOK
   8 C3NK ...      BC  2,OPENJL  
  10 C3P= ...      TESTREP  CLUDGE,XNOJL 
  15 C3PX ...[ THE FOLLOWING 'TESTREP' CAN BE REMOVED WHEN   
  15 C3QJ ...[ 'OPENSYS' IS CORRECTED TO REPLY WITH 'CLUDGE' 
  16 C3R9 ...[ INSTEAD OF 'NAME' WHEN DIRECTORY IS OPEN TO JOB   
  10 C3RW ...      TESTREP  NAME,XNOJL   
  15 C3SH ...      GEOERR  1,JWNOJL      [ UNEXPECTED REPLY  
   5 C3T8 ...OPENOK  
   8 C3TT ...      ADX   7  FX1  
   7 C3WG ...      EXIT  7  0
   4 C3X7 ...[   
   4 C3XS ...[   
   5 C3_2    XNOHLS  
   8 C4DL          LDX   6  Y8(1)
   8 C4Y=          BRN      RCLOS
   4 C5CW    [   
   4 C5XG    [   
   5 C6C6    STARTDG 
   9 C6WQ          LDX   6  Y11(1)   
   8 C7BB          BRN      RCLOS
   4 C7W2    [   
   4 C8*L    [   
   5 C8T=    STNOW   
   9 C9#W          LDX   6  Y12(1)   
   8 C9SG          BRN      RCLOS
   4 C??B    [   
   4 C?R2    [   
   9 C#=L    XNODG LDX   6  Y13(1)   
   8 C#Q=          BRN      RCLOS
   5 C*PG    NOTST   
   8 CB96          LDX   1  FX1  
   9 CBNQ          LDX   6  Y16(1)   
   8 CC8B          BRN      RERR 
   4 CRC2    #   
   8 CRWL    NONX  LDX   6  Y1(1)
   8 CSB=          BRN      RCLOS
   4 CSTW    #   
   9 CT5N ...XNOJL LDX   6  Y21(1)   
   5 CT*G    RCLOS   
   9 CTT6          CALL  5  VOPCLOS  
   4 CW#Q    #   
   7 CWSB    RERR  NGN   7  1
   4 CX#2    RER2
  15 CXRL          COMERRX  6,7                 [REPORT ERROR
   4 CY?=    #   
   9 CYQW    Y1             +JXEXIST 
   9 C_=G    Y8             +EPHLSOFF
   9 C_Q6    Y11            +ASCDGER 
   9 D29Q    Y12            +ASCSTNOW
   9 D392    Y13            +ASCNODG 
  10 D3NL    Y16            +ENOTSTART   
   9 D7*S ...Y21            +JSYSFILE
   4 D7KL    #   
  14 D85=    #     THE FOLLOWING ARE MESSAGE IDENTIFIERS 
   4 D8JW    #   
   7 D94G    Z1    +ASCJOBNO 
   7 D9J6    Z2    +ASCJOBNA 
   4 D=3Q    #END
   6 ____ ...736475010001
