  12 22FL ...      SEG      IPCON,,KEITH MARTIN  
  19 22_=    #     THIS SEGMENT HANDLES TYPE 28 IPBS IN ORDER TO IMPLEMENT   
  15 23DW    #     THE INTER-PROGRAM COMMUNICATION FACILITY. 
   4 23YG    #   
  11 24D6          SEGENTRY K1IPCON,X1IPCON  
  11 24XQ          SEGENTRY K3IPCON,X3IPCON  
   8 25CB    #     STORED   WORDS
   9 25X2    RESBITS        #777700  
   9 26BL    TMESSCODES     #04000040
   9 26W=                   #04000041
   9 27*W                   #04000042
   9 27TG                   #04000043
   9 28*6                   #04000046
   9 28SQ                   #03000047
   9 29#B                   #07000060
   9 29S2                   #07000061
   9 2=?L                   #04000066
   9 2=R=                   #03000067
   9 2?=W                   #02000070
   9 2?QG                   #02000071
   9 2#=6                   #03000077
   9 2#PQ    TBIT1          #20000000
   9 2*9B    TBIT2          #10000000
   9 2*P2    TBIT3          #4000000 
   9 2B8L    TBIT4          #2000000 
   9 2BN=    TBIT5          #1000000 
   9 2C7W    TBIT7          #00200000
   9 2CMG    TBIT11         #10000   
  16 2D76    TTLIM          +8                  [MAX TASK NO + 1 
  17 2DLQ    TIPBACT        +IPCA+IPCBX         [TYPE OF IPB ACTIVITY
   9 2F6B    RNF            #02200000
   8 2FL2    ROK            #0   
   9 2G5L    RTO            #22000000
   9 2GK=    RNI            #02000000
   9 2H4W    RNINE          #02400000
   9 2HJG    OBJW  #40000*IPCG4+#11  
   9 2J46    OBJR  #40000*IPCG4+#12  
  16 2JHQ    #     SUBROUTINE USED TO SET OBJECT PROGRAM REPLIES 
   4 2K3B    #   
   4 2KH2    SREP
   8 2L2L          CALL  7  SREL 
   9 2LG=          STO   4  ACC4(2)  
   9 2L_W          STO   5  ACC5(2)  
  18 2MFG          LDX   5  AREPPTR(3)          [ADDRESS OF REPLY WORD   
   5 2M_6    #SKI  G3
   4 2NDQ    (   
   9 2NYB          SMO      FPTR(3)  
  11 2PD2          LDX   3  APETADDR-ARINGEL 
   4 2PXL    )   
   5 2QC=    #SKI  G4
   4 2QWW    (   
   9 2RBG          LDX   3  FPTR(3)  
   9 2RW6          SBN   3  ARINGEL  
   4 2S*Q    )   
  10 2STB          LDN   4  ACOMMUNE2(2) 
   7 2T*2          LDN   7  1
   6 2TSL    #SKI  IPCG4 
  11 2W#=          HUNT2 2,ADATA,HLOCKDOWN,3 
  12 2WRW          FINMOVE  3,5,4,7,HLOCKB1,2,1  
   8 2X?G          LDX   2  FX2  
   9 2XR6          LDX   4  ACC4(2)  
   9 2Y=Q          LDX   5  ACC5(2)  
   8 2YQB          CALL  7  SREL 
   7 2_=2          NGNC  0  8
  21 2_PL          ANDS  0  ASTATUS1(3)         [CALL TR STORED & IN PROGRESS BITS   
   9 329=          STOZ     ATIMCT(3)
   9 32NW          STOZ     ASTR(3)  
   9 338G          STOZ     ALENTR(3)
  10 33N6          STOZ     AREPPTR(3)   
   9 347Q          LDX   0  ASTMEL(3)
   9 34MB          STO   0  AWORK4(2)
   9 3572          STOZ     ASTMEL(3)
   7 35LL          NGN   0  2
  10 366=          ANDS  0  ASTATUS2(3)  
   9 36KW          LDX   3  FPTR(3)  
   9 375G          SBN   3  ARINGEL  
   6 37K6    #SKI  IPCG4 
   4 384Q    (   
  12 38JB          HUNT2    2,ADATA,HLOCKDOWN,3  
  13 3942          CLEAR    APETADDR(3),0,HLOCKB1,2,2
   8 39HL          LDX   2  FX2  
   4 3=3=    )   
   7 3=GW          CCBB     3
   9 3=P6 ...      STO   5  ACC5(2)  
   9 3=XB ...      LDX   5  AWORK4(2)
  10 3?5L ...      TICKLE   JOBNO(3),5   
   9 3??W ...      LDX   5  ACC5(2)  
  10 3?G6          MFREE    IPCD,ATASK   
   7 3?_Q          EXIT  6  0
  15 3#FB    #     SUBROUTINE TO LOCATE LINKS RING ELEMENT   
  19 3#_2    #     GIVEN THE LINK IDENTIFIER IN ACOMMUNE1(2) [X2 MUST = FX2] 
  17 3*DL    #     ADDRESS IS LEFT IN X3. LINK IS X7. ON EXIT X1=FX1 
   4 3*Y=    #   
   4 3BCW    SREL
  17 3BXG          LDX 3 IPCPTR(2)              [FIND IPC ACTIVITY   
  16 3CC6          HUNT2    3,IPCD,IPSTACK      [FIND STACK BLOCK
  10 3CWQ          LDX   1  ARINGNO(3)   
   8 3DBB          ANDN  1  #7777
  15 3DW2          BZE   1  SFR1                [STACK EMPTY 
   8 3F*L          ADN   3  A1   
   9 3FT=    SFR3  LDX   0  ALINUM(3)
  11 3G#W          BXE   0  ACOMMUNE1(2),SFR2
   9 3GSG          ADN   3  ARECLEN  
   8 3H#6          BCT   1  SFR3 
   8 3HRQ    SFR1  NGN   3  #7777
   8 3J?B    SFR2  LDX   1  FX1  
   7 3JR2          EXIT  7  0
  16 3K=L    #     S/R TO LOCATE ACTIVITY OWNING A GEORGE LINK   
  16 3KQ=    SLACT LDX   2  FPTR(3)             [CONTROL BLOCK   
   8 3L9W          SBN   2  A1   
   5 3LPG    SLACT1  
   9 3M96          LDX   2  BPTR(2)  
   9 3MNQ          LDX   0  ATYPE(2) 
  10 3N8B          BXL   0  CACT,SLACT1  
   7 3NN2          EXIT  7  0
  14 3P7L    #     S/R TO SET REPLY ETC ON GEORGE LINK   
   9 3PM=    SGCLR LDX   2  FPTR(3)  
  15 3Q6W          STO   6  IPCGREP-A1(2)       [STORE REPLY 
   8 3QLG          LDX   0  GSIGN
  15 3R66          ERS   0  ASTATUS1(3)         [UNSET T.I.P.
   9 3RKQ          STOZ     ATIMCT(3)
   9 3S5B          STOZ     ASTR(3)  
   9 3SK2          STOZ     ALENTR(3)
   9 3TJ=          STOZ     ASTMEL(3)
   7 3W3W          LDX   1  7
   8 3WHG          CALL  7  SLACT
   7 3X36          LDX   7  1
  17 3XGQ          DESUSPEND                    [DESUSPEND ACTIVITY  
  16 3Y2B          MFREE    IPCD,ATASK          [FREE TASK BLOCK 
   7 3YG2          EXIT  7  0
  18 3Y_L    #     THIS PART OF THE SEGMENT IS THE POINT TO WHICH CONTROL
  18 3_F=    #     IS RETURNED WHEN A MESSAGE SEQUENCE OR OTHER ACTION IS
   7 3_YW    #     COMPLETED.
   5 42DG    PCNXT   
  10 42Y6          MFREE    IPCD,ATASK   
   5 43CQ    X1IPCON 
   5 43XB    NEXTASK 
   8 443? ...      FSHCODE  AORB 
  15 4478 ...      TRACEIF  K6IPCON,199,299,IPREB(2),IPCON1  
   7 44?5 ...      FSHSKIP   
  11 44C2          TRACE    IPREB(2),IPCON1  
  16 44WL          JDISENG  ,NOP                [J IF INOPERABLE 
  15 45B=          JRESET   ,RESETA             [J IF RESET  
   8 45TW    #     ANY TASKS ?   
  17 46*G          HUNTW    3,IPCD,ATASK        [LOOK FOR TASK BLOCK 
  19 46T6          BPZ   3  TASK                [IF FOUND,GO TO PROCESS TASK 
  17 47#Q          ACROSS   IPCONA,1            [IF NONE,GO QUIESCENT
  10 47SB    #     SWITCH ON TASK TYPE   
   9 48#2    TASK  LDX   0  A1+1(3)  
   8 48RL          BZE   0  TA1  
   9 49?=          TXL   0  TTLIM(1) 
   8 49QW          BCS      TA2  
   9 4==G    TA1   GEOERR   1,IPCON1 
   7 4=Q6    TA2   SMO      0
   8 4?9Q          BRN      TA2+1
   8 4?PB          BRN      OPEN 
   8 4#92          BRN      TRAN 
   8 4#NL          BRN      TCLOS
   8 4*8=          BRN      TRAN 
   8 4*MW          BRN      OPGEO
   8 4B7G          BRN      TFRG 
   8 4BM6          BRN      TCLOS
  15 4C6Q    #     THIS SECTION DEALS WITH OPEN REQUEST TASKS
   9 4CLB    OPEN  ACROSS   IPCONB,1 
  14 4D62    #     THIS SECTION DEALS WITH TRANSFER TASKS
   4 4DKL    #   
   4 4F5=    TRAN
   8 4FJW          LDX   0  A1(3)
  10 4G4G          STO   0  ACOMMUNE1(2) 
   9 4GJ6          TRACE    0,IPCON3 
   9 4H3Q          LDX   0  A1+1(3)  
  10 4HHB          STO   0  ACOMMUNE2(2) 
   8 4J32          CALL  7  SREL 
   8 4JGL ...      BNG   3  PCNXT
  10 4K2= ...      LDX   0  ASTATUS1(3)  
   7 4KFW ...      ANDN  0  1
   8 4K_G ...      BZE   0  PCNXT
   8 4LF6          LDX   0  GSIGN
  18 4LYQ          ORS   0  ASTATUS1(3)         [SET TRANSFER IN PROGRESS
  13 4MDB    #     SET UP TRANSFER REQUEST MESSAGE   
  16 4MY2          LDX   2  FPTR(3)             [ADDRESS OF AOLPT
  15 4NCL          LDX   0  ALINDIR-ARINGEL(2)  [OPENING MODE
  16 4NX=          SRL   0  8                   [0=READ,1=WRITE  
   8 4PBW          SMO      FX2  
   9 4PWG          LDX   5  ACOMMUNE2
  20 4QB6          SBN   5  2                   [0=UNCONDITIONAL,2=CONDITIONAL   
   7 4QTQ          ADX   1  0
   7 4R*B          ADX   1  5
  14 4RT2          LDX   0  TMESSCODES(1)       [HEADER  
   8 4S#L          LDX   1  FX1  
   8 4SS=          LDX   2  FX2  
  15 4T?W          STO   0  IPWRB(2)            [STORE HEADER
  10 4TRG          LDX   0  ACOMMUNE1(2) 
  18 4W?6          STO   0  IPWRB+1(2)          [STORE LINK IDENTIFIER   
   9 4WQQ          LDX   0  ALENTR(3)
  18 4X=B          STO   0  IPWRB+2(2)          [STORE LENGTH OF TRANSFER
   9 4XQ2          LDN   7  IPWRB(2) 
   7 4Y9L          SUM   6  3
  16 4YP=          NGS   6  IPWRB+3(2)          [STORE CHECKSUM  
   9 4_8W    #     SEND  MESSAGE :-  
  10 4_NG    #     SET UP CONTROL AREA   
   8 5286          LDN   0  #31  
  10 52MQ          DCA      ,DLA,0,MODE  
   7 537B          LDN   0  4
  10 53M2          DCA      ,DLA,0,COUNT 
   9 546L          LDN   0  IPWRB(2) 
  10 54L=          DCA      ,STO,0,CTSA  
   8 555W          LDN   0  #32  
  10 55KG          DSCA     ,DLA,0,MODE  
   8 5656          LDN   4  11   
  10 56JQ          DSCA     ,DLA,4,COUNT 
   9 574B          LDN   0  IPREB(2) 
  10 57J2          DSCA     ,STO,0,CTSA  
   7 583L    #     ISSUE PERI
   8 58H=          GPERIIPC ,2,  
  16 592W ...      TIPCREP  ,TE1,TE2,TE3,NPUT,WHOOPS,RESETA,NOP  
   8 59GG    #     CHECKSUM REPLY
   9 5=26          LDX   5  CPRW2(2) 
   8 5=FQ          ANDN  5  #777 
   7 5=_B          SBX   4  5
   9 5?F2          LDN   7  IPREB(2) 
   7 5?YL          SMO      4
  17 5#D=          SUM   6  0                   [CALCULATE CHECKSUM  
  14 5#XW          BNZ   6  TE4                 [WRONG   
  12 5*CG    #     FURTHER VALIDATION OF REPLY   
   9 5*X6          LDCH  0  IPREB(2) 
  15 5BBQ          BXU   0  4,TE4               [COUNT WRONG 
  10 5BWB          LDX   0  IPREB+1(2)   
   8 5CB2          SRC   0  12   
  10 5CTL          ERX   0  IPWRB+1(2)   
   8 5D*=          ANDN  0  #7777
  17 5DSW          BNZ   0  TE4                 [NOT FOR SAME LINK   
   9 5F#G          LDX   0  IPREB(2) 
   8 5FS6          ANDN  0  #77  
   8 5G?Q          SBN   0  #46  
  17 5GRB          BZE   0  OBJPERI             [DO ACTUAL TRANSFER  
   7 5H?2          SBN   0  1
  15 5HQL          BZE   0  WAIT                [NONE READY  
   8 5J==          SBN   0  #20  
  13 5JPW          BNZ   0  TE4                 [EH? 
  11 5K9G    #     DEAL WITH 'NO LINK' REPLY 
  16 5KP6          CALL  7  SREL                [RELOCATE RING EL
   9 5L8Q          LDX   0  TBIT1(1) 
  17 5LNB          ORS   0  ASTATUS2(3)         [MARK NOT ESTABLISHED
  10 5M82          STOZ     APTRIPB(3)   
   9 5MML          LDX   0  TBIT3(1) 
  17 5N7=          ORS   0  ASTATUS2(3)         [MARK 'NE REPLY SET' 
   9 5NLW          LDX   0  RNINE(1) 
  10 5P6G          STO   0  ACOMMUNE2(2) 
   8 5PL6          LDN   0  #7777
  10 5Q5Q          ANDS  0  ACOMMUNE1(2) 
   9 5QKB          ANDS  0  ALINUM(3)
  15 5R52          CALL  6  SREP                [SET REPLY   
   7 5RJL          DECLINK   
   9 5S4=          BRN      NEXTASK  
   9 5SHW    #     NO TRANSFER REPLY 
   8 5T3G    WAIT  CALL  7  SREL 
  10 5TH6          LDX   0  ACOMMUNE2(2) 
   7 5W2Q          SBN   0  4
  19 5WGB          BZE   0  WAIT1               [J IF CONDITIONAL TRANSFER   
   9 5X22          LDX   0  ATIMCT(3)
  18 5XFL          BNZ   0  WAIT2               [J TO WAIT IF TIME NOT UP
   9 5X_=    WAIT1 LDX   6  RTO(1)   
  11 5YDW          JDISENG  CPPTR(2),WAIT1A  
   9 5YYG          ORX   6  TBIT5(1) 
   5 5_D6    WAIT1A  
  10 5_XQ          STO   6  ACOMMUNE2(2) 
  16 62CB          CALL  6  SREP                [SET REPLY ETC   
   9 62X2          BRN      NEXTASK  
   7 63BL    WAIT2 NGNC  0  1
  19 63W=          ANDS  0  ASTATUS1(3)         [UNSET TIP & LEAVE WAITING   
   7 64*W          NGN   0  2
  10 64TG          ANDS  0  ASTATUS2(3)  
   8 65*6          BRN      PCNXT
   9 65SQ    #     DO ACTUAL TRANSFER
   5 66#B    OBJPERI 
  11 66S2          TRACE    IPREB+1(2),IPCON4
  10 67?L          LDX   0  IPREB+2(2)   
  17 67R=          STO   0  AWORK3(2)           [DIFFERENCE IN COUNTS
   5 68=W    X3IPCON 
  16 68QG          CALL  7  SREL                [RELOCATE RING EL
   9 69=6          SMO      FPTR(3)  
  11 69PQ          LDX   5  ALINDIR-ARINGEL  
   8 6=9B          SBN   5  #200 
  16 6=P2          BZE   5  OBJREAD             [READ REQUIRED   
   7 6?8L    #     DO WRITE  
   9 6?N=          LDX   0  OBJW(1)  
  10 6#7W          DCA      ,DLA,0,MODE  
   9 6#MG          LDX   0  AWORK3(2)
   8 6*76          BPZ   0  OB1  
   9 6*LQ          ADX   0  ALENTR(3)
   8 6B6B          BZE   0  TE4  
   8 6BL2          BRN      OB2  
   9 6C5L    OB1   LDX   0  ALENTR(3)
   4 6CK=    OB2 
  10 6D4W          DCA      ,DLA,0,COUNT 
   9 6DJG          LDX   7  ASTR(3)  
   9 6F46          LDX   1  FPTR(3)  
   9 6FHQ          SBN   1  ARINGEL  
  11 6G3B          FINDACTFB 1,,APET,,,BPTR  
   6 6GH2    #SKI  IPCG4 
   4 6H2L    (   
  10 6HG=          LDX   0  ASEGTAB(1)   
  10 6H_W          DCA      ,STO,0,SEGTAB
   4 6JFG    )   
   7 6J_6    #SKI  IPCG4<1$1 
  16 6KDQ          FADDRESS 1,7                 [ABSOLUTE ADDRESS
  10 6KYB          DCA      ,STO,7,CTSA  
   8 6LD2          GPERIIPC ,2,CT
  12 6LXL          TIPCREP  CT,TE1,TE2,TE3,TE4   
  14 6M3H ...      LDX   0  CPRW1(2)         [REPLY WORD 
  17 6M7D ...      ANDN  0  #7777                [LOOK FOR REMAINDER 
  17 6M?* ...      BNZ   0  TE4                  [MAJOR ERROR IF SEEN
   8 6MC=          BRN      OBJP1
   7 6MWW    #     DO READ   
   5 6NBG    OBJREAD 
   9 6NW6          LDX   0  OBJR(1)  
  10 6P*Q          DSCA     ,DLA,0,MODE  
   9 6PTB          LDX   0  AWORK3(2)
   8 6Q*2          BPZ   0  OB3  
   9 6QSL          LDX   0  ALENTR(3)
   8 6R#=          BRN      OB4  
   9 6RRW    OB3   LDX   0  ALENTR(3)
   9 6S?G          SBX   0  AWORK3(2)
   8 6SR6          BZE   0  TE4  
   4 6T=Q    OB4 
  10 6TQB          DSCA     ,DLA,0,COUNT 
   9 6W=2          LDX   7  ASTR(3)  
   9 6WPL          LDX   1  FPTR(3)  
   9 6X9=          SBN   1  ARINGEL  
  11 6XNW          FINDACTFB 1,,APET,,,BPTR  
   6 6Y8G    #SKI  IPCG4 
   4 6YN6    (   
  10 6_7Q          LDX   0  ASEGTAB(1)   
  10 6_MB          DSCA     ,STO,0,SEGTAB
   4 7272    )   
   7 72LL    #SKI  IPCG4<1$1 
  16 736=          FADDRESS 1,7                 [ABSOLUTE ADDRESS
  10 73KW          DSCA     ,STO,7,CTSA  
   8 745G          LDX   1  FX1  
   8 74K6          GPERIIPC ,2,SP
  12 754Q ...      TIPCREP  SP,TE1,TE2,TE3,,TE4  
  10 75JB    #     SET REPLY TO OBJ PROG 
   9 7642    OBJP1 LDX   0  ROK(1)   
   9 76HL          JDISENG  ,OBJP2   
   9 773=          LDX   0  TBIT5(1) 
   9 77GW    OBJP2 LDX   6  AWORK3(2)
   8 782G          BNG   6  OBJP5
   8 78G6          BZE   5  OBJP6
   8 78_Q          BRN      OBJP4
   9 79FB    OBJP5 ORX   0  TBIT2(1) 
   8 79_2          BZE   5  OBJP4
   7 7=DL          NGX   6  6
   5 7=Y=    OBJP6   
  16 7?CW          ORX   0  6                   [REMAINDER COUNT 
  10 7?XG    OBJP4 STO   0  ACOMMUNE2(2) 
  15 7#C6          CALL  6  SREP                [SET REPLY   
   9 7#WQ          BRN      NEXTASK  
  15 7*BB    OBJP3 ANDX  0  TBIT2(1)            [IONG BLOCK  
   8 7*W2          BRN      OBJP4
  12 7B*L    #     DEAL WITH TRANSFER ERROR CASES
   9 7BT=    TE1   LDX   6  RNF(1)   
   8 7C#W          JDISENG  ,TE1A
   9 7CSG          ORX   6  TBIT5(1) 
  10 7D#6    TE1A  STO   6  ACOMMUNE2(2) 
   8 7DRQ          CALL  6  SREP 
   8 7F?B          BRN      TIME 
   9 7FR2    TE2   LDX   6  RNF(1)   
   7 7G=L          ORN   6  1
   8 7GQ=          JDISENG  ,TE2A
   9 7H9W          ORX   6  TBIT5(1) 
  10 7HPG    TE2A  STO   6  ACOMMUNE2(2) 
   8 7J96          CALL  6  SREP 
   9 7JNQ          BRN      RESETA   
   9 7K8B    TE3   LDX   6  RNF(1)   
   7 7KN2          ORN   6  3
  10 7L7L          STO   6  ACOMMUNE2(2) 
   8 7LM=          CALL  6  SREP 
   8 7M6W          BRN      NOPS 
   9 7MLG    TE4   LDX   6  RNF(1)   
   7 7N66          ORN   6  2
   8 7NKQ          JDISENG  ,TE4A
   9 7P5B          ORX   6  TBIT5(1) 
  10 7PK2    TE4A  STO   6  ACOMMUNE2(2) 
   8 7Q4L          CALL  6  SREP 
   9 7QJ=          BRN      WHOOPS   
   4 7R3W    #   
  19 7YXB    #     CLOSE LINK TASK : THE RING ELEMENT ETC WILL HAVE ALREADY  
  19 7_C2    #     BEEN FREED. THE ACTION REQUIRED IS SIMPLY TO INFORM THE   
   7 7_WL    #     OTHER END.
  19 82B=    #     THIS TYPE OF TASK IS USED ALSO WHEN AN OPEN LINK ATTEMPT  
  13 82TW    #     IS ANTICIPATED BY ANOTHER SOURCE. 
   9 83*G    TCLOS ACROSS   IPCONC,1 
  13 83T6    #     OPEN  TASKS FOR GEORGE ACTIVITIES 
   4 84#Q    #   
   5 84SB    OPGEO   
   9 85#2          ACROSS   IPCONB,4 
  14 85RL    #     TRANSFER TASKS FOR GEORGE ACTIVITIES  
   4 86?=    #   
   4 86QW    #   
   9 87=G    TFRG  ACROSS   IPCONG,1 
   9 87Q6    TFREP          #3200000 
   9 889Q    TIREP          #2000000 
  11 88PB    NPUT           ACROSS  IPCONA,2 
   8 8992    NOPS  SIPCM    SI   
  18 89NL    NOP   ACROSS   IPCONE,2            [DEAL WITH INOPERABLE IPB
   5 8=8=    WHOOPS  
  16 8=MW          ACROSS   IPCONE,1            [DEAL WITH ERROR 
   5 8?7G    RESETA  
  18 8?M6          ACROSS   IPCONE,3            [DEAL WITH ERROR DETECTED
   4 8#6Q    TIME
   9 8#LB          ACROSS   IPCONE,5 
   4 8*62    #   
   4 8*KL    #   
  10 8B5=          MENDAREA 100,K9IPCON  
   4 8BJW    #   
   4 8C4G    #   
   4 8CJ6    #END
   8 ____ ...45570403000400000000
