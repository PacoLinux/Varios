  16 22FL    #SEG  CMEMILL                     [IVY BRUNT,CENT   
   8 22_=    #OPT  K0CMEMILL=0   
  12 23DW    #LIS  K0CMEMILL>K0COMMAND>K0ALLGEO  
   7 23YG          8HCMEMILL 
   4 24D6    #   
   4 24XQ    #   
  17 25CB    #     THIS SEGMENT IMPLEMENTS THE MILL COMMAND FOR CME. 
   4 25X2    #   
  10 26BL    #         COMMAND FORMATS:  
   9 26W=    #         1  MILL  NN   
  12 27*W    #         2  MILL  NN,I,NN,I .....  
   8 27TG    #         3  MILL   
  10 28*6    #     WHERE NN = PERCENTAGE 
  11 28SQ    #       AND I    = OCP NUMBER   
   4 29#B    #   
  15 29S2    #     FORMATS 1 AND 2 SET THE MILL PERCENTAGE   
  11 2=?L    #     FORMAT 3 INTERROGATES IT. 
   4 2=R=    #   
  21 2?=W    #     THE SEGMENT IS ALSO ENTERED IN EMS IN ORDER TO INTERROGATE THE MIL
   4 2?QG    #   
  14 2#=6    #           X4 IS USED TO CONTAIN PERCENTAGE
  14 2#PQ ...#           AWORK2                OCP NUMBER
  18 2*9B ...#           AWORK1 IS POSITIVE IF COMMAND ENRTY, ZERO IF EMS
   2 2*P2
   4 2B8L    #   
   4 2BN=    #   
  16 2C7W          SEGENTRY K1CMEMILL,XENT1     [COMMAND ENTRY   
  15 2CMG          SEGENTRY K2CMEMILL,XENT2     [ENTRY IN EMS
   4 2D76    #   
   8 2DLQ    XDUALS         +2   
   8 2F6B    XLOW           +1   
   8 2FL2    XHIGH          +100 
   8 2FYC ...XOCPLOW        +0   
   8 2G=S ...XOCPHIGH       +64  
   4 2GK=    #   
   4 2H4W    #   
  21 2HJG    #     THIS SUBROUTINE OBTAINS AND VETS PERCENTAGE PARA & OCP NO. PARA.  
  10 2J46    #     IT EXITS AS FOLLOWS   
  17 2JHQ    #                 EXIT  6  0  -  PARAMETERS NON-EXISTENT
  18 2K3B    #                           1  -  OCP NUMBER NON-EXISTENT   
  18 2KH2    #                           2  -  BOTH PRESENT AND CORRECT, 
  10 2L2L    #              IN WHICH CASE
  16 2LG=    #                          X4 CONTAINS PERCENTAGE   
  16 2L_W ...#                      AWORK2          OCP NUMBER   
  21 2MFG    #     IF AN ERROR IS FOUND, THE ERROR ROUTINE IS ENTERED (RETURNS TO CP)
   4 2M_6    #   
   5 2NDQ    PARAVET 
  15 2NNJ ...      SBX   6  FX1                   [DATUMIZE  
  16 2NYB          PARAPASS                     [GET PERCENTAGE  
  10 2PD2          MHUNT    3,CPB,CUNI   
   9 2PXL          LDX   4  ANUM(3)  
  16 2QC=          BNG   4  PEXIT0              [J IF NO PARAS   
  15 2QWW          BZE   4  NLPERR              [J IF NULL   
   7 2RBG          CHNUMCOD 1
  11 2RL# ...      TESTREP2 CHNUMERR,PERERR  
  10 2RW6          LDX   4  ACOMMUNE1(2) 
  19 2S*Q          BXL   4  XLOW(1),PERERR      [CHECK PERCENT WITHIN RANGE  
  11 2STB ...      BXGE  4  XHIGH(1),PERERR  
   9 2T58 ...      MFREE    CPB,CUNI 
  15 2T*2          PARAPASS                     [GET OCP NO. 
  10 2TSL          MHUNT    3,CPB,CUNI   
   9 2W#=          LDX   5  ANUM(3)  
  16 2WRW          BNG   5  PEXIT1              [J IF NO OCP NO. 
  16 2X?G          BZE   5  NLOCPERR            [J IF OCP NULL   
   7 2XLM ...      CHNUMCOD 1
  11 2XS9 ...      TESTREP2 CHNUMERR,OCPERR  
   4 2XT2 ...#   
  17 2XTR ...#     PRESENCE OF OCP NUMBER CURRENTLY INVALID. SEE ALSO
  11 2XWJ ...#     COMMENT ABOVE LABEL RATIO 
   4 2XX* ...#   
   9 2XY6 ...      BRN      OCPERR   
   4 2XYX ...#   
  10 2X_S ...      LDX   5  ACOMMUNE1(2) 
  11 2YN6 ...      BXL   5  XOCPLOW(1),OCPERR
  12 2_3? ...      BXGE   5  XOCPHIGH(1),OCPERR  
   9 2_BD ...      STO   5  AWORK2(2)
   9 2_J3 ...      MFREE    CPB,CUNI 
   8 2_LS ...      ADX   6  FX1  
   7 2_PL          EXIT  6  2
   5 329=    PEXIT0  
   8 32F4 ...      ADX   6  FX1  
   7 32NW          EXIT  6  0
   5 338G    PEXIT1  
   8 33D# ...      ADX   6  FX1  
   7 33N6          EXIT  6  1
   4 33N? ...#   
  20 33ND ...#     THIS SUBROUTINE ISSUES THE MILL MACRO & STORES REPLY IN X4.   
   4 33NK ...#   
  15 33NQ ...#     ENTERED AT LABEL FOR FUNCTION REQUIRED.   
   7 33NX ...#    LINK IN X7.
   4 33P4 ...#   
   5 33P9 ...SETQALL 
  15 33PB ...      LDN   4  0                  [INTERROGATE  
   5 33PH ...SETALL  
  19 33PN ...      STOZ      AWORK2(2)         [PERCENTAGE TO BE SET OVERALL 
   7 33PT ...      LDN   6  0
   5 33Q2 ...SETOCPM 
  10 33Q7 ...      MILL     4,AWORK2(2),6
   7 33Q# ...      EXIT  7  0
   4 33QF ...#   
  17 33QL ...#     THIS SUBROUTINE OUTPUTS MESSAGE TO SYSTEM JOURNAL 
  12 33QR ...#     (AND OPS IF COMMAND ENTRY).   
  18 33QY ...#     LABEL ENTERED AT DEPENDS ON WETHER OCP TO BE INCLUDED 
  14 33R5 ...#     IN MESSAGE.             LINK IN X6.   
   4 33R= ...#   
   5 33RC ...REPORTO 
   8 33RF ...      SBX   5  FX1  
  16 33RJ ...      OUTPACK  AWORK2(2),1,NUMB    [INCLUDE OCP NO. 
   8 33RP ...      BRN      REP1 
   5 33RW ...REPORTN 
   8 33RY ...      SBX   5  FX1  
  15 33S3 ...      OUTPACK  AWORK2(2),1,SKIP    [NO OCP NO.  
   4 33S8 ...REP1
   9 33S* ...      LDX   0  AWORK1(2)
  16 33SG ...      BZE   0  RNOT1               [J IF NOT COMMAND
  15 33SM ...      INFORM   1,CMERATIO,1        [SEND MESSAGE
   9 33SS ...      BRN      REXIT1   
   5 33S_ ...RNOT1   
  15 33T6 ...      MONOUT   CMERATIO            [SEND MESSAGE
   5 33T? ...REXIT1  
   8 33T* ...      ADX   5  FX1  
   7 33TD ...      EXIT  5  0
   4 33TK ...#   
  20 33TQ ...#     THIS SUBROUTINE OUTPUTS OVERALL MILL MESSAGE TO SYSTEM JOURNAL
  21 33TX ...#     (AND OPS IF COMMAND ENTRY). STORES MILL PERCENTAGE IN FIXED CORE. 
   8 33W4 ...#     LINK IN X5.   
   4 33W9 ...#   
   5 33WB ...REPALL  
   8 33WD ...      SBX   5  FX1  
  17 33WH ...      OUTPACK  4,1,NUMA            [INCLUDE PERCENTAGE  
   9 33WN ...      LDX   0  AWORK1(2)
   8 33WT ...      BZE   0  RNOT2
  15 33X2 ...      INFORM   1,CMEALLRATIO       [SEND MESSAGE
   8 33X7 ...      BRN      RST2 
   5 33X# ...RNOT2   
  10 33XF ...      MONOUT   CMEALLRATIO  
   4 33XL ...RST2
  16 33XR ...      STO   4  CMEMILLALL          [STORE IN FIXED  
   8 33XT ...      ADX   5  FX1  
   7 33XY ...      EXIT  5  0
   4 33Y5 ...#   
  21 33Y= ...#     THIS SUBROUTINE EXPECTS PERCENTAGE IN X4 AND OCP NO. IN AWORK2. IT
  20 33YC ...#     REPORTS THESE AND THEN FINDS THE RIGHT CMEMILL WORD IN FIXED  
  16 33YJ ...#     CORE AND STORES THEM.            LINK IN X7.  
  16 33YP ...#     NB. THIS S/R IS CALLED FROM OCPSMILL MACRO.   
   4 33YW ...#   
   5 33_3 ...OCPSSR  
  20 33_8 ...#                                   DEAL WITH REPLY FOR EACH OCP.   
   4 33_* ...#   
   8 33_C ...      SBX   7  FX1  
   9 33_G ...      OUTPACK  4,1,NUMA 
  14 33_M ...      CALL  5  REPORTO             [REPORT  
   4 33_S ...#   
  21 33__ ...#     NOW FIND WORD IN CMEMILL LIST IN FIXED WITH MATCHING OCP NUMBER   
  20 3426 ...#     IN B0-8 AND STORE NEW PERCENTAGE IN B9-23. IF WHOLE WORD IS   
  13 342? ...#     ZERO, STORE OCP NUMBER AS WELL.   
   4 342D ...#   
   9 342G ...      BZE   4  STORENOT 
   7 342K ...      LDN   3  0
   5 342Q ...STORE1  
  10 342X ...      LDX   0  CMEMILL(3)   
  15 3434 ...      BZE   0  STORE2              [J IF EMPTY  
   8 3439 ...      SRL   0  15   
   9 343B ...      SBX   0  AWORK2(2)
  15 343H ...      BZE   0  STORE3              [J IF MATCH  
   7 343N ...      ADN   3  1
   9 343T ...      LDN   0  CMAXPROC 
  20 3442 ...      BXL   0  3,STORENOT          [J IF PAST MAX NO. ACTIVE OCPS   
   9 3447 ...      BRN      STORE1   
   5 344# ...STORE2  
   9 344F ...      LDX   0  AWORK2(2)
   8 344L ...      SLL   0  15   
  15 344R ...      STO   0  CMEMILL(3)          [STORE OCP   
   5 344Y ...STORE3  
  16 3455 ...      DLA   4  CMEMILL(3)          [STORE PERCENTAGE
   5 3457 ...STORENOT
   8 3458 ...      ADX   7  FX1  
   7 345= ...      EXIT  7  0
   4 345P ...#   
   5 347Q    XENT1   
  19 34HF ...      LDN   0  1                   [REMEMBER ENTRY FROM COMMAND 
   9 34X8 ...      STO   0  AWORK1(2)
  10 35=X ...      JENVNOT  NOTCME,CME   
  21 35LL          CALL  6  PARAVET             [GET & VET 1ST PERCENT,& OCP IF THERE
  20 366=          BRN      QUERY               [J IF NO PARAS - IS INTERROGATORY
  18 36KW ...      BRN      ZEROCP              [J IF NO OCP SPECIFIED   
   4 36P2 ...#   
  16 36S6 ...#     PRESENCE OF OCP NUMBER CURRENTLY INVALID. THIS
  15 36X= ...#     DETECTED IN PARAVET SR SO NO RETURN MADE. 
   4 372B ...#   
   4 375G ...#   
  18 37K6 ...#                        COMMAND WITH PERCENTAGE AND OCP NO.
   5 384Q ...RATIO   
   7 38JB ...      LDN   6  1
  16 3942 ...      CALL  7  SETOCPM             [ISSUE REQUEST   
  20 39HL ...      CALL  7  OCPSSR              [REPORT REPLY & STORE IN FIXED   
  20 3=3= ...      CALL  6  PARAVET             [GET & VET NEXT PERCENTAGE(&OCP) 
  18 3=GW ...      BRN      XITCOM              [J IF NO PARAS, FINISHED 
  20 3?2G ...      BRN      NOCPERR             [J IF PERCENT, BUT NO OCP - ERROR
  18 3?G6 ...      BRN      RATIO               [J IF PERCENTAGE AND OCP 
   5 3?_Q ...ZEROCP  
   9 3#FB ...      CALL  7  SETALL   
   9 3#_2 ...      LDX   0  APROCNO  
   8 3*DL ...      ANDX  0  #77  
  16 3*Y= ...      BXL   0  XDUALS(1),SKIPOCP   [J IF NOT DUALS  
   9 3BCW ...      CALL  5  REPALL   
   9 3BXG ...      BRN      XITCOM   
  17 3CC6 ...#                       NOT DUALS. COMMAND OR INTERNAL  
   5 3CWQ ...SKIPOCP 
  10 3DBB ...      STO   4  CMEMILLALL   
   9 3DW2 ...      OUTPACK  4,1,NUMA 
  16 3F*L ...      CALL  5  REPORTN             [OUTPUT MESSAGE  
   8 3FT= ...      BRN      XIT  
  18 3G#W ...#                       INTERROGATION - COMMAND OR INTERNAL 
   5 3GSG ...QUERY   
   9 3H#6 ...      CALL  7  SETQALL  
   9 3HRQ ...      LDX   0  APROCNO  
   8 3J?B ...      ANDN  0  #77  
  16 3JR2 ...      BXL   0  XDUALS(1),SKIPOCP   [J IF NOT DUALS  
   9 3K=L ...      CALL  5  REPALL   
  12 3KQ= ...      OCPSMILL 4,AWORK2(2),OCPSSR   
   8 3L9W ...      BRN      XIT  
   4 3LPG ...#   
  18 3M96 ...#                       ENTRY IN EMS, INTERNAL INTERROGATION
   4 3MNQ ...#   
   5 3N8B ...XENT2   
   9 3NN2 ...      STOZ     AWORK1(2)
   8 3P7L ...      BRN      QUERY
   4 3PM= ...XIT 
   9 3Q6W ...      LDX   0  AWORK1(2)
  16 3QLG ...      BZE   0  XITUP               [J IF NOT COMMAND
   5 3R66 ...XITCOM  
   6 3RKQ ...     ENDCOM 
   5 3YG2    XITUP   
   5 3Y_L          UP
  12 3_F=    #                     ERROR ROUTINES
   5 3_YW    NOTCME  
   8 42DG          CALL  3  XERR 
  12 42Y6                   +JENVMUSTBE,+JENVCME 
   5 43CQ    PERERR  
   8 43XB          CALL  3  XERR 
  10 44C2                   +CMEMERR1,-1 
   5 44WL    OCPERR  
   8 45B=          CALL  3  XERR 
   9 45TW                +CMEMERR2,-1
   5 46*G    NOCPERR 
   8 46T6          CALL  3  XERR 
  11 47#Q                   +JPARMIS,+CMEOCP 
   5 47SB    NLPERR  
   8 48#2          CALL  3  XERR 
  11 48RL                   +JPARNULL,+CMEPER
   5 49?=    NLOCPERR
   8 49QW          CALL  3  XERR 
  11 4==G                   +JPARNULL,+CMEOCP
   4 4=Q6    XERR
  19 4?9Q          COMERRX  0(3),1(3)           [REPORT ERROR & RETURN TO CP 
   4 4?PB    #END
   6 ____ ...242071550021
