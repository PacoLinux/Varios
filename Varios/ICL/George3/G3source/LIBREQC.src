   8 229S    #OPT     K0LIBREQC=0
  13 22CW ...      SEG      LIBREQC,867,FILE,,G206   
   4 22KY ...[   
  15 22S2 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  13 2324 ...[     THIS EXCLUDES CODE UNDER #SKI G206
   4 2386 ...[   
   7 23B8 ...#OPT G206 = 0   
   6 23J= ...#SKI G206&1 
  19 23Q# ...# WITH UGUG EDIT M206 (INCLUDE JOB NUMBER IN MAG TAPE REQUESTS) 
   4 23YG    #   
  11 24D6          SEGENTRY K1LIBREQC,QK1LIBR
  11 24XQ          SEGENTRY K4LIBREQC,QK4LIBR
  14 257J          FSHENTRY K5LIBREQC,QK5LIBR,,QK5LIBR   
   4 25CB    #   
  10 25X2    #     A. PURPOSE OF SEGMENT.
  12 26BL    #           TO GET A TAPE FROM THE  
  21 26W=    #           POOL, AND MARK THE DECK THAT THE TAPE IS LOADED ON ONLINE TO
   9 27*W    #           THE USER.   
  14 27TG    #     B. GENERAL DESCRIPTION OF MECHANICS.  
  21 28*6    #           1. CHECK WHETHER TAPE IS LOADED. IF SO, GO TO STATEMENT 6   
  21 28SQ    #           2. IF NOT & A REQUEST HAS ALREADY BEEN OUTPUT,GO TO STATEMEN
  21 29#B    #           3. IF NO REQUEST HAS YET BEEN OUTPUT TO OPERATORS, DO SO.   
  17 29S2    #           4. PUT THE ACTIVITY TO SLEEP AWAITING A TAPE
  21 2=?L    #           5. LIBLOAD REACTIVATES THE ACTIVITY WHEN  A  TAPE IS LOADED 
  11 2=R=    #              TO STATEMENT 1   
  21 2?=W    #           6. CHECK WHETHER THE CORRECT CONDITIONS ARE SET FOR THE TAPE
  21 2?QG    #              BE USED. (E.G. IF NECESSARY ASK FOR A CHANGE OF WPR CONDI
  18 2#=6    #              & GO TO STATEMENT 4 AFTER UNLOADING THE TAPE)
  21 2#PQ    #           7. MARK DECK ON-LINE TO USER & GO UP INFORMING HIM WHICH IT 
   8 2*9B    #     C. ENTRY POINT
  14 2*P2    #              K1LIBREQC - X4 CONTAINS MODES
  14 2B8L    #              B23 SET - WORK TAPE REQUIRED 
  13 2BN=    #              B22 SET - MARK AS ONLINE 
  16 2C7W    #              B21 SET - SET UP FABSNB FOR WORK TAPE
  11 2CMG    #     D. EXITS & ERROR REPLIES. 
  19 2D76    #           OK         :TAPE LOADED ON DECK INDICATED IN EXEC1. 
  21 2DLQ    #           NOTAV      :TAPE NOT AVAILABLE. CANTDO COMMAND HAS BEEN ISSU
  13 2F6B    #                       THE OPERATORS   
  12 2FL2    #     E. LIBREQC IS ENETRERED FROM:-
  11 2G5L    #              GET,OLMTA,OLMTC  
  19 2GK=    SLAN           4HC1                [TEMPORARY DIRECTORY LANGIAGE
   9 2H4W    PLONE          #20000001
   9 2HJG    QFAST          4HFAST   
   9 2J46    QSLOW          4HSLOW   
   8 2JHQ    PENRZI         #201 
   9 2JNY    ZGCR           +HWPGCR  
   4 2QWW    #   
   4 2RBG    #   
  19 2RW6    QK1LIBR                            [SEE HEADING FOR THIS ENTRY  
  10 2S*Q          LDX   0  ACOMMUNE1(2) 
  19 2STB          STO   0  AWORK3(2)           [SPECIAL PROPERTY DESCRIPTION
   8 2SXH          ANDN  0  #40  
  17 2SY=          BZE   0  NOMODE                [J IF NO MODE GIVEN
  11 2SY_          HUNT     3,CPAR,GPERCON   
   8 2S_N          BPZ   3  XGPER
  18 2T2C          SETNCORE 3,3,CPAR,GPERCON      [SET UP CPAR,GPERCON   
  18 2T36          LDCT  0  5                     [IF NONE ALREADY EXISTS
   8 2T3T          STO   0  A1(3)
   9 2T4J          STOZ     A1+1(3)  
   9 2T5?          STOZ     A1+2(3)  
   5 2T62    XGPER   
   9 2T6P          LDX   0  AWORK3(2)
  17 2T7D          SRL   0  18                    [GET MODE REQUIRED 
  16 2T87          ORN   0  1                     [SET WPR BIT   
   9 2T8W          ORS   0  A1+1(3)  
   8 2T9K          LDN   0  #75  
   9 2T=#          ORS   0  A1+2(3)  
   5 2T?3    NOMODE  
   9 2T*2    #SKI  K6LIBREQC>199-199 
  10 2TSL          SWITRACE 0,LBRQCSPD   
   7 2W#=          LDX   0  4
   7 2WRW          ANDN  0  4
  21 2X?G          BZE   0  NWT                 [BRANCH UNLESS FABSNB TO BE SET UP   
  19 2XR6          SETNCORE 18,3,FILE,FABSNB    [SET UP A FILE/FABSNB BLOCK  
   8 2Y=Q          LDN   0  16   
   9 2YQB          STO   0  HDREC(3) 
   9 2_=2          LDN   0  WORK(1)  
   9 2_PL          LDN   1  A1+10(3) 
   7 329=          MOVE 0   7
  16 32NW          FJOCA    1,2                 [FIND SOB BLOCK  
  10 338G          MHUNT    3,FILE,FABSNB
   9 33N6          LDN   0  JUSER(1) 
   9 347Q          LDN   1  A1+1(3)  
  15 34MB          MOVE  0  3                   [USER NAME   
  10 3572          SBN   0  JUSER-JNAME  
   9 35LL          LDN   1  A1+4(3)  
  14 366=          MOVE  0  3                   [SOB NAME
  14 36KW          STOZ     A1+7(3)             [REEL NO.
  16 375G          STOZ     A1+8(3)             [GENERATION NO.  
   8 37K6          SMO      FX1  
  13 384Q          LDX   0  SLAN                [C1  
   9 38JB          STO   0  A1+9(3)  
   5 3942    QK4LIBR 
   4 39HL    NWT 
   7 3=3=          LDX   0  4
   7 3=GW          ANDN  0  1
  18 3?2G          BZE   0  NWT1                [BRANCH UNLESS WORKTAPE  
  21 3?G6          TEMPDIRA XBRKIN,TEMPA        [ENSURE TEMPORARY DIRECTORY IS SET UP
   4 3?_Q    NWT1
  19 3#FB          STOZ     5                   [FIRST TIME THROUGH OUTPAUSE 
   9 3#P8          STOZ     AWORK4(2)
  17 3#_2    #     SEARCH THE DEVICE LISTS FOR A FREE WORK/POOLTAPE  
   5 3*DL    SERCH   
   7 3*Y=          LDX   0  4
   7 3BCW          ANDN  0  1
  17 3BXG          BNZ   0  SER                 [BRANCH IF WORKTAPE  
  10 3CC6          LONGSET  #56,WTSTYLE  
   8 3CWQ          BRN      SER0 
   4 3DBB    SER 
  10 3DW2          LONGSET  #63,WTSTYLE  
   4 3F*L    SER0
  20 3FT=          MTCHAR   AWORK3(2)           [SET UP HARDWARE PROPERTY CODE   
  10 3G#W          LDX   6  ACOMMUNE1(2) 
   9 3GSG    #SKI  K6LIBREQC>199-199 
   9 3H#6          SWITRACE 6,LBQCCHA
   8 3HRQ          LDX   0  GSIGN
   9 3J?B          ORS   0  AWORK4(2)
   9 3JR2          STOZ     CPPTR(2) 
   7 3K=L          LDN   7  5
  11 3KQ=          FINDPEREC 3,APFOTY,7,SER1 
  18 3L9W          BPZ   3  SER1A               [BRANCH IF ANOTHER DECK  
   9 3LPG          LDX   0  CPPTR(2) 
  18 3M96          BNZ   0  SER6                [BRANCH IF TAPE OBTAINED 
   9 3MNQ          LDX   0  AWORK4(2)
   9 3MTL          BPZ   0  NOTAPE   
  17 3N2G          JENV     SER3A,CME           [J IF CME ENVIRONMENT
   5 3N7B    SER2A   
  21 3N#?          SETUPMODE ,0,DUMPER      [   FOR DUMPER ONLY DO NOT REPLY NODECKS 
  21 3ND8          ANDX  0  AWORK2(2)       [   BUT BR TO OUTPUT 'PLEASE LOAD....  AN
  21 3NJ5          BNZ   0  NOT1            [   'WAITING FOR .... MSSGES TO WAIT FOR 
   7 3NN2          LONGOFF   
   9 3P7L          SETREP   NODECKS  
   7 3PM=          UPPLUS   1
   4 3PN9    [   
  13 3PP8    [ CME ENVIRONMENT - SO GROPE FOR DECKS  
   8 3PQ7    SER3A LDCT  0  #200 
   9 3PR6          ANDX  0  AWORK4(2)
   8 3PS5          BNZ   0  SER2A
   8 3PT4          LDN   3  GPDA 
   8 3PW3          LDX   0  16(3)
  16 3PX2          BZE   0  SER2A          [NO ANONYMOUS DEVICES 
   7 3PX_          LDN   7  5
  10 3PYY          GROPEREC 3,APTYPE,7   
   8 3P_X          LDCT  0  #200 
  17 3Q2W          ORS   0  AWORK4(2)           [MARK ALREADY GROPED 
  21 3Q3T          BPZ   3  SERCH               [SOME DECKS FOUND SO REPEAT SEARCH   
   8 3Q4S          BRN      SER2A
   4 3Q5R    [   
   5 3Q6W    SER1A   
   9 3QLG          JWRONG   3,SER1   
  19 3R66          BNG   6  SER1B               [BRANCH IF NO SPECIAL CHAR.  
   8 3RKQ          LPROP    3,0  
   8 3S5B          SMO      FX1  
  10 3SK2          BXL   6  PENRZI,SER1C 
   7 3SSS          LDX   1  0
   8 3T4L          ANDN  0  #203 
  20 3T#D          ANDN  1  4                        [X1 NON-ZERO IF 2510 DECK   
   8 3TJ=          BRN      SER1D
   5 3W3W    SER1C   
   7 3W?N          STOZ     1
   8 3WHG          ANDN  0  #200 
   5 3X36    SER1D   
   9 3X9B          BXE   0  6,SER1B  
  18 3XCL          BZE   1  SER1                     [BRANCH UNLESS 2510 
  15 3XF#    [ SOFTWARE SWITCHABLE DECK - IS GCR REQUESTED   
   8 3XFW          SMO      FX1  
   8 3XGD          TXU   6  ZGCR 
   8 3XH2          BCS      XNGCR
  17 3XHJ    [ GCR TAPE REQUESTED - IS THE DECK CAPABLE OF GCR MODE  
  11 3XJ6          JBC      SER1,3,CAPMODGCR 
   5 3XJN    XNGCR   
   8 3XLH          LDX   0  GSIGN
   9 3XNB          ORS   0  AWORK4(2)
   9 3XQ9          ERS   0  AWORK4(2)
  16 3XS6          BRN      SER1                    [NOT THE TAPE
   5 3Y2B    SER1B   
  11 3Y69          HUNT     1,CPAR,GPERCON   
   8 3Y7G          BNG   1  SER1E
   7 3Y8R          LDN   0  1
  16 3Y=4          ORS   0  A1+2(1)               [SET WRITE BITS
  17 3Y?*          ORS   0  A1+3(1)               [IN CPAR,GPERCON   
  20 3Y#L          MOCH     3                     [CHECK DECK IS SWITCHABLE TO   
  17 3Y*X          TESTREPN  OK,SER1              [  REQUIRED MODE   
   5 3YC8    SER1E   
   8 3YHR          LDX   0  GSIGN
   9 3YNB          ORS   0  AWORK4(2)
   9 3YS_          ERS   0  AWORK4(2)
  10 3Y_L          DFJUMPN  3,LOAD,SER1  
   7 3_F=          LDX   0  4
   7 3_YW          ANDN  0  1
  19 42DG          BZE   0  SER2                [BRANCH IF POOLTAPE REQUIRED 
  18 42Y6          DFJUMPS  3,WORK,SER3         [BRANCH IF WORKTAPE FOUND
   8 43CQ          BRN      SER1 
   4 43XB    SER2
  18 44C2          DFJUMPN  3,POOL,SER1         [BRANCH IF NOT POOLTAPE  
   4 44WL    SER3
  19 45B=          JALLOC   3,SER1              [BRANCH IF ALREADY ALLOCATED 
  16 45TW          JWPR     3,SER5              [BRANCH IF WPR   
   7 46*G          SETALLOC 3
   9 46T6          STO   3  CPPTR(2) 
   8 47#Q          BRN      REL  
   4 47SB    SER5
   9 48#2          LDX   0  AWORK3(2)
  12 48RL          ANDN  0  +GLIBFAST+GLIBSLOW   
  19 49?=          BNZ   0  SPEED               [BRANCH IF SPEED REQUIREMENT 
   7 49QW          SETALLOC 3
   9 4==G          VFREE    GMON,AOUT
   9 4=Q6          STO   3  CPPTR(2) 
  11 4?9Q          HUNT     1,CPAR,GPERCON   
  20 4?PB          BNG   1  SER6                [BRANCH IF NO MODE REQUIREMENT   
  21 4#92          MOCHAS                       [TRY TO SWITCH DECK TO REQUIRED MODE 
  12 4#NL          TESTREP  OK,SER6,SWITCHED,SER6
   4 4*8=    REL 
  10 4*MW          UNLOAD   ,,,UNL,AUT   
   8 4B7G          BRN      SER1 
  19 4BM6    SER6                               [UNSET POOL/WORKTAPE BIT NOW 
   7 4C6Q          LDX   0  4
   7 4CLB          ANDN  0  1
  17 4D62          BZE   0  SER7                [BRANCH IF POOLTAPE  
  17 4DKL          DFCLEAR  ,WORK               [UNSET WORKTAPE BIT  
   8 4F5=          BRN      SER8 
   4 4FJW    SER7
  17 4G4G          DFCLEAR  ,POOL               [UNSET POOLTAPE BIT  
   4 4GJ6    SER8
  14 4H3Q          DFLOAD   ,TSN,6              [X6=TSN  
   7 4HHB          LONGOFF   
   8 4J32          BRN      W5   
   5 4JGL    SPEED   
   9 4K2=          LDX   0  CPPTR(2) 
  21 4KFW          BZE   0  SP3                 [BRANCH IF NO POOL/WORK TAPE AS YET  
  20 4K_G          LPROP    ,1                  [PROPERTIES OF CURRENT FAVOURITE 
  16 4LF6          ANDN  1  #140                [EXTRACT SPEED   
  18 4LYQ          LPROP    3,0                 [PROPERTIES OF CANDIDATE 
   8 4MDB          ANDN  0  #140 
   7 4MY2          SBX   1  0
   9 4NCL          LDX   0  AWORK3(2)
   9 4NX=          ANDN  0  +GLIBFAST
  17 4PBW          BZE   0  SP1                 [BRANCH UNLESS FAST  
  19 4PWG          BNG   1  SP2                 [BRANCH IF CANDIDATE FASTER  
   8 4QB6          BRN      SER1 
   4 4QTQ    SP1 
   8 4R*B          BNG   1  SER1 
   8 4RT2          BZE   1  SER1 
   4 4S#L    SP2 
   9 4SS=          LDX   0  CPPTR(2) 
   4 4T?W    SP3 
   9 4TRG          STO   3  CPPTR(2) 
   7 4W?6          LDX   3  0
   7 4WQQ          SETALLOC  
  11 4X=B          HUNT     1,CPAR,GPERCON   
  20 4XQ2          BNG   1  SP4                 [BRANCH IF NO MODE REQUIREMENT   
   6 4Y9L          MOCHAS
  12 4YP=          TESTREP  OK,SP4,SWITCHED,SP4  
  10 4_8W          UNLOAD   ,,,AUT,RET   
   9 4_NG          STO   3  CPPTR(2) 
   8 5286          BRN      SER1 
   4 52MQ    SP4 
   8 537B          BZE   3  SP4A 
  10 53M2          UNLOAD   3,,,AUT,RET  
   4 546L    SP4A
   9 54L=          LDX   3  CPPTR(2) 
   8 555W          BRN      SER1 
  15 55KG    #     NO POOL/WORKTAPE FOUND - OUTPUT REQUEST   
   5 5656    NOTAPE  
  10 56JQ          SETUPMODE ,0,TAPEUP   
   8 574B          SMO      FX2  
   9 57J2          ANDX  0  AWORK2   
  20 583L          BZE   0  NOT1                [BRANCH IF TAPE TO BE REQUESTED  
   7 58H=          LONGOFF   
   8 592W          SETREP   NOTUP
   7 59GG          UPPLUS   1
   4 5=26    NOT1
   9 5=FQ          LDX   0  AWORK1(2)
  10 5=_B          STO   0  ACOMMUNE9(2) 
  21 5?F2          BNZ   5  REENT2              [BRANCH IF OUTPAUSE LEFT - NON EVENT 
   5 5?YL    REPEAT  
   7 5#D=          LDX   0  4
   7 5#XW          ANDN  0  1
  17 5*CG          BNZ   0  NOT3                [BRANCH IF WORKTAPE  
   9 5*X6          MONOUT   HWAITP   
   8 5BBQ          BRN      NOT4 
   4 5BWB    NOT3
   9 5CB2          MONOUT   HWAITW   
   4 5CTL    NOT4
  11 5D*=          HUNT     3,CPAR,GPERCON   
   8 5DSW          BNG   3  NOT5 
   7 5F#G          NGN   0  2
   9 5FS6          ANDX  0  A1+2(3)  
   8 5G?Q          BZE   0  NOT5 
   9 5GRB          OUTMESS  HMODE2   
   6 5H?2          MONICA
   9 5HQL          HUNT  3,AONBS,GROT
  12 5J==          OUTPARAM A1(3),A1+1,AONBS,GROT
  10 5JPW          MFREE    AONBS,GROT   
   8 5K9G          BRN      NOT6 
   4 5KP6    NOT5
   7 5L8Q          OUTNULL   
   7 5LNB          OUTNULL   
   4 5M82    NOT6
   9 5MML          LDX   0  AWORK3(2)
   9 5N7=          ANDN  0  +GLIBFAST
   8 5NLW          BZE   0  NOT62
   9 5P6G          LDX   0  QFAST(1) 
   5 5PL6    NOT61   
  10 5Q5Q          STO   0  ACOMMUNE1(2) 
   7 5QKB          LDN   3  4
  10 5R52          OUTPARAM 3,ACOMMUNE1  
   8 5RJL          BRN      NOT64
   5 5S4=    NOT62   
   9 5SHW          LDX   0  AWORK3(2)
   9 5T3G          ANDN  0  +GLIBSLOW
   8 5TH6          BZE   0  NOT63
   9 5W2Q          LDX   0  QSLOW(1) 
   8 5WGB          BRN      NOT61
   5 5X22    NOT63   
   7 5XFL          OUTNULL   
   5 5X_=    NOT64   
   9 5YDW          LDX   0  AWORK3(2)
  10 5YYG          STO   0  ACOMMUNE1(2) 
  10 5_D6          DOWN     TRANSPOT,2   
  10 5_XQ          TESTREP  NOTAV,NOT9   
   6 624Y ...#SKI G206&1 
  10 62=6 ...      OUTNUM   JOBNO(2),0   
   7 62CB          LDX   0  4
   7 62X2          ANDN  0  1
   8 63BL          BNZ   0  NOT7 
   9 63W=          MTREQUEST HLOPT   
   8 64*W          BRN      NOT8 
   4 64TG    NOT7
   9 65*6          MTREQUEST HLOWT1  
   4 65SQ    NOT8
   8 66#B          BRN      OUTP 
   4 66S2    NOT9
  10 66_8          OUTMTPROP AWORK3(2)   
   6 67DS ...#SKI G206&1 
  10 67YD ...      OUTNUM   JOBNO(2),0   
   7 6GH2          LDX   0  4
   7 6H2L          ANDN  0  1
   8 6HG=          BNZ   0  NOT10
   9 6H_W          MTREQUEST HLOWHP  
   8 6JFG          BRN      OUTP 
   5 6J_6    NOT10   
   9 6KDQ          MTREQUEST HLOWHW  
   4 6KYB    OUTP
  14 6LD2          OUTPAUSE XBRKIN,WOKEN,YCANTDO,2,REENT2
   9 6LXL          BRN      REPEAT   
   5 6MC=    WOKEN   
  10 6MWW          LDX   0  ACOMMUNE9(2) 
   9 6NBG          STO   0  AWORK1(2)
  19 6NW6          LDN   5  1                   [OUTPAUSE HAS BEEN ENTERED   
   8 6P*Q          BRN      SERCH
   4 6PTB    W5  
   4 6PW4    [   
  20 6PWQ    [ ****************************************************************  
   4 6PXD    [   
   5 6PY6    QK5LIBR 
   4 6PYS    [   
  20 6P_G    [ ***************************************************************** 
   4 6Q28    [   
  17 6Q2W    [ UPDATING SYSTEM SERIAL AND CREATING DIRECTORY ENTRY   
  12 6Q3J    [ DO IT IN THE A MACHINE FOR SAFETY 
   4 6Q4=    [   
  10 6Q*2          MHUNT    3,FILE,FABSNB
   8 6QSL          SMO      A1(3)
  17 6R#=          STO   6  A1+1(3)             [INSERT TSN TO ABSNB 
  20 6RRW          LDN   0  #2001               [NONFILESTORE TYPE - TSN GIVEN   
  18 6S?G          ORS   0  ATYPE(3)            [BIT FOR LIBRARIAN OBJECT
  17 6SR6          OPENSYS  VBRKIN,SERIAL,GENERAL   [TO UPDATE ENTRY 
  19 6S_B          SSEARCH  NOSER,6             [TAPE MAY HAVE BEEN DEADED   
  16 6T7L          LDX   0  5(3)                [IN THE MEANTIME 
   7 6T*W          SLL   0  2
   8 6TJ6          BNG   0  NOSER
   7 6TQB          READAGAIN 
  10 6W=2          HUNT     3,FILE,FRB   
   7 6WPL          LDX   0  4
   7 6X9=          ANDN  0  2
  19 6XNW          BZE   0  YA1                 [BRANCH UNLESS UPDATE COUNT  
   7 6Y8G          LDN   0  1
   9 6YN6          ADS   0  A1+5(3)  
   4 6_7Q    YA1 
  10 6_MB          MHUNT    2,FILE,FABSNB
   9 7272          LDX   0  ATYPE(2) 
   8 72LL          ANDN  0  #200 
  19 736=          BZE   0  YA2                 [BRANCH UNLESS NAMED WORKTAPE
  15 73KW          LDCT  0  #40                 [NAMED BIT   
   9 745G          ORS   0  A1+5(3)  
  16 74K6          LDX   0  PLONE(1)            [LATEST PLUS ONE 
   8 754Q          SMO      A1(2)
   9 75JB          STO   0  A1-2(2)  
   4 7642    YA2 
  10 76HL          NAME     3,FILE,FWB   
   7 773=          LDX   0  4
   7 77GW          ANDN  0  1
  18 782G          BZE   0  NOTEMP2             [BRANCH UNLESS WORK TAPE 
   7 78G6          CREATEB   
  10 78_Q          HUNT     3,FILE,CREATE
  20 79FB          LDCT  0  #220                [TEMPORARY BIT & NOT IN FILESTORE
   9 79_2          STO   0  CEINF2(3)
   5 7=DL    NOTEMP2 
  20 7CM=          USEROPEN YBRKIN,CREATE,REPLY,FROZEN [SET UP DIRECTORY ENTRY   
  11 7CSG          TESTREPN OK,TESTALREADY   
  20 7CTF          OPENDIR  YBRKIN,GENERAL,ERASING,QUERY [OPEN DIRECTORY ENTRY   
  17 7CWD          TESTREPN OK,(GEOERR)         [GEOERR IF NOT OKAY  
  19 7CXC          MHUNTW   3,FILE,ENT          [X3 -> NAME RECORD FOR TAPE  
  15 7CYB          LDX   2  EDATE               [X2 => DATE  
  19 7C_*          LDX   1  CLEANCT             [X1 => TIME SINCE MIDNIGHT   
  17 7D2#          STO   2  EDLA(3)             [DATE LAST ACCESSED  
  17 7D3?          STO   1  ETLA(3)             [TIME LAST ACCESSED  
  17 7D4=          STO   2  EWRITDAY(3)         [DATE LAST WRITTEN   
  17 7D59          STO   1  EWRITTIME(3)        [TIME LAST WRITTEN   
  18 7D68          LDX   0  GMTNOW              [X0 => GEORGE MEAN TIME  
  17 7D77          STO   0  EGMTLA(3)           [GMT LAST ACCESSED   
  17 7D86          NAME     3,FILE,FWB          [RENAME NAME RECORD  
  19 7D95          REWRITE                      [REWRITE UPDATED NAME RECORD 
  16 7D=4          MFREEW   FILE,FWB            [FREE ENT BLOCK  
  16 7D?3          CLOSETOP                     [CLOSE DIRECTORY 
   7 7D#6          LDX   0  4
   7 7DRQ          ANDN  0  1
  17 7F?B          BNZ   0  VW1                 [BRANCH IF WORKTAPE  
  21 7FR2          HUNT     2,FILE,ADICT        [AS LEFT BY USEROPEN CONTAINING USER 
  19 7G=L          ADN   2  A1                  [NAME OF SUPERIOR DIRECTORY  
   8 7GQ=          BRN      VW2  
   4 7H9W    VW1 
   7 7HPG          FJOCA    2
   8 7J96          ADN   2  JUSER
   4 7JNQ    VW2 
  10 7K8B          HUNT     3,FILE,FWB   
   8 7KN2          ADN   3  A1+2 
  17 7L7L          MOVE  2  3                   [MOVE IN USER NAME   
  18 7LM=          REWRITE                      [ ENTRY IN SYSTEM.SERIAL 
  17 7M6W          MFREE    FILE,ADICT          [AS LEFT BY USEROPEN 
   9 7MLG          MFREE    FILE,FWB 
  16 7N66          CLOSETOP                     [SYSTEM.SERIAL   
   4 7N7H    [   
   5 7N8Y    XNOTB1  
  19 7NKQ          ACROSS   LIBREQCA,1          [GO TO CONTINUATION SEGMENT  
   5 7NNW    NOSER   
  14 7NS2          CLOSETOP                     [SERIAL  
   4 7NS?    [   
   5 7NSJ    XNOTB2  
   9 7NX6          UNLOAD   ,,,UNL   
   8 7P2=          BRN      SERCH
   6 7P5B    TESTALREADY 
   7 7PK2          PICKREP  5
  11 7Q4L          TESTREP  ALREADY,SETREP   
   9 7QJ=          TESTREP  NAME,TEMP
  10 7R3W          TESTREP  WRGEN,SETREP 
  11 7RHG          TESTREP  VRYWRONG,SETREP  
  21 7S36          GEOERR   1,TAPESER           [ANY REPLY OTHER THAN ABOVE IS A SYST
  10 7SGQ    WORK           12HWORK TAPE 
   8 7T2B                   +0   
   8 7TG2                   +0   
   9 7T_L                   #20202020
   8 7WF=                   +0   
   5 7WYW    WTSTYLE 
   9 7XDG          GEOERR   1,WTSTYLE
   5 7XY6    UBRKIN  
   7 7YCQ          CLOSETOP  
   5 7YXB    VBRKIN  
   9 7_C2          CALL  7  MTFREE   
   5 7_WL    XBRKIN  
   9 82B=          HUNT  1,FILE,FTRAP
   8 82TW          BNG   1  XBRKA
   7 83*G          FREECORE 1
   9 83T6          BRN      XBRKIN   
   5 84#Q    XBRKA   
  14 84SB          UP                           [BREAKIN 
   5 85#2    YBRKIN  
  19 85RL          GEOERR   1,NOBRKIN           [BREAKIN ON OPENING DIRECTORY
   4 86?=    TEMP
   9 86QW          KEEPREP  5,TEMP   
   5 87=G    SETREP  
   9 87Q6          MFREE    FILE,FWB 
   7 889Q          CLOSETOP  
   4 88?7    [   
   5 88#J    XNOTB3  
  16 88PB          CALL  7  MTFREE              [FREE DECK AGAIN 
   7 8992          PUTREP   5
  14 89NL          UPPLUS   1                   [& EXIT  
   5 8=8=    YCANTDO 
   8 8=MW          SETREP   NOTAV
   7 8?7G          UPPLUS   1
   5 8?M6    TEMPA   
   8 8#6Q          SETREP   TEMP 
   7 8#LB          UPPLUS   1
   5 8BJW    MTFREE  
  16 8C4G          CLALLOC                      [DE-ALLOCATE DECK
   7 8CJ6          LDX   0  4
   7 8D3Q          ANDN  0  1
  17 8DHB          BNZ   0  MTFR1               [BRANCH IF WORKTAPE  
   8 8F32          DFSET    ,POOL
  21 8FGL          LONGON   #56                 [MAKE TAPE AVAILABLE AS A POOL TAPE  
   8 8G2=          BRN      MTFR2
   5 8GFW    MTFR1   
   8 8G_G          DFSET    ,WORK
  21 8HF6          LONGON   #63                 [MAKE TAPE AVAILABLE AS A WORK TAPE  
   5 8HYQ    MTFR2   
   7 8JDB          EXIT  7  0
  10 8JY2          MENDAREA 50,K99LIBREQC
   4 8KCL    #END
   8 ____ ...71032473000200000000
