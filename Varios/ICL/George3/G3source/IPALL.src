  12 22FL          SEG      IPALL,864,G.N.HAWKE  
   4 22_=    #   
  10 23DW          SEGENTRY K1IPALL,XK1  
  12 23YG          FSHENTRY K2IPALL,XK2,XK2,XK2  
  12 24D6          FSHENTRY K3IPALL,XK3,XK3,XK3  
  19 24XQ    [ THIS SEGMENT IMPLEMENTS THE ALL PART OF THE INSTPARA COMMAND  
   4 25CB    #   
  11 25X2    #UNS  IPEXTENSION+1*IPEXTENSION 
   7 26BL    #DEF  XIPEXT=1  
  11 26W=    #UNS  IPEXTENSION+1*IPEXTENSION 
   4 27*W    #SKI
   7 27TG    #DEF  XIPEXT=0  
   8 28*6          FSHCODE  AORB 
   4 28SQ    (   
  20 29#B    #     AWORK3 IS USED TO INDICATE WHETHER AN 'IN' PARAMETER HAS BEEN 
  17 29S2    #  SPECIFIED. ON ENTRY X4 HOLDS THE APPROPRIATE BITS.   
  13 2=?L    #     BIT 3 - 'IN' PARAMETER SPECIFIED  
   8 2=R=    #     BIT 4 - 'IN A'
   8 2?=W    #     BIT 5 - 'IN B'
   4 2?QG    )   
  20 2#=6    [     THE FOLLOWING IP'S VALUES ETC, CANNOT BE CONVERTED BY CHNUMCON
  17 2#PQ    XJOB          +JOBTRACE-INSTPARAM      [PARAM NUMBERS   
  17 2*9B                  +IMINTRACE-INSTPARAM     [OF TRACE TYPE   
  14 2*P2                  +IMOPTRACE-INSTPARAM     [I-PS
  12 2B8L    XOPTR          +IOPTRACE-INSTPARAM  
  10 2BN=    XCON  +ICONTEXT-INSTPARAM   
   8 2C7W    UNS            +5   
   9 2CMG    UNSET          5HUNSET  
  19 2D76    XEMP  GEOERR  1,PARAEMPT             [IF PARAMINDEX EMPTY-GEOERR
  18 2DLQ    [              IF CONTEXT THEN OTHER PARAMETERS ARE A,B OR C
  15 2F6B    [     S/R TO CONVERT AND O/P CONTEXT TYPE PARS  
   5 2FL2    TEXTCON 
   8 2G5L          SBX   6  FX1  
  17 2GK=          IPCONT   7,3                 [CONVERT PARAMETER   
  10 2H4W          OUTPARAM 3,ACOMMUNE1  
   8 2HJG          ADX   6  FX1  
   7 2J46          EXIT  6  0
  19 2JHQ    OUTNUM                             [S/R TO OUTPUT DEC NO IN X6  
   8 2K3B          SBX   3  FX1  
   8 2KH2          OUTNUM   6,7,1
   8 2L2L          ADX   3  FX1  
   7 2LG=          EXIT  3  0
   4 2L_W    #   
   6 2MFG    #SKI  XIPEXT
  18 2M_6    XK1   DOWN     SETUPARA,2          [SET UP FILE/FRB BLOCK   
   7 2NDQ    #SKI  XIPEXT<1$1
   6 2NYB    XK1   READ  
   7 2PD2          CLOSETOP  
   8 2PXL          FSHCODE  AORB 
  20 2QC=          STO   4  AWORK3(2)           [STORE BIT INDICATOR IN AWORK3   
  16 2QWW          MONOUT   IPHEADING           [OUTPUT HEADING  
   9 2RBG          MHUNTW  3,FILE,FRB
   7 2RW6          STOZ     6
   6 2S*Q    #SKI  XIPEXT
  20 2STB          LDX   7  A1+FRH(3)          [NUMBER OF WORDS IN FILE?FRB BLOCK
   7 2T*2    #SKI  XIPEXT<1$1
   9 2TSL          LDEX  7  A1+FRH(3)
  17 2W#=          BZE   7  XEMP                [PARAMINDEX IS EMPTY 
   9 2WRW          LDN   0  IPLINE   
  14 2X?G          DVR   6  0                   [CT IN X7
   9 2XR6          STOZ     AWORK1(2)
   9 2Y=Q    XLOOP ADX   3  AWORK1(2)
  15 2YQB          STO   7  AWORK2(2)           [SAVE COUNT  
   9 2_=2          LDX   1  IPNUM(3) 
   9 2_PL          LDN   4  INSTPARL 
   6 329=    #SKI  XIPEXT
   4 32NW    (   
  19 338G          BPZ   1  NOTXTNUM            [J IF NOT IN EXTENSION TABLE 
  19 33N6          LDEX  0  1                   [REMOVE 'EXTENSION TABLE' BIT
  17 347Q          BXGE  0  IPEXTCOUNT,XOUT     [J IF OUT OF RANGE   
   8 34MB          BRN      XTNUM
   5 3572    NOTXTNUM
   4 35LL    )   
   7 366=          TXL   4  1
  17 36KW          BCS      XOUT                [J IF OUT OF RANGE   
   5 375G    XTNUM   
  14 37K6          STO   4  AWORK4(2)           [MARKER  
   9 384Q          LDN   2  XCON-XJOB
   8 38JB    XYZ    ADX   2  FX1 
   9 3942          TXU   1  XJOB-1(2)
  15 39HL          BCC      TRC                 [J IF TRACE  
   8 3=3=          SBX   2  FX1  
   8 3=GW         BCT    2  XYZ  
   8 3?2G          LDX   2  FX2  
  18 3?G6          NGS   2  AWORK4(2)           [SET MARKER => CONTEXT   
   8 3?_Q          SMO      FX1  
   8 3#FB          TXU   1  XCON 
  15 3#_2          BCC      TRC                 [CONTEXT I.P 
  15 3*DL          STOZ     AWORK4(2)           [CLEAR MARKER
   4 3*Y=    TRC 
   8 3BCW          FSHCODE  AORB 
   4 3BXG    (   
  14 3CC6          LDX   2  FX2                 [RESET X2
  18 3CWQ          LDX   4  1                   [SAVE CURRENT IPNUM IN X4
  10 3DBB          FSHCODE  A,XFSHANUM   
   4 3DW2    (   
  18 3F*L          BBS      4,AWORK3(2),XCURRALL [J IF A'S IPS REQUIRED  
   5 3FT=    XFSHANUM
   4 3G#W    )   
  20 3GSG          FSHIPCON 1,5                 [GET CORRESPONDING IPNUM IN X5   
  18 3H#6          MHUNTW   3,FILE,FRB          [PTR TO FILE/FRB BLOCK   
  10 3HRQ          FSHCODE  B,XFSHBNUM   
   4 3J?B    (   
  18 3JR2          BBS      5,AWORK3(2),XFSHBNUM [J IF B'S IPS REQUIRED  
  19 3K=L          ADX   3  AWORK1(2)           [RESET PTR TO IP WITHIN BLOCK
   9 3KQ=          BRN      XFSHBALL 
   5 3L9W    XFSHBNUM
   4 3LPG    )   
   5 3M96    NXTIP   
  18 3MNQ          BXE   5  IPNUM(3),TESTCURR   [J IF NOT REQUIRED IPNUM 
  17 3N8B          ADN   3  IPLINE              [PTR TO NEXT IP ENTRY
   8 3NN2          BRN      NXTIP
   5 3P7L    TESTCURR
  18 3PM=          LDX   1  4                   [RESTORE CURRENT IPNUM   
  10 3Q6W          FSHCODE  B,XFSHBALL   
   4 3QLG    (   
  20 3R66          BBS      5,AWORK3(2),XCURRALL [J IF CURRENT MACHINE IPS REQD  
   5 3RKQ    XFSHBALL
   4 3S5B    )   
  20 3SK2          LDX   1  5                   [LOAD CORRESPONDING IPNUM INTO X1
   5 3T4L    XCURRALL
   4 3TJ=    )   
   6 3W3W    #SKI  XIPEXT
  18 3WHG          IPUSEX   LDX 4,1             [LOAD CURRENT VALUE OF IP
   7 3X36    #SKI  XIPEXT<1$1
  10 3XGQ          LDX   4  INSTPARAM(1) 
   9 3Y2B          LDX   5  IPMIN(3) 
   9 3YG2          LDX   6  IPMAX(3) 
   9 3Y_L          LDN   3  IPNAME   
   8 3_F=          LDX   2  FX2  
   9 3_YW          ADX   3  AWORK1(2)
   8 42DG          LDN   0  13   
  17 42Y6          OUTBLOCK 0                   [SET UP PARAM BLOCK  
   8 43CQ          LDN   1  12   
  13 43XB          OUTPARAX 1,3,FILE,FRB        [NAME
   9 44C2          LDX   0  AWORK4(2)
  16 44WL          BZE   0  XFIG                [J IF NOT TRACE  
  17 45B=          BPZ   0  TRACX               [J IF TRACE TYPE I.P.
   8 45TW          BNZ   6  XCON1
  17 46*G          CALL  3  OUTNUM              [IF ZERO, USE OUTNUM 
   8 46T6          BRN      T10  
   7 47#Q    XCON1 LDX   7  6
  15 47SB          CALL  6  TEXTCON             [CONVERT MIN 
   8 48#2    T10   BNZ   5  XCON2
   7 48RL          LDX   6  5
   9 49?=          CALL  3  OUTNUM   
   8 49QW          BRN      TRC1 
   7 4==G    XCON2 LDX   7  5
   9 4=Q6          CALL  6  TEXTCON  
   8 4?9Q          BRN      TRC1 
   5 4?PB    TRACX   
   7 4#92          LDX   0  6
   8 4#NL          CALL  6  SYNTH
   7 4*8=          LDX   0  5
   8 4*MW          CALL  6  SYNTH
   8 4B7G          BRN      TRC1 
   4 4BM6    XFIG
  13 4C6Q          CALL  3  OUTNUM              [MAX 
   7 4CLB          LDX   6  5
  13 4D62          CALL  3  OUTNUM              [MIN 
   4 4DKL    TRC1
  15 4F5=          CALL  7  SUNSET              [IS IP  UNSET
   8 4FJW          BRN      XMON 
   8 4G4G          BRN      NOT  
   4 4GJ6    #   
   5 4H3Q    SUNSET  
  10 4HHB          BXE   4  GSIGN,SSUNS  
   7 4J32          EXIT  7  1
   4 4JGL    #   
   5 4K2=    SSUNS   
  17 4KFW    [     THIS CURES B2928 - I.E. AS OUTPARAM MAY COORDINATE
  14 4K_G    [         WE MUST RELITIVIZE THE LINK (X7)  
  15 4LF6    [             DONE    4TH   JUNE  75      :CENT 
   4 4LYQ    [   
   8 4MDB          SBX   7  FX1  
  17 4MY2          LDX   0  UNSET(1)            [CASE WHEN IP UNSET  
  10 4NCL          STO   0  ACOMMUNE5(2) 
  10 4NX=          LDX   0  UNSET+1(1)   
  10 4PBW          STO   0  ACOMMUNE6(2) 
   7 4PWG          LDN   4  7
  10 4QB6          OUTPARAM 4,ACOMMUNE5  
   8 4QTQ          ADX   7  FX1  
   4 4R*B    [   
   7 4RT2          EXIT  7  0
   7 4S#L    NOT   LDX   6  4
   9 4SS=          LDX   0  AWORK4(2)
  16 4T?W          BZE   0  NOTT                [J IF NOT TRACE  
   8 4TRG          BPZ   0  TRCX 
   8 4W?6          BZE   6  NOTT 
   7 4WQQ          LDX   7  6
  16 4X=B          CALL  6  TEXTCON             [CONVERT VALUE   
   8 4XQ2          BRN      XMON 
   4 4Y9L    TRCX
   7 4YP=          LDX   0  4
   8 4_8W          CALL  6  SYNTH
   8 4_NG          BRN      XMON 
   4 5286    NOTT
  16 52MQ          CALL  3  OUTNUM              [CURRENT VALUE   
   9 537B    XMON  MONOUT   IPLISTING
   4 53M2    XOUT
  10 546L          MHUNTW   3,FILE,FRB   
   9 54L=          LDN   0  IPLINE   
   9 555W          ADS   0  AWORK1(2)
  16 55KG          LDX   7  AWORK2(2)           [RESTORE COUNT   
   8 5656          BCT   7  XLOOP
   4 56JQ    XEND
   6 574B          ENDCOM
   4 57J2    #   
  12 583L    [     S/R TO CONVERT TRACE TYPE IPS 
   5 58H=    SYNTH   
   8 592W          SBX   6  FX1  
   7 59GG          SYNTHMON 0
   8 5=26          ADX   6  FX1  
   7 5=FQ          EXIT  6  0
   4 5=_B    #   
   8 5?F2          FSHCODE  AORB 
   4 5?YL    (   
   4 5#D=    XK2 
  21 5#XW    #  THIS ENTRY POINT SETS UP A FSH/FSHIPS BLOCK AND COPIES INTO IT THE   
  20 5*CG    #    CORRESPONDENCE TABLE FOR THE INSTALATION PARAMETERS IN THE TWO 
  21 5*X6    #    MACHINES.  IT IS ENTERED FROM SETUPARA BY A 'DOWN' TO OBTAIN THE   
  21 5BBQ    #    TABLE FOR MACHINE B AND BY A SUPERVISORY 'TRANSBEG' TO OBTAIN THE  
  20 5BWB    #    TABLE FOR MACHINE A AS THIS IS ONLY REQUIRED FOR CHECKING WHEN 
   9 5CB2    #    LOADING MACHINE B. 
   4 5CTL    #   
  19 5D*=          SETUPCORE IPEXTCOUNT,3,FSH,FSHIPS [SETUP FSH/FSHIPS BLOCK 
   8 5DSW    #SKI  K6IPALL>99-99 
   9 5F#G          TRACE    3,INSTIPS
  19 5FS6          ADN   3  A1                  [POINT TO START OF DATA AREA 
  20 5G?Q          LDN   2  XIPTABSTART(1)      [START OF CORRESPONDENCE TABLE   
  19 5GRB          MOVE  2  IPEXTSIZE           [MOVE ALL OF TABLE INTO BLOCK
  10 5H?2          FSHCODE  B,XFSHBTAB   
   4 5HQL    (   
   8 5J==    #SKI  K6IPALL>99-99 
   9 5JPW          TRACE    3,UPFSHIP
  20 5K9G          UP                           [ENTERED BY DOWN FROM SETUPARA   
   5 5KP6    XFSHBTAB
   4 5L8Q    )   
  10 5LNB          FSHCODE  A,XFSHATAB   
   4 5M82    (   
   8 5MML    #SKI  K6IPALL>99-99 
  10 5N7=          TRACE    2,FINFSHIP   
  21 5NLW          TRANSFIN  ,AUT               [ENTERED BY TRANSBEG FROM SETUPARA   
  19 5P6G          TRANSRET                     [   (SUPERVISORY TRANSBEG)   
   5 5PL6    XFSHATAB
   4 5Q5Q    )   
   5 5QKB    #PAGE   
  20 5R52    #  THIS ENTRY POINT IMPLEMENTS THE FSHIPCON MACRO TO CONVERT AN IP  
  19 5RJL    #    NUMBER TO ITS CORRESPONDING IPNUMBER IN THE OTHER MACHINES 
  21 5S4=    #    MATCHING IPVALUE IN FIXED CORE USING THE CORRESPONDENCE TABLE HELD 
  11 5SHW    #    AT THE END OF THIS SEGMENT.
   4 5T3G    #   
   4 5TH6    XK3 
  19 5W2Q          LDX   5  ACOMMUNE1(2)        [IP NUMBER TO BE CONVERTED   
   8 5WGB    #SKI  K6IPALL>99-99 
  10 5X22          TRACE    5,INSTCONV   
  21 5XFL          ADN   1  XIPTABSTART         [PTR TO START OF CORRESPONDENCE TABLE
  20 5X_=          LDX   4  IPEXTCOUNT          [LENGTH OF CORRESPONDENCE TABLE  
   6 5YDW    NEXTIPPAIR  
  21 5YYG          LDX   0  0(1)                [NEXT ENTRY IN CORRESPONDENCE TABLE  
   9 5_D6    #SKI  K6IPALL>199-199   
  10 5_XQ          TRACE    0,IPCORENT   
  20 62CB          BPZ   5  TOPHALF             [J IF IPNUM GIVEN FOR MACHINE A  
  17 62X2          ANDN  0  #7777               [BITS 12-23 OF ENTRY 
  18 63BL          SRC   0  1                   [IP NUMBER FOR MACHINE B 
   9 63W=          BRN      TESTIPNUM
   5 64*W    TOPHALF 
  17 64TG          ANDX  0  HALFTOP             [BITS 0-11 OF ENTRY  
  18 65*6          SLC   0  11                  [IP NUMBER FOR MACHINE A 
   6 65SQ    TESTIPNUM   
  21 66#B          BXE   0  5,XFOUND            [J IF SAME AS IP GIVEN FOR CONVERSION
  19 66S2          ADN   1  1                   [POINT TO NEXT ENTRY IN TABLE
  19 67?L          BCT   4  NEXTIPPAIR          [J IF MORE ENTRIES IN TABLE  
  17 67R=          BRN      XIPUP               [NO MATCHING FOUND   
   5 68=W    XFOUND  
  18 68QG          LDX   0  0(1)                [MATCHING ENTRY IN TABLE 
  21 69=6          BPZ   5  XBOTTOMHALF         [J IF IP NUMBER GIVEN FOR MACHINE A  
  15 69PQ          ANDX  0  HALFTOP             [BITS 0 - 11 
  21 6=9B          SLC   0  11                  [CORRESPONDING IPNUM FOR MACHINE A   
  10 6=P2          BRN      STOREIPNUM   
   6 6?8L    XBOTTOMHALF 
  15 6?N=          ANDN  0  #7777               [BITS 12 - 23
  21 6#7W          SRC   0  1                   [CORRESPONDING IPNUM FOR MACHINE B   
   6 6#MG    STOREIPNUM  
   8 6*76    #SKI  K6IPALL>99-99 
  10 6*LQ          TRACE    0,CONVINST   
  19 6B6B          STO   0  ACOMMUNE1(2)        [STORE CORRESPONDING IPNUM   
   5 6BL2    XIPUP   
  19 6C5L          UP                           [RETURN TO CALLING SEGMENT   
   4 6CK=    #   
   9 6D4W          SEGENTRY K50IPALL 
   6 6DJG    XIPTABSTART 
  21 6F46    #UNS  IPEXTSIZE             [ONLY SET IF EXTENSION TABLE ALREADY DEFINED
  20 6FHQ          FSHIPS   SHAPE               [REDEFINE CORRESPONDENCE TABLE   
  21 6G3B    #UNS  IPEXTSIZE             [LEAVE SPACE FOR CORRESPONDENCE TABLE IF NOT
  19 6GH2    #SKI                        [ALREADY DEFINED BY 'FSHIPS' MACRO  
  21 6H2L    #GAP 50                            [50 WORDS FOR IP CORRESPONDENCE TABLE
   4 6HG=    )   
  10 6H_W         MENDAREA 30,K99IPALL   
   4 6JFG    #END
   6 ____ ...667074460001
