  13 22FL          SEG      GET,6,N.R.BOULT,LIBRARY  
   4 22_=    #   
  10 23DW          SEGENTRY K1GET,QK1GET 
  10 23YG          SEGENTRY K2GET,QK2GET 
  10 24D6          SEGENTRY K3GET,QK3GET 
  10 24XQ          SEGENTRY K4GET,QK4GET 
  21 25CB    #              THIS SEGMENT WILL FETCH A MAGNETIC TAPE FROM THE POOL & W
  21 25X2    #           PUT AN ENRTY IN THE USERS DIRECTORY FOR THE TAPE, UPDATING T
  21 26BL    #              ENTRY IN SYSTEM.SERIAL TO SHOW THE NEW OWNER . IT WIL OUT
  21 26W=    #           VIA LIBREQ A REQUEST FOR THE TAPE TO BE LOADED & WHEN IT IS 
  21 27*W    #           THE HEADER LABEL WILL BE WRITTEN ACCORDING TO THE NEW ENTRY 
  18 27TG    #           THE DIRECTORY R ENTRY. THERE ARE TWO ENTRIES:-  
  16 28*6    #     K1GET IS THE ENTRY FROM THE COMMAND VIA LIBCOM
  16 28SQ    #     K2GET IS FROM THE GET MACRO (USED BY DOUMPER) 
   4 29#B    #   
  10 29S2    WORK           12HWORK TAPE 
   8 2=?L                   0,0  
   9 2=R=    ZANUM          #30021   
   9 2?=W    ZDET           8H(/C1)  
   4 2?QG    #   
   4 2#=6    #   
   4 2#PQ    #   
   5 2*9B    QK4GET  
  15 2*P2          LDN   7  1                   [ENTRY MARKER
  19 2B8L          LDX   0  ACOMMUNE1(2)        [SPECIAL PROPERTY DESCRIPTION
   9 2BN=          STO   0  AWORK3(2)
   8 2C7W          BRN      PGO5 
   5 2CMG    QK3GET  
  15 2D76          LDN   7  1                   [ENTRY MARKER
  19 2DLQ          LDX   0  ACOMMUNE1(2)        [SPECIAL PROPERTY DESCRIPTION
   9 2F6B          STO   0  AWORK3(2)
   8 2FL2          BRN      PGO6 
   5 2G5L    QK2GET  
  18 2GK=          NGN   7  1                   [MARKER FOR THIS ENTRY   
  16 2H4W          STOZ     AWORK2(2)           [INDICATE NO MODE
  20 2HJG          STOZ     AWORK3(2)           [NO SPECIAL PROPERTY DESCRIPTION 
   8 2J46          BRN      T3   
  16 2JHQ    QK1GET                             [COMMAND ENTRY   
  17 2K3B          LDN   7  0                   [MARKER FOR COMMAND  
   7 2KH2          LDN   4  0
  10 2L2L          HUNT     3,FLIB,FSPD  
   8 2LG=          BNG   3  PGO6A
   8 2L_W          LDX   4  A1(3)
   5 2MFG    PGO6A   
   9 2M_6          STO   4  AWORK3(2)
   4 2NDQ    PGO6
  16 2NYB          STOZ     AWORK2(2)           [INDICATE NO MODE
  10 2PD2          HUNT     3,FILE,FABSNB
   9 2PXL          LDX   0  ATYPE(3) 
  16 2QC=          ANDN  0  #2000               [CHECK BIT TSN   
  16 2QWW          BNZ   0  TSN                 [ERROR IF PRESENT
  18 2RBG          LDX   0  A1+4(3)             [FIRST WORD OF LOCAL NAME
  18 2RW6          BZE   0  NOLOCN              [ERROR IF NO LOCAL NAME  
   8 2S*Q          LDN   0  #777 
   9 2STB          SMO      HDREC(3) 
  17 2T*2          ANDS  0  A1-3(3)             [TRUNCATE REEL NUMBER
   4 2TSL    PGO5
  10 2W#=          HUNT     3,FILE,FABSNB
   9 2WRW          LDX   0  ATYPE(3) 
   8 2X?G          ANDN  0  #200 
  18 2XR6          BNZ   0  T3                  [BRANCH IF WORKTAPE NAME 
   9 2Y=Q          LDX   4  HDREC(3) 
   7 2YQB          SBN   4  6
  11 2_=2          SETUPCORE 4,3,FILE,FABSNB 
   7 2_PL          LDN   0  0
  17 329=          DSA   0  ATYPE(3)            [INDICATE FILE WANTED
   9 32NW          LDN   5  HDREC(3) 
  10 338G          HUNT2    3,FILE,FABSNB
   9 33N6          LDN   4  HDREC(3) 
   9 347Q          LDX   3  HDREC(3) 
   7 34MB          SBN   3  6
   8 3572          MOVE  4  0(3) 
  10 35LL          HUNT     3,FILE,FABSNB
   7 366=          LDN   0  6
   9 36KW          SBS   0  HDREC(3) 
  15 375G          OPEN     ZBRKIN,READ,QUERY   [DIRECTORY   
  10 37K6          MFREE    FILE,FABSNB  
   9 384Q          TESTREPN OK,PGOX  
  18 38JB          SETNCORE 6,3,FILE,FLOCNB     [SET UP LOCAL NAME BLOCK 
  10 3942          HUNT     2,FILE,FABSNB
   9 39HL          SMO      HDREC(2) 
   9 3=3=          LDN   5  A1-6(2)  
   8 3=GW          LDN   6  A1(3)
   7 3?2G          MOVE  5  3
  16 3?G6          STOZ     A1+3(3)             [ZEROISE REEL NO 
  16 3?_Q          STOZ     A1+4(3)             [GENERATION NO   
  16 3#FB          STOZ     A1+5(3)             [LANGUAGE CODE   
  17 3#_2          LDN   0  0                   [INDICATE FILE WANTED
   9 3*DL          DSA   0  ATYPE(3) 
   6 3*Y=          GETDIR
  19 3BCW          TESTREP  VRYWRONG,PGX       [MAGTAPE OF THIS NAME PRESENT 
   7 3BXG          PICKREP  5
  15 3CC6          CLOSETOP                     [DIRECTORY   
   7 3CWQ          PUTREP   5
  10 3DBB          MFREE    FILE,FLOCNB  
  19 3DW2          TESTREP  NOFILE,PGX4         [NO FILE OF THIS NAME PRESENT
  20 3F*L          TESTREPN OK,PGO5B            [OK-THERE IS A FILE OF THIS NAME 
  17 3FT=          SETREP   ALREADY             [A FILE OF THIS NAME 
   5 3G#W    PGO5B   
  10 3GSG          TESTERR  ALREADY,PGO5C
  10 3H#6          TESTERR  WRGEN,PGO5C  
   9 3HRQ    PGOX  TESTREPN NOUSER,S1
   8 3J?B          SETREP   NAME 
   4 3JR2    S1  
   9 3K=L          TXU   7  ONE(1)   
  16 3KQ=          BCC      PGO5D               [J IF GETONLINE  
  10 3L9W          TESTERR  NAME,PGO5C   
   8 3LPG    PGO5C BZE   7  PGO9 
   7 3M96    PGO5D UPPLUS   1
  10 3MNQ    PGX4  HUNT     3,FILE,FABSNB
   9 3N8B          SMO      HDREC(3) 
  17 3NN2          LDX   5  A1-2(3)             [ILEGAL GENERATION NO
  21 3P7L          BPZ   5  PGO5A               [IF NEGATIVE RELATIVE FGN SPECIFIED  
   7 3P#S ...      SLL   5  1
  15 3PG2 ...      BZE   5  PGX4A                 [J IF -0   
   8 3PM=          SETREP   WRGEN
   8 3Q6W          BRN      PGO5B
  13 3Q9F ...[ MAKE  -0 = +0 IF NO GENERATION EXISTS 
   8 3Q#4 ...PGX4A LDCT  5  #200 
   9 3QBM ...      SMO      HDREC(3) 
   9 3QF= ...      STO   5  A1-2(3)  
   8 3QHT ...      BRN      PGO5A
  10 3QLG    PGX   HUNT     3,FILE,FABSNB
   8 3R66          LDCT  5  #600 
   9 3RKQ          SMO      HDREC(3) 
  18 3S5B          ANDX  5  A1-2(3)             [RELATIVE FGN SPECIFIED  
  15 3SK2          BNZ   5  RLGN                [J. IF YES   
   7 3T4L          CLOSETOP  
  10 3TJ=          MFREE    FILE,FLOCNB  
   8 3W3W          BRN      PGO5A
  10 3WHG    RLGN  HUNT     2,FILE,FLOCNB
   9 3X36          SMO      HDREC(3) 
  17 3XGQ          LDN   5  A1-6(3)             [SET UP LOCAL NAME   
  16 3Y2B          LDN   6  A1(2)               [REEL NO AND FGN 
   7 3YG2          MOVE  5  5
   9 3Y_L          STOZ     A1+5(2)  
  18 3_F=          LDN   5  1                   [INDICATE MAG TAPE WANTED
   9 3_YW          ORS   5  ATYPE(2) 
   6 42DG          GETDIR
   7 42Y6          PICKREP  5
   7 43CQ          CLOSETOP  
   7 43XB          PUTREP   5
  10 44C2          MFREE    FILE,FLOCNB  
  19 44WL          TESTREP  OK,PGO5A            [ALREADY A TAPE OF THIS NAME 
  18 45B=          TESTREP  WRGEN,PGO5B         [ILLEGAL GENERATION NO   
  17 45TW          TESTREPN NOFILE,PGEO         [NO TAPE OF THIS NAME
  11 46*G    #     UPDATE THE SPACEMT BUDGET 
   5 46T6    PGO5A   
  11 46YK ...      SETNCORE 4,2,FILE,FABSNB  
  11 4744 ...      HUNT2    1,FILE,FABSNB,2  
   7 477H ...      LDN   0  4
   8 47?2 ...      STO   0  A1(2)
   8 47BF ...      ADN   1  A1+1 
  20 47FY ...      ADN   2  A1+1                [COPY USERNAME TO FABSNB USED TO 
  15 47KC ...      MOVE  1  3                   [GET DICT ENT
   8 47NW ...      OPENBUDO PGERR
   7 47SB          LDN   5  1
  10 48#2          BUDGINCR GSPACEMT,5   
   9 48RL          TESTREP  OK,PGO7  
  17 49?=          TESTREP  NOMORE,PGO7         [JUST TAKEN LAST ONE 
  17 49QW          PICKREP  3                   [KEEP ANY OTHER REPLY
  15 4==G          NGN   5  1                   [RESET BUDGET
  10 4=Q6          BUDGINCR GSPACEMT,5   
   7 4?9Q          CLOSBUDG  
   7 4?PB          PUTREP   3
   9 4#92          TESTREP  ODRAWN,T0
  10 4#NL          TESTREP  INCREASE,T2  
  11 4*8=          GEOERR   BRIEFPM,BUDGREP  
  10 4*D4 ...PGERR GEOERR   1,NOUSERGT   
   4 4*MW    T0  
  16 4B7G          BZE   7  PGO1A               [J IF GET COMMAND
   7 4BM6    PGO2  PUTREP   3
  17 4C6Q          UPPLUS   1                   [FINISH IF GETONLINE 
   4 4CLB    T2  
   8 4D62          BNZ   7  PGO2 
  11 4DKL          ERROR    BUDGINC,BUDGSPA  
   8 4F5=          BRN      PGO1 
   5 4FJW    PGO1A   
  11 4G4G          ERROR    BUDGODR,BUDGSPA  
  18 4GJ6    PGO1  ENDCOM                       [FINISH IF GET COMMAND   
   4 4H3Q    PGO7
   7 4HHB          CLOSBUDG  
   4 4J32    #   
  10 4JGL    T3    HUNT     3,FILE,FABSNB
   9 4K2=          LDX   4  HDREC(3) 
  10 4KFW          LDX   5  ALOGLEN(3)   
   7 4K_G          SBX   5  4
   7 4LF6          ADN   4  2
  19 4LYQ          ALTLEN   3,4,FILE,FABSNB     [EXTEND ABSNK TO CONTAIN TSN 
  21 4MDB          BNZ   5  T5                  [BRANCH IF BLOCK HOLDS RET. PERIOD   
  10 4MY2          HUNT     3,FILE,FABSNB
  18 4NCL          NGN   4  1                   [RETENTION PERIOD = -1   
   9 4NX=          SMO      HDREC(3) 
   9 4PBW          STO   4  HDREC(3) 
   4 4PWG    T5  
   7 4QB6          LDN   4  0
  10 4QTQ          MHUNT    3,FILE,FABSNB
   9 4R*B          LDX   0  ATYPE(3) 
   8 4RT2          ANDN  0  #200 
  19 4S#L          BZE   0  T5B                 [BRANCH UNLESS NAMED WORKTAPE
   7 4SS=          ORN   4  1
   4 4T?W    T5B 
  17 4TRG          BXU   7  ONE(1),TFC          [BRANCH UNLESS GETON 
   7 4W?6          ORN   4  2
   4 4WQQ    TFC 
   8 4X=B          LDX   2  FX2  
   9 4XQ2          LDX   0  AWORK3(2)
  10 4Y9L          STO   0  ACOMMUNE1(2) 
  15 4YP=          DOWN     LIBREQC,1           [FIND A TAPE 
   9 4_8W          BRN      XBRKIN   
   8 4_NG          TESTREPN OK,T4
   5 5286    PGO11   
  10 52CY ...      VFREEW   FILE,ADJUNCTS
  10 52MQ          HUNT     3,FILE,FABSNB
  21 537B          SMO      HDREC(3)            [PICK UP SERIAL NUMBER OF 'GOT' TAPE 
  10 53M2          LDX   6  HDREC+1(3)   
  21 546L          CONSERNO 6                   [CONVERT SERIAL NUMBER TO CHARACTERS 
  10 54L=          MHUNT    3,FILE,FABSNB
   9 555W          LDX   0  ATYPE(3) 
   8 55KG          ANDN  0  #200 
  18 5656          BNZ   0  TFC2                [BRANCH IF NAMED WORKTAPE
  10 56JQ          HUNT     3,FLIB,FTSN  
  12 574B          OUTPARAM A1(3),A1+1,FLIB,FTSN 
   8 57J2          MTINFORM GGET 
   4 583L    TFC2
   9 58H=          MFREE    FLIB,FTSN
   8 592W          BNZ   7  TFC1A
   9 59GG          DFLOAD   ,TSN,3   
   4 5=26    TSJ1
  10 5=FQ          OUTPACK  3,1,TSN,TSJ1 
   4 5=_B    TSJ2
  10 5?F2          MHUNT    3,FILE,FABSNB
   9 5?YL          LDX   0  ATYPE(3) 
   8 5#D=          ANDN  0  #200 
  17 5#XW          BNZ   0  TSJ5                [J IF NAMED WORKTAPE 
   8 5*CG          ADX   3  A1(3)
  13 5*X6          OUTPACK  A1-6(3),3,LOCNAME,TSJ2   
   4 5BBQ    TSJ3
  10 5BWB          MHUNT    3,FILE,FABSNB
   8 5CB2          ADX   3  A1(3)
  12 5CTL          OUTPACK  A1-2(3),1,GENNUM,TSJ3
   4 5D*=    TSJ4
  10 5DSW          MHUNT    3,FILE,FABSNB
   8 5F#G          ADX   3  A1(3)
  13 5FS6          OUTPACK  A1-3(3),1,REELNUM,TSJ4   
   8 5G?Q          BRN      TSJ8 
   4 5GRB    TSJ5
   8 5H?2          LDX   2  FX1  
  13 5HQL          OUTPACK  WORK(2),3,LOCNAME,TSJ5   
   4 5J==    TSJ6
   8 5JPW          LDX   2  FX1  
  13 5K9G          OUTPACK  WORK+4(2),1,GENNUM,TSJ6  
   4 5KP6    TSJ7
   8 5L8Q          LDX   2  FX1  
  13 5LNB          OUTPACK  WORK+3(2),1,REELNUM,TSJ7 
   4 5M82    TSJ8
   9 5MML          MONOUT   FUSEDMT  
   5 5N7=    TFC1A   
   9 5NLW          TXU   7  ONE(1)   
   8 5P6G          BCC      PGO3 
   7 5PL6          LDN   3  0
  10 5Q5Q          UNLOAD   ,3,3,AUT,RET 
  18 5QKB          BNZ   7  T10                 [BRANCH IF NOT A COMMAND 
  16 5R52          ENDCOM                       [END OF COMMAND  
  15 5RJL    T10   SETREP   OK                  [END OF MACRO
   5 5S4=          UP
   4 5SHW    PGO3
   8 5T3G          SETREP   OK   
   7 5TH6          UPPLUS   1
  13 5W2Q    #              NO POOL TAPES AVAILABLE  
   4 5WGB    T14 
  18 5X22          BNG   7  T12                 [BRANCH IF OPTIONAL GET  
  20 5XFL          CALL  5  RESET               [RESET BUDGETS UNLESS WORKTAPE   
   8 5X_=          BZE   7  T14A 
   7 5YDW          PUTREP   6
   7 5YYG          UPPLUS   1
   4 5_D6    T14A
  10 5_G* ...      HUNT     3,FILE,FABSNB
   8 5_JJ ...      LDCT  0  #200 
   9 5_LR ...      ANDX  0  ATYPE(3) 
   8 5_P2 ...      BZE   0  POOL 
   8 5_R9 ...      COMERR   JNWTA
   4 5_TD ...POOL
  21 5_XQ          COMERR   JNPT                [MESSAGE SAYING THERE ARE NO POOL TAP
  14 62CB    T12   HUNT     3,FILE,FABSNB       [MACRO   
   9 62X2          LDX   4  HDREC(3) 
   7 63BL          SMO      4
   9 63W=          LDX   0  HDREC(3) 
   7 64*W          ADN   0  1
   8 64TG          BZE   0  T13  
   7 65*6          ADN   4  1
   4 65SQ    T13 
  20 66#B          ALTLEN   3,4                 [RESTORE BLOCK TO ORIGINAL LENGTH
  17 66S2          SETREP   NOTAV               [NOTHING AVAILABLE   
   5 67?L          UP
   4 67R=    #   
  10 68=W    #              ERROR REPORTS
   4 68QG    TSN 
   8 69=6          BZE   7  TSN1 
   8 69PQ          SETREP   TSN  
   7 6=9B          UPPLUS   1
   4 6=P2    TSN1
  17 6?8L          COMERR   AMTDESC,JNTSN       [SERIAL NUMBER GIVEN 
   5 6?N=    NOLOCN  
   9 6#7W          BZE   7  NOLOCN1  
   9 6#MG          SETREP   NOLOCN   
   7 6*76          UPPLUS   1
   5 6*LQ    NOLOCN1 
  17 6B6B          COMERR   AMTDESC,JLOCN       [LOCAL NAME NOT GIVEN
   4 6BL2    T4  
   7 6C5L          PICKREP  6
   9 6CK=          TESTREP  NOTAV,T14
  10 6D4W          TESTREP  NOTUP,PGO4   
  10 6DJG          TESTREP  ALREADY,PGO4 
  10 6F46          TESTREP  WRGEN,PGO4   
  10 6FHQ          TESTREP  VRYWRONG,PGO4
   9 6G3B          TESTERR  NAME,PGO4
   9 6GH2          TESTREP  TEMP,T41 
  10 6H2L          TESTREP  NODECKS,TNOD 
   8 6HG=          GEOERR   1,GET
   4 6H_W    T41 
  10 6JFG          SETNCORE 7,3,CPB,CUNI 
   8 6J_6          FJOCA    2,2  
   9 6KDQ          LDN   4  JNAME(2) 
   9 6KYB          LDN   5  APARA(3) 
   7 6LD2          MOVE  4  3
   9 6LXL          LDN   4  ZDET(1)  
   7 6MC=          ADN   5  3
   7 6MWW          MOVE  4  2
   7 6NBG          LDN   0  1
   7 6NW6          ADX   0  7
   9 6P*Q          DSA   0  ATYPE(3) 
  10 6PTB          STO   0  JPARNUM(3)   
   9 6Q*2          LDX   0  ZANUM(1) 
   9 6QSL          STO   0  ANUM(3)  
   9 6R#=          ERROR    ERALREX  
   9 6RRW          MFREE    CPB,CUNI 
   4 6S?G    PGO4
   8 6SR6          BNG   7  T701 
  20 6T=Q          CALL  5  RESET               [RESET BUDGETS UNLESS WORKTAPE   
   8 6TQB          BZE   7  PGO9 
   7 6W=2          PUTREP   6
   7 6WPL          UPPLUS   1
   4 6X9=    T701
   7 6XNW          PUTREP   6
   5 6Y8G          UP
   4 6YN6    PGO9
   6 6_7Q          ENDCOM
   4 6_MB    TNOD
   8 7272          BNG   7  TNOD2
   8 72LL          CALL  5  RESET
   8 736=          BZE   7  TNOD1
   7 73KW          PUTREP   6
   7 745G          UPPLUS   1
   5 74K6    TNOD1   
   9 754Q          COMERR   ERNODECKS
   5 75JB    TNOD2   
   7 7642          PUTREP   6
   5 76HL          UP
   9 773=    #              BREAKIN  
  20 77GW    XBRKIN                             [BROKEN IN BEFORE POOL TAPE GOT  
  17 782G          BNG   7  ZBRKIN              [BRANCH IF GET MACRO 
  20 78G6          CALL  5  RESET               [RESET BUDGETS UNLESS WORKTAPE   
   5 78_Q    ZBRKIN  
   8 79FB          BZE   7  PGO10
   5 79_2          UP
   5 7=DL    PGO10   
   7 7=Y=          COMBRKIN  
   5 7?CW    RESET   
   8 7?XG          SBX   5  FX1  
  10 7#C6          MHUNT    3,FILE,FABSNB
   9 7#WQ          LDX   0  ATYPE(3) 
   8 7*BB          ANDN  0  #200 
   8 7*W2          BNZ   0  RES1 
  11 7*Y4 ...      SETNCORE 4,2,FILE,FABSNB  
  11 7B26 ...      HUNT2    1,FILE,FABSNB,2  
   7 7B48 ...      LDN   0  4
   8 7B6= ...      STO   0  A1(2)
   8 7B8# ...      LDX   0  A1(1)
   8 7B=B ...      SBN   0  10   
   8 7B#D ...      BZE   0  RES2 
   8 7BBG ...      ADX   1  A1(1)
   8 7BDJ ...      SBN   1  12   
   4 7BGL ...RES2
   8 7BJN ...      ADN   1  A1+1 
  19 7BLQ ...      ADN   2  A1+1                [SET UP USERNAME IN FABSNB TO
  15 7BNS ...      MOVE  1  3                   [GET DICT ENT
   8 7BR3 ...      OPENBUDO RES1 
   7 7BT=          NGN   3  1
  10 7C#W          BUDGINCR GSPACEMT,3   
   7 7CSG          CLOSBUDG  
   4 7D#6    RES1
   8 7DRQ          ADX   5  FX1  
   7 7F?B          EXIT  5  0
   4 7FR2    PGEO
  19 7G=L          GEOERR   1,GETDIR            [UNEXPECTED REPLY FROM GETDIR
   8 7GQ=    ONE            +1   
   9 7H9W          MENDAREA 50,K99GET
   4 7HPG    #END
   8 ____ ...01303037000500000000
