   7 22FL    #OPT  K0DSCNCT=0
  14 22_=    #LIS  K0DSCNCT>K0COMMAND>K0ALLGEO>K0GREATGEO
  16 23DW    #SEG  DSCNCT                      [RICHARD GRIMWADE 
   4 23JR ...[   
  17 23NN ...[ (C)  COPYRIGHT INTERNATIONAL COMPUTERS LIMITED  1982  
   4 23SK ...[   
   7 23YG    #OPT  WELLTEST=0
   9 24D6                   8HDSCNCT 
   4 24XQ    #   
  21 25CB    #     THIS SEGMENT IMPLEMENTS THE DISCONNECT COMMAND,WHICH RESTARTS THE 
  21 25X2    #     JOB JUST BROKEN IN ON AS AN OFFLINE JOB,ALSO LOGGING IN AS A NEW O
  20 26BL    #     JOB IF REQUESTED,USING ROUTINES IN SEGMENT LOGIN TO DO THIS . 
   4 26W=    #   
   8 27*W    #     ENTRY POINTS  
   4 27TG    #   
  17 28*6          SEGENTRY K1DSCNCT,XK1        [ DC COMMAND DIRECT  
  17 28SQ          SEGENTRY K2DSCNCT,XK2        [ ENTRY FROM ENWELL  
  18 29#B          SEGENTRY K3DSCNCT,XK3        [ UPDATE JWELL FOR CN/DC 
  11 29D? ...      FSHENTRY K4DSCNCT,XK4,,XK4
  11 29J8 ...      FSHENTRY K5DSCNCT,XK5,,XK5
  11 29N5 ...      FSHENTRY K7DSCNCT,XK7,,XK7
   9 29S2          SEGENTRY K99DSCNCT
   4 29XX ...[   
  19 2=3S ...[     SEGENTRY K25DSCNCT USED BY RESTORE TIME MACROS PRESETRP   
  20 2=7P ...[     SEGENTRY K70DSCNCT IS USED BY RESTORE TIME MACRO REPORTNEST   
   4 2=?L    #   
   6 2=R=    PLINKLOGIN  
   9 2?=W          PAIR     ENWELL,2 
   6 2?QG    PLINKCOMPRO 
   9 2#=6 ...      PAIR     COMPROA,1
  20 2#PQ    PCONTEXT       #22012              [CONTEXT-NOT BRK-IN,TENT.ST. ,MOP
  15 2*9B                                       [AND NO-USER.
   9 2*F8 ...RCONTEXT       #20012   
   9 2*K5 ...      SEGENTRY K25DSCNCT
   9 2*P2    PRAMXMSK       +AMXMSKPR
   8 2B8L    XONE           +1   
   8 2BN=    TWO            +2   
   8 2BY4 ...THREE          +3   
   4 2C7W    #   
   4 2CMG    #   
   4 2D76    #   
   4 2DLQ    #   
   4 2F6B    XK1 
  15 2FL2          PARUNACC                     [GET JOB NAME
  10 2G5L          HUNT     3,CPB,CUNI   
   9 2GK=          LDX   7  ANUM(3)  
   9 2H4W          BNG   7  NOJOBNAME
  18 2HJG          BZE   7  NOJOBNAME           [J NO JOB NAME SPECIFIED 
   9 2J46          ACROSS   INITJOB,1
  18 2LG=    XK2                                [RE-ENTRY FROM INITJOB   
   8 2L_W          GETACT   CPAT 
  18 2MFG          LDX   3  BPTR(2)             [GET AND LOCK A NEW CPA  
   9 2M_6 ...      LDX   0  ACTNUM(3)
   9 2NDQ ...      STO   0  EXT+1(2) 
   9 2PD2          LDX   0  AWORK2(2)
   4 2PXL    [   
  14 2QC=    [     -3 FOR DC ENDING IN NO-USER CONTECT   
   4 2QWW    [   
   7 2RBG          SBN   0  1
   9 2RW6          STO   0  AWORK2(3)
   7 2S*Q          FJOCA    2
  10 2STB          LDN   5  CPREFIX(3)   
   9 2T*2          LDN   4  JUSER(2) 
   7 2TSL          MOVE  4  3
  19 2W#=          LINKSET  3,PLINKLOGIN(1)     [SET LINK TO ENTER AT K3LOGIN
  20 2WRW          LDX   5  PCONTEXT(1)         [SET INITIAL CONTEXT=NOT BREAKIN,
  15 2X?G          STO   5  CONTEXT(3)          [MOP,NO USER 
   8 2XR6          LDX   2  FX2  
  11 2Y=Q          HUNT     1,ADATA,CSTORE   
   8 2YQB          BNG   1  NOCH 
   7 2_=2          CHAIN 1,3 
  10 2_PL    NOCH  HUNT     3,CPB,CUNI   
  19 329=          CHAIN    3,BPTR(2)           [GIVE BLK CONTAINING JOB NAME
   9 3GSG          LDX   3  BPTR(2)  
  18 3H#6          HUNT     1,JWELL,COPYSYS     [ RECHAIN IF NECESSARY   
  15 3HRQ          BNG   1  XCHAN               [ J IF NOT   
  16 3J?B          CHAIN    1,3                 [ ELSE RECHAIN   
  20 3JR2    XCHAN CHAINJWELL                   [ RECHAIN ANY OTHER JWELL BLOCKS 
  19 3JRD ...#                          MAKE AND GIVE COPY OF PARAMETER BLK  
  10 3JRW ...     MHUNTW    3,CPB,CALAS  
  10 3JS# ...      LDX   4  ALOGLEN(3)   
  17 3JSQ ...      GETCORE  4,1                 [GET BLK SAME SIZE   
   7 3JT8 ...      FINDCORE 3
  10 3JTL ...      NAME     3,CPB,CALAS  
  19 3JW4 ...      CHAIN    3,BPTR(2)           [CHAIN AT END OF NEW ACTIVITY
  10 3JWG ...     MHUNTW    3,CPB,CALAS  
   8 3JWY ...      LDN   4  A1(3)
  16 3JXB ...      SMO      BPTR(2)             [ADDR OF NEW CPB 
   8 3JXS ...      LDN   5  A1   
  21 3JY= ...      LDX   1  ALOGLEN(3)          [COPY CONTENTS OF OLD CPB TO NEW CPB 
   8 3JYN ...      MOVE  4  0(1) 
   9 3J_6 ...      SMO      BPTR(2)  
   9 3J_J ...      STOZ     CPBCPLEV 
   8 3K22 ...      LDX   0  GSIGN
   8 3K2D ...      SMO   BPTR(2) 
   9 3K2W ...      STO   0  CPBFSD   
   9 3K3# ...      SMO      BPTR(2)  
   9 3K3Q ...      STOZ     CMFMSK   
  18 3K48 ...#                          MAKE AND GIVE COPY OF LINE BUFFER
   8 3K4L ...     MHUNTW    3,CLB
  10 3K54 ...      LDX   4  ALOGLEN(3)   
  17 3K5G ...      GETCORE  4,1                 [GET BLK SAME SIZE   
   7 3K5Y ...      FINDCORE 3
   8 3K6B ...      NAME     3,CLB
  19 3K6S ...      CHAIN    3,BPTR(2)           [CHAIN AT END OF NEW ACTIVITY
   8 3K7= ...     MHUNTW    3,CLB
   8 3K7N ...      LDN   4  A1(3)
  16 3K86 ...      SMO      BPTR(2)             [ADDR OF NEW CLB 
   8 3K8J ...      LDN   5  A1   
  21 3K92 ...      LDX   1  ALOGLEN(3)          [COPY CONTENTS OF ORIGINAL CLB INTO  
  14 3K9D ...      MOVE  4  0(1)                [NEW CLB 
   4 3K=L    [   
  18 3KQ=          LDX   6  JOBNO(2)            [ NOW UPDATE JWELL ETC...
  10 3L9W          CALL  7  WELLAMMEND   
   4 3LPG    [   
  20 3S5B    #                          COMPLETE SETTING UP OF NEW CPA AND ADJUST
  18 3SK2    #                          CURRENT CPA(I.E. MAKE OFF-LINE)  
   7 3SR= ...      LDX   3  2
  11 3S_G ...XFIND FINDACTFB 3,,CPAT,,,BPTR  
   9 3T7Q ...      LDX   0  ACTNUM(3)
  11 3TB2 ...      BXU   0  EXT+1(2),XFIND   
   9 3TJ=          LDX   5  AMXMSK(2)
  21 3W3W          STO   5  AMXMSK(3)           [IF STILL LOGGED IN, PASS ON REPORT S
   6 3W4R ...#UNS CMXMSK 
   4 3W5N ...(   
  19 3W6K ...      HUNT2J   1,CPB,CALAS,3,NOCPB        [FIND NEW CPB/CALAS   
   4 3W7G ...[   
   9 3W8C ...      SEGENTRY K70DSCNCT
  20 3W9# ...      BRN      XK71                 [THIS IS MENDED BY REPORTNEST   
   9 3W=9 ...      STO    5 CMXMSK(1)
   8 3W?6 ...      BRN      XK72 
   4 3W#3 ...[   
  19 3W#Y ...NOCPB GEOERR   1,CPBCALAS           [BREAK IF BLOCK NOT FOUND   
   4 3W*T ...[   
   4 3WBQ ...XK71
   9 3WC8 ...      SEGENTRY K71DSCNCT
  19 3WCM ...      STOZ     CMXMSK(1)            [ZERO IF REPORTNEST NOT ON  
   4 3WDJ ...XK72
   4 3WGB ...)   
   8 3WHG    #     J IF HLS ON   
  11 3X36          BSOFF    HLSBIT,SETCPAS   
   8 3XGQ          LDN   5  #4000
  10 3Y2B          ANDX  5  CONTEXT(2)   
  20 3YG2          BZE   5  SETCPAS             [J IF MOP JOB NOT FULLY STARTED  
   8 3Y_L          LDN   5  #6000
  20 3_F=          ERS   5  CONTEXT(3)          [MAKE NEW MOP ACT FULLY STARTED  
  17 3_YW    #     DISCONNECT WITH NO JOBNAME JOINS MAIN PATH HERE   
  13 42DG    #     THE NEW ACTIVITY HAS NO JOBLOCK   
   4 42Y6    TBCT
  18 43CQ          LDX   5  HLSCOUNT+1          [LOAD BACKGROUND COUNT   
  10 43XB          TXL   5  BACKGROUND   
  20 44C2          BCC      NOMORE              [J IF NO. OFFLINE JOBS NOT<LIMIT 
  17 44WL          ADN   5  1                   [INCREASE COUNT BY 1 
  18 45B=          STO   5  HLSCOUNT+1          [STORE NEW VALUE OF COUNT
   4 45G7 ...#   
   9 45TW          BRN      SETCPAS  
   5 46*G    NOMORE  
   8 46T6          LDN   5  #6000
  21 47#Q          ERS   5  CONTEXT(2)          [MAKE BACKGROUND JOB TENT. STARTED   
   7 47CW ...      FJOCA    2
   7 47H2 ...      NGNC  0  1
  18 47L6 ...      ANDS  0  JURGE(2)            [CLEAR JOBBLOCK FS BIT   
   8 47P= ...      LDX   2  FX2  
   5 47SB    SETCPAS 
   8 48RL          CNSOURCE 3,4  
   9 49?=          LDX   4  AWORK1(2)
  20 49QW          STO   4  AWORK1(3)           [MOVE ACROSS CONTEXT PRIVILEGES  
   9 4==G          STOZ     AMXMSK(2)
   6 4=#4 ...#UNS CMXMSK 
   4 4=*L ...(   
   9 4=C8 ...      SEGENTRY K72DSCNCT
   8 4=DQ ...      BRN      XK73 
   7 4=G# ...      SETCMXSK 2
   8 4=HW ...      LDX   2  FX2  
   4 4=KD ...XK73
   9 4=M2 ...      SEGENTRY K73DSCNCT
   4 4=NJ ...)   
  10 4=Q6          LDX   5  JOBEVENTS(2) 
  10 4?9Q          STOZ     JOBEVENTS(2) 
  10 4?PB          STO   5  JOBEVENTS(3) 
  21 4#92          LDN   5  #14                 [CHANGE CONTEXT OF CURRENT JOB FROM  
  16 4#NL          ERS   5  CONTEXT(2)          [MOP TO OFFLINE  
   7 4*8=          FJOCA    2
   8 4*MW ...      LDCT  5  #500 
   9 4J32          ORS   5  JMISC(2) 
   8 4J88 ...      LDCT  5  #100 
  19 4J*B ...      ERS   5  JMISC(2)            [SET OFFLINE AND CLEAR B-IN  
   7 4JGL ...      LDX   2  3
  20 4K_G          FPUT                         [ENTER AT K1COMPRO OR K3LOGIN)   
  15 4LF6          BSON     HLSBIT,XNOTFS       [J IF PRE-HLS
  17 4LYQ    #     INFORM THE JOB STARTER PROGRAM OF THE CHANGE OF   
  12 4MDB    #     STATUS OF THE CONNECTED JOB   
  13 4QK2 ...      HLSINFORM XHLSB,CONNECT,JOBNO(2)  
  10 4QLP ...XHLSB LDX   0  CONTEXT(2)   
   8 4QND ...      ANDN  0  #4000
   9 4QQ7 ...      BZE   0  XNOTFS   
   9 4QTQ          LDX   4  HLSCOUNT 
   7 4R*B          SBN   4  1
   8 4RT2          BPZ   4  XOK  
  10 4S#L          GEOERR   1,HLSCOUNT   
   9 4SS=    XOK   STO   4  HLSCOUNT 
   7 4T?W          LDN   4  1
  10 4TRG          ADS   4  HLSCOUNT+1   
   4 4TXC ...#   
   5 4W?6    XNOTFS  
   8 4WQQ          OUTPAR TIMENOW
   7 4X=B          OUTMILL   
   7 4XQ2          LDN   4  1
  10 4Y9L          LDN   5  JPDJOBTYPE   
  10 4YP=          LDX   7  JSOURCE3(2)  
   9 4_8W          BZE   7  XCENTRAL 
   9 4_NG ...      LDN   3  THREE(1) 
   9 5286          CALL  7  XOUTPACK 
  10 52MQ          LDN   3  JSOURCE3(2)  
   9 537B          LDN   5  JPDPROPS 
   9 53M2          CALL  7  XOUTPACK 
   9 546L          BRN      PARAMOUT 
   5 54L=    XCENTRAL
   9 555W          LDN   3  XONE(1)  
   9 55KG          CALL  7  XOUTPACK 
   8 5656          OUTPAR   SKIP 
   5 56JQ    PARAMOUT
   8 574B          LDN   0  #400 
  10 57J2          ANDX  0  CONTEXT(2)   
   8 583L          BNZ   0  XSK  
   7 58H=          FJOCA 3,2 
  10 592W          LDX   0  HCLOCKTIME(3)
  11 59GG          LDX   1  HCLOCKTIME+1(3)  
   8 5=26          BNZ   0  NSK1 
   7 5=FQ          SRA   1  5
   8 5=_B          SBN   1  1875 
   8 5?F2          BNG   1  XSK  
   4 5?YL    NSK1
   9 5#D=          OUTPAR   PROGMILL 
   8 5#XW          BRN      NSK  
   4 5*CG    XSK 
   8 5*X6          OUTPAR   SKIP 
   4 5BBQ    NSK 
   8 5BWB          MONOUT JTYPE  
   7 5CB2          PERCOUNT  
  10 5CNB ...      VFREEW   APERI,ASSOCS0
  20 5D2S ...      ACROSS   CONTINUE,3          [TERMINATE BREAKIN IN CURRENT JOB
   4 5D*=    #   
   4 5DSW    #   
   6 5F#G    NOJOBNAME   
   7 5FS6          FREECORE 3
   7 5G?Q    #SKI  WELLTEST  
   9 5GRB          RGTRACE  9,XBRK   
  10 5H?2          ADDJOBJ  XDEAD,XBRK   
   6 5HQL          SUBJOB
   9 5J==          LDX   6  JOBNO(2) 
  10 5JPW          CALL  7  WELLAMMEND   
  15 5K9G          GETACT   CPAT                [GET CPA BLK 
   9 5KP6          LDX   3  BPTR(2)  
  10 5L8Q ...      LDX   5  RCONTEXT(1)  
  10 5N7=          STO   5  CONTEXT(3)   
  20 5NLW          LINKSET  3,PLINKCOMPRO(1)    [SET LINK TO ENTER AT K1COMPRO   
  10 5P6G          LDX   5  PRAMXMSK(1)  
  21 5PL6          STO   5  AMXMSK(3)           [SET REPORT STYLES TO ALLBUT,COMMANDS
   4 5PTY ...[   
  14 5Q5Q          BSOFF    HLSBIT,SETCPAS      [J IF HLS
   8 5QKB          LDN   5  #4000
  10 5R52          ANDX  5  CONTEXT(2)   
  20 5RJL          BZE   5  SETCPAS             [J IF MOP ACT. NOT FULLY STARTED 
  17 5S4=          LDN   5  1                   [ REDUCE MOP COUNT   
  14 5SHW          SBS   5  HLSCOUNT            [ BY 1   
  16 5T3G          LDX   5  HLSCOUNT            [ GET NEW COUNT  
   9 5TH6          TXL   5  IMOPLIMIT
  19 5W2Q          BCC      TBCT                [J IF COUNT NOT < LIMIT SET  
  20 5WGB          FON      #31                 [WAKE UP WAITING MOP ACTIVITIES  
   4 5WL? ...#   
   8 5X22          BRN      TBCT 
   4 5XFL    #   
   6 5YDW    XDEAD ENDCOM
   7 5YYG    XBRK  COMBRKIN  
   5 5_D6    XOUTPACK
   8 5_XQ          SBX   7  FX1  
   8 62CB          OUTPACKX 3,4,5
   8 62X2          ADX   7  FX1  
   7 63BL          EXIT  7  0
   4 63W=    [   
   4 64*W    [   
   4 64TG    [   
   4 65*6    [   
  18 65SQ    [     CHANGE STATE OF JOB BEING CONNECTED OR DISCONNECTED   
   7 66#B    [     X6 = JOBNO
   4 66S2    [   
   6 67?L    WELLAMMEND  
   8 67R=          SBX   7  FX1  
   9 68=W          TRACE    6,AMMEND 
  16 68QG ...OFF   POP      OFF,,JWACCESS       [ SET SEMAPHORE  
  11 69=6 ...      OPENSYS  ,JOBLIST,GENERAL 
   9 69PQ          GETJOB   6,SYSTEM 
  16 6=9B          TESTREP2 OK,RD               [ J IF JOB FOUND 
   9 6=P2          GEOERR   1,NO JOB?
  17 6?8L    RD    READAGAIN                    [ READ JOBLIST RECORD
  10 6?N=          MHUNTW   3,FILE,FRB   
  15 6#7W          NAME     3,FILE,FWB          [ FOR REWRITE
   7 6#MG          JLADJUST 3
  19 6*76          LDN   0  1                   [ CHANGE MOP TO BACK OR....  
  15 6*LQ          ERS   0  JLSTAT(3)           [ VICE VERSA 
  16 6B6B          REWRITE                      [ REWRITE RECORD 
  16 6BL2          MHUNT    3,FILE,FWB          [ REFIND RECORD  
  19 6C5L          NAME     3,JWELL,COPYSYS     [ FOR UPDATE OF JWELL BLOCK  
  15 6CK=          JWELLADD LIVE                [ ADD NEW JOB
  11 6D4W          MHUNT    3,JWELL,COPYSYS  
   7 6DJG          JLADJUST 3
   7 6F46          LDN   0  1
   9 6FHQ          ERS   0  JLSTAT(3)
  16 6G3B          JWELLSUB LIVE                [ REMOVE OLD JOB 
   4 6GH2    [   
  19 6H2L    [     NB... JWELLSUB GOES DOWN TO JWUPDATE WHICH DOES A JSASCAN 
  19 6HG=    [     CLOSES JOBLIST AND RELEASES THE SEMAPHORE..... AS WELL AS 
  11 6H_W    [     UPDATING THE JWELL BLOCK. 
   4 6JFG    [   
  17 6J_6          MFREE    JWELL,COPYSYS       [ FREE JWELL BLOCK   
   4 6K53 ...#   
   8 6KDQ          ADX   7  FX1  
  14 6KYB          EXIT  7  0                   [ RETURN 
   4 6LD2    [   
   4 6LXL    [   
  16 6MC=    XK3   CALL  7  WELLAMMEND          [ PERFORM UPDATE 
   5 6MWW          UP
   4 6NBG    [   
   4 6NW6    [   
   4 6P*Q    #END
   8 ____ ...55521025000200000000
