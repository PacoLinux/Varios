   9 22FL    #LIS           K0QUESCOM
  14 22KW ...      SEG    QUESCOM,867,SECTION CENT,,G374 
   4 22Q6 ...[   
  15 22WB ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  13 232L ...[     THIS EXCLUDES CODE UNDER #SKI G374
   4 236W ...[   
   7 23?6 ...#OPT G374 = 0   
   6 23CB ...#SKI G374&1 
  18 23HL ...# WITH G3UG EDIT M374 (QUESTION AND ANSWER BY JOB NUMBER)   
   4 23MW ...[   
   4 23S6 ...[   
  11 23YG          SEGENTRY K1QUESCOM,SENTRY1
  10 24D6    # PARAMETERS OF THE SEGMENT 
  18 24XQ    #DEF           VLENGTH=11          [LENGTH OF 'DISPLAY'BLOCK
  17 25CB    #DEF           VCHARS=A1           [POSN OF CHAR COUNT  
  19 25X2    #DEF           VMESS=A1+1          [& MESSAGE IN'DISPLAY'BLOCK  
   8 26BL    Q1             +1   
   8 26W=    Q4             +4   
   9 27*W    ZJ1            +JZQUMOP 
   9 27TG    ZJ2            +JZQUESM 
   9 28*6    ZJ3            +JZQUESC 
   9 28SQ    ZJ4            +JZANSM  
  20 29#B    Q41            +41                 [MAX LENGTH OF 'DISPLAY' MESSAGE 
   8 29S2    QAST           #32  
   9 2=?L    QMINUS1        #77777777
   9 2=R=    QMASK          #77000000
   4 2?34 ...[   
  19 2?=W    # QUESTION COMMAND.A TEST IS FIRST MADE TO DETERMINE WHETHER THE
  21 2?QG    # QUESTION HAS BEEN ALREADY ASKED,SINCE BREAKIN CAUSES'QUESTION'TO BE   
  19 2#=6    # REOBEYED.IF NOT,A QUESTION BLOCK IS CREATED TO SAY THAT THE   
  20 2#PQ    # QUESTION HAS BEEN ASKED.AFTER OUTPUTTING THE QUESTION LOCALLY AND,
  19 2*9B    # WHERE APPROPRIATE,TO THE OPERATOR AS ROUTED,THE ACTIVITY IS   
  21 2*P2    # LEFT IN A WAITING STYLE.WHEN WOKEN UP BY'ANSWER',THIS ANSWER IS SET   
  20 2B8L    # AS THE CURRENT DISPLAY.THE MONITORING FILE IS ALSO GIVEN A COPY OF
   9 2BN=    # QUESTION AND ANSWER   
   4 2BTD ...[   
   4 2C2L ...[   
   5 2C7W    SENTRY1 
  11 2CMG    #SKIP          K6QUESCOM>99-99  
  10 2D76          TRACE    1,QUESTION   
  10 2DLQ          LONGSET  FORANS,ZA13  
  11 2F6B          HUNTW    3,APED,AQUESTION 
  19 2FL2          BNG   3  ZB7                 [JUMP IF NO QUESTION BLOCK   
   9 2G5L          LDX   6  VCHARS(3)
  10 2GK=          TXU   6  QMINUS1(1)   
  18 2H4W          BCC      ZB12                [JUMP UNLESS ANSWER GIVEN
   7 2HJG          MARKAXES  
  11 2J46          HUNTW    3,APED,AQUESTION 
   8 2JHQ          BRN      ZB8  
   4 2JRJ ...[   
  16 2K3B    ZB12  LDX   4  VMESS(3)            [ROUTING PARAM   
  21 2KH2          PARAPASS                     [TO AVOID'PARAM UNACCESSED'MESSAGE   
   8 2L2L          BRN      ZB1  
   4 2L=D ...[   
  18 2LG=    ZB7   CALL  3  XDEST               [ANALYSE ROUTING PARAM   
   8 2L_W          TXU   4  Q4(1)
  19 2MFG          BCC      ZA14                [IF WAS ZERO,SEE IF MOP JOB  
   4 2MJL ...[   
   6 2MMQ ...#SKI G374&1 
  12 2MQW ...ZB13  SETNCORE 22,3,APED,AQUESTION  
   7 2MW2 ...#SKI G374&1$1   
  17 2M_6    ZB13  SETNCORE 19,3,APED,AQUESTION [MAKE QUESTION BLOCK 
  10 2NDQ          LDX   7  QMINUS1(1)   
  18 2NYB          STO   7  VCHARS(3)           [SET INDICATOR IN BLOCK  
  20 2PD2          STO   4  VMESS(3)            [ROUTING PARAM TO QUESTION BLOCK 
   8 2PXL          TXU   4  Q4(1)
  21 2QC=          BCC      ZB1                 [IF WAS ZERO,JOB AND USER NOT REQD.  
   9 2QWW          LDN   7  A1+11(3) 
  20 2RBG          CJOBLOCK 2                   [JOB & USER NAMES FROM JOB BLOCK 
  18 2RW6          FLENME   JUSER,JNAME         [TO APED/AQUESTION BLOCK 
   9 2S*Q          LDX   4  VMESS(3) 
  17 2STB    ZB15  BCHX  3  /                   [STEP CHARACTER PTR  
   9 2T*2          LDCH  0  A1+12(3) 
   8 2TSL          SBN   0  #36  
  16 2W#=          BNZ   0  ZB15                [JUMP UNLESS '.' 
   8 2WRW          LDN   0  #34  
  17 2X?G          DCH   0  A1+12(3)            [CHANGE '.' TO ','   
   4 2X?S ...[   
   4 2X#6 ...[   
   6 2X#D ...#SKI G374&1 
   4 2X#Q ...(   
  11 2X*4 ...#     MEND EXCHANGE SCHEME CODE 
   4 2X*B ...#   
   4 2X*N ...#   
  16 2XB2 ...#     NOW WE ADD (JOBNO) TO THE APED/AQUESTION BLOCK
   4 2XB# ...#   
  11 2XBL ...      HUNTW    3,APED,AQUESTION 
   8 2XBY ...      SMO      FX2  
   8 2XC= ...      LDX   4  JOBNO
   8 2XCJ ...      SMO      FX1  
   9 2XCW ...      MPY   4  XMAGIC   
   4 2XD8 ...#   
   9 2XDG ...      LDX   1  A1+11(3) 
   7 2XDS ...      SRC   1  2
   9 2XF6 ...      ADN   1  A1+12(3) 
   7 2XFD ...      LDN   0  1
   4 2XFQ ...#   
   8 2XG4 ...      LDN   7  #20  
   8 2XGB ...      DCH   7  0(1) 
   7 2XGN ...      BCHX  1  /
   9 2XH2 ...      ADS   0  A1+11(3) 
   8 2XH# ...      LDN   7  #30  
   8 2XHL ...      DCH   7  0(1) 
   7 2XHY ...      BCHX  1  /
   9 2XJ= ...      ADS   0  A1+11(3) 
   7 2XJJ ...      MODE     0
   7 2XJW ...      LDN   6  0
   7 2XK8 ...      LDN   7  7
   7 2XKG ...OPCSA CBD   4  6
   8 2XKS ...      BZE   6  OPCSB
   8 2XL6 ...      ORN   6  #100 
   8 2XLD ...      DCH   6  0(1) 
   7 2XLQ ...      BCHX  1  /
   9 2XM4 ...      ADS   0  A1+11(3) 
   8 2XMB ...OPCSB BCT   7  OPCSA
   4 2XMN ...#   
   8 2XN2 ...      LDN   7  #31  
   8 2XN# ...      DCH   7  0(1) 
   9 2XNL ...      ADS   0  A1+11(3) 
   9 2XNY ...      LDX   4  VMESS(3) 
   8 2XP= ...      BRN      ZB1  
   9 2XPJ ...XMAGIC         +7036875 
   4 2XPW ...)   
   4 2XQ8 ...[   
   4 2XQG ...[   
   7 2XR6    ZB1   SPARAPASS 
  17 2Y=Q          HUNT     2,CPB,CUNI          [FIND SECOND PARAM   
   9 2YQB          LDX   5  ANUM(2)  
  15 2_=2          TXU   5  QMINUS1(1)          [NON EXISTANT
   8 2_PL          BCS      ZB2  
   7 329=          LDN   5  0
   8 32NW    ZB2   ANDN  5  #7777
   9 338G          TXL   5  Q41(1)   
  19 33N6          BCS      ZB14                [JUMP IF LESS THAN 40 CHARS  
   8 347Q          LDN   5  40   
  11 34MB    ZB14  OUTPARAM 5,APARA,CPB,CUNI 
   8 3572          TXU   4  Q4(1)
   8 35LL          BCS      ZB4  
  20 366=          MONOUTX  ZJ1(1)              [QUESTION NOT SENT TO OPERATORS  
   8 36KW          BRN      ZB3  
  19 375G    ZB4   MONOUTX  ZJ2(1)              [QUESTION TO MONITORING FILE 
  20 37K6    ZB3   TXU   4  Q4(1)               [JUMP IF OPERATOR O/P REQUIRED   
   8 384Q          BCS      ZA11 
  16 38JB    ZA10  LONGSTOP ZB6,ZA10            [WAIT FOR ANSWER 
   8 3942          BRN      ZA12 
   4 394P ...[   
   4 395D ...[   
   4 3969    ZA11
   6 39#W ...#SKI G374&1 
   8 39HH ...      LDN   7  22   
   7 39Q8 ...#SKI G374&1$1   
   8 39YT          LDN   7  19   
   7 3=3=          OUTBLOCK 7
  11 3=GW          OUTPARAM 5,APARA,CPB,CUNI 
  11 3?2G          HUNTW    3,APED,AQUESTION 
  21 3?G6          OUTPARAM A1+11(3),A1+12,APED,AQUESTION  [QUESTION TO OPS REP TILL 
  17 3?_Q          DEMANDX  4,ZJ3(1),,ZB6,ZB10,1    [ANSWER IS GIVEN 
  10 3#FB    ZA12  LONGSET  FORANS,ZA13  
  17 3#_2          HUNTW    3,APED,AQUESTION    [FIND QUESTION BLOCK 
   9 3*DL          LDX   6  VCHARS(3)
  10 3*Y=          TXU   6  QMINUS1(1)   
  17 3BCW          BCS      ZB8                 [JUMP IF ANSWER GIVEN
  16 3BXG          TXU   4  Q4(1)               [ANSWER NOT GIVEN
   8 3CC6          BCS      ZB10 
   8 3CWQ          BRN      ZA10 
   4 3D6J ...[   
  19 3DBB    ZA13  GEOERR   1,LWSET             [LONGWAIT FIELD ALREADY SET  
   4 3DL8 ...[   
   4 3DW2    ZB8 
  11 3F*L          HUNTW    2,APED,ADISPLAY  
  18 3FT=          NAME     3,APED,ADISPLAY     [RENAME QUESTION BLOCK   
   8 3G#W          BNG   2  ZB5  
  20 3GSG          FREECORE 2                   [REMOVE ANY OTHER DISPLAY BLOCK  
  17 3H#6    ZB5   ALTLEND  3,11                [FREE WORKING SPACE  
  12 3HRQ          OUTPARAM 6,VMESS,APED,ADISPLAY
  19 3J?B          MONOUTX  ZJ4(1)              [ANSWER TO MONITORING FILE   
  11 3JR2    #SKIP          K6QUESCOM>99-99  
   9 3K=L          TRACE    1,QUESEND
   4 3KK3    ZB9 
   7 3KXD          LONGOFF   
   4 3KYP    ZB9A
   6 3L9W          ENDCOM
   4 3L?7 ...[   
   4 3L#D ...[   
   4 3L*R    ZB6 
   7 3M59          COMBRKIN  
   4 3M5D ...[   
   4 3M8S ...[   
   8 3M96    ZA14  SMO      FX2  
   9 3MNQ          LDX   7  CONTEXT  
   4 3MTY ...[   
   4 3N36 ...[   
   8 3N8B          ANDN  7  #10  
  16 3NN2          BNZ   7  ZB13                [JUMP IF MOP JOB 
   7 3P7L    ZA15  LONGOFF   
   9 3PM=          COMERR   JZRTPAR  
   7 3Q6W    ZA1   LONGOFF   
  18 3QLG          COMERR   ERUNALLOC           [UNALLOCATED PERIPHERAL  
  21 3R66    # ANALYSES ROUTING PARAM,STORING IT AS 0,1 OR,FOR AN ONLINE PERIPHERAL, 
   9 3RKQ    # GEOG UNIT NO IN X4.   
   5 3S5B    XDEST   
   8 3SK2          SBX   3  FX1  
   8 3T4L          SMO      FX2  
   9 3TJ=          STO   3  AWORK3   
  16 3W3W          PARAPASS                     [PASS FIRST PARAM
  15 3WHG          HUNT     2,CPB,CUNI          [FIND PARAM  
   9 3X36          LDX   6  ANUM(2)  
  17 3XGQ          TXU   6  QMINUS1(1)          [NON EXISTANT PARAM  
   8 3Y2B          BCC      ZA15 
   8 3YG2          ANDN  6  #7777
  15 3Y_L          BZE   6  ZA15                [NULL PARAM  
   9 3_F=          LDCH  4  APARA(2) 
  16 3_YW          BNZ   4  ZA18                [JUMP IF NOT ZERO
   8 42DG          TXU   6  Q1(1)
   8 42Y6          BCC      ZA9  
  20 43CQ          BRN      ZA15                [ERROR IF MORE THAN 1 CHARACTER  
   4 43MJ ...[   
   8 43XB    ZA18  TXL   4  Q4(1)
  19 44C2          BCC      ZA17                [JUMP UNLESS ONE,TWO OR THREE
   8 44WL    ZA16  TXU   6  Q1(1)
   8 45B=          BCC      ZA7  
  20 45TW          BRN      ZA15                [ERROR IF MORE THAN 1 CHARACTER  
   4 465N ...[   
   4 46*G    ZA17
   9 46T6          TXU   4  QAST(1)  
  17 47#Q          BCS      ZA15                [JUMP UNLESS ASTERISK
  21 47SB          PERCON                       [CONVERT PERIPH NAME TO TYPE,UNIT NO 
  18 48#2          HUNTW    2,CPAR,GPERCON      [FIND CONVERTED PARAMETER
  20 48RL          BNG   2  ZB9                 [FINISH-ERROR REPORTED BY PERCON 
   9 4LYQ    #SKI  K6QUESCOM<99-99   
  11 4MDB          TRACE    A1(2),QUESTYUN   
   8 4MY2          SMO      FX2  
   9 4NCL          LDX   4  CONTEXT  
   8 4NX=          ANDN  4  #1000
  18 4PBW          BZE   4  ZA9                 [JUMP IF NO CORE IMAGE   
   8 4PWG          FPCACA   3,FX2
  11 4QB6          HUNT2    3,APERI,APERIRES 
  10 4QTQ          FPERENTA 3,A1(2),ZA1  
   8 4R*B          LDX   4  1(3) 
   8 4RT2          ANDX  4  GSIGN
   8 4S#L          ERX   4  GSIGN
   8 4SS=          BZE   4  ZA9  
  17 4T?W          LDN   4  2                   [CENTRAL CONSOLE ONLY
   8 4TRG          BRN      ZA7  
   4 4WGY ...[   
   7 4WQQ    ZA9   LDN   7  1
  20 4X=B          AMXTRACE 7,NOOUT             [ERROR IF NO MOP CONSOLE OUTPUT  
   4 4XQ2    ZA7 
  11 4Y9L    #SKIP          K6QUESCOM>99-99  
  10 4YP=          TRACE    4,QUESUNUM   
  20 4_8W          ADN   4  4                   [ADD 4 TO ROUTING PARAMETER FOR  
  14 4_NG                                       [CLUSTERS
   8 5286          SMO      FX2  
   9 52MQ          LDX   3  AWORK3   
   8 537B          ADX   3  FX1  
   7 53M2          EXIT  3  0
   4 53WS ...[   
   7 546L    NOOUT LONGOFF   
   9 54L=          COMERR   JMTRACE  
  10 555W          MENDAREA 20,GAPQUESCOM
   4 55KG    #END
   8 ____ ...41376221000300000000
