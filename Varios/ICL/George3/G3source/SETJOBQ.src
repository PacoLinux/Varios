   8 22FL    #OPT  K0SETJOBQ=0   
  15 22_=    #LIS  K0SETJOBQ>K0ALLGEO>K0GREATGEO>K0COMMAND   
  12 23=L ...      SEG   SETJOBQ,867,SECTION CENT
   4 23J2 ...[   
  16 23TB ...[  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982 
   4 246Q ...[   
   4 24D6    #   
  20 24XQ    #     THIS SEGMENT IS ENTERED FROM ENWELL/JSASCAN/SCHEDULE/SCHEDJOB 
  21 25CB    #     TO SET UP THE CPAT AND JOBQ BLOCKS FOR A JOB ABOUT TO BE STARTED. 
   4 25X2    #   
  11 26BL          SEGENTRY K1SETJOBQ,XENWELL
  11 26W=          SEGENTRY K2SETJOBQ,XJSA   
  11 27*W          SEGENTRY K3SETJOBQ,XHLS   
   4 27TG    #   
   5 29XX    XLINKS  
   9 2=3S          LINK     DEWELL,1 
   9 2=7P          LINK     DEWELL,2 
   7 2*P2    X1000       1000
   8 2B8L    XCONT       #22405  
   8 2BN=    MOPCONT     #20411  
   9 2D76    MASKQ       #20000000   
  13 2F6B          BITDEFS  HLSCONREC+6,20,TBEXPREQ  
  17 2KKK    XPRIVS         +APHLSEXT_APSJMESS_APNOTOWNER_APBUDGEXT  
  10 2KN8    XUSERS         12HMANAGER   
   9 2KQR                   12HSYSTEM
  10 2KTB                   12HJOURNAL   
   8 2KX_                   +0   
   4 3B6G    [   
   5 3B84    WELL3   
   8 3B9L          STO   0  GEN0 
  11 3B?8          MHUNTW   3,JWELL,COPYSYS  
   7 3B#Q          JLADJUST 3
   9 3BB#          BRN      (GEN0)   
   4 3BCW    [   
   5 3BH2    FINDOLPA
   8 3BL6          LDX   2  FX2  
  10 3BP=          FINDACTN 2,AWORK2(2)  
   7 3BSB          EXIT  7  0
   4 3BXG    [   
   4 3C25    XHLS
   7 3C4N          LDN   7  0
  20 3C7?          STO   7  ACOMMUNE1(2)        [=0 IMPLIES JOB IS BEING DEWELLED
  20 3C9W          STO   1  ACOMMUNE2(2)        [IMPLIES ENTERED FROM SCHEDJOB   
   9 3C#F          BRN      XENTER   
   5 3CC6    XENWELL 
   7 3CWQ          NGNC  0  1
   4 3DBB    XJSA
  18 3DW2          LDN   7  0                   [X7=0 IF ENTERED FROM JSA
  17 3F*L          STO   7  ACOMMUNE1(2)        [X7=1 IF FROM ENWELL 
  10 3FBX          STOZ     ACOMMUNE2(2) 
   5 3FD8    XENTER  
   8 3FFH          CALL  0  WELL3
  14 3GSG          JBS      XMOP1,3,JLBMOP      [J IF MOP
   8 3H#6          GETACT   CPAT 
   9 3HRQ          LDX   3  BPTR(2)  
   7 3J?B          ADX   1  7
  10 3JR2          LINKSET  3,XLINKS(1)  
   8 3K=L          LDCT  0  #40  
  16 3KQ=          STO   0  JOBEVENTS(3)        [INHIBIT BRK IN  
  16 3L9W          BZE   7  XMOP1               [J IF FROM JSA   
  20 3LPG          CHAINJWELL                   [ELSE PASS OVER 3 JWELL BLOCKS   
   5 3M96    XMOP1   
   9 3MNQ          LDN   4  EXTJOB   
  10 3N8B          LDX   0  ACOMMUNE2(2) 
  17 3NN2          BZE   0  NHLS1               [J IF NOT HLSDEWELL  
   9 3P7L          CALL  7  FINDOLPA 
  11 3PM=          LDX   0  HLSCONREC+7(2)   
  19 3Q6W          BNG   0  NHLS1               [J IF NO NEEDS TO GO IN JOBQ 
  13 3QLG          HUNT2J   3,ADATA,CSTORE,2,(GEOERR)
   8 3R66          ADX   4  A1(3)
   5 3RKQ    NHLS1   
  16 3S5B          SETUPCOR 4,3,JOBQE,,,1,2     [CREATE JOB BLOCK
   9 6BL2          STOZ     JOBNUM(3)
   9 6C5L          LDN   4  JOBNUM(3)
  10 6CK=          LDN   5  JOBNUM+1(3)  
  18 6D4W          MOVE  4  EXTJOB-1-JOBNUM+A1            [ CLEAR BLOCK  
   4 6DJG    [   
  11 6DKC          MHUNTW   2,JWELL,COPYSYS  
  10 6DL#          LDX   6  A1+JLJOBNO(2)
   7 6DM9          JLADJUST 2
   7 6DN6          LDX   7  2
  15 6DP3          JBC      XEASY,2,JLBMOP      [J IF NOT MOP
   8 6DPY          LDX   2  FX2  
  10 6DQT          LDX   0  ACOMMUNE1(2) 
  16 6DRQ          BNZ   0  XEASY1              [J IF FROM ENWELL
  13 6DSM    XFIND FINDACTF 2,2,CPAT,,(GEOERR),FPTR  
  13 6DTJ          HUNT2J   1,JWELL,COPYSYS,2,XFIND  
  12 6DWF          BXU   6  A1+JLJOBNO(1),XFIND  
   7 6DWQ          JLADJUST 1
  10 6DX3          BS       1,JLBRUNNING 
   9 6DXB          BRN      XEASY1   
   8 6DY?    XEASY LDX   2  FX2  
  10 6D_8          LDX   2  ACTRING+1(2) 
   9 6F25          SBN   2  ACTRING  
   5 6F32    XEASY1  
  10 6F46          LDN   1  AJBRING(3)   
  10 6FHQ          LDN   2  JOBRING(2)   
  18 6G3B          ENRING   1,2                 [ RING JOBBLOCK TO CPA   
   4 6GH2    [   
   7 6H2L          LDX   2  7
  16 6H_W          STOZ     JMISC(3)            [ STATE OF JOB   
  11 6JFG          JBS      SETJB,2,JLBMOP   
   8 6J_6          LDX   0  GSIGN
  15 6KDQ          ORS   0  JMISC(3)            [ OFFLINE JOB
   9 6KYB    SETJB LDX   0  IDEFBS   
   7 6L6L          SLA   0  1
   9 6L#W          BVCR     NOOVRFL  
   8 6LH6          LDCT  0  #377 
   5 6LPB    NOOVRFL 
   9 6LXL          STO   0  AMBS(3)  
   4 6MC=    [   
  13 6MWW    [     NB. CUBS AND GUBS ALREADY ZERO   .
   4 6NBG    [   
  19 6NCS          JMBAC    NREST,2,JLBGENRES,JLBBREAK,JLBABANDON,JLBPARTRES 
  10 6NGW          BS       3,JBRESTARTED
   5 6NH_    NREST   
   7 6NL#          LDN   7  0
  18 6NW6          JBC      SUSER,2,JLBSYSIS    [ J IF NOT SYSTEM ISSUED 
  19 6P*Q          SPRIVON  JSISSUE,3           [ SET SYSTEM ISSUED STATUS   
   9 6PKJ          LDX   7  XPRIVS(1)
   5 6PTB    SUSER   
  18 6P_?          JBCC     NSECURE,2,JLBSINFACE1 [J IF NOT SECURE USER  
  16 6Q58          BS       3,JBSINFACE1        [MARK JOB SECURE 
   5 6Q95    NSECURE 
  10 6Q*2          LDX   0  JLBUDGETS(2) 
   9 6QSL          ORS   0  JPRIV(3) 
  10 6RRW          SBX   2  JOBDATASIZE  
  10 6S?G          LDX   0  JLJOBNO(2)   
   9 6SR6          STO   0  JOBNUM(3)
   9 6Y8G          LDN   4  JLUSER(2)
   9 6YN6          LDN   5  JUSER(3) 
  15 6_7Q          MOVE  4  3                   [ USERNAME   
   9 6_8M          LDX   4  JUSER(3) 
  10 6_9J          LDX   5  JUSER+1(3)   
  10 6_=F          LDX   6  JUSER+2(3)   
   9 6_?B    XPR   TXU   4  XUSERS(1)
  10 6_#?          TXU   5  XUSERS+1(1)  
  10 6_*8          TXU   6  XUSERS+2(1)  
   9 6_B5          BCC      XGRANT   
  10 6_C2          LDX   0  XUSERS+3(1)  
   7 6_CX          ADN   1  3
   8 6_DS          BNZ   0  XPR  
   8 6_FP          BRN      XGR  
   5 6_GL    XGRANT  
   8 6_HH          SMO      FX1  
   9 6_JD          LDX   7  XPRIVS   
   8 6_K*    XGR   LDX   1  FX1  
   9 6_L=          ORS   7  JPRIV(3) 
   4 6_MB    [   
  10 7272          LDN   4  JLJOBNAME(2) 
   9 72LL          LDN   5  JNAME(3) 
  14 736=          MOVE  4  3                   [ JOBNAME
  17 73KW          CHJOBLOC 3                   [ CHAIN IN JOBBLOCK  
   8 73TN          FCAJO    2,3  
   8 73XL          CALL  0  WELL3
  16 754Q          JBC      SBITS,3,JLBMOP      [ J IF NOT MOP   
  10 75JB          SBX   3  JOBDATASIZE  
   8 75NS          LDN   0  #6000
  19 75T=          ANDX  0  CONTEXT(2)         [PRESERVE FULLY STARTED-NESS  
   8 75_N          BNZ   0  TSFS 
   8 7666          ORN   0  #2000
   4 76=J    TSFS
  10 76C2          ORX   0  MOPCONT(1)   
  17 76HL          STO   0  CONTEXT(2)          [ INITIAL MOP CONTEXT
  13 773=          BRN      XNO                 [ MOP
  15 77GW    SBITS LDX   4  XCONT(1)            [ CONTEXTS   
  18 782G          JBC      NOSYS,3,JLBSYSIS    [ J IF NOT SYSTEM ISSUED 
   8 78G6          ORX   4  GSIGN
  11 78_Q          JBC      NOSYS,3,JLBSPRIV 
   9 79FB          LDX   0  MASKQ(1) 
   9 79_2          STO   0  CLONG1(2)
  17 7=DL    NOSYS STO   4  CONTEXT(2)          [ INITIAL JOB CONTEXT
   4 7=Y=    [   
   7 7?CW          NGN   0  1
  17 7?XG          STO   0  CPLEV(2)            [ INITIAL CP LEVEL   
  10 7#C6          STOZ     JSOURCE1(2)  
  10 7#WQ          STOZ     JSOURCE2(2)  
   4 7C#W    [   
  11 7CSG          MHUNTW   3,JWELL,COPYSYS  
   9 7D#6          ADN   3  A1+FRH   
  10 7DRQ          LDX   0  JLPROPNO(3)  
  10 7F?B          STO   0  JSOURCE3(2)  
   4 7FR2    [   
  10 7FR=    XNO   LDX   6  JLJOBNO(3)   
  15 7FRG          STO   6  JOBNO(2)            [ CPAT JOBNO 
   8 7FRH          FJOCA    1,2  
  10 7FRJ          ADX   3  JOBDATASIZE  
   9 7FRY          LDX   4  BACKCP   
  18 7FS4          JMBAC    SYSPRI,3,JLBCLUS,JLBMOP [J IF CENTRAL &BACK  
   9 7FS8          LDX   4  CLUSMOPCP
  17 7FS#          JMBAS    SYSPRI,3,JLBCLUS,JLBMOP [J IF CLUSTER&MOP
  10 7FSD          LDX   4  CLUSBACKCP   
  17 7FSJ          JBS      SYSPRI,3,JLBCLUS    [J IF CLUSTER&BACK   
  16 7FSN          LDX   4  IMOPCP              [ELSE CENTRAL&MOP
   5 7FSS    SYSPRI  
  17 7FSY          JBC      NOSYSPRI,3,JLBSYSIS [J IF NOT SYS ISSUED 
   8 7FT4          LDN   4  62   
   9 7FT8          LDX   0  ISISSMP  
  16 7FT#          STO   0  AMILLP(1)           [MILL PRIORITIES 
   5 7FTD    NOSYSPRI
   9 7FTJ          DEX   4  ACOREP(1)
  19 7FTS          JBC      XNOJT,3,JLJTDF      [J IF NO JOBTIME ON JOB CARD 
  20 7FTY          BS       1,JBJOBT            [TO FORBID A FURTHER JT COMMAND  
   5 7FW4    XNOJT   
  10 7FW8          SBX   3  JOBDATASIZE  
   9 7FW#          LDX   4  JLJT(3)  
  10 7FWD          ADX   3  JOBDATASIZE  
  16 7FWJ          BNZ   4  NCV1         [J IF NON ZERO JT FIELD 
  10 7FWN          JBS      NCV1,3,JLJTDF
   9 7FWS          LDX   4  JOBTIME  
  10 7FWY    NCV1  SBX   3  JOBDATASIZE  
   8 7FX4          SMO      FX1  
   8 7FX8          MPY   4  X1000
   9 7FX#          STO   4  ATIMEJ(1)
  19 7FXD          STO   5  ATIMEJ+1(1)         [STORE JOBTIME AS MILLISECS  
   9 7FXJ          LDX   0  JLURG(3) 
   9 7FXN          STO   0  JURGE(1) 
  17 7FXS          SEGENTRY K50SETJOBQ         [FOR NEWNEEDS MACRO   
  10 7FXY          LDX   0  JLMAXSIZE(3) 
   7 7FY4          LDX   4  0
  10 7FY8          ADX   3  JOBDATASIZE  
   8 7FY#          BNZ   4  NCV2 
  10 7FYD          JBS      NCV2,3,JLMZDF
  10 7FYJ          LDX   4  COREDEFAULT  
   4 7FYN    NCV2
   9 7FYS          STO   4  JSIZE(1) 
  10 7FYW          SBX   3  JOBDATASIZE  
   8 7F_P          SMO      FX2  
   9 7F_Q          LDX   0  ACOMMUNE2
  17 7F_R          BZE   0  NHLS2               [J IF NOT HLSDEWELL  
   9 7F_S          CALL  7  FINDOLPA 
  11 7F_T          LDXC  0  HLSCONREC+2(2)   
  17 7F_W          BCS      NOCPI               [J IF NO CPI GIVEN   
   9 7F_X          STO   0  JCPI(1)  
   5 7F_Y    NOCPI   
  11 7F__          LDXC  4  HLSCONREC+3(2)   
  18 7G24          BCS      NOJOBT              [J IF NO JOBTIME GIVEN   
   8 7G28          SMO      FX1  
   8 7G2#          MPY   4  X1000
   9 7G2D          STO   4  ATIMEJ(1)
  10 7G2J          STO   5  ATIMEJ+1(1)  
   5 7G2N    NOJOBT  
  11 7G2S          LDXC  0  HLSCONREC+4(2)   
  18 7G2Y          BCS      NOURGE              [J IF NO URGENCY GIVEN   
   9 7G34          STO   0  JURGE(1) 
   5 7G38    NOURGE  
  11 7G3#          LDXC  0  HLSCONREC+5(2)   
  18 7G3D          BCS      NOMAXS              [J IF NO MAXSIZE GIVEN   
   9 7G3J          STO   0  JSIZE(1) 
   5 7G3N    NOMAXS  
  15 7G4Y          LDX   6  HLSCONREC+6(2)      [STATUS WORD 
  15 7G54          BNG   6  SETEP6              [J IF DEFAULT
   7 7G58          SRC   6  4
  16 7G5#          ERX   6  GSIGN               [UNSET B0 IF EP  
   5 7G5D    SETEP6  
  11 7G5J          LDXC  0  HLSCONREC+10(2)  
  18 7G5N          BCC      TCPR                [J IF CORE PRIORITY GIVEN
  17 7G5S          BNG   6  NOCOREP             [J IF NOT EXPRESSED  
   8 7G5Y          LDN   0  60   
   9 7G64    TCPR  DEX   0  ACOREP(1)
   5 7G68    NOCOREP 
   7 7G6#          LDN   4  4
  18 7G6D    TMPR2 LDCH  5  HLSCONREC+11(2)     [MILL PRIORITY OF MEMBER 
  18 7G6J          BNZ   5  TMPR4               [J IF MILL PRIORITY GIVEN
  17 7G6N          BNG   6  TMPR8               [J IF NOT EXPRESSED  
  18 7G6S          LDN   5  59                  [DEFAULT MILL PRIORITY   
   9 7G6Y    TMPR4 DCH   5  AMILLP(1)
   7 7G74    TMPR8 BCHX  1  /
   7 7G78          BCHX  2  /
   8 7G7#          BCT   4  TMPR2
   7 7G7D          SBN   1  1
   7 7G7J          SBN   2  1
  11 7G7S          LDXC  0  HLSCONREC+7(2)   
  18 7G7Y          BCS      NOPERIS             [J IF NO PERI NEEDS GIVEN
  13 7G84          HUNT2J   2,ADATA,CSTORE,,(GEOERR) 
   8 7G88          LDN   4  A1(2)
   8 7G8#          LDX   0  A1(2)
   8 7G8D          LDX   2  FX2  
  10 7G8J          LDN   5  JBPERIS(1)   
   7 7G8N          SMO      0
  17 7G8S          MOVE  4  0                   [MOVE NEEDS INTO JOBQ
  11 7G8Y          LDN   5  JBPERIS-JOBNUM   
  18 7G94          STO   5  JNEEDSPTR(1)        [STORE POINTER TO NEEDS  
  14 7G98          CALL  7  FINDOLPA            [X2->OLPA
   5 7G9#    NOPERIS 
  11 7G9D          LDX   6  HLSCONREC+6(2)   
   8 7G9J          FCAJO    2,1  
   8 7G9N          BNG   6  NOTFS
   7 7G9S          LDX   5  6
   8 7G9Y          ANDN  5  #14  
  17 7G=4          BZE   5  NCHEP               [J IF NOT CC EP OR EP
  10 7G=8          BS       1,JBEXPREQ   
   8 7G=#          ANDN  5  #10  
  16 7G=D          BNZ   5  NCHEP               [J IF NOT CC EP  
  10 7G=J          BC       1,JBEXPREQ   
   7 7G=N    NCHEP ANDN  6  2
  20 7G=S          BZE   6  NOTFS               [J IF NOT REQUIRED TO BECOME F/S 
   9 7G=Y          LDX   0  JURGE(1) 
   8 7G?4          BNZ   0  NODEF
  21 7G?8          LDN   0  #55                 [TAKE 'M' AS DEFAULT IF NONE GIVEN   
   5 7G?#    NODEF   
   8 7G?D          ORX   0  GSIGN
   9 7G?J          STO   0  JURGE(1) 
   8 7G?N          LDN   0  #6000
  16 7G?S          ERS   0  CONTEXT(2)          [SET F/S IN CPAT 
   7 7G?Y          LDN   0  1
  10 7G#4          LDX   6  CONTEXT(2)   
   7 7G#8          ANDN  6  4
  19 7G##          SRL   6  2                   [X6=0 FOR MOP; =1 FOR BACK   
   7 7G#R          SMO      6
   9 7G#S          ADS   0  HLSCOUNT 
   5 7G#T    NOTFS   
   5 7G#W    NHLS2   
   5 7G#X    #UNS ICT
   4 7G#Y    (   
  12 7G#_    [     SET ACTIVITY PRIORITY OF JOB  
   9 7G*2    [     X1=AD OF JOB BLOCK
   8 7G*3          SMO      FX2  
   9 7G*4          LDX   0  ACOMMUNE2
  17 7G*5          BZE   0  NHLS3               [J IF NOT HLSDEWELL  
  10 7G*6    [     GET HLS CONTROL RECORD
   9 7G*7          CALL  7  FINDOLPA 
  21 7G*8          LDCH  4  HLSCONREC+10(2)      [ACTIVITY PRIORITY IN B0-5 OF WORD9 
   8 7G*9          ANDN  4  #37  
   8 7G*=          LDN   0  #20  
  13 7G*?          ANDX  0  4       [KEEP FIXING BIT 
  14 7G*#          SLL   0  1       [PUT INEXPECTED PLACE
  14 7G**          ORX   4  0       [READY FOR APJOBSET  
   8 7G*B          BRN      NHLS4
   4 7G*C    [   
   5 7G*D    NHLS3   
  16 7G*F          LDN   4  0                   [SET FOR DEFAULT 
   4 7G*G    [   
   5 7G*H    NHLS4   
   9 7G*J          LDX   5  JOBNUM(1)
   8 7G*K          LDX   2  FX2  
  17 7G*L          LDX   6  ACOMMUNE1(2)        [PRESERVE ACOMMUNE1,2
  10 7G*M          LDX   7  ACOMMUNE2(2) 
  10 7G*N    [     SET ACTIVITY PRIORITY 
   8 7G*P          APJOBSET 5,4  
   4 7G*Q    [   
  10 7G*R          STO   6  ACOMMUNE1(2) 
  10 7G*S          STO   7  ACOMMUNE2(2) 
  18 7G*T          MHUNTW   1,JWELL,COPYSYS     [FIND JWELL/COPYSYS AGAIN
   7 7G*W          LDX   3  1
   9 7G*X          ADN   3  FRH+A1   
  10 7G*Y          LDX   6  JLJOBNO(3)   
   4 7G*_    [   
  10 7GB2    [     FIND ACTIVITY AGAIN   
  10 7GB3          ADX   3  JOBDATASIZE  
  18 7GB4          JBS      NHLS5,3,JLBMOP    [J IF MOP JOB FX2 IS CPAT  
  11 7GB5          HUNTACTJ CPAT,6,2,(GEOERR)
   4 7GB6    [   
   5 7GB7    NHLS5   
   4 7GB8    )   
   5 7GB9    #UNS ICT
   4 7GB=    #SKI
   4 7GB?    (   
   7 7GB#          LDX   1  3
   8 7GB*          SBN   1  A1   
  10 7GBB          LDX   6  JLJOBNO(3)   
  10 7GBC          ADX   3  JOBDATASIZE  
   4 7GBD    )   
  14 7GBF          JBS      XMOP2,3,JLBMOP      [J IF MOP
   7 7GBG          LDN   3  0
   8 7GBH          CHAIN    1,2  
  10 7GBJ          LDX   2  ACTRING+1(2) 
   9 7GBK          SBN   2  ACTRING  
   6 7GBL          FPUT  
   8 7GBM    XMOP2 LDX   2  FX2  
  10 7GBN          LDX   7  ACOMMUNE1(2) 
  16 7GBP          BZE   7  XJSA1                [J IF FROM JSA  
   7 7GBQ          CLOSETOP  
   9 7GBR          VOP      ,JWACCESS
  15 7GBS          BZE   3  XENDC               [J IF NOT MOP
  18 7GBY          BNZ   7  WAYON               [J IF FROM ENWELL AND MOP
   8 7GC4    XJSA1 BZE   3  XJSA2
   9 7GC8          LONGON   #145,6   
  10 7GC#          MFREE    JWELL,COPYSYS
   5 7GCD    XJSA2 UP
   5 7GD6    WAYON   
   9 7GXQ          ACROSS   DEWELL,2 
   5 7HCB    XENDC   
   6 7HX2          ENDCOM
   4 7JBL    #END
   6 ____ ...407205310001
