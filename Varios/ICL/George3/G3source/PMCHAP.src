   7 23YG    #OPT  K0PMCHAP=0
  11 24D6    #LIS  K0PMCHAP>K0POST>K0ALLGEO  
   4 24XQ    #   
  17 25CB ...#SEG  PMCHAP84                     [KAREL KOSKUBA-DIAG  
  16 25M8 ...#     (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 25X2    #   
  21 26BL    #     THIS SEGMENT LISTS THE CURRENT CHAPTER,AND ALSO CHAPTER HOLDING   
  21 26W=    #     GEORGE BREAK(IF K0-1 DOESN'T POINT TO CURRENT CHAP. OF FIXED CORE)
  19 27*W    #     THIS SEGMENT ALSO PRINTS THE ACTIVITY DUMP ASSOCIATED WITH
   7 27TG    #     NOISY CORE
   4 28*6    #   
   9 28SQ                   8HPMCHAP 
   4 29#B    #   
   9 29S2    #     ENTRY POINT TABLE 
   4 2=?L    #   
  10 2=R= ... SEGENTRY K1PMCHAP,XK1PMCHAP
   4 2?QG    #   
  12 2#=6    NAMEA          15HCURRENT CHAPTER   
  20 2#PQ    NAMEB          49HCHAPTER CONTAINING LAST GEORGE INSTRUCTION OBEYED 
   9 2*9B    #SKI           FNOISE   
   4 2*P2    (   
  10 2B8L    QUERIES        8H????????   
  14 2BN=    NHEAD1         28HNOISY CORE SYSTEM RUNNING 
  14 2C7W    NHEAD2         24HNOISY CORE ACTIVITY DUMP  
  14 2CMG    NMESS          24HGNCCODE POINTER CORRUPT   
   9 2D76    TIDEN          8HACC3   
   9 2DLQ                   8HACC4   
   9 2F6B                   8HACC5   
   9 2FL2                   8HACC6   
   9 2G5L                   8HACC7   
   9 2GK=    XALNK          8HALNK1  
   9 2H4W                   8HAWRK1  
   9 2HJG                   8HAWRK2  
   9 2J46                   8HAWRK3  
   9 2JHQ                   8HAWRK4  
   9 2K3B                   8HACOM1  
   9 2KH2                   8HACOM2  
   9 2L2L                   8HACOM3  
   9 2LG=                   8HACOM4  
   9 2L_W                   8HACOM5  
   9 2MFG                   8HACOM6  
   9 2M_6                   8HACOM7  
   9 2NDQ                   8HACOM8  
   9 2NYB    TEND           8HACOM9  
   4 2PD2    )   
   9 2PXL    MASK           #17777777
  10 2QWW    XALEN          +ALENFIXCHAP 
   8 2R6N ...XTWO           +0   
   8 2RBG    TWELVE         +12  
   8 2RW6    X45            +45  
  20 2S*Q    XLEN           +0                  [SET = LENGTH OF CHAPTEZ TO PRINT
  21 2STB    XSTART         +0                  [SET =START ADDR  OF CHAP. TO PRINT  
  19 2T*2    XBACK          +0                  [SET = 1 IF BACKWARD CHAINING
   9 2TSL    XBCHAP         +BCHAP   
  20 2W#=    XBACKPR        +0                  [TO HOLD START ADDR. OF PREVIOUS 
  21 2WRW                                       [BLOCK IN CHAIN.USED BY SEARCH ROUTS 
  21 2YQB    ZLINKA         +0                  [TO HOLD LINK ADDR. OF PRCHAP ROUT.  
   9 2_=2    #SKI           FNOISE   
   4 2_PL    (   
   9 329=    ZBSLEN         #02260000
  11 32NW ...ZLCHAPTAB      +AENDCHAPS-KTAB  
  20 338G    TADDR          +0                  [IDENTIFIER ADDR.FOR NOISY CORE  
   4 33N6    )   
   6 33T# ...#UNS  JPSCF 
  21 342G ...SCFCT      +0                 [TEMPORARY STORE FOR COUNT OVER PMSCFREAD 
   4 347Q    #   
  19 34MB    #     ROUTINE TO GET ADDRESS OF NEXT BLOCK IN CHAIN WHEN FORWARD
   7 3572    #     CHAINING  
  13 35LL    #     ENTRY  X2=ADDRESS OF CURRENT BLOCK
  17 366=    #     EXIT 0 &  IF FORWARD AND BACKWARD POINTERS CORRECT
  13 36KW    #         X2,X3=ADDRESS OF NEXT BLOCK   
   8 375G    #            X0=LINK
  14 37K6    #     EXIT 0 0  IF BREAK IN CHAIN DETECTED  
  13 384Q    #            X3=ADDRESS OF NEXT BLOCK   
  16 38JB    #     (SEARCHA:ENTRY POINT FOR FIRST BLOCK ON CHAIN 
   4 3942    #   
   5 39HL    SEARCH  
  17 3=3=          LDX   3  BPTR(2)             [J IF BPTR IN CURRENT
  16 3=GW ...      TXU   3  XBACKPR(1)          [BLOCK INCORRECT 
   8 3?2G          BCS      S1   
   5 3?G6    SEARCHA 
  19 3?_Q ...      STO   2  XBACKPR(1)          [STORE ADD. OF CURRENT BLOCK 
  18 3#FB          LDX   3  FPTR(2)             [GET ADD. OF NEXT BLOCK  
   4 3#_2    S2  
   7 3*DL     PMCORES 3,S1   
  17 3*Y= ...      TXL   3  X45(1)              [J IF ADDR. INVALID  
  16 3BCW          BCS      S1                  [BECAUSE TO SMALL
   7 3BXG          LDX   2  3
   7 3CC6          EXIT  0  1
  16 3CWQ    S1    EXIT  0  0                   [BREAK IN CHAIN  
   4 3DBB    #   
  20 3DW2    #     ROUTINE TO GET ADDRESS OF NEXT BLOCK IN CHAIN, WHEN BACKWARD  
   7 3F*L    #     CHAINING  
  10 3FT=    #     ENTRY & EXIT AS SEARCH
  16 3G#W    #     (SEARCHC:ENTRY POINT FOR FIRST BLOCK ON CHAIN 
   4 3GSG    #   
   5 3H#6    SEARCHB 
  17 3HRQ          LDX   3  FPTR(2)             [J IF FPTR IN CURRENT
  16 3J?B ...      TXU   3  XBACKPR(1)          [BLOCK INCORRECT 
   8 3JR2          BCS      S1   
   5 3K=L    SEARCHC 
  19 3KQ= ...      STO   2  XBACKPR(1)          [STORE ADD. OF CURRENT BLOCK 
  18 3L9W          LDX   3  BPTR(2)             [GET ADD. OF NEXT BLOCK  
   8 3LPG          BRN      S2   
   4 3M96    #   
  13 3MNQ    #     ROUTINE TO PRINT AN AREA OF CORE  
  12 3N8B    #     ENTRY    XLEN=LENGTH TO PRINT 
  13 3NN2    #            XSTART=START ADDR. OF AREA 
   9 3P7L    #                X7=LINK
   9 3PM=    #     EXIT     XLEN=0   
  13 3Q6W    #            XSTART=START ADDR. OF AREA 
   9 3QLG    #     ALL ACCS UNDEFINED
   4 3R66    #   
   5 3RKQ    PRCHAP  
  15 3S2= ...      STO   7  ZLINKA(1)           [STORE LINK  
  17 3SBQ ...      LDX   2  XSTART(1)            [LOAD START ADDRESS 
  21 3SR= ...PRC   CALL  7  (JPRINT1)             [PRINT A LINE (FIRST TIME SPACES)  
   9 3T7Q ...      LDX   7  XLEN(1)  
   9 3TMB ...      LDX   4  ZLINKA(1)
  17 3W*= ...      BZE   7  (4)                 [J IF AREA PRINTED   
   7 3WPQ ...      LDN   4  1
  15 3X6= ...      SBS   4  XLEN(1)             [UPDATE COUNT
  17 3XGQ          ADS   4  JPMOD               [UPDATE LP MODIFIER  
  17 3Y2B ...      LDX   5  2                   [CONVERT ADDRESS TO  
  15 3YG2 ...      CALL  7  (JDECN)               [DECIMAL   
  16 3Y_L ...      SBX   5  XSTART(1)           [CONVERT RELATIVE
  17 3_F= ...      CALL  7  (JDECN)               [ADDRESS TO DECIMAL
  17 3_YW          ADS   4  JPMOD               [UPDATE LP MODIFIER  
  18 42DG ...      LDX   5  0(2)                [GET CONTENTS OF ADDRESS 
  16 42Y6 ...      ADS   4  2                   [UPDATE ADDRESS  
   9 437Y ...      STO   2  XTWO(1)  
  21 43CQ ...      CALL  7  (JCON4)               [CONVERT CONTENTS FOUR DIFFERENT WA
   9 43MJ ...      LDX   2  XTWO(1)  
   8 43XB          BRN      PRC  
   9 44C2    #SKI           FNOISE   
   4 44WL    (   
   4 45B=    #   
  15 45TW    #     ROUTINE TO OBTAIN AND INSERT CHAPTER NAME 
   9 46*G    #     ENTRY    X7=LINK  
  11 46T6    #              X3=CHAPTER NUMBER
  11 47#Q    #           JPMOD=OUTPUT MOD.   
  13 47SB    #     EXIT  JPMOD=UPDATED OUTPUT MOD.   
  10 48#2    #        X3,X5,X6=UNDEFINED 
   4 48RL    #   
   5 49?=    NAMECHAP
  15 49QW ...      STO   7  ZLINKA(1)           [STORE LINK  
  10 4==G ...      TXL   3  ZLCHAPTAB(1) 
  18 4=Q6          BCC      N3                  [J IF CHAPTER NO.ILLEGAL 
  21 4?9Q          LDXC  3  KTAB(3)             [GET CHAPTER ENTRY FROM F.STORE TABLE
  17 4?PB          BCS      N1                  [J IF CHAP. IN CORE  
   6 4?SP ...#UNS  JPSCF 
   4 4?Y4 ...(   
   8 4#3C ...      SLL   3  10   
  16 4#6Q ...      LDX   5  3                   [-VE IF ON SCF   
   7 4#=5 ...      SLL   3  1
  16 4#*D ...      SRL   3  4                   [ADD IN WORDS NOW
   4 4#DR ...)   
   6 4#J6 ...#UNS  JPSCF 
   4 4#MF ...#SKI
   4 4#QS ...(   
  16 4#W7 ...      SLL   3  11                  [CLEAR BITS 0-8  
  16 4#_G ...      SRL   3  4                   [MULTIPLY BY 128 
   4 4*4T ...)   
   9 4*8= ...      TXL   3  ZBSLEN(1)
  18 4*MW          BCC      N3                  [J IF B/S ADDR. ILLEGAL  
  17 4B7G          LDN   6  2                   [LENGTH OF TRANSFER  
   6 4B8H ...#UNS  JPSCF 
   4 4B9J ...(   
   8 4B=K ...      BPZ   5  NTSCF
   9 4B?L ...      STO   6  SCFCT(1) 
  13 4B#M ...      PMSCFREAD SCFCT(1),ZPMCHAP(1),3   
   8 4B*N ...      BRN      NICF 
   5 4BBP ...NTSCF   
   4 4BCQ ...)   
  10 4BDW ...      LDN   5  ZPMCHAP(1)   
  10 4BQ= ...      PMBSADD  GEO,0,6,5,3  
   8 4C3L ...NICF  LDX   1  FXPM1
  10 4C*2 ...      LDN   5  ZPMCHAP(1)   
  17 4CLB    N2    LDX   6  JPMOD               [LOAD OUTPUT ADDR.   
  21 4D62          MOVE  5  2                   [MOVE CHAP. NAME INTO OUTPUT BUFFER  
   9 4DJ4 ...      LDX   5  ZLINKA(1)
  13 4F26 ...      BRN      (5)                 [EXIT
   9 4F?G ...N1    ANDX  3  MASK(1)  
   8 4FJW          PMCORES 3,N3  
  20 4G4G          LDN   5  A1(3)               [LOAD CORE ADDR. OF CHAP. NAME   
   8 4GJ6          BRN      N2   
  18 4H3Q ...N3    LDN   5  QUERIES(1)          [SET TO PRINT '????????' 
   8 4HHB          BRN      N2   
   4 4J32    )   
   4 4JGL    #   
   6 4K2=    XK1PMCHAP   
   4 4K4F ...#   
   4 4K6N ...#   
   8 4K8X ...#     LOAD  MODIFIER
   4 4K?6 ...#   
   8 4K** ...      LDX   1  FXPM1
   4 4KCJ ...#   
   9 4KFW    #SKI           FNOISE   
   4 4K_G    (   
   4 4LF6    #   
  13 4LYQ    #     TEST IF NOISY CORE SYSTEM RUNNING 
   4 4MDB    #   
   9 4MY2          LDX   4  GNCCODE  
  14 4NCL          BNG   4  NEND                [J IF NOT
  10 4NX= ...      PMESSAGE NHEAD1(1),7  
  10 4PBW ...      PMESSAGE NHEAD2(1),6  
  17 4PWG          ADN   4  1                   [OBTAIN [GNCCODE]+1  
  18 4QB6 ...      TXL   4  ENDFIX            [IS [GNCCODE]+1 INSIDE F.C.
  14 4QTQ          BCC      NC3                 [J IF NOT
  17 4R*B ...      CALL  7  (JPRINT1)             [PRINT BLANK LINE  
  21 4RT2 ...      LDN   2  TIDEN(1)            [LOAD ADDR.OF TABLE OF IDENTIFIERS   
  15 4S#L ...      STO   2  TADDR(1)            [AND STORE   
  17 4SS= ...NC1   LDX   2  JLPMOD              [LOAD LP BUFFER ADDR.
  21 4T?W ...      LDX   5  TADDR(1)            [LOAD ADDR.OF FIRST/NEXT IDENTIFIER  
  17 4TRG ...      LDN   6  1(2)                [LOAD OUTPUT ADDR.   
  19 4W?6          MOVE  5  2                   [STORE FIRST/NEXT IDENTIFIER 
   7 4WQQ          SMO      4
  21 4X=B          LDX   5  0                   [LOAD FIRST/NEXT WORD OF DUMP AREA   
  17 4XQ2 ...      ADN   2  4                   [UPDATE BUFFER ADDR. 
  19 4Y9L ...      STO   2  JPMOD               [STORE UPDATED BUFFER ADDR.  
  18 4YP= ...      LDN   6  XALNK(1)            [LOAD ADDR.OF ALNK1 IDEN 
  20 4_8W ...      TXU   6  TADDR(1)            [IS PRESENT IDENT.ADDR. THE SAME 
  16 4_NG          BCC      NALNK               [J IF ALNK1 IDEN.
  20 5286 ...      CALL  7  (JDECP)               [STORE BIN.NO. IN N.N FORMAT   
   7 52MQ          LDN   7  1
   8 537B          ADS   7  JPMOD
   8 53M2          LDCT  7  #600 
  19 546L          ORS   7  JPMOD               [UPDATE TO POINT TO CHAR.3   
  20 54L= ...      CALL  7  (JOCT)                [STORE BIN.NO. IN OCTAL FORMAT 
  14 555W ...      CALL  7  (JPRINT1)             [OUTPUT
  18 55KG ...      LDN   5  TEND(1)             [LOAD ADDR.OF LAST IDEN. 
  19 5656 ...      TXU   5  TADDR(1)            [IS PRESENT IDEN. THE LAST   
  14 56JQ          BCC      NEND                [J IF SO 
  18 574B    NC2   ADN   4  1                   [UPDATE DUMP AREA ADDR.  
   7 57J2          LDN   5  2
  18 583L ...      ADS   5  TADDR(1)            [UPDATE IDENT.TABLE ADDR.
   8 58H=          BRN      NC1  
   4 592W    #   
  11 59GG    #     OUTPUT ALNK1 INFORMATION  
   4 5=26    #   
   5 5=FQ    NALNK   
  20 5=_B          LDCT  7  #400                [POSITION BUFFER ADDR.FOR OUTPUT 
  15 5?F2          ORS   7  JPMOD               [AND STORE   
  19 5?YL ...      CALL  7  (JDECDEC)             [STORE BIN.NO IN N/N FORMAT
  17 5#D=          SRL   5  12                  [OBTAIN CHAPTER NO.  
  15 5#XW          LDX   3  5                   [AND STORE   
   7 5*CG          LDN   6  2
  14 5*X6          ADS   6  JPMOD               [UPDATE  
  17 5BBQ          CALL  7  NAMECHAP            [STORE CHAPTER NAME  
  14 5BWB ...      CALL  7  (JPRINT1)             [OUTPUT
   8 5CB2          BRN      NC2  
   4 5CTL    #   
  18 5D*= ...NC3   PMESSAGE NMESS(1),6             [[GNCCODE]+1 CORRUPT  
   4 5DSW    NEND
   4 5F#G    )   
   4 5FS6    #   
  13 5G?Q ...#     READ PMINPRIN ROUTINE INTO CORE   
   4 5GRB    #   
  10 5H?2 ...      LDN   6  ZPMCHAP(1)   
  10 5HQL ...      PMOVE    PMINPRIN,6   
   8 5J== ...      LDX   1  FXPM1
   4 5JPW    #   
  11 5K9G    #     TO PRINT CURRENT CHAPTER  
   4 5KP6    #   
   8 5L8Q          LDX   2  FX1  
  18 5LNB          BZE   2  PC                  [J IF NO CURRENT CHAPTER 
   7 5M82     PMCORES 2,PC   
  17 5MML ...      STO   2  XSTART(1)           [FOR PRCHAP ROUTINE  
  17 5N7=          SBN   2  A1                  [START ADDR. OF BLOCK
  17 5NLW          LDX   7  ALOGLEN(2)          [LENGTH OF CHAPTER   
   9 5P6G ...      ANDX  7  MASK(1)  
   8 5PL6          BNZ   7  PC4  
   9 5Q5Q          TXU   2  GFIXCHAP 
  17 5QKB          BCC      PC7                 [J IF FIXED CHAPTER  
   9 5R52          LDX   7  ASIZE(2) 
   9 5RJL ...      ANDX  7  MASK(1)  
   9 5S4= ...      TXL   7  TWELVE(1)
  18 5SHW          BCC      PC4                 [IF CHAPTER BLOCK SIZE<12
  18 5T3G          LDN   7  100                 [PRINT FIRST 100 WORDS   
   8 5TH6          BRN      PF   
  18 5W2Q    PC7   LDX   7  GLENFCHAP           [LENGTH OF FIXED CHAPTER 
   8 5WGB          BRN      PF   
   9 5X22 ...PC4   TXL   7  XALEN(1) 
  20 5XFL          BCS      PF                  [J IF CHAPTER LENGTH VALCHAP ROUT
  18 5X_= ...      LDX   7  XALEN(1)            [MAX  LENGTH OF A CHAPTER
  20 5YDW ...PF    STO   7  XLEN(1)             [LENGTH TO PRINT,FOR PRCHAP ROUT 
  16 5YYG ...      CALL  7  (JPRINT1)             [THROW A LINE  
   9 5_D6 ... PMESSAGE   NAMEA(1),4  
  16 5_XQ          CALL  7  PRCHAP              [PRINT CHAPTER   
   4 62CB    #   
  17 62X2    #     TO TEST IF GEORGE BREAK OCCURED IN ANOTHER CHAPTER
   4 63BL    #   
  21 63W= ...PC    LDX   5  GONRDUMP                [GET LAST GEORGE INSTR. OBEYED   
   9 64*W ...      TXL   5  ENDFIX   
   9 64TG ...      BCS      (JPMSEG) 
   8 65*6 ... PMCORES 5,(JPMSEG) 
   8 65SQ          LDX   2  FX1  
  18 66#B          BZE   2  PC5                 [J IF NO CURRENT CHAPTER 
   7 66S2          TXL   5  2
  21 67?L          BCS      PC5                 [J IF BREAK NOT IN CURRENT CHAPTER   
   8 67R=          SBN   2  A1   
  19 68=W          ADX   2  ASIZE(2)            [LAST WORD OF CURRENT CHAPTER
   9 68QG ...      ANDX  2  MASK(1)  
   7 69=6          TXL   5  2
  20 69PQ ...      BCS      (JPMSEG)              [J IF BREAK IN CURRENT C APTER 
   4 6=9B    #   
  21 6=P2    #     TO SEARCH DOWN CHAPTER CHAIN TO SEE IF GEORGE BROKE IN A CHAPTER  
   4 6?8L    #   
   8 6?N=    PC5   LDN   2  BCHAP
  21 6#7W ...      LDN   3  SEARCHA(1)          [TO GET ADDR. FIRST BLOCK ON CHAIN   
   8 6#MG          BRN      PC1  
   9 6*76 ...PC2   LDN   3  SEARCH(1)
   9 6*LQ ...      LDX   7  XBACK(1) 
  18 6B6B          BZE   7  PC1                 [J IF FORWARD CHAINING   
  10 6BL2 ...      LDN   3  SEARCHB(1)   
  20 6C5L    PC1   CALL  0  (3)                 [GET ADDR. OF -EX0 BLOCK ON CHAIN
  17 6CK=          BRN      PC3                 [J IF BREAK IN CHAIN 
   9 6D4W ...      TXU  2  XBCHAP(1) 
  19 6DJG ...      BCC      (JPMSEG)              [J IF END OF CHAIN REACHED 
  16 6F46          LDX   7  ALOGLEN(2)          [LENGTH OF BLOCK 
   9 6FHQ ...      ANDX  7  MASK(1)  
   9 6G3B ...      TXL   7  XALEN(1) 
  17 6GH2          BCS      PC6                 [J IF LENGTH VALID   
  20 6H2L ...      LDX   7  XALEN(1)            [SET TO MAX. LENGTH OF A CHAPTER 
   7 6HG=    PC6   ADX   3  7
   8 6H_W          ADN   3  A1   
   7 6JFG          TXL   5  2
  16 6J_6          BCS      PC2                 [J IF NOT CHAPTER
   7 6KDQ          TXL   5  3
  19 6KYB          BCC      PC2                 [J IF BREAK NOT IN CHAPTER   
   4 6LD2    #   
  12 6LXL    #     PRINT CHAPTER GEORGE BROKE IN 
   4 6MC=    #   
   8 6MWW          ADN   2  A1   
  20 6NBG ...      STO   2  XSTART(1)           [START ADDR. OF CHAP. FOR PRCHAP 
   9 6NW6 ...      STO   7  XLEN(1)  
   9 6P*Q ... PMESSAGE   NAMEB(1),13 
  16 6PTB          CALL  7  PRCHAP              [PRINT CHAPTER   
   9 6Q*2 ...      BRN      (JPMSEG) 
   4 6QSL    #   
  14 6R#=    #     IF BREAK IN CHAPTER CHAIN ENCOUNTERED 
   4 6RRW    #   
   9 6S?G ...PC3   LDX   0  XBACK(1) 
   9 6SR6 ...      BNZ   0  (JPMSEG) 
   7 6T=Q          LDN   0  1
  20 6TQB ...      STO   0  XBACK(1)            [SET BACKWARD CHAINING INDICATOR 
  21 6W=2          LDN   2  BCHAP               [SET TO GET FIRST BLOCK ON CHAIN WHEN
  17 6WPL ...      LDN   3  SEARCHC(1)          [BACKWARD CHAINING   
   8 6X9=          BRN      PC1  
   4 6XNW    #   
   4 6YN6    #   
   5 6_7Q    ZPMCHAP 
   4 6_MB    #   
   4 7272    #END
   8 ____ ...53532256000100000000
