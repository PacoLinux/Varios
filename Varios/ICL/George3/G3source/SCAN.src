  16 22FL ...#SEG  SCAN8                        [PHILIP TURNER   
  14 22_=    #LIS  K0SCAN>K0MULTIPLX>K0ALLGEO>K0GREATGEO 
   9 23DW                   8HSCAN8  
   7 23YG    # ENTRY POINTS  
  10 24D6          SEGENTRY K1SCAN,Z1SCAN
  10 24XQ          SEGENTRY K2SCAN,Z2SCAN
  10 25CB          SEGENTRY K3SCAN,Z3SCAN
  10 25X2          SEGENTRY K4SCAN,Z4SCAN
  10 26BL          MENDAREA 25,K100SCAN  
   4 26W=    [   
  21 27*W    [THIS SEGMENT GETS INFORMATION ABOUT THE DEVICE FROM THE IDF, AND OUT-  
  20 27TG    [PUTS A N/L SEQUENCE TO EACH LINE WITH A MOP TERMINAL OF TYPE KNOWN 
   6 28*6    [TO GEORGE. 
   4 28SQ    [   
   4 29#B    [   
   4 29S2    [   
   8 2=?L    #DEF  SWITCHBOARD=1 
   4 2=R=    [   
   4 2?=W    [   
  17 2?GN ...      SEGENTRY K25SCAN        [USED BY PRESETRP MACRO   
   9 2?QG    XAMXMSKPR      +AMXMSKPR
   9 2#=6    XCONTEXT       #20412   
   6 2#PQ    MXORALINES  
   8 2*9B    #HAL  AMXOR+ALINES,0
   5 2*P2    PCTYPE  
   8 2B8L    #HAL  AMOP+APCT,0   
   5 2BN=    SENTYPE 
   8 2C7W    #HAL  AMOP+ASENT,0  
   4 2CMG    [   
   9 2D76    PAIR1 PAIR     SCAN,4   
  10 2DLQ    PAIR2 PAIR     PCTBEGIN,1   
  10 2F6B    PAIR3 PAIR     SENTINEL,1   
   4 2FL2    [   
  19 2G5L    #DEF  ZALEN=BUFFER-A1              [LINE CONTROL BLOCK LENGTH   
   4 2GK=    [   
  21 2H4W    [THE DATA BELOW IS PUT INTO THE LINE CONTROL BLOCK FOR ALL 7071 TELETYPE
  21 2HJG    [TERMINALS. IT IS DEVICE DEPENDENT, AND MAY CHANGE FOR OTHER CONSOLES.  
   4 2J46    [   
  17 2JHQ ...      SEGENTRY K50SCAN             [USED BY AUTMOP MACRO
  21 2K3B    TNOCHS         +11                 [NORMAL TERMINATOR CHARACTER NO. +1  
  19 2KH2 ...TNLBUFF        #76357632           [NEWLINE AND FES CHARACTERS  
  18 2L2L                   #76207603           [NULL AND DC3 CHARACTERS 
  17 2LG=    TEOM           #76130000           [END OF MESSAGE CH.  
   9 2LQ4 ...      SEGENTRY K31SCAN  
  21 2L_W    TCHARS         +72                 [CARRIAGE WIDTH OF 7071 CONS. (CHARS)
  21 2MFG    #FID           0 5 6               [COUNT OF CHARS. IN NEW LINE MESSAGE.
  20 2M_6    TREADY         +8                  [NUMBER OF CHARACTERS FOR 'READY'
  21 2NDQ                   #76672076           [BACKWARDS POINTING ARROW AND SPACE  
  18 2NYB                   #27760177           [BELL AND DC1 AND NULL   
  14 2PD2                   +0                  [HWGROT  
  18 2PXL    XDELE          +0                  [RESET TO 8 BY AUTOMOP   
   9 2QC=                   #76737673
   9 2QWW                   #76737673
  18 2RBG    #DEF  TERMLEN=0?-TNOCHS            [TERMINAL RECORD LENGTH. 
   4 2RW6    [   
   4 2S*Q    [   
  16 2STB    TABLE BRN      U4C                 [7071 CONSOLE.   
  15 2T*2          BRN      U4P                 [7020 PCT.   
  15 2TSL          BRN      U4S                 [SENTINEL.   
   4 2W#=    [   
   4 2WRW    [   
   5 2X?G    Z1SCAN  
   9 2XB5 ...      FSHCODE  B,SNOPRA1
   4 2XDN ...(   
   7 2XH? ...      FSHACTPR  
   5 2XKW ...SNOPRA1 
   4 2XNF ...)   
  10 2XR6          LDX   3  AMOPADDR(2)  
   7 2XW= ...#UNS  IDFMARK8  
  13 2X_B ...      IDFSCAN  (GEOERR),U4OFF,CPPTR(3)  
   7 2Y4G ...#UNS  IDFMARK8  
   4 2Y7L ...#SKI
  11 2Y=Q          IDFSCAN  U4OFF,CPPTR(3)   
  16 2YQB          SETALLOC CPPTR(3)            [SET AMOP WAITING
   7 2_=2          LDX   2  3
  18 2_PL          FWAIT    #54                 [FOR THE END OF E.M.S.   
  21 329=          FON      #102,1              [WAKE UP NEXT AMSCAN, OR EMS ACTIVITY
  21 32NW          COOR1Y                       [WAIT FOR AMOP TO DO MODE #4000 PERI.
   9 32RF ...      FSHCODE  B,SNOTA1 
   4 32W4 ...(   
   9 32YM ...      BRN      PRDONE   
   5 333= ...SNOTA1  
   4 335T ...)   
  19 338G    Z2SCAN                             [ENTRY FROM MOP-ON COMMAND   
   9 33?5 ...      FSHCODE  B,PRDONE 
   4 33*N ...(   
   7 33D? ...      FSHACTPR  
   5 33GW ...PRDONE  
   4 33KF ...)   
  19 33N6          SETNCORE ZALEN,3,AMXOR,ALCB,1,1,2 [GET LINE CONTROL BLOCK.
   9 347Q          NGS   3  GTIM(3)  
  20 34MB          LDN   7  TNOCHS(1)           [INSERT N/L SEQUENCE IN SCAN LCB 
   9 3572          LDN   0  GNOCHS(3)
   7 35LL          MOVE  7  4
  20 366=          LDX   3  AMOPADDR(2)         [FIND THE ALINES BLOCK, IF ANY   
   9 36KW    U00   LDX   3  FPTR(3)  
   9 375G          LDX   0  ATYPE(3) 
  18 37K6          BXGE  0  CACT,U4FIN          [SUICIDE IF NO ALINES.   
  11 384Q          BXU   0  MXORALINES(1),U00
  20 38JB          MHUNTW   1,IDF,B7071         [FIND THE UNIT DESCRIPTION BLOCK.
  16 3942          STO   3  AWORK2(2)           [SAVE POINTERS.  
  10 39HL          LDX   3  AMOPADDR(2)  
  10 3=3=          LNLI     CPPTR(3),7   
   9 3=GW          STO   1  AWORK1(2)
  21 3?2G    U0    LDX   3  7                   [SET MODIFIER FOR NEXT ALINES ENTRY. 
   8 3?G6          LDCT  0  #40  
   7 3?_Q          SLL   3  2
  21 3#FB          SMO      7                   [GET IDF DESCRIPTION FOR THIS LINE.  
   8 3#_2          LDX   4  A1(1)
  20 3*DL          ADX   3  AWORK2(2)           [POINTER TO CURRENT ALINES ENTRY.
   8 3*Y=          LDCT  6  #200 
  11 3BCW          ANDX  0  A1+FOURTHWD(3)   
  19 3BXG          ANDX  6  4                   [NON-ZERO IF ATTACHED LINE.  
  21 3CC6          BZE   0  U1B                 [J. IF ^TO BE SCANNED^ BIT NOT SET,  
  19 3CWQ          ORX   0  6                   [OTHERWISE CLEAR IT, AND SET 
  19 3DBB          ERS   0  A1+FOURTHWD(3)      [^ATTACHED^ BIT IF NEEDED.   
  21 3DW2          BNG   4  U3                  [J. IF TERMINAL DEVICE TYPE UNKNOWN. 
  19 3F*L          LDCT  5  #420                [OTHERWISE MARK ^SETTING UP^ 
  17 3FT=          ANDN  4  #77                 [AND INSERT THE TYPE.
  21 3G#W          STO   6  AWORK3(2)           [SET NON-ZERO IF LINE IS ATTACHED.   
  11 3GSG          ORS   5  A1+FOURTHWD(3)   
  11 3H#6          DEX   4  A1+FOURTHWD(3)   
  18 3HRQ          LDN   0  HIGHTYPE            [J. IF INTERNAL TYPE #   
  18 3J?B          BZE   4  U4B                 [GIVEN BY IDF IS TOO LOW.
  15 3JR2          BXL   0  4,U4B               [OR TOO HIGH.
  17 3K=L          SMO      4                   [J. ON TERMINAL TYPE.
   9 3KQ=          BRN      TABLE-1  
  17 3L9W    U1B   BZE   6  U1C                 [J. IF NOT ATTACHED. 
  10 3LPG    U1BA  MFREEW   IDF,CONNAME  
   9 3M96          LDX   1  AWORK1(2)
   8 3MNQ          BRN      U3   
  17 3N8B    U1C   SBN   4  2                   [J. IF NOT PCT TYPE. 
   8 3NN2          BNZ   4  U3   
   9 3P7L    U1D   MFREEW   IDF,B7020
   9 3PM=          LDX   1  AWORK1(2)
   8 3Q6W          BRN      U3   
  19 3QLG    U2A   BZE   7  U4FIN               [J. IF FINISHED ALL LINES.   
  14 3R66          HUNT2    3,AMXOR,ALINES,AMOPADDR(2)   
  18 3RKQ    U2B   MHUNTW   1,IDF,B7071         [AND RESET THE POINTERS. 
   9 3S5B          STO   3  AWORK2(2)
   9 3SK2          STO   1  AWORK1(2)
   8 3T4L    U3    BZE   7  U4FIN
   8 3TJ=          BCT   7  U0   
   5 3TN7 ...U4FIN   
   9 3TS4 ...      FSHCODE  B,NOKILLA
   4 3TX_ ...(   
   7 3W3W ...      FSHKILL   
   5 3W7R ...NOKILLA 
   4 3W?N ...)   
  18 3WCK ...      SUICIDE                      [END IF NO MORE LINES.   
  21 3WHG    U4OFF FON      #102,1              [WAKE UP NEXT AMSCAN, OR EMS ACTIVITY
  21 3X36    U4NXT LDX   3  FPTR(3)             [FREE ANY ALINES BLOCK FOR THIS AMOP.
   9 3XGQ          LDX   0  ATYPE(3) 
  10 3Y2B          BXGE  0  CACT,U4FIN   
  12 3YG2          BXU   0  MXORALINES(1),U4NXT  
   7 3Y_L          FREECORE 3
   8 3_F=          BRN      U4FIN
  10 3_YW    U4A   GEOERR   1,IDFERROR   
  10 42DG    U4B   GEOERR   1,TERMTYPE   
   4 42Y6    [   
  10 43CQ    U4S   GETACT   BMOP,ASENT   
  10 43XB          LDX   3  AMOPADDR(2)  
   9 44C2          LDX   2  BPTR(2)  
  17 44WL          STO   7  ACC7(2)             [PRESERVE LINE NO.   
  18 45B=          STO   3  AWORK4(2)           [PRESERVE AMOP ADDRESS   
  17 45TW          LINKSET  2,PAIR3(1)          [LINK TO K1SENTINEL  
  16 46*G          FPUT                         [PUT CPA ON LIST.
  17 46T6          BRN      U2A                 [J. FOR NEXT LINE.   
   4 47#Q    [   
  18 47SB    U4P   LDCT  0  #200                [J. IF LINE IS ATTACHED. 
  11 48#2          ANDX  0  A1+FOURTHWD(3)   
   8 48RL          BNZ   0  U4PAT
  21 49?=          GETACT   BMOP,APCT           [OTHERWISE SET UP AN AMOP/APCT ACTY. 
  10 49QW          LDX   3  AMOPADDR(2)  
   9 4==G          LDX   0  CPPTR(3) 
   7 4=Q6          LDX   6  3
   9 4?9Q          LDX   3  BPTR(2)  
   9 4?PB          STO   0  CPPTR(3) 
  15 4#92          STO   6  ACC6(3)             [AMOP ADDRESS
  16 4#NL          STO   7  ACC7(3)             [PRESERVE LINE # 
  17 4*8=          LINKSET  3,PAIR2(1)          [LINK TO K1PCTBEGIN  
  21 4*MW          MHUNTW   2,IDF,B7020         [FIND SUPPLEMENTARY LINE DESCRIPTION 
  21 4B7G          CHAIN    2,3                 [BLOCK AND CHAIN AFTER NEW ACTIVITY. 
  18 4BM6          LDX   2  3                   [PUT ACTIVITY ON LIST.   
   6 4C6Q          FPUT  
  17 4CLB          BRN      U2A                 [J. FOR NEXT LINE.   
  19 4D62    U4PAT LDCT  5  #420                [UNSET 'SETING UP' MARKERS   
  11 4DKL          ERS   5  A1+FOURTHWD(3)   
   8 4F5=          BRN      U1BA 
   4 4FJW    [   
  18 4G4G    U4C   LDX   1  3                   [ADDRESS OF ALINES ENTRY.
  19 4GJ6          MHUNTW   3,AMXOR,ALCB        [FIND THE LINE CONTROL BLOCK.
  17 4H3Q          STO   7  GUNITNO(3)          [PRESERVE LINE NO.   
   7 4HHB          SLL   7  2
  19 4J32    U5    STO   1  AWORK4(2)           [SAVE ADDRESS OF LINE ENTRY. 
  21 4JGL          LDCT  0  #402                [UNSET B0 & INDICATE PERI FROM AMSCAN
  11 4K2=          ERS   0  A1+FOURTHWD(1)   
  20 4KFW          RINGTWO  A1(1),A1(3)         [ENRING LCB WITH ALINES ENTRY.   
  15 4K_G          LOCKC    3,REFIND            [LOCK THE LCB
   9 4LF6          CALL  1  REFIND   
   7 4LYQ          LDX   3  2
   8 4MDB          LDX   1  FX1  
   8 4MY2          LDX   2  FX2  
  10 4NCL          LDX   2  AMOPADDR(2)  
  10 4NX=          LDX   6  GUNITNO(3)   
   8 4PBW          FINDLCA  ,6   
   8 4PWG          JLBUSY   1,REJ
   9 4QB6          LDN   5  GREP1(3) 
  10 4QTQ          DLCA     1,STO,5,CREP 
   8 4R*B          SLL   6  15   
  10 4RT2          DCA      ,STO,6,MODE  
   8 4S#L          LDN   5  #11  
  10 4SS=          DLCA     1,DLA,5,MODE 
   9 4T?W          STO   5  GLASTM(3)
   9 4TRG          LDX   5  GNOCHS(3)
  10 4W?6          DLCA     1,DLA,5,COUNT
  10 4WQQ          LDN   5  GNLBUFF(3)   
   7 4X=B          PHOTO    4
  10 4XQ2          DLCA     1,STO,5,CTSA 
   8 4Y9L          LDX   0  JTIME
   9 4YP=          STO   0  GTIM(3)  
   6 4_8W          GLPERI
  19 4_NG          COOR1                        [WAIT FOR TRANSFER TO FINISH 
  18 5286          TESTMOVE 4,U5A               [J. IF NO CORE MOVEMENT. 
  19 52MQ          MHUNTW   3,AMXOR,ALCB        [OTHERWISE RELOCATE THE LCB. 
   9 537B    U5A   LDCH  0  GREP1(3) 
   7 53M2          SRC   0  1
  20 546L          BPZ   0  S1                  [J. IF THE LINE IS INOPERABLE.   
   7 54L=          SRC   0  1
  17 555W          BNG   0  S1                  [J IF NOT IMPLEMENTED
   8 55KG          ANDN  0  #1   
  20 5656          BNZ   0  S0                  [BRANCH IF ERROR IN TRANSMISSION 
   8 56JQ    #SKI  SWITCHBOARD   
   4 574B    (   
  21 57J2    [!!!!!!!!!!!!!!!!! FRIG FOR CARLTON DRIVE SWITCHBOARD !!!!!!!!!!!!!!!!!!
  20 583L          LDX   0  GREP1(3)            [LINE INOPERABLE IF IT REPLIES   
  19 58H=          ANDN  0  #77                 [^BREAK IN^ TO NULL TRANSFER.
   8 592W          BCT   0  S1   
  21 59GG    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
   4 5=26    )   
   9 5=FQ          STOZ     GFAIL(3) 
  10 5=_B          LDX   5  GUNITNO(3)   
   8 5?F2          GETACT   CPAT 
  10 5?YL          LDX   3  AMOPADDR(2)  
   9 5#D=          LDX   6  AWORK3(2)
   9 5#XW          LDX   2  BPTR(2)  
  18 5*CG          STO   3  AWORK4(2)           [PRESERVE AMOP ADDRESS   
  14 5*X6          STO   5  ACC7(2)             [LINE NO.
  16 5BBQ          LINKSET  2,PAIR1(1)          [LINK TO K4SCAN  
  18 5BWB          FPUT                         [PUT THE CPA ON THE LIST.
  18 5CB2          BZE   6  U5B                 [J. IF LINE NOT ATTACHED.
  19 5CTL          MHUNTW   1,IDF,CONNAME       [CHAIN IN DATA BLOCK HOLDING 
  16 5D*=          CHAIN    1,BPTR(2)           [CONCEPTUAL NAME.
  18 5DSW    U5B   LDCT  6  #730                [MASK FOR INTERLOCK BITS.
   8 5F#G          BRN      S2   
  14 5FS6    REFIND                             [LOCKC   
  10 5G?Q          MHUNTW   2,AMXOR,ALCB 
   7 5GRB          EXIT  1  0
   4 5H?2    [   
  19 5HQL    XBI   GEOERR   1,NOBISCAN          [ERROR IF BROKEN IN ON HERE  
  20 5J==    REJ   GEOERR   1,REJECT            [ERROR IF OUTPUT NULL REJECTED   
   4 5JPW    [   
  21 5K9G    S0    LDX   0  GFAIL(3)            [J. IF NULL ENDED IN ERROR 2ND. TIME.
   8 5KP6          BNZ   0  S1   
  16 5L8Q          LDN   0  1                   [MARK 1ST. ERROR.
   9 5LNB          STO   0  GFAIL(3) 
  18 5M82          LDX   1  AWORK4(2)           [RELOCATE ALINES ENTRY.  
   8 5MML          TESTMOVE 4,U5 
  14 5N7=          HUNT2    1,AMXOR,ALINES,AMOPADDR(2)   
   7 5NLW          ADX   1  7
  18 5P6G          BRN      U5                  [J. TO REPEAT THE OUTPUT.
   4 5PL6    [   
  17 5Q5Q    S1    STOZ     GFAIL(3)            [CLEAR FAILURE COUNT 
  18 5QKB          LDCT  6  #310                [MASK FOR INTERLOCK BITS.
  18 5R52    S2    LDX   3  AWORK4(2)           [RELOCATE ALINES ENTRY.  
   8 5RJL          TESTMOVE 4,S3 
  14 5S4=          HUNT2    3,AMXOR,ALINES,AMOPADDR(2)   
   7 5SHW          ADX   3  7
  21 5T3G    S3    ADN   6  1                   [MASK OUT EXTRANEOUS INTERLOCK BITS. 
  11 5TH6          ANDS  6  A1+FOURTHWD(3)   
  18 5W2Q          BNG   6  S4                  [J. IF LINE WAS OPERABLE 
  19 5WGB          LDX   0  A1+FOURTHWD(3)      [OTHERWISE, WAS IT ATTACHED? 
   7 5X22          ADXC  0  0
   8 5XFL          BCC      S4   
  21 5X_=          MFREEW   IDF,CONNAME         [IF SO FREE THE CONCEPTUAL NAME BLOCK
   7 5YDW    S4    SBX   3  7
   7 5YYG          SRL   7  2
  17 5_D6          BRN      U2B                 [J. FOR NEXT LINE.   
   4 5_XQ    [   
  21 62CB    [THIS ENTRY POINT IS FOR THE CPA TO FIND THE IDF ENTRY FOR THIS LINE.   
  20 62X2    [IF AN ENTRY IS FOUND FOR A 7071 CONSOLE AN LCB IS CREATED AND IT   
   9 63BL    [OUTPUTS ^GOOD MORNING^.
  21 63W=    [IF AN ENTRY IS FOUND FOR A SENTINEL IT GOES TO DEAL WITH THE TRANSFER. 
  17 64*W    [IF NO RELEVANT ENTRY IS FOUND THE CPA COMMITS SUICIDE. 
   4 64TG    [   
  15 65*6    XPCT  LDX   6  AWORK4(2)           [AMOP ADDRESS
  18 65SQ          ADXC  5  5                   [J IF LINE IS ATTACHED   
   8 66#B          BCS      X0   
  20 66S2          LDX   0  PCTYPE(1)           [.IF WE'RE ALREADY A PCT ACTIVITY
  10 67?L          BXE   0  ATYPE(2),YPCT
  21 67R=          NAMEX    2                   [OTHERWISE RESET OUR TYPE AND LENGTH 
  11 68=W          ALTLEND  2,BMOPLEN+APCTLEN
   9 68QG    YPCT  SMO      AWORK4(2)
   8 69=6          LDX   0  CPPTR
   9 69PQ          STO   0  CPPTR(2) 
  10 6=9B          ACROSS   PCTBEGIN,1   
  10 6=P2    X1A   LDX   0  SENTYPE(1)   
  10 6?8L          BXE   0  ATYPE(2),X1  
  15 6?N=          NAMEX    2                   [RENAME IT   
  12 6#7W          ALTLEND  2,BMOPLEN+ASENTLEN   
   8 6#MG          BRN      X1   
   4 6*76    [   
  19 6*LQ    X0    LDCT  6  #420                [LEAVE LINE LOOKING ^IDLE^.  
  11 6B6B          ERS   6  A1+FOURTHWD(3)   
   8 6BL2          BRN      U4FIN
   4 6C5L    [   
   4 6C9H ...X1  
  10 6C*D ...      FSHCODE  B,NOKILLA2   
   4 6CF* ...(   
   7 6CK= ...      FSHKILL   
   5 6CP7 ...NOKILLA2
   4 6CT4 ...)   
  10 6CY_ ...      ACROSS   SENTINEL,1   
   4 6D4W    [   
   4 6DJG    [   
   5 6F46    Z3SCAN  
   9 6F6P ...     FSHCODE  B,NOPRA3  
   4 6F9# ...(   
   7 6F?X ...     FSHACTPR   
   5 6FBG ...NOPRA3  
   4 6FF5 ...)   
   9 6FHQ          LDX   3  AWORK4(2)
   7 6G3B          LDN   6  1
   7 6G6G ...#UNS  IDFMARK8  
  13 6G9L ...      IDFSCAN (GEOERR),SYSOF,CPPTR(3),7 
   7 6G#Q ...#UNS  IDFMARK8  
   4 6GCW ...#SKI
  19 6GH2          IDFSCAN  SYSOF,CPPTR(3),7    [LOOK FOR THIS LINE IN IDF   
  19 6H2L          LDN   6  0                   [SET MARK TO SHOW MOPPED ON. 
  20 6HG=    SYSOF HUNT2    3,AMXOR,ALINES,AWORK4(2)    [RELOCATE ALINES ENTRY   
   7 6H_W          LDX   5  7
   7 6JFG          SLL   5  2
   7 6J_6          ADX   3  5
  19 6KDQ          BNZ   6  X0                  [J. IF MOPPED OFF ALREADY.   
  19 6KYB          DLA   6  A1+FOURTHWD(3)      [CLEAR THE OLD DEVICE TYPE.  
  21 6LD2          LDXC  5  ACOMMUNE2(2)        [J. IF TERMINAL DEVICE TYPE UNKNOWN  
   8 6LXL          BCS      X0   
  17 6MC=          LDCH  6  5                   [GET INTERNAL TYPE # 
   9 6MWW    #SKI  K6MULTIPLX>99-99  
   4 6NBG    (   
  18 6NW6          BZE   6  U4A                 [SHOULD NEVER BE ZERO,   
  21 6P*Q          LDN   0  HIGHTYPE            [OR > HIGHEST INTERNAL TERMINAL TYPE.
   8 6PTB          BXL   0  6,U4B
   4 6Q*2    )   
  17 6QSL          ERN   6  ASENTMACH           [J. IF NOT SENTINEL. 
   8 6R#=          BNZ   6  X2   
  20 6RRW          ORN   5  #4000               [PUT IN ^ALREADY INTERRUPTED^ BIT
  17 6S?G    X2    ORS   5  A1+FOURTHWD(3)      [SET NEW DEVICE TYPE 
  16 6SR6          BZE   6  X1A                 [J IF SENTINEL   
  19 6T=Q          ERN   6  ASENTMACH$APCTMACH  [J. IF REMOTE BATCH TERMINAL.
   8 6TQB          BZE   6  XPCT 
   9 6TS_ ...      FSHCODE  B,NOPRPR 
   4 6TXJ ...(   
  21 6W27 ...      BRN      NOPRA4              [ OMIT THE FSHACTPR - ALREADY DONE   
   5 6W4Q ...NOPRPR  
   4 6W7* ...)   
   4 6W=2    [   
  14 6WPL    [THIS SECTION SETS UP THE LINE CONTROL BLOCK
   9 6X9=    [ON ENTRY X7=LINE NO.   
   4 6XNW    [   
   5 6Y8G    Z4SCAN  
   9 6Y?5 ...      FSHCODE  B,NOPRA4 
   4 6Y*N ...(   
   7 6YD? ...      FSHACTPR  
   5 6YGW ...NOPRA4  
   4 6YKF ...)   
   9 6YN6          SMO      AWORK4(2)
  21 6_7Q          LDX   4  AWORK1              [OBTAIN MAX. CHAR. TRANSFER COUNT.   
   9 6_MB          ADN   4  ZALEN*4+3
  21 7272          SRL   4  2                   [FORM LENGTH OF LINE CONTROL BLOCK   
  19 72LL          SETUPCORE 4,3,AMXOR,ALCB,1,1,2 [GET LINE CONTROL BLOCK.   
  17 736=          STOZ     A1+BPTR(3)          [ZEROISE THE BLOCK.  
  10 73KW          LDN   0  A1+BPTR(3)   
  10 745G          LDN   1  A1+BPTR+1(3) 
  11 74K6          MOVE  0  BUFFER-A1-BPTR-1 
   9 754Q          NGS   3  GTIM(3)  
   7 75JB          LDX   0  7
  10 7642          STO   0  JSOURCE2(2)  
   9 76HL          LDX   1  AWORK4(2)
  10 773=          LGEOG    CPPTR(1),0   
  10 77GW          STO   0  JSOURCE1(2)  
   9 782G          STO   1  IDENTM(3)
  10 78G6          STO   7  GUNITNO(3)   
  10 78_Q          LTYPE    CPPTR(1),0   
  10 79FB          DCH   0  JSOURCE1(2)  
   8 79_2          LDX   1  FX1  
  10 7=DL          LDX   0  XAMXMSKPR(1) 
  17 7=Y=          STO   0  AMXMSK(2)           [SET MPX MASK WORD   
  10 7?CW          LDX   0  XCONTEXT(1)  
  16 7?XG          STO   0  CONTEXT(2)          [SET CONTEXT BITS
   9 7#C6          LDN   0  TNOCHS(1)
   9 7#WQ          LDN   1  GNOCHS(3)
  19 7*BB          MOVE  0  TERMLEN             [INSERT INFORMATION IN LCB   
  13 7*W2          HUNT2    2,AMXOR,ALINES,AWORK4(2) 
   7 7B*L          LDX   5  7
   7 7BT=          SLL   5  2
   7 7C#W          ADX   2  5
  20 7CSG          RINGTWO  A1(2),A1(3)         [ENRING LCB WITH ALINES ENTRY.   
  17 7D#6          LDX   0  GSIGN               [MARK 'CPA SET UP'.  
  11 7DRQ          ERS   0  A1+FOURTHWD(2)   
  21 7F?B          ACROSS   MORNING,1           [OUTPUT THE 'GOOD MORNING' MESSAGE   
   4 7FR2    [   
   4 7G=L    #   
   4 7GQ=    #   
   4 7H9W    #END
   8 ____ ...66745237000200000000
