   9 22FL    #OPT  K0EDSTART=K0OLP   
   9 22_=    #OPT  K6EDSTART=K6OLP   
   7 23DW    #LIS  K0EDSTART 
  14 23YG    #SEG  EDSTART72                    [7OPAE   
  10 24D6                   8HEDSTART7   
  12 24XQ          SEGENTRY K1EDSTART,Z1EDSTART  
  12 25=7 ...      SEGENTRY K2EDSTART,ZINCRECUBS 
  12 25JJ ...      SEGENTRY K3EDSTART,ZGETBLOCKS 
   4 25X2    #   
  19 26BL    # THIS SEGMENT IMPLEMENTS THE 1ST PERI TO AN OFF-LINE DA FILE,  
  20 26W=    # NON-TRANSFER MODES TO AN OFF-LINE FILE AND IS RETURNED TO AFTER AN
  21 273D ...# ILLEGAL OR MONITOR EVENT. IT IS ALSO ENTERED (ACROSS9 FROM EDOBJ IF   
  19 278L ...[ CUBS IS TO BE INCREASED & (DOWN) IF MORE BLOCKS ARE REQUIRED  
   2 27*W
   4 27TG    #   
   8 28*6    #DEF  MAXMODE=#1500 
   2 28SQ
   6 29#B    #DEF  X=#100
   2 29S2
  21 2=?L    #DEF  NONE=0                       [INDICATES NO MONITORING ON THIS MODE
  19 2=R=    #DEF  MEXT=1                       [    ^     MONITOR ON EXTEND 
  19 2?=W    #DEF  MREN=2                       [    ^        ^       RENAME 
  19 2?QG    #DEF  MCLO=3                       [    ^        ^       CLOSE  
   2 2#=6
   6 2#PQ    #DEF  ORD=0 
  20 2*9B    #DEF  OPEN=#1                      [    ^     THIS IS AN OPEN MODE  
  21 2*P2    #DEF  OFWR=#2                      [    ^     FILE MUST BE OPEN IN WRITE
  21 2B8L                                       [                                MODE
   5 2BN= ...MODETYPE
  12 2C7W ...               +NONE*X+9*X+1*X+OPEN 
  12 2CMG ...               +NONE*X+9*X+1*X+OPEN 
  12 2D76 ...               +NONE*X+9*X+1*X+OPEN 
  12 2DLQ ...               +NONE*X+9*X+1*X+OPEN 
  12 2F6B ...               +MEXT*X+9*X+4*X+OFWR 
  12 2FL2 ...               +NONE*X+5*X+4*X+OPEN 
  12 2G5L ...               +MREN*X+9*X+6*X+OFWR 
  12 2GK= ...               +MCLO*X+5*X+3*X+ORD  
  12 2H4W ...               +NONE*X+17*X+1*X+ORD 
  12 2HJG ...               +NONE*X+17*X+1*X+OPEN
  12 2J46 ...               +MCLO*X+5*X+1*X+OFWR 
  12 2JHQ ...               +NONE*X+8*X+7*X+ORD  
  12 2K3B ...               +MEXT*X+6*X+6*X+OFWR 
   2 2KH2
   9 2L2L    MONTYPE        +BBEXTEND
   9 2LG=                   +BBRENAME
   9 2L_W                   +BBCLOSE 
   2 2MFG
   2 2M_6
   5 2NDQ    OBJLINK 
   9 2NYB          PAIR     EDOBJ,1  
   6 2PD2    STARTLINK   
   9 2PXL          PAIR     EDSTART,1
   4 2P_T ...[   
  17 2Q44 ...TOPCA          [CHANGE TO APET LEAVING LINK TO K1EDSTART
  15 2Q6? ...[              LINK X4; EXPECTS X1=FX1; NO COOR 
  10 2Q8G ...      TOPCA1   STARTLINK(1) 
   7 2Q=P ...      EXIT  4  0
   4 2Q#Y ...[   
   2 2QC=
  21 2QWW    Z1EDSTART                          [ENTRY ON 1ST PERI & AFTER MONITOR OR
  21 2RBG                                       [                             ILLEGAL
  10 2RW6          LDX   0  EDPTIME(2)   
   9 2S*Q          BPZ   0  NOT1ST   
   9 2STB          LDX   0  CLEANCT  
  18 2T*2          STO   0  EDPTIME(2)          [INITIALISE PERI TIMER WD
   5 2TSL    NOT1ST  
  16 2W#= ...      LDX   3  EVENT4(2)           [TYPE-MODE WORD  
  16 2XR6          LDCH  4  3                   [B18-23 OF MODE  
   7 2Y=Q          SRL   3  6
   7 2YQB          LDX   6  3
  16 2_=2          ANDN  3  #17                 [B14-17 OF MODE  
  18 2_PL          BNZ   3  NOTRAN              [JIF NOT A TRANSFER MODE 
   9 329= ...      ACROSS   EDOBJ,1  
   5 32NW    NOTRAN  
  10 34MB          LDN   0  MAXMODE/#100 
  17 3572          BXL   0  3,NOTMODE           [JIF NOT A VALID MODE
  14 35LL          BNZ   4  NOTMODE             [   DITTO
   7 366=          SBN   3  1
   7 36KW          LDX   4  3
   8 375G          SMO      FX1  
  10 37K6          LDCH  7  MODETYPE(3)  
  20 384Q          BZE   7  NOMON               [JIF NO MONITORING ON THIS MODE  
   7 38JB          SMO      7
  17 3942          LDX   5  MONTYPE-1(1)        [GET MONITORING TYPE 
  15 39HL          TESTMON  APETADDR(2),5,FTYPNO(2),MONITOR  
   5 3=3=    NOMON   
   8 3=GW          ANDN  6  #700 
  19 3?2G          BNZ   6  MODERR              [JIF INVALID ADDITIVE MODE   
   8 3?G6          SMO      FX1  
  10 3?_Q          LDX   7  MODETYPE(3)  
   8 3#FB          ANDN  7  OPEN 
  17 3#_2          BZE   7  NOTOPEN             [JIF NOT AN OPEN MODE
  10 3*DL          LDXC  0  ALTMARK(2)   
  21 3*Y=          BCS      OPALRDY             [JIF AN OPEN MODE & FILE ALREADY OPEN
   5 3BCW    NOTOPEN 
   7 3BXG          BCHX  3  /
   8 3CC6          SMO      FX1  
  15 3CWQ          LDCH  7  MODETYPE(3)         [CA LENGTH   
  18 3DBB ...      CHECKA   ACAP(2),7,RVCA,APETADDR(2),,OKCA,,,K1EDSTART 
   4 3DW2    OKCA
   7 3KQ= ...      BCHX  3  /
   8 3L9W ...      SMO      FX1  
  10 3LPG ...      LDCH  7  MODETYPE(3)  
   9 3MNQ          LDX   5  ACAP(2)  
   9 3N8B          LDN   6  ACA1(2)  
  20 3NN2          FOUTMOVE APETADDR(2),5,6,7,HLOCK1,2  [MOVE REQD CA TO AOLPT   
   9 3PM=          STOZ     ACA2(2)  
   8 3PPF ...      SMO      FX1  
  10 3PRN ...      LDX   7  MODETYPE(3)  
   8 3PTX ...      ANDN  7  OFWR 
  20 3PY6 ...      BZE   7  NOCHWR              [JIF FILE DOESN'T HAVE TO BE OFW 
  21 3Q2* ...      JBC      NOTWRITE,FILERING(2),BAMWRITE [J IF FILE NOT OPEN FOR WRI
   5 3Q4J ...NOCHWR  
   7 3Q6W          TOPFCB2  1
   8 3QLG          ADX   4  FX1  
  21 3R66          EXIT  4  /                   [EXIT TO JUMP TO APPROPRIATE ROUTINE 
   8 3RKQ          BRN      M100 
   8 3S5B          BRN      M200 
   8 3SK2          BRN      M300 
   8 3T4L          BRN      M400 
   8 3TJ=          BRN      M500 
   8 3W3W          BRN      M600 
   8 3WHG          BRN      M700 
   8 3X36          BRN      M1000
   8 3XGQ          BRN      M1100
   8 3Y2B          BRN      M1200
   8 3YG2          BRN      M1300
   8 3Y_L          BRN      M1400
   8 3_F=          BRN      M1500
   2 3_YW
  17 42DG    M100                               [OPEN IN OVERLAY MODE
  17 42Y6    M200                               [OPEN IN READ MODE   
  17 43CQ    M300                               [OPEN IN WRITE MODE  
  21 43XB    M400                               [OPEN IN OFFSET (AT END OF FILE) MODE
   9 44C2          LDX   0  AEFLOW(2)
  17 44WL          STO   0  ACA6(2)             [2ND LEVEL O'FLOW BKT
   9 45B=          LDX   0  AEORG(2) 
  17 45TW          STO   0  ACA8(2)             [FILE ORGANISATION   
   9 46*G          LDX   0  AEKEY(2) 
  15 46T6          STO   0  ACA9(2)             [KEY DETAILS 
  21 47#Q          LDN   7  9                   [LENGTH OF CA TO BE MOVED TO OBJ PROG
   4 47SB    NAME
   9 48#2          LDN   4  FLOC1(1) 
   9 48RL          LDN   5  ACA3(2)  
  14 49?=          MOVE  4  3                   [FILENAME
   9 49QW          LDX   0  FGEN(1)  
   8 4==G          SLL   0  12   
  10 4=Q6          ORX   0  FVERSION(1)  
  17 4?9Q          STO   0  ACA7(2)             [GENERATION & VERSION
   8 4?PB          BRN      REPLY
   2 4#92
  14 4#NL    M700                               [RENAME  
   7 4*8=          LDN   7  7
   8 4*MW          BRN      NAME 
   2 4B7G
  16 4BM6    M1100                              [READ FILE SPEC  
   8 4C6Q          LDCT  0  #200 
  18 4CLB          STO   0  ACA2(2)             [UDAS INDICATOR IN REPLY 
   2 4D62
  16 4DKL    M1200                              [CREATE & OPEN   
   9 4F5=          LDX   0  AEFLOW(2)
  17 4FJW          STO   0  ACA6(2)             [2ND LEVEL O'FLOW BKT
   9 4G4G          LDX   0  AEINF3(2)
   9 4GJ6          LDX   4  FSIZE(1) 
   9 4H3Q          BZE   4  NOAREA   
  20 4HHB          ORN   0  1                   [1 FILE AREA IF FILE NOT EMPTY   
   5 4J32    NOAREA  
  20 4JGL          STO   0  ACA8(2)             [INT. CODE & NO. OF FILE AREAS   
  10 4K2=          LDX   0  AEWRITDAY(2) 
  15 4KFW          STO   0  ACA9(2)             [DATE WRITTEN
  10 4K_G          LDX   0  FENDBUCK(1)  
   7 4LF6          ADN   0  1
  16 4LYQ          STO   0  ACA13(2)            [END OF FILE BKT 
   9 4MDB          STOZ     ACA14(2) 
   9 4MY2          STOZ     ACA15(2) 
   9 4NCL          STOZ     ACA16(2) 
   9 4NX=          STOZ     ACA17(2) 
   8 4PBW          LDN   7  17   
   8 4PWG          BRN      NAME 
   2 4QB6
  16 4QTQ    M500                               [EXTEND/CONTRACT 
  21 4R*B          LDN   7  0                   [INDICATE #500 (AS OPPOSED TO #1500) 
  15 4RT2          LDX   4  ACA4(2)             [NO. H/W BLOX
  12 4S#L          SRA   4  1                   [
  18 4SS=          SRA   4  1                   [ROUND TO MULTIPLE OF 8  
  12 4T?W          SRA   4  1                   [
  18 4TRG          SLL   4  3-BLS               [   & CONVERT TO BS BLOX 
  16 4W?6          BPZ   4  NOTCONTRACT         [JIF NOT CONTRACT
   9 4WQQ          ADX   4  FSIZE(1) 
  10 4X=B          SMO      FILERING(2)  
   9 4XQ2          LDX   5  FREADWORD
   9 4Y9L          SMO      ALOG(2)  
   7 4YP=          SLL   5  0
   9 4_8W          SRL   5  GSBSLOG  
  20 4_NG          SBX   4  5                   [CAN'T LOSE E.O.F. BKT IF OFFSET 
   9 5286          BPZ   4  NOTNEG   
  18 52MQ          LDN   4  0                   [FSIZE CAN'T BECOME -VE  
   5 537B    NOTNEG  
  21 53M2          ADX   4  5                   [X4=RESULTANT FILE SIZE IN BS BLOX   
   7 546L          LDX   5  4
   9 54L=          ADN   5  FBLKS-A1 
   9 555W          SBX   5  FBLMOD(1)
  21 55KG          BPZ   5  NOTFBLMOD           [JIF FREEING PART OF FILE NOT WRITTEN
   7 5656          NGX   5  5
  19 56JQ          SUBCUBS  ,5,JOB,DEPTH        [REDUCE ^CURRENT USED B.S.^  
   7 574B          TOPFCB2  3
  15 57J2          SBS   5  FBLMOD(3)           [RESET FBLMOD
   4 583L    #   
  15 58H=    #     FREE ANY USAGE BLOX FOR THE BS BLOX FREED.
   4 592W    #   
  15 59GG          LDX   1  FPTR(3)             [SKIP FSTACK 
   5 5=26    SBUFFS  
   9 5=FQ          LDX   1  FPTR(1)  
   5 5=_B    SBUFFT  
  18 5?F2          BXE   1  CXFI,NOBUFF         [JIF END OF FILE CHAIN   
   9 5?YL          LDX   0  ATYPE(1) 
   8 5#D=          SRL   0  12   
   9 5#XW          ERN   0  FILE+FCB 
  17 5*CG          BZE   0  NOBUFF              [JIF NEXT FILE'S FCB 
  10 5*X6          ERN   0  FEXTRA$FCB   
  19 5BBQ          BZE   0  NOBUFF              [JIF THIS WORKFILE'S FEXTRA  
  10 5BWB          ERN   0  FURRB$FEXTRA 
  15 5CB2          BZE   0  YBUFF               [JIF A FURRB 
  10 5CTL          ERN   0  FURWB$FURRB  
  16 5D*=          BNZ   0  SBUFFS              [JIF NOT A FURWB 
   5 5DSW    YBUFF   
  17 5F#G          BZE   4  RIDBUFF             [JIF WHOLE FILE FREED
   9 5FS6          LDX   0  BACK2(1) 
   8 5G?Q          ORN   0  BLN-1
  16 5GRB          ERN   0  BLN-1               [CLEAR BLT NO.   
   7 5H?2          LDX   5  4
   5 5HQL    STILLQ  
   7 5J==          SMO      5
  10 5JPW          TXU   0  FBLKS-1(3)   
  18 5K9G          BCC      SBUFFS              [JIF BLK STILL IN FILE   
   9 5KP6          BCT   5  STILLQ   
   5 5L8Q    RIDBUFF 
  18 5LNB          LDX   5  FPTR(1)             [KEEP PTR. TO NEXT BLK   
  15 5M82          FREECORE 1                   [FREE BUFFER 
   7 5MML          LDX   1  5
   9 5N7=          BRN      SBUFFT   
   5 5NLW    NOBUFF  
  17 5P6G          LDX   1  3                   [RESTORE PTR. TO FCB 
   6 5PL6    NOTFBLMOD   
  16 5Q5Q          NGX   5  FSIZE(1)            [OLD FILE SIZE   
  16 5QKB          STO   4  FSIZE(1)            [NEW FILE SIZE   
   9 5R52          BRN      SHRUNK   
   6 5RJL    NOTCONTRACT 
  17 5S4=          BNZ   4  XTEND               [JIF NOT DUMMY EXTEND
  16 5SHW          LDX   4  FSIZE(1)            [NEW FILE SIZE   
  20 5T3G          NGX   5  4                   [OLD FILE SIZE (I.E. NO CHANGE)  
   8 5TH6          BRN      ZERO 
   2 5W2Q
  16 5WGB    M1500                              [EXTEND INTO AREA
  20 5X22          LDN   7  4                   [#1500 INDICATOR & REPLY LENGTH  
  14 5XFL          LDXC  3  ACA6(2)             [END SEG 
   8 5X_=          BCS      XTERR
  15 5YDW          LDXC  4  ACA5(2)             [START SEG   
   8 5YYG          BCS      XTERR
  12 5_D6          SRA   3  1                   [
  16 5_XQ          SRA   3  1                   [ROUND UP END SEG
   7 62CB          SRA   3  1
  17 62X2          SRL   4  3                   [ROUND DOWN START SEG
  19 63BL          SBX   3  4                   [GIVING NO. SEGS PER CYL /8  
   8 63W=          BNG   3  XTERR
   8 64*W          BZE   3  XTERR
  19 64TG          SLL   3  3-BLS               [CONVERT TO BS BLOX PER CYL  
   8 65*6          LDX   5  GSIGN
  18 65SQ          ORS   5  ACA4(2)             [SET ^NOT FULL CYL^ REPLY
  14 66#B          ERX   5  ACA4(2)             [END CYL 
   7 66S2          NGNC  4  1
  15 67?L          ANDX  4  ACA3(2)             [START CYL   
   7 67R=          SBX   5  4
   8 68=W          BNG   5  XTERR
  15 68QG          ADN   5  1                   [NO. OF CYLS 
  17 69=6          MPY   3  5                   [X4=TOTAL NO. BS BLOX
   8 69PQ          BNZ   3  XTERR
   5 6=9B    XTEND   
   9 6=P2          ADX   4  FSIZE(1) 
   9 6?8L          SBN   4  FILESIZE 
   9 6?N=          LDN   0  FILESIZE 
  20 6#7W          BNG   4  NUFF                [JIF WOULDN'T BE > MAX FILE SIZE 
   8 6#MG          BZE   4  NUFF 
  15 6*76          BNZ   7  XTERR               [JIF #1500   
  18 6*LQ          SBX   0  FSIZE(1)            [GIVING AMOUNT AVAILABLE 
  17 6B6B          SRL   0  3-BLS               [CONVERT TO H/W BLOX 
  19 6BL2          SLL   0  3                   [   & ROUND TO MULTIPLE OF 8 
   9 6C5L          BNZ   0  REPFAIL  
  17 6CK=          LDN   0  1                   [NONE AVAILABLE REPLY
   9 6D4W          BRN      REPFAIL  
   4 6DJG    NUFF
   7 6F46          ADX   0  4
   9 6FHQ          LDX   4  FSIZE(1) 
  16 6G3B          STO   0  FSIZE(1)            [NEW FILE SIZE   
  16 6GH2          NGX   5  4                   [OLD FILE SIZE   
   5 6H2L    SHRUNK  
  11 6HG= ...      MBS      1,BFALTR,BFALTB  
  15 6JFG          BNZ   7  REPLY               [JIF #1500   
   4 6J_6    ZERO
   9 6KDQ          SLL   4  GSBSLOG  
   9 6KYB          SMO      ALOG(2)  
  18 6LD2          SRL   4  0                   [CONVERT REPLY TO BKTS   
  10 6LXL          SMO      FILERING(2)  
  17 6MC=          SBX   4  FREADWORD           [ZERO UNLESS OFFSET  
  15 6MWW          STO   4  ACA2(2)             [MAIN REPLY  
  21 6NBG          ADX   5  FSIZE(1)            [GIVING NO. BLOX EXTENDED BY (WILL BE
  21 6NW6          SLL   5  GSBSLOG             [                   -VE FOR CONTRACT)
   9 6P*Q          SMO      ALOG(2)  
  16 6PTB          SRA   5  0                   [CONVERT TO BKTS 
  16 6Q*2          STO   5  ACA3(2)             [SUBSIDIARY REPLY
  15 6QSL          STOZ     ACA5(2)             [CSN REPLY   
   7 6R#=          LDN   7  5
   8 6RRW          BRN      REPLY
   2 6S?G
  17 6SR6    M600                               [OPEN SCRATCH FILE   
   9 6T=Q          LDX   5  AEORG(2) 
  16 6TQB          ANDN  5  #77                 [BKT SIZE OF FILE
   9 6W=2          LDX   6  FSIZE(1) 
  18 6WPL          SLL   6  BLS                 [SIZE OF FILE IN H/W BLOX
   9 6X9=          LDX   0  ABITS(2) 
  18 6XNW          BZE   0  NMAYB               [JIF 'MAYBE' NOT GIVEN   
  17 6Y8G          BXE   5  ACA3(2),OKBUCK      [JIF BKT SIZE CORRECT
   7 6YN6          LDN   6  1
   5 6_7Q    TOOSMALL
   8 6_MB          ORX   6  GSIGN
   9 7272          STO   6  ACA2(2)  
   7 72LL          LDN   7  2
   9 736=          BRN      NOTOPEND 
   5 73KW    OKBUCK  
  17 745G          BXL   6  ACA4(2),TOOSMALL    [JIF FILE TOO SMALL  
   5 74K6    NMAYB   
  16 754Q          STO   5  ACA3(2)             [BKT SIZE REPLY  
  16 75JB          STO   6  ACA4(2)             [FILE SIZE REPLY 
  10 7642          SLL   6  GSBSLOG-BLS  
   9 76HL          SMO      ALOG(2)  
   7 773=          SRL   6  0
  17 77GW          STO   6  ACA2(2)             [LAST BKT NO. IN FILE
  15 782G          STOZ     ACA5(2)             [CSN REPLY   
   7 78G6          LDN   7  5
   8 78_Q          BRN      REPLY
   2 79FB
  14 79_2    M1000                              [CLOSE   
   9 7=DL          LDX   4  ACA3(2)  
  20 7=Y=          BZE   4  REL                 [JIF E.O.F. BKT NOT TO BE UPDATED
   7 7?CW ...      TOPFCA2  3
  20 7?XG ...      JBC      REL,3,BAMWRITE      [CAN'T UPDATE EOF BUCKET IF READ 
  17 7*BB          BPZ   4  TAG                 [ALWAYS UPDATE IF +VE
   9 7*W2          SETMODE  0,OFFSET 
  10 7B*L          ANDX  0  FSCMODE(2)   
  21 7BT=          BNZ   0  REL                 [CAN'T REDUCE E.O.F. BKT IF OFFSET   
   4 7C#W    TAG 
  10 7CSG          ADS   4  FENDBUCK(1)  
   9 7D#6 ...      BS       1,BFALTR 
   4 7F?B    REL 
   9 7FR2          RELOFP   FTYPNO(2)
   2 7G=L
  17 7GQ=    M1300                              [CLOSE & DELETE FILE 
   9 7H9W          SETMODE  0,OFFSET 
  10 7HPG          ANDX  0  FSCMODE(2)   
  16 7J96          BNZ   0  NOTWRITE            [CAN'T IF OFFSET 
   9 7JNQ          LDX   0  FSIZE(1) 
  17 7K8B          BNZ   0  NOTEMPTY            [CAN'T IF NOT EMPTY  
   7 7KN2          LDN   7  2
  15 7L7L          CALL  6  REPMOVE             [SET REPLY   
   8 7LM=          BRN      REL  
   2 7M6W
  17 7MLG    M1400                              [DESCRIBE FILE AREA  
   9 7N66          LDX   0  ACA7(2)  
   7 7NKQ          SBN   0  1
  18 7P5B          BNG   0  OUTRANGE            [JIF BKT NO. -VE OR ZERO 
  10 7PK2          SMO      FILERING(2)  
  17 7Q4L          ADX   0  FREADWORD           [ZERO UNLESS OFFSET  
   9 7QJ=          LDX   4  FSIZE(1) 
   9 7R3W          SLL   4  GSBSLOG  
   9 7RHG          SMO      ALOG(2)  
   7 7S36          SRL   4  0
  18 7SGQ          BXGE  0  4,OUTRANGE          [JIF BKT NO. OUT OF RANGE
   8 7T2B          LDCT  0  #200 
  17 7TG2          STO   0  ACA2(2)             [UDAS INDICATOR REPLY
   9 7T_L          STOZ     ACA3(2)  
   8 7WF=          LDCT  0  #400 
  17 7WYW          STO   0  ACA4(2)             [^NOT FULL CYL^ REPLY
   9 7XDG          STOZ     ACA5(2)  
   9 7XY6          STOZ     ACA6(2)  
  10 7YCQ          SMO      FILERING(2)  
   9 7YXB          SBX   4  FREADWORD
  18 7_C2          ADN   4  1                   [GIVING LAST BKT NO. +1  
   9 7_WL          STO   4  ACA7(2)  
   8 82B=          LDCT  0  #40  
  19 82TW          STO   0  ACA8(2)             [G3 FILESTORE FILE INDICATOR 
   7 83*G          LDN   7  8
   8 83T6          BRN      REPLY
   2 84#Q
   5 84SB    NOTWRITE
  20 85#2          LDN   0  5                   [FILE NOT OPEN FOR WRITING REPLY 
   9 85RL          BRN      REPFAIL  
   5 86?=    XTERR   
  17 86QW          LDN   0  4                   [INVALID #1500 REPLY 
   9 87=G          BRN      REPFAIL  
   5 87Q6    NOTEMPTY
  19 889Q          LDN   0  1                   [#1300 FILE NOT EMPTY REPLY  
   9 88PB          BRN      REPFAIL  
   5 8992    OUTRANGE
  19 89NL          LDN   0  0                   [#1400 BKT OUT OF RANGE REPLY
   5 8=8=    REPFAIL 
  14 8=MW          ORX   0  GSIGN               [FAIL BIT
   9 8?7G          STO   0  ACA2(2)  
   7 8?M6          LDN   7  2
   2 8#6Q
   5 8#LB    REPLY   
   8 8*62          LDX   0  GSIGN
  17 8*KL          ORS   0  ALTMARK(2)          [SET FILE OPEN MARKER
  19 8B5=    NOTOPEND                           [FAILED #600. FILE NOT OPENED
  15 8BJW          CALL  6  REPMOVE             [SET REPLIES 
   8 8C4G          LDX   1  FX1  
  10 8CJ6          OLPFIN1  OBJLINK(1)   
   2 8D3Q
   5 8DHB    REPMOVE 
   9 8F32          LDN   4  ACA1(2)  
   9 8FGL          LDX   5  ACAP(2)  
  14 8G2=          FINMOVE  APETADDR(2),5,4,7,HLOCK1,2   
   7 8GFW          EXIT  6  0
   2 8G_G
   5 8HF6    NOTMODE 
   9 8HYQ          LDN   3  FYMNOTREC
   8 8JDB          BRN      XILL 
   5 8JY2    MODERR  
   9 8KCL          LDN   3  FYMODE   
   8 8KX=          BRN      XILL 
   5 8LBW    OPALRDY 
   9 8LWG          LDN   3  FYALLOC  
   8 8MB6          BRN      XILL 
   4 8MTQ    RVCA
   9 8N*B          LDN   3  FYNOCA   
   2 8NT2
   2 8P#L
   4 8PS=    XILL
  19 8Q?W          ACROSS   OLPODD,5            [FOR TOPCA1,UNPLUG,ILLEGAL   
   2 8QRG
   5 8R?6    MONITOR 
   9 8RQQ          LDX   6  FTYPNO(2)
   9 8S=B          LDX   7  ACAP(2)  
  16 8SQ2 ...      CALL  4  TOPCA               [CHANGE TO APET  
   7 8T9L          REPMON   1
   4 8T9T ...[   
   4 8T=4 ...[   
  20 8T=? ...ZINCRECUBS     [ENTRY FROM EDOBJ(ACROSS) TO INCREASE CUBS BY [X6]   
   4 8T=G ...[   
   9 8T=P ...      INCRECUB ANSWER,,6
  11 8T=Y ...      TESTREP2 REFUSED,REFUSED  
  15 8T?7 ...      ACROSS   EDOBJ,2             [OK - RETURN 
   4 8T?B ...[   
   4 8T?K ...[   
   5 8T?S ...REFUSED 
  16 8T#3 ...      CALL  4  TOPCA               [CHANGE TO APET  
  15 8T#= ...      ACROSS   OLPODD,3            [ASK FOR MORE
   4 8T#F ...[   
   4 8T#N ...[   
  17 8T#X ...ZGETBLOCKS     [ENTRY DOWN FROM EDOBJ TO GET MORE BLOCKS
   4 8T*6 ...[   
   4 8T** ...#   
  21 8T*J ...#     THE NEXT SECTION GETS MORE BLOX FOR THE FILE WHEN THERE ARE NOT   
  10 8T*R ...#     ENOUGH FOR THIS PERI. 
   4 8TB2 ...#   
   7 8TB9 ...      TOPFCB2  1
  19 8TBD ...      SBN   3  FBLKS-A1-10         [TRY TO GET 10 MORE THAN REQD
  11 8TBM ...      BXL   3  FSIZE(1),NOTMAX  
  20 8TBW ...      LDX   3  FSIZE(1)            [TOO MUCH - GET AS MUCH AS POSS  
   7 8TC5 ...      SBN   3  1
   5 8TC# ...NOTMAX  
   9 8TCH ...      ADN   3  FBLKS-A1 
  20 8TCQ ...      LDX   5  FUSEBL(1)           [REMEMBER END OF BLK NOS. IN FCB 
  12 8TC_ ...      BXL   3  ALOGLEN(1),NOALTLEN  
   7 8TD8 ...      LDX   4  1
  11 8TDC ...      ALTLENGD 4,1(3),REFINDFCB 
   5 8TDL ...NOALTLEN
  18 8TDT ...      ADN   3  3                   [GIVING NEW FCB SIZE +2  
  19 8TF4 ...      SBX   3  5                   [NOW = NO. OF XTRA BLOX +2   
  16 8TF? ...      SETUPCOR 3,1,BSTB,EMPTYB     [FOR XTRA BLOX   
  15 8TFG ...      STO   3  A1(1)               [REC. HEADER 
   5 8TFP ...NEWBLOX 
   7 8TFY ...      TOPFCB2  2
   9 8TG7 ...      LDX   4  BSPRE(2) 
  18 8TGB ...      STO   4  A1+1(1)             [RESIDENCE (LOG. FILE)   
  15 8TGK ...      GETBAX                       [GET NEW BLOX
   4 8TGS ...[   
  19 8TH3 ...      SEGENTRY BSCLEARZ            [UNIVERSAL FOR BSCLEAR MACRO 
  18 8TH= ...      BRN      XRES                [NULLED BY BSCLEAR MACRO 
  17 8THF ...      DOWN     CREATE,3            [TO ZEROIZE NEW BS   
   4 8THN ...XRES
   9 8THX ...      CALL  1  REFINDFCB
  21 8TJ6 ...      BXE   4  BSPRE(2),NCOPIED    [JIF RESIDECE NOT CHANGED (COPYFILE) 
  20 8TJ* ...      FREEBAX                      [FREE NEW BLOX ON OLD RESIDENCE  
  10 8TJJ ...      MHUNTW   1,BSTB,EMPTYB
  20 8TJR ...      BRN      NEWBLOX             [J TO GET BLOX ON NEW RESIDENCE  
   3 8TK2 ...
   6 8TK9 ...REFINDFCB   
  18 8TKD ...      TOPFCB   2                   [RELOCATE FCB FOR ALTLENG
   7 8TKM ...      EXIT  1  0
   3 8TKW ...
   5 8TL5 ...NCOPIED 
  10 8TL# ...      MHUNTW   1,BSTB,FULLB 
  16 8TLH ...      LDN   4  A1+2(1)             [-> BS BLK NOS.  
  16 8TLQ ...      ADN   5  A1(2)               [-> END OF FCB   
  18 8TL_ ...      SBN   3  2                   [GIVING NO. OF NEW BLOX  
  15 8TM8 ...      MOVE  4  0(3)                [BUNG 'EM IN 
  20 8TMC ...      ADS   3  FUSEBL(2)           [UPDATE NO. OF BLOX THE FILE HAS 
  15 8TML ...      FREECORE 1                   [FREE FULLB  
   5 8TMT ...      UP
   4 8TN4 ...[   
   4 8TN? ...[   
  12 8TP=          MENDAREA AOLPGAP,K99EDSTART   
   4 8W8W    #END
   8 ____ ...26231752000200000000
