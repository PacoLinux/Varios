  17 22FL    #SEG  SETUPB                       [GENERAL EMS - COMM  
  15 22PD ...[      (C) INTERNATIONAL COMPUTERS LIMITED 1982 
   7 22_=    #OPT  K0SETUPB=0
  13 23DW    #LIS  K0SETUP>K0EMS>K0ALLGEO>K0SETUPB   
   9 23YG                   8HSETUPB 
   7 24D6    # E.M.S. SEGMENT
   7 24XQ    # ENTRY POINTS  
  11 25CB          SEGENTRY K1SETUPB,Z1SETUPB
  12 25X2          MENDAREA 10,K100SETUPBSLINK3  
   4 26BL    [   
   5 26W=    SLINK3  
  10 27*W          PAIR     MOPSTART,3   
   5 28SQ    SLINK6  
   9 29#B          PAIR     SJLOOP,1 
   5 29S2    SLINK7  
  10 2=?L          PAIR     LISTPROC,1   
   5 2=R=    SLINK8  
  10 2?=W          PAIR      SCHEDMTX,1  
   7 2#=6    #UNS  FSHTEST   
   4 2#PQ    #SKI
   5 2C97 ...SLINK10 
  10 2C=D ...      PAIR     PERFLOGS,1   
   4 2C?R ...[   
   8 2CCN ...[      SUBROUTINES  
   4 2CHK ...[   
   5 2CMG    #SKI  G3
   4 2D76    (   
  20 2DLQ    SCORAG4        [IN GEORGE 3 THIS ENTRY POINT IS EQUIVALENT TO SCORA 
   5 2F6B    SCORA   
   8 2FL2          SLL   6  12   
  19 2G5L          EMSACTX  6,2                 [GET PERMANENT ACTIVITY FROM 
  19 2GK=    #                                  [TOP OF STORE AND LOCATE IT  
   9 2H4W          LDX   2  BPTR(2)  
   7 2HJG          EXIT  4  0
  19 2J46    SCOREG4        [IN G3 THIS ENTRY POINT IS EQUIVALENT TO SCORE   
   5 2JHQ    SCOREG4A
   5 2K3B    SCORE   
   7 2KH2          STOZ     0
   5 2L2L    SCOREG4F
  19 2LG=          EMSCORE  2,0                 [EMSCORE WITH A DIFFERENCE   
   7 2L_W          EXIT  4  0
   4 2MFG    )   
   5 2M_6    #SKI  G4
   4 2NDQ    (   
   9 2NYB    # NO ROUNDING IF X0 0   
   5 2PD2    SCOREG4F
   8 2PXL          EMSCORE  2,0  
   7 2QC=          EXIT  4  0
  21 2QWW    SCORA SLL   6  12      [THIS ENTRY IS USED IF CORE MUST BE GOT AT TOP   
  20 2RBG          BRN      SCORAM  [OF STORE SO WE JUMP INTO FIXEMSACTX TO DO   
  16 2RW6                           [A STRAIGHT FORWARD EMSACTX  
  21 2S*Q    SCORAG4                [THIS ENTRY IS USED IFCORE CAN BE OBTAINED FROM  
  17 2STB          SLL   6  12      [SPARE SPACE AT END OF FIXED CORE
  10 2T*2          FIXEMSACTX 6,2,SCORAM 
   9 2TSL          LDX   2  BPTR(2)  
   7 2W#=          EXIT  4  0
  21 2WRW    SCORE BRN      SCOREM  [THIS ENTRY IS USED IF CORE MUST BE GOT AT TOP   
  20 2X?G                           [OF STORE-J INTO FIXEMSCR TO DO A STRAIGHT   
  13 2XR6                           [FORWARD EMSCORE 
  21 2Y=Q    SCOREG4                [THIS ENTRY IS USED IF CORE CAN BE OBTAINED FROM 
  20 2YQB                                       [SUT PAGE OR AT END OF FIXED CORE
  21 2_=2          LDX   0  2                   [ATTEMPT TO GET CORE FROM SUT PAGE   
   8 2_PL          ADN   0  A1   
   8 329=          LDN   1  1024 
   9 32NW          SBX   1  FSUTPAGE 
   7 338G          SBX   1  0
   4 33N6    #   
  20 347Q    # AFTER THE INTRODUCTION OF THE 'VOPA IN G4' ENHANCEMENT, A DUMMY   
  20 34MB    # BLOCK IS PLACED AT THE END OF THE SUT PAGE (JUST RED TAPE WORDS)  
  16 3572    # - SO WE MAKE SURE WE DONT TRY TO USE THESE WORDS. 
   4 35LL    #   
   6 366=    #SKI  G4VOPA
   4 36KW    (   
   6 375G    #UNS  CA1D  
   4 37K6    #SKI
   8 384Q          SBN   1  A1D  
   6 38JB    #UNS  CA1D  
   8 3942          SBX   1  CA1D 
   4 39HL    )   
   4 3=3=    #   
   9 3=GW          BNG   1  SCOREG4A 
   9 3?2G          LDX   1  FSUTPAGE 
   8 3?G6          ADX   1  BSUT 
   9 3?_Q          ADS   0  FSUTPAGE 
   9 3#FB          STO   0  ASIZE(1) 
  10 3#_2          STO   2  ALOGLEN(1)   
   8 3*DL          LDX   2  FX2  
   9 3*Y=          STO   2  BPTR(1)  
   9 3BCW          LDX   0  FPTR(2)  
   9 3BXG          STO   0  FPTR(1)  
   7 3CC6          SMO      0
   8 3CWQ          STO   1  BPTR 
   9 3DBB          STO   1  FPTR(2)  
   8 3DW2          LDX   1  FX1  
   7 3F*L          EXIT  4  0
  19 3FT=    SCOREG4A    [THIS ENTRY IS USED IF CORE MUST NOT BE IN SUT PAGE 
   9 3G#W          FIXEMSCR 2,SCOREM 
   7 3GSG          EXIT  4  0
   4 3H#6    )   
   4 3H*N ...[   
  10 3HC= ...[     SUBROUTINE PARAMETER  
   4 3HDS ...[   
   6 3HGB ...PARAMETER   
   8 3HHY ...      SBX   6  FX1  
  10 3HKG ...      OUTPACKX 3,4,5,PARM   
   7 3HLD ...      ADN   6  1
   4 3HMB ...PARM
  10 3HN# ...      MHUNTW   2,ADATA,EMS  
   8 3HP= ...      ADX   6  FX1  
   7 3HQ8 ...      EXIT  6  0
   4 3HRQ    [*  
   4 3J?B    [*  
   9 3JR2    [*        K1 ENTRY POINT
   4 3K=L    [*  
   4 3KQ=    [*  
   5 3L9W    Z1SETUPB
  11 3LPG    [   CURRENT ACTIVITY IS BLANKET 
   4 3M96    [   
  18 3MNQ          LDX   3  ADMULTACT           [J. IF NO MOP ACTIVITIES 
   8 3N8B          BZE   3  NOMOP
  21 3NN2          LDN   2  BMOPLEN+IMOPQLEN    [OTHERWISE GET AMOP/IMOPQ ACTIVITY   
  21 3P7L          LDN   6  AMOP+IMOPQ          [TO QUEUE MOP ACTIVITIES' CORE REQUE 
  13 3PM=          CALL  4  SCORAG4             [STS.
  17 3Q6W          LINKSET  2,SLINK3(1)         [LINK TO K3MOPSTART  
   5 3QLG    NWMOP   
  18 3R66          STO   2  IMOPADDR(3)         [STORE ADDRESS OF QUEUING
  18 3RKQ          LDX   3  ADMP(3)             [ACTIVITY IN EACH AMOP.  
   8 3S5B          BNZ   3  NWMOP
  18 3SK2          FWAIT    #54                 [WAIT FOR END OF E.M.S.  
   5 3T4L    NOMOP   
  10 3WHG    # SYSTEM.JOURNAL ACTIVITY   
  14 3X36          LDN   2  BSALEN+JOUROPENLEN  [LENGTH  
  13 3XGQ          LDN   6  BSA+JOUROPEN        [TYPE
  16 3Y2B          CALL  4  SCORAG4             [FROM TOP OF CORE
  16 3YG2          LINKSET  2,SLINK6(1)         [LINK TO K1SJLOOP
   8 3Y_L          LDCT  0  #400 
   7 3_F= ...      ADS   0  2
  16 3_YW          STO   2  JSJPTR              [PTR TO ACTIVITY 
   6 42DG    XFSHNJRNL   
   8 43CQ    # LISTFILE ACTIVITY 
  14 43XB          LDN   2  ACTLFLEN+BSCHEDLEN  [LENGTH  
  13 44C2          LDN   6  ACTLF+BSCHED        [TYPE
   9 44WL          CALL  4  SCORAG4  
  17 46*G          LINKSET  2,SLINK7(1)         [LINK TO K1LISTPROC  
   6 46T6    XFSHNBLKSET 
   8 4==G          LDCT  0  #400 
   7 4=Q6          ADX   0  2
  16 4?9Q          STO   0  BOUTACT             [PTR TO ACTIVITY 
  17 4#NL          FWAIT    #54                 [WAIT FOR END OF EMS 
   6 4*8=    XFSHNBSCHED 
   7 4C6Q    # MT ACTIVITY   
  12 4CLB          LDN   2  ACTDOCLEN+ASCHEDELEN 
  13 4D62          LDN   6  ACTDOC+ASCHEDE      [TYPE
   9 4DKL          CALL  4  SCORAG4  
  17 4F5=          LINKSET  2,SLINK8(1)         [LINK TO K1SCHEDMT   
   9 4FJW          STO   2  BRETACT  
   6 4G4G    #UNS FIRSTR 
   4 4GJ6    (   
  17 4H3Q          LDX   6  2                   [LONGSET CORRUPTS X2 
  17 4HHB          LONGSET #170,XMTGER,,2       [LONGSET DTS ACTIVITY
  14 4J32          LDX   2  6                   [RESET X2
  21 4JGL          FLONGWAIT #170               [PUT DTSA ON QUEUE WITH WAITING STYLE
   4 4K2=    )   
   6 4KFW    XFSHNBDCHED 
  19 4LF6          LDN   2  ASWINTLEN           [SET UP ACTIVITY TO CONTROL  
  18 4LYQ          LDN   6  ASWINT              [OBJECT PROGRAM SWAPPING.
   9 4MDB          CALL  4  SCORAG4  
  19 4MY2          STO   2  BSWINT              [POINTER TO SWAP ACTIVITY.   
   7 4QTQ    #UNS  CAFSINT   
   4 4R*B    (   
  18 4RT2    #     FOR THE CAFS INTERFACE, WE SET UP A BSA/CAFS ACTIVITY 
  18 4S#L    #     AT THIS POINT IN THE PROCEEDINGS.   THIS IS DONE IN A 
   8 4SS=    #     'CAFS' SEGMENT
   4 4T?W    #   
  10 4TRG          DOWN     CAFSCTRL,1   
   4 4W?6    )   
   4 575M ...[   
  10 576Y ...[     ERROR LOGGING ACTIVITY
   4 5789 ...[   
  10 578T ...      JENVNOT  NOELOGACT,DME
  11 579G ...      LDN   2  BSALEN+ELOGLEN   
   9 57=R ...      LDN   6  BSA+ELOG 
  19 57#4 ...      CALL  4  SCORAG4            [SET UP ERROR LOGGING ACTIVITY
  10 57** ...      LINKSET  2,SLINK10(1) 
   8 57BL ...      LDCT  0  #400 
   7 57CX ...      ADX   0  2
  17 57F8 ...      STO   0  BELOGACT             [ST PTR. TO BASE PTR
   8 57GF ...      FWAIT    #54  
   6 5825 ...NOELOGACT   
  21 58H=    #                       THE EMSCORE BELOW MUST BE THE LAST ONE ISSUED.  
  21 58H_ ...      LDX   2  GLENFCHAP           [GET CORE FOR FIXED CHAPTER SPACE.   
   7 58JN ...      LDN   0  1
   9 58KC ...      CALL  4  SCOREG4F 
   7 58L6 ...      FINDCORE 2
  18 58LT ...      NAME     2,BSTB,CHAPTER      [SET NORMAL CHAPTER TYPE 
   7 58MJ ...      LDN   0  1
   9 58N? ...      STO   0  BACK1(2) 
   9 58P2 ...      STOZ     BACK2(2) 
   8 58PP ...      STOZ     A1(2)
  20 58QD ...      STO   0  ALOGLEN(2)          [INITIALISE AT 1 FOR P1M CHECKSUM
  16 58R7 ...      STO   2  GFIXCHAP            [PTR. TO FIXCHAP 
   7 58RW ...#SKI  CAMK7>5-5 
   4 58SK ...(   
  21 58T# ...      STO   2  CTOP                [ INITIALISE LONGLOCK AREA BOUNDARY  
  20 58W3 ...      LDN   1  #210                [ MARK GFIXCHAP A CHAPTER BLOCK  
  14 58WQ ...      STO   1  AFLAG(2)            [   AND  
   4 58XF ...)   
  18 58Y8 ...      CHAIN    2,BCHAP+1           [CHAIN INTO CHAPTER CHAIN
   7 58YX ...#SKI  CAMK7<6-6 
   4 58_L ...(   
  17 592W          LDX   2  AFREE               [TOTAL MOVEABLE CORE.
  20 59GG          LDX   5  2                   [SAVE CURRENT CONTENTS OF AFREE. 
   5 5=26    #SKI  G4
  17 5=FQ          SRA   2  5                   [1/32ND PART THEREOF 
   5 5=_B    #SKI  G3
  20 5?F2          SRA   2  4                   [1/16TH.PART THEREOF,ROUNDED UP. 
  15 5?YL          ADN   2  1024*3/2            [ADD 1.5K.   
  19 5#D=          CALL  4  SCORE               [SPLIT OFF 'LONGLOC' AREA.   
  19 5#XW          STO   5  AFREE               [RESET TOTAL MOVEABLE CORE   
   7 5*CG          FINDCORE 3
  20 5*X6          STO   3  CTOP                [STORE ADDRESS OF LONGLOCK AREA  
   7 5BBQ          FREECORE 3
   4 5BWB    )   
   8 5CB2    #SKI  CAMK7>5-5_G4  
  21 5CTL          FON      HLLOCKAREA          [WAKE ACTS WAITING FOR LONGLOCK AREA 
  17 5F#G          DOWN     BACKMAP,1           [RESTORE IF NECESSARY
   6 5FS6    XFSHNBBACK  
  10 5G#5 ...      JENVNOT  XNOTEXI,CME  
   9 5G#D ...      DOWN    CMEMILL,2 
  16 5G#R ...      LDN   7  0                   [LD. MOD DOWN BLK
  21 5G*6 ...      OUTBLOCN 13                    [ONE WD EACH PARAM + ONE FOR EACH P
   5 5G*F ...XNEXMES 
  10 5G*S ...      MHUNTW   2,ADATA,EMS  
   9 5GB7 ...      BNG   2  XNOTEXI  
  10 5GBG ...      BXGE  7  A1(2),XENDMES
   7 5GBT ...SL1   SMO      7
  15 5GC8 ...      LDN   3  A1+1(2)             [DEVICE PTR  
   7 5GCH ...      LDN   4  1
   9 5GCW ...      LDN   5  JPDNUMA  
  20 5GD9 ...      CALL  6  PARAMETER           [CONVERT PARAMETER FOR MESSAGE   
   8 5GDJ ...      BRN      SL1  
   7 5GDX ...SL2   SMO      7
  16 5GF= ...      LDN   3  A1+2(2)             [RIGHT OR WRONG  
   7 5GFK ...      LDN   4  2
  10 5GFY ...      LDN   5  JPDVARCHAR   
   9 5GG? ...      CALL  6  PARAMETER
  20 5GGL ...      BRN      SL2                 [CONVERT PARAMETER FOR MESSAGE   
   7 5GG_ ...SL3   SMO      7
  15 5GH# ...      LDN   3  A1+4(2)             [IN OR OUT   
   7 5GHM ...      LDN   4  1
  20 5GJ2 ...      CALL  6  PARAMETER           [CONVERT PARAMETER FOR MESSAGE   
   8 5GJ* ...      BRN      SL3  
   7 5GJN ...SL4   SMO      7
  16 5GK3 ...      LDN   3  A1+5(2)             [DIS OR SPACES   
   9 5GKB ...      CALL  6  PARAMETER
   8 5GKP ...      BRN      SL4  
   7 5GL4 ...SL5   SMO      7
  15 5GLC ...      LDX   5  A1+6(2)             [LD, ADDRESS 
  16 5GLQ ...      GEOPACK  2,5,NJS             [GEOPACK GEOG NO.
   7 5GM5 ...      LDN   3  2
   7 5GMD ...      LDN   4  1
   9 5GMR ...      LDN   5  JPDGEOPER
   9 5GN6 ...      CALL  6  PARAMETER
   8 5GNF ...      BRN      SL5  
   7 5GNG ...SL6   SMO      7
  17 5GNH ...      LDN   3  A1+7(2)             [LD. TYPE OF DEVICE  
   7 5GNJ ...      LDN   4  1
  10 5GNK ...      LDN   5  JPDPERITYPE  
   9 5GNL ...      CALL  6  PARAMETER
   8 5GNM ...      BRN      SL6  
  19 5GNS ...      MONOUT   EMSJOURN            [OUTPUT MESSAGE TO JOURNAL   
   7 5GP7 ...      ADN   7  7
   9 5GPG ...      BRN      XNEXMES  
   5 5GPT ...XENDMES 
  16 5GQ8 ...[   ALL MESSAGES OUTPUT SO FREE OFF THE DATA BLOCK  
   8 5GQH ...      FREECORE  2   
   7 5GQJ ...#UNS  ASHARE1   
   4 5GQK ...(   
  19 5GQL ...[  NOW OUTPUT INFORMATORY MESSAGES FOR CME SHARED D.A. DEVICES  
   8 5GQM ...      LDN   5  26   
  12 5GQN ...      FINDPEREC 3,APFOTY,5,XFINDSH  
   9 5GQP ...      BNG   3  XNOTEXI  
  18 5GQQ ...      JNCMESH  3,XFINDSH               [_ IF NOT CME SHARED 
   8 5GQR ...      LGEOG    3,4  
  18 5GQS ...      OUTPACK  4,1,GEOPER              [OUTPACK GEOG UNIT NO
  19 5GQT ...      MONOUT   CMESH                   [UNIT NN IS CME SHARED   
   9 5GQW ...      BRN      XFINDSH  
   4 5GQX ...)   
   5 5GQY ...XNOTEXI 
   9 5YDW          ACROSS  SETUP,7   
   5 5_XQ    XMTGER  
   9 62CB          GEOERR   1,MTSETUP
   5 62X2    XFSHMT  
   4 63W=    #END
   6 ____ ...165116450003
