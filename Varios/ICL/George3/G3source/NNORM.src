  15 22FL          SEG      NNORM,8,MIKE COLLINS,FILESTORE   
  21 22_=          SEGENTRY K1NNORM,ZFULL   [FULL NAME FROM FABSNB TO BE TRANSFERRED 
  21 23DW          SEGENTRY K2NNORM,ZTWO    [TWO NAMES FROM FABSNB TO BE TRANSFERRED 
  21 23YG          SEGENTRY K3NNORM,ZLOC    [LOCAL NAME ONLY FROM FABSNB OR FLOCNB   
  16 24D6                                   [TO BE TRANSFERRED   
  19 24XQ    [THE SEGMENT CAN BE CONVENIENTLY SPLIT INTO TWO PARTS. THE FIRST
  20 25CB    [CONVERTS THE FABSNB BLOCK,IF NECESSARY,INTO ONE CONTAINING ONLY ONE
  21 25X2    [USER NAME. THE SECOND,BEGINNING AT ZREADY,TRANSFERS THE REQUIRED DATA  
  10 26BL    [TO THE ADATA CREADL BLOCK. 
   9 26W=    TABS           #44400000
   9 27*W    TLOC           #44430000
   8 27TG    SIX            +6   
   9 28*6    TECNO          +10000   
  15 28SQ    TEN            +10         [*** SUBROUTINES *** 
   8 294J ...SEVENPT3       0:7.3
  20 29#B    PACKCREADL     [PACKS REQUIRED CHARS. INTO ADATA,CREADL BLOCK AND   
  15 29S2                   [LENGTHENS BLOCK IF NECESSARY.   
  20 2=?L                   [ENTRY CONDS: X5-LINK X6-NO. OF CHARS. AWORK1,=0-NAME
  21 2=R=                   [ENTRY,=CHARS.-CHAR.ENTRY. AWORK3-BLOCK SUBTYPE AWORK4-  
  18 2?=W                   [NAME ADDRESS X3=FABSNB/FLOCNB ADD. X2=FX2   
   8 2?QG          SBX   5  FX1  
  11 2#=6          MHUNTW   1,ADATA,CREADL   
  10 2#PQ          LDX   7  ALOGLEN(1)   
   9 2*9B          SBN   7  CPDATA-A1
  20 2*P2          SLL   7  2               [CHAR. SPACE AVAIL. IN CREADL BLOCK  
   8 2B8L          SBX   7  A1(1)
   7 2BN=          SBX   7  6
  16 2C7W          BPZ   7  ZROOM           [J IF ENOUGH SPACE   
  16 2CMG          SBS   3  AWORK4(2)       [REL. ELEMENT ADD.   
   7 2D76          LDX   3  1
  10 2DLQ          LDX   7  ALOGLEN(3)   
  14 2F6B          ADN   7  8               [NEW LENGTH  
  15 2FL2          ALTLENG  3,7,ZALTL       [CORRECT LENGTH  
  11 2G5L          MHUNTW   1,ADATA,CREADL   
   9 2GK=          BRN      ZFINDBL  
   5 2H4W    ZALTL   
  11 2HJG          MHUNTW   2,ADATA,CREADL   
   7 2J46          EXIT  1  0
   5 2JHQ    ZFINDBL 
  10 2K3B          MFIND    3,AWORK3(2)  
  16 2KH2          ADS   3  AWORK4(2)       [RESTORE ELEMENT ADD.
   5 2L2L    ZROOM   
   9 2LG=          LDX   7  AWORK1(2)
  16 2L_W          BNZ   7  ZWORD           [J IF CHAR. TRANSFER 
  14 2MFG          LDX   7  AWORK4(2)       [NAME ADD.   
   8 2M_6          BRN      ZLDCH
   5 2NDQ    ZWORD   
  14 2NYB          LDN   7  AWORK1(2)       [CHAR. ADD   
   5 2PD2    ZLDCH   
  13 2PXL          LDX   0  A1(1)           [CREADL  
   7 2QC=          SRC   0  2
  19 2QWW          ADN   0  CPDATA(1)       [POINT TO FIRST FREE CHAR  LOCN. 
  16 2RBG          ADS   6  A1(1)           [UPDATE CHAR. COUNT  
  15 2RW6          LDX   1  6               [NO. OF CHARS.   
  14 2S*Q          MVCH  7  0(1)            [MOVE CHARS. 
   8 2STB          ADX   5  FX1  
   7 2T*2          EXIT  5  0
  19 2TSL    PACKNO         [CONVERTS NUMBER FROM A BINARY TO A DECIMAL FORM 
  17 2W#=                   [SUITABLE FOR THE SUBROUTINE PACKCREADL  
  18 2WRW                   [ENTRY CONDS: X7 CONTAINS THE BINARY NUMBER  
   7 2X?G          LDN   0  0
   8 2XR6          SMO      FX1  
  19 2Y=Q          DVR   7  TECNO           [CONVERT NO. TO BINARY FRACTION  
  13 2YQB          LDCT  1  #200            [ROUND UP
  17 2_=2          LDN   2  7               [ADD. OF CONVERTED NO.   
   7 2_PL          MODE  1   
  13 329=    ZNOS  CBD   0  0(2)            [CONVERT 
  16 32NW          BCHX  2  /               [INCREMENT CHAR. ADD.
   8 32X6 ...      SMO      FX1  
  20 335B ...      BXU   2  SEVENPT3,ZNOS      [J IF 3 CHARS. NOT YET CONVERTED  
   7 33?L ...      MODE  0   
   8 33FW ...      CBD   0  0(2) 
   8 33N6          LDX   2  FX2  
  13 347Q          LDX   6  ACES            [SPACES  
  13 34MB          LDN   1  4               [COUNT   
   5 3572    ZMONOS  
   7 35LL          SLC   67 6
  20 366=          BXU   6  ACES,ZMOVNO     [J IF CHAR. SHIFTED IS NOT A SPACE   
   9 36KW          BCT   1  ZMONOS   
   7 375G          EXIT  5  1
   5 37K6    ZMOVNO  
   7 384Q          SRC   67 6
  15 38JB          LDX   6  1               [NO. OF CHARS.   
  15 3942          STO   7  AWORK1(2)       [CONVERTED NO.   
   7 39HL          EXIT  5  0
  21 3=3=    PACKLANG                       [COUNTS THE NO. OF CHARS. IN A LANGUAGE  
  19 3=GW                                   [CODE AS REQUIRED BY PACKCREADL  
  20 3?2G                                   [ENTRY CONDS: X7 CONTAINS LANG. CODE 
  14 3?G6          STO   7  AWORK1(2)       [STORE CODE  
  13 3?_Q          LDN   6  4               [COUNT   
  13 3#FB          LDX   0  ACES            [SPACES  
   5 3#_2    ZMOLA   
   7 3*DL          SRL   70 6
  20 3*Y=          BXU   0  ACES,ZOUT       [J IF CHAR. SHIFTED IS NOT A SPACE   
   8 3BCW          BCT   6  ZMOLA
   7 3BXG    ZOUT  EXIT  5  0
  20 3CC6    PACKNAME       [PACKS NAME(S) AND DETAILS FROM FABSNB/FLOCNB INTO   
  10 3CWQ                   [CREADL BLOCK
  21 3DBB                   [ENTRY CONDS: X1-FIRST NAME-ELEMENT X4-LOCAL/USER PARAM  
  17 3DW2                   [X5-LINK  X2-FX2  X3-FABSNB/FLOCNB ADD.  
   8 3F*L          SBX   5  FX1  
  12 3FT=          STO   5  AWORK2(2)       [LINK
   5 3G#W    ZAGAIN  
  18 3GSG          STO   1  AWORK4(2)       [STORE ADD. OF FIRST ELEMENT 
  13 3H#6          LDN   7  #20             [SPACE   
  14 3HRQ          LDN   6  12              [CHAR. COUNT 
  14 3J?B    ZLOOP                          [COUNT CHARS.
   7 3JR2          SLC   1  2
   7 3K=L          SBN   1  1
   7 3KQ=          SRC   1  2
   8 3L9W          LDCH  0  3(1) 
  10 3LPG          BXU   0  7,ZMOVCHAS   
   8 3M96          BCT   6  ZLOOP
   9 3MNQ          BRN      ZGEOERR  
   5 3N8B    ZMOVCHAS
  19 3NN2          STOZ     AWORK1(2)       [INDICATES NAME, X6=NO. OF CHARS.
  20 3P7L          CALL  5  PACKCREADL      [TRANSFER NAME, AWORK4=ADD. OF NAME  
  16 3PM=          BPZ   4  ZMORE           [J IF LOCAL NAME(S)  
   9 3Q6W          LDX   5  AWORK2(2)
   8 3QLG          ADX   5  FX1  
   7 3R66          EXIT  5  0
   5 3RKQ    ZMORE   
   7 3S5B          LDN   6  1
  15 3SK2          BXE   4  6,ZLAST          [J. IF LAST LOC.
   8 3T4L          LDCT  0  #360 
   9 3TJ=          STO   0  AWORK1(2)
  10 3W3W          CALL  5  PACKCREADL   
   7 3WHG          BRN   ZFIN
   5 3X36    ZLAST   
  15 3XGQ          LDCT  0  #300            [LEFT PARENTHESIS
   9 3Y2B          STO   0  AWORK1(2)
  10 3YG2          CALL  5  PACKCREADL   
   9 3Y_L          LDX   1  AWORK4(2)
  13 3_F=          LDX   7  4(1)            [GEN. NO.
   9 3_YW          BZE   7  ZNOGEN   
  14 42DG          CALL  5  PACKNO          [CONVERT NO. 
  14 42Y6          CALL  5  PACKCREADL      [TRANSFER NO.
   9 43CQ ...      LDX   1  AWORK4(2)
   5 43XB ...ZNOGEN  
  12 45B=          LDCT  0  #370      [  SOLIDUS 
   9 45TW          STO   0  AWORK1(2)
   7 46*G          LDN   6  1
  10 46T6          CALL  5  PACKCREADL   
   9 47#Q          LDX   1  AWORK4(2)
  14 47FY ...      LDX   7  5(1)            [LANG. CODE  
  10 47M6 ...      BXE   7  ACES,ZNOLAN  
   9 47PQ ...      BZE   7  ZNOLAN   
   8 47SB          LDX   7  5(1) 
  14 48#2          CALL  5  PACKLANG        [CONVERT CODE
  15 48RL          CALL  5  PACKCREADL      [TRANSFER CODE   
   5 49?=    ZNOLAN  
   9 49QW          LDX   7  ATYPE(3) 
   8 4==G          ANDN  7  #2401
   9 4=Q6          BZE   7  ZNOMAG   
  13 4?9Q ...      LDEX  7  3(1)            [REEL NO.
   9 4BM6          CALL  5  PACKNO   
  10 4C6Q          CALL  5  PACKCREADL   
   5 4CLB    ZNOMAG  
  16 4D62          LDCT  0  #310            [RIGHT PARENTHESIS   
   9 4DKL          STO   0  AWORK1(2)
   7 4F5=          LDN   6  1
  10 4FJW          CALL  5  PACKCREADL   
  16 4G4G    ZFIN  LDX   1  AWORK4(2)       [START ADD. OF NAME  
   7 4GJ6          ADN   1  6
  17 4H3Q          BCT   4  ZAGAIN          [J IF MORE LOCAL NAMES   
   9 4HHB          LDX   5  AWORK2(2)
   8 4J32          ADX   5  FX1  
   7 4JGL          EXIT  5  0
  15 4K2=                       [*** END OF SUBROUTINES ***  
   7 4KFW    ZLOC  NGN   6  1
  14 4K_G          LDX   3  2               [LOC PARAM   
  17 4LF6    SEARCH                         [FIND FABSNB OR FLOCNB   
   9 4LYQ          LDX   3  FPTR(3)  
   9 4MDB          LDX   4  ATYPE(3) 
   9 4MY2          ANDX  4  HALFTOP  
  10 4NCL          BXE   4  TABS(1),ZFND 
  10 4NX=          BXE   4  TLOC(1),ZFND 
  10 4PBW          BXL   4  CACT,SEARCH  
   5 4PWG    ZGEOERR 
   9 4QB6          GEOERR   1,UNNORM 
   4 4QTQ    ZTWO
  14 4R*B          LDN   6  1               [TWO PARAM   
   8 4RT2          BRN      ZHUN 
   5 4S#L    ZFULL   
  14 4SS=          LDN   6  0               [FULL PARAM  
   4 4T?W    ZHUN
  10 4TRG          HUNT     3,FILE,FABSNB
  15 4W?6          BNG   3  ZGEOERR         [J IF NOT FOUND  
  13 4WQQ    ZFND  LDX   4  ATYPE(3)        [SUBTYPE 
  15 4X=B          STO   4  AWORK3(2)       [SUBTYPE STORED  
   9 4XQ2          ANDX  4  HALFTOP  
   9 4Y9L    #SKI    K6NNORM>599-599 
  10 4YP=          TRACEVER    6,NORMTYPE
  14 4_8W          BXU   4  TABS(1),ZFLOC   [J IF FLOCNB 
   9 4_NG    [FABSNB FORMAT CHECK ***
   9 5286          LDX   0  HDREC(3) 
   7 52MQ          SBN   0  4
  17 537B          BZE   0  ZREADY          [J IF NO CONVERSION REQD.
   9 53M2          BNG   0  ZGEOERR  
   9 546L          DVS   7  SIX(1)   
  15 54L=          BNZ   7  ZGEOERR         [EXIT IF REM # 0 
   9 555W ...[FABSNB CONVERSION **** 
   9 55KG ...      LDX   4  HDREC(3) 
  15 5656 ...      ADN   4  HDREC(3)        [END OF BLOCK +1 
  15 56JQ ...      LDN   2  A1+4-6(3)       [PSEUDO 1ST. NAME
   5 583L    ZCHECK  
  15 58C* ...      SBN   4  6               [ADD. NEXT NAME  
  16 596R ...      BXE   2  4,ZREADY        [J IF END OF BLOCK   
   7 59GG          SMO      4
  14 5=26          LDX   0  0               [NEXT NAME   
  16 5=FQ ...      BXU   0  HCOLUSER,ZCHECK [LOOP IF USER NAME   
   5 5=_B    ZMOVUE  
   7 5?L8 ...      ADN   4  1
  14 5?YL          LDN   5  A1+1(3)         [DESTINATION 
  14 5#D=          MOVE  4  3               [MOVE USER   
  15 5#XW          ADN   4  5               [FIRST LOCAL NAME
  14 5*CG          ADN   5  3               [DESTINATION 
   9 5*JN ...      LDX   2  HDREC(3) 
   9 5*PW ...      ADN   2  HDREC(3) 
  14 5*X6          SBX   2  4               [LOCAL NAMES 
  16 5BBQ          BZE   2  ZALTER          [J IF NO LOCAL NAMES 
  10 5BWB          LDX   7  ALOGLEN(3)   
  14 5CB2          SBX   7  HDREC(3)        [EXTRA DATA  
  16 5CTL          BZE   7  ZMOVLOC         [J IF NO EXTRA DATA  
   9 5D*=          BNG   7  ZGEOERR  
  18 5DSW          BXGE  7  JTHREE,ZGEOERR  [EXIT IF EXTRA DATA >2 WDS.  
   7 5F#G          ADX   2  7
   5 5FS6    ZMOVLOC 
  16 5G?Q          MOVE  4  0(2)            [MOVE ALL LOCAL DATA 
  17 5GRB    ZALTER                         [CORRECT LENGTH OF FABSNB
   7 5H?2          SBX   4  5
   9 5HQL          SBS   4  HDREC(3) 
   8 5J==          LDN   7  4(2) 
   8 5JPW          ALTLENG  3,7  
  10 5K9G          HUNT     3,FILE,FABSNB
  19 5KP6    [DATA IS NOW IN A STATE TO BE TRANSFERRED TO CREADL BLOCK ****  
   5 5L8Q    ZREADY  
   8 5LNB          LDX   1  FX1  
   8 5M82          LDX   2  FX2  
  13 5MML          BNG   6  ZLOCFAB         [J IF LOC
  14 5N7=          BZE   6  ZALL            [J IF FULL   
  14 5NLW          LDX   7  HDREC(3)        [MUST BE TWO 
  16 5P6G          BXL   7  TEN(1),ZGEOERR  [EXIT IF LENGTH # 10 
  10 5PL6          BXE   7  TEN(1),ZALL  
   9 5Q5Q          LDN   1  HDREC(3) 
   9 5QKB          ADX   1  HDREC(3) 
   8 5R52          SBN   1  12   
   7 5RJL          LDN   4  2
   8 5S4=          BRN      ZLAM 
   4 5SHW    ZALL
  13 5T3G          LDCT  0  #120            [COLON   
   9 5TH6          STO   0  AWORK1(2)
   7 5W2Q          LDN   6  1
  15 5WGB          CALL  5  PACKCREADL      [TRANSFER CHAR.  
   7 5X22          NGN   4  1
   9 5XFL          LDN   1  A1+1(3)  
  15 5X_=          CALL  5  PACKNAME        [PACK USER NAME  
   9 5YDW          LDX   5  HDREC(3) 
   7 5YYG          SBN   5  4
   8 5_D6          BZE   5  ZUP  
   8 5_XQ          SMO      FX1  
   8 62CB          DVS   4  SIX  
  16 62X2          LDX   4  5               [NO. OF LOCAL NAMES  
   8 63BL          LDCT  0   #360
  10 63W=          STO   0   AWORK1(2)   
   8 64*W          LDN   6   1   
  10 64TG          CALL  5  PACKCREADL   
  15 65*6          LDN   1  A1+4(3)         [FIRST ELEMENT   
   9 65SQ    ZLAM  CALL  5  PACKNAME 
   5 66#B    ZUP   UP
  20 66S2    ZLOCFAB                        [LAST LOCAL NAME OF FABSNB REQUIRED  
   9 67?L          LDX   1  HDREC(3) 
   7 67R=          SBN   1  4
   9 68=W          BZE   1  ZGEOERR  
  10 68QG          ADN   1  HDREC-2(3)   
   8 69=6          BRN      ZFLAB
   5 69PQ    ZFLOC   
  19 6=9B          LDX   1  ALOGLEN(3)      [EXIT IF LENGTH OF FLOCNB IS NOT 
  15 6=P2          BXE   1  EIGHT,ZNAM      [EQUAL TO 6 OR 8 
   8 6?8L          SMO      FX1  
  10 6?N=          BXU   1  SIX,ZGEOERR  
   4 6#7W    ZNAM
  15 6#MG          LDN   1  A1(3)           [FIRST ELEMENT   
   5 6*76    ZFLAB   
  13 6*LQ          LDN   4  1               [ONE NAME
  14 6B6B          BRN      ZLAM            [PACK NAME   
  10 6BL2          MENDAREA 30,K99NNORM  
   4 6C5L    #END
   2 6CK=
   6 ____ ...750377000012
