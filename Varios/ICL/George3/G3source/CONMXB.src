   5 2278 ...#SKI  G3
   4 22#B ...(   
  15 22FL    #SEG  CONMXB                       [JOHN BAILEY.
   7 22S3 ...#OPT  K0CONMXB=0
  14 236D ...#LIS  K0CONMXB>K0CONMX>K0MULTIPLX>K0ALLGEO  
   9 23DW                8HCONMXB80  
   4 23YG    #   
  19 24D6    #     THIS SEGMENT IS ENTERED FROM CONMXC60 AND SERVICES PERI   
  20 24XQ    #     INSTRUCTIONS ISSUED BY AN OBJECT PROGRAM TO LINE ZERO OR NON  
  21 25CB    #     ATTACHED LINES OF A CONCEPTUAL MPX OR UPX AS PERIS TO INOPERABLE  
   6 25X2    #     LINES 
   4 26BL    #   
   4 26W=    #   
  11 27*W          SEGENTRY K1CONMXB,X1CONMXB
   4 27TG    #   
   4 28*6    #   
   9 28SQ    RMASK          #60077777
  10 29#B    RECCON         +CONRECLEN   
  20 29S2    PJYNOCA        +JYNOCA             [CONTROL AREA OUT OF RESERVATIONS
  18 2=?L    PJYRESVIOL     +JYRESVIOL          [RESERVATION VIOLATION   
  16 2=R=    PJYCOUNT       +JYCOUNT            [COUNT OUT RANGE 
  16 2?=W    PJYMODE        +JYMODE             [MODE IN ERROR   
  20 2?QG    PAIR1 LINK     CONMXC,1            [ENTRY POINT FOR ILLEGAL ROUTINES
   4 2#=6    #   
  10 2#PQ    SUFR  STO   0  ACOMMUNE9(2) 
  10 2*9B          LDX   2  APETADDR(2)  
  10 2*P2          FADDRESS 2,1,HLOCK2   
   8 2B8L          LDX   2  FX2  
  10 2BN=          LDX   0  ACOMMUNE9(2) 
   7 2C7W          EXIT  0  0
   4 2CMG    #   
  21 2D76    #  SUBROUTINE NAFLC FINDS THE CORRESPONDING FLAGCOUNT IN OLPA,PUTS IT   
  17 2DLQ    #  IN X4 AND IN X3 IS PUT THE CHARACTER/WORD MODIFIER   
   4 2F6B    #   
   4 2FL2    #   
  17 2G5L    NAFLC LDX   3  AWORK3(2)           [CONCEPTUAL LINE NO. 
   7 2GK=          SRC   3  2
   7 2H4W          SMO      2
   9 2HJG          LDCH  4  FLAGCT(3)
   7 2J46          EXIT  0  0
   4 2JHQ    #   
   4 2K3B    #   
  20 2KH2    #  SUBROUTINE NFLWR CREATES THE PATTERN FOR SETTING/UNSETTING THE   
  17 2L2L    #  FLAG IN FLAG AREA IN X4 AND THE WORD MODIFIER IN X3  
   4 2LG=    #   
  14 2L_W    NFLWR LDX   3  AWORK3(2)           [LINE NO.
   8 2MFG          BNZ   3  REGO 
   8 2M_6          LDCT  4  #200 
  15 2NDQ          LDX   5  CMAXLINE(2)         [UPX OR MPX  
  17 2NYB          BZE   5  (0)                 [UPX - NON ATTACHED  
   8 2PD2          LDX   4  GSIGN
   7 2PXL          EXIT  0  0
   7 2QC=    REGO  SRC   3  4
   7 2QWW          LDCH  5  3
   9 2RBG          ANDX  3  HALFTOP  
   7 2RW6          SLC   3  4
   8 2S*Q          LDCT  4  #400 
  17 2STB          SRL   4  0(3)                [CREATE THE PATTERN  
  18 2T*2          LDX   3  5                   [LOAD THE WORD MODIFIER  
   7 2TSL          EXIT  0  0
   4 2W#=    #   
   4 2WRW    #   
   4 2X?G    #   
  21 2XR6    # SUBROUTINE SETS FLAGS.THE NUMBER OF FLAGS,BY WHICH IS THE FLAGCOUNT   
  12 2Y=Q    # TO BE INCREASED,IS EXPECTED IN X5 
   9 2YQB    SUB3  STO   0  AWORK1(2)
   8 2_=2          CALL  0  NAFLC
   7 2_PL          ADX   4  5
   7 329=          SMO      2
   9 32NW          DCH   4  FLAGCT(3)
   7 338G          SBX   4  5
   8 33N6          BZE   4  MFNOS
   9 347Q    ZORKA LDX   0  AWORK1(2)
   7 34MB          EXIT  0  0
   8 3572    MFNOS CALL  0  NFLWR
   9 35LL          LDX   1  CFLAG(2) 
   7 366=          ADX   1  3
   8 36KW          CALL  0  SUFR 
   8 375G          ORS   4  0(1) 
  18 37K6          LDX   4  CMEMBER(2)          [PROGRAM MEMBER NUMBER   
  10 384Q          TICKLE   JOBNO(2),4   
   8 38JB          BRN      ZORKA
   4 3942    #   
  15 39HL    #   THIS SUBROUTINE CHECKS WHETHER 22AM OR 15 AM
  10 3=3=    RMODE LDX   7  APETADDR(2)  
   9 3=GW          LDX   2  EVENT1(2)
   8 3?2G          PMODES   2,7  
   8 3?G6          ANDN  2  #10  
   8 3?_Q          BNZ   2  REXMO
   8 3#FB          LDX   1  FX1  
   9 3#_2          ANDX  5  RMASK(1) 
   8 3*DL    REXMO LDX   2  FX2  
   7 3*Y=          EXIT  0  0
   4 3BCW    #   
  17 3BXG    #  SUBROUTINE CHECKS RESV RESV.VIOLATION AND CALCULATES 
  20 3CC6    #  GE0,ADDRESS OF REPLY WORD FO- NULL LINES AND LINE ZERO ROUTINE   
  14 3CWQ    SUB5  CHECKB   5,,RESV3,APETADDR(2),,RGER1  
   7 3DBB          EXIT  4  0
  10 3DW2    SUB6  ADX   1  APETADDR(2)  
   8 3F*L          FADDRESS 1,5  
   7 3FT=          EXIT  4  0
   4 3G#W    #   
   7 3GSG    SUB7  LDX   4  6
   7 3H#6          ANDN  4  2
   8 3HRQ          BZE   4  RDOP 
   8 3J?B          CALL  0  NAFLC
   8 3JR2          BZE   4  RDOP 
   7 3K=L          LDN   4  0
   7 3KQ=          SMO      2
   9 3L9W          DCH   4  FLAGCT(3)
   8 3LPG          CALL  0  NFLWR
   9 3M96          LDX   1  CFLAG(2) 
  19 3MNQ          ADX   1  3                   [CLEAR THE BIT IN FLAGWORD   
   8 3N8B          CALL  0  SUFR 
   8 3NN2          ERS   4  0(1) 
   7 3P7L    RDOP  EXIT  7  0
  21 3PM=    #  THIS SUBROUTINE MOVES NUMBER OF WORDS DEFINED IN X7 FROM AREA DEFINED
  20 3Q6W    #  IN X4(RELATIVE TO OBJECT PROG.) TO AREA DEFINED IN X5 (IN GEORGE)
  10 3QLG    SUB9  STO   0  ACOMMUNE9(2) 
  10 3R66          SMO      APETADDR(2)  
   9 3RKQ          LDN   1  HLOCK1   
   9 3S5B          FOUTMOVE 1,4,5,7  
  10 3SK2          LDX   0  ACOMMUNE9(2) 
   7 3T4L          EXIT  0  0
  20 3TJ=    #  THIS SUBROUTINE SETS THE AMXOR/CONAR BLOCK AND RINGS IT WITH THE 
  16 3W3W    #  CORRESPONDING ENTRANT IN THE AMXOR/CONLCB BLOCK  
   4 3WHG    #   
   8 3X36    SUB11 SBX   7  FX1  
  12 3XGQ          SETNCORE 9,3,AMXOR,CONAR,,1   
   8 3Y2B          ADN   3  A1   
   9 3YG2          STO   3  AWORK4(2)
  16 3Y_L          LDX   0  CMAXLINE(2)         [IS IT UPX OR MPX
  13 3_F=          BNZ   0  RM                  [MPX 
   7 3_YW          STOZ     2
  13 42DG          BRN      RX                  [UPX 
  15 42Y6    RM    LDX   2  EVENT3(2)           [CONC.LINE NO
   7 43CQ          SRL   2  6
  11 43XB    RX    MHUNTW   3,AMXOR,CONLCB   
   8 44C2          LDX   1  FX1  
  20 44WL          MPA   2  RECCON(1)           [CALCULATE THE ENTRANT ADDRESS   
   8 45B=          LDX   2  FX2  
  18 45TW          LDX   1  AWORK4(2)           [ADDRESS OF AMXOR/CONAR  
  10 46*G          RINGTWO  0(1),A1(3)   
   8 46T6          ADX   7  FX1  
   7 47#Q          EXIT  7  0
   4 47SB    #   
  20 48#2    SREWO SLC   6  1                   [SET EITHER B4 AND B5,OR B4 ONLY 
   8 48RL    RSEW  BNG   6  RMAJ 
   8 49?=          LDCT  4  #30  
   7 49QW          EXIT  0  0
   8 4==G    RMAJ  LDCT  4  #20  
   7 4=Q6          EXIT  0  0
   4 4?9Q    #   
   8 4?PB    OUTAC LDX   2  FX2  
  11 4#92          MACCS    ,APETADDR(2),3   
   8 4#NL          LDCT  0  #400 
   8 4*8=          STO   0  9(3) 
   8 4*MW          BRN      MOLFI
   8 4B7G    OUTRE LDX   2  FX2  
  11 4BM6          MACCS    ,APETADDR(2),3   
   8 4C6Q          STOZ     9(3) 
   5 4CLB    MOLFI   
   8 4D62    #SKI  K6CONMX>99-99 
  10 4DKL          TRACE    9(3),WOKEN UP
   8 4F5=          LDX   1  FX1  
   9 4FJW          OLPFIN1  PAIR1(1) 
   4 4G4G    #   
   4 4GJ6    #   
   8 4H3Q    RESV2 LDX   1  FX1  
  10 4HHB          LDX   5  PJYNOCA(1)   
   8 4J32          BRN      MCURD
   8 4JGL    RESV3 LDX   1  FX1  
  10 4K2=          LDX   5  PJYRESVIOL(1)
   8 4KFW          BRN      MCURD
   8 4K_G    MCOWR LDX   1  FX1  
  10 4LF6          LDX   5  PJYCOUNT(1)  
   8 4LYQ          BRN      MCURD
  10 4MDB    MODIL LDX   5  PJYMODE(1)   
   8 4MY2    MCURD LDX   2  FX2  
  11 4NCL          MACCS    ,APETADDR(2),3   
   8 4NX=          STOZ     9(3) 
   9 4PBW          TOPCA1   PAIR1(1) 
   6 4PWG          UNPLUG
   7 4QB6          ILLEGALX 5
   4 4QTQ    #   
  20 4R*B    #                                  [ENTRY FOR ALL SUBSEQUENT PERIS. 
   4 4RT2    #   
  21 4S#L    #           IN EVENT 2 IN OLPA IS THE ADDRESS OF CA RELATIVE TO OP DATUM
  15 4SS=    #           IN EVENT 3 IN OLPA IS THE LINE NO.  
  15 4T?W    #     IN APETADDR IN OLPA IS THE ADDRESS OF PCA 
   4 4TRG    #   
  10 4W?6    #     IN AWORK1 IS PERI MODE
  20 4WQQ    #     IN AWORK2 IS THE DISTANCE OF THE ENTRANT FROM THE BEGINNING OF
   9 4X=B    #     THE CONLCB BLOCK  
  13 4XQ2    #     IN EVENT3 IS CONCEPTUAL LINE NO   
   4 4Y9L    #   
   4 4YP=    #   
   4 4_8W    #   
   4 4_NG    #   
   5 5286    X1CONMXB
   4 52MQ    #   
  20 537B          MHUNTW   3,AMXOR,CONLCB      [DEFINE GEO.ADDR.OF THE ENTRANT  
  15 53M2          ADS   3  AWORK2(2)           [IN CONLCB   
   8 546L    #SKI  K6CONMX>99-99 
   4 54L=    (   
  10 555W          TRACE EVENT3(2),COLINO
  10 55KG          TRACE AWORK1(2),MODE  
  10 5656          TRACE AWORK2(2),ENADR 
   4 56JQ    )   
   4 574B    #   
  17 57J2    #  TYPE/MODE WORD IS CHECKED-IF MODE #2000 ONLY -B20 SET
  20 583L    #                           -IF MODE #2000 WITH OTHER MODES -B21 SET
  20 58H=    #                           -IF MODE #2000 TOGETHER WITH #10 MODE - 
  21 592W    #                            ERROR IN MODE DISPLAYED AND PERI REJECTED  
   4 59GG    #   
  15 5=26    VUBLI LDX   5  AWORK1(2)           [MODE WORD   
  20 5=FQ          LDX   4  5                   [CHECK IF BITS 14,15,16,17 SET   
   8 5=_B          ANDN  4  #1700
   8 5?F2          BNZ   4  MODIL
   8 5?YL          LDN   4  #2000
  20 5#D=          BXU   5  4,NEDVO             [IF MODE #2000 ONLY SET BIT 20   
   8 5#XW          ORN   6  #10  
   8 5*CG          BRN      NODUT
   7 5*X6    NEDVO ANDX  4  5
  19 5BBQ          BZE   4  NEKAM               [J IF NOT MODE #2000 AT ALL  
   8 5BWB          ANDN  5  #10  
  20 5CB2          BNZ   5  MODIL               [IF MODE #10 PRESENT,GO ILLEGAL  
  18 5CTL          ORN   6  4                   [IN OTHER CASE SET B21   
  17 5D*=    NODUT CALL  0  NAFLC               [FLAGCOUNT INTO X4   
  16 5DSW          BZE   4  MODIL               [MODIFIER INTO X3
   7 5F#G          SBN   4  1
  19 5FS6          SMO      2                   [UNSET IN FLAGCOUNT ONE FLAG 
   9 5G?Q          DCH   4  FLAGCT(3)
   8 5GRB          BZE   4  TYNA 
  10 5H?2          LDX   4  CMEMBER(2)   
   9 5HQL          TICKLE  JOBNO(2),4
   8 5J==          BRN      TONCA
  21 5JPW    TYNA                               [FLAG PATTERN INTO X4, MODIFIER INTO 
  13 5K9G          CALL  0  NFLWR               [X3  
  21 5KP6          LDX   1  CFLAG(2)            [RELATIVE ADDRESS OF F.A AND REL.ADDR
  20 5L8Q          ADX   1  3                   [OF THE CORRESPONDING WORD IN IT 
   8 5LNB          CALL  0  SUFR 
  15 5M82          ERS   4  0(1)                [SET THE FLAG
  19 5MML    TONCA LDX   4  6                   [IF MODE #2000 ONLY  ACCEPTED
  17 5N7=          ANDN  4  #10                 [AND WAKE THE PROGRAM
   8 5NLW          BZE   4  NEKAM
  18 5P6G          LDX   5  EVENT2(2)           [RELATIVE ADDRESS OF CA  
  19 5PL6          ADN   5  1                   [DEFINE REL.ADDRESS OF CA2   
   9 5Q5Q          LDN   1  HLOCK1   
  17 5QKB          CALL  4  SUB6                [CONVERT TO GEO ADDR 
   7 5R52          SMO      5
   7 5RJL          LDX   5  0
  15 5S4=          CALL  0  RMODE               [CHECK MODE  
  21 5SHW          CALL  4  SUB5                [CHECK WHETHER R.W.WITHIN RESERVATION
   8 5T3G          BRN      OUTAC
   4 5TH6    #   
  15 5W2Q    #  THIS SUBROUTINE ANALYSES  MODES LE OR EQ #63 
   4 5WGB    #   
   4 5X22    #   
   4 5XFL    #   
  19 5X_=    NEKAM LDX   3  AWORK2(2)           [ADDRESS OF CONLCB INTO X3   
  13 5YDW          LDX   4  AWORK1(2)           [MODE
   7 5YYG          ANDN  4  7
   7 5_D6          SBN   4  3
   8 5_XQ          BZE   4  RCHE 
   7 62CB          SBN   4  3
  18 62X2          BNZ   4  ROKO                [IF DOUBLE TRANSFER CHECK
  20 63BL    RCHE  LDN   5  7                   [WHETHER THE WHOLE(7 WORDS) IN   
  18 63W=          CHECKEXT EVENT2(2),5,RESV2,APETADDR(2),RGER1,HLOCK1   
  20 64*W          ORN   6  1                   [RESERVATIONS AND SET B23 IN X6  
   9 64TG    ROKO  LDX   4  AWORK1(2)
  17 65*6          ANDN  4  #77                 [MASK LAST SIX BITS  
  16 65SQ          LDN   5  #30                 [IF MODE LE #30 J
   9 66#B          BXL   4  5,MODNI  
   4 66S2    #   
  18 67?L          LDN   5  #40                 [IF MODE GE OR EQ #40 J  
   9 67R=          BXGE  4  5,MODVY  
  20 68=W          BRN      MODIL               [IN ANY OTHER CASE ERROR IN MODE 
   4 68QG    #   
   4 69=6    #   
   4 69PQ    #   
  17 6=9B    MODVY SRC   4  5                   [IF B19 SET J MOD60  
   8 6=P2          BNG   4  MOD60
   4 6?8L    #   
   7 6?N=          SLC   4  1
  17 6#7W          BNG   4  MOD50               [IF B20 SET J MOD50  
   4 6#MG    #   
   7 6*76          SLC   4  4
  15 6*LQ          ERN   4  #42                 [MODE #42 ?  
   8 6B6B          BNZ   4  MOD43
   8 6BL2          BRN      M2VPO
   4 6C5L    [   
  15 6CK=    MOD43 ERN   4  1                   [MODE #43?   
  17 6D4W          BNZ   4  MODIL               [IF NOT GO ILLEGAL   
   4 6DJG    [   
  20 6F46          ORN   6  1                   [SET B23 IN X6 -DOUBLE TRANSFER  
   8 6FHQ          BRN      MFPIS
   7 6G3B    MOD60 SLC   4  5
  15 6GH2          ERN   4  #62                 [MODE #62?   
   8 6H2L          BNZ   4  MOD66
   8 6HG=          BRN      MVP22
   4 6H_W    [   
  15 6JFG    MOD66 ERN   4  4                   [MODE #66?   
   8 6J_6          BNZ   4  MODIL
  20 6KDQ          ORN   6  1                   [SET B23 IN X6 -DOUBLE TRANSFR   
   4 6KYB    [   
   8 6LD2          BRN      M2VPO
   4 6LXL    [   
   7 6MC=    MOD50 SLC   4  4
  15 6MWW          ERN   4  #53                 [MODE #53?   
   8 6NBG          BNZ   4  MODIL
  21 6NW6          ORN   6  3                   [SET B23 IN X6-DOUBLE TRANSFER AND   
  17 6P*Q                                       [B22 IN X6 MODE#10   
   8 6PTB          BRN      MFPIS
  17 6Q*2    MODNI SRC   4  5                   [B19 SET,MOD #20 ?   
   8 6QSL          BPZ   4  MOMAL
  15 6R#=          SLC   4  2                   [B21 SET ?   
   8 6RRW          BNG   4  MOD26
   7 6S?G          SLC   4  3
  15 6SR6          ERN   4  #22                 [MODE #22 ?  
   8 6T=Q          BNZ   4  MOD23
   8 6TQB          BRN      MVP22
   4 6W=2    [   
  19 6WPL    MOD23 ERN   4  3                   [IF NOT MODE #21 MODE ILLEGAL
   8 6X9=          BNZ   4  MODIL
   8 6XNW          BRN      MFPIS
   4 6Y8G    [   
   7 6YN6    MOD26 SLC   4  3
  21 6_7Q          ERN   4  #26                 [IF MODE #26 SET BIT 23 IN X6-DOUBLE 
  20 6_MB          BNZ   4  MODIL               [TRANSFER,OTHERVISE MODE ILLEGAL 
   4 7272    [   
   7 72LL          ORN   6  1
   8 736=          BRN      M2VPO
  20 73KW    MOMAL SLC   4  1                   [IF BIT 20 IN X4 NOT SET J MODJE 
   8 745G          BPZ   4  MODJE
   4 74K6    [   
   7 754Q          SLC   4  4
  15 75JB          ERN   4  #11                 [MODE #11 ?  
   8 7642          BNZ   4  MOD13
  18 76HL          ORN   6  2                   [SET B22 IN X6 -MODE #10 
   8 773=          BRN      MFPIS
   4 77GW    [   
  21 782G    MOD13 ERN   4  2                   [IF ALSO NOT MODE #13,MODE ILLEGAL   
   8 78G6          BNZ   4  MODIL
  21 78_Q          ORN   6  3                   [SET B23 IN X6-DOUBLE TRANSFER AND   
   8 79FB          BRN      MFPIS
   4 79_2    [   
  18 7=DL    MODJE SLC   4  1                   [IF B21 NOT SET J M123   
   8 7=Y=          BPZ   4  M123 
   4 7?CW    [   
  14 7?XG          SLC   4  3                   [MODE#4 ?
   7 7#C6          ERN   4  4
   8 7#WQ          BNZ   4  MODIL
   8 7*BB          BRN      MFOUR
   4 7*W2    [   
  14 7B*L    M123  SLC   4  3                   [MODE #3 
   7 7BT=          SBN   4  2
   8 7C#W          BNG   4  MFPIS
   8 7CSG          BZE   4  M2VPO
   4 7D#6    [   
  20 7DRQ    MOD3  ORN   6  1                   [SET B23 IN X6 DOUBLE TRANSFER   
   8 7F?B          BRN      MFPIS
   8 7FR2    MOD1  BRN      MFPIS
   4 7G=L    #   
   4 7GQ=    #   
   4 7H9W    #   
   4 7HPG    #   
   4 7J96    #   
  20 7JNQ    # THIS ROUTINE SERVICES FOR LINE ZERO AND NULL LINES MODES #22,#62  
   4 7K8B    #   
  20 7KN2    MVP22 SLC   6  1                   [IF NON ATTACHED LINE J ELSE LINE
  15 7L7L          BNG   6  SNUL2               [ZERO.CHECK  
  21 7LM=          LDX   4  ADDMOP(3)           [THEN WHETHER IN ADDMOP ADDRESS OF   
  19 7M6W          BZE   4  OUTRE               [THE 1ST REPLY WORD OR ZERO  
  10 7MLG          LDX   4  ADDMOP+1(3)  
   8 7N66          BNZ   4  OUTRE
  21 7NKQ          STOZ     5                   [PUT ADDRESS OF 1ST REPLY WORD INTO  
  14 7P5B          ADN   3  1                   [ADDMOP+1
   8 7PK2          BRN      ZIVOC
   4 7Q4L    #   
  21 7QJ=    SNUL2 SRL   6  1                   [UNLESS PERI HAS BEEN SENT TO THIS   
  17 7R3W          LDX   1  BPTR(3)             [LINE REJECT ELSE J  
   8 7RHG          BZE   1  OUTRE
  20 7S36          LDX   4  2(1)                [TYPE/MODE WORD FROM AMXOR/CONAR 
  17 7SGQ          ANDN  4  #77                 [MASK LAST CHARACTER 
  16 7T2B          SBN   4  2                   [IS IT MODE #2   
  21 7TG2          BNZ   4  RABRA               [IF NOT J ELSE CHANGE THE MODE IN THE
  20 7T_L    RKUN  LDX   5  AWORK1(2)           [THE CA EITHER TO #66 IF THE NEXT
  21 7WF=          ANDN  5  #40                 [PERI MODE IS #62 OR TO #26 MODE IF  
  19 7WYW          BZE   5  RTELE               [THE NEXT PERI MODE IS #22   
   8 7XDG          LDN   4  #64  
   8 7XY6    RHUSA ADS   4  2(1) 
   8 7YCQ          BRN      ROSEL
   8 7YXB    RTELE LDN   4  #24  
   8 7_C2          BRN      RHUSA
  21 7_WL    RABRA SBN   4  #40                 [IF THE MODE IS NOT #42 REJECT,ELSE  
  21 82B=          BNZ   4  OUTRE               [CHANGE THE MODE IN CA IN ANY CASE   
  14 82TW          BRN      RTELE               [TO #66  
  16 83*G    ROSEL LDX   4  EVENT2(2)           [ADDRESS OF OP CA
  16 83T6          ADN   4  1                   [ADDRESS OF CA2  
  20 84#Q          LDN   7  3                   [NO OF WORDS TO BE TRANSFERRED TO
  20 84SB          LDN   5  6(1)                [AREA IN AMXOR/CONAR FROM OP CA  
  14 85#2          ADN   1  3                   [MODIFIER
   9 85RL          STO   1  AWORK4(2)
  17 86?=          CALL  0  SUB9                [MOVE DEFINED AREA   
   7 86QW          STOZ  6   
   8 87=G          BRN      RJEON
   4 87Q6    #   
  20 889Q    #  THIS ROUTINE SERVICES FOR LINE ZERO AND NULL LINES MODES #2,#42  
   6 88PB    #  #26 #66  
   4 8992    #   
   7 89NL    M2VPO SLC   6  1
  19 8=8=          BNG   6  RNUL1               [IF NON ATTACHED LINE J ELSE 
  21 8=MW          LDX   4  ADDMOP(3)           [FOR LINE ZERO THE CONTENT OF ADDMOP 
   8 8?7G          BNZ   4  OUTRE
   7 8?M6          STOZ     5
  18 8#6Q    ZIVOC ADX   5  EVENT2(2)           [RELATIVE ADDRESS OF 3A  
  20 8#LB          ADN   5  1                   [DEFINE REL'ADDR'OF CA2 OR CA5   
   9 8*62          LDN   1  HLOCK1   
  18 8*KL          CALL  4  SUB6                [GEO ADDR.OF CA2 OR CA5  
   7 8B5=          SMO      5
  20 8BJW          LDX   5  0                   [REL'ADDR'OF 1ST/2ND REPLY WORD  
  15 8C4G          CALL  0  RMODE               [CHECK MODE  
  16 8CJ6          CALL  4  SUB5                [CHECK RESV'VIOL 
  21 8D3Q          STO   5  ADDMOP(3)           [STORE REL ADDR'OF CA2 OR CA5 INT0   
  19 8DHB          LDN   1  HLOCK3              [ADDMOP OR ACTUAL IN CONLCB  
  17 8F32          CALL  4  SUB6                [CONVERT TO GEO.ADDR.
   7 8FGL          LDX   1  5
  20 8G2=          NGS   3  0(1)                [SET 1ST OR 2ND REPLY WORD NEG   
  20 8GFW          ANDN  6  2                   [CHECK WHETHER DOUBLE TRANSFER   
   8 8G_G          BZE   6  OUTAC
   5 8HF6    #SKI  G4
  12 8HYQ          CLEAR    APETADDR(2),0,HLOCK3 
   7 8JDB          LDN   5  3
   7 8JY2          ADN   3  1
   7 8KCL          STOZ     6
   8 8KX=          BRN      ZIVOC
   4 8LBW    #   
   7 8LWG    RNUL1 SRC   6  1
  18 8MB6          LDX   4  BPTR(3)             [NULL LINE - PERI SENT TO
  19 8MTQ          BNZ   4  OUTRE               [IT - IF YES REJECT THE PERI 
  20 8N*B          CALL  7  SUB11               [SUBROUTINE SETS AMXOR/CONAR AND 
  19 8NT2                                       [RINGS IT TO CONLCB ENTRANT  
  17 8P#L          LDX   4  EVENT2(2)           [ADDRESS OF THE OP CA
  17 8PS=          LDN   5  2(1)                [CA IN AMXOR/CONAR   
  20 8Q?W          LDX   7  6                   [CHECK WHETHER SINGLE OR DOUBLE  
  14 8QRG          ANDN  7  1                   [TRANSFER
   8 8R?6          BZE   7  SITRA
   7 8RQQ          LDN   7  3
  21 8S=B    SITRA ADN   7  4                   [MOVE THE CA FROM OP INTO AMXOR/CONAR
  14 8SQ2          CALL  0  SUB9                [BLOCK   
  21 8T9L    RJEON SMO      AWORK4(2)           [ADDRESS OF THE 1ST REPLY WORD FROM  
  18 8TP=          LDX   5  3                   [THE AMXOR/CONAR BLOCK   
  15 8W8W          CALL  0  RMODE               [CHECK MODE  
  17 8WNG          CALL  4  SUB5                [CHECK RESV.VIOLATION
   9 8X86          LDN   1  HLOCK3   
  21 8XMQ          CALL  4  SUB6                [CALCULATE GEO.ADDR OF 1ST/2ND R.W.  
   9 8Y7B          STO   5  AWORK3(2)
   9 8YM2          SMO      AWORK4(2)
  21 8_6L          LDX   4  4                   [CHECK NO OF CHARS TO BE TRANSFERRED 
  16 8_L=          BZE   4  MCOWR               [ERROR IF ZERO   
  18 925W          LDX   5  4                   [IF LESS OR EQ TO MAX OK 
   8 92KG ...      SBX   5  BIT11
   8 9356          BPZ   5  MCOWR
   9 93JQ          SMO      AWORK4(2)
  20 944B          LDX   5  5                   [CHECK THE BUFFER ADDRESS MODE   
   8 94J2          CALL  0  RMODE
   5 953L    #SKI  G3
   4 95H=    (   
  21 962W          LDX   7  5                   [CALCULATE THE BUFFER SIZE IN WORDS  
   7 96GG          SLC   7  2
  20 9726          ADN   4  3                   [CHECK WHETHER THE 1ST/2ND WORD  
  17 97FQ          ANDN  7  3                   [WHITHIN RESERVATION 
   7 97_B          ADX   4  7
   7 98F2          SRL   4  2
  14 98YL          CHECKA   5,4,RESV3,APETADDR(2),,RGER1 
   4 99D=    )   
   9 99XW          SMO      AWORK3(2)
   7 9=CG          NGS   3  0
   7 9=X6          ANDN  6  1
  20 9?BQ          BZE   6  OUTAC               [IF DOUBLE TRANSFER DO THE SAME  
   7 9?WB          LDN   4  3
   9 9#B2          ADS   4  AWORK4(2)
   7 9#TL          STOZ     6
   5 9**=    #SKI  G4
  12 9*SW          CLEAR    APETADDR(2),0,HLOCK3 
   8 9B#G          BRN      RJEON
   4 9BS6    #   
  13 9C?Q    [THIS ROUTINE SERVICES ALL WRITING MODES
  21 9CRB    MFPIS LDX   4  ADDMOP(3)           [EITHER THE ADDRESS OF THE 1ST REPLY 
  15 9D?2          BNZ   4  OUTRE               [WORD OR ZERO
  21 9DQL          LDX   4  BPTR(3)             [HAS BEEN SENT ALREADY A PERI TO THIS
  18 9F==          BNZ   4  OUTRE               [LINE? IF YES REJECT ELSE
   7 9FPW          ADN   3  1
  17 9G9G          STO   3  AWORK4(2)           [POINTER TO ADDMOP+1 
  21 9GP6          CALL  7  SUB7                [CALL SUB7 WHICH CHECKS THE PRESENCE 
  15 9H8Q                                       [OF MODE #10 
   9 9HNB          LDX   5  EVENT2(2)
   7 9J82          ADN   5  1
  21 9JCS ...MIN3                               [RETURN HERE TO SET 2ND REP WD INOP F
   9 9JML          LDN   1  HLOCK1   
   8 9K7=          CALL  4  SUB6 
   7 9KLW          SMO      5
   7 9L6G          LDX   5  0
   8 9LL6          CALL  0  RMODE
   8 9M5Q          CALL  4  SUB5 
   9 9MKB          LDN   1  HLOCK3   
   8 9N52          CALL  4  SUB6 
   7 9NJL          LDX   1  5
  16 9P4=          CALL  0  SREWO               [1ST REPLY WORD  
   8 9PHW          STO   4  0(1) 
  15 9Q3G          LDN   5  1                   [NO OF FLAGS 
  19 9QH6          CALL  0  SUB3                [CALL THE FLAGSETTING ROUTINE
  20 9R2Q          LDX   4  6                   [CHECK WHETHER DOUBLE TRANSFER   
   7 9RGB          ANDN  4  2
   8 9S22          BZE   4  OUTAC
  21 9SFL          LDN   1  4                   [CHECK ADDRESS MODE OF 2ND REP.WORD, 
  21 9S_= ...      BNG   6  ZIN3                [IF NULL LINE J ELSE LINE ZERO AND   
   7 9TDW          LDN   5  3
   7 9TYG          STOZ     6
   8 9WD6          LDX   2  FX2  
   9 9WXQ          LDX   3  AWORK4(2)
   8 9XCB          BRN      ZIVOC
  21 9XF5 ...ZIN3                              [2ND REP WD INOP FOR MODE3 INOP TO LET
  21 9XGS ...#SKI  G4                           [MOST OF THIS SEG WOULDNT WORK IN G4 
  15 9XJH ...      CLEAR    APETADDR(2),0,HLOCK3,APETTADR(2) 
   9 9XL= ...      LDX   5  EVENT2(2)
  16 9XM_ ...      ADN   5  4                  [POINT TO 2ND REP 
  17 9XPN ...      ERN   6  2                   [UNSET DOUBLE TRANS  
  17 9XRC ...      SRC   6  1                    [WAS SLC'ED IN SREWO
  19 9XT6 ...      BRN      MIN3                [GO TO DEAL WITH OTHER REP WD
  21 9XX2    RLILI CALL  7  SUB11               [SUBROUTINE SETS AMXOR/CONAR BLOCK   
  20 9YBL                                       [AND RINGS IT TO CONLCB ENTRANT  
   7 9YW=          LDN   0  2
   8 9_*W          STO   0  2(1) 
   9 9_TG          LDX   4  EVENT2(2)
  21 =2*6          ADN   4  4                   [CONTROL AREA OF READ PART MODE#3 PER
   7 =2SQ          LDN   7  3
   8 =3#B          LDN   5  3(1) 
   8 =3S2          CALL  0  SUB9 
   7 =4?L          STOZ     6
   8 =4R=          BRN      RJEON
   4 =5=W    #   
  19 =5QG    #     THIS ROUTINE SERVICES FOR LINE ZERO AND NULL LINES MODE#4 
   4 =6=6    #   
  19 =6PQ    MFOUR SLC   6  1                   [JUMP IF NULL LINE OTHERWISE 
   8 =79B          BNG   6  TNUL1
  20 =7P2          LDX   5  ADDMOP(3)           [ADDRESS OF THE FIRST REPLY WORD 
  14 =88L          BZE   5  OUTAC               [OR ZERO 
  19 =8N=          CALL  4  SUB5                [CHECK RESERVATION VIOLATION 
   9 =97W          LDN   1  HLOCK3   
  20 =9MG          CALL  4  SUB6                [CONVERT RW1 ADDR.INTO GEO ADDR  
   7 ==76          LDX   1  5
  18 ==LQ          CALL  0  RSEW                [SET BIT 4 AND B5 IN THE 
  17 =?6B          STO   4  0(1)                [1ST REPLY WORD IN OP
  18 =?L2          STOZ     ADDMOP(3)           [ZEROIZE ADDMOP IN CONLCB
  20 =#5L          LDX   5  ADDMOP+1(3)         [ADDRESS OF THE 2ND REPLY WORD   
  14 =#K=          BNZ   5  RDVTR               [OR ZERO 
  15 =*4W          LDN   5  1                   [NO OF FLAGS 
   8 =*JG          BRN      RJETR
   5 =B46    RDVTR   
  20 =BHQ          CALL  4  SUB5                [CHECK RESV.VIOL.OF THE 2ND R.W. 
   9 =C3B          LDN   1  HLOCK4   
   8 =CH2          CALL  4  SUB6 
  17 =D2L          LDX   1  5                   [SET B4 AND B5 IN THE
  16 =DG=          CALL  0  RSEW                [2ND REPLY WORD  
   8 =D_W          STO   4  0(1) 
  16 =FFG          STOZ     ADDMOP+1(3)         [ZEROIZE ADDMOP+1
  15 =F_6          LDN   5  2                   [NO OF FLAGS 
  19 =GDQ    RJETR CALL  0  SUB3                [CALL THE FLAGSETTING ROUTINE
   8 =GYB          BRN      OUTAC
   4 =HD2    #   
  19 =HXL    TNUL1 LDX   1  BPTR(3)             [IS THERE AMXOR/CONAR BLOCK  
  19 =JC=          BZE   1  OUTAC               [IF NOT DO NOTHING OTHERWISE 
   8 =JWW          STOZ     1(1) 
   9 =KBG          STO   1  AWORK1(2)
  18 =KW6          STOZ     BPTR(3)             [UNRING CONLCB ENTRANT   
   8 =L*Q    RJILM LDX   5  3(1) 
  20 =LTB          CALL  0  RMODE               [CHECK MODE AND RES.VIOLATION IN 
  17 =M*2          CALL  4  SUB5                [THE 1ST REPLY MODE  
   9 =MSL          LDN   1  HLOCK3   
   8 =N#=          CALL  4  SUB6 
  20 =NRW          LDX   1  5                   [SET B4 IN THE 1ST REPLY NORD NOT
   8 =P?G          CALL  0  RMAJ 
   8 =PR6          STO   4  0(1) 
   9 =Q=Q          SMO      AWORK1(2)
   7 =QQB          LDX   6  2
   8 =R=2          ANDN  6  #70  
   8 =RPL          BZE   6  RLIPA
   9 =S9=          SMO      AWORK1(2)
   7 =SNW          LDX   5  6
  20 =T8G          CALL  0  RMODE               [CHECK MODE AND RES VIOLATION OF 
  17 =TN6          CALL  4  SUB5                [THE 2ND REPLY WORD  
   9 =W7Q          LDN   1  HLOCK4   
  20 =WMB          CALL  4  SUB6                [SET B4 IN THE 2ND REPLY WORD NOT
  18 =X72          LDX   1  5                   [IMPLEMENTED,INOPERABLE  
   8 =XLL          CALL  0  RMAJ 
   8 =Y6=          STO   4  0(1) 
  18 =YKW          LDN   5  2                   [NO OF FLAGS TO BE SET   
   8 =_5G          BRN      ROLSE
  18 =_K6    RLIPA LDN   5  1                   [NO OF FLAGS TO BE SET   
  16 ?24Q    ROLSE LDX   1  AWORK1(2)           [FREE AMXOR/CONAR
   8 ?2JB          SBN   1  A1   
   7 ?342          FREECORE 1
   8 ?3HL          BRN      RJETR
   4 ?43=    #   
  10 ?4GW          MENDAREA 20,K100CONMXB
   4 ?52G    #END
   4 ?5G6 ...)   
   8 ____ ...42505671000600000000
