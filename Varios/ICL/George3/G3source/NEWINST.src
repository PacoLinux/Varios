   9 22FL    #LIS  K0NEWINST>K0ALLGEO
  16 22_=    #SEG  NEWINST82                      [OWEN N. JOHN  
   7 23DW          8HNEWINST 
  15 23NN ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23YG    [   
  11 24D6          SEGENTRY   K1NEWINST,START
   4 24XQ    [   
  21 25CB    [        THIS SEGMENT CHECKS FOR THE EXISTENCE OF :SYSTEM.PARAMINDEX(G4)
  20 25X2    [        IF IT EXISTS AND THE FOLLOWING CONDITIONS ARE NOT TRUE THEN
  10 26BL    [        NO ACTION IS TAKEN.
  21 26W=    [               A)RESTORE FROM INCREMENT 1     (WORD  JINCLAST IN FIXED 
  19 27*W    [               B)WE HAVE WRONG VERSION NO.    (WORD  IPVERNO   
  13 27TG    [               C)FILE DOES NOT EXIST   
  16 28*6    [               D)FILE EXISTS BUT CANNOT BE ACCESSED
  14 28JY ...[               E)FILE EXISTS BUT IS EMPTY  
   4 28SQ    [   
  18 29#B    [        IF ANY OF THE ABOVE ARE TRUE THE FILE IS RECREATED 
  21 29S2    [                   IF B) WE TRY TO PRESERVE CHANGES IN THE CURRENT VAL.
   4 2=?L    [   
  19 2=DS ...#DEF XCONTEXT   =+JCONTEXTA+JCONTEXTB+JCONTEXTC     [MAX CONTEXT
   4 2=L2 ...[   
  20 2=R8 ...[    THE MAX TRACE VALUE MUST INCLUDE FORCEPRINT (EXCEPT OPTRACE)   
   8 2=YB ...[           BUG 2533
  16 2?5J ...[      DONE   15TH APRIL  75                :CENT   
   4 2?=Q ...[   
  21 2?CY ...#DEF TRACEMAX=+ALLBITS_(1@L(23-CBFORCEPRI))      [TRACE ALL FOR  JOB/MOP
  19 2?K6 ...#DEF  OPTRAMAX   =  +ALLBITS       [NO FORCEPRINT FOR OPTRACE   
  17 2?QG    #DEF NOCHECK=AWORK1                [THIS IS PURE CODE !!
  21 2#=6    #DEF TRACEMIN=1@L(23-CBLOGGING)    [LOGGING FOR JOB/MIN TRACE-OTHERS NON
   8 2#PQ    #DEF TERMPARA=AWORK2
  21 2*9B    XMESS          +IPDEFAULT          [ALL IP S SET TO DEFAULT:PLSE RUN IPE
   5 2*P2    #PAGE   
   4 2B8L    [   
  19 2BN=    [                      ***     TABLE OF INSTALLATION PARAMETERS 
   4 2C7W    [   
   4 2CMG    [   
  18 2D76    [                 IPNAME      IPMAX    IPMIN   FIXED-CORE   
  17 2DLQ    [              (PARAMETER  * (MAX   *  (MIN  *  NAME/   
  18 2F6B    [                 NAME)      VALUE)   VALUE)      ADDRESS   
   4 2FL2    [   
   4 2G5L    [   
   9 2GK=          SEGENTRY   IPTABLE
   9 2H4W    NEWREC         +RECLEN  
   9 2HJG                   +IPVERNO 
   6 2HPN ...#UNS  ECS   
  18 2HWW ...      INST     BACKCP      ,       60,        1,    BACKCP  
  19 2J46          INST     BACKGROUND  ,        1,        0,    BACKGROUND  
  18 2JHQ          INST     BACKJAM     ,       99,       50,    BSJAM   
  18 2K3B          INST     BACKJOBS    ,        1,        0,    BACKJOBS
  19 2KH2          INST     BACKTHRESH  ,       50,        0,    BACKTHRESH  
  18 2L2L          INST     BSINTERVAL  ,  8388607,        0,    IDEALJAM
  19 2LG=          INST     CHAPTERQUOTA,        0,        0,    CHAPQUOTA   
   6 2LJT ...#UNS  ECS   
   4 2LMD ...(   
  19 2LQ3 ...      INST     CLUSBACKCP  ,       60,        1,    CLUSBACKCP  
  19 2LSL ...      INST     CLUSMOPCP   ,       60,        1,    CLUSMOPCP   
   4 2LX9 ...)   
  19 2L_W          INST     CLUSTERBACK ,        0,        0,    CLUSTERBACK 
  19 2MFG          INST     CLUSTERMOP  ,        0,        0,    CLUSTERMOP  
  18 2M_6          INST     CONTEXT     , XCONTEXT,        0,    ICONTEXT
  19 2NDQ          INST     COREOBJECT  ,     5120,        0,    AOBJCORES   
  18 2NYB          INST     CPIRATIO    ,      500,      100,    CPIRATIO
  18 2PD2          INST     DATE        ,     1440,        1,    ADINT   
  18 2PXL          INST     DATINTERVAL ,  8388607,        1,    CDATINT 
  18 2QC=          INST     DEFAULTBS   ,    10000,       10,    IDEFBS  
  19 2QWW          INST     DUMPTAPES   ,        3,        1,    AINCCOPIES  
  19 2RBG          INST     DUMPTIME    ,     2000,        0,    ADUMPTIME   
  18 2RW6          INST     FORMULA     ,  1000000,        0,    FORMULA 
  19 2S*Q          INST     HLSREVIEW   ,       20,        1,    HLSREVIEW   
  18 2STB          INST     HOLDUP      ,    10000,        1,    HOLDUP  
  17 2T*2          INST     IDIV        ,      100,        1,    IDIV
  18 2TSL          INST     IMULT       ,      100,        0,    IMULT   
  18 2W#=          INST     IPAGE       ,       50,        1,    IPAGE   
   6 2WRW    #SKI  IPL   
  18 2X?G          INST     IPLIMIT     ,IPLMAXDEF,        0,    IPLIMIT 
  18 2XR6          INST     JOBLIMIT    ,        2,        0,    JOBLIMIT
  18 2Y=Q          INST     JOBTIME     ,     3600,       50,    JOBTIME 
  18 2YQB          INST     JOBTRACE    , TRACEMAX, TRACEMIN,    JOBTRACE
  17 2_=2          INST     L           ,  8388607,        0,    IMIN
  18 2_C8 ...      INST     LPSTIME     ,     2000,        0,    INIPTIME
  19 2_JB ...      INST     LPSVALUE    ,#37777777,        0,    INIPCONTROL 
  19 2_PL          INST     MANAGER0    ,  8388607,        0,    0+INSTPARAM 
  19 329=          INST     MANAGER1    ,  8388607,        0,    1+INSTPARAM 
  19 32NW          INST     MANAGER2    ,  8388607,        0,    2+INSTPARAM 
  19 338G          INST     MANAGER3    ,  8388607,        0,    3+INSTPARAM 
  19 33N6          INST     MANAGER4    ,  8388607,        0,    4+INSTPARAM 
  19 347Q          INST     MANAGER5    ,  8388607,        0,    5+INSTPARAM 
  19 34MB          INST     MANAGER6    ,  8388607,        0,    6+INSTPARAM 
  19 3572          INST     MANAGER7    ,  8388607,        0,    7+INSTPARAM 
   5 35LL    #SKI  G4
  19 366=          INST     MAXQUOTA    ,     5120,        0,    AMAXQUOTA   
  19 36KW          INST     MESSAGETIME ,      400,        1,    ASYSTEMMESS 
  19 375G          INST     MINTRACE    , TRACEMAX, TRACEMIN,    IMINTRACE   
   6 37=N ...#UNS  ECS   
  18 37CW ...      INST     MOPCP       ,       60,        1,    IMOPCP  
  18 37K6          INST     MOPCPI      ,      100,        0,    IMOPCPI 
  18 384Q          INST     MOPJOBS     ,        1,        0,    IMOPJOBS
  19 38JB          INST     MOPLIMIT    ,        1,        0,    IMOPLIMIT   
   7 38PJ ...#SKI  JWPHASE3  
  19 38WQ ...      INST     MOPTOTAL    ,  8388607,        0,    IMOPTOTAL   
  19 393Y ...      INST     MOPTRACE    , TRACEMAX,        0,    IMOPTRACE   
   6 3996 ...#SKI  JSKI37
  19 39B# ...      INST     MOPWAITTIME ,#37777777,        0,    IPMOPWAIT   
  19 39HL          INST     OBJECTQUOTA ,        0,        0,    COBJQUOTA   
  18 3=3= ...      INST     OPTRACE     , OPTRAMAX,        0,    IOPTRACE
   7 3=8D ...#UNS  ENHOPER   
  19 3=*L ...      INST     PAGEWAIT    ,   IPWMAX,   IPWMIN,    IPAGEWAIT   
  19 3=GW          INST     PERFWAIT    ,  8388607,       10,    APERFWAIT   
  19 3?2G          INST     PROGTIME    ,      600,        1,    IPROGTIME   
  18 3?G6          INST     RELOAD      ,        1,        0,    ARELOAD 
  18 3?_Q          INST     REPORTTIME  ,  8388607,        1,    IPROGREP
  19 3#FB          INST     SIZEDEFAULT ,     5120,        0,    COREDEFAULT 
  19 3#_2          INST     SLOTTIME    ,    32767,       10,    ASLOTTIME   
  18 3*DL          INST     TIME        ,     1440,        1,    ATINT   
  18 3*Y=          INST     VOLUME      ,  8388607,        1,    AZVOLUME
  18 3BCW          INST     WAITTIME    ,       90,        0,    AWAITEND
  17 3BXG          INST     WAITWEIGHT  ,      500,      100,    IK  
   7 3BY2 ...#UNS  FSHTEST   
   4 3BYG ...#SKI
   9 3CC6    #DEF  RECLEN=0?-NEWREC  
   5 3CWQ    #PAGE   
   4 3DBB    [   
   4 3DW2    [   
  21 3F*L    XBRK  GEOERR   1,EMSBRKIN          [BREAK-IN ON OPENING PARAMINDEX(G4). 
   5 3FH= ...START   
   9 3G9Q ...      STOZ   TERMPARA(2)
   7 3G#W           LDN  5 1 
  17 3GSG          STO  5 NOCHECK(2)          [SETTING INITIAL VALUES
  16 3H#6          OPENSYS  XBRK,PARAMINDEX,GENERAL,QUERY,NOWAIT 
   4 3HRQ    [   
  16 3J?B    [  FIRST ACTION IS TO ATTEMPT TO OPEN PARAMINDEX(G4)
   4 3JR2    [   
  10 3K=L    [  WE NOW TEST REPLIES **   
  19 3KQ=          TESTREP  OK,OKAY             [SUCCESS? IF YES GO TO  OKAY 
  21 3L9W          TESTREP  NOFILE,XFAIL        [DOESNOT EXIST? IF YES GO TO XFAIL   
  19 3LPG          TESTREP  MAG,OFFLN           [OFFLINE? IF YES GO TO OFFLN 
   4 3M96    [   
  20 3MNQ    [  OH-OH!!  SOMETHINGS WRONG!   WE ARE UNABLE TO CONTINUE SO GEOERR 
   4 3N8B    [   
  10 3NN2    SFAIL GEOERR   1,IPOPFAIL   
   4 3P7L    [   
  20 3PM=    [     OKAY THE FILE IS OFFLINE - SO WE EMPTY IT AND RECREATE USING  
  20 3Q6W    [                                                TABLE IN NEWINST   
   4 3QLG    [   
  14 3R66    OFFLN OPENSYS  XBRK,PARAMINDEX,GENERAL,EMPTY
  18 3RKQ          BRN      XCONT               [OFF WE GO TO TEST REPLY 
   9 3S5B    #SKI K6NEWINST>200-200  
  10 3SK2 ...      TRACE EXEC3(2),OFFLINE
   4 3T4L    [   
  18 3TJ=    [   THE FILE DOESNOT EXIST SO WE CREATE A DIRECTORY ENTRY   
   4 3W3W    [   
  21 3WHG    XFAIL CREATEB  3                   [CREATE A FILE/CREATE BLOCK-ADD IN X3
  19 3X36          MBS      3,CESERIAL,CECARE   [SET -- THIS IS A SERIAL FILE
  18 3XGQ                                       [    -- UPDATE CAREFULLY 
  17 3Y2B          MBS      3,CEONLINE,CEALLINCS[SET -- KEEP ONLINE  
  20 3YG2                                        [   -- DUMP ON ALL INCREMENTS   
   9 3Y_L    #SKI K6NEWINST>200-200  
  11 3_F= ...      TRACE    EXEC3,NOTEXIST   
  15 3_YW          OPENSYS  XBRK,PARAMINDEX,GENERAL,CREATE   
  20 42DG    XCONT TESTREPN OK,SFAIL            [IF IT IS NOT OKAY -- WE GEOERR  
  17 42Y6    XINC  MONOUTX  XMESS(1)            [O?P DEFAULT MESSAGE 
  18 43CQ          BRN      XAPPD               [NOCHECK SET FOR NO CHECK
  18 43XB                                       [OFF NOW TO APPEND RECORD
   4 44C2    [   
  18 44WL    [    SO WE HAVE MANAGED TO OPEN PARAMINDEX WITH NO PROBLEMS 
   4 45B=    [   
  19 45L4 ...OKAY  STEP                         [READ THE ONE + ONLY RECORD  
  18 45TW ...      BZE   3  XINC                [FTLE IS EMPTY SO APPEND 
  18 465N ...      LDX   5  JINCLAST            [INC NO. OF LAST RESTORE 
   9 46*G    #SKI K6NEWINST>200-200  
  11 46T6 ...      TRACE    JINCLAST,ALLOKAY 
  21 47#Q           NGS   5  NOCHECK(2)          [NEGATE TO INDICATE A DELETE-NOCHECK
  18 47SB          SBN   5  1                   [IF IT WAS INC 1 : X5=0  
  20 48RL          BZE   5  XINC                [JUMP IF INC 1 & WE JUST APPEND  
  21 49?=          LDX   5  IPVERSION-A1(3)     [LOAD VALUE OF IPVERNO HELD IN FILE  
   9 49DD ...#SKI  K6NEWINST>200-200 
   9 49KL ...      TRACE    5,IPVERNO
  21 49QW          STOZ     NOCHECK(2)          [WE MUST CHECK IF VERSION NO.S WRONG 
  20 4==G          SBN   5  IPVERNO             [SUBTRACT FIXED CORE VERSION NO  
  11 4=CN ...#UNS  IPEXTENSION+1*IPEXTENSION 
  20 4=JW ...      SBX   5  GSIGN               [FORCE JUMP IF DIFFERENT FORMATS 
  20 4=Q6          BNZ   5  XAPPD               [JUMP IF VERSION NO. NOT EQUAL   
   9 4?9Q    #SKI K6NEWINST>200-200  
  10 4?PB ...      TRACE    5,FINISHED   
  18 4#92          CLOSETOP                     [HOME+DRY WITH NO EFFORT 
  16 4#NL          UP                           [UP UP AND AWAY  
   4 4*8=    [   
  17 4*MW    [    SOMETHING IS WRONG SO WE MUST RECREATE BY APPENDING
   4 4B7G    [   
  21 4BM6    XAPPD SETNCORE  RECLEN,4,FILE,FAPB [SET UP A FILE APPEND BLOCK:ADD IN X3
  20 4C6Q                                       [RECLEN = LENGTH OF PARAMINDEX   
  20 4CLB          LDN   3  NEWREC(1)           [START ADDR. OF TABLE IN NEWINST 
  19 4D62          ADN   4  A1                  [START ADDR. OF FILE/FAPB +A1
  11 4D89 ...#UNS  IPEXTENSION+1*IPEXTENSION 
   4 4D=D ...(   
  21 4D#M ...      LDN   0  RECLEN              [NUMBER OF WORDS IN IP TABLE (>512?) 
  19 4DBW ...      MOVLONG  3,4,0               [MOVE TABLE INTO FILE/FAPB   
   4 4DF5 ...)   
   8 4DH# ...#UNS  IPEXTENSION-1 
  19 4DKL          MOVE  3  RECLEN              [MOVE TABLE INTO FILE/FAPB   
  21 4F5=          LDX   5  NOCHECK(2)          [SEE IF WE MUST COMPARE WITH PREVIOUS
  17 4FJW          BNG  5   XEOB                [DELETE IS REQUIRED  
  21 4G4G          BNZ   5  XSKIP               [    PARAMINDEX - ONLY IF NOCHECK=0  
   5 4GJ6    #PAGE   
   4 4H3Q    [   
  21 4HHB    [   IF WE GET HERE WE MUST COMPARE NEW AND OLD VERSIONS OF PARAMINDEX   
   4 4J32    [   
   9 4JGL    #SKI K6NEWINST>200-200  
  12 4K2= ...      TRACE    NOCHECK(2),COMPARE   
  19 4KFW          STEPAGAIN                    [BACK TO START OF PARAMINDEX 
  21 4K_G          MHUNTW   2,FILE,FAPB         [LOOK FOR FILE/FAPB IN CASE WE COORD 
  21 4LF6          LDX   1  FX2                 [PUT FX2 IN X1 -- SORRY I AM PUSHED  
   4 4LYQ    [   
  20 4MDB    [    WE ARE NOW GOING TO SET UP THE ACCUMULATORS IN A SPECIAL WAY   
  14 4MY2    [         WHERE TO CONTENTS ARE AS FOLLOWS  
   4 4NCL    [   
  16 4NX=    [                     OLD                FILE/FAPB  
  16 4PBW    [                  PARAMINDEX               BLOCK   
   4 4PWG    [   
  15 4QB6    [   START ADDRESS :    X3                    X6 
   4 4QTQ    [   
  15 4R*B    [   MAX ADDRESS   :    TERMPARA              X4 
   4 4RT2    [   
  21 4S#L    [   RUNNING POSIT :    X3                    X2    (WE DO NOT HAVE TO   
  21 4SS=    [                                                     (KEEP START OF POS
  21 4T?W    [      VALUE  OF BLOCK IPVAL  :  X7                   (IN OLD PARAMINDEX
   4 4TRG    [   
  18 4W?6    [      WORDS  OF BLOCK IPNAME :  X5 (1ST WORD OTHER 2 IN  X0
   4 4WQQ    [   
  12 4X=B    [      VALUE  OF FX2 IS KEPT IN:  X1
   4 4XQ2    [   
   4 4Y9L    [   
   5 4YP=    [  NOTE:
  19 4_8W    [         IN  WRITING  THE FOLLOWING IT IS REQUIRED THAT THE OLD
  19 4_NG    [                AND NEW ARE COMPARED BY USING IPNAME  AND NOT  
  18 5286    [                BY USING IPNUM  (WHY?? JE NE SAIS PAS !!   
  21 52MQ          LDX   6  2                   [PUT START ADDR OF FILE/FAPB IN X6   
   9 537B          SBN   6  IPLINE   
  11 53?? ...#UNS  IPEXTENSION+1*IPEXTENSION 
  18 53C8 ...      LDX   4  IPHEAD(2)           [PUT RECORD LENGTH IN X4 
   8 53H5 ...#UNS  IPEXTENSION-1 
  18 53M2          LDEX  4  IPHEAD(2)           [PUT RECORD LENGTH IN X4 
  21 546L          ADX   4  6                   [SO OBTAINING ADDR OF END OF BLOCK   
   8 5499 ...#UNS  IPEXTENSION   
   4 54?S ...(   
  11 54BC ...      LDX   0  IPVERSION-A1(3)  
  21 54F2 ...      BNG   0  XAGNNEW             [J IF PARAMINDEX IN EXTENSION FORMAT 
   4 54HK ...)   
  20 54L=          LDEX  5  IPHEAD-A1(3)        [PUT RECORD LTN OF OLD PARA IN X5
  21 5656          SBN   3  IPLINE              [TO AVOID JUMPS WE SUBTRACT IPLINE   
  20 56=#          ADX   5  3                   [SO OBTAIN END ADDR. OF  OLD PARA
  20 56CG          STO   5  TERMPARA(1)         [STORE END ADDR OF OLD PARAMINDEX
   4 56G_ ...XAGN
   8 56LD ...#UNS  IPEXTENSION   
  19 56PX ...      BNG   3  XAGNNEW             [J IF IP IN EXTENSION TABLE  
  19 56TB ...      ADN   3  IPLINE              [UPDATE POINTER OF OLD PARA  
   9 56YT ...#UNS  IPEXTENSION<1$1   
  21 574B         BXGE   3  TERMPARA(1),XEOB    [WHEN WE REACH END OF OLD PARA JUMP  
   8 575C ...#UNS  IPEXTENSION   
   4 576D ...(   
   8 577F ...      SMO      FX2  
   9 578G ...      TXL   3  TERMPARA 
  21 579H ...      BCC      XEOB                [J IF REACHED END OF OLD PARAMINDEX  
   8 57=J ...      BRN      VALUE
   5 57?K ...XAGNNEW 
  16 57#L ...      STEP                         [READ NEXT RECORD
  18 57*M ...      BZE   3  XEOB                [J IF REACHED END OF FILE
  16 57BN ...      SBN   3  IPNAME-IPHEAD-1     [PTR TO IPHEAD-1 
   8 57CP ...      LDCT  0  #400 
  20 57DQ ...      ORX   3  0                   [SET 'EXTENSION TABLE FORMAT' BIT
   5 57FR ...VALUE   
   4 57GS ...)   
  16 57J2          LDX   7  IPVAL-A1(3)         [LOAD VALUE OF IP
   4 57NN ...[   
  21 57TB ...[           THE FOLLOWING LINES HAVE BEEN ALTERED DUE TO G5456(B O878)  
  18 5824 ...[       THIS IS TO ENABLE NEWINST TO CARRY OVER ANY CHANGES 
  19 586Q ...[       TO IP NAMES(E.G. BY IPEDIT)ON RE-CREATING PARAMINDEX(G4)
  20 58?D ...[       THIS IS DONE BY COMPARING IPNUM INSTEAD OF IPNAME(3 WORDS)  
   4 58D6 ...[   
  14 58JS ...[    DONE    3.SEPT.74            (:CENT)   
   4 58PG ...[   
  19 58W8 ...     LDX    5  IPNUM-A1(3)        [LOAD LOCATION OF IP IN FIXED 
   9 58YH ...#UNS  IPEXTENSION<1$1   
  19 592W          LDX   2  6                   [RESET X2 TO START OF FAPB   
   8 595F ...#UNS  IPEXTENSION   
   4 5984 ...(   
  18 59=M ...      MHUNT    2,FILE,FAPB         [PTR TO FILE/FAPB BLOCK  
   9 59*= ...      SBN   2  IPLINE   
   4 59CT ...)   
  16 59GG    XFBL  ADN   2  IPLINE              [UPDATE POINTER  
  20 5=26          BXGE  2  4,XAGN              [JUMP WHEN END OF FAPB IS REACHED
  19 5=6J ...     BXU    5  IPNUM(2),XFBL      [JUMP IF TWO NUMBERS NOT EQUAL
   4 5=?2 ...[   
  20 5=CD ...[      WE NOW HAVE A MATCH SO WE MERELY HAVE COPY ACROSS THE NAME   
   4 5=HW ...[   
  15 5=N# ...       LDX   0  IPNAME-A1(3)        [FIST WORD  
  18 5=SQ ...       STO   0  IPNAME(2)           [STORE IN NEW PARAMINDEX
  20 5=_B          LDX   0  IPNAME-A1+1(3)      [REPEAT FOR 2ND WORD OF IPNAME   
  17 5?F2 ...       STO   0  IPNAME+1(2)          [STORE THE SECOND  
  17 5?YL          LDX   0  IPNAME-A1+2(3)      [AND FOR THE THIRD   
  16 5#D= ...       STO   0  IPNAME+2(2)          [STORE THE LAST
   4 5#XW    [   
  12 5*CG    [    IF WE GET HERE WE HAVE A MATCH 
   4 5*X6    [   
  18 5*_8          STO   7  IPVAL(2)            [PUT OLD VALUE INTO FAPB 
   4 5B3=    [   
  18 5B5#    [         THE FOLLOWING 4 LINES WHERE INSERTED TO CORRECT   
  14 5B7B    [                     BUG  0585 (G 5395).   
  19 5B9D    [         THIS RESULTS IN NEWINST82 PASSING OVER THE MAX & MIN  
  17 5B?G    [           VALUES AS WELL AS THE CURRENT VALUE WHEN IT 
  13 5B*J    [            RE CREATES PARAMINDEX(G4). 
  15 5BCL    [    DONE   17.JULY.74                  :CENT   
   4 5BFN    [   
  10 5BHQ           LDX   0  IPMAX-A1(3) 
  18 5BKS           STO   0  IPMAX(2)             [FIRST THE MAX VALUE   
  10 5BMW           LDX   0  IPMIN-A1(3) 
  18 5BPY           STO   0  IPMIN(2)              [THEN THE MIN VALUE   
   4 5BS2    [   
   9 5BWB    #SKI K6NEWINST>200-200  
   9 5CB2 ...      TRACE    7,MATCH  
  17 5CTL          BRN      XAGN                [BACK TO DO NEXT IP  
   5 5D*=    #PAGE   
   4 5DB9 ...XEOB
   8 5DC8 ...#UNS  IPEXTENSION   
   4 5DD7 ...(   
  18 5DF6 ...      FASTREWIND 3                 [REWIND TO START OF FILE 
   7 5DG5 ...      LDN   3  0
   7 5DH4 ...      CURTAIL  3
   4 5DJ3 ...)   
   8 5DK2 ...#UNS  IPEXTENSION   
   4 5DK_ ...#SKI
  20 5DLY ...      DELETE                       [REMOVE RECORD IN OLD PARAMINDEX 
   5 5DMX ...XSKIP   
  11 5DNW ...#UNS  IPEXTENSION+1*IPEXTENSION 
   4 5DPT ...(   
  18 5DQS ...      STEP     ,2                  [RENAME /FURB TO /FUWB   
  18 5DRR ...      LDN   0  2                   [2 WORDS IN FIRST RECORD 
  18 5DSQ ...      STO   0  0(3)                [HEADER OF FIRST RECORD  
  18 5DTP ...      MHUNTW   2,FILE,FAPB         [PTR TO FILE/FAPB BLOCK  
  19 5DWN ...      LDX   0  IPVERSION(2)        [VERSION NUMBER OF PARAMINDEX
  20 5DXM ...      ORX   0  GSIGN               [SET 'EXTENSION TABLE FORMAT' BIT
  18 5DYL ...      STO   0  1(3)                [STORE AS FIRST RECORD   
   7 5D_K ...      LDN   7  2
   5 5F2J ...XLOOP   
  21 5F3H ...      STEP     ,IPLINE+1           [RENAME NEXT FILE/FURB TO FILE/FUWB  
  18 5F4G ...      MHUNTW   1,FILE,FAPB         [PTR TO FILE/FAPB BLOCK  
   9 5F5F ...      LDN   0  IPLINE+1 
  17 5F6D ...      STO   0  0(3)                [SETUP RECORD HEADER 
  19 5F7C ...      ADN   3  1                   [PTR TO START OF RECORD ENTRY
   7 5F8B ...      SMO      7
  10 5F9* ...      LDN   2  IPNAME-2(1)  
   9 5F=# ...#SKI  K6NEWINST>300-300 
  10 5F?? ...      TRACE    2,EXTIPWRI   
  16 5F#= ...      MOVE  2  IPLINE              [MOVE IP DETAILS 
  16 5F*9 ...      ADN   7  IPLINE              [UPDATE POINTER  
  18 5FB8 ...      BXL   7  ALOGLEN(1),XLOOP    [J IF STILL MORE TO WRITE
   4 5FC7 ...)   
  11 5FD6 ...#UNS  IPEXTENSION-1*IPEXTENSION 
   4 5FF5 ...(   
  18 5FG4 ...      MHUNT    3,FILE,FAPB         [PTR TO FILE/FAPB BLOCK  
   8 5FH3 ...      LDX   0  GSIGN
   9 5FJ2 ...      ORS   0  A1+1(3)  
  21 5FJ_ ...      ERS   0  A1+1(3)             [ENSURE 'EXTENSION TABLE' BIT UNSET  
  18 5FKY ...      APPEND                       [APPEND OUR FAPB BLOCK.  
   4 5FLX ...)   
   8 5FMW ...#UNS  IPEXTENSION   
   4 5FNT ...#SKI
  18 5FPS ...      APPEND                       [APPEND OUR FAPB BLOCK   
  16 5FS6          CLOSETOP                     [CLOSE PARAMINDEX
  18 5G?Q          MFREE    FILE,FAPB           [RELEASE FILE/FAPB BLOCK 
   9 5GRB    #SKI K6NEWINST>200-200  
  12 5H?2 ...      TRACE    NOCHECK(2),ENDOFSEG  
  17 5HQL          UP                           [UP UP AND AWAY !!   
   4 5J==    #END
   8 ____ ...36530721000300000000
