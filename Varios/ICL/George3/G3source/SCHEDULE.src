  16 22FL ...#LIS  K0SCHEDULE>K0HLS>K0COMMAND>K0GREATGEO>K0ALLGEO
  17 22_= ...#SEG  SCHEDULE60                    [RICHARD GRIMWADE   
  10 23DW ...               8HSCHEDULE   
   4 23JR ...[   
  16 23NN ...[   (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982
   4 23SK ...[   
   4 23YG    #   
  16 24D6 ...#   THIS COMMAND  INFORMS THE HLS SUBJECT PROGRAM   
  15 24XQ ...#   OF THE OPERATOR JOB SCHEDULING REQUIREMENTS 
  19 25CB    #   TO  DO THIS IT PUTS AN ENTRY FOR THE JOB ON THE TEMP HLSQ   
   4 25X2    #   
  11 26BL ...      SEGENTRY K1SCHEDULE,XK1   
  11 26P2 ...      SEGENTRY K2SCHEDULE,XK2   
  11 26SG ...      SEGENTRY K3SCHEDULE,XK3   
   4 26W=    #   
  10 26X3 ...      BITDEFS  AWORK3,6,SCOM
   6 26X8 ...#UNS  ICT   
  10 26X9 ...      BITDEFS  AWORK3,12,XAP
  11 26XH ...      BITDEFS  AWORK3,14,XCP,XMP
  12 26XS ...      BITDEFS  AWORK3,16,XCC,XST,XEP
  13 26Y# ...      BITDEFS  AWORK3,20,XGP,XGB,XGR,XGQ
  19 26YK ...      BITDEFS  AWORK4,0,NUM,OPENJL,USER,XME,NULL,WELL,OPENUSER  
  12 26YT ...      BITDEFS  AWORK4,7,XBACK,POPPED
   4 26_B ...[   
   4 2727 ...[   
   7 272Y ...#DEF  TABSIZE=7 
  15 2735 ...TABLE          +7,8HGRBREAK ,+2,2HGB,#4,#5540   
  15 2736 ...               +7,8HGRRERUN ,+2,2HGN,#2,#5540   
  15 2737 ...               +7,8HGRQUERY ,+2,2HGQ,#1,#5540   
  15 2738 ...               +8,8HGRRUNDUM,+2,2HGP,#10,#5540  
  14 2739 ...               +7,8HEXPRESS ,+2,2HEP,#40,#17
  15 273= ...               +5,8HSTART   ,+2,2HST,#100,#217  
  15 273? ...XCPR           +7,8HCOREPRI ,+2,2HCP,#1000,#17  
  15 273# ...XMPR           +7,8HMILLPRI ,+2,2HMP,#400,#17   
  15 275? ...               +6,8HCANCEL  ,+2,2HCC,#200,#100  
  14 2764 ...XMES           +7,8HMESSAGE ,+2,2HME,#20,+0 
   6 276# ...#UNS  ICT   
  16 276J ...XAPR               +6,8HACTPRI  ,2,2HAP,#4000,#17   
  11 276T ...#DEF  TABENTS=0?-TABLE/TABSIZE  
   4 277L ...[   
   8 278C ...#DEF  SIZELIST=10   
   9 2798 ...XJOBLIST       +SIZELIST
   8 279_ ...               12H  
  10 27=Q ...               12HJOBLIST   
   8 27## ...               +1   
   8 27*5 ...               +1   
   9 27*F ...               4H****   
   8 27*W    STRING         +1   
   8 27TG                   4H:  
   8 28*6    TWO            +2   
   4 28FJ ...[   
   8 28G2 ...SPARA SBX   7  FX1  
   7 28GD ...      SPARANOX 4
   8 28GW ...      ADX   7  FX1  
   7 28H# ...      EXIT  7  0
   4 28K4 ...[   
   9 28L2 ...[     START OF MAIN CODE
   4 28N9 ...[   
   4 28QD ...[   
   4 28SQ    XK1 
   9 294J ...      STOZ     AWORK1(2)
  16 29#B ...      STOZ     AWORK3(2)           [ HLSYONQ WORD   
  16 29S2 ...      STOZ     AWORK4(2)           [ VARIOUS MARKERS
   9 2=3S ...      BS       2,SCOM   
   9 2=?L ...      LDN   3  STRING(1)
   9 2=R= ...      CALL  7  SPARABEG 
   8 2?=W ...      BRN      XJONO
  19 2?QG ...      CHEKLFN2 XJONOF,NLFN          [J TO XJONO IF NO USERNAME  
  20 2#=6                                       [AND TO NLFN  IF NOT L.F.N.FORMAT
  16 2#PQ          LDX   4  JPARNUM(3)          [GET PARAM NO.   
   7 2*9B          LDN   5  3
   7 2*P2          TXL   4  5
  19 2B8L          BCC      NUNA                [J IF NOT 1ST OR 2ND PARAM   
   7 2BN=          LDN   5  1
   9 2C7W          TXU   4  TWO(1)   
  19 2CMG          BCC      X1                  [J IF USERNAME IS 2ND PARAM  
   7 2D76          LDN   5  2
  18 2DLQ    X1    SPARANOX 5                   [GET JOBNAME PARAMETER   
  19 2F6B          CHEKLFN2 NJBNA,NLFN          [J TO NJBNA IF NO JOBNAME AND
  19 2FL2                                       [  TO NLFN IF FORMAT ERROR   
   9 2G5L ...      CALL  7  SNAMES   
   8 2GK= ...      CALL  6  SRNAM
   9 2H4W ...      STO   4  AWORK1(2)
   9 2HJG ...      BS       2,USER   
   4 2STB    #   
  16 2T*2    #                      RETURNS WITH JOBNUMBER IN X4 
   4 2TSL    #   
   5 2TT7 ...OTHERS  
  16 2TTN ...      LDN   5  TABENTS             [ NO OF ENTRIES  
  15 2TW9 ...      LDN   6  TABLE               [ TABLE START
  18 2TWQ ...NEXT  LDN   4  2                   [ TWO IDENTS PER ENTRY   
   7 2TX5 ...      SMO      6
  16 2TXD ...      LDN   3  0(1)                [ CURRENT ENTRY  
  16 2TXS ...XL    CALL  7  SPARABEG            [ LOOK FOR IDENT 
  16 2TY* ...      BRN      NOT                 [ J IF NOT FOUND 
  17 2TYW ...      SMO      6                   [ CHECK FOR ME PARAM 
  18 2T_C ...      LDX   7  5(1)                [ AS HANDLED DIFFERENTLY 
   9 2T_Y ...      TXU   7  XMES+5(1)
   8 2W2F ...      BCC      XPLUS
  12 2W2J ...      BXE   7  XCPR+5(1),PRIORITY   
  12 2W2K ...      BXE   7  XMPR+5(1),PRIORITY   
   6 2W2L ...#UNS  ICT   
   4 2W2M ...(   
  11 2W2P ...      BXE   7  XAPR+5(1),XACTPRI
   4 2W2Q ...)   
   7 2W35 ...      FREECORE 3
   9 2W36 ...XM    ORX   7  AWORK3(2)
   9 2W44 ...      STO   7  AWORK3(2)
  19 2W4K ...      SMO      6                   [ CHECK FOR VALIF COMBINATION
   8 2W56 ...      ANDX  7  6(1) 
  17 2W5M ...      BNZ   7  WRONG               [ J IF NOT ALLOWED   
  19 2W68 ...      BRN      MORE                [ OK SO LOOK FOR NEXT PARAM  
  16 2W6H ...NOT   SMO      6                   [ TRY SHORT FORM 
   8 2W6W ...      LDN   3  3(1) 
  19 2W7= ...      BCT   4  XL                  [ IF NOT TESTED FOR ALREADY  
  17 2W7R ...MORE  ADN   6  TABSIZE             [ NEXT ENTRY IN TABLE
  18 2W8# ...      BCT   5  NEXT                [ J IF NOT END OF TABLE  
  15 2W8T ...      BRN      SDUN                [ FINISHED   
  11 2W8Y ...[ STORE MP/CP IN ADATA/CSTORE   
   5 2W8_ ...PRIORITY
  19 2W92 ...      LDN   4  1                   [COUNT OF CP/MP PARAMETERS   
   9 2W93 ...      LDX   0  ANUM(3)  
  17 2W94 ...      BZE   0  NOT3                [J IF NULL PRIORITY  
   9 2W95 ...      LDCH  0  APARA(3) 
   8 2W96 ...      SBN   0  #30  
  18 2W97 ...      BNZ   0  PR52                [J IF NOT OF FORM X(Y)   
  18 2W98 ...      BXE   7  XCPR+5(1),NOT3      [ERROR IF CORE PRIORITY  
  10 2W99 ...      LDX   4  JPARNUM(3)   
   8 2W9= ...      PARALYSE ,,4  
  10 2W9? ...      TESTREP2 UNPAIR,UNPAIR
   7 2W9# ...      PARANUMB 4
   7 2W9* ...      SBN   4  2
  19 2W9B ...      BNZ   4  NOT3                [ERROR IF NOT 2 PARAMETERS   
   7 2W9C ...      LDN   4  2
  18 2W9D ...      PARALYSE #34,,4              [REMOVE BRACKETS FROM (Y)
   7 2W9F ...      PARANUMB 4
   7 2W9G ...      SBN   4  5
  19 2W9H ...      BPZ   4  NOT3                [ERROR IF> 4MILL PRIORITIES  
   7 2W9J ...      ADN   4  5
   9 2W9K ...      MFREE    CPB,CUNI 
   7 2W9L ...PR5   PARAPASS  
  10 2W9M ...PR52  MHUNT    2,CPB,CUNI   
   9 2W9N ...      NGX   0  ANUM(2)  
  17 2W9P ...      BPZ   0  NULLPRI            [J IF NULL PRIORITY   
   7 2W9Q ...      CHNUMCO1 1
  19 2W9R ...      TESTREP2 CHNUMERR,VNUMERR   [ERROR IF PRIORITY NOT NUMERIC
  10 2W9S ...      LDX   0  ACOMMUNE1(2) 
   7 2W9T ...      SBN   0  1
  20 2W9W ...      BNG   0  XERANGE            [ERROR IF PRIORITY OUT OF RANGE   
   8 2W9X ...      SBN   0  63   
  20 2W9Y ...      BPZ   0  XERANGE             [ERROR IF PRIORITY OUT OF RANGE  
  10 2W9_ ...PR7   HUNT     2,ADATA,ASUP 
  17 2W=2 ...      BPZ   2  PR8                 [J IF NOT FIRST CP/MP
   6 2W=3 ...#UNS  ICT   
   5 2W=4 ...#SKIP   
   4 2W=5 ...(   
  10 2W=6 ...      MHUNT    3,CPB,CUNI   
  17 2W=7 ...      NAME     3,ADATA,ASUP   [SET UP BLOCK TO HOLD.... 
  15 2W=8 ...      ALTLENGD 3,2                 [...CP AND MP
   7 2W=9 ...      LDX   2  3
  16 2W== ...      STOZ     A1(2)               [INITIALISE MP   
  16 2W=? ...      STOZ     A1+1(2)             [INITIALISE CP   
   4 2W=# ...)   
   6 2W=* ...#UNS  ICT   
  21 2W=B ...      CALL  6  SRASUP                  [SET UP & INITIALISE ASUP BLOCK  
  18 2W=C ...[                                          TO HOLD CP$MP$AP 
   8 2W=D ...      BRN      PR83 
   7 2W=F ...PR8   LDX   6  2
   9 2W=G ...      MFREE    CPB,CUNI 
   7 2W=H ...      LDX   2  6
   8 2W=J ...PR83  SMO      FX2  
   9 2W=K ...      LDX   0  ACOMMUNE1
  14 2W=L ...      BXE   7  XCPR+5(1),PR87     [J IF CP  
  18 2W=M ...      SRL   3  22                  [SET X3=0 IF 1ST MP......
  19 2W=N ...      SRC   3  2                   [...OTHERWISE LEAVE CHAR. MOD
   7 2W=P ...      ADX   3  2
  18 2W=Q ...      DCH   0  A1(3)                [SAVE MP IN ADATA/CSTORE
   7 2W=R ...      BCHX  3  /
  20 2W=S ...      SBX   3  2                   [X3 IS PTR TO MP IN ADATA/CSTORE 
   8 2W=T ...      LDN   6  XMPR 
   8 2W=W ...      BRN      PR9  
  18 2W=X ...PR87  STO   0  A1+1(2)             [SAVE CP IN ADATA/CSTORE 
   8 2W=Y ...      LDN   6  XCPR 
   8 2W=_ ...PR9   BCT   4  PR5  
   8 2W?2 ...      LDX   2  FX2  
   8 2W?3 ...      BRN      XM   
   5 2W?4 ...NULLPRI 
   8 2W?5 ...      SMO      FX2  
   9 2W?6 ...      STOZ     ACOMMUNE1
   8 2W?7 ...      BRN      PR7  
   6 2W?9 ...#UNS  ICT   
   4 2W?= ...(   
  14 2W?? ...#                VALIDATE ACTPRI PARAMETER  
   4 2W?# ...#   
   5 2W?* ...XACTPRI 
  20 2W?B ...      JBS      XAP0,,ICTSW       [DO NOT PROCESS ANY ACTPRI UNLESS  
  14 2W?C ...      FREECORE    3          [ICTSW IS SET  
   8 2W?D ...      BRN      MORE 
   4 2W?F ...XAP0
   9 2W?G ...      LDX   0  ANUM(3)  
  18 2W?H ...      BZE   0  VNUMERR             [   'NOT NUMERIC' ERROR  
   7 2W?J ...      CHNUMCO1 1
  18 2W?K ...      TESTREP2 CHNUMERR,VNUMERR     [   'NOT NUMERIC' ERROR 
  19 2W?L ...      LDX   0  ACOMMUNE1(2)         [  PICK UP VALUE OF PARAM   
  19 2W?M ...      SBN   0  1                    [  - MUST BE BETWENN 1 AND 3
  18 2W?N ...      BNG   0  XERANGE              [  'OUT OF RANGE' ERROR 
   7 2W?P ...      SBN   0  3
  18 2W?Q ...      BPZ   0  XERANGE              [  'OUT OF RANGE' ERROR 
  10 2W?R ...      HUNT     2,ADATA,ASUP 
  18 2W?S ...      BPZ   2  XAP1                 [  J IF NOT 1ST CP/MP/AP
  20 2W?T ...      CALL  6  SRASUP              [SET UP AND INITIALISE ASUP BLOCK
   4 2W?W ...#   
  16 2W?X ...                                 [TO HOLD CP/MP/AP  
   4 2W?Y ...[   
   8 2W?_ ...      BRN      XAP2 
   7 2W#2 ...XAP1  LDX   6  2
   9 2W#3 ...      MFREE    CPB,CUNI 
   7 2W#4 ...      LDX   2  6
   8 2W#5 ...XAP2  SMO      FX2  
   9 2W#6 ...      LDX   0  ACOMMUNE1
   7 2W#7 ...      SRC   0  8
  18 2W#8 ...      STO   0  A1+2(2)               [SAVE AP IN ADATA/ASUP 
   8 2W#9 ...      LDX   2  FX2  
   8 2W#= ...      LDN   6  XAPR 
  21 2W#? ...      BRN      XM                     [RETURN TO PARAMETER VALIDATION LO
   4 2W## ...)   
   4 2W#* ...[   
  10 2W#B ...[     VALIDATE MESSAGE PARAM
   4 2W#C ...[   
  10 2W#D ...XPLUS MHUNT    3,CPB,CUNI   
   9 2W#F ...      LDX   0  ANUM(3)  
  16 2W#G ...      SRL   0  12                  [ CHECK IF TYPE 2
  17 2W#J ...      SBN   0  2                   [ CHECK FOR TYPE 2   
  17 2W*L ...      LDX   4  JPARNUM(3)          [ SPLIT OUT MESSAGE  
   8 2W*W ...      BNZ   0  NOT2 
   7 2WB7 ...      FREECORE 3
   7 2WBG ...      LDN   3  2
   8 2WBT ...PLYSE PARALYSE ,,4  
   7 2WC9 ...      LDN   4  2
   8 2WCC ...      BCT   3  PLYSE
   7 2WCK ...      LDN   4  1
   8 2WCR ...      CALL  7  SPARA
   7 2WC_ ...      PARAFREE  
   8 2WD? ...      BS       2,XME
  10 2WDD ...      MHUNT    3,CPB,CUNI   
  11 2WDK ...      NAME     3,ADATA,CSTORE   
   8 2WDS ...      BRN      NEXT 
   4 38BL ...#   
   4 38G7 ...SDUN
   4 38KN ...#   
  20 38P9 ...#     THE REST OF THE SCHEDULE COMMAND IS DEALT WITH IN AN OVERFLOW 
   7 38SQ ...#     SEGMENT.  
   4 38Y? ...#   
  20 393S ...      ACROSS   JWELLONE,4           [GO TO DEAL WITH REST OF COMMAND
   4 397* ...#   
  15 39=W ...XK2            [RE-ENTRY POINT FROM 'JWELLONE'  
   4 39BC ...#   
   5 39HL    THEND   
   8 39MH ...      LDX   2  FX2  
  10 39RD ...      JBS      SNUL,2,NULL  
   5 39X* ...TENDCOM 
   6 3=3=          ENDCOM
   4 3=3G ...[   
   9 3=3Q ...SNUL  MONOUT   ASCMESS  
   9 3=42 ...      BRN      TENDCOM  
   6 3=43 ...#UNS  ICT   
   4 3=44 ...(   
  15 3=45 ...#               SR TO SET UP ADATA/ASUP BLOCK   
   4 3=46 ...#   
   5 3=47 ...SRASUP  
   8 3=48 ...      SBX   6  FX1  
  10 3=49 ...      MHUNT    3,CPB,CUNI   
  10 3=4= ...      NAME     3,ADATA,ASUP 
   8 3=4? ...      ALTLENGD 3,3  
   7 3=4# ...      LDX   2  3
   8 3=4* ...      STOZ     A1(2)
   9 3=4B ...      STOZ     A1+1(2)  
   9 3=4C ...      STOZ     A1+2(2)  
   8 3=4D ...      ADX   6  FX1  
   7 3=4F ...      EXIT  6  0
   4 3=4G ...)   
   4 3=4H ...[   
   4 3=4J ...[   
  18 3=4Q ...SPARABEG                           [ OBTAIN SPECIFIED PARAM 
  14 3=52 ...      SBX   7  FX1                 [ LINK   
  18 3=5= ...      SPARABEG 1,0(3),,,0             [ GET PARAM IF EXISTS 
  16 3=5G ...      ADX   7  FX1                 [ RESTORE LINK   
  10 3=5Q ...      MHUNT    3,CPB,CUNI   
  16 3=62 ...      LDX   0  ANUM(3)             [ SEE IF EXISTS  
  15 3=6= ...      BNG   0  SPOUT               [ J IF NOT   
   7 3=6G ...      EXIT  7  1
   4 3=6Q ...[   
  16 3=72 ...SPOUT FREECORE 3                   [ FREE CPB/CUNI  
   7 3=7= ...      EXIT  7  0
   4 3=7G ...[   
   4 3=7Q ...[   
   4 3=82 ...[   
   8 3=8= ...POP   SBX   5  FX1  
   3 3=8C ...
  11 3=8J ...POPC  POP      POPC,,JWACCESS   
   4 3=8K ...POPX
   9 3=8L ...      BS      2,POPPED  
   8 3=8M ...      ADX   5  FX1  
   7 3=92 ...      EXIT  5  0
   4 3=9= ...[   
   5 3=9G ...VOPCLOS 
   8 3=9Q ...      SBX   5  FX1  
  20 3=9W ...      LDX   2  FX2                 [X2 SOMETIMES CORRUPT ON ENTRY!! 
  12 3==2 ...      JMBAC    PEX,2,OPENJL,OPENUSER
   7 3=== ...      CLOSETOP  
   8 3==G ...PEX   ADX   5  FX1  
  10 3==M ...VOP   JBCC     (5),2,POPPED 
   9 3==S ...      VOP      ,JWACCESS
   7 3=?2 ...      EXIT  5  0
   4 3=?= ...[   
   4 3=?G ...[   
   4 3=?Q ...[   
   5 3=#2 ...SNAMES  
  10 3=#= ...      HUNT     3,CPB,CUNI   
  15 3=#G ...      LDX   4  ACES                [LOAD SPACES 
  10 3=#Q ...      STO   4  ACOMMUNE1(2) 
  10 3=*2 ...      LDN   4  ACOMMUNE1(2) 
  17 3=*= ...      LDN   5  ACOMMUNE2(2)        [ SPACEFILL  ACOMMUNE
  14 3=*G ...      MOVE  4  5                   [ AREA   
  17 3=*Q ...      LDEX  0  ANUM(3)             [NO. CHARS IN JOBNAME
  16 3=B2 ...      LDN   4  APARA(3)            [PTR TO JOBNAME  
  10 3=B= ...      LDN   5  ACOMMUNE1(2) 
   7 3=BG ...      SMO      0
  16 3=BQ ...      MVCH  4  0                   [MOVE IN JOBNAME 
  19 3=C2 ...      STO   0  AWORK1(2)           [SAVE COUNT OF JOBNAME CHARS 
  20 3=C= ...      HUNT2    3,CPB,CUNI          [FIND /CUNI CONTAINING USERNAME  
   9 3=CG ...      LDEX  0  ANUM(3)  
   9 3=CQ ...      LDN   4  APARA(3) 
  10 3=D2 ...      LDN   5  ACOMMUNE4(2) 
   7 3=D= ...      SMO      0
  20 3=DG ...      MVCH  4  0                   [MOVE USERNAME INTO ACOM. AREA   
   9 3=DQ ...      STO   0  AWORK2(2)
   7 3=F2 ...      EXIT  7  0
   4 3=GW    #   
  21 3?2G    #  THIS PATH IS ENTERED WHEN THERE IS NO USERNAME PARAM. , AND THEREFORE
  12 3?G6    #  A  JOBNUMBER PARAMETER IS ASSUMED
   4 3?_Q    #   
   5 3#9J ...XJONOF  
   7 3#FB ...      FREECORE 3
   5 3#P8 ...XJONO   
  16 3#_2          PARAPASS                     [GET JOBNO. PARAM
  10 3*DL          MHUNT    3,CPB,CUNI   
   9 3*Y=          NGX   4  ANUM(3)  
  17 3BCW          BPZ   4  NJBNO               [JOBNO. PARAM MISSING
  18 3BXG          CHNUMCOD 1,3                 [CONVERT NO. TO BINARY   
  18 3CC6          TESTREP  CHNUMERR,NUMERR     [TO REPORT NUMBER ERROR  
   8 3CLY ...      BS       2,NUM
  10 3CWQ          LDX   4  ACOMMUNE1(2) 
   8 3DBB    #SKI  K6ALLGEO>99-99
  10 3DW2          TRACE    4,*JOBNO**   
  19 3F*L          BZE   4  NOTVAL              [  JOBNUMBER MUST BE NON-ZERO
  15 3FT=          BNG   4  NOTVAL              [  AND +VE.  
   8 3G54 ...      CALL  5  POP  
  19 3G#W          CALL  6  SRNUM               [  S/R TO CHECK IF JOB EXISTS
   9 3GSG ...      STO   4  AWORK1(2)
   9 3H#6 ...      BRN      OTHERS   
   4 3J?B    #   
  19 3JR2    #  THE  FOLLOWING S/R CHECKS FOR THE EXISTENCE OF JOB , GIVEN   
   7 3K=L    #  THE JOBNUMBER
   4 3KQ=    #   
   8 3L4M ...SRNUM SBX   6  FX1  
   8 3LC4 ...      JOBLOCK  4,3  
  16 3LPG          BNG   3  SRNUM1              [J IF NO JOBBLOCK
  17 3M96          SPRIVJUMP  JSISSUE,3,SYS     [J IF SYSTEM ISSUED  
   5 3MNQ    SRNUM2  
   8 3MYJ ...      ADX   6  FX1  
   7 3N8B          EXIT  6  0
   5 3NN2    SRNUM1  
  19 3NRF ...      SHLSQ    4,3,NONEX           [SEARCH HLSQ FOR JOB ENTRY   
   9 3NT8 ...      BRN      SRNUM2   
   4 3NWY ...[   
   4 3P2C ...[   
  14 3P2W ...[ USE SUPPLEMENTARY MODE 'QUERY' TO PREVENT 
  14 3P3* ...[ GEOERR IF FILE ALREADY OPEN TO JOB - B1511
   6 3P3S ...[ 04.03.82  
  15 3P4? ...NONEX OPENSYS  (GEOERR),JOBLIST,GENERAL,QUERY   
   6 3P4Q ...[ TEST REPLY
   9 3P59 ...      TESTREP  OK,OPENOK
  10 3P5N ...      TESTREP  CLUDGE,XNOJL 
  15 3P67 ...[ THE FOLLOWING 'TESTREP' CAN BE REMOVED WHEN   
  15 3P6L ...[ 'OPENSYS' IS CORRECTED TO REPLY WITH 'CLUDGE' 
  16 3P75 ...[ INSTEAD OF 'NAME' WHEN DIRECTORY IS OPEN TO JOB   
  10 3P7J ...      TESTREP  NAME,XNOJL   
  15 3P83 ...      GEOERR  1,SCNOJL      [ UNEXPECTED REPLY  
   5 3P8G ...OPENOK  
   9 3P9* ...      BS       2,OPENJL 
   9 3P#S ...      GETJOB   4,SYSTEM 
   9 3PD? ...      TESTRPN2 OK,NONX  
  10 3PHQ ...      MBS      2,OPENJL,WELL
  15 3PM=          BRN      SRNUM2              [J IF FOUND  
   4 3Q6W    #   
  19 3QLG    #     THE FOLLOWING  S/R CHECKS FOR THE EXISTENCE OF JOB, GIVEN 
  11 3R66    #     THE JOBNAME AND USERNAME  
   4 3RKQ    #   
   8 3S5B    SRNAM LDN   3  BJOBQ
   8 3S9? ...      SBX   6  FX1  
   8 3S*8 ...      CALL  5  POP  
   8 3SF5 ...      ADX   6  FX1  
   5 3SK2    SRNAM2  
   9 3T4L          LDX   3  FPTR(3)  
   8 3TJ=          TXU   3  CXJO 
  17 3W3W          BCC      SRNAM1              [J IF END OF Q FOUND 
   7 3WHG          LDN   0  3
   5 3X36    SRNAM3  
   7 3XGQ          SMO      0
  10 3Y2B          LDX   4  JNAME-1(3)   
   7 3YG2          SMO      0
  11 3Y_L          TXU   4  ACOMMUNE1-1(2)   
  16 3_F=          BCS      SRNAM2              [J IF NO MATCH   
   9 3_YW          BCT   0  SRNAM3   
   7 42DG          LDN   0  3
   5 42Y6    SRNAM4  
   7 43CQ          SMO      0
  10 43XB          LDX   4  JUSER-1(3)   
   7 44C2          SMO      0
  11 44WL          TXU   4  ACOMMUNE4-1(2)   
  16 45B=          BCS      SRNAM2              [J IF NO MATCH   
   9 45TW          BCT   0  SRNAM4   
  17 46*G          SPRIVJUMP  JSISSUE,3,SYS     [J IF SYSTEM ISSUED  
  19 46T6          LDX   4  JOBNUM(3)           [BLOCK FOUND - LOAD JOBNUMBER
   8 47#Q          EXIT  6   0   
   4 47SB    #   
  15 48#2    #   THE FOLLOWING IS THE SEARCH DOWN THE HLSQ   
   4 48RL    #   
   5 49?=    SRNAM1  
   8 49QW          LDN   3  BHLSQ
   5 4==G    SRNAM5  
   9 4=Q6          LDX   3  FPTR(3)  
   8 4?9Q          TXU   3  CXHL 
  18 4?PB ...      BCC      NOJOB               [J IF JOB NON EXISTENT   
   7 4#92          LDN   0  3
   5 4#NL    SRNAM6  
   7 4*8=          SMO      0
  10 4*MW          LDX   4  HLSJNAM-1(3) 
   7 4B7G          SMO      0
  11 4BM6          TXU   4  ACOMMUNE1-1(2)   
  16 4C6Q          BCS      SRNAM5              [J IF NO MATCH   
   9 4CLB          BCT   0  SRNAM6   
   7 4D62          LDN   0  3
   5 4DKL    SRNAM7  
   7 4F5=          SMO      0
  10 4FJW          LDX   4  HLSUNAM-1(3) 
   7 4G4G          SMO      0
  11 4GJ6          TXU   4  ACOMMUNE4-1(2)   
  16 4H3Q          BCS      SRNAM5              [J IF NO MATCH   
   9 4HHB          BCT   0  SRNAM7   
  16 4J32          LDX   4  HLSJONO(3)          [GET JOBNUMBER   
   7 4JGL          EXIT  6  0
   4 4KH8 ...[   
   5 4KHT ...NOJOB   
   8 4KJG ...      SBX   6  FX1  
  13 4KK7 ...      SETNCORE SIZELIST,3,FILE,FABSNB   
   8 4KKS ...      LDN   5  A1(3)
  10 4KLF ...      LDN   4  XJOBLIST(1)  
   9 4KM6 ...      MOVE  4  SIZELIST 
  11 4KM# ...      JBC      NOJOB1,2,OPENJL  
   7 4KMB ...      STEPAGAIN 
   9 4KMD ...      LDN   4  JLUSER(3)
   9 4KMG ...      BRN      NOJOB2   
   5 4KMJ ...NOJOB1  
   9 4KMR ...      CALL  7  SNAMES   
  10 4KN2 ...      LDN   4  ACOMMUNE4(2) 
   5 4KN9 ...NOJOB2  
  10 4KND ...      HUNT     3,FILE,FABSNB
   9 4KP5 ...      LDN   5  A1+1(3)  
   7 4KQC ...      MOVE  4  3
  14 4KQH ...      USEROPEN XBRK,GENERAL,UNTRAP,FROZEN   
  10 4KRF ...[ TEST FOR CLUDGE - B2032   
  10 4KRJ ...      TESTREP  OK,OPENUSEROK
  18 4KRM ...      TESTREP  CLUDGE,XNOJL      [ FILE ALREADY OPEN TO JOB 
   8 4KRQ ...      BRN      NONX 
   6 4KRT ...OPENUSEROK  
  11 4KS3 ...      MBS      2,WELL,OPENUSER  
  11 4KS6 ...      JBC      NOJOB3,2,OPENJL  
   8 4KS7 ...      STEPAGAIN 1   
  10 4KS8 ...      LDN   4  JLJOBNAME(3) 
   9 4KS9 ...      BRN      NOJOB4   
   5 4KS= ...NOJOB3  
   9 4KSB ...      CALL  7  SNAMES   
  10 4KT3 ...      LDN   4  ACOMMUNE1(2) 
   5 4KT* ...NOJOB4  
   9 4KTN ...      GETJOB   4,USER   
   9 4KW* ...      TESTRPN2 OK,NONX  
   7 4KWJ ...      STEPAGAIN 
  10 4KWR ...      LDX   4  JLJOBNO(3)   
   8 4KX2 ...      ADX   6  FX1  
   7 4KY# ...      EXIT  6  0
   4 4KYF ...[   
   4 4KYL ...[   
   9 4KYR ...SYS   CALL  5  VOPCLOS  
   8 4KYY ...      MONOUT   EXSYS
   8 4K_5 ...      BRN      THEND
   4 4K_= ...[   
   4 4K_G    #   
  16 4LF6    #     THE FOLLOWING IS THE ERROR REPORTING ROUTINE  
   4 4LYQ    #   
   4 4M#F ...NONX
  16 4MN8 ...      LDX   6  Y1(1)               [JOB NON EXISTENT
   8 4N3X ...      BRN      RCLOS
  16 4NCL    NLFN  LDX   6  Y2(1)               [NOT LFN FORMAT  
   8 4NX=          BRN      RERR 
  18 4PBW    NUNA  LDX   7  Y3(1)               [USERNAME PARAM MISSING  
   8 4PWG          BRN      RER1 
   5 4QB6    NJBNA   
  18 4QTQ          LDX   7  Y4(1)               [JOBNAME PARAM MISSING   
   8 4R*B    RER1  LDX   6  Y5(1)
   8 4RT2          BRN      RER2 
  15 4S#L    NJBNO FREECORE 3                   [FREE /CUNI  
   8 4SS=          BNZ   4  X3   
   9 4T?W ...      BS       2,NULL   
   9 4TRG ...      BRN      OTHERS   
   4 4_8W    X3  
   8 4_NG          LDX   6  Y5(1)
   8 5286          BRN      RERR 
   5 52MQ    NUMERR  
  16 537B          LDX   6  Y6(1)               [NO. FORMAT ERROR
   8 53M2          BRN      RERR 
   5 546L    NOTVAL  
   8 54L=          LDX   6  Y7(1)
   7 555W    RERR  NGN   7  1
   4 55KG    RER2
  15 5656          COMERRX  6,7                 [REPORT ERROR
   4 565M ...[   
   4 5668 ...[   
   4 566P ...[   
   8 5674 ...NOT2  CALL  7  SPARA
   6 5676 ...#UNS  ECS   
   8 5678 ...NOT3  LDX   6  Y9(1)
   8 567R ...      BRN      RCLOS
   4 568# ...[   
   5 56*C ...VNUMERR 
   8 56*D ...      LDX   6  Y6(1)
   8 56*F ...      BRN      RCLOS
   5 56*G ...UNPAIR  
   9 56*H ...      LDX   6  Y14(1)   
   8 56*J ...      BRN      RCLOS
   5 56*K ...XERANGE 
   9 56*L ...      LDX   6  Y15(1)   
   8 56*M ...      BRN      RCLOS
   9 56*T ...XNOJL LDX   6  Y21(1)   
   8 56*W ...      BRN      RCLOS
   4 56*X ...[   
   4 56*Y ...[   
   4 56*_ ...XK3 
   4 56B4 ...[   
   9 56B7 ...XBRK  CALL  5  VOPCLOS  
   4 56BH ...#   
   5 56BK ...XBRKIN  
   7 56BN ...      COMBRKIN  
   4 56C9 ...[   
   4 56CQ ...[   
   5 56D? ...WRONG   
   9 56DS ...      LDX   6  Y10(1)   
   5 56F* ...RCLOS   
   9 56FW ...      CALL  5  VOPCLOS  
   8 56GC ...      BRN      RERR 
   4 56GY ...[   
   4 56HF ...[   
   7 56JQ    Y1    +JXEXIST  
   7 574B    Y2    +JNLFNF   
   6 57J2    Y3    +JUSNA
   7 583L    Y4    +JJOBNA   
   7 58H=    Y5    +JPARMIS  
   7 592W    Y6    +CHNUMERR 
   7 59GG    Y7    +FWHSTAT  
   7 5=63 ...Y9    +APFERR   
   7 5=9Y ...Y10   +ASCOMBER 
   9 5=FB ...Y14            +ERUNPAIR
   9 5=FF ...Y15            +ERANGE  
   9 5=FN ...Y21            +JSYSFILE
   4 5=FQ    #   
  14 5=_B    #     THE FOLLOWING ARE MESSAGE IDENTIFIERS 
   4 5?F2    #   
   7 5#XW ...Z3    +ASCMESS  
   4 5*CG    #   
   4 5B#B ...#   
   4 5BBQ    #END
   8 ____ ...33556317000100000000
