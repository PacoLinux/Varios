  14 22FL ...      SEG   INCCHECK,850,MIKE PUTNAM,INCDUMP
   4 22_=    #   
  12 23DW          SEGENTRY K1INCCHECK,X1INCCHECK
  13 23NN ...      SEGENTRY  K2INCCHECK,X2INCCHECK   
   4 23YG    #   
  12 24D6    #  ENTERED DURING BACKMAP IN EMS.   
  21 24XQ    #  CHECKS THAT :SYSTEM.INCINDEX & :DUMPER ARE COMPATIBLE.  IF THEY ARE N
  11 25CB    #  TAKES ACTION AS APPROPRIATE. 
  16 25JJ ...#  BOTH X1 AND X2 ENTRIES ARE USED BUT AT DIFFERENT 
   7 25PQ ...#  STAGES OF EMS
   4 25X2    #   
   4 26BL    #   
  11 26W=    NAME           12HGEO3DUMPTAPE  
  11 27*W    MPROC          12HGEO3PROCTAPE  
  21 27TG    MBOT  ANDN  0  #7777               [THIS 'OBEYED' IF JUMP ADDRESS IN B12
  21 28*6    MTOP  SRL   0  12                  [THIS 'OBEYED' IF JUMP ADDRESS IN  B0
  21 28SQ    MREM           +INCREMOVE          [^ENTRY FOR MT %A REMOVED FROM RECORD
  17 29#B                                       [   FOR INCREMENT %B^
  21 29S2    MMTMARK        +INCMTMARK          [^ENTRY FOR MT %A IN RECORD FOR INCRE
  20 2=?L                                       [   %B MARKED 'NO TSN PRESENT'^  
  21 2=R=    MMISS          +INCMISS            [^ENTRY FOR MT %A PRESENT IN RECORD F
  21 2?=W                                       [   INCREMENT %B BUT NOT IN :DUMPER^ 
  21 2?QG    MMARK          +INCMARK            [^MT %A NOT MARKED 'USED BY DUMPER' S
  21 2#=6                                       [   CONTAINS INCREMENTS SUITABLE FOR 
  18 2#PQ                                       [           RETRIEVING^  
   4 2*9B    #   
   7 2*P2    # DECISION TABLE
   4 2B8L    #   
   8 2BN=    MDECIDE        +NC  
   8 2C7W    #FID  0     11 +NA  
   8 2CMG                   +NA  
   8 2D76    #FID  0     11 +NB  
   8 2DLQ                   +ND  
   8 2F6B    #FID  0     11 +NA  
   8 2FL2                   +NE  
   8 2G5L    #FID  0     11 +NA  
   4 2GK=    #   
   7 2H4W    #  SUBROUTINES  
   4 2HJG    #   
  21 2J46    #  JUST TO STEPWRITE TO FILE OPEN AT DEPTH 0.  STEPWRITE EXPANSION TOO  
  12 2JHQ    #  LARGE TO USE IT MORE THAN ONCE   
  19 2K3B    #  ON ENTRY, X3->RECORD BEING STEP-WRITTEN.    S/R LINK IN X6   
  21 2KH2    #  ON EXIT , X1=FX1, X2=FX2, X3->RECORD STEP-WRITTEN. COORS.  USES X0.  
   4 2L2L    #   
   6 2LG=    STEPWRITE   
   8 2L_W          SBX   6  FX1  
  21 2MFG          STEPWRITE                    [REWRITE RECORD IN FILE AT DEPTH 0   
   8 2M_6          ADX   6  FX1  
   7 2NDQ          EXIT  6  0
   4 2NYB    #   
  21 2PD2    #  ASSEMBLES THE PARAMETERS OF A MON. FILE MESSAGE OF WHICH TSN IS 1ST  
  21 2PXL    #  PARAMETER & INCR. NO. 2ND PARAMETER AND OUTPUTS A SPECIFIED MESSAGE. 
  21 2QC=    #  ON ENTRY,X0= UNIVERSAL IDENTIFIER OF MESSAGE, X3-> CURRENT RECORD IN 
  17 2QWW    #           INCINDEX, X7= TSN OF MT.       X6 IS LINK.  
  17 2RBG    #  ON EXIT ,X1=FX1, X2=FX2, X3-> CURRENT RECORD, X7=TSN.
  12 2RW6    #           COORS. USES X0, AWORK1. 
   4 2S*Q    #   
   4 2STB    MESS
   8 2T*2          SBX   6  FX1  
  16 2TSL          LDX   3  AINCNO-A1(3)        [X3= INCR. NO.   
   8 2W#=          SMO      FX2  
  21 2WRW          STO   0  AWORK1              [REMEMBER WHIFH MESSAGE (IN AWORK1)  
   7 2X?G          OUTBLOCN 6
  17 2XR6          OUTPACK  7,1,TSN             [ TSN AS PARAMETER   
  18 2Y=Q          OUTPACK  3,1,INCNUM          [ INC.NO. AS PARAMETER   
  19 2YQB          MONOUTX  AWORK1(2)           [OUTPUT APPROPRIATE MESSAGE  
  18 2_=2                                       [  TO CENTRAL OPS CONSOLE
  17 2_PL          STEPAGAIN                    [X3-> INCINDEX RECORD
   8 329=          ADX   6  FX1  
   7 32NW          EXIT  6  0
   4 338G    #   
   4 33N6    #   
  21 347Q    #  ---------------------------------------------------------------------
   4 34MB    #   
  20 3572    #     OPEN INCINDEX & :DUMPER IN GENERAL MODE & SET UP FINTER BLOCK.
  12 35LL    #     FORMAT OF FILE/FINTER BLOCK :-
  21 366=    #           2-WORD ENTRIES FOR EACH TAPE IN :DUMPER CALLED GEO3DUMPTAPE.
  11 36KW    #              1ST WORD :  TSN  
  18 375G    #              2ND WORD :  B0  TAPE MARKED 'USED' IN :DUMPER
  21 37K6    #                          B1  TAPE MENTIONED IN INCINDEX BUT NOT MARKED
  14 384Q    #                              'RELEASED'   
  21 38JB    #                          B2  TAPE TO BE MARKED AS 'UNUSED' - MARKED AS
  16 3942    #                              RELEASED IN INCINDEX 
  17 39HL    #                          B3  TAPE IS A GEO3DUMPTAPE   
  17 3=3=    #                          B4  TAPE IS A GEO3PROCTAPE   
  21 3=GW    #                          B5  INSERT 'USED BY DUMPER' BIT (GEO3DUMPTAPE
   4 3?2G    #   
   6 3?G6    X1INCCHECK  
  16 3?_Q          OPENINC  ,GENERAL            [OPEN INCINDEX   
  19 3#FB          DOWN  INCTAPES,2             [OPEN :DUMPER IN GENERAL MODE
  21 3#_2          SETNCORE 0,2,FILE,FINTER     [FINTER BLOCK READY FOR MT WORD PAIRS
  18 3*DL                                       [       FOR GEO3DUMPTAPES
   4 3*Y=    #   
  21 3BCW    #     GOES THROUGH :DUMPER PICKING OUT ALL TAPES CALLED GEO3DUMPTAPE & S
  21 3BXG    #     UP AN ENTRY FOR THEM IN FILE/FINTER BLOCK . SETS B0 OF 2ND WORD IN
  14 3CC6    #     IF TAPE MARKED AS USED BY THE DUMPER  
   4 3CWQ    #   
   5 3DBB    PSTEP   
  19 3DW2          STEP                         [X3-> NEXT RECORD IN :DUMPER 
  17 3F*L          BZE   3  PEND                [J IF END OF :DUMPER 
  10 3FT=          LDX   0  ERES-A1(3)   
  17 3G#W          BNZ   0  PSTEP               [J IF NOT NAME RECORD
  17 3GSG          LDX   4  ESER-A1(3)          [X4= SERIAL NO. IF MT
  16 3H#6          BZE   4  PSTEP               [J IF NOT A MT   
  15 3HRQ          TESTNAMX 3,NAME(1),ELOC1-A1(3),PNOTDUMP,2 
  18 3J?B                                       [J IF NOT GEO3DUMPTAPE   
  18 3JR2          LDCT  5  #040               [SET GEO3DUMPTAPE MARKER  
   4 3K=L    POK 
  21 3KQ=          ORX   5  EINCLOOK-A1(3)      [SET ^BEING USED BY DUMPER^ BIT IF SE
  10 3L9W          MHUNTW   3,FILE,FINTER
  10 3LPG          LDX   6  ALOGLEN(3)   
   7 3M96          ADN   6  2
  19 3MNQ          ALTLEN   3,6,FILE,FINTER     [LENGTHEN FINTER BY 2 WORDS  
  10 3N8B          MHUNTW   3,FILE,FINTER
   7 3NN2          SMO      6
  21 3P7L          STO   4  A1-2(3)             [INSERT TSN IN 1ST WORD OF PAIR IN FI
   7 3PM=          SMO      6
  21 3Q6W          STO   5  A1-1(3)             [INSERT STATE OF TAPE INTO 2ND WORD  
   9 3QLG    #SKI  K6INCCHECK>199-199
   4 3R66    (   
  17 3RKQ          TRACE 4,INCSERDU             [TSN OF GEO3DUMPTAPE 
  21 3S5B          TRACE 5,INCSERST             [STATE OF TAPE (B0 IF USED BY DUMPER)
   4 3SK2    )   
   8 3T4L          BRN      PSTEP
   5 3TJ=    PNOTDUMP
  15 3W3W          TESTNAMX 3,MPROC(1),ELOC1-A1(3),PSTEP,2   
  19 3WHG                                       [J IF NOT GEO3PROCTAPE EITHER
  16 3X36          LDCT  5  #020               [SET GEO3PROCTAPE 
   8 3XGQ          BRN      POK  
   4 3Y2B    PEND
   4 3Y2T ...#   
  18 3Y3# ...#  NOW WE OPEN :SYSTEM.SERIAL AND SET UP THE MTWRONG BITS   
  14 3Y3R ...#  IN THE FINTER STATE WORD FOR EACH TAPE   
   4 3Y4= ...#   
  10 3Y4P ...      OPENSYS  ,SERIAL,READ 
   5 3Y58 ...SSTEP   
  18 3Y5M ...      STEP                         [X3->NEXT SERIAL RECORD  
  14 3Y66 ...      BZE   3  SEND                [J IF EOF
  13 3Y6K ...      TESTNAMX 3,MDUMP(1),2(3),SSTEP,2  
  17 3Y74 ...                                   [J IF NOT DUMPER TAPE
  15 3Y7H ...      LDX   4  1(3)                [TSN OF TAPE 
  10 3Y82 ...      MHUNTW   2,FILE,FINTER
  10 3Y8F ...      LDX   6  ALOGLEN(2)   
  18 3Y8Y ...      SRL   6  1                   [X6=NO OF FINTER ENTRIES 
  15 3Y9C ...      BZE   6  SSTEP               [J IF NONE   
   5 3Y9W ...SNEXT   
  19 3Y=* ...      BXE   4  A1(2),SFOU          [J IF TAPE FOUND IN FINTER   
  20 3Y=S ...      ADN   2  2                   [A1(2) IS NEXT TAPE TSN IN FINTER
  18 3Y?? ...      BCT   6  SNEXT               [J IF MORE TSNS IN FINTER
  17 3Y?Q ...      BRN      SSTEP               [TAPE NOT IN FINTER  
   4 3Y#9 ...SFOU
   8 3Y#N ...      LDCT  0  #030 
  17 3Y*7 ...      ANDX  0  5(3)                [EXTRACT WRONGED BITS
  16 3Y*L ...      BZE   0  SSTEP               [J IF NOT WRONGED
  18 3YB5 ...      SRL   0  18                [GET WRONGED BITS IN B22-23
  19 3YBJ ...      ORS   0  A1+1(2)             [KEEP IN FINTER STATE WORD   
  15 3YC3 ...                                   [STATE WORD  
   8 3YCG ...      BRN      SSTEP
   9 3YC_ ...MDUMP          12HDUMPER
   4 3YDD ...SEND
  15 3YDX ...      CLOSETOP                     [CLOSE SERIAL
   4 3YG2    #   
  20 3Y_L    #  MOVE INCINDEX TO DEPTH 0 SO CAN STEPWRITE AT LEVEL 0 WHEN WANT TO
   9 3_F=    #  CHANGE RECORD IN IT. 
  21 3_YW          FILEMOVE 0,1                 [MOVE INCINDEX TO DEPTH 0 , :DUMPER D
  21 42DG          STOZ     AWORK2(2)           [ZEROISE ^INC. NO. IN PREVIOUS RECORD
   4 42Y6    #   
   4 43CQ    #   
  21 43XB    #  ---------------------------------------------------------------------
   4 44C2    #   
  21 44WL    #  NOW GO THROUGH INCINDEX COMPARING THE TAPES MENTIONED & THEIR STATE W
  11 45B=    #  TAPES IN FILE/FINTER BLOCK.  
  21 45TW    #  ACTION IS TAKEN ACCORDING TO THE FOLLOWING DECISION TABLE (ADDRESSES 
  10 46*G    #  BRANCH TO ARE SHOWN):-   
   4 46T6    #   
  21 47#Q    #     ----- :DUMPER    _  TAPE USED      TAPE NOT USED     TAPE ABSENT  
   9 47SB    #          ----        _
   9 48#2    #     INCINDEX --------_
  21 48RL    #  --------------------_------------------------------------------------
  20 49?=    #     IN OBSOLETE      _     NA                NC              NC   
   9 49QW    #     INCREMENT RECORD _
   9 4==G    #                      _
   9 4=Q6    #     IN OBSOLETE      _
  20 4?9Q    #     INCR. REC. &     _     NB                NA              NA   
   9 4?PB    #     MARKED 'RELEASED'_
   9 4#92    #                      _
   9 4#NL    #     IN NON-OBS. INCR._
  20 4*8=    #     SUITABLE FOR     _     NA                NE              NE   
   9 4*MW    #     RETRIEVING       _
   9 4B7G    #                      _
   9 4BM6    #     IN NON-OBS. INCR._
  20 4C6Q    #     NOT SUITABLE FOR _     NA                ND              ND   
   9 4CLB    #     RETRIEVING       _
   9 4D62    #                      _
   4 4DKL    #   
   5 4F5=    QSTEP   
  10 4FJW    #  READ NEXT INCINDEX RECORD
  19 4G4G          STEP                         [X3-> NEXT RECORD IN INCINDEX
  17 4GJ6          BZE   3  QEND                [J IF END OF INCINDEX
   9 4H3Q          LDEX  0  FRH(3)   
   9 4HHB          SBN   0  AMAG-A1  
  21 4J32          BNG   0  QDEL                [DELETE RECORD IF TOO SHORT & PUT OUT
  21 4JGL                                       [   MESSAGE (INCR. NO. COULD BE GARBL
   9 4K2=          LDX   0  AWORK2(2)
  20 4KFW          BXGE  0  AINCNO-A1(3),QWRONGORD [J IF INC. NOS. IN WRONG ORDER
  10 4K_G          LDX   0  AINCNO-A1(3) 
  19 4LF6          STO   0  AWORK2(2)           [REMEMBER CURRENT INCR. NO.  
  21 4LYQ          LDX   4  AMTS-A1(3)          [X4= NO. OF TAPE ENTRIES IN RECORD   
  17 4MDB          LDN   5  AMAG-A1             [X5= MOD. DOWN RECORD
   9 4MY2    #SKI  K6INCCHECK>199-199
   4 4NCL    (   
  16 4NX=          TRACE AINCNO-A1(3),INC INC   [INCREMENT NO.   
  17 4PBW          TRACE AINCSTATE-A1(3),INCSTATE [STATE OF INCREMENT
   4 4PWG    )   
   6 4QB6    QNEXTTAPE   
  21 4QTQ    #  PICK UP THE TSN & STATE WORD FROM NEXT TAPE ENTRY IN INCINDEX RECORD 
  21 4R*B          BZE   4  QCHECK              [J IF NO MORE MT ENTRIES IN THIS RECO
  18 4RT2          SBN   4  1                   [REDUCE COUNT OF TAPES   
   7 4S#L          SMO      5
  18 4SS=          LDX   7  0(3)                [X7= TSN OF TAPE ENTRY   
  17 4T?W          BPZ   7  REALTAPE            [J IF NOT DUMMY ENTRY
  19 4TRG          JBC   QNTAPE,3,BINSUITRVR    [REMOVE ENTRY UNLESS NSUITRV 
  17 4W?6          BRN      QMOD                [IF NSUITRV,IGNORE IT
   5 4WQQ    REALTAPE
   7 4X=B          SMO      5
  18 4XQ2          LDX   2  AMAGSTATE-AMAG(3)   [STATE WORD OF MT ENTRY  
  16 4_8W          TRACE 7,INC TSN              [TSN OF MT ENTRY 
  18 4_NG          TRACE 2,INCMTSTA             [STATE WORD OF MT ENTRY  
   4 52MQ    #   
  21 537B    #  NOW SEE IF THE TAPE IN X7 IS MENTIONED IN FILE/FINTER BLOCK (I.E. IN 
   6 53M2    #  :DUMPER).
  21 546L    #  IF IT IS PRESENT IN :DUMPER & MARKED 'USED', X2 IS SET TO THE ADDRESS
  21 54L=    #           READY TO PICK UP THE ADDRESS IN THE TOP HALF OF THE REQUIRED
  12 555W    #           IN THE DECISION TABLE.  
  20 55KG    #  IF IT IS NOT PRESENT OR IS NOT MARKED 'USED', X2 IS SET TO MBOT  
  21 5656    #           READY TO PICK UP THE ADDRESS IN THE BOTTOM HALF OF THE REQUI
  21 56JQ    #  ALSO SETS B1 (TAPE PRESENT IN INCINDEX & NOT IN THAT INCR. RECORD MAR
  21 574B    #                 'RELEASED') OF STATE WORD FOR TAPE IN FILE/FINTER BLOC
  11 57J2    #                 APPROPRIATE.  
  10 583L          MHUNTW   1,FILE,FINTER
  10 58H=          LDX   6  ALOGLEN(1)   
   5 592W    QNEXTTSN
  18 59GG          BZE   6  QNOTUSED            [J IF TAPE NOT IN :DUMPER
   7 5=26          SBN   6  2
   7 5=FQ          SMO      6
  20 5=_B          BXU   7  A1(1),QNEXTTSN      [J IF ENTRY FOR TSN NOT YET FOUND
   8 5?24 ...      SMO      FX2  
  17 5?2Q ...      STO   4  AWORK4             [SAVE X4 OVER REUSE   
   7 5?3D ...      SMO      6
  17 5?46 ...      LDEX  4  A1+1(1)             [PICK UP WRONGED BITS
   8 5?4S ...      LDCT   0  #060
  21 5?5G ...      ANDX   0  2                   [EXTRACT BITS FROM AMAGSTATE WORD   
   8 5?68 ...      SRL   0  19   
  16 5?6W ...      BXE   0   4,WREQ             [J IF EQUIVALENT 
   9 5?7J ...      CALL   6 STEPWRITE
   8 5?8= ...      SLL   4  19   
   8 5?8Y ...      LDCT   0  #060
  17 5?9L ...      ERX   0  GMINUS1             [MASK OUT BITS IN REC
   7 5?=# ...      SMO      5
  11 5??2 ...      ANDS  0  AMAGSTATE-AMAG(3)
   7 5??N ...      SMO      5
  17 5?#B ...      ORS   4  AMAGSTATE-AMAG(3)   [MAKE THEM EQUIVALENT
  16 5?*4 ...      LDX   4  AWORK4(2)           [RESTORE SAVED X4
  20 5?*Q ...      BRN      REALTAPE            [LOOP BACK TO REFIND FINTER ENTRY
   4 5?BD ...WREQ
   8 5?C6 ...      SMO      FX2  
  18 5?CS ...      LDX   4  AWORK4                   [RESTORE SAVED X4   
   7 5?F2          SLC   2  2
  17 5?YL          BNG   2  QREL                [J IF TAPE RELEASED  
   8 5#D=          LDCT  0  #200 
   7 5#XW          SMO      6
  21 5*CG          ORS   0  A1+1(1)             [SET ^MENTIONED IN INCINDEX^ BIT IN T
   4 5*X6    QREL
   7 5BBQ          SMO      6
  19 5BWB          LDX   0  A1+1(1)             [X0= STATE OF TSN IN :DUMPER 
  21 5CB2          LDN   2  MTOP                [OPTIONALLY SET TOP HALF OF DEC. WORD
  16 5CTL          BNG   0  QUSED               [J IF TAPE USED  
   7 5D*=          SLL   0  4
  21 5DSW          BNG   0  QUSED               [J IF PROC TAPE (ALWAYS TREAT AS ^USE
  20 5F#G                                       [   BY DUMPER^ FOR DECISION TABLE
   5 5FS6    QNOTUSED
  21 5G?Q          LDN   2  MBOT                [NEED BOTTOM HALF OF DECISION TABLE W
   5 5GRB    QUSED   
  21 5H?2    #  DECIDE WHICH WORD IN DECISION TABLE IS NEEDED & BRANCH TO THE ADDRESS
  11 5HQL    #  THE APPROPRIATE HALF OF IT.  
  21 5J==          LDN   1  0                   [CLEAR MODIFIER DOWN DECISION TABLE  
  11 5JPW          LDX   0  AINCSTATE-A1(3)  
   7 5K9G          SLC   0  4
   7 5KP6          SMO      5
  11 5L8Q          LDX   6  AMAGSTATE-AMAG(3)
   7 5LNB          SRC   6  7
  20 5M82          BNG   6  QMTOBS              [J IF TAPE IS MARKED AS OBSOLETE 
  18 5MML          BPZ   0  QNOTOBS             [J IF INCR. NOT OBSOLETE 
   5 5N7=    QMTOBS  
   7 5NLW          SLC   6  9
  21 5P6G          BPZ   6  QDEC                [J IF TAPE NOT MARKED ^RELEASED^ IN I
  21 5PL6                                       [          (WORD 0 OF DECISION TABLE)
  16 5Q5Q          ADN   1  1                   [WORD 1 OF TABLE 
   8 5QKB          BRN      QDEC 
   5 5R52    QNOTOBS 
   7 5RJL          ADN   1  2
   7 5S4=          SLC   0  1
  20 5SHW          BNG   0  QDEC                [J IF NOT SUITABLE FOR RETRIEVING
  21 5T3G                                       [       (WORD 2 OF DECISION TABLE)   
  21 5TH6          ADN   1  1                   [WORD 3 OF DECISION TABLE FOR RELIABL
   4 5W2Q    QDEC
   8 5WGB          SMO      FX1  
  21 5X22          LDX   0  MDECIDE(1)          [X0= REQUIRED WORD IN DECISION TABLE 
   8 5XFL          SMO      FX1  
  18 5X_=          OBEY     0(2)                [X0= ADDRESS TO BRANCH TO
   8 5YDW          ADX   0  FX1  
  19 5YYG          EXIT  0  0                   [GO TO ACT IN REQUIRED WAY   
   4 5_D6    #   
  21 5_XQ    #  HAVING DEALT WITH RECORD AS NECESSARY, IF ALL TAPES HAVE NOW BEEN REM
  16 62CB    #  FROM THE RECORD, DELETE IT & OUTPUT A MESSAGE.   
   4 62X2    #   
   5 63BL    QCHECK  
  19 63W=          LDX   4  AMTS-A1(3)          [X4= NO. OF TAPES IN RECORD  
  21 64*W          BNZ   4  QSTEP               [J IF TAPES STILL LEFT IN THIS RECORD
   4 64TG    QDEL
  12 65*6 ...      OUTPACK AINCNOR(3),1,INCNUM   
  10 65SQ ...      MONOUT   INCDELETED   
   6 66#B ...      DELETE
   8 67?L          BRN      QSTEP
   6 67R=    QWRONGORD   
  20 68=W          GEOERR   BRIEFPM,INCWRORD    [INCINDEX RECORDS IN WRONG ORDER 
   4 68QG    #   
   4 69=6    #   
   4 69PQ    #   
  15 6=9B    NA                                 [TAPE IS OK  
   9 6=P2    #SKI  K6INCCHECK>199-199
  19 6?8L          TRACE 7,INCTSNOK             [TAPE IS OK - NOTHING TO DO  
   4 6?N=    QMOD
  21 6#7W          ADN   5  INCMAGLEN           [UPDATE MOD. DOWN MT LIST IN RECORD  
   9 6#MG          BRN      QNEXTTAPE
   4 6*76    #   
   4 6*LQ    NB  
  21 6B6B    #  TAPE NO LONGER TO BE MARKED 'USED' IN :DUMPER UNLESS IT HAS BEEN USED
  14 6BL2    #  AGAIN IN ANOTHER NON-OBSOLETE INCREMENT. 
   4 6C5L    #   
  17 6CK=    #  SETS B2 OF STATE WORD FOR TAPE IN FILE/FINTER BLOCK, 
  18 6D4W    #     HAVING FOUND TAPE IS MARKED 'RELEASED' IN INCINDEX.   
   9 6DJG    #SKI  K6INCCHECK>199-199
  21 6F46          TRACE 7,INCNUSED             [TAPE TO BE MARKED NOT ^USED^ IN :DUM
  10 6FHQ          MHUNTW   1,FILE,FINTER
  10 6G3B          LDX   6  ALOGLEN(1)   
   5 6GH2    NBTSN   
   7 6H2L    #SKI  K6INCCHECK
  21 6HG=          BZE   6  NBGEOERR            [J IF TSN ENTRY NOT IN FINTER BLOCK  
  21 6H_W                                       [       (TSN PREVIOUSLY FOUND IN :DUM
  20 6JFG          SBN   6  2                   [DECREASE MOD. IN FINTER BLOCK   
   7 6J_6          SMO      6
  18 6KDQ          BXU   7  A1(1),NBTSN         [CONTINUE TO LOOK FOR TSN
   8 6KYB          LDCT  0  #100 
   7 6LD2          SMO      6
  21 6LXL          ORS   0  A1+1(1)             [SET ^RELEASED IN INCINDEX^ BIT IN ST
  21 6MC=                                       [       WORD FOR TSN IN FINTER BLOCK 
   8 6MWW          BRN      QMOD 
   5 6NBG    NBGEOERR
   7 6NW6    #SKI  K6INCCHECK
  19 6P*Q          GEOERR   BRIEFPM,INCNOTSN    [TSN ABSENT FROM FINTER BLOCK
   4 6PTB    #   
   4 6Q*2    NC  
  16 6QSL    #  REMOVE ENTRY FOR THIS MT FROM INCREMENT RECORD   
   4 6R#=    #   
  17 6T=Q    QNTAPE                             [ENTRY TO DELETE TAPE
  16 6TQB                                       [WITHOUT MESSAGE 
  18 6W=2          READAGAIN                    [READ RECORD FOR REPLACE 
  10 6WPL          MHUNTW   3,FILE,FRB   
  10 6X9=          NAME     3,FILE,FWB   
  17 6XNW          LDX   6  A1+FRH(3)           [X6= LENGTH OF RECORD
   9 6Y8G          SBN   6  INCMAGLEN
  21 6YN6          STO   6  A1+FRH(3)           [REDUCE RECORD HEADER BY INCMAGLEN   
   7 6_7Q          LDN   0  1
  18 6_MB          SBS   0  AMTS(3)             [REDUCE NO. OF TAPES BY 1
   8 7272          LDN   2  A1(3)
  20 72LL          ADX   2  5                   [X2-> ELEMENT IN FWB TO REMOVE   
  10 736=          LDN   1  INCMAGLEN(2) 
  17 73KW          SBX   6  5                   [X6= LENGTH OF MOVE  
  15 745G          BZE   6  ZEROMOVE            [J IF ZERO   
   7 74K6          SMO      6
  19 754Q          MOVE  1  0                   [OVERWRITE UNWANTED ELEMENT  
   5 75JB    ZEROMOVE
  19 7642          REPLACE                      [SHORTEN RECORD IN INCINDEX  
  17 76HL          STEPAGAIN                    [X3-> CURRENT RECORD 
  21 773=          BRN      QNEXTTAPE           [NO NEED TO ADD ON INCMAGLEN SINCE MO
  18 77GW                                       [   EVERYTHING BELOW UP  
   4 782G    #   
   4 78G6    ND  
  21 78_Q    #  SET B0 OF TSN FOR THIS TAPE IN THIS RECORD OF INCINDEX - DUMPER WILL 
  13 79FB ...#  LOOK FOR ANOTHER TAPE IN ITS PLACE.  
  20 7=DL          CALL  6  STEPWRITE           [ENSURE INFORMATION WRITTEN TO BS
   8 7=Y=          LDCT  0  #400 
   7 7?CW          SMO      5
  20 7?XG          ORS   0  0(3)                [SET B0 OF TSN WORD IN INCINDEX  
   8 7C#W          BRN      QMOD 
   4 7CSG    #   
   4 7D#6    NE  
  21 7DRQ    #  TO DEAL WITH THIS WOULD BE QUITE INVOLVED & SHOULDN'T HAPPEN UNDER NO
  21 7F?B    #  RUNNING & FUNCTIONING CONDITIONS.  IT COULD HAPPEN HOWEVER THROUGH   
  11 7FR2    #  MALFUNCTIONING OF THE SYSTEM.
   4 7G=L    #   
  17 7GQ=          MHUNTW   1,FILE,FINTER       [X1-> FINTER BLOCK   
  10 7H9W          LDX   6  ALOGLEN(1)   
   5 7HPG    NETSN   
  19 7J96          BZE   6  NEX                 [J IF NO MORE TSNS TO LOOK AT
   7 7JNQ          SBN   6  2
   7 7K8B          SMO      6
  19 7KN2          BXU   7  A1(1),NETSN        [J IF NOT FOUND REQUIRED MT   
   8 7L7L          LDCT  0  #010 
   7 7LM=          SMO      6
  21 7M6W          ORS   0  A1+1(1)             [SET  ^USED BY DUMPER^ MARKER NEEDS S
   9 7MLG    #SKI  K6INCCHECK>99-99  
  21 7N66          TRACE 7,INCNOTUS             [TAPE NOT MARKED ^USED BY DUMPER^ EVE
  21 7NKQ                                       [ THOUGH CONTAIN RETRIEVABLE INCRS.  
   8 7P5B          BRN      QMOD 
   4 7PK2    NEX 
   9 7Q4L    #SKI  K6INCCHECK>99-99  
  21 7QJ=          TRACE 7,INCNOTPR             [TAPE NOT PRESENT IN :DUMPER EVEN THO
  19 7R3W                                      [ CONTAINS RETRIEVABLE INCRS. 
   8 7RHG          BRN      QMOD 
   4 7S36    #   
   4 7SGQ    QEND
  16 7T2B          CLOSETOP                     [CLOSE INCINDEX  
   4 7TG2    #   
   4 7T_L    #   
  21 7WF=    #  ---------------------------------------------------------------------
   4 7WYW    #   
  20 7XDG ...#  NOW GO THROUGH FILE/FINTER BLOCK & ALTER MT ENTRIES IN :DUMPER.  
  20 7XY6 ...#     REMOVES 'USED' BIT (B0 OF EINCLOOK) OF DIR. ENTRY FOR TAPE IF 
  21 7_C2    #          1) MARKED 'RELEASED' IN INCINDEX & NOT MARKED ELSEWHERE IN IN
  19 7_WL    #             AS NOT 'RELEASED', BUT MARKED 'USED' IN :DUMPER.  
  21 82B=    #          2) MARKED USED IN :DUMPER, BUT NOT MENTIONED IN INCINDEX (EXC
  13 82TW    #             PERHAPS AS 'RELEASED').   
   4 83*G    #   
  10 83T6          MHUNTW   1,FILE,FINTER
  10 84#Q          LDX   5  ALOGLEN(1)   
   5 84SB    RNEXTTSN
  21 85#2          BZE   5  REND                [J IF ALL TSNS IN :DUMPER LOOKED AT  
   7 85RL          SBN   5  2
   7 86?=          SMO      5
  17 86QW          LDX   0  A1+1(1)             [X0= STATE OF TSNS   
   8 86WR ...      LDCT  4  #010 
  20 872N ...       ANDX  4  0                   [J IF 'USED BY DUMPER' MARKER   
  16 876K ...       BNZ   4  XSET                [NEEDS SETTING  
   7 87=G          SLC   0  1
  19 87Q6          BNG   0  RNEXTTSN            [J IF MENTIONED IN INCINDEX  
   7 889Q          SLC   0  1
  19 88PB          BNG   0  RREM                [J IF MARKED AS 'RELEASED'   
   7 8992          SRC   0  2
  16 89NL          BPZ   0  RNEXTTSN            [J IF NOT USED   
   4 89YD ...XSET
   7 8=8=          SMO      5
  20 8=MW          LDX   7  A1(1)               [X7= TSN TO BE MARKED AS NOT USED
  16 8CJ6          REWIND                       [REWIND :DUMPER  
   5 8D3Q    RSTEP   
  19 8DHB          STEP                         [X3-> NEXT RECORD IN :DUMPER 
   7 8F32    #SKI  K6INCCHECK
   4 8FGL    (   
  15 8G2=          BNZ   3  RNOTEND             [J IF NOT END
   5 8GFW    RGEOERR 
  21 8G_G          GEOERR   BRIEFPM,INC NOMT    [TAPE TO BE MARKED 'UNUSED' EITHER NO
  21 8HF6                                       [   IN :DUMPER OR MARKED 'UNUSED' ALR
   5 8HYQ    RNOTEND 
   4 8JDB    )   
  10 8JY2          LDX   0  ERES-A1(3)   
  17 8KCL          BNZ   0  RSTEP               [J IF NOT NAME RECORD
  17 8KX=          BXU   7  ESER-A1(3),RSTEP    [J IF NOT REQUIRED MT
  19 8LBW          CALL  6  STEPWRITE           [ENSURE RECORD WRITTEN TO BS 
  20 8LLN ...      BNZ   4  SETUSE             [J TO SET 'USED BY DUMPER' BIT.   
   7 8LWG    #SKI  K6INCCHECK
   4 8MB6    (   
  11 8MTQ          LDX   0  EINCLOOK-A1(3)   
  19 8N*B          BPZ   0  RGEOERR             [J IF NO 'USED' BIT PRESENT  
   4 8NT2    )   
   5 8P4S ...SETUSE  
   8 8P#L          LDX   0  GSIGN
  17 8PS=          ERS   0  EINCLOOK-A1(3)      [REMOVE 'USED' BIT   
  10 8Q?W          MHUNTW   1,FILE,FINTER
   9 8QRG          BRN      RNEXTTSN 
   4 8R?6    REND
  16 8RQQ          FREECORE 1                   [FREE FILE/FINTER
  16 8S=B          CLOSETOP                     [CLOSE :DUMPER   
   5 8SQ2          UP
   4 8T9L    RREM
   7 8TP=          SLC   0  9
  18 8W8W          BNG   0  RNEXTTSN            [J IF CALLED GEO3PROCTAPE
   9 8W8_ ...      BRN      (GEOERR) 
   4 8W94 ...#   
   4 8W97 ...#   
  20 8W9= ...#  THE X2 ENTRY CARRIES OUT ANY UPDATING NECESSARY TO INCINDEX AS   
  16 8W9* ...#  AS A RESULT OF A GENRES OR BREAK WHILE DUMPING.  
   4 8W9D ...#   
  19 8W9F ...THIRTYTHOU     30000               [VALUE PUT IN SIZE FIELDS FOR
  20 8W9G ...                                   [INCREMENTS WHICH WE CANNOT FULLY
  14 8W9H ...                                   [UPDATE  
   6 8W9J ...X2INCCHECK  
  15 8W9L ...      OPENINC  ,GENERAL            [OPENINCINDEX
  19 8W9P ...      STOZ     AWORK4(2)           [ZERO 1ST COMPLETE INCR NO   
   5 8W9S ...RSTEP2  
   6 8W9X ...      STEP  
  18 8W=2 ...      BZE   3  REND2                [J IF END OF INCINDEX   
  16 8W=5 ...      JBS   RSTEP2,3,BIOBSR         [J IF OBSOLETE  
   8 8W=8 ...      LDN   0  #17  
  17 8W=? ...      ANDX  0  AINCSTATER(3)       [EXTRACT FORMAT NO   
   7 8W=B ...      SBN   0  4
  18 8W=F ...      BPZ   0  NRED                [J IF NO NEED TO REDUMP  
  18 8W=J ...      CALL  6  STEPWRITE           [ENSURE RECORD REWRITTEN 
  18 8W=M ...      MBS   3,BIREDUMPR,BIDUMPROCR  [MARK AS TO BE REDUMPED 
   4 8W=Q ...NRED
   8 8W=T ...      LDN   6  #4000
  20 8W=Y ...      ANDX  6  AINCSTATER(3)       [X6=0     IF INCR. WAS WRONGED   
  20 8W?3 ...                                   [X6=#4000 IF INCR. WAS AVAILABLE 
  21 8W?6 ...      LDN   5  #4000               [SET OPTIONALLY ^INCR. IS NOW AVAILAB
  20 8W?9 ...      LDX   4  AMTSR(3)            [X4= NO. OF MTS IN THIS RECORD   
  15 8W?# ...      BZE   4  RVERIF              [J IF NONE   
  15 8W?C ...      LDX   1  3                   [X1->RECORD  
   5 8W?G ...RNEXT   
  18 8W?K ...      JMBAC RVERIF,1,BMOPWRR,BMUSWRR [J IF TAPE NOT WRONGED 
  17 8W?N ...      ADN   1  INCMAGLEN           [UPDATE TAPE MODIFIER
  21 8W?R ...      BCT   4  RNEXT               [J IF MORE MT STATE WORDS TO LOOK AT 
  20 8W?W ...      BS    ,BWRINC                [SET WRONGED INCRS PRESENT BIT   
  19 8W?_ ...      LDN   5  0                   [SET ^INCR. IS NOW WRONGED^  
   5 8W#4 ...RVERIF  
  18 8W#7 ...      ERX   6  5                   [DO WE NEED TO ALTER BIT 
  18 8W#= ...      BZE   6  NFLIP               [BIAVEST.  J IF WE DONT  
   9 8W#* ...      CALL  6  STEPWRITE
  19 8W#D ...      BI    3,BIAVESTR             [INVERT STATE OF AVAILIBILITY
  20 8W#H ...                                   [VERIFIED BIT SO IT IS CORRECT   
  19 8W#L ...                                   [CLEAR -WRONGED,SET-OTHERWISE
   5 8W#P ...NFLIP   
  17 8W#S ...      JBC      NNRV,3,BINSUITRVR [J IF COMPLETED INCR   
  20 8W#X ...      LDX   0  GINCTSN             [NONZERO IF JUST DONE A GENRES   
  15 8W*2 ...      BZE   0  NRESTORE            [J IF HAVENT 
  15 8W*5 ...      LDX   5  AINCNOR(3)          [NO OF INCR  
  20 8W*8 ...      MBSXD    6,BINCGENRES,BINCENDBAT [STATE WORD FOR GENRES INCR  
  21 8W*? ...      BXE   5  JINCLAST,YGRINC     [IF ITS NOT THE INC WE RESTORED FROM 
  20 8W*B ...      BSXD     6,BINCENDBAT       [COULD BE THE PARALLEL FILE INCR  
  18 8W*F ...                                   [SO SET UP X6 ACCORDINGLY
  19 8W*J ...      JBC      NUNFINGR,3,BIUNFINR [J IF NOT AN INCR PRODUCED IN
  21 8W*M ...                                   [PARALLEL WITH THE INC JUST RESTORED 
   5 8W*Q ...YGRINC  
  18 8W*T ...      CALL  4  SETTAB              [KEEP A TAB ON FILE POSN 
  21 8W*Y ...      DOWN     INCEND,3            [CARRY OUT UPDATING RELEVANT TO EMS  
   9 8WB3 ...      CALL  4  UNSETTAB 
  15 8WB6 ...      BRN      NNRV                [LOOP ROUND  
   3 8WB9 ...
   6 8WB= ...NOTSTARTED  
   5 8WB# ...NUNFINGR
  20 8WBC ...      DELETE                       [SCRUB INCS BEING REDUMPED COS OF
  19 8WBG ...                                   [BREAK AS THEY WILL HAVE BEEN
  20 8WBK ...                                   [REDUMPED AS PART OF RESORED INC 
   9 8WBN ...      BRN      RSTEP2   
   3 8WBR ...
   4 8WBW ...NNRV
  18 8WB_ ...      BWNZ     AWORK4(2),RSTEP2     [J IF HAVE ALREADY FOUND
   7 8WC2 ...      STEPAGAIN 
  19 8WC4 ...      LDX   0  AINCNOR(3)          [1ST RVABLE INCR IN INCINDEX 
  17 8WC7 ...      STO   0  AWORK4(2)           [OTHERWISE THIS IS IT
   9 8WC= ...      BRN      RSTEP2   
   3 8WC* ...
   5 8WCD ...NRESTORE
  19 8WCH ...      JBC      NUNFINNGR,3,BIUNFINR[J IF INCR NOT IN PROGRESS   
  16 8WCL ...                                   [AT TIME OF BREAK
  21 8WCM ...      BWZ      AINCDR(3),NOTSTARTED[THIS WORD CLEAR FOR UNFINISHED INC  
  21 8WCN ...                                   [IMPLIES INCR NEVER STARTED, SO WE   
  18 8WCP ...                                   [JUST DELETE THE RECORD. 
  21 8WCQ ...                                   [THE OTHER INCCHECK SCAN WILL CLEAR  
  21 8WCR ...                                   [ANY 'TAPE IN USE' BITS IN :DUMPER   
  20 8WCS ...                                   [FOR TAPES NEWLY OBTAINED FOR THE
  15 8WCT ...                                   [ABORTED INC.
   9 8WCW ...      CALL  6  STEPWRITE
   7 8WCX ...#UNS  FPARTRES  
   4 8WCY ...#SKI
  15 8WC_ ...      BC       3,BIUNFINR          [TAKE OUT BIT
   7 8WD4 ...#UNS  FPARTRES  
   4 8WD# ...(   
  20 8WF# ...      LDX   0  THIRTYTHOU(1)       [SET GUESS VALUE IN SIZE FIELDS  
  10 8WFJ ...      STO   0  AINCSIZER(3) 
  10 8WFS ...      STO   0  AINCFILESR(3)
  20 8WFX ...      LDX   5  AINCNOR(3)          [SET UP INCNO AND STATE WORD FOR 
  20 8WG2 ...      MBSXD    6,BINCEOT,BINCWRFAIL [A WRITE-FAILED INCR AND JUMP   
  21 8WG5 ...      BRN      YGRINC              [TO COMPLETE THE RECORD'S UPDATING   
  15 8WG8 ...                                   [ACCORDINGLY.
   4 8WG# ...)   
   6 8WGJ ...NUNFINNGR   
  18 8WGS ...#  WE USE TO CARRY OUT A COMPLICATED ACTION ON NSUITRV INCRS
  21 8WH4 ...#  BUT IN MARK 860 THESE MUST EITHER HAVE BEEN DEALT WITH BY A PREVIOUS 
  20 8WH# ...#  LOAD OF MK 850 OR MUST HAVE THE UNFINISHED BIT SET, WHICH WILL   
  17 8WHJ ...#  RESULT IN OUR TREATING THEM AS USABLE (SEE ABOVE).   
  10 8WHS ...#  WE THEREFORE IGNORE THEM.
   4 8WJ4 ...#   
   9 8WJ# ...      BRN      RSTEP2   
   4 8WJW ...#   
   4 8WJ_ ...#   
   5 8WK4 ...SETTAB  
   8 8WK7 ...      SBX   4  FX1  
   7 8WK= ...      SETUPTAB  
   6 8WK* ...      TABSET
   8 8WKD ...      ADX   4  FX1  
   7 8WKH ...      EXIT  4  0
   4 8WKL ...#   
   5 8WKP ...UNSETTAB
   8 8WKS ...      SBX   4  FX1  
   7 8WKX ...      TABULATE  
   7 8WL2 ...      FREETAB   
   8 8WL5 ...      ADX   4  FX1  
   7 8WL8 ...      EXIT  4  0
   4 8WL? ...#   
   5 8WLB ...REND2   
  16 8WLF ...      CLOSETOP                     [CLOSE INCINDEX  
   5 8WLJ ...      UP
   5 8WLM ...NOBREAK 
  11 8WLQ ...      GEOERR   BRIEFPM,INCBREAK 
   4 8WLT ...#   
   4 8WLY ...#   
   4 8WNG    #   
  11 8X86          MENDAREA 40,K99INCCHECK   
   4 8XMQ    #   
   4 8Y7B    #END
   8 ____ ...07066512001300000000
