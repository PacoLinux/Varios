  16 22*P ... SEG  IPBLISTB,,,,G170                    [R.WEYAND 
  16 22PD    # (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982.   
  11 22_=    #LIS  K0IPBLISTB>K0IPB>K0ALLGEO 
   7 2357 ...#OPT G170 = 0   
   6 2394 ...#SKI G170&1 
  16 23#_ ...# WITH UGUG EDIT M170 (CHANGE REMOTE *LP WIDTHS.)   
   4 23YG    #   
  13 24D6          SEGENTRY K1IPBLISTB,SK1IPBLISTB   
  13 24XQ          SEGENTRY K2IPBLISTB,SK2IPBLISTB   
  13 25CB          SEGENTRY K3IPBLISTB,SK3IPBLISTB   
  13 25PQ          SEGENTRY K4IPBLISTB,SK4IPBLISTB   
   4 25X2    #   
   8 266S    #DEF  SAFETMARGIN=5 
  12 26W=    SEMP           20H  SETUP FILE EMPTY
  12 27*W    PCONT          #20203576,#32000000  
   8 27TG    SDEL           #76  
   8 28*6    SHIFT          #74  
   8 28SQ    SPACE          #20  
  14 294J    XDC3           #76357603           [CR + DC3
  15 29#B    PMAGIC         +7036875            [2**46/10**7 
  14 29S2    PRINT          #76327632,#76207620,#76207620
   9 2=?L    SHTAB          #763173  
   9 2=R=    STESC          #76130075
  11 2?5L    PAGESEQ        #76007631,#763500
   9 2?8=    XMSK           #77700037
   4 2?=W    #   
   4 2?#D    [   
  19 2?F6    RECHE                              [SEE IF RESD ERROR ON STEP   
  17 2?GN          LFBBUS   M,2,(7)             [J IF NO READ ERROR  
  10 2?J=          STO   4  IWORK15(2)   
   7 2?KS          LDN   3  0
  17 2?MB          BRN      WOBL                [J TO CLEAR UP IOUT  
  21 2?QG    #     ROUTINE TO SEE WHETHER RECORD WILL FIT ON ONE LINE ; ON EXIT X5   
  21 2#=6    #   CONTAINS ^A7021WIDTH-4^ IF MORE THAN 1 LINE, SIZE OF RECORD IF LESS 
   4 2#PQ    #   
  10 2*9B    REM   LDX   5  IWORK13(1)   
   7 2*P2          SRC   5  1
  18 2B8L          BPZ   5  NOTS                [SPECIAL NOT REQUESTED   
  20 2BN=          SBN   3  1                   [MOVE RECORD POINTER FOR SPECIAL 
   7 2C7W    NOTS  SLC   5  3
  21 2CMG          ANDN  5  12                  [EXTRA WORDS FOR SPECIAL AND NUMBER  
  10 2D76          ADX   5  ACOMMUNE5(1) 
  10 2DLQ          SEGENTRY K50IPBLISTB  
   6 2DN_ ...#SKI G170&1 
   4 2DR8 ...(   
   9 2DTC ...      SBX   5  IWIDTH(1)
   7 2DXL ...      SBN   5  1
   4 2D_T ...)   
   7 2F44 ...#SKI G170&1$1   
  10 2F6B          SBN   5  A7021WIDTH+1 
  19 2FL2          BNG   5  SMALL               [RECORD LESS THAN ONE LINE   
  21 2G5L          ADN   5  5                   [ADD 5 CHARS FOR CONTINUATION MARKER 
  18 2GK=          DSA   5  IWORK9(1)           [PRESERVE REMAINDER COUNT
  10 2H4W          SEGENTRY K52IPBLISTB  
   6 2H75 ...#SKI G170&1 
   4 2H9# ...(   
   9 2H?H ...      LDX   5  IWIDTH(1)
   7 2H*Q ...      SBN   5  4
   4 2HC_ ...)   
   7 2HG8 ...#SKI G170&1$1   
  10 2HJG          LDN   5  A7021WIDTH-4 
  20 2J46          STO   5  IWORK20(1)          [INITIALISE 'LAST SPACE ' MARKER 
  21 2JHQ          LDX   4  GSIGN               [TO PRESERVE POINTERS AFTER SPACES   
   7 2K3B          EXIT  7  0
  10 2KH2          SEGENTRY K51IPBLISTB  
   6 2KK9 ...#SKI G170&1 
   4 2KMD ...(   
   9 2KPM ...SMALL ADX   5  IWIDTH(1)
   7 2KRW ...      ADN   5  1
   4 2KW5 ...)   
   7 2KY# ...#SKI G170&1$1   
  10 2L2L    SMALL ADN   5  A7021WIDTH+1 
  20 2LG=          LDN   4  0                   [NO CONTINUATION AT END OF LINE  
   7 2L_W          EXIT  7  0
   4 2MFG    #   
  20 2M_6    #     STORE SPACES OR HORIZONTAL TAB CHARACTERS ; NO OF SPACES IN X7
   4 2NDQ    #   
   8 2NYB    STOSP LDX   1  FX2  
  10 2PD2          STOZ     ACOMMUNE4(1) 
  18 2PXL          STO   3  IWORK18(1)          [PRESERVE RECORD POINTER 
  17 2QC=          BPZ   4  SAC                 [LAST LINE OF RECORD 
  10 2QWW          STO   6  IWORK19(1)   
  20 2RBG          STO   5  IWORK20(1)          [PRESERVE COUNTS AND DCH POINTER 
   7 2RW6    SAC   SBN   6  3
  19 2S*Q          BNG   6  OPUND               [NOT ENOUGH ROOM IN THIS NPU 
   8 2STB          BZE   6  OPUND
   7 2T*2          LDX   3  7
   7 2TSL          SBN   7  3
  21 2W#=          BPZ   7  SPAN                [MORE THAN 2 SPACES : USE HORIZ. TAB 
   7 2WRW          SBX   6  7
   8 2X?G          LDN   1  ACES 
  17 2XR6          MVCH  1  0(3)                [INSERT 1 OR 2 SPACES
   8 2Y=Q          BRN      STLS 
   8 2YQB    SPAN  SMO      FX1  
  14 2_=2          LDX   3  SHTAB               [#763173 
   7 2_PL          LDN   1  3
   7 329=          BCHX  1  /
   8 32NW          SBN   7  24   
  21 338G          BPZ   7  STAB                [>26 SPACES : INSERT 27 FOR PRESENT  
  20 33N6          ADX   3  7                   [<27 SPACES : FINAL HORIZ. TAB   
  19 347Q    STAB  MVCH  1  3                   [INSERT HORIZONTAL TAB CHARS 
  17 34MB          BZE   7  STLS                [EXACTLY 27 SPACES   
   8 3572          BPZ   7  SAC  
   8 35LL    STLS  SMO      FX2  
  18 366=          LDX   3  IWORK18             [RETRIEVE RECORD POINTER 
   8 36KW          SMO      FX2  
  17 375G          STO   2  ACOMMUNE6           [PRESERVE DCH POINTER
   8 37K6          SMO      FX2  
  21 384Q          STO   6  IWORK19             [UPDATE COUNT OF IOUT UNUSED CHARS   
   8 38JB          LDX   1  FX1  
   7 3942          EXIT  0  0
   4 39HL    #   
  15 3=3=    #     RE-FIND BLOCK IF IT MOVES DURING ALTLENG  
   4 3=GW    #   
  10 3?2G    RFND  MHUNTW   2,ADATA,ACONV
   7 3?G6          EXIT  1  0
   4 3?_Q    #   
  20 3#FB    #     GET AMXOR/IOUT BLOCK FOR OUTPUT ; WORD COUNT IN X3 ON ENTRY   
   4 3#_2    #   
   9 3*DL    SAMIO LDXC  3  AWORK1(2)
   8 3*Y=          SRL   3  12   
   7 3BCW          LDX   6  3
  10 3BXG          SBN   6  SAFETMARGIN  
   7 3CC6          SRL   3  2
   8 3CWQ    SETU  SBX   7  FX1  
  14 3DBB          SETNCORE IDATA-A1(3),2,AMXOR,IOUT,1   
   8 3DW2          SMO      FX2  
  19 3F*L          STO   2  ACOMMUNE2           [PRESERVE AMXOR/IOUT ADDRESS 
   7 3FT=          LDN   0  1
   8 3G#W          SMO      FX2  
  19 3GSG          ADS   0  AWORK1              [COUNT OF BLOCKS FOR OUTPUT  
  20 3H#6          STOZ     IMOPTY(2)           [MADE NON-ZERO IF OUTPUT ALONE   
   8 3HRQ          LDN   0  IDATA
   7 3J?B          SLL   3  2
  19 3JR2          STO   0  IBSW(2)             [START ADDRESS FOR DEPOSITING
  10 3K=L          SBN   3  SAFETMARGIN  
   7 3KQ=          SMO   FX2 
  17 3L9W          LDX   0  JSOURCE1            [IDENTIFIER NUMBER   
   8 3LPG          ORX   3  GSIGN
   8 3M96          ANDN  0  #7777
  17 3MNQ          STO   0  IPBIDE(2)           [IDENTIFIER NUMBER   
   8 3N8B          ADX   7  FX1  
  18 3NN2          STO   3  IPBIDE+1(2)         [INITIAL ROOM IN BLOCK   
   7 3P7L          EXIT  7  0
   4 3PM=    #   
  14 3Q6W    #              TEST FOR MOP OFF OR FINISH   
   4 3QLG    #   
  10 3R66    TMF   MHUNTW   3,AMXOR,ADCB 
  10 3RKQ          LDX   1  A1+FPTR(3)   
  10 3S5B          LDX   0  FOURTHWD(1)  
   9 3SK2          ORX   0  FINISH   
   7 3T4L          SLL   0  2
   8 3TJ=          BPZ   0  (7)  
  10 3W3W          ACROSS   IPBLISTC,5   
   4 46TX    #   
   4 47#Q    #   
   6 47DK    SK4IPBLISTB 
  18 47FW          CALL  7  TMF                 [CHECK FOR MOP OFF ETC.  
  15 47H7          CALL  7  SAMIO               [GET NEW IOUT
  16 47JD          STEPAGAIN                    [REFIND RECORD   
  10 47KP          LDX   4  IWORK15(2)   
  18 47M2          BZE   4  REOK                [J TO CONTINUE LISTING   
  21 47N?          LDX   5  IWORK1(2)           [RESET X5 IF NOT REACHED START REC.  
  16 47PJ          BRN      STEPOK              [AND GO O FIND IT
   4 47SB    #   
  21 48#2    #     ENTRY FROM IPBLISTA TO LISTFILE TO A 7020 BULK DEVICE VIA THE IPB 
  19 48RL    #     STEP RECORDS TO START RECORD NUMBER , OR TO NEXT RECORD   
  20 49?=    #     AWORK2 IS UNTOUCHED AS IT CONTAINS GMODE FOR ENTERING LISTEND 
   4 49QW    #   
   4 4==G    #   
  21 4=Q6    #     K1 ENTRY FOR LISTING MAINFILE ; K2 ENTRY FOR LISTING TESTPATTERN  
   4 4?9Q    #   
   6 4?PB    SK2IPBLISTB 
  17 4#92          LDN   6  IWORK1(2)           [MAINFILE INFO. STORE
  15 4#NL          LDN   5  IWORK10(2)          [WORKING AREA
  18 4*8=          LDN   4  IWORK5(2)           [SETUP FILE INFO. STORE  
  20 4*MW          MOVE  5  4                   [PRESERVE MAINFILE INFORMATION   
  20 4B7G          MOVE  4  4                   [ACCESS REQUIRED FILE INFORMATION
   8 4D62          LDCT  0  #200 
  21 4DKL          ORS   0  AWORK4(2)           [OUTPUT 'PLEASE LINE UP' IMMEDIATELY 
  16 4FJW          CALL  7  SAMIO               [GET IOUT BLOCK  
   6 4FX?          STEP  
  18 4G9N          BZE   3  SEMPT               [ J IF SETUP FILE EMPTY  
   8 4GJ5          BRN      START
   4 4GWG    [   
   4 4H8X    [   
   6 4HH#    SK1IPBLISTB 
   8 4HTP          CALL  7  SAMIO
   7 4J86          STEPAGAIN 
  18 4JGH          BZE   3  SEMPT               [ J IF SETUP FILE EMPTY  
  20 4JSY                                       [ ( ON REENTRY FROM IPBLISTC )   
   5 4K7*    STEPOK  
   9 4KFW          LDX   0  IWORK9(2)
   8 4LF6          BRN      START
   4 4LYQ    #   
   8 4MDB    NEWS  CALL  7  TMF  
  10 4MG#          HUNTW    3,AMXOR,#77  
  18 4MJ=          BNG   3  NEWNPU              [NO UNPACKED NPU(IOUT)   
  19 4ML8          NAME     3,AMXOR,IOUT        [USE UNPACKED NPU (LP ONLY)  
   7 4MN6          LDN   0  1
  21 4MQ4          ADS   0  AWORK1(2)           [RESET COUNT OF NPUS(IOUTS) FOR OUTPU
  20 4MS2          BRN      NXREC               [PACK IN NEXT RECORD IF POSSIBLE 
   5 4MTY    NEWNPU  
  17 4MY2          CALL  7  SAMIO               [GET NEW IOUT BLOCK  
  20 4N2X          SEGENTRY K62IPBLISTB         [USED BY IPBNOPCK RESTORE MACRO  
   7 4N5S    NXREC LDN 4 0   
   6 4N7Q          STEP  
   8 4NMC          CALL  7  RECHE
  19 4NQ2    REOK                               [RETURN LABEL FROM READ RROR 
  17 4NX=          BZE   3  WOBL                [END OF FILE REACHED 
  16 4P9L          LDX   6  IWORK17(2)          [PAGE LINE COUNT 
  10 4P=X          SEGENTRY K56IPBLISTB  
  11 4P#9          SBN   6  A7021PAGE-1<62   
  20 4P*G          LDXC  0  IWORK9(2) [ENSURE END OF RESTART SECTION BIT IS UNSET
  16 4PBS          BNG   6  START               [NOT END OF PAGE 
   8 4PFC          LDCT  4  #010 
  20 4PJ2          ADX   0  4                   [ADD 1 TO RESTART SECTION NUMBER 
  20 4PLK          ORX   0  GSIGN               [RESTART SECTION=1 PAGE FOR LP   
   8 4PP8          BRN      START
   4 4SS=    #   
  10 4T?W    START LDX   7  IWORK13(2)   
  19 4TRG          STO   3  IWORK15(2)          [SAVE INITIAL RECORD POINTER 
   7 4W?6          ANDN  7  4
  15 4WQQ          BZE   7  NOMON               [NOT MONFILE 
  18 4X=B          LDX   7  1(3)                [MONFILE CATEGORY WORD   
  17 4XQ2          ANDX  7  IWORK14(2)          [MONFILE DISCARD MASK
  19 4Y9L          BNZ   7  NOMON               [DON'T DISCARD THIS RECORD   
   7 4YP=          LDN   7  1
  17 4_8W          ADS   7  IWORK10(2)          [UPDATE RECORD NUMBER
   8 4_NG          BRN      NXREC
   4 5286    #   
   7 52MQ    NBSS  PHOTO    4
  11 537B          SETUPCORE 6,1,ADATA,ACONV 
   9 53M2          TESTMOVE 4,SMVE   
   8 546L          BRN      STAN 
   9 54L=    NOMON STO   0  IWORK9(2)
  19 5656          LDN   6  #140                [B17 : ALLCHAR ; B18 : NORMAL
  10 56JQ          ANDX  6  IWORK13(2)   
   8 574B          BZE   6  STT1 
  10 57J2          LDX   0  IWORK13(2)   
   7 583L          SRC   0  1
  18 58H=          BNG   0  STT1                [J IF SPECIAL REQUESTED  
  10 592W          HUNTW    1,ADATA,ACONV
   9 59GG          LDEX  6  FRH(3)   
   8 5=26          BNG   1  NBSS 
  10 5=FQ          TXU   6  ALOGLEN(1)   
   8 5=_B          BCC      SMVE 
   7 5?F2          PHOTO    4
   9 5?YL          ALTLENG  1,6,RFND 
   9 5#D=          TESTMOVE 4,SHNT   
   7 5#XW    STAN  STEPAGAIN 
  10 5*CG    SHNT  HUNTW    1,ADATA,ACONV
   9 5*X6    SMVE  LDN   4  A1+FRH(1)
   7 5BBQ          SMO      6
   7 5BWB          MOVE  3  0
   7 5CB2          LDN   4  2
  15 5CTL          LDN   6  #40                 [B18 : NORMAL
  10 5D*=          ANDX  6  IWORK13(2)   
   8 5DSW          BNZ   6  NMD  
   8 5F#G          LDN   4 #22   
   8 5FS6    NMD   LDN   6  #10  
   8 5G?Q          TRANSFOR 4,6  
  10 5GRB          HUNTW    1,ADATA,ACONV
   9 5H?2          LDN   3  A1+FRH(1)
  10 5HQL          STO   3  IWORK15(2)   
   9 5J==    STT1  LDCH  5  IWORK9(2)
  10 5JPW          MHUNTW   2,AMXOR,IOUT 
   8 5JSF          LDX   1  FX2  
   7 5JX4          LDCT  6  4
   9 5J_M          ANDX  6  IWORK9(1)
  21 5K4=          ERS   6  IWORK9(1)           [UNSET^FIRST REC OF SECTION^BIT IFSET
  18 5K6T          BZE   6  NSOS                [NOT FIRST REC OF SECTION
   8 5K9G          ANDN  5  #37  
   7 5KP6          BCHX  2  /
  18 5L8Q          DCH   5  IPBIDE+1(2)         [RESTART NO. IN CHAR 1   
   9 5LNB          ANDX  2  BITS22LS 
  10 5M82    NSOS  LDX   6  IPBIDE+1(2)  
  19 5N7=          STO   2  ACOMMUNE2(1)        [PRESERVE AMXOR/IOUT ADDRESS 
   8 5NLW          ANDN  6  #7777
  21 5P6G          ADX   2  IBSW(2)             [POINTER TO DEPOSIT NEXT CHARACTER   
   8 5PL6          LDCT  4  #400 
  18 5Q5Q          ANDX  4  IWORK13(1)          [B0 SET IF PERI-TYPE FILE
   7 5QKB          LDX   5  4
   8 5R52          BZE   4  SPON 
   8 5RJL          LDCT  4  #600 
  19 5S4=          ANDX  4  1(3)                [POSITION OF LAST CHARACTER  
  20 5SHW          BNZ   4  SPLAS               [LAST WORD OF RECORD SPACE-FILLED
   7 5T3G    SPON  ADN   4  1
  17 5TH6    SPLAS ADX   4  0(3)                [WORD COUNT OF RECORD
   7 5W2Q          SLC   4  2
  19 5WGB          SBN   4  12                  [RECORD HEADER AND LAST WORD 
   8 5X22    PERY  BPZ   4  PER1 
   7 5XFL          LDN   4  0
  18 5X_=    PER1  STO   4  ACOMMUNE5(1)        [NO OF CHARS IN RECORD   
   4 5_D6    #   
  21 5_XQ    #     OUTPUT TO REMOTE LINE PRINTER : INSERT FE CHARS FOR PFCC , END OF 
  20 62CB    #     PRINT LINE AND MORE THAN 3 SUCCESSIVE SPACES ; TRANSLATE SHIFT
  19 62X2    #     CHARS INTO LINE CODE ; INSERT '-' IF NEWLINE WITHIN RECORD
   4 63BL    #   
   8 63YC          LDCT  0  #100 
   9 63_6          ANDX  0  AWORK4(1)
  17 63_T          BZE   0  XNNEWPAG            [J IF NOT ON NEW PAGE
   9 642J          ERS   0  AWORK4(1)
   8 643?          SMO      FX2  
   9 6442          LDX   0  IWORK11  
  15 644P          ANDN  0  #37                 [MODULO 32   
   7 645D          SRC   0  6
   8 6467          SMO      FX1  
  16 646W          LDN   1  PAGESEQ             [PAGE SEQUENCE   
   7 647K          MVCH  1  4
   8 648#          ADX   0  0(1) 
   7 6493          LDN   1  0
   7 649Q          MVCH  1  3
  15 64=F          SBN   6  7                   [CHAR COUNT  
   8 64?8          LDX   1  FX2  
   5 64?X    XNNEWPAG
   9 64#2          LDEX  0  AWORK4(1)
  16 64#3          BZE   0  XNLOOP              [ J IF NOT FAILED
   7 64#4          LDN   0  0
  17 64#5          DEX   0  AWORK4(1)           [ CLEAR FAILED STATUS
  10 64#6          MHUNTW   1,AMXOR,ADCB 
  16 64#7          JBS      XNLOOP,1,ADCBTSPOOL [ J IF SPOOLING  
  17 64#8          JBC      XNLOOP,1,ADCB7502LP [ J IF NOT 7502 LP   
  11 64#9          HUNTW    1,ADATA,FORMLOOP 
  19 64#=          BNG   1  XNLOOP              [ J IF NO FORMAT LOOP DATA   
  10 64#?          LDX   0  FORMCHAR(1)  
  10 64##          LDN   1  FORMDATA(1)  
   7 64#*          SMO      0
   7 64#B          MVCH  1  0
  20 64#C          SBX   6  0                   [ CHAR COUNT LEFT IN IOUT BLOCK  
   8 64#D          SMO      FX2  
  19 64#F          LDXC  5  AWORK1              [NPU SIZE IN CHARS IN B1-11  
   8 64#G          SRL   5  12   
  18 64#H          SBN   5  SAFETMARGIN         [REPLACE SAFETY MARGIN   
  10 64#J          MHUNT    2,AMXOR,IOUT 
  19 64#K          SBX   5  6                   [X5 = CT. OF CHARS DEPOSITED 
   9 64#L          STOZ     IBSW(2)  
  10 64#M          DSA   5  IPBIDE+1(2)  
  10 64#N          ADN   5  IDATA-A1*4+3 
  20 64#P          SRL   5  2                   [REQUIRED AMXOR/IOUT BLOCK LENGTH
   8 64#Q          ALTLEN   2,5  
  18 64#R          CALL  7  SAMIO               [ GET ANOTHER IOUT BLOCK 
   7 64#S          STEPAGAIN 
  10 64#T          MHUNTW   2,AMXOR,IOUT 
   8 64#W          SMO      FX2  
   9 64#X          STO   2 ACOMMUNE2 
   9 64#Y          ADX   2  IBSW(2)  
   5 64#_    XNLOOP  
   8 64*2          LDX   1  FX2  
  17 64*P          SBN   6  4                   [MAXIMUM PFCC SIZE   
   8 64*W          LDN   0  #76  
   8 64TG          DCH   0  0(2) 
   7 65*6          BCHX  2  /
   8 65SQ          LDN   0  #32  
  18 66#B          DCH   0  0(2)                [STORE #7632 = NEWLINE   
  18 66S2          LDX   0  IWORK17(1)          [PAGE POSITION INDICATOR 
  10 673S          SEGENTRY K57IPBLISTB  
  11 67?L          SBN   0  A7021PAGE-1<62   
  10 67R=          LDX   4  IWORK13(1)   
  21 68=W          LDN   7  0                   [TO INDICATE NO PFCC SO ONE NEWLINE  
  17 68QG          BPZ   4  NOPFC               [NO PFCC IN RECORD   
   7 69=6          ANDN  4  1
  18 69PQ          BNZ   4  NOPFC               [IGNORE PFCC IF SPECIAL  
   4 6=9B    #   
  19 6=P2    #     INTERPRET PFCC IF PRESENT ; FORM FEED IF AT BOTTOM OF PAGE
   4 6?8L    #   
   8 6?N=          LDX   7  1(3) 
   7 6#7W          LDCH  4  7
   7 6#MG          ANDN  7  7
   7 6*76          SRC   4  6
  17 6*LQ          BNG   4  WIP                 [PFCC WITH PRINTING  
  16 6B6B          LDN   3  0                   [WITHOUT PRINTING
   8 6BL2    WIP   BZE   7  NOMOV
   7 6C5L          SLC   4  2
  18 6CK=          BNG   4  VTAB                [VERTICAL TAB REQUIRED   
  16 6D4W          ORX   6  GSIGN               [SET NL PFCC FLAG
  18 6DJG    NOPFC BNG   0  NOFF                [NOT AT BOTTOM OF PAGE   
   7 6F46          LDN   7  1
   4 6FHQ    VTAB
  15 6G3B          BNG   6  PR                  [J IF NL PFCC
   8 6GH2          LDCH  0  0(2) 
   7 6H2L          ADN   0  1
  20 6HG=          DCH   0  0(2)                [CONVERT TO #7633 = VERTICAL TAB 
   7 6H_W          BCHX  2  /
  16 6JFG          DCH   7  0(2)                [CHANNEL NUMBER  
   7 6J_6          ADN   6  1
   7 6KDQ          BCHX  2  /
  19 6KQ6    XIGPG BCT   7  NCHN1               [THROW TO CHANNEL 2-7 PFCC   
  11 6L3G    #              FORM FEED PFCC   
   4 6L88    XPR 
   9 6L#W          LDXC  0  IWORK9(1)
  21 6LH6          BCS      PR              [ALREADY FLAGGED ^END OF RESTART SECTION^
  20 6LPB          LDCT  4  #010                [UPDATE RESTART SECTION NUMBER   
   7 6LXL          ADX   0  4
  20 6M5W          ORX   0  GSIGN               [INDICATE END OF RESTART SECTION 
   9 6M#6          STO   0  IWORK9(1)
   9 6MMB    #              NEW PAGE 
   4 6MP=    PR  
  18 6MQ8          STOZ     IWORK17(1)          [CLEAR PAGE LINE COUNT   
  10 6MR6          LDX   7  IWORK10(1)   
  11 6MWW          MHUNTW   1,ADATA,IPTEMP   
  10 6NBG          LDX   0  ALOGLEN(1)   
   7 6NDD          SBN   0  2
   9 6NGB          LDX   5  A1+1(1)  
   7 6NJ#          DVS   4  0
   7 6NL=          SMO   4   
   9 6NN8          LDX   5  A1+2(1)  
   7 6NQ6          SBX   5  7
   8 6NS4          BZE   5  PRX  
  19 6NW6          LDX   5  A1(1)               [NO. OF PAGES OUTPUT ALREADY 
   7 6PTB          ADN   5  1
   8 6Q*2          STO   5  A1(1)
  20 6QGS          LDX   5  A1+1(1)             [NO OF CURRENT FILE PAGES PRINTED
   7 6QJQ          ADN   5  1
   9 6QLN          STO   5  A1+1(1)  
  21 6QSL          DVS   4  0                   [REMAINDER WILL BE POINTER DOWN BLOCK
   7 6R#=          SMO   4   
   9 6RJ4          STO   7  A1+2(1)  
   4 6RXR    PRX 
   8 6S?G          LDX   1  FX2  
  10 6SB5          LDX   0  IWORK11(1)   
   7 6SDN          ADN   0  1
   8 6SH?          SMO      FX1  
   8 6SKW          ANDX  0  XMSK 
  10 6SNF          STO   0  IWORK11(1)   
  16 6SR6    NPR   BPZ   6  PRES                [J IF NOT NL PFCC
  19 6T=Q          SMO      IWORK15(1)          [SET X7 TO ZERO IF NL PFCC   
   7 6TQB          LDX   7  1
   7 6W=2          ANDN  7  7
   7 6WPL          SBN   7  2
  18 6X9=    UICC  ADN   6  2                   [UPDATE IOUT CHAR COUNT  
  17 6XNW          ANDX  6  BITS22LS            [CLEAR NL PFCC FLAG  
  16 6Y8G          BCHX  2  PFC1                [UPDATE IOUT PTR 
   4 6YN6    #   
   8 6_7Q    NOMOV LDCH  0  0(2) 
   7 6_MB          ADN   0  3
  21 7272          DCH   0  0(2)                [CONVERT TO #7635 = CARRIAGE RETURN  
   7 72LL          ADN   6  2
   8 736=          BCHX  2  PRES 
   7 73KW    NOFF  SBN   7  2
  17 745G          BNZ   7  UPLC                [J IF SINGLE NL PFCC 
  14 74K6          ADN   0  1                   [TWO NLS 
  10 754Q          SEGENTRY K58IPBLISTB  
  18 75JB    UPLC  ADN   0  A7021PAGE<63        [UPDATE PAGE LINE COUNT  
  10 7642          STO   0  IWORK17(1)   
   8 76HL          BRN      UICC 
   5 76JB    NCHN1   
  10 76K6          LDX   0  IWORK17(1)   
  10 76KW          SEGENTRY   K55IPBLISTB
  11 76LL          SBN   0  A7021PAGE-1<62   
   7 76MB          ADX   0  7
   8 76N6          BPZ   0  XPR  
  10 76NW          SEGENTRY K59IPBLISTB  
  10 76PL          ADN   0  A7021PAGE<63 
  10 76QL          STO   0  IWORK17(1)   
   8 76SB          BRN      PRES 
  18 76W6    [              THROWS TO CHANNEL 2-7 PFCCS ARE TREATED AS   
  19 76XW    [              2-7 NEWLINES FOR LINE COUNT & RESTART PURPOSES   
   4 76_N    #   
  17 773=    PFC1  BNZ   7  PRES                [J IF SINGLE NL PFCC 
  20 77GW          SBN   6  2                   [PFCC = 2 OR #42 : TWO NEWLINES  
   8 782G          LDN   0  #7632
   8 78G6          LDX   1  GSIGN
   7 78_Q          MVCH  1  2
   8 79FB          LDX   1  FX2  
  17 79_2    PRES  BZE   3  PEND                [NON-PRINTING PFCC   
  21 7=DL          CALL  7  REM                 [CALCULATE & PRESERVE REMAINDER COUNT
  18 7=Y=          ADN   3  2                   [MOVE PAST RECORD HEADER 
   4 7?CW    #   
  18 7?XG    #     INSERT LINE NUMBER IF REQUIRED AT BEGINNING OF LINE   
   4 7#C6    #   
  10 7#WQ    PINS  LDX   7  IWORK13(1)   
  20 7*BB          ANDN  7  2                   [B22 SET IF NUMBERING REQUIRED   
  10 7*W2          STOZ     ACOMMUNE4(1) 
   8 7B*L          BZE   7  PST  
  10 7BT=          LDX   0  IWORK10(1)   
  18 7C#W          BPZ   0  PUNUM               [LINE NO. NOT TOO LARGE  
   8 7CSG          SMO      FX1  
   8 7D#6          LDX   0  SHTAB
  18 7DRQ          SBN   0  19                  [HORIZ. TAB FOR 8 SPACES 
  19 7F?B          LDCT  1  #200                [START MVCH AT CHAR. 1 OF X0 
   7 7FR2          MVCH  1  3
   7 7G=L          SBN   6  3
  21 7GGD          BNZ   6  PROOM               [STILL ROOM IIN NPU X6 = ROOM STILL L
  14 7GQ=          ADN   6  3                   [RESET X6
  17 7H24          BRN      NPUND               [J. NEW NPU REQUIRED 
   8 7H9W    PUNUM SMO      FX1  
   9 7HPG          MPY   0  PMAGIC   
  16 7J96          LDN   7  6                   [CHARACTER COUNT 
   7 7JNQ          MODE     1
  19 7K8B    PCBD  CBD   0  0(2)                [CONVERT LINE NO. TO DECIMAL 
   7 7KN2          BCHX  2  /
   8 7L7L          BCT   7  PCBD 
  20 7LM=          MODE     0                   [DON'T ZERO-SUPPRESS LAST CHAR   
   8 7M6W          CBD   0  0(2) 
   7 7MLG          BCHX  2  /
   8 7N66          LDN   1  #20  
   8 7NKQ          DCH   1  0(2) 
   7 7P5B          BCHX  2  /
  18 7PK2          SBN   6  8                   [ROOM LEFT IN AMXOR/IOUT 
  17 7PNX          BNZ   6  PROOM               [STILL ROOM IN NPU   
   7 7PSS          SBN   5  8
   8 7PYP          BRN      NPUND
  18 7Q4L    PROOM SBN   5  8                   [ROOM LEFT ON PRINT LINE 
   8 7QJ=    PST   LDX   1  FX1  
   8 7R3W          SMO      FX2  
  17 7RHG          LDX   7  ACOMMUNE4           [INITIAL SPACE COUNT 
  21 7S36          BZE   5  PUNL                [NULL RECORD OR NPUND EXIT FROM STOSP
   4 7SGQ    #   
  21 7T2B    #     PRINT TRANSFER LOOP : SPACE CT. IN X7 , X4<0 IF MULTI-LINE RECORD 
   4 7TG2    #   
   8 7T_L    PLOOP LDCH  0  0(3) 
   9 7WF=          TXU   0  SPACE(1) 
  15 7WYW          BCS      PNOS                [NOT A SPACE 
   7 7XDG          SBN   5  1
   7 7XY6          ADN   7  1
   7 7YCQ          BCHX  3  /
  15 7YXB          BZE   5  PUNL                [END OF LINE 
   8 7_C2          BRN      PLOOP
  18 7_WL    PNOS  BZE   7  NOSP                [NO SPACES TO BE INSERTED
  19 82B=          CALL  0  STOSP               [STORE SPACES OR HORIZ. TAB  
  17 82TW          LDN   7  0                   [ZEROISE SPACE COUNT 
   8 83*G          LDCH  0  0(3) 
  13 83T6    NOSP  TXL   0  SHIFT(1)            [#74 
  21 84#Q          BCS      PLOP                [CHAR < #74 : NO CONVERSION NEEDED   
   8 84SB          LDN   7  #76  
  19 85#2          DCH   7  0(2)                [CONVERT E.G. #74 TO #7664   
   7 85RL          BCHX  2  /
   7 86?=          SBN   6  1
   7 86QW          LDN   7  0
   8 87Q6          SBN   0  #10  
  15 87X#          BNZ   6  PLOP                [NPU NOT FULL
  19 884G          BUX   6  NPUND               [ RESET(+1) HOOM LEFT IN IOUT
   8 889Q    PLOP  DCH   0  0(2) 
   7 88PB          SBN   5  1
   7 8992          BCHX  3  /
  17 89NL          SBN   6  1                   [SPACE LEFT IN NPU   
   7 8=8=          BCHX  2  /
  15 8=MW          BZE   5  PUNL                [END OF LINE 
   8 8?7G          BNZ   6  PLOOP
  15 8?M6          BRN      NPUND               [END OF NPU  
   4 8#6Q    #   
  21 8#LB    #     END OF LINE ; IF NOT END OF RECORD INSERT '-' AND INDENT NEXT LINE
   4 8*62    #   
   4 8*=D    PUNL
  10 8*BW          SEGENTRY K53IPBLISTB  
   6 8*H#          NULL  
   6 8*MQ          NULL  
   6 8*S8          NULL  
  17 8*YL          BZE   4  PEND                [ALSO END OF RECORD  
  19 8B5=          BZE   7  PUATE               [LINE NOT ENDED WITH SPACE(S)
   8 8HF6    PUNOW CALL  0  STOSP
   8 8HYQ    PUATE LDX   1  FX1  
  15 8JDB          ADN   1  PCONT               [#2020357632 
  15 8JY2          MVCH  1  5                   [INSET HYPHEN
  19 8KCL          SBN   6  5                   [UPDATE ROOM LEFT IN IOUT BLK
   8 8KX=          LDX   1  FX2  
  10 8LBW          LDX   7  IWORK13(1)   
  18 8LWG          LDX   5  IWORK9(1)           [CHAR COUNT FOR NEXT LINE
   7 8MB6          LDX   0  7
   8 8MTQ          ANDN  5  #3777
  19 8N*B          ANDN  7  2                   [B22 SET IF NUMBER SPECIFIED 
   7 8NT2          LDN   4  1
  18 8P#L          ADS   4  IWORK17(1)          [UPDATE PAGE LINE COUNT  
  19 8PS=          ANDN  0  1                   [B23 SET IF SPECIAL LISTING  
  19 8Q?W          ADN   7  1                   [PLUS WORD FOR INDENTATION   
   7 8QRG          SLL   7  2
  19 8R?6          BNZ   0  NOAD                [4 ADDED IN REM IF SPECIAL   
  20 8RQQ          ADN   5  4                   [ADD INDENTATION TO CHAR COUNT   
  21 8S=B    NOAD  STO   5  ACOMMUNE5(1)        [IN CASE END OF NPU BEFORE NEXT NL   
  21 8SQ2          ADX   3  0                   [COUNTERACT 'SBN 3 1' IN REM IF SPEC.
  20 8T9L          STO   7  ACOMMUNE4(1)        [NUMBER OF SPACES FOR INDENTING  
  20 8TP=          CALL  7  REM                 [GET COUNT OF CHARS IN NEXT LINE 
  10 8W8W          LDX   7  ACOMMUNE4(1) 
   7 8WNG          SBX   5  7
  19 8X86          LDX   1  ACOMMUNE2(1)        [START ADDRESS OF AMXOR/IOUT 
  20 8XMQ          BNG   6  NPUND               [J IF HAVE RUN INTO SAFETY MARGIN
  16 8Y7B          BZE   6  NPUND               [J IF NPU FULL   
  18 8YM2          DSA   6  IPBIDE+1(1)         [ROOM LEFT IN AMXOR/IOUT 
  17 8_6L          BRN      PST                 [CONTINUE CONVERSION 
   4 ?L5B    #   
  21 ?LK2    #     AMXOR/IOUT BLOCK FULL : END OF NPU ; SEND WHOLE BLOCK IF IT DOES  
  20 ?M4L    #     NOT CONTAIN NEWLINE ; IF IT DOES , COPY CHARACTERS SINCE LAST 
  17 ?MJ=    #     NEWLINE INTO NEW AMXOR/IOUT AND SHORTEN OLD ONE   
   4 ?N3W    #   
   8 ?NHG    NPUND LDX   1  FX2  
  19 ?P36          LDX   0  IWORK18(1)          [SAVE OLD VALUE OF IWORK18   
  19 ?PGQ          SBX   0  IWORK15(1)          [RELATIVE TO START OF BLOCK  
  15 ?Q2B          STO   0  ACOMMUNE6(1)        [IN ACOMMUNE6
  18 ?QG2          STO   3  IWORK18(1)          [PRESERVE RECORD POINTER 
   8 ?Q_L          BRN      PPUND
  19 ?RF=    OPUND LDX   1  FX2                 [ENTRY FROM STOSP SUBROUTINE 
   7 ?RYW          ADN   6  3
  19 ?SDG    PPUND LDX   2  ACOMMUNE2(1)        [START ADDRESS OF AMXOR/IOUT 
  18 ?SY6          LDX   0  IWORK15(1)          [INITIAL RECORD POINTER  
  10 ?TCQ          SBS   0  IWORK18(1)   
   7 ?TXB          PHOTO    0
  10 ?WC2          STO   0  ACOMMUNE7(1) 
  20 ?WWL          LDX   0  6                   [NO PREV. NL SO TAKE FULL BLOCK  
  21 ?XB=          STO   7  ACOMMUNE4(1)        [SPACE COUNT IF ENTERED FROM STOSP   
  18 ?XTW          STO   5  ACOMMUNE5(1)        [CURRENT REMAINDER COUNT 
  20 ?Y*G          LDX   6  IPBIDE+1(2)         [CHAR. REM. CT. AT LAST NEWLINE  
   9 ?YT6          LDXC  5  AWORK1(1)
  18 ?_#Q          SRL   5  12                  [INITIAL CHARACTER COUNT 
   8 ?_SB          ORX   6  CACT 
  10 #2#2          STOC  6  IPBIDE+1(2)  
   8 #2RL          ANDN  6  #7777
  18 #3?=          LDX   3  5                   [PRESERVE FOR SETUPCORE  
  10 #3QW          SBN   5  SAFETMARGIN  
   7 #4=G          SRL   3  2
  18 #4Q6          SBX   5  6                   [EQUAL IF NO NL IN BLOCK 
  20 #59Q          SBX   6  0                   [NO OF CHARS SINCE NL OR START   
   8 #5PB          BNZ   5  WHOB 
  21 #692          LDX   5  6                   [NO OF CHARS DEPOSITED SINCE START   
   7 #6NL          LDN   6  0
  18 #78=    WHOB  DSA   5  IPBIDE+1(2)         [CHAR. COUNT FOR OUTPUT  
  17 #7MW          CALL  7  SETU                [GET NEW AMXOR/IOUT  
   7 #87G          LDX   3  6
   9 #8M6          LDN   7  IDATA(2) 
   7 #96Q          LDX   1  2
  18 #9LB          HUNT2    1,AMXOR,IOUT        [FIND OVERFLOWED BLOCK   
   8 #=62          BZE   6  OWFUL
   7 #=KL          LDX   6  5
   7 #?5=          SRC   6  2
  20 #?JW          ADN   6  IDATA(1)            [POINT TO FIRST CHAR TO REPLACE  
  21 ##4G          MVCH  6  0(3)                [MOVE CHARS SINCE NL INTO NEW BLOCK  
  10 ##J6    OWFUL ADN   5  IDATA-A1*4+3 
   7 #*3Q          SBX   7  2
   7 #*HB          SRL   5  2
  21 #*R8          STOZ     IBSW(1)             [SET BLOCK STATUS WORD FOR DATA SEG  
  17 #B32          ALTLEN   1,5                 [SHORTEN OLD BLOCK   
   8 #BGL          LDX   1  FX2  
   9 #C2=          LDXC  6  AWORK1(1)
   8 #CFW          SRL   6  12   
  17 #C_G          SBN   6  SAFETMARGIN(3)      [ROOM LEFT IN BLOCK  
  18 #DF6          LDX   5  ACOMMUNE5(1)        [RECORD REMAINDER COUNT  
  10 #DYQ          LDX   0  ACOMMUNE7(1) 
   9 #FDB          TESTMOVE 0,NSTL   
  10 #FY2          HUNTW    3,ADATA,ACONV
   8 #GCL          ADN   3  A1   
   8 #GX=          BPZ   3  NST  
  10 #HBW          LDX   0  ACOMMUNE4(1) 
  10 #HWG          STO   0  IWORK15(1)   
   7 #JB6          STEPAGAIN 
   7 #JTQ          LDX   1  2
  10 #K*B          LDX   0  IWORK15(1)   
  10 #KT2          STO   0  ACOMMUNE4(1) 
   8 #L#L          BRN      NST  
  10 #LS=    NSTL  LDX   3  IWORK15(1)   
  10 #M?W    NST   STO   3  IWORK15(1)   
  10 #MRG          ADX   3  IWORK18(1)   
  10 #N?6          MHUNTW   2,AMXOR,IOUT 
  10 #NQQ          STO   3  IWORK18(1)   
  10 #P=B          STO   2  ACOMMUNE2(1) 
   7 #PQ2          ADX   2  7
  21 #R8W          LDX   0  ACOMMUNE6(1)        [REPLACE ABSOLUTE PTR IN OLD IWORK18 
  10 #RNG          ADX   0  IWORK15(1)   
  10 #S86          STO   0  IWORK18(1)   
  21 #SMQ          LDX   0  2                   [GET PTR IN IWORK19 TO POINT TO LAST 
  21 #T7B          SLC   0  2                   [GROUP OF SPACES(SUBTRACT NO OF CHARS
  19 #TM2          SBX   0  IWORK19(1)          [LEFT IN OLD AMXOR BLK TO GET
  15 #W6L          SRC   0  2                   [POSITION)   
  10 #WL=          STO   0  ACOMMUNE6(1) 
  21 #X5W          ADS   6  IWORK19(1)          [ADD TO NO OF CHARS LEFT IN NEW BLOCK
   8 #XKG          BRN      PST  
   4 #_4B    #   
  21 #_J2    #     END OF RECORD : OUTPUT IF END OF RESTART SECTION ; GET NEXT RECORD
   4 *23L    #   
   8 *2H=    PEND  LDX   1  FX2  
   7 *32W          LDX   4  2
   9 *3=M          LDXC  0  IWORK9(1)
  19 *3*=          BCC      NDC3                [NOT END OF RESTART SECTION  
  19 *3*F    #              END OF RESTART SECTION : APPEND CR + DC3 CHARS IF
  21 *3*N    #              SPOOLING. (AND IF PF778 RJE WHEN S1901A MACRO IN USE).   
  10 *3*X          MHUNTW   3,AMXOR,ADCB 
   9 *3B6          LDX   0  AWORK4(1)
   8 *3B*          SRL   0  11   
  10 *3BJ          SEGENTRY K30IPBLISTB  
  21 *3BR          BRN      NRJE                [IF NULLIFIED BY S1901A APPEND IF RJE
  10 *3C2          SMO      A1+FPTR(3)   
   9 *3C9          ORX   0  FOURTHWD 
   8 *3CD    NRJE  ANDN  0  #1000
  16 *3CM          BZE   0  NDC3                [NOT SPOOLING.   
  16 *3CW          SMO      FX1                 [INSERT CR + DC3 
   8 *3GG          LDN   1  XDC3 
   7 *3K6          MVCH  1  4
   8 *3MQ          LDX   1  FX2  
  12 *3N=          JBC      XNDC3,3,ADCBTSPOOL   
  12 *3NQ          JBC      XNDC3,3,ADCB7502LP   
   7 *3P=          ADN   6  2
   5 *3PQ    XNDC3   
  19 *3QB          SBN   6  4                   [UPDATE ROOM LEFT IN IOUT BLK
   8 *3TJ          LDCT  0  #40  
   9 *3TS          ANDX  0  AWORK4(1)
  17 *3W4          BZE   0  XNSPL               [J IF NOT SPOOLING   
  18 *3W#          SLL   0  1                   [PAGE NO SEQUENCE TO BE  
  19 *3WJ          ORS   0  AWORK4(1)           [INSERTED BEFORE NEXT OUTPUT 
   5 *3WS    XNSPL   
  19 *3XC    NDC3  LDX   2  ACOMMUNE2(1)        [START ADDRESS OF AMXOR/IOUT 
  21 *426          LDX   0  IWORK11(1)          [B2 SET IF THIS WAS 2000-CHAR RECORD 
   7 *4FQ          SLL   0  2
  21 *4S7          BNG   0  NOWTH               [QUEUE THIS 2000-CHAR TP RECORD AND  
  18 *56J                                       [CONCATENATE NEXT RECORD 
   7 *5F2          LDN   0  1
  17 *5YL          ADS   0  IWORK10(1)          [UPDATE LINE NUMBER  
  20 *68D          LBW      3,ADCB7502LP,7      [LD. BIT TO INDICATE IF 7502 LP  
  10 *6D=          LDX   3  IWORK12(1)   
  16 *6XW          BNG   3  RESEC               [PRINT ALL LINES 
   7 *7CG          SBN   3  1
  10 *7X6          STO   3  IWORK12(1)   
  21 *8BQ          BZE   3  NOTF                [END OF LISTING : ALL LINES OUTPUT   
   9 *8KD    RESEC LDX   5  IWORK9(1)
  18 *8S6          BNG   5  NOTF                [END OF RESTART SECTION  
  19 *8Y#          JBWS     PNOW,7                    [J IF X7 SAYS 7502 LP  
  20 *94G          SEGENTRY K60IPBLISTB         [IPBNOPCK - RESTORE TIME MACRO   
   6 *98N          NULL  
   6 *9#W          NULL  
   6 *9F4          NULL  
   6 *9K=          NULL  
  20 *9PD          SEGENTRY K64IPBLISTB         [USED BY IPBNOPCK RESTORE MACRO  
   7 *9TL    PNOW  LDX   0  6
  20 *=*=          SBN   0  12                  [LENGTH OF PFCC + NUMBER IF PRINT
  21 *=SW          BNG   0  NOWTH               [NOT WORTH STARTING NEXT RECORD HERE 
  17 *?#G          DSA   6  IPBIDE+1(2)         [ROOM LEFT IN BLOCK  
   7 *?S6          SBX   4  2
  20 *#?Q          STO   4  IBSW(2)             [POINTER TO DEPOSIT NEXT CHAR.   
  17 *##8          JBWS     PNOWA,7             [J IF X7 SAYS 7502 LP
  20 *##L          SEGENTRY K61IPBLISTB         [IPBNOPCK - RESTORE TIME MACRO   
   6 *#*4          NULL  
   5 *#*G    PNOWA   
   9 *#CY          LDXC  5  AWORK1(1)
  20 *#GC    #              LP : OUTPUT IF COUNT OF NPUS(IOUTS) EXCEEDS THRESHOLD
  21 *#JL    #              (DEFAULT=2) BUT RETAIN CURRENT UNPACKED NPU FOR NEXT REC 
   8 *#LT          ANDN  5  #777 
  10 *#P4          SEGENTRY K40IPBLISTB  
   7 *#R?          SBN   5  3
  21 *#TG          BNG   5  NXREC               [NPU COUNT NOT EXCEEDING THRESHOLD : 
  20 *#XP                                       [PACK IN NEXT RECORD IF POSSIBLE 
  18 *#_Y          NAME     2,AMXOR,#77         [DISGUISE UNPACKED NPU   
   7 **47          LDN   0  1
  20 **6B          SBS   0  AWORK1(1)           [ADJUST COUNT OF NPUS FOR OUTPUT 
  17 **8K          BRN      SK3IPBLISTB         [OUTPUT ^PACKED^ NPUS
   4 **?2    #   
   8 **QL    WOBL  LDX   1  FX2  
  10 *B==          MHUNTW   2,AMXOR,IOUT 
  20 *BPW          LDX   6  IPBIDE+1(2)         [ROOM LEFT IN BLOCK AFTER FIRST  
  16 *C9G          ANDN  6  #7777               [RECORD INSERTED 
  20 *CP6    NOTF  LDCT  0  4                   [INDICATE END OF RESTART SECTION 
  10 *D8Q          ORS   0  IPBIDE+1(2)  
  20 *DDJ          ORS   0  IWORK9(1)           [SET ^FIRST REC OF SECTION^ BIT  
  20 *DJF          SEGENTRY K63IPBLISTB         [USED BY IPBNOPCK RESTORE MACRO  
  19 *DNB    NOWTH LDXC  5  AWORK1(1)           [NPU SIZE IN CHARS IN B1-11  
   8 *F82          SRL   5  12   
  18 *FML          SBN   5  SAFETMARGIN         [REPLACE SAFETY MARGIN   
   9 *G7=          STOZ     IBSW(2)  
  19 *GLW          SBX   5  6                   [X5 = CT. OF CHARS DEPOSITED 
  10 *HL6          DSA   5  IPBIDE+1(2)  
  10 *J5Q          ADN   5  IDATA-A1*4+3 
  20 *JKB          SRL   5  2                   [REQUIRED AMXOR/IOUT BLOCK LENGTH
  10 *K52          TXU   5  ALOGLEN(2)   
   8 *KJL          BCC      PREN 
   8 *L4=          ALTLEN   2,5  
  16 *LHW    PREN  BZE   3  OUDEL               [END OF LISTING  
   4 *M3G    #   
  20 *MH6    #     RETURN HERE FROM IPBLISTC IF PRINTER HELD AND THEN RE-ENGAGED 
   4 *N2Q    #   
   6 *NGB    SK3IPBLISTB 
  18 *P22          CALL  7  TMF                 [TEST MOP OFF OR FINISH  
  10 *PFL          LDXC  5  ISTATUS(3)   
  18 *P_=          BCS      TINOP               [IDENTIFIER INOPERABLE   
  10 *Q57          LDCH  0  ISTATUS(3)   
   7 *Q94          ANDN  0  2
   8 *Q#_          BNZ   0  TINOP
   8 *QDW          LDCT  0  #400 
   7 *QYG          ADXC  5  5
   8 *RD6          BCC      NOWAR
   8 *RXQ          ADN   0  #2000
   9 *SCB          ORS   0  IWAIT(3) 
   6 *SX2          COOR1 
  15 *TBL          BRN      K3IPBLISTB          [TRY AGAIN   
   4 *TW=    #   
   9 *W*W    NOWAR ORS   0  IWAIT(3) 
  19 *WTG          LDX   3  IADIN(3)            [ADDRESS OF IPBIN ACTIVITY   
  10 *X*6          LDX   3  IPBRING(3)   
  19 *XSQ          SBN   3  IPBRING             [ADDRESS OF IPBOUT ACTIVITY  
  10 *Y#B          LDX   2  ACTRING(3)   
  19 *YS2          SBN   2  ACTRING             [FIND ACTIVITY AFTER IPBOUT  
   9 *_?L          LDX   4  BPTR(2)  
   8 *_R=          LDX   2  FX2  
   9 B2=W          LDEX  5  AWORK1(2)
  10 B2GN          SEGENTRY   K54IPBLISTB
  19 B2QG          ADS   5  ALFTRANS(2)         [UPDATE TRANSMISSIONS COUNT  
  19 B3=6          MHUNTW   1,AMXOR,IOUT        [THIS WILL BE THE LAST OUTPUT
   7 B3PQ          LDN   0  1
  17 B49B          ERS   0  IMOPTY(1)           [WAKE ME AFTER OUTPUT
   8 B4P2          BRN      NOWCH
  10 B58L    NEBO  MHUNTW   1,AMXOR,IOUT 
  10 B5N=    NOWCH LDX   0  ALOGLEN(1)   
   9 B67W          SBN   0  IDATA-A1 
  19 B6MG          ADS   0  ALFWORDS(2)         [COUNT OF WORDS TRANSFERRED  
   8 B776          CHAIN    1,4  
   8 B7LQ          BCT   5  NEBO 
  19 B86B          DEX   5  AWORK1(2)           [CLEAR COUNT OF OUTPUT BLOCKS
   4 B8L2    #   
   8 B95L          LDCT  0  #100 
  18 B9K=          ANDX  0  IPBSW(3)            [B2 SET IF IPBOUT ASLEEP 
   8 B=4W          BZE   0  WAKE 
  16 B=JG          ERS   0  IPBSW(3)            [INDICATE ON LIST
   7 B?46          LDX   2  3
   6 B?HQ          FPUT  
   6 B#3B    WAKE  COOR1 
   8 B#H2          BRN      NEWS 
   4 B*2L    #   
  20 B*G=    TINOP LDN   7  0                   [INDICATE LISTING NOT FINISHED   
  20 B*_W          LDCH  0  ISTATUS(3)          [B4 SET IF UNAV RATHER THAN HELD 
   8 BBFG          LDX   2  FX2  
   7 BB_6          ANDN  0  2
  21 BCDQ          DEX   0  AWORK4(2)           [0 IF HOLD BUTTON ; 2 IF STOP BUTTON 
   8 BCJM          LDCT  0  #030 
  10 BCNJ          ORS   0  ISTATUS(3)   
  10 BCSF          ERS   0  ISTATUS(3)   
  10 BCYB          ACROSS   IPBLISTC,2   
   4 BDD2    #   
   9 BDXL    #     SETUP FILE EMPTY  
   4 BFC=    #   
   5 BFWW    SEMPT   
   8 BGBG          LDN   4  24   
   7 BGW6          LDN   3  5
  16 BK#=          LDN   6  SEMP(1)             [SETUP FILE EMPTY
  10 BKRW          MHUNTW   2,AMXOR,IOUT 
  19 BQ8G          SBS   4  IPBIDE+1(2)         [DECREASE ROOM LEFT IN BLOCK 
  15 BRMB          LDX   0  PRINT(1)            [#76327632   
  19 BS72          STO   0  IDATA(2)            [INSERT TWO NEWLINES FOR LP  
  10 BSLL          LDN   7  IDATA+1(2)   
   8 BT6=          MOVE  6  0(3) 
  18 BTKW          LDN   3  0                   [INDICATE END OF LISTING 
   8 BW5G          BRN      WOBL 
   4 BWK6    [   
   4 BX4Q    [   
   8 BXPH    OUDEL LDX   2  FX2  
   9 BXR?          LDX   0  AWORK4(2)
   8 BXR#          BNG   0  OUDEM
  10 BXR*          MHUNTW    2,AMXOR,ADCB
  12 BXRB          JBC      OUDEM,2,ADCBTSPOOL   
  12 BXRC          JBC      OUDEM,2,ADCB7502LP   
  10 BXRD          ACROSS   IPBLISTD,7   
   5 BXRF    OUDEM   
  10 BXRM          MHUNT    2,AMXOR,IOUT 
   8 BXS4          LDCT  0  #40  
  17 BXSF          ORS   0  IPBIDE+1(2)         [ REPORT OUTPUT SENT 
   8 BXSM          LDX   2  FX2  
  17 BXSP          LFBBS    M,2,SAYCOR              [J IF READ ERROR 
  10 BXS_          ACROSS   IPBLISTC,1   
   4 BY97    [   
   4 BY=W    [   
   5 BY#K    SAYCOR  
   8 BYB#          LFBITCLR M,2  
  10 BYD3          ACROSS   IPBLISTC,3   
   4 B_3=    #END
   8 ____ ...10400502000600000000
