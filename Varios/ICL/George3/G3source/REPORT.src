  14 22FL    #LIS  K0REPORT>K0ALLGEO>K0GREATGEO>K0COMMAND
  16 22_=    #SEG  REPORT                       [RICHARD GRIMWADE
   9 23DW                   8HREPORT 
  14 23YG          SEGENTRY K5REPORT,SENTRY5    [TANAMONS
  16 24D6          SEGENTRY K7REPORT,SENTRY7    [REPORT COMMAND  
  14 24XQ          SEGENTRY K8REPORT,SENTRY8    [ANAMONST
  14 25CB          SEGENTRY K9REPORT,SENTRY9    [CANAMONS
  15 25X2          SEGENTRY K10REPORT,SENTRY10  [BANAMONST   
  14 26BL          SEGENTRY K11REPORT,SENTRY11  [SJANAMON
   4 26HS ...[   
  19 26P2 ...[     SEGENTRY K70REPORT USED BY RESTORE TIME MACRO REPORTNEST  
   4 26W=    #   
  19 27*W    #     THIS SEGMENT CREATES A BIT PATTERN CORRESPONDING TO THE   
  18 27TG    #     MONITORING STYLES SPECIFIED,PROVIDED ALL STYLES ARE   
  19 28*6    #     CORRECT,OTHERWISE A COMMAND ERROR REPORT IS MADE FOR EACH 
  10 28SQ    #     PARAMETER IN ERROR.   
   4 29#B    #   
  19 29S2    #     IT IS ENTERED AT K7REPORT FROM THE REPORT COMMAND AND IN  
  20 2=?L    #     THIS CASE THE MASK IS RETURNED IN AMXMSK OF THE ACTIVITY BLOCK
   4 2=R=    #   
  19 2?=W    #     FOR ENTRIES K5REPORT AND K7REPORT THE MASK IS RETURNED IN 
   7 2?QG    #     ACOMMUNE1 
   4 2#=6    #   
  20 2#PQ    #     FOR ENTRIES K8REPORT,K9REPORT AND K10REPORT THE SEGMENT WILL  
  20 2*9B    #     ALSO CHECK FOR RETAIN AND IF PRESENT WILL CHECK VALIDITY OF   
  21 2*P2    #     FILENAME.THIS AND THE MASK ARE RETURNED IN AN ADATA/AREPORT BLOCK 
   4 2B8L    #   
   8 2B?9 ...      FSHCODE  AORB 
   4 2B*S ...(   
   8 2BDC ...ZINA           4HINA
   8 2BH2 ...ZINB           4HINB
   4 2BKK ...)   
  18 2BN=    REPALL  CATMASK EN,JL,CR,CL,CM,LG,PM,FL,CT,CE,OJ,LS,OL,DP   
  13 2C7W    REPNEC  CATMASK READY,BREAKIN,FORCETYP  
   9 2CMG    TAN         + ALLBITS   
   9 2D76            CATMASK FORCEPRI
   9 2DLQ    SJAN           +ALLBITS 
   8 2F6B                   +0   
   7 2FL2    RT             2
   8 2G5L                   4HRT 
   7 2GK=    RETAIN         6
   9 2H4W                   8HRETAIN 
   9 2HJG    SUBALL         +ALLBITS 
  10 2J46    NECALL  CATMASK  FORCEPRI   
  20 2JHQ    TABL           8HALLBUT            [TABLE OF LONG VERSIONS OF PARAMS
   9 2K3B                   8HFULLBUT
   8 2KH2                   4HALL
   8 2L2L                   +0   
   9 2LG=                   4HFULL   
   8 2L_W                   +0   
   9 2MFG                   4HNONE   
   8 2M_6                   +0   
  10 2NDQ                   8HENGINEER   
   9 2NYB                   8HJOURNAL
  10 2PD2                   8HOPERATOR   
   9 2PXL                   8HCLUSTER
   9 2QC=                   8HCENTRAL
  10 2QWW                   8HBROADCAS   
  10 2RBG                   8HCOMMANDS   
   9 2RW6                   8HLOGGING
  10 2S*Q                   8HPOSTMORT   
   9 2STB                   8HFILES  
   9 2T*2                   8HCOMMENT
   9 2TSL                   8HCOMERR 
   9 2W#=                   8HOBJECT 
   9 2WRW                   8HLISTING
   9 2X?G                   8HONLINE 
   9 2XR6                   8HDISPLAY
   8 2Y=Q                   +0   
  21 2YQB    TABS           4HAB                [TABLE OF SHORT VERSIONS OF PARAMS   
   8 2_=2                   4HFB 
   8 2_PL                   4HAL 
   8 329=                   4HFU 
   8 32NW                   4HNO 
   8 338G                   4HEN 
   8 33N6                   4HJL 
   8 347Q                   4HOP 
   8 34MB                   4HCR 
   8 3572                   4HCL 
   8 35LL                   4HBC 
   8 366=                   4HCM 
   8 36KW                   4HLG 
   8 375G                   4HPM 
   8 37K6                   4HFL 
   8 384Q                   4HCT 
   8 38JB                   4HCE 
   8 3942                   4HOJ 
   8 39HL                   4HLS 
   8 3=3=                   4HOL 
   8 3=GW                   4HDP 
   8 3?2G                   +0   
  14 3?G6    TAB            +ALLBITS            [ALLBUT  
  14 3?_Q                   +FULLBITS           [FULLBUT 
  13 3#FB    TAB1           +ALLBITS            [ALL 
  13 3#_2                   +FULLBITS           [FULL
  13 3*DL                   #40000000           [NONE
   8 3*Y=    TAB2  CATMASK  EN   
   8 3BCW          CATMASK  JL   
  14 3BXG          CATMASK  CR,CL               [OPERATOR
   8 3CC6          CATMASK  CR   
   8 3CWQ          CATMASK  CL   
   8 3DBB          CATMASK  BC   
   8 3DW2          CATMASK  CM   
   8 3F*L          CATMASK  LG   
   8 3FT=          CATMASK  PM   
   8 3G#W          CATMASK  FL   
   8 3GSG          CATMASK  CT   
   8 3H#6          CATMASK  CE   
   8 3HRQ          CATMASK  OJ   
   8 3J?B          CATMASK  LS   
   8 3JR2          CATMASK  OL   
   8 3K=L          CATMASK  DP   
   4 3KCS ...[   
   4 3KK2 ...[   
  20 3KQ=    REPORT                             [ENTRY FOR OTHER THAN EJ,LT,CN   
   8 3L9W          LDX   0  GSIGN
  17 3LPG          STO   0  AWORK1 (2)          [MARK AS NOT EJ,LT,CN
   9 3M96          STOZ     AWORK3(2)
   5 3MNQ    REPORT1 
  20 3N8B          SBX   6  FX1                 [ENTRY FOR EJ,LT,CN -LINK IS X6  
  18 3NN2          STOZ     AWORK2(2)           [SET UP 1ST PARAM MARKER 
   4 3P7L    NEXT
   7 3PM=          PARAPASS  
  16 3Q6W          MHUNT    2,CPB,CUNI          [GET NEXT PARAM  
   9 3QLG          LDX   4  ANUM(2)  
  16 3R66          BNG   4  ZEND                [JUMP IF NO MORE 
  17 3RKQ          BZE   4  PFREE               [JUMP IF NULL PARAM  
   9 3S5B          LDX   3  APARA(2) 
   4 3S5_ ...[   
   8 3S6J ...      FSHCODE  AORB 
   4 3S7Q ...(   
   8 3S8Y ...      SBN   5  TAN  
  18 3S=6 ...      BNZ   5  NOTTAN              [J IF NOT TANAMONST MACRO
  15 3S?# ...      BXE   3  ZINA(1),XIN         [J IF 'INA'  
  16 3S#G ...      BXU   3  ZINB(1),NOTTAN      [J IF NOT 'INB'  
   4 3S*N ...XIN 
  14 3SBW ...      LDN   5  TAN                 [RESET X5
  17 3SD4 ...      BRN      NEXT                [TRY NEXT PARAMETER  
   5 3SF= ...NOTTAN  
  14 3SGD ...      ADN   5  TAN                 [RESET X5
   4 3SHL ...)   
   4 3SJ# ...[   
   8 3SK2          ANDN  4  #7777
   7 3T4L          SBN   4  2
  18 3TJ=          BZE   4  WS                  [JUMP IF 2LETTER PARAM   
   7 3W3W          SBN   4  7
  17 3WHG          BPZ   4  RTEST               [JUMP IF >8 LETTERS  
  10 3X36          LDX   4  APARA+1(2)   
   8 3XGQ          BRN      WL   
   4 3XQJ ...[   
   7 3Y2B    WS1   ADN   1  1
  18 3YG2    WS    LDX   0  TABS(1)             [NEXT ENTRY IN SHORT LIST
  18 3Y_L          BZE   0  RTEST               [JUMP IF NO MORE ENTRIES 
   7 3_F=          TXU   0  3
  18 3_YW          BCS      WS1                 [JUMP IF PARAM NOT ENTRY 
   8 42DG          BRN      SETB 
   4 42N# ...[   
   7 42Y6    WL1   ADN   1  2
  21 43CQ    WL    LDX   0  TABL(1)             [GET 1ST WORD OF ENTRY IN LONGLIST   
  18 43XB          BZE   0  RTEST               [JUMP IF NO MORE ENTRIES 
   7 44C2          SBX   0  3
  19 44WL          BNZ   0  WL1                 [JUMP UNLESS 1ST WORDS AGREE 
   9 45B=          LDX   0  TABL+1(1)
  19 45TW          BZE   0  WL2                 [JUMP IF 4 OR LESS LETTERS   
   7 46*G          SBX   0  4
  19 46T6          BNZ   0  WL1                 [JUMP UNLESS 2ND WORDS AGREE 
   8 47#Q    WL2   ADX   1  FX1  
  17 47SB          SRL   1  1                   [FIND ENTRY NUMBER   
  18 48#2    SETB  LDX   3  TAB(1)              [GET CORRESPONDING MASK  
   8 48RL          SMO      FX2  
   9 49?=          LDX   0  AWORK2   
   8 49QW          SMO      FX1  
   7 4==G          SBN   1  5
   8 4=Q6          BPZ   1  PNORM
   8 4?9Q          BZE   0  PFRST
   9 4?PB          CALL  4  ZERROR   
   7 4#92          +IPTWIT   
   8 4#NL          BRN      PERR1
   4 4#TS ...[   
   4 4*32 ...[   
   7 4*8=    PFRST ADN   1  3
  16 4*MW          BPZ   1  PNORM                [JUMP IF NOT BUT
  20 4B7G          LDX   0  3                   [STORE  PATTERN IN MARKER WORD   
   8 4BM6          BRN      PANY 
  16 4C6Q    PNORM ORX   7  3                   [ADD IN NES BITS 
  17 4CLB          ORX   0  GSIGN               [SET NOT FIRST MARKER
   8 4D62    PANY  SMO      FX2  
   9 4DKL          STO   0  AWORK2   
   8 4F5=          BRN      PFREE
   4 4F=D ...[   
   4 4FCL ...[   
  16 4FJW    RTEST SMO      FX2                 [TEST FOR RETAIN 
   9 4G4G          LDX   0  AWORK1   
  18 4GJ6          BNG   0  PERR                [J IF RETAIN NOT ALLOWED 
  10 4H3Q          LDX   0  JPARNUM(2)   
   8 4HHB          SMO      FX2  
   9 4J32          TXU   0  AWORK1   
  19 4JGL          BCC      PFREE                [J IF BANAMONST PARAMETER   
   8 4K2=          SMO      FX2  
   9 4KFW          TXU   0  AWORK3   
  15 4K_G          BCC      PFREE                [J IF RETAIN
  17 4LF6    PERR  CALL  4  ZERROR              [OUTPUT ERROR MESSAGE
   7 4LYQ          +JUNKEN   
  17 4MDB    PERR1 MHUNT 2,CPB,CUNI             [FIND  CPB,CUNI BLOCK
  15 4MY2    PFREE FREECORE 2                   [FREE  BLOCK 
   8 4NCL          BRN      NEXT 
   4 4NJS ...[   
   4 4NQ2 ...[   
  19 4NX=    ZEND  FREECORE 2                   [RETURN LAST CPB/CUNI BLOCK  
  21 4PBW          LDXC  0  AWORK2(2)           [GET MARKER WORC AND CLEAR SIGN BIT  
  19 4PWG          BZE   0  ZEND1               [JUMP IF NO ALLBUT OR FULLBUT
   7 4QB6          ANDX  7  0
  17 4QTQ          ERX   7  0                   [NEGATE SENSE OF MASK
  15 4R*B    ZEND1 ADX   6  FX1                 [ADJUST LINK 
   8 4RT2    #SKI  K6REPORT>99$99
   8 4S#L          TRACE 7,REPORT
   7 4SS=          EXIT  6  0
   4 4SY7 ...[   
   4 4T44 ...[   
   4 4T7_ ...[   
  21 4T?W    ZERROR                             [SUBROUTINE TO OUTPUT ERROR MESSAGE  
   7 4TRG          SMO      4
  17 4W?6          LDX   3  0                   [FIND MESSAGE WORD   
   8 4WQQ          SBX   4  FX1  
   7 4X=B          ERRORX   3
   8 4XQ2          ADX   4  FX1  
  16 4Y9L          EXIT  4  1                   [EXIT TO CALL+1  
   4 4Y*H ...[   
   4 4YFD ...[   
   4 4YK* ...[   
  18 4YP=    SENTRY7                            [ENTRY FOR REPORT COMMAND
   7 4_8W          STOZ     7
   9 4_NG          CALL  6  REPORT   
  17 5286          TERROR   XERR               [JUMP IF COMMAND ERROR
  18 52MQ          ANDX  7  REPALL(1)           [CLEAR  UNALLOWED BITS   
  18 537B          ORX   7  REPNEC(1)           [ADD IN NECCESARY BITS   
  17 53M2          STO   7  AMXMSK(2)           [STORE GENERATED MASK
   6 53N3 ...#UNS CMXMSK 
   4 53P4 ...(   
  10 53Q5 ...      MHUNTW   3,CPB,CALAS  
   4 53R6 ...[   
   9 53S7 ...      SEGENTRY K70REPORT
   8 53T8 ...      BRN      XK71 
   9 53W9 ...      STO   7  CMXMSK(3)
   8 53X= ...      BRN      XK72 
   4 53Y? ...XK71
   9 544C ...      SEGENTRY K71REPORT
   9 544M ...      STOZ     CMXMSK(3)
   4 544X ...[   
   4 5457 ...XK72
   4 545D ...)   
   6 546L    XERR  ENDCOM
   4 54?S ...[   
   4 54F2 ...[   
  19 54L=    SENTRY5                            [ENTRY FOR TANAMOS (TRACE)   
  20 555W          LDN   5  TAN                 [POINTER TO UNALLOWED/NEC MASKS  
   8 55KG          BRN      ZSJT 
   4 55T# ...[   
  21 5656    SENTRY11                           [ENTRY FOR SJANAMON (SYSTEM JOURNAL) 
  20 56JQ          LDN   5  SJAN                [POINTER TO UNALLOWED/NEC MASKS  
  19 574B    ZSJT  LDN   7  0                   [MARK AS RETAIN NOT ALLOWED  
   9 57J2          CALL  6  REPORT   
  18 583L          TERROR   ZUP                 [JUMP IF COMMAND ERROR   
   7 58H=          LDX   3  5
  18 592W          ADX   3  FX1                 [REMOVE UNALLOWED BITS   
   8 59GG          ANDX  7  0(3) 
  18 5=26          ORX   7  1(3)                [ADD IN NECCESSARY BITS  
  17 5=FQ          STO   7  ACOMMUNE1(2)        [STORE GENERATED MASK
   7 5=_B          UPPLUS   1
   4 5?6J ...[   
   4 5??Q ...[   
  16 5?F2    SENTRY8                            [ANAMOST ENTRY   
  16 5?YL    SENTRY9                            [CANAMOS ENTRY   
   9 5#D=          STOZ     AWORK1(2)
   8 5#XW          BRN      ZJOIN
   4 5*7N ...[   
  16 5*CG    SENTRY10                           [BANAMONST ENTRY 
  10 5*X6          LDX   0  ACOMMUNE1(2) 
   9 5BBQ          STO   0  AWORK1(2)
   9 5BWB    ZJOIN STOZ     AWORK3(2)
  21 5CB2          SETNCORE  5,3,ADATA,AREPORT   [SETUP ADATA,AREPORT BLOCK TO HOLD  
  19 5CTL                                       [REPORT STYLES AND FILENAME  
  19 5D*=          PARABEG  1,RETAIN(1),,,0     [FIND ANY RETAIN PARAMETER   
  10 5DSW          MHUNT    2,CPB,CUNI   
   9 5F#G          LDX   4  ANUM(2)  
  17 5FS6          BPZ   4  RET                 [JUMP IF RET FOUND   
   4 5G3Y ...[   
  17 5G?Q          FREECORE 2                   [FREE CPB/CONI BLOCK 
  18 5GRB          PARABEG  1,RT(1),,,0         [FIND ANY RT PARAMETER   
  10 5H?2          MHUNT    2,CPB,CUNI   
   9 5HQL          LDX   4  ANUM(2)  
  16 5J==          BPZ   4  RET                 [JUMP IF RT FOUND
   4 5JG4 ...[   
  11 5JPW          MHUNTW   3,ADATA,AREPORT  
  21 5K9G          STOZ     A1+1(3)             [STORE ZERO FILENAME SINCE NORETAIN  
   9 5KP6          STOZ     A1+2(3)  
  21 5L8Q          LDN   7  0                   [MARK AS NO RETAIN FOR REPORT CALL   
   8 5LNB          BRN      RCALL
   4 5LY8 ...[   
  19 5M82    RET   LDX   3  JPARNUM(2)          [NUMBER OF RETAIN PARAMETER  
   8 5MML          SMO       FX2 
   9 5N7=          STO   3  AWORK3   
  20 5NLW          LDX   7  GSIGN               [MARK AS RETAIN FOR REPORT CALL  
  18 5P6G          BZE   4  RETERR              [JUMP IF RETAIN/RT ONLY  
   4 5PB# ...[   
   9 5PL6          LDX   5  ANUM(2)  
   8 5Q5Q          FREECORE  2   
  19 5QKB          ANDN  5  #7777               [CHARACTER COUNT OF FILENAME 
  17 5R52          PARALYSE ,,3                 [SPLIT  RET PARAMETER
   7 5R=8 ...#UNS  ANSTOOMANY
   4 5RCB ...#SKI
  21 5RJL          TESTREPN UNPAIR,RFIL         [JUMP IF UNPAIRED BRACKET NOT FOUND  
   7 5RL* ...#UNS  ANSTOOMANY
   4 5RN4 ...(   
  14 5RPR ...      TESTREP2 TOOMANY,TOOMUCH,UNPAIR,RTUNP 
  18 5RRG ...      BRN      RFIL                    [J IF RET PARAM IS OK
   4 5RT9 ...[   
   5 5RWY ...RTUNP   
  13 5RYM ...[     UNPAIRED DELIMITER IN RET PARAM   
   4 5S2B ...)   
  17 5S4=          SPARANOX 3                   [GET RET PARAMETER   
  17 5SHW          CALL  4  ZERROR              [OUTPUT COMMAND ERROR
   7 5T3G          +ERUNPAIR 
   8 5TH6          BRN      RCALL
   4 5TQY ...[   
  21 5W2Q    RFIL  PARANUMB 4                   [GET NUMBER OF PARAMETERS AFTER SPLIT
   7 5WGB          SBN   4  2
  19 5X22          BNZ   4  RETERR              [JUMP IF NOT  2 PARAMETERS   
   7 5XFL          PARANOT  2
  10 5X_=          MHUNT    2,CPB,CUNI   
   9 5YDW          LDX   6  ANUM(2)  
   8 5YYG          ANDN  6  #7777
  17 5_D6          FREECORE 2                   [FREE LAST UNIBLOCK  
   7 5_XQ          TXU   6  5
  19 62CB          BCS      RETERR              [JUMP IF LENGTHS NOT EQUAL   
   4 62M8 ...[   
   7 62X2          LDN   4  2
  16 63BL          PARALYSE ,,4                 [REMOVE BRACKETS 
   7 63HS ...#UNS  ANSTOOMANY
  11 63P2 ...      TESTREP2 TOOMANY,TOOMUCH  
  16 63W=          SPARAPAS                     [GET FILE NAME   
   7 64*W          LDX   4  3
  17 64TG          CHEKLFN2 NULLP,NLFNF         [JUMP IF NOT FILENAME
   7 65*6          LDX   3  4
  10 65SQ          MHUNT    2,CPB,CUNI   
  11 66#B          MHUNTW   1,ADATA,AREPORT  
   9 66S2          LDX   4  ANUM(2)  
   7 67?L          ADN   4  3
  18 67R=          SRL   4  2                   [LOAD ADATA/AREPORT BLOCK
  16 68=W          STO   4  A1+1(1)             [WITH FILENAME   
   9 68QG          LDN   5  APARA(2) 
   9 69=6          LDN   6  A1+2(1)  
   7 69PQ          MOVE  5  3
  17 6=9B          NAMETOP  2,FILE,FNAME     [RENAME CPB/CUNI BLOCK  
   4 6==Y ...[   
   6 6=#G ...#UNS  FTS1  
  17 6=CL ...      USEROPEN XBRK,WRITE,DIR,FROZEN  [OPENFILEDIRECTORY
   6 6=GQ ...#UNS  FTS1  
   4 6=KW ...#SKI
  17 6=P2          USEROPEN XBRK,WRITE,DIR      [OPENFILEDIRECTORY   
   4 6=YS ...[   
  17 6?8L          TESTREP2 NOFILE,RCALL        [JUMP IF NO SUCH FILE
  18 6?N=          REPERR2  RCLOS               [JUMP IF OK ELSE ERROR   
   8 6#7W          BRN      RCALL
   4 6#CN ...[   
  15 6#MG    RCLOS CLOSETOP                     [CLOSE FILE  
   9 6*76 ...RCALL VFREE    CPB,CUNI 
   7 6*LQ          PARAFREE  
   9 6B6B          CALL  6  REPORT1  
  18 6BL2          TERROR   WRONG               [JUMP IF COMMAND ERROR   
  18 6C5L          ANDX  7  SUBALL(1)           [REMOVE UNALLOWED BITS   
   9 6D4W          BZE   7  NOBITS   
   9 6DJG          ORX   7  NECALL(1)
   5 6F46    NOBITS  
  11 6FHQ          MHUNTW   3,ADATA,AREPORT  
  16 6G3B          STO   7  A1(3)               [STORE  AWAY MASK
   7 6GH2          UPPLUS   1
   4 6GLX ...[   
   4 6GQS ...[   
   4 6GWP ...[   
   9 6H2L    NULLP MFREE    CPB,CUNI 
   5 6HG=    RETERR  
   7 6H_W          LDN   6  0
   8 6JFG          SPARANOX 3,6  
  17 6J_6          CALL  4  ZERROR              [OUTPUT ERROR MESSAGE
   7 6KDQ          +APFERR   
   8 6KYB          BRN      RCALL
   4 6L5J ...[   
   4 6L=Q ...[   
  17 6LD2    NLFNF CALL  4  ZERROR              [OUTPUT ERROR MESSAGE
  17 6LXL                                       [PARAM FORMAT ERROR  
   7 6MC=          +JNLFNF   
   8 6MWW          BRN      RCALL
   4 6MYK ...[   
   4 6N2# ...[   
   5 6N43 ...TOOMUCH 
  17 6N5Q ...      CALL  4  ZERROR              [OUTPUT ERROR MESSAGE
  18 6N7F ...     +JMAXPAR                      [MAX NO. PARAMS EXCEEDED 
   8 6N98 ...      BRN      RCALL
   4 6N=X ...[   
   4 6N#L ...[   
   9 6NBG    XBRK  GEOERR   1,BRKIN  
   4 6NL# ...[   
  10 6NW6    WRONG MFREEW   ADATA,AREPORT
   5 6P*Q    ZUP   UP
   4 6PGY ...[   
   4 6PN6 ...[   
  10 6PTB          MENDAREA 30,GAPREPORT 
   4 6Q*2    #END
   8 ____ ...26740304000200000000
