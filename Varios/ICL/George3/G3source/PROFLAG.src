  14 22FL ...      SEG      PROFLAG,,G. CONSTANTINIDES.  
  15 22_=    #LIS  K0PROFLAG>K0MULTIPLX>K0ALLGEO>K0GREATGEO  
   7 23YG    # ENTRY POINTS  
  12 24D6          SEGENTRY K1PROFLAG,Z1PROFLAG  
   6 24K# ...#SKI JSKI14 
  12 24QG ...      SEGENTRY K92PROFLAG,X92PROFLAG
  11 24XQ          MENDAREA 30,K100PROFLAG   
   4 25CB    [   
  16 25X2    [THIS SEGMENT DEALS WITH FLAGS ON THE MULTIPLEXOR   
   4 26BL    [   
  14 26W=    TABM           #11                 [OUTPUT. 
  16 27*W                   #13                 [OUTPUT + INPUT. 
  18 27TG                   #40000000           [SPECIAL FOR B.I. IDLE   
  18 28*6    #DEF           NMODES=0?-TABM      [NUMBER OF POSSIBLE MODES
   8 28SQ    TABJ  BRN      OUT  
   8 29#B          BRN      OUTIN
   8 29S2          BRN      UNSET
   8 2=?L    TC1            #7621
   9 2=R=    SCANCEL        #76100000
   9 2?=W    PMASK          #77737777
  20 2?QG    QMASK          #20040000           [MASKS FOR NEW LINE SUPPRESSION. 
   9 2#=6    RMASK          #40000   
   8 2#PQ    TIMSK          #40:3
  10 2*9B ...      SEGENTRY K50PROFLAG   
   8 2*P2    SERROR         +34  
  16 2B8L                   34HTRANSMISSION ERROR - PLEASE REPEAT
   8 2BN=    SERRORA        +18  
  12 2C7W                   18HTRANSMISSION ERROR
   8 2CMG    SBI            +8   
  10 2D76                   8HBREAK IN   
   5 2DLQ    SAOUT   
   8 2F6B    #HAL  AMXOR+AOUT,0  
   4 2FL2    [   
  20 2G5L    #  THIS SUBROUTINE WAKES CPA IF ASLEEP AND UNSETS THE WAITING BIT   
   4 2GK=    #   
   9 2GTS ...XLIST HUNTACT  CPAT,2,3 
   9 2H6B ...      LDXC  4  GWAIT(3) 
   8 2HBY ...      BCC      TOLCL
   9 2HMG ...      STO   4  GWAIT(3) 
   6 2HY4 ...      FPUT  
   7 2J8L ...      EXIT  5  0
  11 2JF8 ...TOLCL LONGCLEAR 2,AWAITMOP,RESFX
   8 2JPQ ...RESFX LDX   1  FX1  
   8 2K2# ...      LDX   2  FX2  
   7 2K=W ...      EXIT  5  0
   6 2KHG ...#SKI JSKI14 
   4 2KJ2 ...(   
   8 2KJG ...RCHN  LDX   2  FX1  
   8 2KK2 ...      SBN   2  A1   
   9 2KKG ...      LDX   1  ATYPE(2) 
   8 2KL2 ...      BNZ   1  ZNNN 
   9 2KLG ...      LDX   1  BPTR(2)  
   8 2KM2 ...      BZE   1  ZNNN 
   9 2KMG ...      SMO      FPTR(2)  
   8 2KN2 ...      STO   1  BPTR 
   9 2KNG ...      LDX   1  FPTR(2)  
   9 2KP2 ...      SMO      BPTR(2)  
   8 2KPG ...      STO   1  FPTR 
   9 2KQ2 ...      STOZ     BPTR(2)  
   9 2KQG ...      STO   2  FPTR(2)  
   8 2KR2 ...ZNNN  LDX   1  FX1  
   8 2KRG ...      LDX   2  FX2  
   7 2KS2 ...      EXIT  0  0
   7 2KSG ...RMV   LDEX  3  4
   8 2KT2 ...      BZE   3  ZEMV 
   7 2KTG ...      SMO      3
   7 2KW2 ...NMV   MOVE  1  0
   7 2KWG ...      ADX   1  3
   7 2KX2 ...      ADX   2  3
   7 2KXG ...      SBX   4  3
   8 2KY2 ...ZEMV  LDN   3  512  
   8 2KYG ...      BNZ   4  NMV  
   7 2K_2 ...      EXIT  0  0
   4 2K_G ...)   
   4 2L2L    #   
   4 2LG=    [   
  21 2L_W    [THIS ENTRY POINT OBEYS A MODE *4000 PERI TO SET UP THE FLAG AREA AND   
  13 2MFG    [PUTS THE SCANNING ACTIVITY ON THE LIST 
   4 2M_6    [   
   6 2NDQ    Z1PROFLAG   
   6 2NKY ...#SKI JSKI14 
   8 2NR6 ...      CALL  0  RCHN 
  18 2NYB          LDX   3  2                   [FIND SCANNING ACTIVITY  
  16 2PD2    PAST1 HUNTACT  AMSCAN,2            [FOR THIS DEVICE 
  11 2PXL          BXU   3  AMOPADDR(2),PAST1
  17 2QC=          FPUT                         [PUT IT ON THE LIST  
   8 2QWW          LDN   0  #4000
  12 2RBG          DCA      CPPTR(3),STO,0,MODE  
  10 2RW6          LNLI     CPPTR(3),7   
   8 2S*Q          GPERI    ,3   
   7 2STB    PERX3 SLL   7  2
   6 2T*2    PERY2 COOR1 
   6 2TG8 ...#SKI  JSKI14
   8 2TMB ...      CALL  0  RCHN 
  14 2TSL    PLOOP BZE   7  U1                  [J IF UPX
  17 2W#=          SRL   7  2                   [REDUCE TO LINE NO.  
   7 2WRW          LDX   5  7
   8 2X?G          LNLI     ,6   
   8 2XR6    NLINE BXU   5  6,NSA
   7 2Y=Q          LDN   5  0
   7 2YQB    NSA   ADN   5  1
   8 2_=2          FINDLCA  ,5   
  10 2_PL          DLCA     1,LDX,4,FLAG 
   8 329=          ANDN  4  #77  
  16 32NW          BNZ   4  SFLAG               [J. IF FLAG FOUND
  21 338G          BXU   5  7,NLINE             [J. IF NOT BEEN ROUND WHOLE FLAG AREA
   8 33N6    #SKI  ADEVELOP<1$1  
   4 347Q    (   
   8 34MB          LDCT  0  #400 
  10 3572          STO   0  CHAINADD(2)  
   8 35LL          BRN      PERX3
   4 366=    )   
   7 36KW    #SKI  ADEVELOP  
   9 375G          GEOERR   1,NO FLAG
  17 37K6    SFLAG LDX   7  5                   [PUT LINE NO. IN X7. 
  16 384Q          SLL   7  2                   [TURN INTO PTR.  
  11 38JB    U1    HUNTW    3,AMXOR,ALINES   
  20 3942          BNG   3  UNSET               [UNSET FLAG IF NO ALINES BLOCK   
   7 39HL          ADX   3  7
  20 3=3=          LDX   4  A1+FOURTHWD(3)      [GET DETAILS OF STATE OF LINE.   
  10 3=GW          LDX   3  A1+BPTR(3)   
  21 3?2G          BZE   3  NLCB                [RING ELT. NULL, J. TO ANALYSE X4.   
  19 3?G6          BNG   4  MXCON               [J. TO SET CONCEPTUAL FLAG.  
   8 3?_Q          SBN   3  A1   
   8 3#FB          JLBUSY   1,XX1
   9 3#_2          NGS   3  GTIM(3)  
   7 3*DL    XX1   LDCT  0  2
  21 3*Y=          ANDX  0  4                   [J. IF ^PERI ISSUED BY AMSCAN^ MARKED
   9 3BCW          BNZ   0  SCANLCB  
   7 3BK4 ...      LDCT  0  4
   7 3BQ= ...      ANDX  0  4
  21 3BXG          ANDN  4  #77                 [MASK OUT INTERNAL TERMINAL TYPE NO. 
  21 3CC6          BCT   4  PCT                 [J. IF NOT TYPE 1 TERMINAL (7071).   
   8 3CLY ...      BNZ   0  UNSET
   9 3CWQ          LDX   0  GLASTM(3)
  19 3DBB          BZE   0  NOTRANS             [NO TRANSFER IF GLASTM = 0   
   9 3DW2          LDN   4  NMODES   
   8 3F*L          LDX   1  FX1  
   7 3FT=    NMD   SMO      4
   9 3G#W          TXU   0  TABM-1(1)
  16 3GSG          BCC      MFD                 [J IF MODE FOUND 
   8 3H#6          BCT   4  NMD  
  19 3HRQ          GEOERR   1,MODENAL           [ERROR IF MODE NOT IN TABLE  
   7 3J?B    MFD   SMO      4
  18 3JR2          BRN      TABJ-1              [JUMP ACCORDING TO MODE  
  20 3K=L    NLCB  LDCT  0  #530                [J. IF NONE OF ^IGNORE FLAGS^,   
  20 3KQ=          ANDX  0  4                   [^MOPPING OFF^, ^SETTING UP CPA^ 
  19 3L9W          BZE   0  NIGNO               [OR ^ONLINE^ BITS ARE SET.   
  18 3LPG          ANDN  4  #77                 [MASK OUT TERMINAL TYPE. 
  21 3M96          SBN   4  ASENTMACH           [J. IF NOT SENTINEL TO IGNORE FLAG.  
   8 3MNQ          BNZ   4  UNSET
   8 3N8B          LDN   6  #4000
  11 3NN2          MHUNTW   3,AMXOR,ALINES   
  19 3P7L          SMO      7                   [MARK SENTINEL LINE ENTRY TO 
  19 3PM=          ORS   6  A1+FOURTHWD(3)      [SHOW 'ALREADY INTERRUPTED'. 
   8 3Q6W          BRN      UNSET
  11 3QLG    NIGNO MHUNTW   3,AMXOR,ALINES   
  21 3R66          LDCT  0  #424                [^IGNORE FLAGS^, ^SETTING UP^, ^CORE^
  17 3RKQ          ADXC  4  4                   [J. IF NOT ATTACHED. 
   8 3S5B          BCC      NATCH
  18 3SK2          LDCT  0  #624                [ADD IN ^ATTACHED^ BIT.  
  16 3T4L    NATCH SMO      7                   [SET STATUS MASK.
  11 3TJ=          STO   0  A1+FOURTHWD(3)   
   7 3W3W    WKIMQ LDN   4  1
  10 3WHG          SMO      IMOPADDR(2)  
  20 3X36          ADS   4  IWAKE               [ADD 1 TO COUNT OF CORE REQUESTS 
  13 3XGQ          LONGCLEAR IMOPADDR(2),#125,UNSETA 
   5 3Y2B    UNSETA  
   8 3YG2          LDX   2  FX2  
  15 3Y_L    UNSET LDX   6  7                   [GET LINE/   
   7 3_F=          SRL   6  2
   8 3_YW    UNS1  FINDLCA  ,6   
  20 42DG    PERY3 DLCA     1,LDX,4,FLAG        [CANCEL ALL FLAGS ON THIS LINE   
   8 42Y6          ANDN  4  #77  
  21 43CQ    XUFLAG                             [J HERE TO CANCEL A GIVEN NO OF FLAG 
  13 43XB                                       [S.  
  10 44C2          DLCA     1,SBS,4,FLAG 
   9 44WL          LDX   3  CPPTR(2) 
  19 45B=          SBS   4  JFAW(3)             [AND FLAG ACCUMULATION WORD  
   9 45TW          LDX   0  JFAW(3)  
  18 46*G          BNZ   0  PLOOP               [J. UNLESS NO MORE FLAGS 
   8 46T6          LDX   4  GSIGN
   7 47#Q          ADN   4  1
  18 47SB          STO   4  CHAINADD(2)         [SET CHAINADD = #40000001
   9 48#2          LDX   0  JFAW(3)  
  17 48RL          BZE   0  PERY2               [J. IF STILL NO FLAG 
   8 49?=          SMO      FX1  
   8 49QW          LDX   4  XMASK
  17 4==G          ANDS  4  CHAINADD(2)         [UNSET B0 OF CHAINADD
  10 4=Q6          LDX   4  CHAINADD(2)  
   7 4?9Q          SBN   4  1
  17 4?PB          BZE   4  PLOOP               [J. IF CHAINADD = 1  
   8 4#92          BRN      PERY2
   9 4#NL    XMASK          #37777777
  15 4*8=    MXCON LDX   1  BPTR(3)             [AMOP ADDRESS
   7 4*MW          LDCT  5  1
  19 4B7G          ANDX  5  FOURTHWD(1)         [J IF B8 IN FOURTHWRD NOT SET
  10 4BM6          ERS   5  FOURTHWD(1)  
  14 4C6Q          LDX   6  ACTUAL(3)           [LINE NO 
   8 4CLB          ANDN  6  #7777
   8 4D62          FINDLCA  ,6   
   8 4DKL          BZE   5  RNF  
   7 4F5=          LDN   4  1
   9 4FJW          BRN      XNFLAG   
  20 4G4G    RNF   LDCT  5  #777                [CONCEPTUAL LINE CONTROL SECTION 
  15 4GJ6          DLCA     1,LDX,7,FLAG        [FLAG COUNT  
   8 4H3Q          ANDN  7  #77  
  17 4HHB          ANDX  5  ACTUAL(3)           [CONCEPTUAL LINE NO. 
  20 4J32          SLC   5  7                   [TURN INTO CHARACTER MODIFIER.   
  15 4JGL          ADX   3  BSTART(3)           [LOCATE OLPA.
   9 4K2=          HUNTACT  AOLPT,3  
  18 4KFW          LDX   1  3                   [SAVE ITS START ADDRESS. 
  21 4K_G          ADX   1  5                   [GET THE FLAG COUNT FOR THIS LINE.   
   9 4LF6          LDCH  4  FLAGCT(1)
   7 4LYQ          ADX   4  7
   8 4MDB          LDN   0  64   
   9 4MY2          BXL   4  0,XUXF   
  18 4NCL          LDX   7  6                   [IGNORE THIS LOT OF FLAGS
  18 4NX=          SLL   7  2                   [RESTORE X7-NEEDIT LATER 
   8 4PBW          BRN      UNS1 
   4 4PWG    XUXF
  18 4QB6          DCH   4  FLAGCT(1)           [OTHERWISE SET NEW COUNT.
   7 4QTQ          SBX   4  7
  19 4R*B          BZE   4  XUXF1               [J IF NO FLAGS SET BEFORE NOW
  19 4RT2    YUF   LDX   4  7                   [FLAGS PASSED ON TO OBJ PROG 
   8 4S#L          FINDLCA  ,6   
   5 4SS=    XNFLAG  
   7 4T?W          LDX   7  6
   7 4TRG          SLL   7  2
  21 4W?6          BRN      XUFLAG              [J TO CANCEL THOSE FLAGS THAT HAVE   
  16 4WQQ                                       [BEEN PASSED ON  
   5 4X=B    XUXF1   
  18 4XQ2          LDCT  4  #200                [FLAG BIT FOR UNIPLEXOR. 
  16 4Y9L          BZE   5  UXCON               [J. IF UNIPLEXOR.
   8 4YP=          LDX   4  GSIGN
  20 4_8W          SLC   5  2                   [CONCEPTUAL LINE NO. IN B15-23.  
   7 4_NG          LDX   1  5
   7 5286          SRL   5  4
   8 52MQ          ANDN  1  #17  
  20 537B          SRL   4  0(1)                [CORRECT FLAG BIT FOR THIS LINE. 
  21 53M2    UXCON ADX   5  CFLAG(3)            [REL. ADDRESS OF CORRECT WORD IN F.A.
  20 546L          ADDRESS  APETADDR(3),5,1     [ABSOLUTE ADDRESS OF FLAG WORD.  
  15 54L=          ORS   4  0(1)                [SET FLAG.   
  21 555W          TICKLE   JOBNO(3),CMEMBER(3) [DESUSPEND CONTROLLING MEMBER IN O.P.
  17 55KG          BRN      YUF                 [J TO CANCEL FLAGS   
  19 5656    PCT   BCT   4  SENT                [IF NOT PCT MUST BE SENTINEL.
  18 56JQ          LDXC  4  GWAIT(3)            [IS PCT ACTIVITY ON LIST?
  18 574B          BCC      SNULL               [IF SO JUST UNSET FLAG   
  19 57J2          STO   4  GWAIT(3)            [OTHERWISE WAKE IT UP FIRST. 
  18 583L          HUNTACT  AMOP,2,3            [BLOCK TYPE AMOP/APCT.   
  20 58H=          FPUTX                        [PUT IT ON THE FRONT OF THE LIST.
   8 592W          BRN      SNULL
  21 59GG    TTYPE GEOERR   1,TERMTYPE          [ILLEGAL DEVICE TYPE IN ALINES ENTRY.
  21 5=26    SENT  BCT   4  TTYPE               [IF NOT SENTINEL IDF/SCAN HAS BOOBED.
  18 5=FQ          LDXC  4  GWAIT(3)            [J. IF ALREADY ENLISTED. 
   8 5=_B          BCC      NWAKE
  15 5?F2          HUNTACT  AMOP,2,3            [AMOP/ASENT  
   6 5?YL          FPUT  
  20 5#D=    NWAKE ADN   4  1                   [ADD 1 TO COUNT OF INTERRUPTS.   
   9 5#XW          STO   4  GWAIT(3) 
   8 5*CG          BRN      SNULL
   5 5*X6    SCANLCB 
   9 5BBQ          LDX   0  GREP1(3) 
  19 5BWB          BNG   0  SNULL               [J IF'NULL'STILL IN PROGRESS 
  21 5CB2          LDCT  0  #402                [ELSE UNSET ^AMSCAN'S PERI^ BIT, SET 
  19 5CTL          LDX   2  A1+FPTR(3)          ['IGNORE FLAGS' AND NULLIFY  
  17 5D*=          ERS   0  FOURTHWD(2)         [THE RING ELEMENTS.  
  10 5DSW          STOZ     A1+BPTR(3)   
   9 5F#G          STOZ     BPTR(2)  
  21 5FS6          UNLOCK   3                   [UNLOCK LCB AND WAKE UP SCANNING ACT.
  10 5G?Q          HUNTACT  AMSCAN,2,3   
   6 5GRB          FPUT  
  14 5H?2    SNULL LDX   6  GUNITNO(3)          [LINE NO.
   8 5HQL          BRN      UNS1 
   9 5J==    OUT   LDX   4  GREP1(3) 
  18 5JPW          BNG   4  POUT                [J IF STILL IN PROGRESS  
   9 5K9G          LDCH  0  GREP1(3) 
   7 5KP6          SRC   0  1
  16 5L8Q          BPZ   0  XINOP1              [J IF INOPERABLE 
   7 5LNB          SRC   0  1
  18 5M82          BNG   0  XERROR1             [J. IF NOT IMPLEMENTED   
   7 5MML          SRC   0  1
   9 5N7=          BNG   0  XERROR1  
   8 5NLW          ANDN  4  #376 
   8 5P6G          BNZ   4  XBI2 
  20 5PL6          STOZ     HWGROT(3)           [ZEROISE B.I. COUNT FOR GROTTY   
  14 5Q5Q                                       [HARDWARE
   8 5QKB          LDCT  0  #2   
   9 5R52          ANDX  0  GWAIT(3) 
  21 5RJL          BZE   0  NOTBI               [J. IF NOT B. I. BEFORE LAST OUTPUT. 
   9 5S4=          ERS   0  GWAIT(3) 
   8 5SHW          LDCT  0  #40  
   9 5T3G          ANDX  0  GWAIT(3) 
  20 5TH6          BZE   0  XBI                 [J. IF NOT B. I. ON ^TIMED OUT^. 
   9 5W2Q    RTO   ERS   0  GWAIT(3) 
   8 5WGB    NOTBI LDCT  0  #200 
   9 5X22          ANDX  0  GWAIT(3) 
  20 5XFL          BNZ   0  NFREE               [J. IF NO OUTPUT BLOCKS TO FREE. 
  11 5X_=          HUNT2B   1,AMXOR,AOUT,3   
   9 5YDW          LDX   0  AMOPTY(1)
  16 5YYG          ANDN  0  1                   [BREAKIN MESSAGE?
  13 5_D6          BZE   0  XYBI                [NO  
   8 5_XQ          LDCT  0  #100 
  18 62CB          ADN   0  #4000               [UNSET BREAKIN BITS(2,12)
   9 62X2          ANDX  0  GWAIT(3) 
   9 63BL          ERS   0  GWAIT(3) 
   7 63W=    XYBI  FREECORE 1
   8 64*W          BRN      NSET 
  21 64TG    NLSUP LDX   0  QMASK(1)            [UNSET NL SUPPRESS & NOT TO FREE BITS
  18 65*6    NFREE ERS   0  GWAIT(3)            [UNSET ^DO NOT FREE^ BIT.
   7 65SQ    NSET  UNLOCK   3
  17 66#B ...SBII  STOZ     GLASTM(3)           [MARK LCB NOT IN USE 
  18 66S2          CALL  5  XLIST               [WAKE UP CPA IF ASLEEP.  
   8 67?L          BRN      SNULL
   9 67R=    OUTIN LDX   4  GREP1(3) 
  20 68=W          BNG   4  XBI1                [J IF OUTPUT STILL IN PROGRESS   
   8 68QG          LDCT  5  #4   
   9 69=6          ANDX  5  GWAIT(3) 
  20 69PQ          BZE   5  PIN                 [J IF OUTPUT NOT YET DEALT WITH  
   9 6=9B          LDX   0  GREP2(3) 
  20 6=P2          BNG   0  XBI1                [J IF OUTPUT DEALT WITH AND INPUT
  17 6?8L          ERS   5  GWAIT(3)            [STILL IN PROGRESS   
   9 6?N=          LDX   0  GWAIT(3) 
   7 6#7W          ANDN  0  4
  21 6#MG          BNZ   0  XERROR1             [J IF OUTPUT PART ENDED IN 'ERROR'   
  18 6*76          LDCH  0  GREP2(3)            [OTHERWISE END OF INPUT  
   7 6*LQ          SRC   0  1
  16 6B6B          BPZ   0  XINOP1X             [J IF INOPERABLE 
  16 6BL2          LDCT  4  #200                [SET BIT TO SAY  
  18 6C5L          ORS   4  GWAIT(3)            [DON'T FREE OUTPUT BLOCK 
  19 6CK=          LDCT  4  #10                 [J. IF B. I. ON OUTPUT PART. 
   9 6D4W          ANDX  4  GWAIT(3) 
   9 6DJG          ERS   4  GWAIT(3) 
   8 6F46          BNZ   4  XBI  
   7 6FHQ          SRC   0  1
  14 6G3B          BNG   0  XERROR              [NOT IMP 
   7 6GH2          ANDN  0  1
  21 6H2L          LDX   2  AWORK1(2)           [MAX. NO. OF CHARACTERS TRANSFERABLE.
  15 6HG=          LDEX  4  GREP2(3)            [REPLY COUNT.
  20 6H_W          SBX   2  4                   [NO. OF CHARACTERS IN TRANSFER.  
  17 6JFG          SRC   2  2                   [TURN INTO MODIFIER. 
  18 6J_6          ADX   2  3                   [PTR. TO LAST CHARACTER. 
  14 6KDQ          LDN   4  4                   [COUNT.  
  10 6KYB    NCH   LDCH  6  BUFFER-1(2)  
   7 6LD2          SLL   5  6
   7 6LXL          DCH   6  5
   7 6MC=          BCHX  2  /
   8 6MWW          BCT   4  NCH  
  20 6NBG          SLC   45 12                  [X4 = 0 AFTER BRANCH ON COUNT.   
   8 6NW6          LDX   2  FX2  
   9 6P*Q          TXU   5  GEOM(3)  
  15 6PTB          BCS      TCANC               [J IF NO EOM 
  17 6Q*2          BNZ   0  XERROR              [J. IF ERROR BIT SET.
  17 6QSL          STOZ     GFAIL(3)            [CLEAR FAILURE COUNT.
   9 6R#=          TXU   4  TC1(1)   
  16 6RRW          BCC      XBI                 [J IF BREAK IN   
  21 6S?G          LDX   4  GWAIT(3)            [J. IF NEW LINE SUPPRESSION BIT SET. 
   9 6SR6          ANDX  4  RMASK(1) 
   8 6T=Q          BNZ   4  NLSUP
  18 6TQB    WAKE  CALL  5  XLIST               [WAKE UP CPA IF ASLEEP.  
   9 6W=2    WAKE1 LDX   4  GNOCHS(3)
  10 6WPL          LDN   5  GNLBUFF(3)   
   5 6X9=    OUTERR  
   9 6X?T ...      LDX   0  GLASTM(3)
  17 6XBD ...      BZE   0  TALOK               [J IF IDLE TO LOCK   
  20 6XF3 ...      BPZ   0  NREQL               [ALREADY LOCKED UNLESS BI ON IDLE
   7 6XHL ...TALOK LOCK     3
   5 6XL9 ...NREQL   
  10 6XNW          LDX   6  GUNITNO(3)   
   8 6Y8G          FINDLCA  ,6   
   9 6YN6          JLBUSY   1,PERY3  
   8 6_7Q          SLL   6  15   
  10 6_MB          DLCA     1,DLA,4,COUNT
  10 7272          DLCA     1,STO,5,CTSA 
   8 72LL          LDN   5  #11  
   9 736=          STO   5  GLASTM(3)
  10 73KW          DLCA     1,DLA,5,MODE 
   9 745G          LDN   5  GREP1(3) 
  10 74K6          DLCA     1,STO,5,CREP 
  10 754Q          DCA      ,STO,6,MODE  
  18 75JB          DLCA     1,LDX,4,FLAG        [NEXT PERI MIGHT FINISH  
  18 7642          ANDN  4  #77                 [BEFORE EXEC RETURNS TO G
   6 76HL          GLPERI
  20 773=          LDX   0  JTIME               [REMEMBER WHEN TRANSFER STARTED  
  21 77GW          STO   0  GTIM(3)             [IN CASE IT NEEDS TO BE TERMINATED   
   9 782G          BRN      XUFLAG   
   7 78G6    TCANC LDN   0  2
  16 78_Q          BXE   5  SCANCEL(1),UCANC    [J. IF CANCEL.   
   9 79FB          LDX   0  TIMSK(1) 
  19 79_2    UCANC ORS   0  GWAIT(3)            [SET TIME OUT OR CANCEL BITS.
   8 7=DL          BRN      WAKE 
   5 7=Y=    NOTRANS 
   9 7?CW          LDX   0  GFAIL(3) 
  18 7?XG          BNG   0  SNULL               [J. IF BEING DEALT WITH  
   8 7#C6          LDCT  0  #40  
   9 7#WQ          ANDX  0  GWAIT(3) 
   8 7*BB ...      BZE   0  NTRBI
  18 7*W2          ERS   0  GWAIT(3)            [UNSET ^TIMED OUT^ BIT.  
  18 7B*L          CALL  5  XLIST               [WAKE UP CPA IF ASLEEP.  
   8 7BT=          BRN      SNULL
   9 7C#W    PIN   LDCH  0  GREP1(3) 
   7 7CSG          SRC   0  1
  17 7D#6          BPZ   0  XINOP2              [J. IF INOPERABLE.   
  19 7DRQ          LDCT  5  #4                  [SET MARKER TO SAY 'END OF   
  17 7F?B          ORS   5  GWAIT(3)            [OUTPUT DEALT WITH'  
   7 7FR2          SRC   0  1
  17 7G=L          BNG   0  XINOP2              [J IF NOT IMPLEMENTED
   7 7GQ=          SRC   0  1
  17 7H9W          BNG   0  XERROR2             [J IF ERROR BIT SET  
   8 7HPG          ANDN  4  #377 
  20 7J96          BNZ   4  XBI1                [B.I. IF REMAINDER COUNT NON-ZERO
  14 7JNQ          SRC   0  1                   [OR IF   
   8 7K8B          BPZ   0  XBI1 
   8 7KN2          LDX   0  JTIME
   9 7L7L          STO   0  GTIM(3)  
  18 7LM=          LDX   6  GUNITNO(3)          [IN CASE READ HAS ALSO   
  15 7M6W          FINDLCA  ,6                  [TERMINATED  
   7 7MLG          LDN   4  1
   9 7N66          BRN      XUFLAG   
   8 7NKQ    XBI1  LDCT  0  #10  
   9 7P5B          ORS   0  GWAIT(3) 
  10 7PK2    XCANC LDX   6  GUNITNO(3)   
   8 7Q4L          FINDLCA  ,6   
  10 7QJ=          DLCA     1,LDX,4,FLAG 
   8 7R3W          ANDN  4  #77  
   9 7RHG          JLBUSY   1,XCM4   
   9 7S36          NGS   3  GTIM(3)  
   8 7SGQ          BRN      NSET 
   8 7T2B    XCM4  SLL   6  15   
   7 7TG2          ADN   6  4
  10 7T_L          DCA      ,STO,6,MODE  
   6 7WF=          GLPERI
   9 7WYW          BRN      XUFLAG   
   5 7XDG    XERROR  
  21 7XY6          LDN   0  4                   [SET BIT TO SAY READY REPEAT NEEDED  
   9 7YCQ          ORS   0  GWAIT(3) 
   5 7YXB    XERROR1 
  19 7_C2          LDEX  4  GFAIL(3)            [J. TO SIMULATE INOPERABLE   
  20 7_WL          SBN   4  AMREPT              [LINE IF ERROR COUNT TOO HIGH.   
   9 82B=          BPZ   4  XINOP1   
   8 82TW          LDCT  0  #200 
  19 83*G          ORS   0  GWAIT(3)            [SET BIT TO SAY DON'T FREE   
   7 83T6          LDN   0  1
  17 84#Q          ADS   0  GFAIL(3)            [ADD 1 TO FAIL COUNT 
   7 84SB          LDN   0  4
   9 85#2          ANDX  0  GWAIT(3) 
  15 85RL          BNZ   0  YRXA                [J IF READY  
   9 86?=          ADN   1  SERRORA  
   8 86QW          BRN      YRXB 
  18 87=G    YRXA  ADN   1  SERROR              [ADDRESS OF ERROR MESSAGE
   8 87Q6    YRXB  LDX   0  BIT11
  18 889Q          ORS   0  GWAIT(3)            [SET ^CANCEL DC1^ MARKER 
  17 88PB    XOP   LDN   4  GNLBUFF(3)          [SET UP NL MESSAGE NL
   9 8992          LDN   5  BUFFER(3)
   9 89NL          LDCH  0  CHARS(3) 
   7 8=8=          SMO      0
   7 8=MW          MVCH  4  0
   8 8?7G          LDN   4  1(1) 
   8 8?M6          SMO      0(1) 
   7 8#6Q          MVCH  4  0
  10 8#LB          LDN   4  GNLBUFF(3)   
   9 8*62          SMO      GNOCHS(3)
   7 8*KL          MVCH  4  0
   8 8B5=          ADX   0  0(1) 
   9 8BJW          ADX   0  GNOCHS(3)
   7 8C4G          LDX   4  0
   7 8CJ6          SLC   5  2
   7 8D3Q          SBN   5  2
  19 8DHB          SRC   5  2                   [POINT TO LAST BUT ONE CHAR  
   7 8F32          LDX   1  5
  18 8FGL          LDN   0  #10                 [REPLACE EOM WITH CANCEL 
   8 8G2=          DCH   0  0(1) 
   9 8GFW          LDN   5  BUFFER(3)
   8 8G_G          LDX   1  FX1  
   9 8HF6          BRN      OUTERR   
   5 8HYQ    XINOP2  
  20 8JDB          LDXC  6  GFAIL(3)            [J. IF ALREADY CANCELLED READ.   
   8 8JY2          BCS      NSET 
   8 8KCL          LDX   5  GSIGN
  19 8KX=          ORS   5  GFAIL(3)            [SET BIT TO SAY INOPERABLE.  
   5 8LBW    XERROR2 
  16 8LWG          LDN   0  4                   [SET BIT TO SAY  
  17 8MB6          ORS   0  GWAIT(3)            [READY TO BE REPEATED
   8 8MTQ          BRN      XCANC
  15 8N*B    REFIND                             [FOR LOCKC   
   6 8NCK ...#SKI JSKI14 
   4 8NFS ...(   
   7 8NJ3 ...      LDX   3  1
   8 8NL= ...      CALL  0  RCHN 
   7 8NNF ...      LDX   1  3
   4 8NQN ...)   
  11 8NT2          MHUNTW   2,AMXOR,ALINES   
   7 8P#L          ADX   2  7
  10 8PS=          LDX   2  A1+BPTR(2)   
   8 8Q?W          SBN   2  A1   
   7 8QRG          EXIT  1  0
   5 8R?6    XINOP1X 
   7 8RQQ          LDN   5  4
  15 8S=B          ORS   5  GWAIT(3)            [REPEAT READY
   5 8SQ2    XINOP1  
   8 8T9L          LDX   5  GSIGN
  19 8TP=          ORS   5  GFAIL(3)            [SET BIT TO SAY INOPERABLE   
   8 8W8W          BRN      NSET 
  18 8WNG    POUT  LDCT  0  2                   [MARK B.I. BEFORE OUTPUT.
   9 8X86          ORS   0  GWAIT(3) 
   8 8XMQ          BRN      SNULL
  20 8Y7B    XBI3  LOCKC    3,REFIND            [LOCK LCB FOR OUTPUTTING BREAK IN
   9 8YM2 ...NTRBI CALL  1  REFIND   
   7 8_6L          LDX   3  2
   8 8_L=          LDX   1  FX1  
   8 925W          LDX   2  FX2  
  19 92KG          LDX   0  GSIGN               [FRIG SO THAT OTHER TRANSFERS
  20 9356          STO   0  GLASTM(3)           [CANNOT GET IN DURING B.I. IDLE. 
  20 93JQ    XBI2  LDX   0  HWGROT(3)           [TEST TO SEE IF LIMIT EXCEEDED   
  10 944B          SBN   0  HWGROTCT-1   
   9 94J2          BZE   0  XINOP1   
  18 953L          LDN   0  1                   [INCREASE GROTTY HW BI   
  14 95H=          ADS   0  HWGROT(3)           [COUNT   
   8 962W          LDCT  0  #40  
   9 96GG          ANDX  0  GWAIT(3) 
   8 9726          BNZ   0  RTO  
  16 97FQ          LDCT  0  #200                [SET BIT TO SAY  
  18 97_B          ORS   0  GWAIT(3)            [DON'T FREE OUTPUT BLOCK 
  18 98F2          LDCT  0  #2                  [UNSET 'BREAK IN BEFORE  
  16 98YL          ANDX  0  GWAIT(3)            [OUTPUT BIT' IF  
  15 99D=          ERS   0  GWAIT(3)            [IT IS SET   
  17 99XW    XBI   LDX   0  PMASK(1)            [UNSET B10 IN GWAIT  
   9 9=CG          ANDS  0  GWAIT(3) 
   8 9=X6          LDX   0  BIT11
  15 9?BQ          ORS   0  GWAIT(3)            [CANCEL DC1  
  21 9?WB          LDCT  0  #100                [J. IF BREAK-IN BIT IS ALREADY SET.  
   9 9#B2          ANDX  0  GWAIT(3) 
  15 9#TL          BZE   0  XBIX                [1ST BREAKIN 
   8 9**=          LDN   0  #4000
  17 9*SW          ORS   0  GWAIT(3)            [SET DOUBLE BREAKIN  
   8 9B#G          BRN      WAKE1
   4 9BS6    XBIX
   9 9C?Q          HUNTACT  CPAT,2,3 
   8 9CRB    #SKI  ANOUSER<1$1   
   4 9D?2    (   
   9 9DQL          LDX   6  JOBNO(2) 
  21 9F==          BZE   6  NBI                 [J. IF BEFORE JOB BLOCK ETC. SET UP. 
   4 9FPW    )   
   8 9G9G          LDN   0  #1000
   7 9GP6          LDCT  4  4
  10 9H8Q          SMO      A1+FPTR(3)   
   9 9HNB          ANDX  4  FOURTHWD 
   8 9J82          BNZ   4  THSEC
   9 9JML          CHKBREAK  2,NBI   
   7 9K7=          LDCT  0  4
  10 9KLW    THSEC SMO      A1+FPTR(3)   
   9 9L6G          ORS   0  FOURTHWD 
   9 9LL6          BNZ   4  UNSETA   
   7 9N52    #SKI   ANOUSER  
   8 9NJL          BRUSEN   NUZ,2
  10 9P4=          JOBLOCK  JOBNO(2),2   
   8 9QH6    NUZ   LDX   2  FX2  
   8 9R2Q          BRN   WKIMQ   
  18 9RGB    NBI   LDX   4  GLASTM(3)           [J. IF IN B.I. ON IDLE.  
   8 9S22          LDCT  0  #200 
   8 9SFL          LDX   2  FX2  
   8 9SMW ...      BPZ   4  NSBII
   9 9SW6 ...      ERS   0  GWAIT(3) 
   8 9T4B ...      BRN      SBII 
   8 9TGD ...NSBII SBN   4  #13  
  21 9TYG          BNZ   4  WAKE1               [J. TO OUTPUT NL IF NOT MODE 3, ELSE 
  21 9WD6          ADN   0  4                   [SET BITS TO SAY DON'T FREE,& RPT RDY
   9 9WXQ          ORS   0  GWAIT(3) 
  16 9XCB          BRN      WAKE1               [J TO OUTPUT NL  
   6 9XCW ...#SKI JSKI14 
   4 9XDB ...(   
   6 9XDW ...X92PROFLAG  
  18 9XFB ...      LDX   2  3                   [START ADDRESS IN BLK.   
   8 9XFW ...      SBN   1  A1   
  16 9XGB ...      LDX   6  ALOGLEN(1)          [LTH. OF CHAPTER 
   7 9XGW ...      LDX   4  6
   8 9XHB ...      ADN   4  A1   
  16 9XHW ...      CALL  0  RMV                 [MOVE INTO BLOCK 
  16 9XJB ...      LDX   3  2                   [SAVE END OF CHAP
   7 9XJW ...      SBX   2  6
   7 9XKB ...      SBX   1  6
  15 9XKW ...      STO   2  FX1                 [RESET FX1   
   8 9XLB ...      SBN   2  A1   
  18 9XLW ...      STOZ     ATYPE(2)            [MARK ^CHAPTER IN BLOCK^ 
   9 9XMB ...      STOZ     BPTR(2)  
   9 9XMW ...      STO   2  FPTR(2)  
   8 9XNB ...      ORX   2  GSIGN
  17 9XNW ...      STO   2  KTAB+KPROFLAG        [RESET KTAB ENTRY   
   9 9XPB ...      ADNC  2  ZRS+A1   
  18 9XPW ...      BRN      (2)                 [JUMP INTO COPIED VERSION
   8 9XQB ...ZRS   SBN   1  A1   
   9 9XQW ...      TXU   1  GFIXCHAP 
   8 9XRB ...      BCS      NOTFX
   9 9XRW ...      STOZ     BACK2(1) 
   8 9XS6 ...      BRN      TINFX
   5 9XSB ...NOTFX   
   9 9XSL ...      LDX   0  ASIZE(1) 
   9 9XSN ...      LDX   2  AFLAG(1) 
   7 9XSQ ...      ANDN  2  2
   8 9XSS ...      BZE   2  NVREZ
  18 9XSW ...[                                   J IF NOT FROZEN CHAPTER 
   9 9XSY ...      SBS   0  FREZCHAP 
  19 9XT2 ...[                                    UPDATE FROZEN CHAPTER SIZE 
   8 9XT4 ...      BRN      TVREE
   5 9XT6 ...NVREZ   
  15 9XT8 ...      SBS   0  ACHAP               [UPDATE ACHAP
   5 9XT= ...TVREE   
  17 9XTB ...      FREECORE 1                   [FREE CHAPTER BLOCK  
   9 9XTW ...TINFX ACROSS   CONMXC,92
   4 9XWB ...)   
   4 9XX2    #END
   8 ____ ...46030050000700000000
