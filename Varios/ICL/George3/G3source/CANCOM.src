  12 22FL    #LIS  K0CANCOM>K0COMMAND>K0ALLGEO   
  17 22_=    #SEG  CANCOM                       [ BILL IZATT : CENT  
   7 23DW          8HCANCOM  
  15 23YG    [ ENTRY POINT.  THIS LOCATION MUST REMAIN FIXED 
  11 24D6          SEGENTRY K1CANCOM,SENTRY1 
   4 24XQ    [   
   4 25CB    [   
  19 25X2    [              THIS SEGMENT CHECKS THAT THE SPECIFIED COMMAND IS
  21 26BL    [              CANCELLABLE AND IN AN ALLOWABLE CONTEXT TO BE CANCELLED. 
  20 26W=    [              IT THEN PASSES CONTROL TO THE APPROPRIATE SEGMENT TO 
  21 27*W    [              EFFECT THE CANCELLATION OR OUTPUTS THE APPROPRIATE ERROR 
   9 27TG    [              MESSAGE. 
  21 28*6    [              THE FOLLOWING IS A LIST OF CANCELLABLE COMMANDS (SHORT-  
  21 28SQ    [              FORM,LONG-FORM),FORBIDDEN CONTEXTS FOR CANCELLATION, AND 
  15 29#B    [              LINKS TO THE CANCELLING SEGMENTS 
  18 29S2          SEGENTRY K90CANCOM           [ THE START OF THE LIST  
   5 2=?L    VERBLIST
   6 2=R= ...#SKI  JSKI31
  13 2?=W          BICE     QI,QUIET,#00264,PNTLAST,3
   7 2?QG    #SKI  ICANWEN   
  14 2#=6          BICE     WE,WHENEVER,#00000,CANWEN,1  
  14 2#PQ          BICE     RV,RETRIEVE,#00000,CANCRVX,1 
  14 2*9B          BICE     AH,ATTACH,#00051,ATTACHA,5   
  14 2*P2          BICE     AE,ASSOCIATE,#00351,ASSOC,2  
  14 2B8L          BICE     PR,PROPERTY,#351,PROPERTY,2  
  14 2BN=          BICE     AU,ATTRIBUTE,#151,ATTRIBUT,2 
  14 2C7W          BICE     CL,CONCEPTUAL,#00051,DELCON,1
  13 2CMG          BICE     FN,FINISH,#00351,FINISH,2
  14 2D76          BICE     ET,ENTRUST,#00400,ENTRUST,2  
  14 2DLQ          BICE     AO,ALLOCATE,#00340,ALLOCATE,2
   6 2DRY ...#SKI  JSKI15
  14 2D_6 ...      BICE     OP,OPERATOR,#00062,OPERATOR,2
   5 2F6B    #SKI  AR
  14 2FL2          BICE     AR,ARCHIVE,#00002,ARCHIVE,2  
   6 2FR8 ...#UNS  FTS1  
  11 2FYB ...      BICE  FZ,FREEZE,2,FREEZE,2
   6 2G26 ...#UNS  EXSEX2
  13 2G3W ...      BICE     SX,SECUREX,#00002,ERASE,5
   6 2G4D ...#UNS  IDC   
  12 2G52 ...      BICE     DU,DUMP,#251,DUMP,2  
   7 2G58 ...#UNS  ILOGACC   
  14 2G5B ...      BICE     LA,LOGACCESS,#00002,LOGCOM,1 
   7 2G5D ...#UNS  AV7900S   
  14 2G5G ...      BICE     MV,MAKEVCC,#00351,MAKEVCC,2  
  17 2G5L          SEGENTRY K91CANCOM           [ THE END OF THE LIST
  10 2GK=    #DEF  VBLISTLEN=0?-VERBLIST 
  19 2H4W    #DEF  SIZEBICE=BICESIZE            [ NO OF WORDS IN BICE MACRO  
  20 2HJG    [              THE FOLLOWING IS A LIST OF SUB-TYPES FOR THE CONTEXT 
  11 2J46    [              ERROR MESSAGES.  
   5 2JHQ    PCOMPLIS
   9 2K3B                   +JBREAK  
   9 2KH2                   +JNOBREAK
   9 2L2L                   +JPROGISH
   9 2LG=                   +JFULL   
   9 2L_W                   +JTENT   
   8 2MFG                   +JCOR
   9 2M_6                   +JNOCOR  
   8 2NDQ                   +JREM
   8 2NYB                   +JATT
   9 2PD2                   +JPTCR   
   9 2PXL                   +JOPCON  
   9 2QC=                   +JMOPCON 
   8 2QWW                   +JJDF
   8 2RBG                   +JDIR
   8 2RW6                   +JCOM
   8 2S*Q    TWO            +2   
   8 2STB    THTEN          +13  
  10 2T*2    NUM            +VBLISTLEN   
   9 2TSL    MERCONT        +ERCONT  
   4 2W#=    [   
   5 2WRW    SENTRY1 
  21 2X?G          PARANEXT #20             [SEPARATE COMMAND TO BE CANCELLED FROM   
  20 2XR6                                   [ITS FIRST PARAMETER AND PARAPASS IT 
  13 2Y=Q                                   [ACROSS  
  18 2YQB ...      TESTREP2 UNPAIR,ZN2      [ERROR IF UNPAIRED DELIMITER 
  17 2_28 ...      TESTREP2 TOOMANY,ZN3     [ERROR IF TOOMANY PARAMS 
  10 2_=2          MHUNT    3,CPB,CUNI   
  21 2_PL          LDX   7  ANUM(3)         [GET WORD CONTAINING PARAMETER TYPE AND  
  17 329=                                   [THE NO. OF CHARACTERS   
  18 32NW          BNG   7  NOK             [BRANCH IF PARAMETER MISSING 
   8 338G          ANDN  7  #7777
  21 33N6          TXL   7  THTEN(1)        [TEST IF LONGER THAN TWELVE CHARACTERS   
   8 347Q          BCS      NOK  
   9 34MB          COMERR   INVERB   
  17 3572    NOK   BNZ   7  N1              [TEST IF PARAMETER NULL  
  11 35LL          COMERR   JPARNULL,JCOMA   
  15 366=    N1    BPZ   7  N2              [TEST IF MISSING 
  10 36KW          COMERR   JPARMIS,JCOMA
  18 375G    N2    ANDN  7  #7777           [LEAVE ONLY NO. OF CHARACTERS
  21 37K6          LDX   4  NUM(1)          [GET COUNT FOR TESTING LIST OF COMMANDS  
  16 384Q          TXU   7  TWO(1)          [TEST IF SHORT-FORM  
   8 38JB          BCS      N3   
   7 3942          LDN   6  0
  16 39HL    N4    LDX   5  VERBLIST(1)     [GET NEXT SHORT-FORM 
   9 3=3=          SBX   5  APARA(3) 
  20 3=GW          BZE   5  VRBOK           [WAS IT THE SAME AS THE COMMAND TO BE
  14 3?2G                                   [CANCELLED?  
  19 3?G6          ADN   6  SIZEBICE        [ LOOK AT NEXT COMMAND IN TABLE  
   9 3?_Q          ADN   1  SIZEBICE 
  20 3#FB          TXL   6  4               [DOES AT LEAST ONE POSSIBLE COMMAND  
  13 3#_2                                   [REMAIN? 
   8 3*DL          BCS      N4   
   9 3*Y=    ZN1   COMERR   ERCOMM   
  16 3B5D ...ZN2   SPARAPAS                 [SET UP CUNI BLOCK   
   9 3B=L ...      COMERR   ERUNPAIR 
   4 3B#B ...ZN3 
   9 3BB6 ...      COMERR   JMAXPAR  
  21 3BCW    N3    ADN   7  3               [CALCULATE NO. OF WORDS TAKEN UP BY THE  
  21 3BXG          SRL   7  2               [COMMAND (INCLUDING THE SPACE-FILLING OF 
  18 3CC6                                   [THE LAST WORD,IF NECESSARY) 
   7 3CWQ          LDN   6  0
   7 3DBB    N5    LDN   2  1
   7 3DW2          SMO      2
  17 3F*L          LDX   5  VERBLIST(1)     [GET NEXT WORD OF COMMAND
   7 3FT=          SMO      2
  10 3G#W          SBX   5  APARA-1(3)   
  17 3GSG          BZE   5  N6              [DOES IT COMPARE SO FAR? 
  19 3H#6          ADN   6  SIZEBICE        [ LOOK AT NEXT COMMAND IN TABLE  
   9 3HRQ          ADN   1  SIZEBICE 
  21 3J?B          TXL   6  4               [AT LEAST ONE POSSIBLE COMMAND REMAINS?  
   8 3JR2          BCS      N5   
   9 3K=L          COMERR   ERCOMM   
   7 3KQ=    N6    ADN   2  1
  21 3L9W          TXL   7  2               [HAVE WE CHECKED ALL THE WORDS THAT THE  
  17 3LPG                                   [COMMAND TAKES UP YET?   
   8 3M96          BCC      N5+1 
  17 3MNQ    VRBOK LDX   5  VERBLIST+4(1)   [GET FORBIDDEN CONTEXTS  
   9 3N8B    #SKI  K6CANCOM>999-999  
   4 3NN2    (   
  11 3P7L          TRACE    APARA(3),COMCAN  
  10 3PM=          TRACE    5,FORBTEXT   
   8 3Q6W          LDX   2  FX2  
  12 3QLG          TRACE    CONTEXT(2),CURRTEXT  
   4 3R66    )   
   8 3RKQ          SMO      FX2  
  20 3S5B          ANDX  5  CONTEXT         [MASK OUT NON-COINCIDENTAL CONTEXTS  
  21 3SK2 ...      BZE   5  TXTPRV          [DOES A FORBIDDEN CONTEXT COINCIDE WITH  
  16 3T4L                                   [A CURRENT CONTEXT?  
   7 3TJ=          SLL   5  9
   7 3W3W          LDN   7  0
  20 3WHG    N8    BPZ   5  N7              [DOES THIS BIT INDICATE THE CONTEXT  
  13 3X36                                   [ERROR?  
   8 3XGQ          LDX   1  FX1  
   7 3Y2B          SMO      7
  21 3YG2          LDX   5  PCOMPLIS(1)     [GET POINTER TO APPROPRIATE SUB-MESSAGE  
  21 3Y_L          COMERRX  MERCONT(1),5    [POINTER TO ERROR MESSAGE (+ERCONT) IS   
  13 3_F=                                   [IMPLICIT
  15 3_YW    N7    SLL   5  1               [PREPARE NEXT BIT
  15 42DG          ADN   7  1               [UPDATE POINTER  
   8 42Y6          BRN      N8   
   5 42_N ...TXTPRV  
   7 4322 ...      LDN   5  2
   8 432# ...      SMO      FX2  
   9 432L ...      ANDX  5  CONTEXT  
   8 432Y ...      BNZ   5  TXTOK
  10 433= ...      LDX   5  VERBLIST+4(1)
  17 434S ...      ANDX  5  ICONTEXT        [OBTAIN REQD PRIVILEGE   
  18 436B ...      BZE   5  TXTOK           [J IF PRIVILEGE NOT REQUIRED 
  15 437Y ...      FJOCA    1,2             [GET JOBQE BLOCK 
  16 439G ...      LDX   7  JPRIV(1)        [OBTAIN JOB PRIVILEGE
   7 43?4 ...      ANDX  7  5
   7 43#L ...      ERX   7  5
  20 43B8 ...      BNZ   7  WRONG           [J IF USER HAS NOT REQUIRED PRIVILEGE
  17 43CQ    TXTOK LDX   7  ANUM(3)         [GET NO. OF CHARACTERS   
  18 43XB          ANDN  7  #7777           [IN COMMAND TO BE CANCELLED  
  20 44C2          PARAMOVE 7               [MOVE UP PARAMETERS OF COMMAND TO BE 
  21 44WL                                   [CANCELLED IN CPB/CALAS BLOCK,OVERWRITING
  16 45B=                                   [THE COMMAND ITSELF  
  18 45TW          PARAFREE                 [FREE ALL CPB/CMULTI BLOCKS  
  17 46*G          MFREE    CPB,CUNI        [FREE THE CPB/CUNI BLOCK 
   7 46T6          SMO      6
  20 47#Q          LDN   5  VERBLIST+5(1)   [GET POINTER TO LINK-PAIR OF SEGMENT 
  20 47SB                                   [WHICH WILL EFFECT THE CANCELLATION  
   9 48#2          LDN   6  AWORK1(2)
  17 48RL          MOVE  5  HLINK           [ MOVE LINK FOR ACROSSVAR
  21 49?=          CCON                         [ SET BIT INDICATING CANCEL COMMAND  
  10 49QW          ACROSSVAR AWORK1(2)   
   9 4=2N ...WRONG COMERR   ANOPRIV  
  10 4==G          MENDAREA 30,GAPCANCOM 
   4 4=Q6    #END
   8 ____ ...46151774000900000000
