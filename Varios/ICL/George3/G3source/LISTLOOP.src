   8 22FL    #OPT  K0LISTLOOP=0  
   7 22PD ...#OPT ENHPLOW=0  
   7 22_=    #OPT  AERRSIM=0 
   9 23DW ...#OPT           HATP=0   
   9 23YG ...#OPT           ATPOLTR=0
  10 24D6 ...#OPT           ATPOLTRP2=0  
  18 25CB    #LIS  K0LISTLOOP>K0LISTFILE>K0ALLGEO>K0GREATGEO>K0COMMAND   
   7 25X2    #SEG  LISTLOOP  
  10 26BL                   8HLISTLOOP   
  15 26LD ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
  11 26W=          SEGENTRY K1LISTLOOP,POINT1
   4 27TG    [   
  10 28*6    [CENTRAL LINE PRINTER CODE  
  21 28SQ    [THIS SEGMENT CONTAINS THE INNER LOOP OF LISTFILE WHICH READS RECORDS   
  20 29#B    [FROM THE SPECIFIED FILE  DOES ANY SIMPLE CONVERSION NECESSARY AND  
  14 29S2    [OUTPUTS THE RECORD ON THE SPECIFIED PRINTER
  18 2=?L    [EXTRA CODE SHOULD NOT BE INSERTED IN THE MAIN PATH WITHOUT 
  13 2=R=    [CAREFUL CONSIDERATION OF THE OVERHEAD. 
  14 2?=W    [THE CONVERSION IMPLEMENTED IS AS FOLLOWS:  
  10 2?QG    [IF A SHIFT FILE IS LISTED  
  17 2#=6    [DATA IS FIRST CONVERTED TO GRAPHIC,UNLESS 'SPECIAL' WAS
  18 2#PQ    [ONE OF THE COMMAND PARAMETERS,WHEN NO CONVERSION IS DONE.  
  17 2*9B    [THE DATA IS CONVERTED ONE RECORD AT A TIME BY DECANTING
  18 2*P2    [THE RECORD FROM THE USAGE BLOCK INTO AN ADATA/ACONV BLOCK  
  18 2B8L    [AND USING THE TRANSFORM MACRO.THE FLAG K IS USED TO SHOW   
  17 2BN=    [THE PRESENCE OF AN ADATA/ACONV BLOCK TO AVOID THE HUNT 
  14 2C7W    [OVERHEAD FOR THE NORMAL,NO-CONVERSION PATH.
   4 2CMG    [   
  19 2D76    [IF THE RECORD IS LONGER THAN CAN BE ACOMODATED IN ONE TRANSFER 
  10 2DLQ    [THEN THE RECORD IS SPREAD  
  20 2F6B    [OVER MANY TRANSFERS WITH SUITABLE CONTINUATION LINES FOR LP OUTPUT.
  19 2FL2    [THE FLAG H IS SET TO INDICATE THAT THE CURRENT TRANSFER IS ONE 
  18 2G5L    [OF THE SUBSEQUENT COMPONENTS OF A MULTI-TRANSFER RECORD.   
   4 2GK=    [   
  19 2H4W    [THE  BUFFER FOR A TRANSFER IS CONTAINED IN THE ACTLF AND STARTS
  19 2HJG    [AT THE IDENTIFIER GZDATA.WHILE THE TRANSFER IS IN PROGRESS THE 
  20 2J46    [ACTLF MUST REMAIN LOCKED [SO THAT EXECUTIVE CAN FIND IT] AND OFF   
  19 2JHQ    [THE LIST OR THE QUEUE (ACHIEVED BY COOR1).WHEN THE TRANSFER HAS
  13 2K3B    [FINISHED EXECUTIVE RESTARTS THE ACTLF. 
   4 2KH2    [   
  18 2L2L    [DATA MANIPULATION IS CONTROLLED BY INFORMATION CONTAINED IN
  13 2LG=    [THE FOLLOWING LOCATIONS IN THE ACTLF   
   4 2L_W    [   
  19 2MFG    [     GZPOINT  CONTAINS A POINTER RELATIVE TO THE START OF THE  
  21 2M_6    [              RECORD,INDICATING HOW MUCH DATA HAS ALREADY BEEN TRANSFER
   4 2NDQ    [   
  19 2NYB    [     GZDUMP   CONTAINS THE DUMP ADDRESS RELATIVE TO THE START  
  11 2PD2    [              OF THE BUFFER.   
   6 2PGK ...#UNS  ANIP  
   4 2PK8 ...(   
  19 2PMR ...[              ALSO, JUST BEFORE THE PERI,CONTAINS THE NUMBER OF
  14 2PQB ...[              CHARS FOR CURRENT TRANSFER   
   4 2PS_ ...)   
   4 2PXL    [   
  18 2QC=    [     GZTRANS  CONTAINS THE NUMBER OF CHARACTERS OF DATA FOR
  11 2QWW    [              CURRENT TRANSFER.
   4 2RBG    [   
  18 2RW6    [     GZLENGTH CONTAINS THE NUMBER OF CHARACTERS OF DATA IN 
  12 2S*Q    [              THE CURRENT RECORD   
   4 2STB    [   
   4 2T*2    [   
  21 2TSL    [***********************************************************************
   6 2W#=    [DATA AREA  
  17 2WRW    [              ---------------------------------------- 
   4 2X?G    [   
   8 2XR6    TLP2           +2   
  15 2Y=Q    SM             #07777777           [EQUAL MASK  
  17 2YQB    TLP            +1                  [LP COMPARISON WORD  
  19 2_=2    SMLP           +7036875            [LP MAGIC CONVERSION NUMBER  
  18 2_PL    NUMLP          +8000000            [LP MAXIMUM LINE NUMBER  
  17 329=    TCA            +0.3                [LP BUFFER MODIFIER  
  21 32NW    XCONT          4H  -           [CONTINUATION LINE MARK-OUTPUT AT END OF 
  15 338G                   [EACH LINE OF A MULTILINE RECORD 
   4 33N6    [   
   9 347Q    SETUPBIT       #10000000
   8 34MB    PAGELEN        +66  
   4 3572    [   
  21 35LL    [***********************************************************************
   7 366=    [SUBROUTINE AREA
   5 36KW    TIDYUP  
  14 375G    [THIS S/R TIDIES UP WHEN A LISTING IS TO END
  18 37K6          LFBBUS   K,2,NOADATA         [J IF NO CONVERSION BLOCK
  10 384Q          MHUNTW   1,ADATA,ACONV
   7 38JB          FREECORE 1
  18 3942          LFBITCLR K,2                 [CLEAR CONVERSION FLAG   
   5 39HL    NOADATA 
  11 3=3= ...      MHUNTW   1,GOUT,INFOLIST  
   9 3=GW          LDX   4  GREST(1) 
   9 3?2G          LDX   0  AWORK4(2)
  20 3?G6          BPZ   0  NLSU                [J IF NOT CURRENTLY LISTING SETUP
   9 3?_Q          STOZ     GREST(1) 
   9 3#FB          STOZ     GNUM(1)  
   7 3#_2          EXIT  3  0
   7 3*DL    NLSU  FREECORE 1
   6 3*H9 ...#SKI ENHPLOW
   4 3*KS ...(   
  19 3*NC ...      LFBITCLR U,2                 [CLEAR ANY PAPER LOW BITS SET
   8 3*R2 ...      LFBITCLR V,2  
   4 3*TK ...)   
  10 3*Y=          LDX   0  SETUPBIT(1)  
   9 3BCW          ANDX  0  AWORK2(2)
  19 3BXG          BZE   0  NSUA                [J IF NO SETUP FILE AT ALL   
  11 3CC6 ...      MHUNTW   1,GOUT,INFOSET   
   7 3CWQ          FREECORE 1
   7 3DBB    NSUA  EXIT  3  1
   4 3DW2    [   
   5 3F*L    SLOCKC  
  16 3FT=    [THIS S/R IS USED BY THE LOCK MACRO TO SET X2=FX2   
   8 3G#W          LDX   2  FX2  
   7 3GSG          EXIT  1  0
  21 3H#6    [THIS IS A SUBROUTINE FOR CONVERTING THE BINARY NUMBER IN X5 TO DECIMAL 
  21 3HRQ    [CHARACTERS STORING THE RESULT IN THE LOCATION WHOSE ADDRESS IS IN X2   
   4 3J?B    [   
   5 3JR2    SEQUENCE
   7 3K=L          MODE     1
   8 3KQ=    SCBD  CBD   4  0(2) 
   7 3L9W          BCHX  2  /
   8 3LPG          BCT   3  SCBD 
  20 3M96          MODE     0                   [DON'T SUPPRESS ZEROES FOR LAST  
  15 3MNQ          CBD   4  0(2)                [CHARACTER   
   7 3N8B          BCHX  2  /
   8 3NN2          DCH   1  0(2) 
   8 3P7L          LDX   1  FX1  
   8 3PM=          LDX   2  FX2  
   7 3Q6W          EXIT  0  0
   4 3QLG    [   
  21 3R66    [THIS ROUTINE PERFORMS THE PERI ON THE DEVICE AND CHECKS THE REPLY FOR  
  12 3RKQ    [DISENGAGED AND INOPERABLE STATES   
   4 3S5B    [   
   6 3S6C ...#UNS  ANIP  
   4 3S7D ...(   
  17 3S8F ...[ ENTRY FROM SRER SUBROUTINE (CORRUPT FILE) IF NIP ACTLF
   9 3S9G ...PERI4 KEEPREP 0,CORRUPT 
  11 3S=H ...[ ENTRY TO GIVE CHANGED REPLY   
   8 3S?J ...PERI5 ORX   0  GSIGN
   9 3S#K ...      STO   0  GZDUMP(2)
   8 3S*L ...      SBX   5  FX1  
  10 3SBM ...XLPS  DOWN     LISTPREP,4   
   9 3SCN ...      LDX   0  GZDUMP(2)
   8 3SDP ...      BNG   0  NOT  
  21 3SFQ ...      BRN      XNIP                 [NORMAL ENTRY - ADD TO ALFTRANS ETC 
   4 3SGR ...[   
   4 3SHS ...)   
  15 3SK2    PERI  SBX   5  FX1                 [ADJUST LINK 
   6 3SLC ...#UNS  ANIP  
   4 3SMS ...(   
  15 3SP9 ...[ IF NIP ACTLF GO DOWN TO LISTPREP TO DO PERI   
   8 3SQL ...      LDX   2  FX2  
   9 3SS3 ...      LDX   4  GZDUMP(2)
  20 3STD ...      BZE   4  NOT                  [ZERO CHAR COUNT - DO NOT PRINT 
   9 3SWT ...      LDX   0  CPPTR(2) 
   8 3SY= ...      BZE   0  XLPS 
  21 3S_M ...      DCA      ,STO,4,COUNT         [STORE CHAR COUNT IN CONTROL AREA   
   4 3T34 ...)   
   9 3T4L    REP   DCA ,LDX,4,COUNT  
   8 3TJ=          BZE   4  NOT  
   8 3W3W          LDX   1  FX1  
   9 3WHG          LDX   4  TCA(1)   
   9 3X36          LOCKC    2,SLOCKC 
   9 3XGQ          ADN   4  GZDATA(2)
  10 3Y2B          DCA      ,STO,4,CTSA  
   7 43CQ          GPERI ,2  
   6 43XB          COOR1 
   7 44C2          UNLOCK   2
   9 45TW          LDX   4  FINISH   
   7 46*G          SLL   4  2
  16 46T6          BNG   4  SCANTY              [ 'FINISH NOW'   
   8 47#Q          LDCT  4  #50  
   9 47SB          ANDX  4  CPRW1(2) 
  18 48#2          BNZ   4  NOTENG              [J IF FAIL OR DISENGAGED 
   6 48F8 ...#UNS  ANIP  
   4 48LB ...XNIP
   7 48RL          LDN   0  1
  10 49?=          ADS   0  ALFTRANS(2)  
   6 49BB ...#UNS  ANIP  
   9 49FG ...      LDX   4  GZDUMP(2)
   6 49JL ...#UNS  ANIP  
   4 49MQ ...#SKI
  10 49QW          DCA      ,LDX,4,COUNT 
   7 4==G          SRL   4  2
  10 4=Q6          ADS   4  ALFWORDS(2)  
   8 4?9Q    NOT   ADX   5  FX1  
  13 4?PB          EXIT  5  0                   [EXIT
   4 4#92    #   
  12 4#NL    # THE FOLLOWING DEALS WITH RESTARTS 
   4 4*8=    #   
   8 4*MW    [ MULTIPAGE RESTARTS
  16 4B7G    [ IF GLEN=0 THEN NO MULTIPAGE RESTART IS REQUIRED   
  21 4BM6    [ GLEN=IPAGE-1 AND IS SET UP IN LISTPREP. IPAGE IS MAX NUMBER OF PAGES  
   9 4C6Q    [ THAT CAN BE REPEATED  
  15 4CLB    [ GNEXT IS A POINTER TO THE NEXT GREST TO BE SET
  21 4D62    [ IF GREST+1=0 THEN NO MULTIPAGE RESTART IS REQUIRED AS ONLY ONE PAGE   
   8 4DKL    [ HAS BEEN LISTED.  
  16 4F5=    [ ALPSETUP IS SET EQUAL TO IPAGE BEFORE THE REQUEST 
  18 4FJW    [ AFTER THE PAGES COMMAND ALPSETUP IS EQUAL TO THE NUMBER-1 
   4 4G4G    [   
   5 4GJ6    PAGEREST
   8 4H3Q          SBX   3  FX1  
  11 4HHB ...      MHUNTW   1,GOUT,INFOLIST  
   9 4J32          LDXC  4  GREST(1) 
  17 4JGL          BCC      PAGES1              [J NOT PAGE RESTART  
   6 4JHX ...#SKI ENHPLOW
   4 4JK8 ...(   
  20 4JLF ...      LFBBUS   U,2,SUSU            [J IF PAPER LOW NOT BEEN FOUND   
  20 4JMQ ...      LFBITCLR U,2                 [OTHERWISE CLEAR PAPER LOW BITS  
  20 4JP3 ...      LFBITCLR V,2                 [AND OUTPUT RELOAD MESSAGE BEFORE
  21 4JQ# ...      LDCT  6  #100                [IF NO PLOW REPLY ON LAST TRANSFER   
  21 4JRK ...      ANDX  6  CPRW1(2)            [THEN ASSUME OPERATORS HAVE BEATEN   
  14 4JSW ...      BZE   6  SUSU                [US TO IT
  19 4JW7 ...      DOWN     LISTNEXT,10         [CONTINUING RESTART ACTION   
   4 4JXD ...SUSU
   4 4JYP ...)   
   9 4K2=          LDX   6  GLEN(1)  
  15 4KFW          BZE   6  PAGES1              [J IPAGE=1   
  10 4K_G          LDX   5  GREST+1(1)   
  17 4LF6          BZE   5  PAGES1              [J ONLY 1 PAGE LISTED
   9 4LYQ          LDX   6  GLEN(1)  
  21 4MDB          ADN   6  1                   [THIS LISTFILE (FOR PAGES COMMAND)   
  18 4MY2          STO   6  ACOMMUNE6(2)        [THEN PUT IT IN ACOMMUNE6
   7 4PBW          GEOPACK  1
  10 4PWG          LONGSET  #104,XGEOER,1
   6 4RT2          OUTPER
  14 4S#L          DEMAND   ,JPAGES,PAGES2,XGEOER,REE,1  
  20 4SS=                       [PLEASE GIVE PAGES FOR ERROR RESTART ON UNIT %A  
   4 4T?W    PCD 
  11 4TRG ...      MHUNTW   1,GOUT,INFOLIST  
   9 4W?6          LDX   5  GNEXT(1) 
  21 4WQQ          LDX   6  ACOMMUNE6(2)        [SET X6=NO OF PAGES FOR ERROR RESTART
  14 4X=B          SBN   6  1                   [MINUS 1 
   7 4XQ2          SBX   5  6
   8 4Y9L          BPZ   5  OK   
   9 4YP=          ADX   5  GLEN(1)  
   7 4_8W          ADN   5  1
  21 4_NG    OK    SMO      5                   [PICK UP CORRECT RESTART LINE NUMBER 
   9 5286          LDX   5  GREST(1) 
  20 52MQ          BNZ   5  YOK                 [NO OF PAGES GIVEN MIGHT BE<IPAGE
   9 537B          LDX   5  GREST(1) 
   9 53M2    YOK   STO   5  GREST(1) 
  16 546L          LDX   5  GNUM(1)             [CURRENT LINE NO.
  16 54G*          LDX   4  GREST(1)            [RESTART LINE NO.
   9 54W4          ANDX  4  BITS22LS 
   7 559R          SBX   5  4
   9 55KG          LDXC  4  GLNO(1)  
  18 5656          BCS      SDT                 [J IF LIST TO END OF FILE
   7 56JQ          ADN   5  2
  17 574B          ADS   5  GLNO(1)             [UPDATE NO REMAINING 
   7 57J2          SBN   5  2
  18 583L    SDT   SBS   5  GNUM(1)             [SET NEW START LINE NO   
   9 58H=          STOZ     GPREM(1) 
  17 592W          LFBITSET J,2                 [PAGE RESTART SWITCH 
  18 594D ...      STOZ     GNEXT(1)            [ZEROISE GREST POINTER   
  10 5962 ...      STOZ     GREST+1(1)   
  20 597J ...      LDX   6  GLEN(1)                 [LOAD LENGTH OF GREST AREA   
   7 5996 ...      SBN   6  1
  18 59=N ...      BZE   6  PAGES1              [J IF ONLY 2 GREST WORDS 
  20 59#= ...      LDN   4  GREST+1(1)          [OTHERWISE ZEROISE OTHER GREST   
  14 59*S ...      LDN   5  GREST+2(1)          [WORDS   
   7 59CB ...      SMO      6
   7 59DY ...      MOVE  4  0
   5 59GG    PAGES1  
   8 5=26          LDX   1  FX1  
   7 5=FQ          ADX   3  1
   7 5=_B          EXIT  3  0
  16 5?F2          [THIS S/R TRANSFERS DATA FROM FILE TO BUFFER  
   5 5?YL    TRANSFER
   7 5#D=          SLL   3  2
  10 5#XW          ADX   3  GZPOINT(2)   
   7 5*CG          SRC   3  2
  10 5*X6          LDN   4  GZDATA+1(2)  
   7 5BBQ          SLL   4  2
   9 5BWB          ADX   4  GZDUMP(2)
   7 5CB2          SRC   4  2
  10 5CTL          LDX   2  GZTRANS(2)   
   9 5D*=          BZE   2  NODATA   
   7 5DSW          SMO      2
   7 5F#G          MVCH  3  0
   5 5FS6    NODATA  
   8 5G?Q          LDX   2  FX2  
   7 5GRB          EXIT  0  0
   5 5H?2    NOTENG  
   5 5H#3 ...#UNS AR1
   4 5H*4 ...(   
   9 5HB5 ...      LDX   4  CPPTR(2) 
   9 5HC6 ...      JNHLFDON 4,NOTENC 
  11 5HD7 ...      ENGPER   NOTENA,4,NOTENB,0
   8 5HF8 ...      BRN      REP  
   5 5HG9 ...NOTENB  
   7 5HH= ...      CLHLFDON 4
   9 5HJ? ...      BRN      SCANTY   
   5 5HK# ...NOTENA  
   7 5HL* ...      CLHLFDON 4
   9 5HMB ...      BRN      SBRKIN   
   5 5HNC ...NOTENC  
   4 5HPD ...)   
   8 5HQL          LDCT  0  #40  
   9 5J==          ANDX  0  CPRW1(2) 
   9 5KP6          BNZ   0  XERREST  
   7 5L8Q          LDN   4  0
  10 5LNB          STOZ     ACOMMUNE7(2) 
   6 5M82    YESINVALID  
  10 5MML          LDX   0  ACOMMUNE7(2) 
  21 5N7=          BNG   0  TEND                [J IF INV CHAR ON LAST REC OF FILE   
  11 5NLW          ENGPER   SBRKIN,,SCANTY   
  19 5P6G          BZE   4  REP                 [J IF NOT INVALID CHAR ERROR 
  15 5PL6          ANDX  4  BITS22LS            [REMOVE B0   
  21 5Q5Q          BNZ   4  SAMEREC             [J IF STILL ON SAME REC AS BEFORE CHE
  20 5QKB ...      MHUNTW   3,GOUT,INFOLIST     [ELSE UPDATE GNUM=CURRENT RECORD 
   7 5R52          LDN   0  1
   9 5RJL          ADS   0  GNUM(3)  
   9 5S4=          LDX   4  GLNO(3)  
  16 5SHW          BNG   4  SAMEREC             [J IF LIST TO EOF
   7 5T3G          SBN   4  1
   9 5TH6          STO   4  GLNO(3)  
  20 5W2Q          BZE   4  TEND                [J IF LAST REC OF LISTING INVALID
   5 5XFL    SAMEREC 
   8 5X_=          BRN      NOTP1
   5 5YDW    SCANTY  
   9 5YYG          CALL  3  PAGEREST 
   8 5_D6          BRN      SCANT
   5 5_XQ    XERREST 
   7 6296 ...      LADDP   ,0
   7 62GG ...      SMO      0
   9 62RW ...      LDX   0  AREPERT  
   8 635= ...      SLL   0  13   
  17 63BL          BPZ   0  NOTECCP             [J IF NOT 48 CHAR REP
  20 64TG          DOWN  LISTEXCP,3             [ELSE TEST FOR INVALID CHARACTER 
   6 65*6    TOINVTEST   
  10 65SQ          LDX   4  ACOMMUNE8(2) 
  18 68=W          BNZ   4  YESINVALID          [J IF INVALID CHARACTER  
  17 68QG          BRN      NOTINV              [J IF NO INVALID CHAR
   5 69=6    NOTECCP 
   8 69PQ          LDN   0  #1000
   9 6=9B          ANDX  0  AWORK1(2)
  16 6=P2          BZE   0  NOTINV              [J IF NOT MODE 4 
  19 6?8L          DOWN     LISTEXCP,5          [ELSE TEST FOR INVALID CHAR  
   9 6?N=          BRN      TOINVTEST
   5 6#7W    NOTINV  
   9 6#MG          CALL  3  PAGEREST 
  10 6*76          ENGPER   SBRKIN,,SCANT
  11 6BL2 ...      MHUNTW   1,GOUT,INFOLIST  
   9 6C5L          LDX   4  AWORK4(2)
   8 6DJG          ADX   5  FX1  
  17 6F46          BNG   4  PERI                [J IF SETUP LISTING  
   9 6FHQ          LDXC  0  GREST(1) 
  18 6G3B          BCC      NPAGE               [J IF NOT PAGE RESTART   
   6 6GH2          REWIND
  11 6GN8 ...      MHUNT    1,GOUT,INFOLIST  
   9 6GTB ...      LDX   4  GNUM(1)  
   7 6H57 ...      ADN   4  1
   5 6H#Y ...SKIPLOOP
  17 6HG6 ...      STEP                         [ TO RESTART POINT   
   9 6HM# ...      BCT   4  SKIPLOOP 
  10 6HSG ...      LDX   0  SETUPBIT(1)  
   9 6H_W          ANDX  0  AWORK2(2)
  17 6JFG          BZE   0  K1LISTLOOP          [J IF NO SETUP FILE  
   7 6J_6          LPUNSET   
  21 6KDQ          LFBITCLR I,2                 [LINE-UP MESSAGE SWITCH IN LISTPREP  
  17 6KYB ...      MHUNTW   1,GOUT,INFOSET      [REPEAT SETUP FILE   
  11 6LD2 ...      NAME     1,GOUT,INFOLIST  
   8 6LXL          FILEMOVE 1,0  
  15 6MC=          LDX   3  GSIGN               [SETUP FILE  
  15 6MWW          ORS   3  AWORK4(2)           [INDICATOR   
   6 6NBG          REWIND
  10 6NW6          BRN      K1LISTLOOP   
   8 6P*Q    NPAGE SMO      FX1  
   9 6PTB          LDX   0  SETUPBIT 
   9 6Q*2          ANDX  0  AWORK2(2)
   8 6QSL          BZE   0  NSET 
   7 6R#=          LPUNSET   
   8 6RRW    NSET  LDCT  4  #200 
   9 6S?G          ANDX  4  GREST(1) 
  17 6SR6          BZE   4  PERI                [J IF LINE RESTART   
  17 6T=Q          BRN      SCANT               [ALL RESTART=CANTDO  
  21 6TQB    #                              WE WANT TO ISOLATE THE CONDITION OF PAGE 
  21 6W=2    #                              RESTART ON A MAIN SPEC STAT FILE SO THAT 
  17 6WPL    #                              WE CAN REALIGN THE PAPER 
   5 6X9=    PAGES2  
   7 6XNW          LDN   6  1
  10 6Y8G          STO   6  ACOMMUNE6(2) 
   8 6YN6          BRN      PCD  
   5 6_7Q    SBRKIN  
   9 6_MB          CALL  3  TIDYUP   
  17 7272          BRN      QSETUP              [LISTING SETUP FILE  
  15 72BS          DOWN     LISTINIT,3          [ TERMINATORS
   8 72LL          LFEND    TE   
   5 736=    QSETUP  
  10 73KW          ACROSS   LISTPREP,3   
   4 73M5    [   
  17 73MG    [  THE FOLL. S/R IS PART OF THE LISTFILE ENHANCEMENT TO 
  10 73MX    [  READ ERROR RESILIENCE.   
  17 73N#    [  THE S/R IS CALLED IF STEP ENCOUNTERS A CORRUPT BLOCK 
  16 73NP    [  (INDICATED BY FLAG 'M' BEING SET IN THE ACTLF)   
  15 73P6    [  THE DEVICE IS DISENGAGED WITH THE MESSAGE :- 
  20 73PH    [  'PSE ENGAGE UNIT N IF COMPLETION OF CORRUPT LISTING REQUIRED '   
   4 73PY    [   
   4 73Q*    [   
   4 73QQ    SRER
   6 73QR ...#UNS  ANIP  
   4 73QS ...(   
  13 73QT ...[ IF NIP ACTLF GO TO TELL NIP PROGRAM   
   8 73QW ...      LDX   2  FX2  
   9 73QX ...      LDX   0  CPPTR(2) 
   8 73QY ...      BZE   0  PERI4
   4 73Q_ ...)   
   7 73R2 ...#UNS G3SIGNET   
   4 73R3 ...(   
   8 73R4 ...      SMO      FX2  
   8 73R5 ...      LDX   0  CPPTR
   9 73R6 ...      IF  0,U,ASIGNETPTR
   6 73R7 ...      THEN  
   4 73R8 ...)   
   8 73R9 ...      SBX   5  FX1  
  19 73RJ    XBSY  JUNBUSY  ,NOTBSY             [WAIT UNTIL DEVICE NOT BUSY  
   8 73R_          BRN      XBSY 
   5 73SB    NOTBSY  
  17 73SR          DISENGPER                    [THEN DISENGAGE IT   
  17 73T8          OUTPER                       [PSE ENGAGE MESSAGE  
  18 73TK          ENGPER   STERM,,SCDE,5      [APP. JUMPS IF TERM OR CD 
   8 73W2          ADX   5  FX1  
   7 73W4 ...#UNS G3SIGNET   
   4 73W6 ...(   
   5 73W8 ...      FI
   4 73W= ...)   
   7 73WC          EXIT  5  0
   4 73WS    [   
   4 73X9    [   
   9 73XL    [  TERMINATING ACTION   
   5 73Y3    STERM   
   9 73YD          CALL  3  TIDYUP   
  20 73YT          NULL                         [TO SATISFY EXIT FROM TIDYUP S/R 
  15 73_3          DOWN     LISTINIT,3          [ TERMINATORS
   8 73_=          LFEND    TE   
  13 73_M    [  IF CD REPLY GIVEN WE MUST RE-SCHEDULE
  11 7424    [  THE REQUEST FROM BEGINNING...
   4 742F    [   
   4 742W    SCDE
   9 743?          CALL  3  TIDYUP   
   6 743N          NULL  
   8 7445          LFEND    CD   
  21 745G    [***********************************************************************
   9 74K6    [MAIN BODY OF SEGMENT   
  21 754Q    [***********************************************************************
   5 75JB    POINT1  
   6 75JJ ...#UNS  ANIP  
   4 75JQ ...(   
  18 75JY ...[ IF NIP ACTLF OUTPUT CHANGED REPLY BEFORE LISTING PROPER   
   9 75K6 ...      LDX   0  CPPTR(2) 
   8 75K# ...      BNZ   0  NOTNP
   9 75KG ...      KEEPREP  0,CHANGED
   8 75KN ...      CALL  5  PERI5
   5 75KW ...NOTNP   
   4 75L4 ...)   
  10 75L# ...      MHUNT    1,GOUT,INFO  
   9 75N= ...      LDX   0  GMODE(1) 
   9 75Q8 ...      STO   0  AWORK2(2)
   8 75S8          LFBITCLR N,2  
   7 7642          LDN   7  0
  15 76HL          LDX   0  ALOGLEN(2)          [HAS ACTLF   
  16 773=          SBN   0  120                 [BEEN EXTENDED?  
   6 7758 ...#UNS  ANIP  
   4 7776 ...(   
   9 7794 ...      LDX   5  CPPTR(2) 
   9 77?2 ...      BNZ   5  NOTNIP   
  17 77#Y ...      SBN   0  70                   [NIP BLOCK LONGER   
   5 77BW ...NOTNIP  
   4 77DS ...)   
  15 77GW          BNG   0  NOTEXTENDED         [IF NOT JUMP 
  15 782G          LDN   7  1                   [ELSE SET KEY
   6 78G6    NOTEXTENDED 
  11 78RP ...      MHUNTW   3,GOUT,INFOLIST  
  13 795#    [  THIS CODE IS FOR THE RER ENHANCEMENT 
  15 796G    [  IF B11 SET IN GQUAL...THEN A CORRUPTION WAS  
  16 797N    [  DETECTED WHEN DOING CONTEXT SEARCH IN LISTOPEN   
  12 798W    [  IF SO WE MUST CALL S/R SRER...   
   9 79=4          LDX   0  GQUAL(3) 
   8 79?=          ANDX  0  BIT11
  16 79#D          BZE   0  NORERR              [J IF NO ERROR   
   8 79*L          CALL   5  SRER
   5 79BS    NORERR  
   9 79_2          LDX   0  GQUAL(3) 
   9 7=DL          STO   0  AWORK1(2)
  14 7=Y=          ANDN  0  1                   [SPECIAL?
  14 7?CW          BNZ   0  NOTMODE4            [J IF YES
   8 7?XG          LDN   0  #700 
   9 7#C6          ANDX  0  AWORK1(2)
  17 7#WQ          BZE   0  NOTMODE4            [J IF NOT SHIFT FILE 
   6 7#YF ...#UNS  ANIP  
   4 7*28 ...(   
  10 7*3X ...      LDX   0  JSOURCE3(2)  
   8 7*5L ...      SLL   0  12   
   4 7*7* ...)   
   6 7*94 ...#UNS  ANIP  
   4 7*=R ...#SKI
   4 7*#G ...(   
   8 7*FJ ...      LADDP      ,0 
   7 7*LL ...      SMO      0
   9 7*RN ...      LDX   0  AREPERT  
   8 7*YQ ...      SLL   0  13   
   4 7B5S ...)   
  13 7B*L          BNG   0  NOTMODE4            [J IF
  14 7BT=          SLL   0  1                   [NOT 96  
  19 7C#W          BPZ   0  NOTMODE4            [CHARACTER REPERTOIRE PRINTER
   8 7DRQ          LDN   0  #1000
   9 7F?B          ORS   0  GQUAL(3) 
   9 7FR2          ORS   0  AWORK1(2)
  15 7F_= ...      BNZ   7  TOK2                [J IF KEY SET
   6 7G7G ...#UNS  ANIP  
   4 7G*Q ...(   
   9 7GJ2 ...      LDX   0  CPPTR(2) 
   9 7GQ= ...      BNZ   0  NOTNIP1  
   9 7GYG ...      ALTLEND  FX2,320  
   8 7H6Q ...      BRN      TOK2 
   5 7H*2 ...NOTNIP1 
   4 7HH= ...)   
  20 7HPG          ALTLEND  FX2,141             [EXTEND ACTLF FOR MODE 4 PRINTING
  14 7JYJ ...TOK2  LDN   5  4                     [MODE 4
   8 7K8B          BRN      TOKEY
   5 7L7L    NOTMODE4
  16 7L*W ...      BZE   7  TOK3                [J IF KEY NOT SET
   6 7LJ6 ...#UNS  ANIP  
   4 7LQB ...(   
   9 7LYL ...      LDX   0  CPPTR(2) 
   9 7M6W ...      BNZ   0  NOTNIP2  
   9 7M*6 ...      ALTLEND  FX2,187  
   8 7MHB ...      BRN      TOK3 
   5 7MPL ...NOTNIP2 
   4 7MXW ...)   
  17 7N66          ALTLEND  FX2,100             [AND SHORTEN ACTLF   
  14 7N*Y ...TOK3  LDN   5  0                     [MODE 0
   5 7NKQ    TOKEY   
  10 7P5B          LDX   0  GCATBITS(3)  
   9 7PK2          STO   0  AWORK3(2)
   6 7PL? ...#UNS  ANIP  
   4 7PMJ ...(   
   9 7PNT ...      LDX  0  CPPTR(2)  
  15 7PQ6 ...      BZE  0  XNIP2                [NIP ACTLF   
   4 7PRC ...)   
  15 7PSN ...      DCA     ,DEX,5,MODE          [STORE MODE  
   6 7PT_ ...#UNS  ANIP  
  16 7PX= ...XNIP2 LDEX 6  JSOURCE3(2)          [GET PRINT WIDTH 
   6 7PYH ...#UNS  ANIP  
   4 7P_S ...#SKI
   4 7Q35 ...(   
  17 7Q4L          LADDP    ,4                  [GET POINTER TO APIA 
   7 7QJ=          SMO      4
  19 7R3W          LDEX  6  AREPERT             [SET UP X6 WITH PRINT WIDTH  
   4 7RHG ...)   
   9 7TG2 ...      STOZ     GZDUMP(2)
   9 7T_L ...      LDX   0  AWORK4(2)
  18 7WF= ...      BNG   0  SETUP               [ J IF LISTING SETUP FILE
   5 7WYW ...NOTP1   
   7 7XDG ...      STEPAGAIN 
   9 7XY6 ...      BRN      STARTED  
   5 7YCQ ...SETUP   
   6 7YXB ...      STEP  
  18 7_C2 ...      BZE   3  SERT                [ J IF SETUP FILE EMPTY  
   9 86QW          BRN      STARTED  
   5 87=G    START   
   6 87Q6          STEP  
  16 87X?          LFBBUS   M,2,NOCOR3          [J IF NO ERROR   
   8 87_2          LFBITCLR M,2  
   8 882P          CALL  5  SRER 
   7 884D          STEPAGAIN 
   5 8867    NOCOR3  
  16 889Q          BZE   3  TEND                [J IF END OF FILE
   5 88PB    STARTED 
  21 8992          LDEX  5  FRH(3)              [PICK UP NUMBER OF WORDS IN RECORD   
   8 89NL          LDN   0  #40  
   9 8=8=          ANDX  0  AWORK1(2)
  19 8=MW          LDN   4  4                   [INITIALISE PICKUP ADDRESS   
  19 8?7G          BNZ   0  PERIFILE            [J IF BASIC PERIPHERAL FILE  
   7 8?M6          LDN   0  4
   9 8#6Q          ANDX  0  AWORK1(2)
  16 8#LB          BZE   0  OK1                 [J IF NOT MONFILE
  19 8*62          ADN   4  4                   [INCREMENT PICKUP ADDRESS TO 
  18 8*KL                                       [ALLOW FOR CATEGORY WORD.
  20 8B5=          LDX   0  FRH+1(3)            [PICK UP RED-TAPE(CATEGORY WORD) 
   9 8BJW          ANDX  0  AWORK3(2)
  20 8C4G          BNZ   0  OK1                 [J IF THIS CATEGORY TO BE LISTED 
  11 8CJ6 ...      MHUNTW   1,GOUT,INFOLIST  
   7 8D3Q          LDN   0  1
  19 8DHB          ADS   0  GNUM(1)             [UPDATE CURRENT LINE NUMBER  
   9 8F32          LDX   2  GLNO(1)  
  18 8FGL          BNG   2  START               [J IF LIST TO END OF FILE
   9 8G2=          SBS   0  GLNO(1)  
  16 8GFW          BRN      START               [TRY NEXT RECORD 
   5 8G_G    PERIFILE
   7 8HF6          LDN   0  1
   9 8HYQ          ANDX  0  AWORK1(2)
  17 8JDB          BNZ   0  SPECIAL             [J IF SPECIAL LISTING
   8 8JY2          LDN   0  #1000
   9 8KCL          ANDX  0  AWORK1(2)
  17 8KX=          BNZ   0  NOCONV              [J IF MODE 4 PERIS   
   8 8LBW          LDN   0  #700 
   9 8LWG          ANDX  0  AWORK1(2)
  17 8MB6          BZE   0  NOCONV              [J IF NOT SHIFT FILE 
  19 8MTQ          LFBBS    K,2,OLDCONV         [J IF ALREADY AN ACONV BLOCK 
  19 8N*B          SETUPCORE 5,3,ADATA,ACONV    [BLOCK FOR SHIFT CONVERSION  
   8 8NT2          LFBITSET K,2  
   7 8P#L          PHOTO    7
   8 8PS=          LDN   4  A1(3)
   7 8Q?W          STEPAGAIN 
   9 8QRG          BRN      TMOVEMENT
   5 8R?6    OLDCONV 
  10 8RQQ          MHUNTW   3,ADATA,ACONV
   8 8S=B          LDN   4  A1(3)
   7 8SQ2          PHOTO    7
  11 8T9L          ALTLEN   3,5,ADATA,ACONV  
   7 8TP=          STEPAGAIN 
   6 8W8W    TMOVEMENT   
  17 8WNG          TESTMOVE 7,NOMOVE1           [J IF NO BLOCKS MOVED
  10 8X86          MHUNTW   1,ADATA,ACONV
   8 8XMQ          LDN   4  A1(1)
   5 8Y7B    NOMOVE1 
   7 8YM2          SMO      5
   7 8_6L          MOVE  3  0
  18 8_L=          LDN   1  #26                 [SET MODES FOR ALLCHAR TO
   9 925W          LDX   0  AWORK1(2)
   8 92KG          ANDN  0  #100 
   9 9356          BZE   0  NOTNORM  
   7 93JQ          LDN   1  6
   5 944B    NOTNORM 
  17 94J2          LDN   2  #10                 [GRAPHIC CONVERSION. 
   8 953L          TRANSFOR 1,2  
  10 95H=          MHUNTW   3,ADATA,ACONV
  20 962W          ADN   3  A1-FRH              [SET X3 TO POINT TO RECORD HEADER
  18 96GG          LDEX  5  FRH(3)              [RESET LENGTH OF RECORD  
   5 9726    NOCONV  
  17 97FQ          LDN   4  8                   [SET PICKUP ADDRESS  
   5 97_B    SPECIAL 
  18 98F2          LDEX   0 FRH(3)              [CHECK FOR EMPTY RECORD  
   7 98YL          SBN   0  2
   8 99D=          BZE   0  OK2  
  20 99XW          LDX   0  FRH+1(3)            [CALCULATE NUMBER OF CHARACTERS  
  18 9=CG          SLC   0  2                   [IN LAST WORD OF RECORD  
   7 9=X6          ANDN  0  3
   8 9?BQ          BZE   0  OK2  
  18 9?WB          SBN   5  1                   [ADJUST CHARACTER COUNT  
   8 9#B2          BRN      OK2  
  18 9#TL    OK1   LDN   0  0                   [NO ADJUSTMENT NECESSARY 
  17 9**=    OK2   SLL   5  2                   [CONVERT TO CHARS.   
  16 9*SW          ADX   5  0                   [ADJUST IF NEC.  
  19 9B#G          SBX   5  4                   [SUBTRACT P.U.ADDRESS TO GIVE
  19 9BS6                                       [NO OF CHARS OF DATA IN X5.  
  19 9C?Q          STO   5  GZLENGTH(2)         [RECORD LENGTH (I.E. DATA)   
  16 9CRB          STO   4  GZPOINT(2)          [PICK UP ADDRESS 
  13 9D?2          LDX   7  FRH+1(3)            [GET 
  14 9DQL          SRL   7  6                   [CURRENT 
  14 9F==          ANDN  7  #77                 [SHIFT   
   7 9FPW          LDN   0  2
  16 9G9G          ANDX  0  AWORK1(2)           [TEST FOR NUMBER 
   7 9GP6          SLL   0  2
  15 9H8Q          STO   0  GZDUMP(2)           [DUMP ADDRESS
   8 9HNB          BZE   0  NOSEQ
  10 9J82          STO   3  ACOMMUNE9(2) 
  10 9JML          LDN   2  GZDATA+1(2)  
  11 9K7= ...      MHUNTW   1,GOUT,INFOLIST  
   9 9KLW          LDX   4  GNUM(1)  
   8 9L6G          LDX   1  FX1  
  19 9LL6          TXL   4  NUMLP(1)            [TEST DOES NOT EXCEED MAXIMUM
   9 9M5Q          BCC      NOSEQ1   
   7 9MKB          LDN   3  6
   9 9N52          MPY   4  SMLP(1)  
   8 9NJL          LDN   1  #20  
   9 9P4=          CALL  0  SEQUENCE 
   5 9PHW    NOSEQ1  
  16 9Q3G          LDX   3  ACOMMUNE9(2)        [RESET POINTERS  
   5 9QH6    NOSEQ   
  17 9R2Q          LDN   4  #51                 [ HEAD OF FORM PFCC  
  17 9RGB          LFBBS    J,2,NEWPFCC         [ J IF PAGE RESTART  
  15 9S22          LDN   4  #41                 [ NORMAL PFCC
   9 9SFL          LDX   0  AWORK1(2)
   8 9S_=          ANDN  0  #340 
  21 9TDW          BZE   0  PFCCAM              [ J IF AMORPHOUS OR TAPE PUNCH FILE  
   9 9TYG          LDX   0  AWORK1(2)
   7 9WD6          ANDN  0  1
  16 9WXQ          BNZ   0  PFCC                [ J IF SPECIAL   
  18 9XCB          LFBBS    H,2,PFCC            [ J IF CONTINUED RECORD  
   9 9XX2          LDX   4  FRH+1(3) 
  16 9YBL          ANDN  4  #77                 [ PFCC FROM FILE 
   4 9YW=    PFCC
  10 9_8M ...      SEGENTRY K50LISTLOOP  
  17 9_H4 ...      BRN      NEWPFCC            [BRN NOT LFBASOVERFLO 
   7 9_TG          LDX   0  4
   8 =2*6          SBN   0  #41  
  19 =2SQ          BZE   0  NUPFCC              [ J IF NOT USER SUPPLIED PFCC
  18 =3#B          LFBITSET N,2                 [ SET USER FORMATTED FILE
   5 =3S2    NUPFCC  
  10 =3_8 ...      SEGENTRY K51LISTLOOP  
  18 =46B ...      BRN      PFCCAM            [J IF USER FORMATTED FILE  
  19 =4?L          LFBBS    N,2,NEWPFCC         [ J IF USER FORMATTED FILE   
  10 =4CH          SEGENTRY K61LISTLOOP  
   5 =4HD    PFCCAM  
   6 =4JP ...#UNS  ANIP  
   4 =4L2 ...(   
   9 =4M? ...      LDX   0  CPPTR(2) 
   9 =4NJ ...      BZE   0  NEWPFCC  
   4 =4PT ...)   
   8 =4R=          LDCT  0  #200 
  15 =5=W          ANDX  0  CPRW1(2)            [ REPLY WORD 
  18 =5QG          BZE   0  NEWPFCC             [ J IF NOT PAGE OVERFLOW 
   8 =6=6          LDN   4  #51  
  10 =6PQ          SEGENTRY K60LISTLOOP  
   5 =JWW    NEWPFCC 
   6 =JXN ...#SKI ENHPLOW
   4 =JYG ...(   
  20 =J_# ...      LFBBUS   U,2,SNPTH           [J IF PAPER LOW NOT BEEN FOUND   
  20 =K26 ...      LFBBS    V,2,SNPTH           [J IF PRINTING CONTINUATION LINE 
   8 =K2Y ...      LDN   0  #51  
   7 =K3Q ...      SBX   0  4
  21 =K4J ...      BZE   0  TISPL               [J IF THROW TO HEAD OF FORM PFCC TO  
  18 =K5B ...      SBN   0  #40                 [TAKE PAPER LOW ACTION   
   8 =K68 ...      BZE   0  TISPL
  20 =K72 ...      LADDP    ,1                  [J IF NO OF LINES SHOWS FULL PAGE
  20 =K7M ...      LDX   5  AREPERT(1)          [LISTED TO TAKE PAPER LOW ACTION 
   8 =K8# ...      SRL   5  12   
  11 =K8_ ...      MHUNTW   1,GOUT,INFOLIST  
   9 =K9L ...      LDX   1  GPREM(1) 
   7 =K=? ...      TXL   1  5
   8 =K?4 ...      BCC      TISPL
   8 =K?W ...      LDX   1  FX1  
  17 =K#N ...SNPTH                              [OTHERWISE CONTINUE  
   4 =K*G ...)   
   9 =KBG          STO   4  GZDATA(2)
  18 =KW6          LDX   0  6                   [MAX PERMISSABLE TRANSFER
  18 =L*Q          SBX   0  GZDUMP(2)           [MAX ROOM FOR DATA IN X0 
  16 =LTB          TXL   0  GZLENGTH(2)         [IS THERE ROOM?  
  13 =M*2          BCS      XLONG               [NO! 
   9 =MSL          LDX   0  AWORK1(2)
   8 =N#=          ANDN  0  #1000
  16 =NRW          BZE   0  NOTM4               [J IF NOT MODE 4 
   9 =Q=Q          LDX   0  GZDUMP(2)
   7 =QQB          SRL   0  2
   9 =RTH          BNZ   7  NMOBM4   
  16 =R_D          LDN   7  #74                 [GRAPHIC SHIFT   
   5 =S5*    NMOBM4  
   7 =S9=          SMO      0
  18 =SNW          DCH   7  GZDATA+1(2)         [MOVE IN CURRENT SHIFT   
   7 =T8G          LDN   0  1
   9 =TN6          ADS   0  GZDUMP(2)
   5 =WMB    NOTM4   
  17 =X72          LDX   5  GZLENGTH(2)         [TRANSFER WHOLE LOT  
  10 =XLL          STO   5  GZTRANS(2)   
   9 =Y6=          CALL  0  TRANSFER 
   8 =YKW          LDN   0  #20  
  20 =_5G          ANDX  0  AWORK1(2)           [J IF SPACE FILLING NOT REQUIRED.
   8 =_K6          BZE   0  NOSP1
   7 ?24Q          LDX   3  6
   9 ?2JB          SBX   3  GZDUMP(2)
  18 ?342          SBX   3  5                   [X3 = NO OF UNUSED CHARS 
  16 ?3HL          BZE   3  NOSP1               [J IF BUFFER FULL
   7 ?43=          LDX   2  4
   8 ?4GW    PUTSP DCH   0  0(2) 
   7 ?52G          BCHX  2  /
   8 ?5G6          BCT   3  PUTSP
   8 ?5_Q          LDX   2  FX2  
   8 ?6FB ...      BRN      STCNT
  18 ?7DL    XLONG                              [DEAL WITH LONG RECORD   
   9 ?7Y=          LDX   5  AWORK1(2)
   8 ?8CW          ANDN  5  #1000
  16 ?8XG          BZE   5  NOTM4HERE           [J IF NOT MODE 4 
  20 ?=BB          DOWN     LISTEXCP,4          [ELSE ALLOW FOR EXTENDED COUNT   
  16 ?=W2          LDX   7  ACOMMUNE2(2)        [GET TRACED SHIFT
   9 ?##W          BRN      SPACED   
   6 ?*#6    NOTM4HERE   
  14 ?*RQ          LDX   5  0                   [MAXIMUM 
  18 ?B?B          SBN   5  4                   [MINUS 4 FOR CONTINUATION
  10 ?BR2          STO   5  GZTRANS(2)   
   9 ?C=L          CALL  0  TRANSFER 
   8 ?CQ=          SMO      FX1  
   8 ?D9W          LDN   3  XCONT
   7 ?DPG          MVCH  3  4
   5 ?DS5 ...STCNT   
   6 ?DWN ...#UNS  ANIP  
   9 ?D_? ...      STO   6  GZDUMP(2)
   6 ?F3W ...#UNS  ANIP  
   4 ?F6F ...#SKI
  10 ?F96          DCA      ,STO,6,COUNT 
   9 ?FNQ          BRN      SPACED   
   7 ?G8B    NOSP1 LDX   3  5
   9 ?GN2          ADX   3  GZDUMP(2)
   6 ?GR6 ...#UNS  ANIP  
   9 ?GW= ...      STO   3  GZDUMP(2)
   6 ?G_B ...#UNS  ANIP  
   4 ?H4G ...#SKI
  10 ?H7L          DCA      ,STO,3,COUNT 
   5 ?HM=    SPACED  
   7 ?J6W          LDN   3  1
   6 ?J=2 ...#UNS  ANIP  
   9 ?J*6 ...      ADS   3  GZDUMP(2)
   6 ?JD= ...#UNS  ANIP  
   4 ?JHB ...#SKI
  14 ?JLG          DCA      ,ADS,3,COUNT        [FOR PFCC
   5 ?K66    RPERI   
  12 ?KKQ    [OUTPUT RECORD AND UPDATE COUNTS ETC
  14 ?L5B    [OUTPUT RUNOUT IF REQUIRED FOR PAPER TAPE   
   8 ?LK2          CALL  5  PERI 
  18 ?M4L          LFBITCLR J,2                 [UNSET PAGEREST MARKER   
  11 ?MJ= ...      MHUNTW   3,GOUT,INFOLIST  
  14 ?N3W          LDX   0  GZDATA(2)           [UPDATE  
  13 ?NHG          ANDN  0  7                   [LINE
  14 ?P36          ADS   0  GPREM(3)            [COUNT   
   9 ?P73          LDX   0  GZDATA(2)
   8 ?P=Y          ANDN  0  #50  
  17 ?PBT          BZE   0  XFIN                [ J IF NO PRINT PFCC 
  10 ?PGQ          LDX   0  GZLENGTH(2)  
  10 ?Q2B          SBX   0  GZTRANS(2)   
   8 ?QG2          BZE   0  XFIN 
  10 ?Q_L          STO   0  GZLENGTH(2)  
  10 ?RF=          LDX   0  GZTRANS(2)   
  10 ?RYW          ADS   0  GZPOINT(2)   
   7 ?SDG          LDN   0  2
  16 ?SY6          ANDX  0  AWORK1(2)           [CHECK FOR NUMBER
   7 ?TCQ          SLL   0  2
  15 ?TXB          ADN   0  4                   [INDENTATION 
   9 ?WC2          STO   0  GZDUMP(2)
   8 ?WWL          LDX   0  ACES 
  10 ?XB=          STO   0  GZDATA+1(2)  
  10 ?XTW          STO   0  GZDATA+2(2)  
  10 ?Y*G          STO   0  GZDATA+3(2)  
   8 ?YT6          LFBITSET H,2  
   6 ?YX* ...#SKI ENHPLOW
   4 ?Y_J ...(   
  18 ?_3R ...      LFBBUS   U,2,SSSS            [J PAPER LOW NOT FOUND   
  21 ?_62 ...      LFBITSET V,2                 [OTHERWISE SET CONTINUATION LINE BIT 
   4 ?_89 ...SSSS
   4 ?_=D ...)   
  16 ?_#Q          LFBBS    K,2,NOSTEP          [J IF ACONV BLOCK
   7 ?_SB          STEPAGAIN 
   8 #2#2          BRN      NOSEQ
   5 #2RL    NOSTEP  
  10 #3?=          MHUNTW   3,ADATA,ACONV
   9 #3QW          ADN   3  A1-FRH   
   8 #4=G          BRN      NOSEQ
  20 #4Q6    XFIN  LFBITCLR H,2                 [UNSET ANY LONG RECORD MARKERS   
   5 #59Q    RPERI2  
  11 #5PB ...      MHUNTW   3,GOUT,INFOLIST  
   7 #692          LDN   4  1
   9 #6NL          ADS   4  GNUM(3)  
  20 #78=          LDX   4  GLNO(3)             [TEST AND J IF LAST LINE OUTPUT  
   8 #7MW          BNG   4  TOEND
   7 #87G          SBN   4  1
   9 #8M6          STO   4  GLNO(3)  
  16 #96Q          BZE   4  TEND                [LAST LINE OUTPUT
   5 #9LB    TOEND   
  17 #=62          LDX   4  GREST(3)            [J IF PAGE RESTART   
   8 #=KL          BNG   4  PAGE 
  16 #?5=          LDCT  4  128                 [J IF ALL RESTART
   9 #?JW          ANDX  4  GREST(3) 
   8 ##4G          BNZ   4  SPLIT
  20 ##J6          LDX   4  GNUM(3)             [OTHERWISE SET LINE NUMBER FOR   
  14 #*3Q          STO   4  GREST(3)            [RESTART 
   5 #*HB    SPLIT   
   4 #*M? ...SLPH
   6 #*R8 ...#UNS  ANIP  
   4 #*X5 ...(   
   9 #B32 ...      LDX   0  CPPTR(2) 
  16 #B6X ...      BZE   0  START               [J IF NIP ACTLF  
   4 #B=S ...)   
  17 #BBP ...      LDCT  4  #100                [TEST FOR PAPER LOW  
   9 #BGL          ANDX  4  CPRW1(2) 
  14 #C2=          BNZ   4  PLOW                [J IF LOW
  18 #CFW    SCARD BRN      START               [J TO OUTPUT NEXT RECORD 
   9 #C_G    PAGE  LDX   4  GZDATA(2)
   8 #DF6          ANDN  4  63   
   5 #DYQ    SPERI   
   8 #FDB          LDN   0  #51  
   7 #FY2          SBX   0  4
   8 #GCL          BZE   0  PAGRE
   8 #GX=          SBN   0  #40  
   8 #HBW          BZE   0  PAGRE
   9 #HWG          LDX   4  GPREM(3) 
   8 #J2Y          LADDP    ,1   
  10 #J7B          LDX   0  AREPERT(1)   
   8 #J?S          SRL   0  12   
   7 #JD=          ADN   0  1
   8 #JJN          LDX   1  FX1  
   7 #JP6          TXL   4  0
   8 #JTQ          BCC      PAGRE
   8 #K*B          BRN      SLPH 
   5 #KT2    PAGRE   
  20 #L#L          LDN   4  0                   [INITIALISE GREST TABLE POINTER  
  21 #LS=          LDX   5  GLEN(3)             [GLEN=N WHERE GREST+N IS LAST WORD   
  21 #M?W          BZE   5  PAGE1               [J IF GOUT NOT LENGTHENED FOR MULTIP 
   9 #MRG          LDX   4  GNEXT(3) 
   7 #N?6          TXL   4  5
  15 #NQQ          BCS      XOK1                [J GNEXT<N   
   7 #P=B          NGN   4  1
   7 #PQ2    XOK1  ADN   4  1
  17 #Q9L          STO   4  GNEXT(3)            [GNEXT=0 OR GNEXT+1  
   5 #QP=    PAGE1   
   8 #R8W          LDCT  5  256  
   9 #RNG          ORX   5  GNUM(3)  
  19 #S86          SBN   5  1                   [ADJUST TO GET RIGHT LINE NO.
   7 #SMQ          SMO      4
   9 #T7B          STO   5  GREST(3) 
   7 #TM2          LDN   0  1
  19 #W6L          STO   0  GPREM(3)            [ONE LINE PRINTED ON NEW PAGE
   8 #WL=          BRN      SLPH 
   4 #X5W    PLOW
   6 #X6R ...#SKI ENHPLOW
   4 #X7N ...(   
  21 #X8K ...      LDX   4  GREST(3)            [J TO TAKE PLOW ACTION IF NOT PAGE   
  14 #X9G ...      BPZ   4  TISPL               [RESTART 
  20 #X=C ...      LFBBS    V,2,PLOW3           [ALSO J IF CONTINUATION LINES OR 
  18 #X?# ...      JBUSY    ,PLOW3              [BUSY PRINTER ENCOUNTERED
  18 #X#9 ...      LFBITSET U,2                 [ELSE SET PAPER LOW BIT  
  19 #X*6 ...      BRN      START               [J TO CONTINUE PRINTING PAGE 
   5 #XB3 ...PLOW3   
  17 #XBY ...      LFBITCLR V,2                 [CLEAR MARKER BITS   
   8 #XCT ...      LFBITCLR U,2  
   5 #XDQ ...TISPL   
  20 #XFM ...      LDCT  4  #100                [IF NO PLOW REPLY TO LAST PERI   
  21 #XGJ ...      ANDX  4  CPRW1(2)            [THEN ASSUME OPERATORS HAVE BEATEN   
  14 #XHF ...      BZE   4  OPSAL               [US TO IT
   4 #XJB ...)   
  19 #XKG          DOWN     LISTNEXT,10         [INFORM OPERATOR OF PAPER LOW
   3 #XPC ...
   7 #XT# ...#SKI ENHPLOW$1  
   4 #X_9 ...(   
  11 #Y56          ENGPER   SBRKIN,,SCANTY   
   8 #Y74 ...      BRN      START
   4 #Y92 ...)   
   6 #Y=Y ...#SKI ENHPLOW
   4 #Y#W ...(   
  11 #YBS ...      ENGPER   SBRKIN,,SCANTY1  
   5 #YDQ ...OPSAL   
  19 #YGN ...      LFBBUS   U,2,START           [IF NOT STARTING SAME PAGE J 
  17 #YJL ...      LFBITCLR U,2                 [ELSE CLEAR PLAN BIT 
  20 #YLJ ...      STEPAGAIN                    [AND J TO PRINT RECORD WE WERE   
  14 #YNG ...      BRN      STARTED             [PRINTING
   5 #YQD ...SCANTY1 
  20 #YSB ...      LFBBUS   U,2,SCANTY          [CLEAR PLOW BIT IF SET SO PLOW   
  20 #YW# ...      LFBITCLR U,2                 [MESSAGE ISN'T OUTPUT TWICE IE BY
  16 #YY= ...      BRN      SCANTY              [PAGEREST ROUTINE
   4 #_28 ...)   
   9 #_4B    SCANT CALL  3  TIDYUP   
  17 #_J2          BRN      SLIST               [LISTING SETUP FILE  
   9 *23L          ANDX  4  BITS22LS 
   8 *2H=          LFEND    CD,4 
  12 *2MN ...SLIST          HUNTW 3,GOUT,INFOLIST
   9 *2S6          BNG   3  SLIST1   
   7 *2YJ          FREECORE 3
   8 *352          BRN      SLIST
   5 *39D    SLIST1  
   8 *3*W          LFEND    CD   
   4 *3GG    [   
   4 *4_B    SERT
   8 *5F2 ...[ SETUP FILE EMPTY  
  10 *5YL ...      DOWN     LISTINIT,6   
   4 *85B    TEND
   8 *8?L          LDX   2  FX2  
  19 *8FW          LFBITCLR N,2                 [ CLEAR USER FORMATTED FILE  
   9 *8N6          CALL  3  TIDYUP   
  19 *8WB          BRN      TLIS                [CURRENTLY LISTING SETUP FILE
  14 *9B2          STO   2  AWORK1(2)           [SWITCH  
  17 *9TL          DOWN     LISTINIT,3          [OUTPUT TERMINATORS. 
   6 *=*=          LFEND 
  17 *=SW    TLIS  LDN   6  1                   [SWITCH FOR LISTPREP 
   5 *=XF ...TLIST   
   7 *?24 ...#UNS G3SIGNET   
   4 *?4M ...(   
   8 *?7= ...      SMO      FX2  
   8 *?9T ...      LDX   0  CPPTR
   9 *?#D ...      IF  0,E,ASIGNETPTR
   6 *?C3 ...      THEN  
  10 *?FL ...        ACROSS  LISTPREP,3  
   5 *?J9 ...      FI
   4 *?LS ...)   
   6 *?PC ...      REWIND
  10 *?S6          ACROSS   LISTPREP,2   
   5 *#?Q    XGEOER  
  10 *#RB          GEOERR   1,BREAK IN   
   4 **?2    #END
   8 ____ ...42107625000100000000
