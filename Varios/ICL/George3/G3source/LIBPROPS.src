   7 22FL    #LIS  K0LIBPROPS
  15 22_=    #SEG           LIBPROPS            [N.R.BOULT   
  10 23DW                   8HLIBPROPS   
   4 23YG    #   
  12 24D6          SEGENTRY K1LIBPROPS,QK1LIBP   
  12 24XQ          SEGENTRY K2LIBPROPS,QK2LIBP   
  12 25CB          SEGENTRY K3LIBPROPS,QK3LIBP   
  12 25JJ ...      SEGENTRY K4LIBPROPS,QK4LIBP   
  12 25PQ ...      SEGENTRY K5LIBPROPS,QK5LIBP   
   4 25X2    #   
  15 26BL    #     K1LIBPROPS IMPLEMENTS THE ENTPROP MACRO   
  15 26W=    #     K2LIBPROPS IMPLEMENTS THE MTEXPROP MACRO  
  14 27*W    #     K3LIBPROPS IMPLEMENTS THE MTCHAR MACRO
  15 27KN ...[     K4LIBPROPS IMPLEMENTS THE OUTMTPROP MACRO 
  15 27TG ...[     K5LIBPROPS IMPLEMENTS THE MTSERSPD MACRO  
  10 285# ...      SEGENTRY K9LIBPROPS   
   9 28*6    STAND          #40000000
  10 28SQ    PROP           8HPROPERTY   
   8 29#B    MPERSAND       #26  
   8 29S2    SPACE          #20  
   8 2=?L    THIRT          +13  
   8 2=R=    TEN            +10  
   8 2?=W    YPHEN          #35  
   4 2?QG    #   
   4 2#=6    #   
   5 2#PQ    QK1LIBP 
  10 2*9B          MHUNT    3,CPB,CUNI   
   9 2*P2          LDX   4  ANUM(3)  
  15 2B8L          ANDN  4  #7777               [NO. CHARS   
   9 2BN=          LDX   0  APARA(3) 
   8 2C7W          SRL   0  12   
   8 2CMG          SBN   0  #6062
  19 2D76          BNZ   0  PERR1               [BRANCH IF NOT PR QUALIFIER  
   9 2DLQ          LDX   0  APARA(3) 
  10 2F6B          BXU   0  PROP(1),PE1  
  10 2FL2          LDX   0  APARA+1(3)   
  10 2G5L          BXU   0  PROP+1(1),PE1
   7 2GK=          SBN   4  8
   7 2H4W          ADN   3  2
   8 2HJG          BRN      PE1A 
   4 2J46    PE1 
   7 2JHQ          BCHX  3  /
   7 2K3B          BCHX  3  /
   7 2KH2          SBN   4  2
   4 2L2L    PE1A
   8 2LG=          BZE   4  PERR1
  11 2L_W    #     FIND NUMBER OF PROPERTIES 
   7 2MFG          LDX   5  4
   7 2M_6          LDX   2  3
   7 2NDQ          LDN   6  1
   9 2NYB    PE2   LDCH  0  APARA(2) 
  11 2PD2          BXU   0  MPERSAND(1),PE3  
   7 2PXL          ADN   6  1
   4 2QC=    PE3 
   7 2QWW          SBN   5  1
   7 2RBG          BCHX  2  /
  20 2RW6          BNZ   5  PE2                 [BRANCH UNLESS END OF QUALIFIER  
   7 2S*Q          STO   6  5
   7 2STB          SLL   6  2
   7 2T*2          ADN   6  2
  10 2TSL          MHUNT    1,CPB,CUNI   
   7 2W#=          SBX   3  1
  11 2WRW          SETUPCORE 6,2,FILE,FPROPS 
  16 2X?G          STO   6  A1(2)               [LOGICAL LENGTH  
  17 2XR6          STO   5  A1+1(2)             [NO. OF PROPERTIES   
  17 2Y=Q          LDN   6  A1+2(2)             [FIRST PROPERTY CELL 
  10 2YQB          MHUNT    1,CPB,CUNI   
   7 2_=2          ADX   3  1
   8 2_PL          LDX   1  FX1  
   4 329=    PE3A
   7 32NW          LDX   2  6
   7 338G          LDN   5  0
  10 33N6    #     PROCESS NEXT PROPERTY 
   4 347Q    PE4 
   9 34MB          LDCH  0  APARA(3) 
  19 3572          BXE   0  MPERSAND(1),PE8     [BRANCH IF END OF PROPERTY   
  19 35LL          BXGE  5  THIRT(1),PERR       [BRANCH IF PROPERTY TOO LONG 
   8 366=          BNZ   5  PE5  
  20 36KW          BXE   0  SPACE(1),PE7A       [BRANCH IF NON-SIGNIFICANT SPACE 
   8 375G          BRN      PE6  
   4 37K6    PE5 
  10 384Q          BXL   0  TEN(1),PE7   
  10 38JB          BXE   0  YPHEN(1),PE7 
  10 3942          BXE   0  SPACE(1),PE7 
   4 39HL    PE6 
   8 3=3=          SBN   0  #41  
  18 3=GW          BNG   0  PERR                [BRANCH IF NON-ALPHABETIC
   9 3?2G          SBN   0  #73-#41  
  18 3?G6          BPZ   0  PERR                [BRANCH IF NON-ALPHABETIC
   8 3?_Q          ADN   0  #73  
   4 3#FB    PE7 
  17 3#_2          DCH   0  1(2)                [DEPOSIT CHARACTER   
   7 3*DL          BCHX  2  /
  15 3*Y=          ADN   5  1                   [NO. CHARS   
   4 3BCW    PE7A
   7 3BXG          BCHX  3  /
   7 3CC6          SBN   4  1
   8 3CWQ          BNZ   4  PE4  
   8 3DBB          BRN      PE8A 
  16 3DW2    PE8                                [END OF PROPERTY 
   7 3F*L          BCHX  3  /
   7 3FT=          SBN   4  1
   4 3G#W    PE8A
   7 3GSG          SMO      6
  18 3H#6          STO   5  0                   [STORE PROPERTY LENGTH   
   7 3HRQ          ADN   6  4
  20 3J?B          BNZ   4  PE3A                [BRANCH UNLESS END OF QUALIFIER  
   8 3JR2          SETREP   OK   
   5 3K=L          UP
  15 3KQ=    PERR                               [FORMAT ERROR
  10 3L9W          MFREE    FILE,FPROPS  
   5 3LPG    PERR1   
   9 3M96          SETREP   NAMEFORM 
   5 3MNQ          UP
   4 3N8B    #   
  11 3NN2    #     CODE FOR MTEXPROP MACRO   
   4 3P7L    #   
  17 3P#S ...[ N.B. THIS TABLE IS ALSO USED BY THE OUTMTPROP MACRO   
   4 3PG2 ...[   
   8 3PM=    QTABLE         +6   
   9 3Q6W                   12HTRACK7
   9 3QLG                   +GLIBTRA7
   7 3QRN ...#UNS  JMODGCR   
   4 3QYW ...#SKI
  13 3R66                  +GLIBTRA9+GLIBPE+GLIBNRZI 
   7 3R?# ...#UNS  JMODGCR   
  15 3RDG ...              +GLIBTRA9+GLIBPE+GLIBNRZI+GLIBGCR 
   8 3RKQ                   +3   
   7 3RPM ...#UNS  JMODGCR   
   4 3RTJ ...#SKI
   4 3R_F ...(   
   8 3S5B                   +6   
   9 3SK2                   12HTRACK9
   9 3T4L                   +GLIBTRA9
  11 3TJ=                  +GLIBTRA7+GLIBMODE
   8 3W3W                   +3   
   4 3W?N ...)   
   8 3WHG                   +2   
   8 3X36                   12HPE
   9 3XGQ                   +GLIBPE  
   7 3XMY ...#UNS  JMODGCR   
   4 3XT6 ...#SKI
  14 3Y2B                  +GLIBTRA7+GLIBMODE+GLIBNRZI   
   7 3Y7J ...#UNS  JMODGCR   
  16 3Y#Q ...              +GLIBTRA7+GLIBMODE+GLIBNRZI+GLIBGCR   
   8 3YG2                   +3   
   8 3Y_L                   +4   
   9 3_F=                   12HNRZI  
   9 3_YW                   +GLIBNRZI
   7 4264 ...#UNS  JMODGCR   
   4 42?= ...#SKI
  13 42DG                   +GLIBTRA7+GLIBMODE+GLIBPE
   7 42KN ...#UNS  JMODGCR   
  15 42QW ...               +GLIBTRA7+GLIBMODE+GLIBPE+GLIBGCR
   8 42Y6                   +3   
   7 42_9 ...#UNS  JMODGCR   
   4 432# ...(   
   8 433C ...               +3   
   9 434G ...               12HGCR   
   9 435K ...               +GLIBGCR 
  16 436N ...               +GLIBTRA7+GLIBMODE+GLIBPE+GLIBNRZI   
   8 437R ...               +3   
   8 438W ...               +6   
   9 439_ ...               12HTRACK9
   9 43?4 ...               +GLIBTRA9
  11 43#7 ...              +GLIBTRA7+GLIBMODE
   8 43*= ...               +3   
   4 43B* ...)   
   8 43CQ                   +4   
   9 43XB                   12HMODE  
  12 44C2                   +GLIBMODE+GLIBTRA7   
   7 44J8 ...#UNS  JMODGCR   
   4 44PB ...#SKI
  13 44WL                  +GLIBTRA9+GLIBPE+GLIBNRZI 
   7 453S ...#UNS  JMODGCR   
  15 4592 ...              +GLIBTRA9+GLIBPE+GLIBNRZI+GLIBGCR 
   8 45B=                   +3   
   8 45TW                   +4   
   9 46*G                   12HNONS  
   9 46T6                   +GLIBNONS
   8 47#Q                   +0   
   8 47SB                   +1   
   8 48#2                   +4   
   9 48RL                   12HFAST  
   9 49?=                   +GLIBFAST
   9 49QW                   +GLIBSLOW
   8 4==G                   +2   
   8 4=Q6                   +4   
   9 4?9Q                   12HSLOW  
   9 4?PB                   +GLIBSLOW
   9 4#92                   +GLIBFAST
   8 4#NL                   +2   
   8 4*8=    QTABND         +0   
   9 4*MW    QMODE          4HMODE   
   9 4B7G    QMASK          #77777703
   7 4B=L ...#UNS     JMT21  
   7 4B*Q ...QTHSV       +45 
   7 4BDW ...#UNS     JMT21  
   4 4BJ2 ...#SKI
   8 4BM6    QTHSV          +37  
   5 4C6Q    QK2LIBP 
  17 4CLB          STOZ  AWORK1(2)              [ILLEGAL COMBINATION 
  18 4D62          STOZ  AWORK2(2)              [SPECIAL PROPERTY WORD   
  10 4DKL          LDX   7  ACOMMUNE1(2) 
   7 4F5=          SBN   7  1
  20 4FJW          BNZ   7  Q1                  [BRANCH UNLESS TAPE PROPERTIES   
  10 4G4G          MHUNT    3,FILE,FPROPS
   8 4GJ6          BRN      Q2   
   4 4H3Q    Q1  
  11 4HHB          MHUNT    3,APERI,APROPS   
   4 4J32    Q2  
  19 4JGL          LDX   6  A1+1(3)             [NO. OF PROPERTIES IN BLOCK  
   9 4K2=          LDN   3  A1+2(3)  
   4 4KFW    Q2B 
   8 4K_G          SMO      FX1  
  18 4LF6          LDN   5  QTABLE              [ADDRESS OF PROPERTY LIST
   4 4LYQ    Q2A 
   7 4MDB          LDX   1  5
  18 4MY2          LDX   7  0(1)                [NO OF SIGNIFICANT CHARS.
   7 4NCL          LDX   2  3
   4 4NX=    Q3  
  17 4PBW          LDCH  0  1(2)                [PROPERTY FROM BLOCK 
  17 4PWG          LDCH  4  1(1)                [PROPERTY FROM LIST  
   8 4QB6          BXU   0  4,Q3A
   7 4QTQ          BCHX  1  /
   7 4R*B          BCHX  2  /
   8 4RT2          BCT   7  Q3   
   7 4S#L          SMO      5
   7 4SS=          LDX   0  6
   8 4T?W          SMO      FX2  
   9 4TRG          ANDX  0  ACOMMUNE1
  18 4W?6          BZE   0  Q5                  [IGNORE IF INAPPROPRIATE 
   7 4WQQ          SMO      5
   7 4X=B          LDX   0  4
   8 4XQ2          SMO      FX2  
   9 4Y9L          ANDX  0  AWORK1   
  20 4YP=          BNZ   0  QERR1               [BRANCH IF ILLEGAL COMBINATION   
   8 4_8W          SMO      FX1  
   8 4_NG          LDX   0  QMODE
  17 5286          BXU   0  1(3),Q4             [BRANCH UNLESS MODE  
   8 52MQ          LDX   0  0(3) 
   7 537B          SBN   0  7
  18 53M2          BPZ   0  QERR2               [BRANCH IF TOO MANY CHARS
   7 546L          ADN   0  3
  17 54L=          BZE   0  QM2                 [BRANCH IF VALUE NULL
   7 555W          SBN   0  1
  17 55KG          BNZ   0  QM1                 [BRANCH IF 2 CHARS   
   8 5656          LDCH  0  2(3) 
   8 56JQ          SMO      FX1  
   9 574B          BXGE  0  TEN,QERR2
   8 57J2          BRN      QM2  
   4 583L    QM1 
   8 58H=          LDX   0  2(3) 
   8 592W          SRL   01 18   
   8 59GG          SRL   1  18   
   8 5=26          SMO      FX1  
   8 5=FQ          MPA   0  TEN  
   7 5=_B          LDX   0  1
   4 5?F2    QM2 
   7 5?YL          STO   0  1
   8 5#D=          SMO      FX1  
   8 5#XW          ANDX  1  QMASK
  18 5*CG          BNZ   1  QERR2               [BRANCH IF WRONG VALUE   
   8 5*X6          SMO      FX1  
  18 5BBQ          BXGE  0  QTHSV,QERR2         [BRANCH IF ILLEGAL MODE  
   8 5BWB          SLL   0  18   
   8 5CB2          SMO      FX2  
   9 5CTL          ORS   0  AWORK2   
   4 5D*=    Q4  
   7 5DSW          SMO      5
   7 5F#G          LDX   0  4
   8 5FS6          SMO      FX2  
  16 5G?Q          ORS   0  AWORK2              [STORE PROPERTY  
   7 5GRB          SMO      5
   7 5H?2          LDX   0  5
   8 5HQL          SMO      FX2  
  20 5J==          ORS   0  AWORK1              [STORE ILLEGAL COMBINATION BITS  
   4 5JPW    Q5  
   7 5K9G          ADN   3  4
   7 5KP6          SBN   6  1
  21 5L8Q          BNZ   6  Q2B                 [BRANCH UNLESS ALL PROPERTIES EXAMINE
   8 5LNB          LDX   2  FX2  
   9 5M82          LDX   0  AWORK1(2)
  18 5MML          STO   0  ACOMMUNE2(2)        [ILLEGAL COMBINATION BITS
   9 5N7=          LDX   0  AWORK2(2)
  16 5NLW          STO   0  ACOMMUNE1(2)        [PROPERTY BITS   
   8 5P6G          SETREP   OK   
   5 5PL6          UP
   5 5Q5Q    QERR1   
   9 5QKB          SETREP   ILLCOMB  
   8 5R52          BRN      QERR3
   5 5RJL    QERR2   
   9 5S4=          SETREP   ILLMODE  
   5 5SHW    QERR3   
   5 5T3G          UP
   4 5TH6    Q3A 
   7 5W2Q          ADN   5  7
   7 5WGB          SMO      5
   7 5X22          LDX   0  0
  19 5XFL          BNZ   0  Q2A                 [BRANCH UNLESS END OF LIST   
   8 5X_=          BRN      Q5   
   4 5YDW    #   
  10 5YYG    #     CODE FOR MTCHAR MACRO 
   4 5_D6    #   
   4 5_XQ    RTAB
   9 62CB                   +GLIBTRA9
   8 62X2                   #200 
   9 63BL                   +GLIBTRA7
   8 63W=                   +0   
   9 64*W                   +GLIBPE  
   8 64TG                   #202 
   9 65*6                   +GLIBNRZI
   8 65SQ                   #201 
   7 65X* ...#UNS  JMODGCR   
   4 65_Y ...(   
   9 664H ...               +GLIBGCR 
   9 6676 ...               +HWPGCR  
   4 669P ...)   
   5 66#B    QK3LIBP 
   9 66S2          LDX   5  STAND(1) 
  10 67?L          LDX   4  ACOMMUNE1(2) 
   7 67DS ...#UNS  JMODGCR   
   4 67L2 ...#SKI
  15 67R=          ANDN  4  GLIBTRA9+GLIBTRA7+GLIBPE+GLIBNRZI
   7 67YD ...#UNS  JMODGCR   
  17 685L ...      ANDN  4  GLIBTRA9+GLIBTRA7+GLIBPE+GLIBNRZI+GLIBGCR
  20 68=W          BZE   4  R4                  [BRANCH IF NO SPECIAL PROPERTIES 
   7 68QG          LDX   0  4
   9 69=6          ANDN  0  GLIBTRA9 
  16 69PQ          BNZ   0  R5                  [BRANCH IF TRACK9
   4 6=9B    R1  
   7 6=#G ...#UNS  JMODGCR   
   8 6=CL ...      LDN   0  10   
   7 6=GQ ...#UNS  JMODGCR   
   4 6=KW ...#SKI
   7 6=P2          LDN   0  8
   4 6?8L    R2  
   7 6?N=          SMO      0
  21 6#7W          BXE   4  RTAB-2(1),R3        [BRANCH WHEN SPECIAL PROPERTY FOUND  
   7 6#MG          SBN   0  2
   8 6*76          BNZ   0  R2   
   9 6*LQ          GEOERR   1,LIBPROP
   4 6B6B    R3  
   7 6BL2          SMO      0
  20 6C5L          LDX   5  RTAB-1(1)           [LOAD HARDWARE PROPERTY CODE/#100
   4 6CK=    R4  
  10 6D4W          STO   5  ACOMMUNE1(2) 
   5 6DJG          UP
   4 6F46    R5  
   7 6FHQ          LDX   0  4
   7 6FNY ...#UNS  JMODGCR   
   4 6FW6 ...#SKI
  11 6G3B          ANDN  0  GLIBPE+GLIBNRZI  
   7 6G8J ...#UNS  JMODGCR   
  13 6G*Q ...      ANDN  0  GLIBPE+GLIBNRZI+GLIBGCR  
  19 6GH2          BZE   0  R6                  [J UNLESS TRACK9 SUPERFLUOUS 
   7 6GN8 ...#UNS  JMODGCR   
   4 6GTB ...#SKI
  11 6H2L          ANDN  4  GLIBPE+GLIBNRZI  
   7 6H7S ...#UNS  JMODGCR   
  13 6H*2 ...      ANDN  4  GLIBPE+GLIBNRZI+GLIBGCR  
   8 6HG=          BRN      R1   
   4 6H_W    R6  
   8 6JFG          BNG   5  R1   
   8 6J_6          ANDN  5  #200 
   8 6KDQ          BZE   5  R1   
   9 6KYB          LDX   5  STAND(1) 
   8 6LD2          BRN      R4   
   4 6LD8 ...[   
  21 6LDB ...[ ********************************************************************  
  11 6LDJ ...[ CODE FOR THE OUTMTPROP MACRO  
  21 6LDQ ...[ ********************************************************************  
   4 6LDY ...[   
  12 6LF6 ...[ USES QTABLE - SEE MTEXPROP MACRO  
  16 6LF# ...[ ACOMMUNE1 CONTAINS SPD OF PROPERTY TO BE OUTPUT   
   4 6LFG ...[   
   5 6LFN ...QK4LIBP 
   7 6LFW ...#UNS  JMODGCR   
   4 6LG4 ...(   
  10 6LG= ...      LDX   6  ACOMMUNE1(2) 
  17 6LGD ...      ANDN  6  GLIBTRA7+GLIBTRA9+GLIBPE+GLIBNRZI+GLIBGCR
   8 6LGL ...      BZE   6  NPROP
  10 6LGS ...[ PROPERTY MUST BE IN TABLE 
   7 6LH2 ...      LDN   5  5
   9 6LH8 ...      LDN   7  QTABLE(1)
   7 6LHB ...XLOOP SMO      7
  17 6LHJ ...      LDX   0  4           [PICK UP 4TH WORD OF ENTRY   
   7 6LHQ ...      ANDX  0  6
  16 6LHY ...      BNZ   0  XFOND       [FOUND THE CORRECT ENTRY 
   7 6LJ6 ...      ADN   7  7
   8 6LJ# ...      BCT   5  XLOOP
   9 6LJG ...      GEOERR   1,MTPROP 
  16 6LJN ...[ PROPERTY FOUND IN TABLE - GET OUT CHARACTER STRING
   7 6LJW ...XFOND SMO      7
   7 6LK4 ...      LDN   5  0
  10 6LK= ...      LDN   6  ACOMMUNE1(2) 
   7 6LKD ...      MOVE  5  3
  13 6LKL ...      OUTPARAM ACOMMUNE1(2),ACOMMUNE2   
   5 6LKS ...      UP
   9 6LL2 ...[ NO PROPERTY TO OUTPUT 
   7 6LL8 ...NPROP OUTNULL   
   5 6LLB ...      UP
   4 6LLJ ...)   
   4 6LLQ ...[   
  21 6LLY ...[ **********************************************************************
  11 6LM6 ...[ CODE FOR THE MTSERSPD MACRO   
  21 6LM# ...[ **********************************************************************
   4 6LMG ...[   
   5 6LMN ...QK5LIBP 
   7 6LMW ...#UNS  JMODGCR   
   4 6LN4 ...(   
  10 6LN= ...      LDX   7  ACOMMUNE1(2) 
  10 6LND ...      HUNT     3,FLIB,FSPD  
   8 6LNL ...      BNG   3  NEXST
   8 6LNS ...      LDX   0  A1(3)
  17 6LP2 ...      ANDN  0  GLIBTRA7+GLIBTRA9+GLIBPE+GLIBNRZI+GLIBGCR
   8 6LP8 ...      BNZ   0  UP   
  17 6LPB ...[ CONVERT THE PROPERTY FROM THE ENTRY IN :SYSTEM.SERIAL 
   9 6LPJ ...[ HELD IN X7 INTO AN SPD
  17 6LPQ ...NEXST MBSXD    0,GSERBTRA7,GSERBPE,GSERBNRZI,GSERBGCR   
   7 6LPY ...      ANDX  0  7
   8 6LQ6 ...      BZE   0  UP   
   8 6LQ# ...      BPZ   3  XIST 
  16 6LQG ...[ NO FLIB/FSPD BLOCK EXISTS SO WE MUST CREATE ONE   
  10 6LQN ...      SETNCORE 1,3,FLIB,FSPD
   8 6LQW ...      STOZ     A1(3)
  10 6LR4 ...XIST  BSXD     0,GSERBTRA7  
   9 6LR= ...      LDN   6  GLIBTRA7 
   8 6LRD ...      CALL  5  XIST2
   9 6LRL ...      BSXD     0,GSERBPE
   9 6LRS ...      LDN   6  GLIBPE   
   8 6LS2 ...      CALL  5  XIST2
  10 6LS8 ...      BSXD     0,GSERBNRZI  
   9 6LSB ...      LDN   6  GLIBNRZI 
   8 6LSJ ...      CALL  5  XIST2
  10 6LSQ ...      BSXD     0,GSERBGCR   
   9 6LSY ...      LDN   6  GLIBGCR  
   8 6LT6 ...      CALL  5  XIST2
   9 6LT# ...      GEOERR   1,SERSPD 
   4 6LTG ...[   
   7 6LTN ...XIST2 ANDX  0  7
   8 6LTW ...      BZE   0  (5)  
   8 6LW4 ...      ORS   6  A1(3)
   5 6LW= ...UP    UP
   4 6LWD ...)   
   7 6LWL ...#UNS  JMODGCR   
   4 6LWS ...#SKI
  10 6LX2 ...      GEOERR   1,LIBPROPS   
  11 6LXL          MENDAREA 50,K99LIBPROPS   
   4 6MC=    #END
   8 ____ ...62473474000300000000
