  15 22FL ...#SEG  IDFENQS                      [LEN WAGSTAFF
  13 22PD ...# COPYRIGHT INTERNATIONAL COMPUTERS 1982
   8 22_=    #OPT  K0IDFENQS=0   
  16 23DW    #LIS  K0IDFENQS>K0ALLGEO>K0GREATGEO>K0COMMAND>K0IDF 
  10 23YG                   8HIDFENQS8   
   7 24D6    # ENTRY POINTS  
  12 24XQ          SEGENTRY K1IDFENQS,Z1IDFENQS  
  12 25CB          SEGENTRY K2IDFENQS,Z2IDFENQS  
  11 25X2          MENDAREA 50,K100IDFENQS   
   4 26BL    #   
   9 26W=    XNOMMES        +JNOMCONS
  14 27*W    OPENS          [OPEN IDF & FIND UNIT CELL   
   8 27TG          SBX   7  FX1  
  11 28*6          P        IDFANDPROP,XBRKN 
  16 28SQ ...      IDFOPEN  XBRKIN              [UP IF BREAK-IN  
   9 29#B          CALL  6  SEARCH   
  17 29S2          LDX   0  4                   [SWAP X4 & X5 ROUND  
   7 2=?L          LDX   4  5
   7 2=R=          LDX   5  0
  19 2?=W          LDN   3  BLINE(3)            [SEARCH LINE/IDENTIFIER CHAIN
   9 2?QG          CALL  6  SEARCH   
   8 2#=6          ADX   7  FX1  
   7 2#PQ          EXIT  7  0
   5 2#WY ...XBRKIN  
  10 2*46 ...      V        IDFANDPROP   
  14 2*9B    XBRKN UP                           [BREAK-IN
   4 2*P2    #   
   5 2B8L    ZCLOSE  
   8 2BN=          SBX   7  FX1  
   9 2C7W          MFREEALL FILE,FRRB
   6 2CMG          CLOSE 
  10 2D76          V        IDFANDPROP   
   8 2DLQ          ADX   7  FX1  
   7 2F6B          EXIT  7  0
   4 2FL2    #   
   5 2G5L    SEARCH  
   8 2GK=          SBX   6  FX1  
   8 2H4W          MSEARCH  2,3,4
   8 2HJG          SMO      FX2  
   9 2J46          LDX   0  ACOMMUNE1
   8 2JHQ          BNZ   0  S1   
   9 2K3B    QGEO  GEOERR   1,IDFENQS
   8 2KH2    S1    ADX   6  FX1  
   7 2L2L          EXIT  6  0
   4 2LG=    #   
  19 2L_W    YREC  BPZ   3  SAME                     [FIND CELL X3 POINTS TO 
   8 2MFG    YREC1 SBX   7  FX1  
   7 2M_6          LDX   0  3
   7 2NDQ          SRL   0  9
   8 2NYB          ANDN  0  #7777
   8 2PD2          LDX   2  FX2  
  10 2PXL          STO   0  ACOMMUNE3(2) 
   9 2QC=          MXB      2,NOFX12 
   8 2QWW          ADX   7  FX1  
   8 2RBG    SAME  ANDN  3  #777 
   7 2RW6          ADX   3  2
   7 2S*Q          EXIT  7  0
   4 2STB    #   
   4 2T*2    #   
  18 2TSL    Z1IDFENQS                          [ENTRY FROM JOBCONS MACRO
   6 2TWT ...#SKI  JSKI27
   4 2T_4 ...(   
  19 2W3? ...      VFREEW   APERI,ASSOCS0       [THERE SINCE CNSOURCE IN DC  
   8 2W5G ...      LDX   1  FX1  
   8 2W7P ...      LDX   2  FX2  
   4 2W9Y ...)   
   8 2W#=          BRN      TRACE
  17 2WRW    XCOPAS                             [COPY ASSOCS AND JSE3
  19 2X?G                                       [IF JBNO=0 JUST COPY ASSOCS  
  21 2XR6          HUNTW    3,APERI,ASSOCS      [IS THERE ALREADY AN ASSOCS BLOCK?   
  15 2Y=Q          BNG   3  Q8                  [GO COPY JSE3
   8 2YQB          BZE   4  ZEJN 
  18 2_=2          BXE   4  JOBNUM(2),YXY       [DO NOT BOTHER IF SAME   
  18 2_PL    ZEJN  SETUPCORE ALOGLEN(3),2,APERI,0     [ELSE COPY OLD ONE 
  17 329=          HUNTW    3,APERI,ASSOCS      [REFIND OLD ASSOCS   
   8 32NW          LDN   4  A1(3)
   8 338G          LDN   5  A1(2)
  10 33N6          SMO      ALOGLEN(2)   
  16 347Q          MOVE  4  0                   [COPY ACROSS DATA
  11 34MB          NAME     2,APERI,ASSOCS   
   8 3572          LDX   2  FX2  
   8 35LL          BRN      Q8   
  17 366=    TRACE              [UNPACK AND FIND RELEVANT DEVICE CELL
  21 36KW          STOZ     AWORK3(2)           [WILL BE SET LATER FOR LOCAL DEVICES 
  16 375G          SAWCE    CENTRAL,XUNPAC      [J IF NOT REMOTE 
   9 37K6          CALL  7  XNCONS   
  16 384Q          BRN      XNOTITA             [NO NOMINATION   
  16 38JB          BRN      XUNPAC              [NOT A CONSOLE   
  16 3942          STO   0  JSOURCE3(2)         [CONSOLE PROPERTY
   5 39HL    XUNPAC  
  12 3=3=          UNPACK   QLOC,MOP,Q7900,,5,4  
  15 3=GW          LDN   3  BUNIT               [7020 ON 7007
  19 3?2G          CALL  7  OPENS               [OPEN IDF & FIND UNIT & LINE 
   7 3?G6          SMO      2
  16 3?_Q          LDX   3  BLINE(3)            [POINTER TO 7020 
  17 3#FB          BNZ   3  Q1                  [J.IF 7020'S EXIST   
   4 3#_2    QERP
   7 3*DL          LDN   3  0
  17 3*Y=          BRN      Q21                 [J.IF IDFCONS MACRO  
   8 3BCW    Q1    CALL  7  YREC 
  20 3BXG          LDX   0  BNO(3)              [2 CHAR IDENTIFIER/POLLING ADD   
  18 3CC6          ERX   0  4                   [TO COMPARE IDENTIFIERS  
   9 3CWQ          ANDX  0  HALFTOP  
  15 3DBB          BZE   0  Q2                  [J.IF SAME   
   8 3DW2          LDX   3  0(3) 
  18 3F*L          BNZ   3  Q1                  [J.TO LOOK FOR NEXT 7020 
  17 3FT=          BRN      QERP                [J.AS 7020 NOT FOUND 
   7 3G#W    Q2    LDN   1  0
  17 3GSG          LDCH  5  5(1)                [REQUIRE INTERFACE   
  18 3H#6          BZE   5  QGEO                [INTERFACE ZERO INVALID  
   7 3HRQ    Q4    SMO      3
   8 3J?B          LDCH  0  BTYPE
  19 3JR2          TXU   0  5                   [IS THIS REQUIRED INTERFACE? 
  14 3K=L          BCC      QASS                [J.IF YES
   7 3KQ=          SMO      3
  19 3L9W          LDCH  0  BTYPE+1             [IS THIS REQUIRED INTERFACE? 
   7 3LPG          TXU   0  5
  14 3M96          BCC      QASS                [J IF YES
  10 3MNQ          LDX   0  BTYPE+2(3)   
   8 3N8B          BNG   0  QERP 
   8 3NN2          LDX   3  0(3) 
   8 3P7L          BZE   3  QERP 
  18 3PM=          CALL  7  YREC                [FIND NEXT INTERFACE CELL
   8 3Q6W          BRN      Q4   
   4 3QLG    QLOC
  17 3R66          LDCH  0  JSOURCE1(2)         [JSOURCE3 SET IF CI  
   8 3RKQ          SBN   0  60   
  15 3S5B          BNZ   0  XNCI                [J IF NOT CI 
   8 3SK2          BRN      YXUPA
   4 3T4L    XNCI
  18 3TJ=          STOZ     JSOURCE3(2)         [LOCAL DEVICE - NOT CI   
  16 3W3W          BRN      XLOCCL              [LOCAL CLUSTERS  
   5 3WHG    YXUPA   
   7 3X36          UPPLUS   1
  20 3XGQ    XLOCCL                             [LOCAL CLUSTERS-ASSOCIATIONS ONLY
   7 3Y2B          LDN   0  1
  18 3YG2          STO   0  AWORK3(2)           [INDICATE LOCAL DEVICE   
  10 3Y_L          LDCH  0  JSOURCE1(2)  
  15 3_F=          BZE   0  XCRTR               [J. IF TR-OK 
   7 3_YW          SBN   0  3
  19 42DG          BNZ   0  YXUPA               [J. IF NOT CR-NO ASSOCIATIONS
  20 42Y6                                       [(NOTE THAT NO NEED TO LOOK FOR  
  21 43CQ                                       [A CONSOLE PROP. TO PUT IN JSE3-YET) 
  20 43XB    XCRTR                              [NOW TO FIND THE ASSOCIATIONS FOR
  21 44C2          LDEX  4  JSOURCE1(2)         [THIS DEVICE IN ITS APIA-GEOG. NO.   
  19 44WL          BZE   4  YXUPA               [MUST BE INTERNALLY ISSUED   
  16 45B=          FINDPERE 3,APGEOG,4          [FIND DEVICE LIST
  15 45TW          BNG   3  QGEO                [MUST EXIST  
  15 46*G          LADDP    3,3                 [FIND APIA   
  15 46T6          BNG   3  QGEO                [MUST EXIST  
  18 47#Q          LDN   6  0                   [NUMBER OF ASSOCIATIONS  
   7 47SB          STOZ     5
  20 48#2          LDN   7  ASSMAX              [MAXIMUM NO. OF ASSOCS.PERMITTED 
   7 48RL    XAGAS SMO      6
  19 49?=          LDX   4  APERPROP(3)         [TYPE OF COM-TYPE OF DEVICE  
  15 49QW          BZE   4  XENDAS              [FREE PAIR   
   7 4==G          ADN   5  1
   5 4=Q6    XENDAS  
  19 4?9Q          BCT   7  YNEXAS              [HAVE A LOOK AT THE NEXT PAIR
  19 4?PB          BRN      XENDY               [FINISHED FOR THE TIME BEING 
   5 4#92    YNEXAS  
   8 4#NL          BDX   6  XAGAS
  16 4*8=    XENDY BZE   5  YXUPA               [NOTHING TO DO   
   7 4*MW          STO   5  7
  17 4B7G          BRN      XGBLOCK             [GO GET ASSOCS BLOCK 
  10 4BM6    MOP   LDCH  0  JSOURCE1(2)  
   8 4C6Q          SBN   0  #30  
   8 4CLB          BZE   0  Q7900
  17 4D62          LDN   3  BUNIT               [SEARCH 7007 CHAIN   
   8 4DKL          BRN      Q3   
  17 4F5=    Q7900 LDN   3  BIPB                [SEARCH 7900 CHAIN   
   8 4FJW    Q3    CALL  7  OPENS
   7 4G4G          ADX   3  2
  21 4GJ6    QASS                               [NOW TEST AW1 TO SEE WHICH MACRO ITIS
  15 4H3Q          LDX   7  3                   [SAVE POINTER
  17 4HHB          LDX   3  BASS(3)             [ASSOCIATES POINTER  
  17 4J32          BNZ   3  QASSY               [J IF ASSOCS EXIST   
  16 4JGL          LDX   3  7                   [RESTORE POINTER 
  19 4K2=          BRN      QONS                [NO ASSOCIATIONS-FORM JSE3   
   5 4KFW    QASSY   
   9 4K_G          LDX   0  BRECNO(2)
   7 4LF6          SBX   7  2
  20 4LYQ          SLL   0  12                  [SAVE IDF POINTER TO THE DEVICE  
  21 4MDB          ADX   0  7                   [CELL SO THAT IT CAN BE FOUND LATER  
   8 4MY2          SMO      FX2  
  21 4NCL          STO   0  AWORK1              [B0-11 REC NO - B12-23 DIST OF CELL  
  18 4NX=          LDX   6  3                   [KEEP ASSOCIATES POINTER 
  17 4PBW          CALL  7  YREC                [FIND ASSOCIATES CELL
  18 4PWG          LDX   7  BNO(3)              [FIND NO. OF ASSOCIATES  
  17 4QB6          BZE   7  QGEO                [ERROR NO ASSOCIATES 
   5 4QTQ    XGBLOCK 
   9 4R*B          LDN   0  ASSOCSLEN
   7 4RT2          MPY   0  7
  20 4S#L          LDN   7  1(1)                [X7 NOW LENGTH OF ASSOCS BLOCK   
  11 4SS=          SETUPCORE 7,1,APERI,ASSOCS
   9 4T?W          LDX   0  AWORK3(2)
  16 4TRG          BZE   0  XREMT               [REMOTE DEVICE   
  17 4W?6          STO   5  A1(1)               [NO. OF ASSOCIATIONS 
  21 4WQQ          LDN   4  A1+1(1)             [MOVE ASSOCIATION PAIRS INTO BLOCK   
  20 4X=B          LDN   3  APERPROP(3)         [LEAVING ROOM FOR THE PROP. NAME 
   4 4XQ2    XAS3
   8 4Y9L          LDX   0  0(3) 
  19 4YP=          BZE   0  XAS1                [GO TO NEXT PAIR-NOTHING HERE
   7 4_8W          MOVE  3  2
   9 4_NG          ADN   4  ASSOCSLEN
   8 5286          BCT   5  XAS1 
  14 52MQ          BRN      XAS2                [FINISHED
  17 537B    XAS1  BDX   3  XAS3                [GO TO COPY NEXT PAIR
   5 53M2    XREMT   
   7 546L          LDX   3  6
  19 54L=          CALL  7  YREC1               [FIND ASSOCIATES CELL AGAIN  
   9 555W          LDX   7  BNO(3)   
   8 55KG          STO   7  A1(1)
  14 5656          ADN   3  BTYPE               [FOR MOVE
  14 56JQ          LDN   4  A1+1(1)             [FOR MOVE
   8 574B    Q6    LDX   0  0(3) 
  20 57J2          BZE   0  Q7                  [J.IF THIS ASSOCIATION IS NULL   
   7 583L          MOVE  3  2
  15 58H=          ADN   4  ASSOCSLEN           [UPDATE X4   
  16 592W    Q7    BCT   7  Q5                  [J.FOR NEXT MOVE 
   4 59GG    XAS2
   7 59LC ...#SKI  JSKI18<1$1
   7 59Q# ...      PROPASSOCS
   6 59W9 ...#SKI  JSKI18
  17 5=26 ...      PROPASSOCS XBRK              [COMPLETE THE BLOCK  
  17 5=FQ          LDX   0  AWORK3(2)           [J IF LOCAL DEVICE   
   8 5=_B          BNZ   0  YXUPA
  21 5?F2          BRN      QONSA               [J TO SET JSE3 FROM THE IDF-REMOTE   
   4 5?YL    Q8  
   9 5#D=          LDX   0  AWORK2(2)
   8 5#XW          BZE   0  YXY  
  17 5*CG          JOBLOCK  AWORK2(2),3         [FIND NEW JOB BLOCK  
   8 5*X6          BPZ   3  Q1A  
  10 5BBQ          GEOERR   1,NOJOBLOK   
   7 5BWB    Q1A   FCAJO    3
  17 5CB2          HUNTW    1,APERI,ASSOCS      [REFIND ASSOCS BLOCK 
   8 5CTL          BNG   1  XJS  
  18 5D*=          CHAIN    1,3                 [CHAIN ASSFCS TO NEW CPA 
   4 5DSW    XJS 
  10 5F#G          LDX   0  JSOURCE3(2)  
  20 5FS6          STO   0  JSOURCE3(3)         [STORE CONSOLE PROP IN NEW CPAT  
  20 5G?Q    YXY   UP                           [ALL FINISHED -JOBSOURCE MACRO   
  15 5GRB    Q5    ADN   3  2                   [UPDATE X3   
   8 5H?2          BRN      Q6   
   9 5HB6 ...XBRK  LDX   0  AWORK3(2)
  17 5HF= ...      BNZ   0  XBRKN                  [J IF IDF NOT OPEN
   9 5HJB ...      CALL  7  ZCLOSE   
   8 5HMG ...      BRN      XBRKN
   4 5HQL    #   
   4 5J==    #   
  19 5JPW    Z2IDFENQS                          [ENTRY FROM JOBSOURCE MACRO  
  16 5K9G          LDX   4  ACOMMUNE1(2)        [JOBSOURCE MACRO 
  15 5KP6          STO   4  AWORK2(2)           [SAVE JOBNO  
  17 5L8Q          BRN      XCOPAS              [COPY ASSOCIATIONS   
   9 5LNB    QONSA LDX   6  AWORK1(2)
   7 5M82          STOZ     3
   7 5MML          DSA   6  3
  16 5N7=          SRL   6  12                  [RECORD NOMBER   
  20 5NLW    XRECO                              [FIND RECORD AND DEVICE CELL AS  
  21 5P6G          HUNT2    2,FILE,FRRB         [INDICATED BY AWORK1(ALREADYINCORE)  
  10 5PL6          LDX   0  BRECNO+A1(2) 
   7 5Q5Q          TXU   0  6
  15 5QKB          BCS      XRECO               [NOT THIS ONE
   8 5R52          ADN   2  A1   
  19 5RJL          ADX   3  2                   [X3 ABS PTR TO DEVICE CELL   
  18 5S4=    QONS                               [BACK HAVING FOUND DEVICE
   9 5SHW          LDX   3  BATT(3)  
  19 5T3G          BZE   3  Q21A                [NO CONSOLE PROP ATTRIBUTION 
   8 5TH6          CALL  7  YREC 
   8 5W2Q          LDX   2  FX2  
   9 5WGB          LDCH  0  ATYPE(2) 
   9 5X22          SBN   0  ACTLF/64 
  16 5XFL          BNZ   0  NOTACTLF            [BR IF NOT ACTLF 
   8 5X_=          SMO      FX2  
  18 5YDW          LDN   0  ACOMMUNE1           [MOVE ATTRIBUTIONS INTO  
  16 5YYG          LDN   7  BTYPE(3)            [ACOMMUNE WORDS  
  16 5_D6          MOVE  7  6                   [ACOMMUNE WORDS  
  19 5_XQ          LDX   3  BTYPE(3)            [LOAD CONSOLE PROPERTY NUMBER
  16 62CB          BZE   3  Q21A                [NOT CLUSTERED   
  17 62X2          SETNCORE 6,1,GOUT,GPROPNOS   [SET GPROPNOS BLOCK  
   9 63BL          ADN   1  GPROPNUM 
  18 63W=          LDN   0  ACOMMUNE1(2)        [AND MOVE IN ATTRIBUTIONS
   7 64*W          MOVE  0  6
  16 64TG          LFBITSET G,2                 [SET FLAG G .....
  15 65*6          LFBITSET F,2                 [ ... AND F  
   8 65SQ          POINTACT OUT,2
   8 66#B          LDX   1  FX2  
  10 66S2          LDN   2  JOBRING(2)   
  19 67?L          RERING   JOBRING(1),2            [AND RING IN TO JOBRING  
   5 67*3 ...SNORINGB
   9 67R=          BRN      XACTLF   
   5 68=W    NOTACTLF
  19 68QG          LDX   3  BTYPE(3)            [LOAD CONSOLE PROPERTY NUMBER
   5 69=6    XACTLF  
  20 69PQ          BZE   3  Q21A                [NO CONSOLE PROPERTY ATTRFBUTED  
   9 6=9B          ANDX  3  BITS22LS 
  21 6=P2          CALL  7  XNCONS              [CHECK THAT CONS PROP ATTRIBUTED IS  
  20 6?8L          BRN      XNOTIT              [ALSO A NOMINATION FOR US-IF NOT 
   8 6?N=          BRN      Q21  
  20 6#7W    XAGP  BXE   0  3,Q21               [PUT NO OF A NOMINATION IN JSE3  
  20 6#MG          STO   0  AWORK4(2)           [(PROVIDED THAT JSE1-2 DESCRIBE A
  16 6*76          CALL  7  XN3                 [REMOTE CONSOLES)
  20 6*LQ                                       [REENTER NOMCONS-STILL ACHANCE   
  20 6B6B          BRN      XNOT                [J IF PROP ATTR NOT A NOMINATION 
   6 6BL2          NULL  
  20 6C5L          BRN      XAGP                [CHECK IF THIS IS THE ATTRIBUTION
  19 6CK=    XNOT  LDX   3  AWORK4(2)           [COPY A NOMINATION IN JSE3   
   8 6D4W          BRN      Q21  
  20 6DJG    XNOTIT                             [NO NOMINATIONS FOR THIS CONSOLE 
  19 6F46          CALL  7  ZCLOSE              [BUT PART OF ACLUSTER -ERROR 
   5 6FHQ    XNOTITA 
  18 6G3B          OUTPER                       [PERIPHERAL DESCRIPTION  
  17 6GH2          LDN   3  1                   [TO CENTRAL CONSOLE  
  19 6H2L          INFORMX  3,XNOMMES(1),1      [ERROR ON UNIT %A,THIS IS NOT
  17 6HG=                                       [A NOMINATED CONSOLE 
  21 6H_W                                       [COMMANDS INPUT FROM REMOTE CONSOLES 
  19 6JFG          ACROSS   SUICIDE,1           [WHICH ARE NOT NOMINATED ARE 
   8 6J_6    Q21   SMO      FX2  
  15 6KDQ          STO   3  JSOURCE3            [SET OUR JSE3
   9 6KYB    XIT   CALL  7  ZCLOSE   
   7 6LD2          UPPLUS   1
   4 6LXL    Q21B
  15 6MC=          STO   3  JSOURCE3(2)         [SET MY JSE3 
  13 6MWW          BRN      YXUPA               [UP  
  21 6NBG    Q21A  CALL  7  ZCLOSE              [NO CONSOLE PROP ATTRIBUTRON-REMOTE  
  19 6NW6    # IF CONSOLE AND NOT PART OF A CLUSTER THEN CHECK IF NOMINATED  
  10 6P*Q    # (IE ENTRY IN HUIDGE BLOCK)
  10 6PTB          LDCH  0  JSOURCE1(2)  
   8 6Q*2          SBN   0  #77  
  18 6QSL          BZE   0  YXUPA               [REM CONSOLE-JSCE3 SET   
  14 6R#=          BRN      Q21B                [X3 ZERO 
   5 6RRW    XNCONS  
  10 6S?G          NOMCONS  XN1,XN2,XN3  
  16 6SR6          EXIT  7  2                   [SOME NOMINATION 
  16 6T=Q    XN1   EXIT  7  0                   [NO NOMINATION   
  16 6TQB    XN2   EXIT  7  1                   [NOT A CONSOLE   
   4 6W=2    #END
   8 ____ ...50066142000100000000
