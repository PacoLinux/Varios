  10 22FL    #OPT           K0SHOWCOMM=0 
  16 22_=    #LIS           K0SHOWCOMM > K0ALLGEO > K0GREATGEO   
  17 23DW    #SEG           SHOWCOMM            [COMM - STIG TOWNSEND
   4 23YG    [   
  10 24D6                   8HSHOWCOMM   
   4 24XQ    [   
   4 25CB    [   
   4 25X2    [   
  16 26BL    [ IMPLEMENTS THE SHOW COMMAND AND PROVIDES ROUTINES 
  14 26W=    [ TO SIMULATE THE COMMAND FOR THE OPER CODE 
   4 27*W    [   
   4 27TG    [   
   4 28*6    [   
   4 28SQ    [   
   9 29#B    [ X2 = FX2 THROUGHOUT   
   4 29S2    [   
   4 2=?L    [   
  18 2=R=    [ AWORK1 AND AWORK2 ARE USED TO STORE THE SCREEN AND PAGE   
  16 2?=W    [ NUMBERS AS THEY ARE ANALYSED. THEY ARE REFERRED TO
  11 2?QG    [ LOCALLY AS 'SCREEN' AND 'PAGE.
   4 2#=6    [   
  10 2#PQ    #DEF           SCREEN=AWORK1
  10 2*9B    #DEF           PAGE=AWORK2  
   4 2*P2    [   
   4 2B8L    [   
   4 2BN=    [   
  15 2C7W       SEGENTRY K1SHOWCOMM,SHOWCOMMK1 [SHOW COMMAND 
  17 2CMG       SEGENTRY K2SHOWCOMM,SHOWCOMMK2 [SIMULATE A 'SH -1,1' 
  17 2D76       SEGENTRY K3SHOWCOMM,SHOWCOMMK3 [SIMULATE A 'SH OFF,0'
  17 2DLQ       SEGENTRY K4SHOWCOMM,SHOWCOMMK4 [SIMULATE A 'SH +1,1' 
  16 2F6B       SEGENTRY K5SHOWCOMM,SHOWCOMMK5 [FORCEPAGE COMMAND
   4 2FL2    [   
   4 2G5L    [   
   4 2GK=    [   
   7 2H4W    [ PRESET DATA   
   7 2HJG    [ ===========   
   4 2J46    [   
   7 2JHQ    #DEF  PARAM = 1 
   5 2K3B    TABLE   
   8 2KH2       BRN      RAUTO   
   6 2L2L       4HAU00   
   4 2LG=    [   
   8 2L_W       BRN      RLATEST 
   6 2MFG       4HLA00   
   4 2M_6    [   
   7 2NDQ       BRN      ROFF
   6 2NYB       4HOF00   
   4 2PD2    [   
   8 2PXL       BRN      ROLDEST 
   6 2QC=       4HOL00   
   4 2QWW    TEND
   5 2RBG    RAUTO   
   9 2RW6       LDX   0  SCREEN(2)   
   9 2S*Q       BZE   0  ZMAINAUTO   
   7 2STB       NGN   7  1   
   8 2T*2       BRN      RETURN  
   5 2TSL    RLATEST 
   7 2W#=       LDN   7  1   
   9 2WRW       CALL  4  SINBOUNDS   
   8 2X?G       BRN      RETURN  
   4 2XR6    ROFF
   7 2Y=Q       LDN   7  0   
   7 2YQB       BRN   RETURN 
   5 2_=2    ROLDEST 
   9 2_PL       SMO      CONSCNTRL   
   8 329=       LDX   7  CORET   
   8 32NW       BRN      RETURN  
   4 338G    [   
   4 33N6    [   
   4 347Q    [   
   7 34MB    [ SUBROUTINES   
   7 3572    [ ===========   
   4 35LL    [   
   4 366=    [   
  21 36KW    [*********************************************************************  
   4 375G    [   
   5 37K6    SGETPAR 
   4 384Q    [   
  21 38JB    [*********************************************************************  
   4 3942    [   
  11 39HL    [ GETS A PARAMETER IN A CPB/CUNI
   4 3=3=    [   
   6 3=GW    [ LINK X4   
   4 3?2G    [   
  14 3?G6    [ ON ENTRY, X3 CONTAINS PARAM NUMBER WANTED 
   4 3?_Q    [   
  13 3#FB    [ ON EXIT,  X3 -> CPB/CUNI CONTAINING IT
   4 3#_2    [   
   7 3*DL       SBX   4  FX1 
   7 3*Y=       PARANOTX 3   
   9 3BCW       MHUNT    3,CPB,CUNI  
   7 3BXG       ADX   4  FX1 
   7 3CC6       EXIT  4  0   
   4 3CWQ    [   
   4 3DBB    [   
   4 3DW2    [   
  21 3F*L    [*********************************************************************  
   4 3FT=    [   
   5 3G#W    SNUMCON 
   4 3GSG    [   
  21 3H#6    [*********************************************************************  
   4 3HRQ    [   
  17 3J?B    [ CONVERTS A NUMBER HELD IN CHARACTER FORMAT TO BINARY  
   4 3JR2    [   
   6 3K=L    [ LINK X4   
   4 3KQ=    [   
  14 3L9W    [ ON ENTRY, X3 -> CPB/CUNI HOLDING NUMBER   
   4 3LPG    [   
  14 3M96    [ ON EXIT,  ACOMMUNE1 HOLDS BINARY NUMBER   
   4 3MNQ    [   
  18 3N8B    [ IF A FORMAT ERROR IS FOUND, AN ERROR MESSAGE IS GENERATED 
  12 3NN2    [ AND A JUMP IS MADE TO 'ZENDCOMM'  
   4 3P7L    [   
   7 3PM=       SBX   4  FX1 
   7 3Q6W       CHNUMCOD ,3  
   9 3QLG       TESTRPN2 OK,ZENDCOMM 
   7 3R66       ADX   4  FX1 
   7 3RKQ       EXIT  4  0   
   4 3S5B    [   
   4 3SK2    [   
   4 3T4L    [   
  21 3TJ=    [*********************************************************************  
   4 3W3W    [   
   5 3WHG    SMAKEABS
   4 3X36    [   
  21 3XGQ    [*********************************************************************  
   4 3Y2B    [   
  15 3YG2    [ TURNS A RELATIVE PAGE NUMBER INTO AN ABSOLUTE 
   4 3Y_L    [   
   6 3_F=    [ LINK X4   
   4 3_YW    [   
  13 42DG    [ ON ENTRY, X7 = RELATIVE PAGE NUMBER   
  14 42Y6    [           SCREEN = RELEVANT SCREEN NUMBER 
   4 43CQ    [   
  13 43XB    [ ON EXIT,  X7 = ABSOLUTE PAGE NUMBER   
   4 44C2    [   
   9 44WL       LDX   3  CONSCNTRL   
   9 45B=       ADX   3  SCREEN(2)   
   8 45TW       ADN   3  COVID   
  16 46*G       LF       3,COCURRENT,0       [X0 = DISPLAYED PAGE
   7 46T6       NGX   7  7   
  18 47#Q       ADX   7  0                   [X7 = DISPLAYED - SPECIFIED 
   7 47SB       EXIT  4  0   
   4 48#2    [   
   4 48RL    [   
   4 49?=    [   
  21 49QW    [*********************************************************************  
   4 4==G    [   
   6 4=Q6    SINBOUNDS   
   4 4?9Q    [   
  21 4?PB    [*********************************************************************  
   4 4#92    [   
  17 4#NL    [ ENSURES THAT AN ABSOLUTE PAGE NUMBER IS WITHIN BOUNDS 
   4 4*8=    [   
   6 4*MW    [ LINK X4   
   4 4B7G    [   
  13 4BM6    [ ON ENTRY, X7 = ABSOLUTE PAGE NUMBER   
   4 4C6Q    [   
  15 4CLB    [ ON EXIT,  0  < X7 <= OLDEST PAGE RECALLABLE   
  19 4D62    [           OR X7 = -1 (AUTO FOR MONITOR, OFF FOR INTERACTIVE)  
   4 4DKL    [   
   9 4F5=       LDX   3  CONSCNTRL   
  16 4FJW       IFR      7,NG                [IF BEFORE ACTIVE   
  17 4G4G       OR       7,ZE                [OR ACTIVE ITSELF THEN  
   5 4GJ6       THEN 
  16 4H3Q          NGN   7  1                   [TREAT AS AUTO   
  19 4HHB       ELSF     7,GE,CORET(3)       [ELSF TOO OLD THEN GET OLDEST   
   5 4J32       THEN 
   9 4JGL          LDX   7  CORET(3) 
  12 4K2=       FI                           [FI 
   7 4KFW       EXIT  4  0   
   4 4K_G    [   
   4 4LF6    [   
   4 4LYQ    [   
  21 4MDB    [*********************************************************************  
   4 4MY2    [   
   6 4NCL    SCHAINSHOW  
   4 4NX=    [   
  21 4PBW    [*********************************************************************  
   4 4PWG    [   
  19 4QB6    [ SETS UP A SHOW BLOCK, INSERTS THE PARAMETERS, CHAINS THE BLOCK
   4 4QTQ    [   
   6 4R*B    [ LINK X4   
   4 4RT2    [   
  13 4S#L    [ ON ENTRY, SCREEN HOLDS SCREEN NUMBER  
  14 4SS=    [           PAGE HOLDS ABSOLUTE PAGE NUMBER 
   4 4T?W    [   
  14 4TRG    [ ON EXIT,  SHOW BLOCK IS CHAINED FOR ACTION
   4 4W?6    [   
   7 4WQQ       SBX   4  FX1 
   9 4X=B       LDX   3  CONSCNTRL   
   8 4XQ2       LDX   0  PAGE(2) 
   9 4Y9L       ORX   0  SCREEN(2)   
  15 4YP=       IFR      0,ZE                [IF 'SH @0,0'   
  18 4_8W       OR       BC,3,CONSNOFILE     [OR WE HAVE RECALL FILE THEN
   5 4_NG       THEN 
  11 5286          SETNCORE 2,3,CONBUFF,CSHOW
   9 52MQ          LDX   0  SCREEN(2)
   9 537B          STO   0  CSVID(3) 
   9 53M2          LDX   0  PAGE(2)  
   9 546L          STO   0  CSPAG(3) 
   9 54L=          LDX   1  CONSCNTRL
   9 555W          ADX   1  SCREEN(2)
   8 55KG          ADN   1  COVID
  10 5656          LF       1,COSHOWS,0  
   7 56JQ          ADN   0  1
   8 574B          LDN   1  64   
  19 57J2          IF       0,L,1               [IF FIELD WON'T OVERFLOW THEN
   6 583L          THEN  
  19 58H=             CONSOUT  3                   [CHAIN BLOCK, WAKE CONSA  
  10 592W             LDX   3  CONSCNTRL 
  10 59GG             ADX   3  SCREEN(2) 
   9 5=26             ADN   3  COVID 
  21 5=FQ             INCF     3,COSHOWS           [INCREASE RELEVANT 'SHOWS WANTED' 
  18 5=_B          ELSE                         [ELSE (FIELD OVERFLOW)   
  15 5?F2             FREECORE 3                   [FORGET IT
  16 5?YL          FI                           [FI FULL FIELD   
  16 5#D=       FI                           [FI HAVE RECALL FILE
   7 5#XW       ADX   4  FX1 
   7 5*CG       EXIT  4  0   
   4 5*X6    [   
   4 5BBQ    [   
  11 5BWB    [                    WW   WW   W
  11 5CB2    [                    WW  WW   WW
  11 5CTL    [                    WW WW   WWW
  11 5D*=    [                    WWWW     WW
  11 5DSW    [                    WWWW     WW
  11 5F#G    [                    WW WW    WW
  11 5FS6    [                    WW  WW   WW
  11 5G?Q    [                    WW   WW  WW
   4 5GRB    [   
  15 5H?2    SHOWCOMMK1                         [SHOW COMMAND
   4 5HQL    [   
   4 5J==    [   
  16 5JPW    [ CHECK WE ARE IN DME AND HAVE A RECALL FILE SET UP 
   4 5K9G    [   
  10 5KP6       JENVNOT  ZWRONGENV,DME   
   9 5L8Q       LDX   3  CONSCNTRL   
  11 5LNB       JBS      ZNOFILE,3,CONSNOFILE
   4 5M82    [   
   4 5MML    [   
   8 5N7=    [ SCREEN PARAMETER  
   4 5NLW    [   
   7 5P6G       LDN   3  2   
   8 5PL6       CALL  4  SGETPAR 
   8 5Q5Q       LDX   0  ANUM(3) 
  17 5QKB       IF       EITHER,0,NG         [IF OMITTED OR NULL THEN
   7 5R52       OR       0,ZE
   5 5RJL       THEN 
  19 5S4=          LDN   7  1                   [SUPPLY DEFAULT SCREEN (1)   
  17 5SHW       ELSE                         [ELSE (SCREEN SPECIFIED)
  17 5T3G          CALL  4  SNUMCON             [CONVERT TO BINARY   
  10 5TH6          LDN   0  CLUSTOPER+1  
  10 5W2Q          LDX   7  ACOMMUNE1(2) 
  17 5WGB          BXGE  7  0,ZTOOHIGH          [ERROR IF TOO HIGH   
  12 5X22       FI                           [FI 
   8 5XFL       MFREE    CPB,CUNI
  15 5X_=       STO   7  SCREEN(2)           [REMEMBER SCREEN
   4 5YDW    [   
   4 5YYG    [   
   7 5_D6    [ PAGE PARAMETER
   4 5_XQ    [   
   7 62CB       LDN   3  1   
   8 62X2       CALL  4  SGETPAR 
   8 63BL       LDX   5  ANUM(3) 
  16 63W=       IF       5,ZE                [IF PARAM NULL THEN 
   5 64*W       THEN 
  16 64TG          NGN   5  1                   [TREAT AS OMITTED
  15 65*6       ELSF     5,PZ                [ELSF GIVEN THEN
   5 65SQ       THEN 
  20 66#B          ANDN  5  #7777               [ENSURE ONLY HAVE CHARACTER COUNT
  12 66S2       FI                           [FI 
  16 67?L       IF       5,PZ                [IF ALPHA PARAM THEN
   9 67R=          LDCH  0  APARA(3) 
   8 68=W          SBN   0  #41  
   7 68QG       AND      0,PZ
   9 69=6          SBN   0  #73-#41  
   7 69PQ       AND      0,NG
   5 6=9B       THEN 
  18 6=P2          LDX   0  APARA(3)            [CHECK PARAMETER ALLOWED 
   9 6?8L          ANDX  0  HALFTOP  
   9 6?N=          LDN   1  TABLE(1) 
  11 6#7W          LDN   6  TEND-TABLE / 2   
   5 6#MG          DO
  19 6*76             BXE   0  PARAM(1),(1)        [OBEY APPROP ROUTINE IF SO
   8 6*LQ             ADN   1  2 
   8 6B6B          REPEAT   CT 6 
  18 6BL2          BRN      ZUNRECOG            [ERROR IF ALPHA UNKNOWN  
   9 6C5L          [**************** 
   5 6CK=          [ 
  16 6D4W       ELSE                         [ELSE (NUMBER GIVEN)
  17 6DJG          IF       5,PZ                [IF PARAMETER GIVEN  
  10 6F46             LDCH  0  APARA(3)  
   9 6FHQ             SBN   0  #40   
  20 6G3B          AND      0,ZE                [AND STARTS ^@^ THEN (ABSOLUTE)  
   6 6GH2          THEN  
   8 6H2L             LDX   4  5 
   8 6HG=             SBN   4  1 
  20 6H_W             BZE   4  ZNULL               [ERROR IF NO NUMBER FOLLOWS   
  10 6JFG             LDN   0  APARA(3)  
   8 6J_6             BCHX  0  / 
  10 6KDQ             LDN   1  APARA(3)  
  18 6KYB             DSA   4  ANUM(3)             [REDUCE COUNT IN CUNI 
   8 6LD2             SMO      4 
  19 6LXL             MVCH  0  0                   [OVERWRITE ^@^ WITH NUMBER
   9 6MC=             LDN   0  #20   
  19 6MWW             DCH   0  0(1)                [AND SPACEFILL FINAL CHAR 
  10 6NBG             CALL  4  SNUMCON   
  19 6NW6             LDX   7  ACOMMUNE1(2)        [GET ABSOLUTE PAGE NUMBER 
  18 6P*Q          ELSE                         [ELSE (RELATIVE NUMBER)  
  16 6PTB             IF       5,NG                [IF NULL THEN 
   7 6Q*2             THEN   
  20 6QSL                LDN   7  1                   [SUPPLY DEFAULT PAGE (+1)  
  17 6R#=             ELSE                         [ELSE (SPECIFIED) 
  11 6RRW                MHUNT    3,CPB,CUNI 
  10 6S?G                CALL  4  SNUMCON
  19 6SR6                LDX   7  ACOMMUNE1(2)        [GET SPECIFIED NUMBER  
  16 6T=Q             FI                           [FI NULL/NOT  
  19 6TQB             CALL  4  SMAKEABS            [MAKE RELATIVE ABSOLUTE   
  15 6W=2          FI                           [FI ABSOLUTE 
  19 6WPL          CALL  4  SINBOUNDS           [ENSURE PAGE NUMBER IN BOUNDS
  14 6X9=       FI                           [FI ^OLDEST^
   5 6XNW    RETURN  
   8 6Y8G       MFREE    CPB,CUNI
   8 6YN6       STO   7  PAGE(2) 
  19 6_7Q       CALL  4  SCHAINSHOW          [CHAIN THE COMMAND FOR ACTIONING
   5 6_MB    ZENDCOMM
   6 7272       ENDCOM   
   6 72LL    ZWRONGENV   
  11 736=       COMERR   JENVMUSTBE,JENVDME  
   5 73KW    ZUNRECOG
   8 745G       COMERR   APFERR  
   6 74K6    ZMAINAUTO   
   9 754Q       COMERR   JMAINAUTO   
   5 75JB    ZTOOHIGH
   8 7642       COMERR   JHIGHVID
   5 76HL    ZNULL   
   8 773=       COMERR   JNULLPAR
   5 77GW    ZNOFILE 
   9 782G       COMERR   CNORECALL1  
   4 78G6    [   
   4 78_Q    [   
  12 79FB    [                    WW   WW   WWWWW
  13 79_2    [                    WW  WW   WW   WW   
  13 7=DL    [                    WW WW         WW   
  13 7=Y=    [                    WWWW          WW   
  12 7?CW    [                    WWWW        WW 
  12 7?XG    [                    WW WW     WW   
  11 7#C6    [                    WW  WW   WW
  13 7#WQ    [                    WW   WW  WWWWWWW   
   4 7*BB    [   
   4 7*W2    [   
   6 7B*L    SHOWCOMMK2  
   4 7BT=    [   
   4 7C#W    [   
  10 7CSG    [ SHOW -1,1 (PAGE REVERSE)  
   4 7D#6    [   
   7 7DRQ       LDN   0  1   
   9 7F?B       STO   0  SCREEN(2)   
   7 7FR2       NGN   7  1   
   8 7G=L       CALL  4  SMAKEABS
   9 7GQ=       CALL  4  SINBOUNDS   
   8 7H9W       STO   7  PAGE(2) 
   9 7HPG       CALL  4  SCHAINSHOW  
   5 7J96       UP   
   4 7JNQ    [   
   4 7K8B    [   
  12 7KN2    [                    WW   WW   WWWWW
  13 7L7L    [                    WW  WW   WW   WW   
  13 7LM=    [                    WW WW         WW   
  12 7M6W    [                    WWWW       WWWW
  13 7MLG    [                    WWWW          WW   
  13 7N66    [                    WW WW         WW   
  13 7NKQ    [                    WW  WW   WW   WW   
  12 7P5B    [                    WW   WW   WWWWW
   4 7PK2    [   
   4 7Q4L    [   
   6 7QJ=    SHOWCOMMK3  
   4 7R3W    [   
   4 7RHG    [   
  10 7S36    [ SHOW OFF,0 (NULL INPUT)   
   4 7SGQ    [   
   7 7T2B       LDN   0  0   
   8 7TG2       STO   0  PAGE(2) 
   9 7T_L       STO   0  SCREEN(2)   
   9 7WF=       CALL  4  SCHAINSHOW  
   5 7WYW       UP   
   4 7XDG    [   
   4 7XY6    [   
  12 7YCQ    [                    WW   WW     WW 
  12 7YXB    [                    WW  WW     WW  
  12 7_C2    [                    WW WW     WW   
  12 7_WL    [                    WWWW     WW  WW
  13 82B=    [                    WWWW    WWWWWWWWW  
  12 82TW    [                    WW WW        WW
  12 83*G    [                    WW  WW       WW
  12 83T6    [                    WW   WW      WW
   4 84#Q    [   
   4 84SB    [   
   6 85#2    SHOWCOMMK4  
   4 85RL    [   
   4 86?=    [   
  10 86QW    [ SHOW +1,1 (PAGE FORWARD)  
   4 87=G    [   
   7 87Q6       LDN   0  1   
   9 889Q       STO   0  SCREEN(2)   
   7 88PB       LDN   7  1   
   8 8992       CALL  4  SMAKEABS
   9 89NL       CALL  4  SINBOUNDS   
   8 8=8=       STO   7  PAGE(2) 
   9 8=MW       CALL  4  SCHAINSHOW  
   5 8?7G       UP   
   4 8?M6    [   
   4 8#6Q    [   
  13 8#LB    [                    WW   WW   WWWWWWWW 
  12 8*62    [                    WW  WW    WW   
  13 8*KL    [                    WW WW     WWWWWW   
  13 8B5=    [                    WWWW           WW  
  13 8BJW    [                    WWWWW           WW 
  13 8C4G    [                    WW  WW    WW    WW 
  13 8CJ6    [                    WW   WW   WW   WW  
  13 8D3Q    [                    WW    WW   WWWWW   
   4 8DHB    [   
   4 8F32    [   
   6 8FGL    SHOWCOMMK5  
   4 8G2=    [   
   4 8GFW    [   
   6 8G_G    [  FORCEPAGE
   4 8HF6    [   
  10 8HYQ       JENVNOT  ZWRONGENV,DME   
  11 8JDB       SETUPCOR  0,3,CONBUFF,FORCE  
   7 8JY2       CONSOUT  3   
   6 8KCL       ENDCOM   
   4 8KX=    #END
   6 ____ ...610464330001
