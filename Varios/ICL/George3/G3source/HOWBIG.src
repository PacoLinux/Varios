  13 22FL          SEG    HOWBIG,867,SECT.FILE,,G263 
   7 22_=    #LIS K0HOWBIG   
  13 23DW    #  FOR UGUG MEND M263 (HOWBIG COMMAND)  
   4 23YG    #   
  21 24D6    #     HOWBIG COMMAND - GIVES SIZE OF A FILE OR A DIRECTORY-FULL OF THEM.
  21 24XQ    #     DESIGNED AND ORIGINALLY PRODUCED BY ASTON; REWRITTEN BY UIJPC TO  
  17 25CB    #     IMPROVE HANDLING OF WORKFILES, MULTIFILES, ETC.   
   4 25X2    #   
  10 26BL    #     FORMAT 1  -  HB <FILE>
  20 26W=    #     IF %A IS A TERMINAL FILE OR WORKFILE, ITS SIZE IS GIVEN IN THE
  11 27*W    #     FORMAT  $6ZDX^BLOCKS^$ .  
   4 27TG    #   
  14 28*6    #     FORMAT 2  -  HB <ENTRANT>, <OPTION>   
  20 28SQ    #     IF %A IS A DIRECTORY, GIVES TOTAL SIZE OF THE FOLLOWING FILES:
   8 29#B    #        <OPTION>   
  19 29S2    #     NULL OR OMITTED   ALL FILES IN THIS DIRECTORY AND INFERIOR
  14 2=?L    #                           PSEUDO-USERS.   
  18 2=R=    #         ^ONLY^        ALL FILES IN THIS DIRECTORY ONLY.   
  18 2?=W    #         ^ALL^         ALL FILES IN THIS AND ALL INFERIORS.
  18 2?QG    #         ^FILE^        SIZE OF THE DIRECTORY FILE ITSELF.  
  21 2#=6    #     IF %A IS NULL OR OMITTED, FORMAT 2 IS ASSUMED WITH %A = :%;CUR;.  
   4 2#PQ    #   
  20 2*9B    #     %A MUST NOT HAVE QUALIFIERS.  FORMAT 2 IS ONLY ALLOWED IN USER
  20 2*P2    #     CONTEXT AND THE USER MUST HAVE A READ TRAP TO %A (BUT TRAPS ON
  21 2B8L    #     INFERIOR DIRECTORIES ARE NOT CHECKED).  IF %A IS A MULTIFILE, THE 
  21 2BN=    #     TOTAL SIZE WILL BE GIVEN; IF A MULTIFILE ELEMENT, ONLY THE SIZE OF
   8 2C7W    #     THAT ELEMENT. 
   4 2CMG    #   
  21 2D76    #     IN FORMAT 2, FILES WHICH ARE (E) OR (T) ARE OMITTED; FILES WHICH  
  21 2DLQ    #     ARE OPEN FOR WRITING COUNT AS WHATEVER SIZE IS IN THE DIRECTORY   
  20 2F6B    #     ENTRY (ALWAYS (?) ZERO).  IN FORMAT 1, IF THE FILE IS OPEN FOR
  21 2FL2    #     WRITING, A SNAPSHOT OF ITS CURRENT SIZE IS OBTAINED FROM THE FCB. 
   4 2G5L    #   
  19 2GK=    #     IF CALLED VIA ENTRY POINT K2HOWBIG, RETURN IS TO CALLING  
  21 2H4W    #     CHAPTER BY  UPPLUS 2  WITH RESULT IN ACOMMUNE1.  FAILURE RETURNS :
  15 2HJG    #     UP (COMMAND ERROR)   UPPLUS 1 (BREAKIN).  
   4 2J46    #   
  11 2JHQ          SEGENTRY K1HOWBIG,START   
  11 2K3B          SEGENTRY K2HOWBIG,SPENT   
   4 2KH2    #   
   9 2L2L    #              ERRORS   
  16 2LG=    MZERR LDN   3  GMINUS1             [ PRETEND ZERRORY
   9 2L_W    MZER2 ERRORX   0(1),0(3)
  11 2MFG          JBCC     NDING,2,G263SP   
   7 2M_6          UPPLUS   1
  10 2NDQ    XUP   STO   6  ACOMMUNE1(2) 
  10 2NYB          MFREE    CPB,CMULTI   
  14 2PD2          UPPLUS   2                   [ O.K.   
  14 2PXL    XBKUP UPPLUS   1                   [ BREAKIN
   4 2QC=    #   
   8 2QWW    UNMIS CALL  3  MISS 
   9 2RBG                   +JUSNA   
   8 2RW6    MISS  CALL  1  MZER2
   9 2S*Q                   +AMISS   
   8 2STB    NUDIR CALL  3  MISS 
   9 2T*2                   +JLOCNA  
   9 2TSL    PFERR ZERRORX  APFERR   
   8 2W#=    QUILL CALL  3  ZGEN 
   9 2WRW                   +ALQUAL  
   8 2X?G    ZGEN  CALL  1  MZER2
   9 2XR6                   +GENERR  
   8 2Y=Q    TN    ZERRORX  FNAPP
  18 2YQB    CLVWR CLOSETOP                     [ OR 'GEOERR HBMAGEXO'   
  10 2_=2    VRYWR ZERRORX  ERVRYWRONG   
   7 2_PL    NTEMP CLOSETOP  
  18 329=          ZERRORX  ERTEMPDIR           [ TEMP FILES NOT ALLOWED 
   7 32NW    YUKK  CLOSETOP  
  17 338G          ZERRORX  HBYUK               [ TOO BIG/COMPLICATED
   4 33N6    #   
  19 347Q    WFNX  GEOERR   1,WKFNOFCB          [ WKFILE EXISTS BUT NO FCB ??
  19 34MB    XGDIR GEOERR   1,MFGETDIR          [ IMPOSS DIR ENTRIES FOR MULT
  19 3572    XFCB  GEOERR   1,UWBNOFCB          [ OPEN UNCLEANLY, BUT NO FCB 
   4 35LL    #   
  11 366=    XBRK  JBSC     XBKUP,FX2,G263SP 
  15 36KW          ABANDCOM                     [ NO MESSING 
   4 375G    #   
  20 37K6    SONLY          4HONLY              [ POSSIBLE 2ND PARAMETER STRINGS 
   8 384Q    SALL           4HALL
   9 38JB    SFILE          4HFILE   
   9 3942    PSTEP          +APARA.1 
   4 39HL    #   
   9 3=3=    SPENT BS       2,G263SP 
   7 3=GW          LDN   1  1
   9 3?2G          PARALYSE #34,,1   
   9 3?G6          TESTRPN2 OK,PFERR 
   4 3?_Q    #   
  13 3#FB    START SPARAPASS                    [ %A 
  10 3#_2          MHUNT    1,CPB,CUNI   
   9 3*DL          NGX   0  ANUM(1)  
  19 3*Y=          BPZ   0  USCUR               [ DEFAULT %A = CURRENT USER  
  10 3BCW          NAMETOP  1,FILE,FNAME 
  10 3BXG          BCONTEXT USER,2,USER  
   9 3CC6          LDCH  0  APARA(1) 
  20 3CWQ          ERN   0  #12                 [ NO USER - MUST BE ABSOLUTE NAME
   8 3DBB          BNZ   0  UNMIS
   7 3DW2    USER  FNORM    3
  10 3F*L          MHUNT    3,FILE,FNAME 
  15 3FT=          NAMETOP  3,CPB,CUNI          [ FOR ERRORS 
  18 3G#W          TESTREP2 NAMEFORM,XERR       [ ERROR REPORTED BY FNORM
  10 3GSG          HUNT     3,FILE,FABSNB
   8 3H#6          BNG   3  PFERR
  18 3HRQ          JBS      WKF,3,BFABWORK      [ WORKFILE - SPECIAL CASE
  11 3J?B          TESTREP2 ADJUNCTS,QUILL   
   8 3JR2          TESTRPN2 OK,TN
  17 3K=L          JMBS     VRYWR,3,BFABTSN,BFABREEL [ MAG TAPE ETC. 
   4 3KQ=    #   
  21 3L9W    NOTWF SMO      A1(3)               [ IF LOCAL NAME WAS GIVEN, GET FGN   
  18 3LPG          LDX   4  A1-2(3)             [  IF SUPPLIED BY USER   
   8 3M96          NGN   0  #100 
  17 3MNQ          ANDS  0  ATYPE(3)            [ REQUEST 'FILE ONLY'
  11 3N8B          OPENDIR  XBRK,READ,QUERY  
  19 3NN2          TESTREP2 OK,SUPOK            [ SUPERIOR DIRECTORY OPENED  
   7 3P7L          REPALLER  
  11 3PM=    XERR  JBC      NDING,2,G263SP   
  14 3Q6W          UPPLUS   1                   [ ERROR  
  15 3QLG    WKF   FINDWFN  ,3                  [ FIND FCB - 
  20 3R66          TESTRPN2 OK,WFNX             [  SHOULD ALWAYS EXIST FOR W/F   
  20 3RKQ          LDX   6  FBLMOD(3)           [  AND ALWAYS HOLDS CORRECT SIZE 
  17 3S5B          SBN   6  FBLKS-A1            [  EVEN IF FILE OPEN 
   8 3SK2          BRN      XFIN 
   4 3T4L    #   
  18 3TJ=    SZFCB FCBFIND  3,XFCB              [ FILE IS BEING WRITTEN, 
  19 3W3W          LDX   6  FBLMOD(3)           [  SO FCB SHOULD EXIST AND   
  18 3WHG          SBN   6  FBLKS-A1            [  CONTAIN CURRENT SIZE  
   7 3X36          EXIT  5  0
   4 3XGQ    #   
  20 3Y2B    QSIZ  JBC      SIZ,1,BEUWB         [ DIR ENT OK IF NOT BEING WRITTEN
  20 3YG2          CALL  5  SZFCB               [ ELSE GET CURRENT SIZE FROM FCB 
   8 3Y_L          BRN      PCLOS
  18 3_F=    OK    MHUNTW   1,FILE,ENT          [ GET SIZE FROM FILE/ENT 
   9 3_YW    SIZ   LDX   6  ECOPS(1) 
  15 42DG          FREECORE 1                   [ FILE/ENT   
   8 42Y6          SRL   6  15   
  17 43CQ    PCLOS CLOSETOP                     [ USUALLY A DIRECTORY
   4 43XB    #   
   8 44C2    XFIN  LDX   2  FX2  
  10 44WL          JBSC     XUP,2,G263SP 
  15 45B=          OUTNUM   6,7,1               [ THE ANSWER 
   9 45TW          MONOUT   HBMESS   
   6 46*G    NDING ENDCOM
   4 46T6    #   
  10 47#Q    SUPOK MHUNTW   1,FILE,ENT   
  20 47SB          JMBS     CLVWR,1,BELIB,BEEXO [ VERY WRONG - SHOULD BE IMPOSS  
  17 48#2          JMBS     NTEMP,1,BETEMP      [ NOT QUITE SO WRONG 
   9 48RL          LDXC  0  EUSE1(1) 
  15 49?=          BCS      PDIR                [ DIRECTORY  
  21 49QW          BNZ   4  QSIZ                [ FGN GIVEN - COULD BE M/F ELEMENT   
  19 4==G          JBC      QSIZ,1,BEMDF        [ EASY IF NOT WHOLE MULTIFILE
  19 4=Q6          LDX   5  ELAN(1)             [ LANG TO PICK OUT OTHER ELTS
  18 4?9Q          LDX   7  ELOC1(1)            [ -VE INDICATES MULTIFILE
   9 4?PB          LDX   4  ELOC2(1) 
   9 4#92          LDX   6  ELOC3(1) 
   9 4#NL          STO   6  AWORK3(2)
   7 4*8=          FREECORE 1
  11 4*MW          SETNCORE 6,1,FILE,FLOCNB  
   8 4B7G          STO   7  A1(1)
   9 4BM6          STO   4  A1+1(1)  
   9 4C6Q          STO   6  A1+2(1)  
   9 4CLB          STOZ     A1+3(1)  
   9 4D62          STOZ     A1+4(1)  
   9 4DKL          STO   5  A1+5(1)  
   7 4F5=          LDN   6  0
  19 4FJW          GETDIR   6                   [ BACK TO FIRST ENTRY IN M/F 
  19 4G4G          TESTRPN2 OK,XGDIR            [ DIR IS A COMPLETE SHAMBLES 
  10 4GJ6          MHUNTW   1,FILE,ENT   
  17 4H3Q          JBC      NUWB,1,BEUWB        [ SIZE OK IN DIR ENT 
   7 4HHB          LDX   2  1
  18 4J32          CALL  5  SZFCB               [ CURRENT TOP ELT SIZE   
  17 4JGL          LDX   5  ELAN(2)             [ REINSTATE X5 AND   
  17 4K2=          LDX   1  2                   [  FILE/ENT POINTER  
  19 4KFW          BRN      SIZMF               [ OTHERS SHOULD BE OK IN DIR 
  17 4K_G    NUWB  LDX   6  ECOPS(1)            [ INITIALISE TOTAL   
   8 4LF6          SRL   6  15   
  17 4LYQ    SIZMF JBS      SIZ,1,BEMDF         [ STILL M.D.F. ! ?   
   7 4MDB          FREECORE 1
  10 4MY2          MFREE    FILE,FLOCNB  
  18 4NCL          BRN      TROG                [ TO FIND OTHER ELEMENTS 
   4 4NX=    #   
  21 4PBW    USCUR BRUSEN   UNMIS,2             [ NO CURRENT USER IN NO-USER CONTEXT 
  10 4PWG          LDN   4  CPREFIX(2)   
   9 4QB6          LDN   5  AWORK2(2)
  18 4QTQ          MOVE  4  3                   [ FOR COMMON CODE LATER  
  21 4R*B          NGN   6  1                   [ IF 'FILE', NO FILE/ENT ON THIS PATH
  15 4RT2          FREECORE 1                   [ CPB/CUNI   
   8 4S#L          BRN      UPAR 
   4 4SS=    #   
  19 4T?W    PDIR  LDN   4  EUSE1(1)            [ USERNAME OF GIVEN DIRECTORY
   9 4TRG          LDN   5  AWORK2(2)
   7 4W?6          MOVE  4  3
  19 4WQQ          LDX   6  ECOPS(1)            [ IN CASE 'FILE' PARAMETER   
   8 4X=B          SRL   6  15   
  21 4XQ2          FREECORE 1                   [ FREE FILE/ENT BEFORE CLOSETOP COOR 
   7 4Y9L          CLOSETOP  
   4 4YP=    #   
  21 4_8W    UPAR  STOZ     AWORK1(2)           [ INDICATES QUEUED USERNAMES IF >= 0 
  19 4_NG          BRUSEN   NUDIR,2             [ DIR NOT ALLOWED IN NO-USER 
  20 5286          PARAPASS                     [ MAY BE OPTIONS WITH DIRECTORY  
  10 52MQ          MHUNT    2,CPB,CUNI   
  13 537B          SETUPMODE 3,4,READ,DIRONLY,FROZEN 
   9 53M2          NGX   0  ANUM(2)  
   8 546L          LDN   7  #40  
   8 54L=          BPZ   0  X7SET
   9 555W          LDX   5  APARA(2) 
   7 55KG          LDN   7  0
   9 5656          TXU   5  SONLY(1) 
   8 56JQ          BCC      X7SET
   8 574B          LDN   7  #240 
   9 57J2          TXU   5  SALL(1)  
   8 583L          BCC      X7SET
   9 58H=          TXU   5  SFILE(1) 
   8 592W          BCS      PFERR
  17 59GG          BPZ   6  XFIN                [ 'FILE' - SIZE KNOWN
  20 5=26          LDCT  7  #400                [ SPECIAL MARKER TO OPEN CUR DIR 
  18 5=FQ          ADDINMODE 3,4,LEAVE          [  AND GET ITS FILE SIZE 
   4 5=_B    #   
  19 5?F2    X7SET LDCT  6  #400                [ -VE INDICATES FIRST TIME   
  17 5?YL    #     ADDITIVE BITS IN X7 : #40 - INCLUDE INFERIOR DIRS 
  19 5#D=    #                          #200 - INCLUDE REAL AS WELL AS PSEUDO
  17 5#XW    #     SIGN BIT USUALLY INDICATES TOTTING UP A MULTIFILE.
   4 5*CG    #   
  21 5*X6    TBRK  IF       BC,FX2,G263SP       [ SEEMS UNSAFE TO TEST B-I IN %;..;  
   6 5BBQ          THEN  
  21 5BWB             TESTBRKIN XBRK               [ NOT TESTED IN USEROPEN OF DIR   
   5 5CB2          FI
  21 5CTL          LDXC  6  6                   [ TEST IF 1ST TIME AND CLEAR IF SO   
   8 5D*=          BCS      NSMOD
  15 5DSW          SETUPMODE 3,4,READ,DIRONLY,FROZEN,UNTRAP  
  12 5F#G    NSMOD SETNCORE APARA-A1+4,1,CPB,CUNI
   7 5FS6          LDN   0  1
  19 5G?Q          STO   0  JPARNUM(1)          [  CPB/CUNI FOR ERROR IN OPEN
   8 5GRB          LDN   5  13   
   9 5H?2          STO   5  ANUM(1)  
   8 5HQL          LDN   0  #12  
   9 5J==          DCH   0  APARA(1) 
   8 5JPW          LDX   0  ACES 
  10 5K9G          STO   0  APARA+3(1)   
   8 5KP6          SMO      FX1  
   8 5L8Q          ADX   1  PSTEP
   9 5LNB          LDN   0  AWORK2(2)
   8 5M82          MVCH  0  12   
   7 5MML          LDN   0  3
   9 5N7=          SBS   0  AWORK1(2)
  11 5NLW          SETNCORE 4,1,FILE,FABSNB  
   7 5P6G          LDN   0  4
   8 5PL6          STO   0  A1(1)
   9 5Q5Q          LDN   0  AWORK2(2)
   8 5QKB          ADN   1  A1+1 
   7 5R52          MOVE  0  3
   9 5RJL          USEROPEX XBRK,3,4 
   9 5S4=          TESTREP2 OK,OPOK  
   7 5SHW          PICKREP2 4
  11 5T3G          OUTPARAM 5,APARA,CPB,CUNI 
   9 5TH6          MONOUT   AWSNAME  
   7 5W2Q          PUTREP2  4
   8 5WGB          REPERR2  OPOK 
   8 5X22          BRN      XERR 
   4 5XFL    #   
  19 5X_=    OPOK  BNG   7  OK                  [ SPECIAL CASE  ' HB ,FILE ' 
   9 5YDW          MFREE    CPB,CUNI 
  17 5YYG    VF    VFREE    FILE                [ SCRUB USEROPEN JUNK
   8 5_D6          BPZ   1  VF   
  18 5_XQ          STOZ     AWORK2(2)           [ NO 'WAITING' DIRECTORY 
   8 62CB          BRN      TROG 
   4 62X2    #   
   9 63BL    TOTUP LDX   0  ECOPSN(3)
   8 63W=          SRL   0  15   
   7 64*W          ADX   6  0
  20 64TG          BVSR     YUKK                [ ABOUT 800 TAPES OR 80 EDS200'S 
   4 65*6    #   
   6 65SQ    TROG  STEP  
  18 66#B          BZE   3  NDIR                [ END OF THIS DIRECTORY  
   9 66S2          LDX   0  ERESN(3) 
  16 67?L          BNZ   0  TROG                [ NOT NAME RECORD
  20 67R=    #     FOR SUPER-FAST BREAK-IN RESPONSE, TESTBRKIN HERE, JUMPING TO  
  20 68=W    #     A CLOSETOP FOLLOWED BY 'BRN XBRK'.  STANDARD CODE WAITS TILL  
  15 68QG    #     END OF THE CURRENT INDIVIDUAL DIRECTORY.  
  15 69=6          JMBS     TROG,3,BNTEMP,BNERASE,BNLIB,BNEXO
   8 69PQ          BPZ   7  NMULT
   9 6=9B          LDX   0  AWORK3(2)
   9 6=P2          TXU   5  ELANN(3) 
   9 6?8L          TXU   7  ELOC1N(3)
   9 6?N=          TXU   4  ELOC2N(3)
   9 6#7W          TXU   0  ELOC3N(3)
  18 6#MG          BCS      TROG                [ NOT IN OUR MULTIFILE   
  19 6*76          JBS      PCLOS,3,BNMDF       [ MDF IS LAST, AND SIZE ZERO 
  18 6*LQ          BRN      TOTUP               [ ORDINARY M/F ELEMENT   
   9 6B6B    NMULT LDCH  0  EUSE1N(3)
   7 6BL2          ANDX  0  7
  19 6C5L          BZE   0  TOTUP               [ NON-DIR, OR DIRS NOT WANTED
   7 6CK=          TXU   0  7
  19 6D4W          BCS      WALL                [ X0#X7 HERE INDICATES 'ALL' 
  11 6DJG          JBC      TOTUP,3,BNPSEUDO 
   7 6F46    WALL  LDN   0  3
  19 6FHQ          ADS   0  AWORK1(2)           [ CONTENTS OF THIS DIR WANTED
  18 6G3B          LDXC  0  AWORK2(2)           [ NAME TO AWK2-4 IF FREE 
  20 6GH2          BCS      USEBL               [ ELSE HAVE TO USE ADATA/CSTORE  
   9 6H2L          LDN   1  EUSE1N(3)
   9 6HG=          LDN   2  AWORK2(2)
   7 6H_W          MOVE  1  3
   8 6JFG          BRN      TOTUP
  11 6J_6    USEBL HUNTW    1,ADATA,CSTORE   
   9 6KDQ          LDX   4  AWORK1(2)
   8 6KYB          BPZ   1  OLDBL
   7 6LD2          LDX   5  7
  18 6LXL          SRL   5  3                   [ REASONABLE SIZE REQUEST
  10 6MC=          ADN   5  IROUND-1+A1  
   9 6MWW          ANDN  5  -IROUND  
  17 6NBG          SBN   5  A1                  [ ASK FOR TIDY SIZE  
  11 6NW6          SETUPCORE 5,1,ADATA,CSTORE
   7 6P*Q          STEPAGAIN 
  11 6PTB          MHUNTW   1,ADATA,CSTORE   
  10 6Q*2    OLDBL LDX   5  ALOGLEN(1)   
   7 6QSL          TXL   5  4
   8 6R#=          BCC      WILGO
  20 6RRW          ADN   5  IROUND*2            [ REASONABLE EXTENSION (5 NAMES) 
   8 6S?G          ANDN  5  #7777
   7 6SR6          TXL   5  4
  18 6T=Q          BCS      YUKK                [ BLK WANTED > 4K - EUKK!
   9 6TQB          ALTLENG  1,5,ROT  
   7 6W=2          STEPAGAIN 
  11 6WPL          MHUNTW   1,ADATA,CSTORE   
   7 6X9=    WILGO SMO      4
  17 6XNW          ADN   1  A1-3                [ POSITION IN CSTORE 
   9 6Y8G          LDN   0  EUSE1N(3)
   7 6YN6          MOVE  0  3
   8 6_7Q          BRN      TOTUP
   4 6_MB    #   
  20 7272    ROT   MHUNTW   2,ADATA,CSTORE      [ ROUTINE REQUIRED BY ALTLENG FOR
  20 72LL          EXIT  1  0                   [  REASONS BEST KNOWN TO ITSELF  
   4 736=    #   
   7 73KW    NDIR  CLOSETOP  
   9 745G          LDXC  4  AWORK1(2)
  17 74K6          BCS      XFIN                [ NO MORE DIRS TO DO 
  17 754Q          LDXC  0  AWORK2(2)           [ IF NAME ALREADY IN 
  17 75JB          BCS      TBRK                [  AWK2-4, CARRY ON  
  11 7642          MHUNTW   3,ADATA,CSTORE   
  17 76HL          ADN   4  A1(3)               [ LAST IN, FIRST OUT 
   9 773=          LDN   5  AWORK2(2)
   7 77GW          MOVE  4  3
   8 782G          BRN      TBRK 
   4 78G6    #END
   2 78_Q
   6 ____ ...001214320001
