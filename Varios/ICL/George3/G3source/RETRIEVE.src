  13 22FL ...  SEG RETRIEVE,84,C.R.JOHNSON,SCHEDULER 
   4 22_=    [   
  21 23DW    [     THIS SEGMENT IMPLEMENTS THE ^RETRIEVE^ COMMAND FOR AUTONOMOUSLY   
   9 23YG    [     RESTORING FILES   
   4 24D6    [   
  14 24XQ    [     ENTRY 1 IS FROM THE COMMAND PROCESSOR 
  20 25CB    [           READS A PARAMETER - ANALYSES IT FOR MISSING,NULL,CONTEXT
  20 25X2    [     TSN AND !. AFTER USEROPEN INC NUMBER CHECKED - IF NOT ZERO AND
  14 26BL    [     FILE NOT ONLINE, GO DOWN TO SCHEDENX,2
  18 26W=    [     ON RETURN - CHECK REPLY THEN GO TO READ NEXT PARAMETER
   4 27*W    [   
  14 27TG    [     ENTRY 2 FROM RV MACRO -FABSNB SUPPLIED
   4 28*6    [   
  18 28SQ    [     ENTRY 3 DIRECTORY ALREADY OPEN - SETS UP AN AUTONOMOUS
  10 29#B    [           RETRIEVE REQUEST
   4 29S2    [   
  12 2=?L          SEGENTRY K1RETRIEVE,X1RETRIEVE
  12 2=R=          SEGENTRY K2RETRIEVE,X2RETRIEVE
  12 2?=W          SEGENTRY K3RETRIEVE,X3RETRIEVE
  12 2?QG          SEGENTRY K5RETRIEVE,X5RETRIEVE
   4 2#=6    [   
   4 2#PQ    [   
   4 2*9B    [   
  11 2*P2    [     MONITORING FILE MESSAGES  
   4 2B8L    [   
  17 2BN=    [     MON2 SHOULD BE COMPILED ONE WORD BEFORE +HALRON   
   9 2C7W    MON2           +ALRONL  
  19 2CMG                   +HALRON             [FILES ALREADY ONLINE: %A %B 
   9 2D76    XJNOPAR        +JNOPAR  
   7 2D## ...      FSHSKIP  B
   4 2DFG ...(   
   9 2DLQ    XAMISS         +AMISS   
  10 2F6B    XJFENT         +JFENTRANT   
   9 2FL2    XERLOST        +ERLOST  
  10 2G5L    XERENT         +ERENTTYPE   
   9 2GK=    XJLOC          +JLOCNA  
   9 2H4W    XJUSE          +JUSNA   
  10 2HJG    XERTEMP        +ERTEMPLOST  
  17 2HKK ...XGENERR        +GENERR             [ERROR %B %%%A : %C  
  21 2HLN ...XALQUAL        +ALQUAL             [AN ILLEGAL QUALIFIER HAS BEEN GIVEN 
  19 2HMR ...XERB4          +ERB4               [YOU DO NOT HAVE %C PRIVILEGE
  14 2HNW ...XERHLS         +ERHLS              [HLSEXT  
  21 2HP_ ...XERNUSER       +ERNUSER            [THIS QUALIFIER NOT ALLOWED IN NO USE
  17 2HR4 ...XHLSEXT        8HHLSEXT            [USED BY CHECKPRIV   
   4 2HRN ...)   
  20 2HS# ...SLSGMT         #177777             [MASK FOR L.S. 16 BITS OF GMTNOW 
   8 2J46    ONERR SBX   7  FX1  
   8 2JHQ          SMO      FX1  
   8 2K3B          LDX   1  0(1) 
   7 2KH2          ERRORX   1
   8 2L2L          ADX   7  FX1  
   7 2LG=          EXIT  7  0
   4 2LL7 ...#   
   7 2LQ4 ...      FSHSKIP  B
   4 2LT_ ...(   
   5 2L_W    TWOERR  
   8 2MFG          SMO      FX1  
   8 2M_6          LDX   1  0(1) 
   8 2NDQ          SMO      FX1  
   8 2NYB          LDX   2  0(2) 
   8 2PD2          ERRORX   1,2  
   8 2PXL          BRN      X1   
   4 2PY# ...)   
   4 2P_3 ...[   
  17 2Q2D ...SGMTNOW        [ROUTINE TO SET L.S. 16 BITS OF GMTNOW IN
  15 2Q3T ...                [FIELD FEGMTRV OF FILE ENT BLOCK
  15 2Q4H ...               [AND TO UPDATE THE NAME RECORD   
   8 2Q4W ...      SBX   6  FX1  
  10 2Q5= ...      MHUNTW   3,FILE,ENT   
   9 2Q6M ...      LDX   5  GMTNOW   
   8 2Q76 ...      SMO      FX1  
   9 2Q7K ...      ANDX  5  SLSGMT   
   8 2Q99 ...      BNZ   5  SFNZ 
   7 2Q=W ...      LDN   5  1
  18 2Q?P ...SFNZ  STF      3,FEGMTRV,5         [IF FIELD =0 SET IT =1   
  10 2Q#J ...      NAME     3,FILE,FWB   
  17 2Q#T ...      REWRITE                      [UPDATE NAME RECORD  
  10 2Q*6 ...      MHUNTW   3,FILE,FWB   
  10 2Q*C ...      NAME     3,FILE,ENT   
   8 2Q*T ...      ADX   6  FX1  
   7 2QB# ...      EXIT  6  0
   4 2QC=    [   
  17 2QWW    [     ENTRY FROM COMMAND PROCESSOR WITH PARAMETER LIST  
   4 2RBG    [   
  18 2RW6    [     AWORK1 ZERO IF NO PARAMS READ YET,NON-ZERO OTHERWISE  
  17 2S*Q    [     AWORK2 CONTAINS NO.OF FILES IN RV WHICH ARE ONLINE
  14 2STB    [     AWORK3 CONTAINS LINK FOR RVAUT S.R.   
  18 2T*2    [     AWORK4 IS NEGATIVE IF THE CURRENT PARAM IS A MULTIFILE
   6 2TSL    X1RETRIEVE  
  10 2TW* ...      FSHCODE  B,XFSHBTRAN  
   4 2TY4 ...(   
  15 2T_R ...#  ALL RETRIEVES TO BE CARRIED OUT ON MACHINE A 
  16 2W3G ...      TRANSBEG FSHRETRIEVE,RETRIEVE,1,CLB,,ACOMMUNE1
   6 2W59 ...XFSHBTRAN   
   4 2W6Y ...)   
   9 2W8M ...      FSHSKIP  B,XFSHNB1
   4 2W=B ...(   
   9 2W#=          STOZ     AWORK1(2)
   9 2WRW          STOZ     AWORK2(2)
   4 2X?G    X1  
   7 2XR6          UNIFREE   
  15 2Y2Y ...      VFREE    FILE,ADJUNCTS       [JUST IN CASE
  14 2Y6T ...      VFREE    FI,FONCE            [DITTO   
  17 2Y=Q          SPARAPAS                     [FETCH NEXT PARAMETER
   9 2YQB          STOZ     AWORK4(2)
  10 2_=2          MHUNT    3,CPB,CUNI   
   9 2_PL          LDX   0  ANUM(3)  
  17 329=          BPZ   0  PEXISTS             [J IF PARAM EXISTS   
   5 32=_ ...XFSHNB1 
   4 32#N ...)   
  10 32BC ...      FSHCODE  A,XFSHATRAN  
   4 32D6 ...(   
  21 32FT ...      TRANCHKN FSHRETRIEVE,EQUAL,XFSHATRAN,XFSHATRAN [J IF NOT FROM B   
  17 32HJ ...      TRANSFIN                     [RETURN TO MACHINE B 
   6 32K? ...XFSHATRAN   
   4 32M2 ...)   
   4 32NW    [   
  10 338G    [     PARAMETER NON-EXISTENT
   4 33N6    [   
  21 347Q          LDX   0  AWORK1(2)           [ZERO IF THERE HAVE BEEN NO PARAMS YE
  15 34MB          BZE   0  NOTHERS          [J IF NO OTHERS 
   9 3572          LDX   6  AWORK2(2)
  16 35LL          BZE   6  UP                  [J IF ALL OFFLINE
   4 366=    [   
  15 36KW    [     DECIDE WHICH MONOUT- %A IS ALREADY ONLINE 
  18 375G    [                      OR- FILES ALREADY ONLINE: %A %B %C %D
   7 37K6    [     AND DO IT 
   4 384Q    [   
   7 38JB          SBN   6  1
  17 3942          BZE   6  SINGMESS            [J IF ONLY ONE PARAM 
   7 39HL          LDN   6  1
   5 3=3=    SINGMESS
   4 3=GW    [   
   9 3?2G    [     OUTPUT PARAMETERS 
   4 3?G6    [   
  10 3?_Q          MHUNTW   3,FILE,FINTER
   9 3#FB          BNG   3  OUTMESS  
  20 3#_2          OUTPARAM A1(3),CPDATA,FILE,FINTER    [REALLY AN ADATA/CREADL  
  10 3*DL          MFREEW   FILE,FINTER  
   9 3*Y=          BRN      SINGMESS 
   5 3BCW    OUTMESS 
   7 3BXG          SMO      6
  16 3CC6          LDX   7  MON2(1)             [ALRONL OR HALRON
   7 3CWQ          MONOUTX  7
   8 3DBB          BRN      UP   
   4 3DW2    [   
  14 3F*L    [     THERE WERE NO PARAMS TO THE COMMAND   
   4 3FT=    [   
   5 3G#W    NOTHERS 
   9 3GSG          LDN   1  XJNOPAR  
  17 3H#6          CALL  7  ONERR               [PARAMETER MISSING   
   4 3HRQ    UP  
  20 3J?B          ENDCOM                       [AND RETURN TO COMMAND PROCESSOR 
   4 3JC? ...#   
   7 3JH8 ...      FSHSKIP  B
   4 3JM5 ...(   
   5 3JR2    PEXISTS 
   7 3K=L          LDN   1  1
  19 3KQ=          STO   1  AWORK1(2)           [SET'PARAM READ' IN AWORK1   
  17 3L9W          BNZ   0  NORM                [J IF PARAM NOT NULL 
   4 3LPG    [   
   9 3M96    [     NULL PARAM READ   
   4 3MNQ    [   
   9 3N8B          LDN   1  XAMISS   
   9 3NN2          LDN   2  XJFENT   
  19 3P7L          BRN      TWOERR              [ENTRANT DESCRIPTION MISSING 
   4 3PM=    NORM
   4 3Q6W    [   
  21 3QLG    [     WE FIRST MUST DO SOME WORK FOR FNORM-IT WONT COPE WITH A RELATIVE 
  11 3R66    [     NAME IN NO USER CONTEXT   
   4 3RKQ    [   
  10 3S5B    [     X3 POINTS TO CPB/CUNI 
   4 3SK2    [   
  16 3T4L          LDCH  0  APARA(3)            [LOAD FIRST CHAR 
   8 3TJ=          SBN   0  #12  
  17 3W3W          BZE   0  PRESCOL             [J IF COLON PRESENT  
  21 3WHG          BBUS     23,CONTEXT(2),XLOCAL    [J IF RELATIVE IN NO USER CONTEXT
   4 3X36    [   
  13 3XGQ    [     EITHER COLON OR IN USER CONTEXT   
   4 3Y2B    [   
   5 3YG2    PRESCOL 
  10 3Y_L          NAMETOP  3,FILE,FNAME 
  20 3_9D ...      FNORM 3                      [CONVERT PARAMETER TO FILE/FABSNB
  12 3_H8 ...NXTTRAP                            [
  18 3_K6 ...      VFREE    FILE,FTRAP          [FREE FTRAP IF PRESENT   
  18 3_M4 ...      BPZ   1  NXTTRAP             [ANY MORE FTRAP BLOCKS?  
  17 3_P2 ...      HUNT     3,FILE,ADJUNCTS     [ADJUNCTS PRESENT?   
  16 3_QY ...      BNG  3   NOADJUNCTS          [J IF NOT PRESENT
  20 3_SW ...      NAME     3,FI,FONCE          [HIDE ADJUNCTS DURING USEROPEN!!!
  12 3_WS ...NOADJUNCTS                         [
  10 3_YW          MHUNT    3,FILE,FNAME 
  10 42DG          NAMETOP  3,CPB,CUNI   
  18 42Y6          TESTREP2 NAMEFORM,X1     [GO FOR NEXT IF ERROR IN NAME
  21 43CQ                                       [NO FABSNB SET UP BY FNORM IN THIS   
  13 43XB                                       [CASE
  10 44C2          MHUNT    1,FILE,FABSNB
  17 44WL          LDN   0  #2400               [B13,15 TSN,REEL/RET 
   9 45B=          ANDX  0  ATYPE(1) 
  19 45TW          BNZ   0  XENTTYPE            [J IF ENTRANT TYPE INVALID   
  18 46*G          BBS   16,ATYPE(1),X2         [IF !,THEN ALREADY ONLINE
  10 46T6          LDX   0  ALOGLEN(1)   
   7 47#Q          SBN   0  4
  17 47SB          BZE   0  XUSE                [J IF ONLY USERNAME  
  19 482L ...      USEROPEN XBR,READR,LEAVE,STREAMS,DIR,TERMDIR,UNTRAP,FROZEN
  17 48C6 ...      HUNT     3,FI,FONCE          [ADJUNCTS PRESENT?   
  14 48G= ...      BNG   3  NOFONCE             [J IF NOT
  18 48KB ...      NAME     3,FILE,ADJUNCTS     [RENAME BACK TO ADJUNCTS 
  12 48NG ...NOFONCE                            [
   8 48RL          REPERR   OK2  
  19 49?=          BRN      X2                  [TRY NEXT PARAM AFTER ERROR  
   4 49QW    OK2 
  10 4==G          TREPN    STREAM ,OK3  
   8 4=Q6          LDX   0  GSIGN
  18 4?9Q          STO   0  AWORK4(2)           [REMEMBER TO CLOSE SDF   
   4 4?PB    OK3 
  21 4?_8 ...      CALL  6  SGMTNOW             [SET L.S. 16 BITS OF GMTNOW IN ENT   
  10 4#92          HUNT     3,FILE,ENT   
   9 4#NL          LDX   0  ECOPS(3) 
   8 4*8=          ANDN  0  #7777
  17 4*MW          BNZ   0  ONLALREADY          [ J IF ALREADY ONLINE
  18 4B7G          LDX   6  EINC(3)             [PICK UP INCREMENT NUMBER
  17 4BM6          BZE   6  XER                 [BRANCH IF FILE LOST 
  21 4BNX ...      LDX   6  JOBNO(2)            [X6 => JOB NO OF REQUESTING ACTIVITY 
  18 4BPG ...      HUNT 1,FILE,ADJUNCTS         [DOES AN ADJUNCTS EXIST? 
  14 4BQ5 ...      BNG   1  XSCHEDENX           [J IF NOT
  18 4BQF ...      LDN   2  21                  [X2 => JOBNO IDENTIFIER  
  17 4BQT ...      LDX   3  A1(1)               [X3 => RECORD HEADER 
  19 4BR9 ...      SBN   3  ADPAIR-A1           [X3 => LENGTH OF WORD PAIRS  
  17 4BRK ...      BZE   3  XFREEADJ            [J IF NO WORD PAIRS  
  12 4BR_ ...XADJLOOP                           [
  12 4BS* ...      SBN   3  2                   [
  17 4BSP ...      SMO      3                   [X3 USED AS MODIFIER 
  21 4BT5 ...      LDX   0  ADPAIR(1)           [X0 => QUALIFIER NUMBER IDENTIFIER   
  14 4BTM ...      BXE   0  2,XADJOK            [J IF OK 
  17 4BWT ...      BNZ   3  XADJLOOP            [J IF MORE WORD PAIRS
  15 4BY3 ...      BRN      XFREEADJ            [J IF NO MORE
   5 4B_9 ...XADJOK  
  18 4B_S ...      BRUSEN   XNOUSER             [J IF NOT USER CONTEXT   
  19 4C2C ...      CHEKPRIV 0,XHLSEXT,XHLS      [J TO XHLS IF NO PRIVILEGE   
  17 4C32 ...      MHUNT 1,FILE,ADJUNCTS        [FIND ADJUNCTS AGAIN 
  12 4C3? ...      SMO      3                   [
  20 4C3J ...      LDX   6  ADPAIR+1(1)         [X6 => JOBNUMBER FROM ADJUNCTS   
  12 4C3T ...XFREEADJ                           [
  16 4C48 ...      FREECORE 1                   [FREE ADJUNCTS   
   6 4C4R ...XSCHEDENX   
   3 4C5B ...
  10 4C6Q          DOWN     SCHEDENX,2   
  15 4CLB          TESTREP  OK,XCLS,LOST,XLOST,TEMPLOST,XTEMP
   9 4D62          LDX   0  EXEC3(2) 
   8 4DKL          BNG   0  XCLS 
   9 4F5=          GEOERR   1,FUNYREP
   5 4F67 ...XNOUSER 
  10 4F74 ...      MFREE FILE,ADJUNCTS   
  16 4F7_ ...      LDN   1  XGENERR             [SET UP X1 AND X2
  15 4F8W ...      LDN   2  XERNUSER            [FOR ERRORX  
   9 4F9R ...      BRN      XOUTWO   
   4 4F=N ...XHLS
  17 4F?K ...      MFREE FILE,ADJUNCTS          [FREE FILE/ADJUNCTS  
  16 4F#G ...      LDN   1  XERB4               [SET UP X1 AND X2
  15 4F*C ...      LDN   2  XERHLS              [FOR ERRORX  
   5 4FB# ...XOUTWO  
   8 4FC9 ...      SMO      FX1  
   8 4FD6 ...      LDX   1  0(1) 
   8 4FF3 ...      SMO      FX1  
   8 4FFY ...      LDX   2  0(2) 
   8 4FGT ...      ERRORX   1,2  
   8 4FHQ ...      BRN      XFR  
   5 4FJW    XLOST   
   9 4G4G ...      LDN   1  XERLOST  
   8 4GJ6          BRN      XONE 
   5 4H3Q    XTEMP   
   9 4HHB          LDN   1  XERTEMP  
   4 4J32    XONE
   8 4JGL          CALL  7  ONERR
   4 4K2=    RV1 
   8 4KFW          LDX   2  FX2  
  18 4K_G          BRN      XCLS                [GO TO CLOSE SDF IF OPEN 
   4 4LF6    XCL 
  15 4LYQ          CLOSETOP                     [DIRECTORY   
   4 4MDB    XCLS
   9 4MY2          LDX   0  AWORK4(2)
  14 4NCL          BPZ   0  X2                  [NO SDF  
   7 4NX=          CLOSESTR  
  10 4PBW    X2    MFREE    FILE,FABSNB  
  18 4PWG          BRN      X1                  [GO FOR NEXT PARAMETER   
  20 4QB6         [MUSTNT COME EARLIER OR PEOPLE WILL CANCEL BEFORE FCANCL SET UP
   4 4QTQ    [   
  10 4R*B    [     FILE ALREADY ONLINE   
   4 4RT2    [   
   6 4S#L    ONLALREADY  
  11 4SS=          SETNCORE 20,3,ADATA,CREADL
   8 4T?W    NEWBL STOZ     A1(3)
   7 4TRG          LDN   0  1
  18 4W?6          ADS   0  AWORK2(2)           [INCR.CT OF FILES ONLINE 
   8 4WQQ          UNNORM   TWO  
  11 4X=B          MHUNTW   3,ADATA,CREADL   
  10 4XQ2          NAME     3,FILE,FINTER
   9 4Y9L    XFR   MFREEW   FILE,ENT 
   8 4YP=          BRN      XCL  
   4 4_8W    XER 
  15 4_NG          LDN   1  XERLOST             [FILE LOST   
   8 5286          CALL  7  ONERR
   8 52MQ          BRN      XFR  
   4 537B    [   
  10 53M2    [     ENTRANT TYPE INVALID  
   4 546L    [   
   5 54L=    XENTTYPE
   9 555W          LDN   1  XERENT   
   8 55KG          CALL  7  ONERR
   8 5656          BRN      X2   
   4 56JQ    [   
   9 574B    [     LOCAL NAME MISSING
   4 57J2    [   
  10 583L    XUSE  MFREE    FILE,FABSNB  
   9 58H=          LDN   1  XAMISS   
   8 592W          LDN   2  XJLOC
   9 59GG          BRN      TWOERR   
   4 5=26    [   
   9 5=FQ    [     USER NAME MISSING 
   4 5=_B    [   
   5 5?F2    XLOCAL  
   9 5?YL          LDN   1  XAMISS   
   8 5#D=          LDN   2  XJUSE
   9 5#XW          BRN      TWOERR   
   4 5*7N ...)   
   4 5*CG    XBR 
  19 5*X6          COMBRKIN                     [RETURN TO CP AFTER BREAKIN  
   4 5BBQ    [   
  13 5BWB    [     K2 ENTRY-RV MACRO-FABSNB SUPPLIED 
   4 5CB2    [   
   6 5CTL    X2RETRIEVE  
   9 5CXJ ...      FSHCODE  B,XFSHBRV
   4 5C_G ...(   
  14 5D3D ...#  RV MACRO TO BE OBEYED ON MACHINE A ONLY  
  16 5D5B ...      TRANSBEG FSHRETRIEVE,RETRIEVE,2,CLB,,ACOMMUNE1
   5 5D7# ...      UP
   5 5D9= ...XFSHBRV 
   4 5D?8 ...)   
  17 5D*=          CALL  7  RVAUT               [SET UP RV ACTIVITY  
   9 5DCF ...      FSHCODE  A,XFSHARV
   4 5DFN ...(   
  21 5DHX ...      TRANCHKN FSHRETRIEVE,EQUAL,XFSHARV,XFSHARV [J IF NOT FROM M/C B   
  17 5DL6 ...      TRANSFIN                     [RETURN TO MACHINE B 
   5 5DN* ...XFSHARV 
   4 5DQJ ...)   
   5 5DSW          UP
   4 5F#G    [   
  16 5FS6    [     K3 ENTRY - RVDIR MACRO - DIRECTORY OPENED AND 
  13 5G?Q    [                 ENT  BLOCK SUPPLIED   
   4 5GRB    [   
   6 5H?2    X3RETRIEVE  
  10 5H#Y ...      FSHCODE  B,XFSHBRVDIR 
   4 5HBW ...(   
   9 5HDS ...      BRN      (GEOERR) 
   6 5HGQ ...XFSHBRVDIR  
   4 5HJN ...)   
   7 5HLL ...      FSHSKIP  B
   4 5HNJ ...(   
   9 5HQL          CALL  7  RVDIRX   
   5 5J==          UP
   4 5JG4 ...)   
   4 5JPW    [   
  12 5K9G    [     SR TO SET UP REQUEST FOR FILE 
   4 5KP6    [   
   8 5L8Q    RVAUT SBX   7  FX1  
  13 5LNB ...      OPENDIR  XBR,GENERAL,QUERY,ERASING
   9 5M82          TESTREP  OK,OK4   
   8 5MML          BRN      UP2  
   4 5MXD ...#   
   5 5N7=    RVDIRX  
  16 5NLW          SBX   7  FX1                 [ FOR RVDIR MACRO
   4 5P6G    OK4 
  16 5PL6          HUNT     3,FILE,ENT          [FIND THE DIR/ENT
   9 5Q5Q          LDX   0  ECOPS(3) 
   8 5QKB          ANDN  0  #7777
  16 5R52          BZE   0  ONLNOT              [J IF NOT ONLINE 
  10 5RJL    [     FILE ALREADY ONLINE   
   9 5S4=          KEEPREP  5,ONLINE 
   9 5SHW          BRN      ZCLOSE   
   8 5T3G    [     FILE OFFLINE  
   5 5TH6    ONLNOT  
   9 5W2Q          LDX   6  EINC(3)  
  19 5WGB          BNZ   6  NOTLOST             [VERY UNLIKELY NOT TO BRANCH 
   9 5X22          KEEPREP  5,LOST   
   5 5XFL    ZCLOSE  
   7 5X_=          FREECORE 3
   7 5YDW          CLOSETOP  
   7 5YYG          PUTREP2  5
   4 5_D6    UP2 
   8 5_XQ          ADX   7  FX1  
   7 62CB          EXIT  7  0
   5 62X2    NOTLOST 
  19 636S ...      CALL  6  SGMTNOW           [UPDATE NAME RECORD WITH GMTNOW
   8 63BL ...      LDX   2  FX2  
  19 63W= ...      LDX   6  JOBNO(2)            [X6 => JOBNO FROM ACTIVITY   
  16 6464 ...      STO   7  AWORK3(2)           [PRESERVE LINK   
  10 64*W          DOWN     SCHEDENX,2   
   6 64TG          ANSOK 
   9 65*6          LDX   7  AWORK3(2)
   8 65SQ          ADX   7  FX1  
   7 66#B          EXIT  7  0
   4 66S2    XBK 
  20 67?L          GEOERR   1,RVBRKIN           [BREAK-IN ON AUTONOMOUS ACTIVITY 
  11 67R=    X5RETRIEVE     GEOERR 1,K5RET?  
  11 68=W          MENDAREA 30,K99RETRIEVE   
   4 68QG    #END
   8 ____ ...26157377000200000000
