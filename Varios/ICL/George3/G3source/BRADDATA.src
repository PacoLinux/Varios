  15 22FL ...      SEG  BRADDATA,860,FRANK BLUNDELL,BRADDATA 
  16 22PD ...#     (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 22_=    #   
  13 23DW    #     ********************************  
  13 23YG    #     *BRADFORD UNIVERSITY SCF SYSTEM*  
  13 24D6    #     ********************************  
   4 24XQ    #   
  12 25X2          SEGENTRY K1BRADDATA,Q1BRADDATA
   9 26BL    SCFMASK        #77777600
   9 26W=    SCFBIT         #00020000
   9 27*W    XRR   MHUNT 2,BSTB,BREAD
   7 27TG          EXIT  1  0
   6 28*6    Q1BRADDATA  
   5 28*G ...#SKI G3 
   4 28*W ...(   
   6 28B# ...#UNS  EMUSCF
   4 28CG ...(   
  19 28DN ...      JENVNOT  STAND,MEEP          [J IF NOT BMEEP ENVIRONMENT  
  14 28FW ...      SETNCORE AENDCHAPS-KTAB,3,BSTB,FSTAB  
   8 28H4 ...      SMO      FX1  
   9 28J= ...      LDN   4  XBRADDATA
   8 28KD ...      LDN   5  A1(3)
  11 28LL ...      LDN   6  AENDCHAPS-KTAB   
  16 28MS ...      MOVLONG  4,5,6               [MOVE CHAPTER NOS
  10 28P2 ...      ACROSS   BRADEMUL,1   
   5 28Q8 ...STAND   
   4 28QR ...)   
   4 28RB ...)   
   4 28SQ    #   
  14 29#B    #     EVALUATE TOTAL SIZE OF SCF REQUIRED   
   4 29S2    #   
  16 2=?L          LDN   6  0                [NEXT ADDRESS ON SCF
  15 2=R= ...      LDN   7  AENDCHAPS-KTAB      [NO. OF CHAPS
  16 2?=W ...WAGN  LDN   3  AENDCHAPS-KTAB      [NO. OF ENTRIES  
  16 2?QG          SBX   3  7                [POINT TO POSITION  
   8 2#=6          SMO      FX1  
  10 2#PQ          LDX   1  XBRADDATA(3) 
  18 2*9B          BZE   1  WNEXT+1          [IF ZERO END OF CHAP DEFS   
   9 2*P2          LDX   5  KTAB(1)  
   8 2B8L          BPZ   5  NCIN 
   7 2BN=          SMO   5   
  16 2C7W          LDX   5  BACK2            [CHAPTER WAS IN CORE
   8 2CMG    NCIN  SLC   5  11   
  15 2D76          ANDN  5  #3774            [LENGTH IN X5   
   8 2DLQ          BNZ   5  W1   
  15 2F6B          LDN   5  1024             [MAXIMUM SIZE   
   8 2FL2    W1    ADN   5  127  
   8 2G5L          SMO      FX1  
  16 2GK=          ANDX  5  SCFMASK          [ROUND TO BUCKETS   
   7 2H4W          ADX   6  5
   8 2HJG    WNEXT BCT   7  WAGN 
  15 2J46          STO   6  ACOMMUNE4(2)     [TOTAL SIZE REQD
   4 2JHQ    #   
  12 2K3B    #     EVALUATE SIZE OF THE SCF GIVEN
   4 2KH2    #   
   9 2L2L          LDX   1  BINDEX   
   7 2LG=          ADN   1  6
  13 2L_W          LDCH  4  A1(1)            [TYPE   
   7 2MFG          SBN   4  9
  13 2M_6          BZE   4  TDRM             [DRUM   
   8 2NDQ          SBN   4  16   
  13 2NYB          BNZ   4  WXCT             [DRUM   
   9 2PD2           LDX   5  A1+2(1) 
   8 2PXL           SLL   5  7   
   8 2QC=           BRN      WSIZ
   7 2QWW    WXCT  LDN   5  0
  14 2RBG          LDX   4  A1+3(1)          [NO. OF CYL 
   9 2RW6    WADD  ADX   5  A1+4(1)  
   8 2S*Q          BCT   4  WADD 
  13 2STB          SLL   5  7                [SIZE   
   8 2T*2          BRN      WSIZ 
   9 2TSL    TDRM  LDX   5  A1+2(1)  
  15 2W#=    WSIZ  STO   5  ACOMMUNE5(2)     [SIZE OF FILE   
   9 2WRW          TRACE 5,SIZEAVAL  
   4 2X?G    #   
  11 2XR6    #     START TRANSFERING CHAPTERS
   4 2Y=Q    #   
  16 2YQB          STOZ     AWORK2(2)        [SCF START ADDRESS  
  16 2_=2 ...      LDN   7  AENDCHAPS-KTAB      [NO. OF ENTRIES  
  16 2_PL ...XAGN  LDN   3  AENDCHAPS-KTAB      [NO. OF ENTRIES  
  19 329=          SBX   3  7                [POINTER TO POSITION IN TABLE   
   8 32NW          SMO      FX1  
  16 338G          LDX   1  XBRADDATA(3)   [DATA WORD FOR CHAPTER
  17 33N6          BZE   1  YFAL             [THATS ALL NO CHAPS LEFT
  15 347Q          STO   1  AWORK1(2)        [CHAPTER NUMBER 
  14 34MB          LDX   5  KTAB(1)          [CHAP WORD  
   8 3572          BPZ   5  MCIN 
   7 35LL          SMO   5   
  16 366=          LDX   5  BACK2            [CHAPTER WAS IN CORE
  16 36?D ...MCIN  SLC   5  10                  [ SCFBIT IN B0   
   8 36DL ...      BPZ   5  NTYT 
   8 36KS ...      BCT   7  XAGN 
   8 36R2 ...      BRN      YFAL 
   7 36Y8 ...NTYT  SLC   5  1
   8 375G          ANDN  5  #3774
   8 37K6          BNZ   5  XW1  
   8 384Q          LDN   5  1024 
  17 38JB    XW1   STO   5  ACOMMUNE1(2)     [STORE SIZE OF TRANSFER 
   8 3942          ADN   5  127  
   8 39HL          SMO      FX1  
  16 3=3=          ANDX  5  SCFMASK          [ROUND TO BUCKETS   
  17 3=GW          LDX   0  AWORK2(2)        [SIZE OF SCF USED SO FAR
  15 3?2G          STO   0  AWORK4(2)        [DA ADDRESS SCF 
  18 3?G6          STO   0  ACOMMUNE3(2)     [DA ADDRESS SCF FOR TRANSFER
  16 3?_Q          ADX   0  5                [ADD THIS CHAP SIZE 
  14 3#FB          BXE   0  ACOMMUNE5(2),SJF [JUST ROOM  
  13 3#_2          BXGE  0  ACOMMUNE5(2),YFAL[NO ROOM
  18 3*DL    SJF   ADS   5  AWORK2(2)        [INCREMENT POSITION IN SCF  
   8 3*Y=          SMO      FX1  
  13 3BCW          LDX   3  XBRADDATA(3)   [DATA WORD
  16 3BXG          LDX   1  KTAB(3)          [DA OR CORE ADDRESS 
  18 3CC6          BPZ   1  XBACK            [JUMP AS ON BACKING STORE   
  14 3CWQ          LDX   1  BACK2(1)         [DA ADDRESS 
  19 3DBB ...XBACK SLL   1  11                  [ REMOVE BITS 0-10 INCLUSIVE 
  17 3DW2 ...      SRL   1  4                   [ =  DA ADDR * 128   
  14 3F*L          STO   1  ACOMMUNE2(2)     [DA ADDRESS 
  15 3FT=          STO   1  AWORK3(2)        [DA ADDRESS ICF 
   9 3G#W          TRACE 3,CHAPOVER  
  15 3GSG          STO   7  ACOMMUNE6(2)     [SAVE LOOP COUNT
   4 3H#6    #   
  11 3HRQ    #     DO READ(S) AND WRITE(S)   
   4 3J?B    #   
  11 3JR2    #     ACOMMUNE1 = SIZE OF CHAPTR
  11 3K=L    #     ACOMMUNE2 = ICF ADDRESS   
  11 3KQ=    #     ACOMMUNE3 = SCF ADDRESS   
   4 3L9W    #   
  19 3LPG    XTRAN SETNCORE 1024,3,BSTB,BREAD[GET A BLOCK TO READ AND WRITE  
  18 3M96    RPTZ  LOCKC 3,XRR               [LOCK IT DOWN IN LONG LOCK  
   9 3MDY ...      MHUNT 3,BSTB,BREAD
   8 3MNQ          LDX   2  FX2  
  14 3N8B          LDX   5  ACOMMUNE2(2)     [D ADDRESS  
  16 3NN2          LDX   6  ACOMMUNE1(2)     [SIZE OF TRANSFER   
  17 3P7L          DATUMADD 5,BINDEX,6       [DTUMISE TO EXEC INFO   
   9 3PM=          BNZ   2  TRANOK   
   9 3Q6W          BNZ   1  TRANOK   
  16 3QLG          GEOERR 1,SPLITF11         [INVALID TRANSFER   
   5 3R66    TRANOK  
  14 3RKQ          LDX   5  2                [KEEP SIZE  
   8 3S5B          LDX   2  FX2  
  15 3SK2          BNZ   5  SAB              [NOT ALL OF TRAN
  15 3T4L          LDX   5  ACOMMUNE1(2)     [SET SIZE AS ALL
  18 3TJ=    SAB   SBS   5  ACOMMUNE1(2)     [NE SIZE FOR NEXT TRANSFR   
  15 3W3W          ADS   5  ACOMMUNE2(2)     [NEW GR ADDRESS 
  14 3WHG          LDN   6  1                [BSUN FOR GR
  15 3X36          LDN   4  0                [MODE IS READ   
  18 3XGQ          ADN   3  A1               [X3 ABSOLUTE TRANSFR ADDRESS
  15 3Y2B          BACKTRAN EX4,BSET+BONL,EX5,,EX6,1,3[READ  
   8 3YG2          LDCT  4  #150 
  14 3Y_L          ANDX  4  ACOMMUNE9(2)     [REPLY WORD 
   9 3_F=          BZE   4  REPOKA   
   8 3_LD          LDCT  7  #010 
   7 3_RL          ANDX  7  4
   9 3_YS          BNZ   7  NOPICF   
   9 4262          GEOERR 1,ICF-FAIL 
   9 42?8    NOPICF GEOERR 1,ICF-INOP
   5 42DG    REPOKA  
   4 43XB    #   
  14 44C2    #     CHANGE CHECKSUMM FOR NEW DA ADDRESS   
   4 44WL    #   
  17 45B=          LDX   7  ACOMMUNE1(2)     [SIZE LEFT TO TRANSFER  
  17 45TW          BNZ   7  XNSCY            [NOT END OF CHAPTER YET 
   7 465N ...      SBN   3  1
  15 46*G          LDX   7  AWORK3(2)        [ICF DA ADDRESS 
   7 46T6          SMO      5
  18 47#Q ...      SBS   7  0(3)            [REDUCE LAST WORD IN CHAPTER 
   9 47CW ...      SMO      AWORK1(2)
   8 47H2 ...      LDX   4  KTAB 
  16 47L6 ...      BPZ   4  NIC                 [J IF NOT IN CORE
  14 47P= ...      LDXC  1  4                   [CLEAR B0
  15 47SB ...      BCS      /                   [FORGET CARRY
  10 47TY ...      ADX   1  ALOGLEN(1)   
  17 47XG ...      ADN   1  A1-1                [ELSE UPDATE CHECKSUM
  18 485Q ...      SBS   7  0(1)                 [SUBTRACT OLD DA ADDRESS
  16 488W ...NIC   LDX   7  AWORK4(2)            [SCF DA ADDRESS 
   7 48#2          SMO      5
  19 48RL ...      ADS   7  0(3)            [FORM CORRECT CHECKSUMM WITH LUCK
  16 48_W ...      BPZ   4  XNIC                [J IF NOT IN CORE
   8 49BB ...      ADS   7  0(1) 
   7 49JL ...XNIC  ADN   3  1
   9 49QW    XNSCY LDX   7  BINDEX   
  21 4==G          ADN   7  6                [MOVER POINTER TO SECOND AREA IN CCTABLE
  17 4=Q6          LDX   4  ACOMMUNE3(2)     [SPLIT RESIDENCE ADDRESS
  17 4?9Q          DATUMADD 4,7,5            [DTUMISE TO EXEC INFO   
   9 4?PB          BNZ   2  TRANOKA  
   9 4#92          BNZ   1  TRANOKA  
  16 4#NL          GEOERR 1,SPLITF13         [INVALID TRANSFER   
   5 4*8=    TRANOKA 
   7 4*MW          LDX   7  2
   8 4B7G          LDX   2  FX2  
  15 4BM6          BNZ   7  SAA              [NOT ALL OF TRAN
  15 4C6Q          LDX   7  5                [SET SIZE AS ALL
  20 4CLB    SAA   ADS   7  ACOMMUNE3(2)     [UPDTE SPLIT ADDRESS BY TRAN SIZE   
  15 4D62          LDX   6  BRADSPLTBSU      [BSUN FOR SPLIT 
  15 4DKL          LDN   4  1                [MODE IS WRIT   
  15 4F5=          BACKTRAN EX4,BSET+BONL,EX7,,EX6,1,3[WRITE 
   8 4FJW          LDCT  4  #150 
  14 4G4G          ANDX  4  ACOMMUNE9(2)     [REPLY WORD 
   9 4GJ6          BZE   4  REPOKB   
   8 4GP#          LDCT  7  #010 
   7 4GWG          ANDX  7  4
   8 4H3N          BNZ   7  SCFI 
   9 4H8W          GEOERR 1,SCF-FAIL 
   9 4HB4    SCFI  GEOERR 1,SCF-INOP 
   5 4HHB    REPOKB  
  17 4J32          ADX   3  7                [INCREMENT COR ADDRESS  
  17 4JGL          SBX   5  7                [SUBTRCT SIZE WRITTEN   
   9 4K2=          BPZ   5  SIZEOK   
  15 4KFW          GEOERR 1,SPLITF15         [WRITTN TOO MUCH
   5 4K_G    SIZEOK  
  16 4LF6 ...      BNZ   5  XNSCY            [MORE TO WRITE OUT  
  16 4LYQ          HUNT 3,BSTB,BWRITE        [GET BLOCK ADDRESS  
   9 4MDB          BPZ   3  OKFIND   
   9 4MY2          HUNT 3,BSTB,BREAD 
   9 4NCL          BPZ   3  OKFIND   
   9 4NX=          GEOERR 1,SPLITF16 
   5 4PBW    OKFIND  
   7 4PWG          UNLOCK 3  
  17 4QB6          LDX   5  ACOMMUNE1(2)     [SIZE LEFT TO TRANSFR   
   8 4QTQ          BPZ   5  TRSZ 
   9 4R*B          GEOERR 1,SPLITF17 
  19 4RT2    TRSZ  BNZ   5  RPTZ             [REPEAT FOR THE RST OF CHAPTER  
   7 4S#L          FREECORE 3
   4 4SS=    #   
  15 4T?W    #     NOW UPDATE THE CHAPTER TABLE IN CORE TO   
  11 4TRG    #     SHOW CHAPTER ON THE SCF   
   4 4W?6    #   
  16 4WQQ    #     CHAPTERS ARE MARKED AS SCF BY SETTING BIT 10  
   4 4X=B    #   
  15 4XQ2    XMARK LDX   3  AWORK1(2)        [CHAPTER NUMBER 
  18 4Y9L          LDX   6  AWORK4(2)        [NEW DA ADDRESS ON THE SCF  
   7 4YP=          SRL   6  7
   8 4_8W          SMO      FX1  
  17 4_NG          ADX   6  SCFBIT           [SET BIT SAYING ON SCF  
  16 5286          LDX   1  KTAB(3)          [DA OR CORE ADDRESS 
  14 52=4 ...      BPZ   1  TBACK            [JIF IN CORE
  21 52#2 ...      LDX   1  BACK2(1)         [ELS GET KTAB ENTRY FROM CHAPTER BLOCK  
   8 52*Y ...TBACK LDCT  0  #777 
  18 52CW ...      ORX   0  BIT9             [MASK FOR BITS 0-9 INCLUSIVE
  17 52FS ...      ANDX  0  1                [REMOVE OLD DA ADDRESS  
  19 52HQ ...      ADX   0  6                [ ADD IN NEW DA ADDRESS + SCFBIT
  18 52KN ...      LDX   1  KTAB(3)          [NOW DECIDE WHERE TO PUT IT 
  18 52MQ          BPZ   1  YBACK            [JUMP AS ON BACKING STORE   
  16 537B ...      STO   0  BACK2(1)         [MARK AS ON SPLIT   
   8 53M2          BRN      YNEXT
  16 546L ...YBACK STO   0  KTAB(3)          [MARK AS ON SPLIT   
  10 54L=    YNEXT LDX   7  ACOMMUNE6(2) 
  17 555W          BCT   7  XAGN             [BACK FOR NEXT CHAPTER  
  14 55KG    YFAL  LDN   7  10               [BLOCK SIZE 
  14 5656          OUTBLOCK 7                [GET A BLOCK
  15 56JQ          OUTNUM ACOMMUNE5(2),0     [SCF FILE SIZE  
  15 574B          OUTNUM ACOMMUNE4(2),0     [SCF CHAPS SIZE 
  10 57J2          INFORM 1,BRADSPT2,1   
   5 57JT ...#SKI G4 
   4 57KN ...(   
  16 57LH ...      LDX   0  GIVE6REP            [PROCESSOR TYPE  
   8 57MB ...      SBN   0  21   
  15 57N9 ...      BZE   0  PROC                [J IF 1904A  
   7 57P4 ...      SBN   0  4
  15 57PX ...      BZE   0  PROC                [J IF 1904S  
   7 57QQ ...      SBN   0  4
  16 57RK ...      BNZ   0  NPROC               [J IF NOT 1903T  
  17 57SD ...PROC  LDX   1  BINDEX              [PTR TO BSTB CCTABLE 
   9 57T? ...      LDCH  0  A1+6(1)  
   8 57W6 ...      SBN   0  25   
  16 57W_ ...      BNZ   0  NPROC               [J IF NOT ON HSD 
   9 57XS ...      LDN   0  A1B-A1   
   9 57YM ...      STO   0  BSTSPROC 
   5 57_G ...NPROC   
   4 582* ...)   
  14 583L          UP                        [THATS ALL  
   4 58H=    #   
  14 592W    #     DEFINE AN AREA OF 1 WORD PER CHAPTER  
   4 59GG    #   
  10 5=26 ...      SEGENTRY K96BRADDATA  
   6 5=FQ    XBRADDATA   
   7 5=_B    #DEF 19?+1=1?   
   5 5?F2    #LIS -1 
   6 5?YL ...#REP 1023-0?
   8 5#D=                   +0   
  10 5#XW          SEGENTRY K97BRADDATA  
   6 5*CG    #LIS 19?+1] 
   4 5*X6    #   
  14 5BBQ ...#    THE REMAINDER OF THE CHAPTER HAS BEEN  
  13 5BWB ...#    RESERVED FOR CHAPTERS FOR THE SCF  
   4 5CTL    #   
   4 5D*=    #END
   8 ____ ...70045332000100000000
