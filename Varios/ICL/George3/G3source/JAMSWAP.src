  15 22FL    #SEG  JAMSWAP                      [RUTH PORTER 
   8 22_=    #OPT  K0JAMSWAP=0   
  11 23DW    #LIS  K0JAMSWAP>K0ALLGEO>K0LLS  
   7 23YG          8HJAMSWAP 
   4 24D6    #   
  20 24XQ    #     THIS IS THE PART OF THE CORE UNJAMMER THAT SELECTS PROGRAMS TO
  21 25CB    #     BE SWAPPED OUT EITHER BECAUSE OBJECT QUOTA IS BEING EXCEEDED OR   
  21 25X2    #     THERE IS A SECOND OR GREATER ORDER GEORGE COREJAM OR A GEORGE FOUR
  12 26BL    #     SPECIAL SWAPOUT IS REQUIRED   
   4 26W=    #   
   4 27*W    #   
  11 27TG          SEGENTRY K1JAMSWAP,XX1J   
   4 28*6    #   
   9 28SQ    UNPL  LINK     UNPLUG,5 
   5 29#B    #SKI  G3
   4 29S2    (   
  14 2=?L    P13   PAIR  SWAP,2                 [SWAP OUT
  19 2=R=    P14   PAIR  SWAP,4                 [SWAP OUT FOR CORE UNJAMMER  
   4 2?=W    )   
   5 2?QG    #SKI  G4
   4 2#=6    (   
   9 2#PQ    P13   PAIR     SWAPOUT,1
   9 2*9B    P14   PAIR  SWAPOUT,2   
   4 2*P2    )   
  10 2B8L    T15            +CJTHRESH15  
  10 2BN=    T2             +CJTHRESH2   
  10 2C7W    T3             +CJTHRESH3   
  10 2CMG    T4             +CJTHRESH4   
   4 2D76    #   
  10 2DLQ    PROG  PROGAXES 3,(GEOERR)   
   7 2F6B          EXIT  4  0
   4 2FL2    #   
  15 2G5L    #     S/R TO CHECK PORIPHERALS,LINKX4,X3 ->PRB  
  15 2GK=    #     EXITS 0 : IF FAILED, 1 IF OK, 2 IF BUSY   
   4 2H4W    #   
   5 2HJG    PERCHECK
   8 2J46          LDXC  0  0(3) 
  16 2JHQ          BCS      PEND                [J IF END OF PRB 
   8 2K3B          LDXC  2  1(3) 
  15 2KH2          BCC      ONLINE              [J IF ONLINE 
   9 2L2L          ANDX  2  BITS22LS 
   9 2LG=          SBN   2  CHAINADD 
  15 2L_W          JCBB      2,XIT2             [EXIT IF BUSY
   7 2M74 ...#UNS  CAFSINT   
  19 2M#= ...      JCAFSBSY  2,XIT2             [EXIT IF BUSY CAFS CHANNEL   
   8 2MFG          LDX   0  0(3) 
   7 2M_6          SLC   0  9
   8 2NDQ          ANDN  0  #177 
   8 2NYB          SBN   0  #30  
   8 2PD2          BNZ   0  YSYA 
  16 2PXL          CONCISBY  2,TOUR              [J IF NOT BUSY  
   7 2QC=    XIT2  EXIT  4  2
   9 2QWW    YSYA  JAUTOFF  2,TOUR   
  15 2RBG          JAUTFAIL 2,(4)               [EXIT IF FAIL
   8 2RW6          BRN      XIT2 
   5 2S*Q    ONLINE  
   9 2STB          ANDX  2  BITS22LS 
   9 2T*2          JCANTENG 2,TOUR   
   9 2TSL          CALL  6  UNBUSY   
  13 2W#= ...      BRN      XIT2               [BUSY 
  14 2WRW ...      EXIT  4  0                  [FAILED   
  16 2X?G    TOUR  BDX   3  PERCHECK            [BACK FOR NEXT   
  13 2XR6    PEND  EXIT  4  1                   [OK  
  21 2Y=Q    #     ROUTINE TO JBUSY AN ONLINE PERIPHERAL.ENTERED WITH X2 -> DEVICE   
  21 2YQB ...#     LIST.X6=LINK.DOES JBUSY AND EXITS 0 IF BUSY.IF FAILED,EXIT 1 IS   
  12 2_=2    #     OBEYED.EXIT 2 IS OBEYED IF THE
  15 2_PL    #     PERIPHERAL IS NOT BUSY AND HAS NOT FAILED.
   4 329=    #   
   5 32NW    UNBUSY  
  16 338G ...      JBUSY    2,(6)              [EXIT 0 IF BUSY   
   8 33N6 ...      JSBUSY   2,(6)
   7 347Q ...      ADN   6  1
  15 34MB ...      JFAIL     2,(6)            [EXIT 1 IF FAIL
  14 3572          EXIT  6  1                   [2 IF OK 
   4 35LL    [   
  13 366=    [ ENTRY TO SELECT FOR SPECIAL SWAPOUT   
   4 36KW    [   
   4 375G    [   
   4 37K6    XX1J
  21 384Q          JBS      R11A,,ASWOUTING     [J IF SWAPOUT IN PROGRESS TO TERMINAT
  12 38JB          LDX   0  CJACCLINK+ALINK1-ACC3
  19 3942          BNZ   0  R11A                [SPEC SWAPOUT ALREADY GOING  
   6 395P ...#UNS  ISTDP 
   4 397D ...(   
  21 3997 ...[      IF TOTAL FREE PROGRAM CORE/PAGES > CUNJAMTARG,EXIT SUCCESSFULLY  
   4 39=W ...[   
   7 39#K ...      COBJUNUSE 
  10 39B# ...      SBN   0  CUNJAMTARG   
   8 39D3 ...      BPZ   0  R11AA
   4 39FQ ...)   
   8 39HL          STOZ     GEN3 
   8 3=3=          LDN   3  BJOBQ
  20 3=GW    TH5A  LDX   3  FPTR(3)             [GET ADDR OF NEXT IN JOB QUEUE   
  16 3?2G          BXE   3  CXJO,QEND           [J IF END REACHED
  21 3?G6          JMBS     TH5A,3,JBREEF,JBAXES,JBPLUGGING,JBSO,JBNSW    [IGNORE THE
  17 3?_Q          JBC      TH5A,3,JBSI         [J IF NOT SWAPPABLE  
  19 3#FB          JMBAC    SUSP,3,JBPLUG,JBSTOP    [J IF NORMALLY SWAPPABLE 
   8 3#_2          LDX   0  GEN3 
   8 3*DL          BNZ   0  TH5A 
   8 3*Y=          FCAJO    2,3  
   8 3BCW          CALL  5  TEST 
  15 3BXG          FPCACA   2                   [X2 -> PCA   
   8 3CC6          CALL  5  TEST 
  10 3CWQ          PCATEST  2,TH5C,TH5C  
  16 3DBB          FINDTP   2,2                 [X2 -> TP IF PUC 
   8 3DW2    TH5C  CALL  5  TEST 
  10 3F*L          LDX   2  ACTRING(2)   
   9 3FT=          SBN   2  ACTRING  
   9 3G#W          LDCH  0  ATYPE(2) 
   9 3GSG          SBN   0  AOLPT/64 
   8 3H#6          BNZ   0  TH5A 
   8 3HRQ          BRN      TH5C 
   9 3J?B    TEST  LDX   0  CLONG1(2)
   5 3JR2    #SKI  G3
  19 3K=L          BPZ   0  (5)                 [EXIT IF NOT WAITING FOR CORE
   5 3KQ=    #SKI  G4
   4 3L9W    (   
  18 3LPG          BNG   0  TH5B                [J IF WAITING FOR CORE   
  17 3M96          SLL   0  4                   [EXIT IF NOT WAITING 
  17 3MNQ          BPZ   0  (5)                 [ FOR PROGRAM CORE   
   4 3N8B    )   
   9 3NN2    TH5B  LDEX  0  ATYPE(2) 
  16 3P7L          BZE   0  TH5A                [ONLY IF ON QUEUE
  18 3PM=          JBC      TH5FA,3,JBPLUG       [J IF ALREADY UNPLUGGED 
   8 3Q6W          SMO      FX2  
  19 3QLG          STO   3  ACOMMUNE1           [REMEMBER JOB BLOCK ADDRESS  
   8 3R66          SMO      FX2  
  21 3RKQ          STO   2  ACOMMUNE2           [REMEMBER ADDR OF ACTIVITY BLOCK WAIT
   8 3S5B          FPCAJO   2,3  
  10 3SK2          PCATEST  2,RCTP1,RCTP1
  16 3T4L          FINDTP   2,2                 [X2 -> TP IF PUC 
   5 3TJ=    RCTP1   
  17 3W3W          LDX   3  APRB(2)             [YES, GET ADDR OF PRB
   9 3WHG          CALL  4  PERCHECK 
  14 3X36          BRN      TOUR                [FAILED  
  13 3XGQ          BRN      TH5F                [O.K.
   8 3Y2B    TH5AA SMO      FX2  
  19 3YG2          LDX   3  ACOMMUNE1           [RESTORE JOB BLOCK ADDRESS   
  19 3Y_L          BRN      TH5A                [LOOK AT NEXT JOB IN QUEUE   
   8 3_F=    TH5F  SMO      FX2  
   9 3_YW          LDX   3  ACOMMUNE1
   8 42DG          SMO      FX2  
   9 42Y6          LDX   2  ACOMMUNE2
   8 43CQ    TH5FA STO   3  GEN3 
   9 43XB          STO   2  CJACCLINK
   8 44C2          BRN      TH5A 
   8 44WL    QEND  LDX   3  GEN3 
  17 45B=          BZE   3  RETURN                [J IF NONE FOUND   
   9 45TW          LDX   2  CJACCLINK
  21 46*G          UNQUEUE                      [REMOVE WAITING ACTIVITY FROM QUEUE  
  21 46T6          LDN   0  1                   [ADJUST COUNT OF OUTSTANDING CORE REQ
   5 47#Q    #SKI  G3
   8 47SB          SBS   0  CFAIL
   5 48#2    #SKI  G4
   4 48RL    (   
   9 49?=          LDX   1  CLONG1(2)
   8 49QW          BPZ   1  XLAB1
   8 4==G          SBS   0  CFAIL
   8 4=Q6          BRN      XLAB2
   9 4?9Q    XLAB1 SBS   0  COBJFAIL 
  10 4?PB          LDX   0  ACOMMUNE2(2) 
  21 4#92          SRL   0  12                  [LOAD REQUEST(SEE GETPROGCORE MAC)   
  18 4#NL          SBS   0  AOBJFAIL            [ & REDUCE TOTAL OF FAILS
   5 4*8=    XLAB2   
   4 4*MW    )   
   8 4B7G          FPCAJO   2,3  
   8 4BM6          LDX   1  FX1  
   7 4BP* ...#UNS  ADPLLSKIP 
   4 4BRJ ...(   
  17 4BTR ...      LDN   0  1                    [- DATAPASS COUNT OF
  17 4BY2 ...      SMO      ADPSWPPTR            [- SPECIAL SWAPOUTS 
   7 4C29 ...      ADS   0  8
   4 4C4D ...)   
   8 4C6Q          CALL  4  PROG 
   9 4CLB          LDN   4  ACC3(2)  
  21 4D62          LDN   5  CJACCLINK           [DUMP ACCUMULATORS AND LINK OF PCA   
   8 4DKL          MOVE  4  ALINK
  10 4F5=          LDN   4  ACOMMUNE1(2) 
  11 4FJW          LDN   5  CJACCLINK+ALINK  
  17 4G4G          MOVE  4  9                   [AND ACOMMUNE WORDS  
   9 4GJ6          LDX   4  CLONG1(2)
  15 4H3Q          STO   4  CJACCLINK+ALINK+9   [SAVE CLONG1 
   9 4HHB          STOZ     CLONG1(2)
  17 4J32          JBC      STOPPED,3,JBPLUG    [J IF NOT PLUGGED IN 
  15 4JGL          MBI      3,JBPLUG,JBPISO     [MARK SPECIAL
   9 4K2=          LDN   1  UNPL(1)  
   8 4KFW          BRN      MEET 
   5 4K_G    STOPPED 
   9 4LF6          LDN   1  P14(1)   
  15 4LYQ          MBI      3,JBSTOP,JBSTSO     [MARK SPECIAL
   9 4MDB    MEET  LINKSET  2,0(1)   
  11 4MY2          TRACE    JOBNO(2),SPECOUT 
  18 4NCL          FPUT                         [LET PCA DO UNPLUG & SWAP
  17 4NX=          MBI      3,JBBEING,JBSI      [MARK BEING SWAPPED  
  18 4PBW          BS       ,ASWOUTING          [MARK SWAPOUT IN PROGRESS
  15 4PWG          BRN      R11A                [TERMINATE   
   4 4QB6    [   
  17 4QTQ    [  IF WE FIND NORMALLY SWAPPABLE,NOT PLUGGED IN ON WAY  
  11 4R*B    [     ROUND QUEUE,WE COME HERE  
   4 4RT2    [   
   4 4S#L    #   
   4 4SS=    #   
   4 4T?W    #   
   4 4TRG    SUSP
   8 4W?6          CALL  4  PROG 
   8 4WQQ          FPCAJO   2,3  
   8 4X=B          SMO     FX1   
   8 4XQ2          LDN  1  P13   
   7 4XSK ...#UNS  ADPLLSKIP 
   4 4XX8 ...(   
  17 4X_R ...      LDN   0  1                    [- DATAPASS COUNT   
  17 4Y4B ...      STO   0  ADPSWPSW             [- (COREJAM SWAPOUT)
   4 4Y6_ ...)   
   8 4Y9L          BRN     MEET  
   4 4YP=    #   
   7 4_8W    #     TERMINATE 
   4 4_NG    #   
   5 4_WQ ...R11AA   
   7 5252 ...#UNS  ISTDPSTATS
   9 52?= ...      TRACEDP AJAMFPC   
   5 52FG ...RETURN  
   2 52MQ
  10 537B    R11A  MBC ,CJNOSO,ASWCORJAM 
   7 53#J ...#UNS  ISTDPSTATS
   9 53FQ ...      TRACEDP AJAMSUC   
  14 53M2    [ NO NEED TO KICKASWINT AS END OF SWAPOUT   
  15 546L    [ WILL AND IF NO SWAPOUT THEN JAM NOT CLEARED   
  10 54L=    R11D  ACROSS   CHARGESC,1   
  11 555W          MENDAREA 50,K100JAMSWAP   
   4 55KG    #END
   6 ____ ...241044430002
