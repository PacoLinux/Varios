   9 22FL    #OPT           K0WLC = 0
   8 22_=    #LIS           K0WLC
   8 23DW    #SEG           WLC  
   6 23YG          8HWLC 
  15 248# ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   4 24D6    [   
  13 24XQ       SEGENTRY K1WLC,WLCK1   [ ACTION PHASE
  17 24__ ...   FSHENTRY K2WLC,WLCK2,,WLCK2      [NORMAL TIDYUP ENTRY
  19 2548 ...   FSHENTRY K3WLC,WLCK3,,WLCK3      [TIDYUP AFTER BREAKIN ENTRY 
  20 256C ...   FSHENTRY K4WLC,WLCK4,,WLCK4      [TAKE AWHATBIT SEMAPHORE ENTRY  
  21 258L ...   FSHENTRY K5WLC,WLCK5,,WLCK5      [RETURN AWHATBIT SEMAPHORE ENTRY 1  
  21 25=T ...   FSHENTRY K6WLC,WLCK6,,WLCK6      [RETURN AWHATBIT SEMAPHORE ENTRY 2  
  20 25*4 ...   FSHENTRY K7WLC,WLCK7,,WLCK7      [UNANTICIPATED CLOSEDOWN ENTRY  
   4 25CB    [   
   4 25X2    [   
  19 26BL    [************************************************************   
   4 26W=    [   
   7 27*W    [ PRESET DATA   
   4 27TG    [   
  19 28*6    [************************************************************   
   4 28SQ    [   
   4 29#B    [   
  18 29S2    PHEADING       +AWLIST  [ STANDARD ^USER.JOB ...^ HEADING   
  18 2=?L    PHEADNAR       +AWLIST1 [ NARROW ^DEVICE UR^-ONLY HEADING   
  10 2=R=    PCOUNTMESS1    +JLISTSUM1   
  10 2?=W    PCOUNTMESS2    +JLISTSUM2   
  10 2?QG    PCOUNTMESS3    +JLISTSUM3   
  15 2#=6    P10000             [ GENERATION NO. CONVERSION  
   6 2#PQ          +10000
  17 2*9B    PFILEDES           [ FILE DESCRIPTION FOR OUTPUT FILE   
   5 2*P2          10
   6 2B8L    POPERATORS  
  11 2BN=          12HOPERATORS [ USERNAME   
  11 2C7W          12HWHATLIST  [ FILENAME   
  11 2CMG          1            [ REEL NUMBER
  13 2D76          #20000001    [ GENERATION NUMBER  
  12 2DLQ          4HB1         [ LANGUAGE CODE  
  18 2F6B    PRUNJOB            [ RUNJOB COMMAND TO TO LIST OUTPUT FILE  
  14 2FL2          32HRJ WLIST0000,:OPERATORS,WLISTJDF   
  15 2G5L    PRJPROP            [ EXTRA FOR LIST(PR .....)   
   8 2GK=          9H,PARAM(PR   
   5 2H4W    PRJBRAC 
   6 2HJG          1H)   
  18 2J46    PLF                [ LISTFILE COMMAND FOR OUTPUT TO WORKFILE
  10 2JHQ          17HLF !,*LP,PA,MO#60  
  14 2K3B    PLFPROP            [ EXTRA FOR LIST(PR....) 
   6 2KH2          3H,PR 
   4 2L2L    [   
  19 2LG=    [************************************************************   
   4 2L_W    [   
   5 2MFG    SINSERT 
   4 2M_6    [   
  19 2NDQ    [************************************************************   
   4 2NYB    [   
  13 2PD2    [ INSERTS A MESSAGE INTO THE OUTPUT FILE
   4 2PXL    [   
  11 2QC=    [ LINK X7, MESSAGE IDENT IN X6  
   4 2QWW    [   
   8 2RBG    [ X0, X3 CORRUPTED  
   9 2RW6    [ X1 := FX1, X2 := FX2  
   4 2S*Q    [   
   7 2STB       SBX   7  FX1 
   7 2T*2       ASSMESS  6   
   9 2TSL       MHUNT    3,GMON,AOUT 
   9 2W#=       NAME     3,FILE,FWB  
   9 2WRW       LDEX  0  A1+FRH(3)   
   7 2X?G       ADN   0  11  
   7 2XR6       SRL   0  2   
  11 2Y=Q       DEX   0  A1+FRH(3)   [ LENGTH
  14 2YQB       INSERT       [ COUNT MESSAGE AS HEADING  
   8 2_=2       MFREE    FILE,FWB
   7 2_PL       ADX   7  FX1 
   7 329=       EXIT  7  0   
   4 32NW    [   
   4 338G    [   
  19 33N6    [************************************************************   
   4 347Q    [   
   5 34MB    SCLOSE  
   4 3572    [   
  19 35LL    [************************************************************   
   4 366=    [   
  15 36KW    [ CLOSE OUTPUT FILE ( AFTER  :SYSTEM.OUTPUT )   
   4 375G    [   
   7 37K6       SBX   7  FX1 
  11 384Q       MHUNT    2,ADATA,AWHATLIST   
  16 38JB       IF       MBS,2,AWLBLIST,AWLBHERE   [ OUTPUT FILE 
   5 3942       THEN 
  11 3=3=          MHUNTW   2,ADATA,AWHATLIST
  21 3=GW          IF       MBS,2,AWLBUSERCON,AWLBOPCOM   [ USER CONTEXT OR OP COMMAN
  11 3?2G          THEN    [ ERASE WORKFILE  
  12 3?G6             SETNCORE 10,3,FILE,FABSNB  
   8 3?_Q             LDN   0  10
   9 3#FB             STO   0  A1(3) 
   6 3#_2    #SKI   IFS  
  11 3*DL             ADN   2  FUSER1-FCBRING
   7 3*Y=    #SKI   IFS<1$1  
  11 3BCW             LDX   2  BWORKRING(2)  
   9 3BXG             ADN   2  FUSER1
   9 3CC6             ADN   3  A1+1  
   8 3CWQ             MOVE  2  9 
   8 3DBB             ERASEWORK 1
  11 3DW2             TESTREP2 NOFILE,ZERREW 
   8 3F*L          ELSF     TRUE 
  11 3FT=             LDX   0  AWLCOUNT(2)   
  11 3G#W             ADX   0  AWLCOUNTUN(2) 
  14 3GSG          AND      EITHER,0,ZE   [ NO LISTFILES 
  13 3H#6          OR       BS,2,AWLBHERE   [ HERE   
  14 3HRQ          THEN    [ ERASE :OPERATORS.WHATLIST   
  12 3J?B             SETNCORE 10,3,FILE,FABSNB  
  11 3JR2             LDN   1  PFILEDES(1)   
   9 3K=L             LDN   2  A1(3) 
   8 3KQ=             MOVE  1  10
   9 3L9W             SMO      FX2   
   9 3LPG             LDX   0  AWORK1
  14 3M96             STO   0  A1+8(3)   [ GENERATION NO.
   7 3MNQ             ERASE  
  11 3N8B             TESTREP2 NOFILE,ZERREOW
   5 3NN2          FI
   6 3NXS ...      CLOSE 
   5 3P7L       FI   
   7 3PM=       ADX   7  FX1 
   7 3Q6W       EXIT  7  0   
   4 3QLG    [   
   4 3R66    [   
   8 3RKQ    [ WW   WW       W   
   8 3S5B    [ WW  WW       WW   
   8 3SK2    [ WW WW       WWW   
   8 3T4L    [ WWWW         WW   
   8 3TJ=    [ WWWWW        WW   
   8 3W3W    [ WW  WW       WW   
   8 3WHG    [ WW   WW      WW   
   8 3X36    [ WW    WW     WW   
   4 3XGQ    [   
   4 3Y2B    [   
   5 3YG2    WLCK1   
   4 3Y_L    [   
   4 3_F=    [   
  19 3_YW    [************************************************************   
   4 42DG    [   
   8 42Y6    [ SET UP OUTPUT FILE
  10 43CQ    [ INTERFACE TO LF SCEDULER  
  14 43XB    [ LIST OUTPUT FILE OR  OUTPUT COUNT MESSAGE 
   6 44C2    [ TIDY UP   
   4 44WL    [   
  19 45B=    [************************************************************   
   4 45TW    [   
   4 46*G    [   
  11 46T6       MHUNT    2,ADATA,AWHATLIST   
   9 47#Q       STOZ     AWLCOUNT(2) 
  10 47SB       STOZ     AWLCOUNTUN(2)   
  12 48#2       IF       BS,2,AWLBFULL   [ FULL  
  11 48RL       THEN    [ OPEN OUTPUT FILE   
   7 49?=          CREATEB  4
  11 49QW          MHUNT    2,ADATA,AWHATLIST
  16 4==G          IF       BS,2,AWLBOPER   [ OPERATOR CONTEXT   
  17 4=Q6          THEN    [ CREATE AND OPEN :OPERATORS.WHATLIST(+1) 
  19 4?9Q             LDN   1  POPERATORS(1)   [ CREATION OF FILES REQUIRES  
   9 4?PB             SMO      FX2   
  20 4#92             LDN   2  CPREFIX   [ OWNERSHIP SO INSERT :OPERATORS IN CPAT
   8 4#NL             MOVE  1  3 
  12 4*8=             SETNCORE 10,3,FILE,FABSNB  
  11 4*MW             LDN   1  PFILEDES(1)   
   9 4B7G             LDN   2  A1(3) 
   8 4BM6             MOVE  1  10
  18 4C6Q             USEROPEN ZOUTFILEBRK,GENERAL,CREATE,LEAVE,FROZEN   
  10 4CLB             TESTRPN2 OK,ZERROOW
  10 4D62             MHUNT    3,FILE,ENT
  10 4DKL             LDX   0  EGEN(3)   
  10 4F5=             STO   0  AWORK1(2) 
   8 4FJW             FREECORE 3 
  15 4G4G          ELSE    [ OPEN WORKFILE AT TOP LEVEL FI   
  10 4GJ6             FRIGMONF ZERROW,1  
   8 4H3Q             LOBSFILE   
   5 4HHB          FI
  10 4J32          MFREE    FILE,FABSNB  
   5 4JGL       FI   
  11 4K2=       MHUNTW   2,ADATA,AWHATLIST   
  12 4KFW       IF       BS,2,AWLBWL   [ WHATLIST
   5 4K_G       THEN 
  18 4LF6          UNBUSACT OUT,RESTING   [ J IF LF SCHEDULER NOT ACTIVE 
   8 4LYQ          POINTACT OUT,3
  13 4MDB          LFBITSET W,3   [ SET WL WAITING   
  12 4MY2          LONGSET  AWLSLCHWAIT,(GEOERR) 
   9 4NCL          LONGSTOP ZSTOPBRK 
   5 4NX=    RESTING 
  12 4PBW          OPENSYS  ZSOUTBRK,OUTPUT,READ 
   8 4PWG          DOWN     WLD,1
  13 4QB6             BRN      ZWLDBRK   [ BREAKIN   
   5 4QTQ       ELSE 
  18 4R*B          UNBUSACT OUT,RESTIN   [ J IF LF SCHEDULER NOT ACTIVE  
   8 4RT2          SMO      FX2  
   9 4S#L          LDX   7  ACTNUM   
  13 4SS=          LONGSET  AWLSLCHWAIT,(GEOERR),7   
  10 4T?W          SETNCORE 1,3,GOUT,COMP
   8 4TRG          STO   7  A1(3)
   9 4W?6          LFCHAIN  GOUT,COMP
   8 4WQQ          STARTACT OUT  
   9 4X=B          LONGSTOP ZSTOPBRK 
   5 4XQ2    RESTIN  
  13 4Y9L          OPENSYS  ZSOUTBRK,OUTPUT,GENERAL  
  16 4YP=                            [ ************************  
  10 4_8W                            [   
  16 4_NG          DOWN     WLD,2    [ GO DOWN TO WLD FOR ACTION 
  10 5286                            [   
  16 52MQ                            [ ************************  
  13 537B             BRN      ZWLDBRK   [ BREAKIN   
   5 53M2       FI   
   8 546L       IF       REP2,OK 
   5 54L=       THEN 
   7 555W          CLOSEOUT  
   8 55KG          OUTBLOCN 20   
  20 5656          LDX   6  PCOUNTMESS1(1)   [ PRESET COUNT MESSAGE TO BE USED   
  11 56JQ          MHUNTW   2,ADATA,AWHATLIST
  14 574B          IF       +AWLCOUNT(2),NZ   [ LISTFILES
   6 57J2          THEN  
  11 583L             OUTNUM   AWLCOUNT(2),0 
   6 58H=          ELSE  
   9 592W             OUTMESS  EBPNO 
   5 59GG          FI
  11 5=26          MHUNTW   2,ADATA,AWHATLIST
  13 5=FQ          IF       BS,2,AWLBSL   [ STOPLIST 
   6 5=_B          THEN  
  10 5?F2             OUTMESS  JWLSTOP   
  11 5?YL             LDX   6  PCOUNTMESS2(1)
  14 5#D=          ELSF     BS,2,AWLBCH   [ CHANGELIST   
   6 5#XW          THEN  
  10 5*CG             OUTMESS  JWLCHANGE 
  11 5*X6             LDX   6  PCOUNTMESS2(1)
   5 5BBQ          FI
  11 5BWB          MHUNTW   2,ADATA,AWHATLIST
  18 5CB2          IF       +AWLCOUNTUN(2),NZ   [ LISTFILES NOT AFFECTED 
   6 5CTL          THEN  
  12 5D*=             OUTNUM   AWLCOUNTUN(2),0   
  11 5DSW             LDX   6  PCOUNTMESS3(1)
   5 5F#G          FI
  11 5FS6          HUNT     2,ADATA,CREADL   
  14 5G?Q          IF       2,PZ   [ SELECTION DETAILS   
   6 5GRB          THEN  
  14 5H?2             OUTPARAM A1(2),A1+2,ADATA,CREADL   
  11 5HQL             MFREE    ADATA,CREADL  
   5 5J==          FI
  11 5JPW          MHUNTW   2,ADATA,AWHATLIST
  10 5K9G          LDX   0  AWLCOUNT(2)  
  10 5KP6          ADX   0  AWLCOUNTUN(2)
  14 5L8Q          IF       0,ZE   [ NO LISTFILES FOUND  
   6 5LNB          THEN  
   8 5M82             MONOUTX  6 
   6 5MML          ELSE  
  16 5N7=             IF       BS,2,AWLBLIST   [ LIST FULL OUTPUT
   7 5NLW             THEN   
   8 5P6G                REWIND  
   7 5PL6                STEP
  10 5Q5Q                CALL  7  SINSERT
  18 5QKB                LDX   6  PHEADING(1)   [ ASSUME STANDARD HEADING
  13 5R52                MHUNTW   2,ADATA,AWHATLIST  
  17 5RJL                IF       BS,2,AWLBNARROW   [ NARROW FORMAT  
   7 5S4=                THEN
  19 5SHW                   DELETE   [ REPLACE BLANK LINE BY ^DEV UR^ HEADING
   8 5T3G                   STEP 
  12 5TH6                   LDX   6 PHEADNAR(1)  
   7 5W2Q                FI  
  20 5WGB                CALL  7  SINSERT   [ INSERT APPROPRIATE HEADING IN FILE 
  13 5X22                MHUNTW   2,ADATA,AWHATLIST  
   9 5XFL                STOZ     7  
  16 5X_=                IF       BS,2,AWLBLISTPR   [ LIST(PR...)
   7 5YDW                THEN
  12 5YYG                   MHUNT    2,CPB,CUNI  
  11 5_D6                   LDEX  7  ANUM(2) 
   7 5_XQ                FI  
  12 62CB                ADN   7  CPDATA-A1*4+51 
   9 62X2                SRL   7  2  
  13 63BL                SETUPCORE 7,3,ADATA,CREADL  
  13 63W=                MHUNTW   2,ADATA,AWHATLIST  
  17 64*W                IF       BS,2,AWLBOPER   [ OPERATOR CONTEXT 
  10 64TG                THEN    [ RUNJOB
  12 65*6                   LDN   5  PRUNJOB(1)  
  12 65SQ                   LDN   6  CPDATA(3)   
  10 66#B                   MVCH  5  32  
  18 66S2                   IF       BS,2,AWLBLISTPR   [ ,PARAM(PR...)   
   8 67?L                   THEN 
  13 67R=                      LDN   5  PRJPROP(1)   
  14 68=W                      MVCH  5  9   [,PARAM(PR   
  13 68QG                      MHUNT    2,CPB,CUNI   
  12 69=6                      LDN   5  APARA(2) 
  12 69PQ                      LDEX  2  ANUM(2)  
  16 6=9B                      MVCH  5  0(2)   [ PROPERTY STRING 
  13 6=P2                      LDN   5  PRJBRAC(1)   
  10 6?8L                      MVCH  5  1
   8 6?N=                   FI   
  10 6#7W                   LDX   2  FX2 
  12 6#MG                   LDX   4  AWORK1(2)   
  10 6*76                   LDN   5  0   
  12 6*LQ                   DVR   4  P10000(1)   
  10 6B6B                   LDX   4  5   
  10 6BL2                   LDCT  5  #200
  12 6C5L                   LDN   1  CPDATA+2(3) 
  10 6CK=                   LDN   0  4   
  16 6D4W                   DO           [ CONVERT GENERATION NO.
  11 6DJG                      CBD   4  0(1) 
  10 6F46                      BCHX  1  /
  10 6FHQ                   REPEAT   CT 0
  12 6G3B                   SBN   6  CPDATA(3)   
  10 6GH2                   SLC   6  2   
  11 6H2L                   STO   6  A1(3)   
  18 6HG=                   LDX   5  GSIGN   [ FOR SYSTEM ISSUED CONTEXT 
  12 6H_W                   ORS   5  CONTEXT(2)  
  14 6JFG                   ISSUCOMB XCOMERRJ,XCOMERRJ   
   5 6J_6    XCOMERRJ
  12 6KDQ                   ERS   5  CONTEXT(2)  
  11 6KYB                ELSE    [ LISTFILE  
  11 6LD2                   LDN   5  PLF(1)  
  12 6LXL                   LDN   6  CPDATA(3)   
  12 6MC=                   MVCH  5  17   [ LF   
  16 6MWW                   IF       BS,2,AWLBLISTPR   [ ,PR...  
   8 6NBG                   THEN 
  13 6NW6                      LDN   5  PLFPROP(1)   
  10 6P*Q                      MVCH  5  3
  13 6PTB                      MHUNT    2,CPB,CUNI   
  12 6Q*2                      LDN   5  APARA(2) 
  12 6QSL                      LDEX  2  ANUM(2)  
  16 6R#=                      MVCH  5  0(2)   [ PROPERTY STRING 
   8 6RRW                   FI   
  12 6S?G                   SBN   6  CPDATA(3)   
  10 6SR6                   SLC   6  2   
  13 6T=Q                   STO   6  A1(3)   [ LENGTH
  14 6TQB                   ISSUCOMB XCOMERLF,XCOMERLF   
   5 6W=2    XCOMERLF
   7 6WPL                FI  
  16 6X9=             ELSF     BS,2,AWLBHERE   [ FULL OUTPUT HERE
   7 6XNW             THEN   
  17 6Y8G                IF       BS,2,AWLBOPER   [ OPERATOR CONTEXT 
   7 6YN6                THEN
   5 6_7Q    XLOCK   
  11 6_MB                   [ IF CONSOLE FREE
  13 7272                   BSON     AWHATBIT,WAIT   
  13 72LL                   [ THEN LOCK OUT CONSOLE  
  11 736=                   ON       AWHATBIT
  11 73KW                   BRN      XLOCKED 
  14 745G                   [ ELSE WAIT,THEN TRY AGAIN   
   4 74K6    WAIT
  10 754Q                   COOR3    #114
  11 75JB                   BRN      XLOCK   
   8 7642                   [ FI 
   5 76HL    XLOCKED 
   7 773=                FI  
  19 77GW                LDN   5  0   [ X5 = 0(NORMAL HEADING) OR 1(NARROW)  
  13 782G                MONOUTX  6   [ COUNT MESSAGE
  19 78G6                LDX   6  PHEADING(1)   [ SET X6 TO HEADING MESSAGE  
  21 78_Q                                       [ DEPENDING ON WHETHER NARROW OR NOT 
  19 79FB                                       [ (RESETTING X5 IF NECESSARY)
  13 79_2                MHUNTW   2,ADATA,AWHATLIST  
  12 7=DL                IF       BS,2,AWLBNARROW
   7 7=Y=                THEN
  10 7?CW                   LDN   5  1   
  12 7?XG                   LDX   6  PHEADNAR(1) 
   7 7#C6                FI  
  14 7#WQ                MONOUTX  6   [ HEADING MESSAGE  
   4 7*BB    [   
   8 7*W2                REWIND  
  19 7B*L                IF       5,NZ   [ DON'T OUTPUT FIRST (BLANK) LINE IF
  21 7BT=                THEN            [ NARROW FORMAT - HEADING ACTS AS SEPARATOR 
   8 7C#W                   STEP 
   7 7CSG                FI  
  10 7D#6                WHILE    TRUE   
   8 7DRQ                   READ 
  12 7F?B                   MHUNT    3,FILE,FRB  
  12 7FR2                   LDX   0  ALOGLEN(3)  
  10 7G=L                   SBN   0  1   
  11 7GQ=                   ORX   0  A1(3)   
  15 7H9W                AND      0,NZ   [ NOT END OF FILE   
   7 7HPG                DO  
  12 7J96                   TESTBRKIN ZHEREBRK   
  12 7JNQ                   LDEX  6  A1+FRH(3)   
  10 7K8B                   SLL   6  2   
  10 7KN2                   SBN   6  8   
  17 7L7L                   OUTMON   6,A1+FRH+2,A1+FRH+1(3),FILE,FRB 
  11 7LM=                   MFREE    FILE,FRB
   8 7M6W                REPEAT  
  13 7MLG                MHUNTW   2,ADATA,AWHATLIST  
  17 7N66                IF       BS,2,AWLBOPER   [ OPERATOR CONTEXT 
   7 7NKQ                THEN
  16 7P5B                   OFF      AWHATBIT   [ RELEASE CONSOLE
  10 7PK2                   FON      #114
   7 7Q4L                FI  
  13 7QJ=             ELSE    [ OUTPUT COUNT MESSAGE 
   9 7R3W                MONOUTX  6  
   6 7RHG             FI 
   5 7S36          FI
   9 7SGQ          CALL  7  SCLOSE   
   6 7T2B          ENDCOM
   5 7TG2       ELSE 
   7 7T_L          CLOSEOUT  
   9 7WF=          CALL  7  SCLOSE   
   7 7WYW          SPARANOT 1
   9 7XDG          COMERR   HWSUSER  
   5 7XY6       FI   
   4 7YCQ    [   
  21 7YXB    [***********************************************************************
   4 7_C2    [   
   4 7_WL    [   
  21 82B=    [***********************************************************************
   4 82TW    [   
   6 83*G    [ BREAKIN   
   4 83T6    [   
  21 84#Q    [***********************************************************************
   4 84SB    [   
   4 85#2    [   
  21 85RL    [***********************************************************************
   4 86?=    [   
   4 86QW    [   
  14 87=G    ZWLDBRK            [ BREAKIN IN SEGMENT WLD 
   4 87Q6    [   
   9 889Q       IF       REP2,OPEN   
   5 88PB       THEN 
   7 8992          CLOSEOUT  
   5 89NL       FI   
   8 8=8=       CALL  7  SCLOSE  
   6 8=MW       ABANDCOM 
   4 8?7G    [   
  15 8?M6    ZHEREBRK           [ BREAKIN DURING HERE OUTPUT 
   4 8#6Q    [   
  11 8#LB       MHUNTW   2,ADATA,AWHATLIST   
  15 8*62       IF       BS,2,AWLBOPER   [ OPERATOR CONTEXT  
   5 8*KL       THEN 
  14 8B5=          OFF      AWHATBIT   [ RELEASE CONSOLE 
   8 8BJW          FON      #114 
   5 8C4G       FI   
   4 8CJ6    [   
  17 8D3Q    ZSTOPBRK           [ BREAKIN ON WAIT FOR :SYSTEM.OUTPUT 
   4 8DHB    [   
  17 8F32    ZSOUTBRK           [ BREAKIN ON OPENING :SYSTEM.OUTPUT  
   4 8FGL    [   
   8 8G2=       CALL  7  SCLOSE  
   4 8GFW    [   
  16 8G_G    ZOUTFILEBRK        [ BREAKIN ON OPENING OUTPUT FILE 
   4 8HF6    [   
   6 8HYQ       ABANDCOM 
   4 8JDB    [   
   4 8JY2    [   
  21 8KCL    [***********************************************************************
   4 8KX=    [   
   4 8LBW    [   
  21 8LWG    [***********************************************************************
   4 8MB6    [   
   6 8MTQ    [ GEOERRS   
   4 8N*B    [   
  21 8NT2    [***********************************************************************
   4 8P#L    [   
   4 8PS=    [   
  21 8Q?W    [***********************************************************************
   4 8QRG    [   
   4 8R?6    [   
   5 8RQQ    ZERROOW 
   5 8S=B    ZERROW  
   8 8SQ2       GEOERR   1,WLOPEN
   5 8T9L    ZERREOW 
   5 8TP=    ZERREW  
   9 8W8W       GEOERR   1,WLERASE   
   4 8WNG    [   
   4 8X86    #END
   6 ____ ...706744740001
