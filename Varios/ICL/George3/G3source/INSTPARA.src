  15 22FL    #LIS  K0INSTPARA>K0COMMAND>K0ALLGEO>K0GREATGEO  
  15 22_=    #SEG  INSTPARA82                   [OWEN N. JOHN
  15 2394 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  10 23DW                   8HINSTPARA   
   6 23L4 ...#UNS G400   
  13 23R= ...# THIS CHAPTER MODIFIED FOR G3PLUS-IH   
  11 23YG          SEGENTRY K1INSTPARA,XK1   
  11 24D6          SEGENTRY K2INSTPARA,XK2   
  13 24J3 ...      FSHENTRY K3INSTPARA,XK3,XK3,XK3   
  12 24MY ...      FSHENTRY K4INSTPARA,XK4,,XK4  
  12 24RT ...      FSHENTRY K5INSTPARA,XK5,,XK5  
  20 24XQ    [ THIS SEGMENT IMPLEMENTS THE INSTPARA COMMAND (SURPRISE!SURPRISE!) 
  21 25CB    [     THE FIRST PARAMETER IS COMPARED WITH THE TABLE OF NAMES IN FILE   
  21 25X2    [     :SYSTEM.PARAMINDEX. IF A MATCH IS ACHIEVED AND THERE IS 'NO SECOND
  21 26BL    [     PARAMETER,THE CURRENT VALUE OF THE INSTALLATION PARAMETER IS O/P  
  11 26W=    [     TO THE OPERATOR'S CONSOLE 
  19 27*W    [                         IF A 2ND PARAMETER IS SUPPLIED,AND IF 
  21 27TG    [     IT LIES BETWEEN THE LIMITS LAID DOWN IN THE TABLE,IT IS SUBSTITUTE
  21 28*6    [     FOR THE CURRENT VALUE,AND THE NEW VALUE IS O/P TO THE OPERATORS' C
  16 28SQ    [     TO CONFIRM THAT THE CHANGE HAS BEEN EFFECTED  
  20 29#B    [           THERE ARE TWO SPECIAL TYPES OF IP:- TRACE AND CONTEXT   
   4 29S2    #   
  11 29SP ...#UNS  IPEXTENSION+1*IPEXTENSION 
   7 29TD ...#DEF  XIPEXT=1  
  11 29W7 ...#UNS  IPEXTENSION+1*IPEXTENSION 
   4 29WW ...#SKI
   7 29XK ...#DEF  XIPEXT=0  
  20 2=?L    [     THE FOLLOWING IP'S VALUES ETC, CANNOT BE CONVERTED BY CHNUMCON
   8 2=R=    ZALL           4HALL
  17 2?=W    XJOB          +JOBTRACE-INSTPARAM      [PARAM NUMBERS   
  17 2?QG                  +IMINTRACE-INSTPARAM     [OF TRACE TYPE   
  14 2#=6                  +IMOPTRACE-INSTPARAM     [I-PS
  12 2#PQ    XOPTR          +IOPTRACE-INSTPARAM  
  10 2#Y2 ...XCON  +ICONTEXT-INSTPARAM   
  19 2*6= ...XFORCE                           [THIS IS FOR RU120-OPTRACE BUG 
   9 2*#G ...      CATMASK  FORCEPRI 
   4 2*GQ ...[   
  10 2*P2    PJCONA         +JCONTEXTA   
  10 2B8L                   +JCONTEXTB   
  10 2BN=                   +JCONTEXTC   
   2 2C7W
  18 2CMG    XBKJM          +BSJAM - INSTPARAM      [REL ADDR OF  BACKJAM
  18 2D76    XJOBL          +JOBLIMIT - INSTPARAM   [REL ADDR OF JOBLIMIT
  18 2DLQ    XBACJ          +BACKJOBS - INSTPARAM   [REL ADDR OF BACKJOBS
  19 2F6B    XBACG          +BACKGROUND - INSTPARAM [REL ADDR OF BACKGROUND  
  18 2FL2    XMOPJ          +IMOPJOBS - INSTPARAM   [REL ADDR OF MOPJOBS 
  18 2G5L    XMOPL          +IMOPLIMIT - INSTPARAM  [REL ADDR OF MOPLIMIT
  18 2G*D ...XMOPT        +IMOPTOTAL-INSTPARAM     [REL ADD OF MOPTOTAL  
   8 2GK=    UNS            +5   
   9 2H4W    UNSET          5HUNSET  
   5 2HJG    MISTAKE 
  15 2J46          SBX   3  FX1                 [RELATIVISE  
   7 2JHQ          CLOSETOP  
  15 2K3B          ADX   3  FX1                 [RESET PTR   
   7 2KH2    ODEAR NGN   7  1
   9 2L2L    OUTER COMERRX  0(3),7   
   5 2LG=    OUTSIDE 
  15 2L_W          BZE   4  OUTS                [J IF COMMAND
   9 2MFG          GEOERR   1,OUTSIDE
   4 2M_6    OUTS
   9 2NDQ          CALL  3  MISTAKE  
   8 2NYB          +INSTPAROUT   
   5 2PD2    NULLABS 
   8 2PXL          BNG   5  PABS 
   8 2QC=    PNULL CALL  3  ODEAR
   7 2QWW          +JNAMISS  
   8 2RBG    PABS  CALL  3  ODEAR
   7 2RW6          +JPARMIS  
   5 2S*Q    MISTAKE1
  15 2STB          LDX   7  1(3)                [SUBMESSAGE  
   8 2T*2          SBX   3  FX1  
   7 2TSL          CLOSETOP  
   8 2W#=          ADX   3  FX1  
   8 2WRW          BRN      OUTER
   4 2X?G    #   
   9 2XR6    [     ENTRY FOR MACRO   
   4 2Y=Q    #   
  16 2YQB    XK2                                [K2 MACRO ENTRY  
   7 2_=2          LDN   4  1
  20 2_PL          SETNCORE 5,3,CPB,CUNI        [STORE IP NAME IN A CUNI BLOCK   
  18 329=          LDN   0  12                  [AND IN THE AWORK WORDS  
   9 32NW          STO   0  ANUM(3)  
   9 338G          LDN   6  ACOM2(2) 
   9 33N6          LDN   7  APARA(3) 
   7 347Q          MOVE  6  3
   9 34MB          LDN   7  AWORK2(2)
   7 3572          MOVE  6  3
  10 35LL          LDX   5  ACOMMUNE1(2) 
   9 366=          STO   5  AWORK1(2)
   8 36KW          BRN   XJOIN   
   4 375G    #   
   9 37K6    [     ENTRY FOR COMMAND 
   4 384Q    #   
  16 38JB    XK1                                [K1 COMMAND ENTRY
  16 3942          SPARAPAS                     [PASS 1ST PARAM  
  20 39HL          HUNT2    3,CPB,CUNI,2        [GET POINTER TO CPB/CUNI BLOCK.  
  21 3=3=          LDX   5  ANUM(3)             [EXAMINE 'TYPE/NO.OF CHARACTERS'-WORD
   8 3=GW          BNG   5  PABS 
   8 3?2G          BZE   5  PNULL
   9 3?G6          LDX   0  APARA(3) 
  14 3?_Q          LDCT  4  #400                [MARKER  
   9 3#FB          SETMODE  3,READ   
   9 3#_2          TXU   0  ZALL(1)  
  16 3*DL          BCC      XALL                [IS IT ALL CASE  
   7 3*Y=          LDN   4  0
  10 3BCW          CHECKLFN NULLABS,REN2 
  10 3BXG          MHUNT    1,CPB,CUNI   
   8 3CC6    TAB   LDX   0  ACES 
  14 3CWQ          STO   0  AWORK2(2)           [SPACE   
  15 3DBB          STO   0  AWORK3(2)           [FILL AWORK  
  14 3DW2          STO   0  AWORK4(2)           [WORDS   
   8 3F*L          ANDN  5  #7777
   9 3FT=          LDN   6  APARA(1) 
  17 3G#W          LDN   7  AWORK2(2)           [MOVE IN PARAMETER   
   7 3GSG          SMO      5
   7 3H#6          MVCH  6  0
   5 3HRQ    XJOIN   
   9 3J?B          SETMODE  3,CLEAN  
   4 3JR2    XALL
  14 3K8X ...[ USE SUPPLEMENTARY MODE 'QUERY' TO PREVENT 
  16 3K9T ...[ GEOERR IF FILE ALREADY OPEN TO JOB - B9083 & B2034
   6 3K=R ...[ 11.02.82  
   9 3K?P ...      ADDMODE 3,QUERY   
  12 3K#M ...      OPENSYSX XBREAK,PARAMINDEX,3  
   6 3K*K ...[ TEST REPLY
   9 3KBH ...      TESTREP OK,XOPENOK
  17 3KCF ...      TESTREP CLUDGE,XOPENERR     [ FILE ALREADY OPEN   
  16 3KDC ...      GEOERR 1,IPNOFILE           [ UNEXPECTED REPLY
   5 3KF* ...XOPENERR
  13 3KK5 ...      BZE   4  XCMND      [ J IF COMMAND
  14 3KL3 ...      GEOERR 1,JSYSPAR    [ FAIL IF MACRO   
  15 3KL_ ...XCMND CALL  3  ODEAR      [ REPORT COMMAND ERROR
   9 3KMX ...               +JSYSFILE
   5 3KNT ...XOPENOK 
  20 3KQ=    [     TO PREVENT A NASTY MESS IF GEORGE BREAKS WHILE FILE OPEN IN   
   8 3L9W    [     GENERAL MODE  
  16 3LPG          BNG   4  XLIST               [J IF 'ALL' CASE 
  15 3M96          STEP                         [READ RECORD 
   8 3MB# ...      BNZ   3  XOK  
  18 3MHG ...[     WE CAN BRANCH TO THIS GEOERR FROM APPROX ADDRESS 580  
  14 3MNN ...[     IF THIS ROUTE IS TAKEN X7=0,ELSE X3=0 
  19 3MTW ...XEMP  GEOERR  1,PARAEMPT             [IF PARAMINDEX EMPTY-GEOERR
   4 3MTY ...#   
  20 3MW2 ...XLIST ACROSS  IPALL,1              ['IP ALL' COMMAND DONE IN IPALL  
   4 3MW4 ...#   
   6 3MW8 ...#SKI  XIPEXT
   4 3MWG ...(   
   4 3MWS ...XOK 
   5 3MX6 ...MLOOP2  
  16 3MXD ...      STEP                         [READ NEXT RECORD
  18 3MXQ ...      BZE   3  NFOUND              [J IF REACHED END OF FILE
  16 3MY4 ...      SBN   3  IPNAME-IPHEAD-1     [PTR TO IPHEAD   
  15 3MYB ...      LDX   0  IPNAME-A1(3)        [NAME OF IP  
  17 3MYN ...      BXU   0  AWORK2(2),XOK       [J IF NOT REQUIRED IP
   7 3M_2 ...      NGS   3  1
  15 3M_# ...      BRN      MATCH               [FOUND IT!   
   5 3M_L ...NFOUND  
   4 3M_Y ...)   
   7 3N2= ...#SKI  XIPEXT<1$1
   4 3N2J ...(   
   7 3N34 ...XOK   NGS   3  1
  15 3N8B          LDEX  6  FRH(3)              [REC HEADER  
  14 3NN2          STOZ     5                   [CLEAR X 
   9 3P7L          LDN   7  IPLINE   
  18 3PM=          DVR   5  7                   [RESULT=LOOP COUNT IN X6 
   5 3Q6W    MLOOP   
  10 3QLG          LDX   0  IPNAME-A1(3) 
   9 3R66    #SKIP   K6INSTPARA>99-99
   9 3RKQ ...      TRACE    0,TABLE  
   9 3S5B          TXU   0  AWORK2(2)
  18 3SK2          BCC      MATCH               [1ST 4 CHARACTERS MATCH  
   5 3T4L    MLOOP2  
   9 3TJ=          ADN   3  IPLINE   
   8 3W3W          BCT   6  MLOOP
   4 3W?N ...)   
   9 3WHG    #SKIP   K6INSTPARA>99-99
   9 3WPQ ...      TRACE  3,NO MATCH 
  15 3XGQ          BZE   4  UNKN                [J IF COMMAND
  10 3Y2B          GEOERR   1,IPUNKNWN   
   4 3YG2    UNKN
   9 3Y_L          CALL  3  MISTAKE  
   7 3_F=          +IPUNKNOWN
  15 3_YW                                       [ NOT EXIST  
   5 42DG    MATCH   
  11 42Y6          LDX   0  IPNAME+1-A1(3)   
  11 43CQ          LDX   5  IPNAME+2-A1(3)   
   9 43XB          TXU   0  AWORK3(2)
   9 44C2          TXU   5  AWORK4(2)
   9 44WL          BCS      MLOOP2   
   9 45B=    #SKI  K6INSTPARA>99-99  
  11 45TW ...      TRACE AWORK2(2),MATCHED   
   7 46*G          ADS   3  1
  10 46T6          LDX   6  IPMAX-A1(3)  
  10 47#Q          LDX   7  IPMIN-A1(3)  
   7 47JJ ...#SKI  XIPEXT<1$1
  10 47SB          LDX   3  IPNUM-A1(3)  
  18 48#2                                       [WORD 0.I.E. INSTPARAM.  
  15 48RL                                       [INSTPARAM   
   6 48T8 ...#SKI  XIPEXT
   4 48WQ ...(   
  10 48Y# ...      LDXC  3  IPNUM-A1(3)  
  19 48_W ...      BCC      OLDIP               [J IF NOT IN EXTENSION TABLE 
  21 493D ...      BXGE  3  IPEXTCOUNT,OUTSIDE  [J IF NOT IN RANGE OF EXTENSION TABLE
  19 4952 ...      ORX   3  GSIGN               [RESET EXTENSION TABLE BIT   
   8 496J ...      BRN      NUMOK
   5 4986 ...OLDIP   
   4 499N ...)   
   9 49?=          LDN   0  INSTPARL 
   7 49QW          TXL   3  0
  20 4==G          BCC      OUTSIDE             [CHECK WITHIN FIXED CORE TABLE   
   5 4=G# ...NUMOK   
   9 4=Q6          STO   1  AWORK2(2)
  19 4=SG ...                                   [INSTALLATION PARAMETER AREA.
   4 4=WW ...[   
  21 4=_= ...[        THE FOLLOWING TWO(2) LINES ARE INSERTED DUE TO G5467(B 0969)   
  18 4?3L ...[         THIS IS DUE TO THE FACT THAT IF WE ARE OBEYING THE
  18 4?62 ...[         INSTPARA MACRO ENTRY WE MUST -NOT- LOOK FOR THE   
  16 4?8B ...[          'UNSET' PARAMETER AS WE WOULD READ OVER  
  13 4?=Q ...[         THE FOLLOWING ACTIVITY !!!!!  
  16 4?*6 ...[               DONE  17.SEPT.74           (:CENT)  
   4 4?CG ...[   
  16 4?FW ...     STO    4   AWORK3(2)           [STORE INDICATOR
  20 4?J= ...      BNZ    4   PARA2               [JUMP IF THIS IS MACRO ENTRY   
   4 4?LL ...[   
   4 4?PB    [   
  15 4#92    [   CHECK IF ONE OF THE PARAMETERS IS -- UNSET  
   4 4#NL    [   
   8 4*8=          LDX   1  FX1  
   9 4*MW          PARABEG  1,UNS(1) 
  10 4B7G          MHUNT    1,CPB,CUNI   
  10 4C6Q          LDX   4  JPARNUM(1)   
   9 4CLB          LDX   5  ANUM(1)  
   7 4D62          FREECORE 1
  16 4DKL          BNG   5  PARA2               [J IF NO UNSET   
  16 4F5=          BZE   5  YNST                [J IF JUST UNSET 
   4 4FJW    [   
  14 4G4G    [    UNSET EXISTS BUT HAS INCORRECT FORMAT  
   4 4GJ6    [   
  16 4H3Q    [  MOVE THIS BELOW +APFERR WHEN WE MIND A MESSAGE   
   4 4HHB    YERR
  14 4J32          PARANOTX  4                   [RE-READ
  16 4JGL          CALL  3  MISTAKE              [SO NOW  COMMERX
   8 4K2=                +APFERR 
  21 4KFW    YNST  LDN   0  2                   [CHECK THAT UNSET IS 2ND PARAMETER   
   9 4K_G          BXL   0  4,YERR   
  19 4LF6          LDX   4  GSIGN               [LOAD UNSET INDICATOR VALUE  
   9 4LYQ          STOZ     AWORK3(2)
   8 4MDB          BRN      STORE
   4 4MY2    #   
   8 4NCL          [2ND PARAMETER
   4 4NX=    #   
   5 4PBW    PARA2   
   9 4PWG          LDX   4  AWORK3(2)
  18 4QB6          LDN   1  XCON-XJOB           [NR OF TRACE-TYPE IPS.   
   8 4QTQ    RJH   ADX   1  FX1  
   9 4R*B          TXU   3  XJOB-1(1)
  17 4RT2          BCC      TRACE               [J IF TRACE TYPE IP  
   8 4S#L          SBX   1  FX1  
   8 4SS=          BCT   1  RJH  
   8 4T?W          LDX   1  FX1  
   9 4TRG          TXU   3  XCON(1)  
  15 4W?6          BCC      TEXT                [J IF CONTEXT
  19 4WQQ          LDN   5  0                   [CLEAR MARKER-NOT TRACE TYPE 
  15 4X=B          BZE   4  XCOM                [J IF COMMAND
   9 4XQ2          LDX   4  AWORK1(2)
   7 4Y9L          BRN   OK1 
   4 4YP=    XCOM
   7 4_8W          PARAPASS  
  21 4_NG          HUNT2    2,CPB,CUNI          [GET A POINTER TO THE CPB/CUNI BLOCK.
  21 5286          LDX   0  ANUM(2)             [TEST WORD CONTAINING: TYPE/NR OF CHA
  21 52MQ          BNG   0  REQUEST             [NO SECOND PARAMETER=REQUEST FOR CURR
   8 537B          BNZ   0  THERE
  18 53M2          CLOSE                        [IF WILL CLOSE PARAMINDEX
  15 546L          BRN      WOE                 [AND COMERR  
   5 54L=    THERE   
  19 555W                                       [VALUE OF NAMED PARAMETER.   
  16 55KG          CHNUMCOD   ,2                [CONVERT NUMBER  
   9 5656          TESTREPN OK,WOOPS 
   9 56JQ    OK    MFREE    CPB,CUNI 
  10 574B          LDX   4  ACOMMUNE1(2) 
   4 57J2    OK1 
  16 583L          TXL   4  7                   [J.IF PARAM<MIN  
   9 58H=          BCS      OUTRANGE 
  16 592W          TXL   6  4                   [J.IF PARAM>MAX  
   9 59GG          BCS      OUTRANGE 
   5 59JW ...STORE   
   4 59M= ...[   
  19 59PL ...[        THE FOLOWING 'LDX' WAS INSERTED DUE TO G5465(B 0968)   
  17 59S2 ...[    THE BUG IS A RESULT OF JOBWELL NOT RUNNING JOBS !!!
  19 59WB ...[     CAUSED BY THE FACT THAT PREVIOSLY INSTPARA DID A RUNJSA   
  20 59YQ ...[     ONLY IF THE NEW VALUE(VIA IP) WAS GREATER THAN OLD FILE VALUE 
  19 5=36 ...[     THIS WAS WRONG !! WE MUST COMPARE NEW VALUE WITH CURRENT  
  19 5=5G ...[     FIXED CORE VALUE....(THE LINE LOADING X7 WITH FILE VALUE  
  16 5=7W ...[                               HAS BEEN DELETED)   
  14 5=== ...[           DONE    26.SEP.74         :CENT 
   4 5=#L ...[   
   6 5=#X ...#SKI  XIPEXT
   4 5=*8 ...(   
  18 5=*F ...      IPUSEX  LDX   7,3            [LOAD CURRENT FIXED VALUE
  16 5=*Q ...      IPUSEX  STO   4,3            [STORE NEW VALUE 
   4 5=B3 ...)   
   7 5=B# ...#SKI  XIPEXT<1$1
   4 5=BK ...(   
  18 5=C2 ...      LDX   7  INSTPARAM(3)        [LOAD CURRENT FIXED VALUE
  10 5=FQ          STO   4  INSTPARAM(3) 
   4 5=PJ ...)   
  11 5=_B    #SKI           K6INSTPARA>99-99 
   9 5?3K ...      TRACE    4,STORED 
  19 5?YL          LDX   6  3                   [SAVE X3 : PTR TO IP IN FIXED
   7 5#D=          STEPAGAIN 
   8 5#XW          STEPREWRITE   
   2 5*CG
  18 5*X6          ADX   3  AWORK2(2)           [LOCATION IN PARAMINDEX  
   6 5B9H ...#SKI  XIPEXT
  21 5BHY ...      SBN   3  1                   [GET PTR CORRECT FOR EXTENSION VERSN 
  15 5BWB          STO   4  IPVAL-A1(3)         [CHANGE VALUE
   7 5CB2          LDX   3  6
  19 5CTL          BXE   4  7,NOTPRE            [IF NO CHANGE NOTHING TO DO  
   9 5D*=          SETBIT   2,HLSWAIT
   4 5DQS ...XK4 
  21 5DSW          FON      #33                 [KICK HLS - TO CHECK B2 OF HLSWAIT   
   6 5F24 ...#UNS G400   
  16 5F7= ...      LONGON G400IPWT                [KICK WT ON IP 
  17 5F#G          BXE   3  XJOBL(1),YES           [IS IT JOBLIMIT   
  20 5FS6 ...      BXU   3  XMOPT(1),RUNS           [IF IT IS NOT BACKJOB--JUMP  
  20 5GDY ...YES   LONGON   #44                 [GENERAL KIICK-- WAKE UP FOLKS   
   8 5GXG ...      BRN      YNO  
  11 5HB4 ...RUNS  BXE   3  XMOPJ(1),RUNDO   
  10 5HSL ...      BXU   3  XBACJ(1),YNO 
   5 5J?8 ...RUNDO   
  21 5JPW         RUNJSA                        [KICK ANY JOBS WAITING ON BACK MOP JO
   4 5JX# ...YNO 
  19 5KCL ...      BXU   3  XBACG(1),YNCH          [JUMP IF NOT BACKGROUND   
   4 5KK8 ...[   
  19 5KR3 ...[       THE FOLLOWING TWO 'FON'S HAVE BEEN ALTERED TO CURE B2368
  18 5KYW ...[      CAUSED BY ME GETTING #31,#32 THE WRONG WAY ROUND !!  
  13 5L6P ...[          DONE  26 MARCH  75      :CENT
   4 5L#J ...[   
   5 5LCF ...WAKEUP32
  21 5LGC ...      FON      #32                 [WAKE UP JOBS WAITING ON BACKGROUND  
   9 5LNB          BRN      NOTPRE   
   4 5LTJ ...YNCH
  19 5M*6 ...      BXU   3  XMOPL(1),NOTPRE        [PREHAPS MOPLIMIT  ????   
   5 5MG# ...WAKEUP31
  20 5MML ...      FON      #31                 [WAKEY-WAKEY OMPLIMIT TYPE JOBS  
   5 5N7=    NOTPRE  
   9 5NLW          BRN      VALUENOW 
   4 5P6G    #   
  19 5PL6    [     THIS  SECTION CHECKS A TRACE TYPE IP,USING TANAMONST MACRO
   4 5Q5Q    #   
   5 5Q8W ...TRACE   
  18 5Q_W ...      NGN   5  1                   [SET TRACE TYPE MARKER   
  15 5R52          BZE   4  TRAC1               [J IF COMMAND
   9 5RJL          LDX   4  AWORK1(2)
   9 5S4=          BRN   TEXTRANGE   
   5 5SHW    TRAC1   
   8 5T3G          PARANUMB  4   
   7 5TH6          SBN   4  1
  20 5W2Q          BZE   4  VALUENOW            [J IF ONLY 1 PARAM = REQUEST FOR 
  16 5WGB          TANAMONST WOOPS              [CURRENT VALUE   
  10 5X22          LDX   4  ACOMMUNE1(2) 
   6 5X49 ...TEXTRANGE   
   4 5X6D ...[   
  19 5X8M ...[       THE FOLLOWING LINES OF CODE WERE INSERTED DUE TO RU120  
  16 5X=W ...[        WHICH MEANT THAT OPTRACE WAS UNALTERABLE   
  18 5X*5 ...[        THIS WAS BECAUSE TANAMONST ADDED THE FORCEPRI BIT  
  18 5XC# ...[       AND OPTRACE IS NOT ALLOWED THE FORCEPRI BIT- SO THE 
  18 5XFH ...[       FOLLOWING LINES REMOVE FORCEPRI BIT IF IP IS OPTRACE
  12 5XHQ ...[  DONE  16.JAN.75           :CENT  
   4 5XK_ ...[   
  11 5XN8 ...      BXU   3  XOPTR(1),XNOTOPTR
  16 5XQC ...      ERX   4  XFORCE(1)       [REMOVE FORCEPRI BIT 
   5 5XSL ...XNOTOPTR
   4 5XWT ...[   
   7 5X_=          LDX   0  4
   7 5YDW          ERX   0  6
   7 5YYG          ANDX  0  4
  15 5_D6          BNZ   0  OUTRANGE            [J IF >MAX   
   7 5_XQ          LDX   0  4
   7 62CB          ERX   0  7
   7 62X2          ANDX  0  7
  15 63BL          BNZ   0  OUTRANGE            [J IF <MIN   
   8 63W=          BRN      STORE
  18 64*W    [              IF CONTEXT THEN OTHER PARAMETERS ARE A,B OR C
  15 64TG    [     S/R TO CONVERT AND O/P CONTEXT TYPE PARS  
   5 65*6    TEXTCON 
   8 65SQ          SBX   6  FX1  
  17 66#B          IPCONT   7,3                 [CONVERT PARAMETER   
  10 66S2          OUTPARAM 3,ACOMMUNE1  
   8 67?L          ADX   6  FX1  
   7 67R=          EXIT  6  0
   4 68=W    #   
  20 68QG    [     THIS  SECTION CHECKS A CONTEXT PARAMETER AND IF VALID JOINS   
  15 69=6    [     AT TEXTRANGE LABEL TO CHECK WITHIN RANGE  
   4 69PQ    #   
   4 6=9B    TEXT
   7 6=P2          LDN   5  1
  15 6?8L          BZE   4  XTEX1               [J IF COMMAND
   9 6?N=          LDX   4  AWORK1(2)
   9 6#7W          BRN   TEXTRANGE   
   5 6#MG    XTEX1   
   9 6*76          STO   7  AWORK1(2)
   7 6*LQ          PARANUMB 7
   7 6B6B          SBN   7  1
  18 6BL2          BZE   7  VALUENOW            [J IF NO OTHER PARAMETERS
   7 6C5L    T4    PARAPASS  
  10 6CK=          MHUNT    2,CPB,CUNI   
   7 6D4W          LDN   5  3
   4 6D8* ...T2  
   9 6DYL ...      LDCH  0  APARA(2) 
   8 6F46          BZE   0  TT1  
   7 6FHQ          SMO      5
   8 6G3B          SBN   0  #40  
  15 6GH2          BZE   0  T3                  [J IF MATCH  
   8 6H2L          BCT   5  T2   
   9 6HG=          CALL  3  MISTAKE  
   7 6H_W          +APFERR   
   7 6JFG    T3    SMO      5
  16 6J_6          ORX   4  PJCONA-1(1)         [OR IN NEW VALUE 
   4 6KDQ    TT1 
   7 6KYB          FREECORE 2
  17 6LD2          BCT   7  T4                  [COUNT OF PARAMETERS 
   9 6LXL          LDX   7  AWORK1(2)
   9 6MC=          BRN      TEXTRANGE
   5 6MWW    REQUEST 
  21 6NBG    [NO SECOND PARAMETER IS INTERPRETED AS A REQUEST THAT THE CURRENT VALUE 
  20 6NW6    [NAMED INSTALLATION PARAMETER BE OUTPUT TO THE OPERATORS' CONSOLE.  
  14 6P*Q    [FORMAT:-INSTALLATION PARAMETER  'FRED'=999 
  21 6PTB          FREECORD 2                   [FREES THE CPB/CUNI BLOCK BUT DOES NO
  21 6Q*2                                       [UP OTHER ACTIVITIES WAITING FOR CORE
   5 6QSL    VALUENOW
   7 6R#=          CLOSETOP  
   9 6RRW          TXU   3  XBKJM(1) 
   9 6S?G          BCS      XNOTBKJM 
   4 6SP4 ...XK5 
   7 6SR6          IPBACKJAM 
   5 6T=Q    XNOTBKJM
   7 6TQB          LDN   4  7
   4 6W=2    OPT 
  21 6WPL          OUTBLOCK 4                   [ET UP A BLOCK OF CORE FOR O/P PARAM.
  21 6X9=          [WHERE X4=TOTAL NO.OF WORDS OCCUPIED BY THE PARAMS. OF THE BASIC M
  13 6XNW          [PLUS ONE WORD FOR EACH PARAMETER.
   9 6Y8G          MHUNT 2,CPB,CUNI  
   9 6YN6          LDEX  4  ANUM(2)  
  16 6_7Q          OUTPARAM 4,APARA,CPB,CUNI    [OUTPUT PAR NAME 
   9 6_MB          MFREE    CPB,CUNI 
   6 723Q ...#SKI  XIPEXT
  16 724R ...      IPUSEX   LDX   4,3           [LOAD VALUE OF IP
   7 725S ...#SKI  XIPEXT<1$1
  10 7272          LDX   4  INSTPARAM(3) 
  15 72LL          CALL  7  SUNSET              [IS IP  UNSET
   8 736=          BRN      OUT  
  16 73KW          BPZ   5  NUMB                [J IF NOT TRACE  
   6 73PR ...#SKI  XIPEXT
  16 73TN ...      IPUSEX   LDX   0,3           [LOAD VALUE OF IP
   7 73_K ...#SKI  XIPEXT<1$1
  10 745G          LDX   0  INSTPARAM(3) 
   8 74K6          CALL  6  SYNTH
   8 754Q          BRN      OUT  
   4 75JB    NUMB
  15 7642          BNZ   5  T6                  [J IF CONTEXT
   4 76HL    XNUM
   6 76KT ...#SKI  XIPEXT
   4 76N4 ...(   
   9 76Q? ...      IPUSEX   LDX   7,3
  21 76SG ...      OUTNUM   7,0                 [3RD PARAM OF MESSAGE IPRM IS THE CUR
   4 76WP ...)   
   8 76YY ...#SKI    XIPEXT<1$1  
  21 773=          OUTNUM   INSTPARAM(3),0      [2ND PARAM OF MESSAGE IPRM IS THE CUR
   4 77GW    OUT 
  21 782G          MONOUT   IPRM                [O/P  RESULTING MESSAGE TO THE OP.S' 
   4 78G6    REND
   9 78_Q          LDX   0  AWORK3(2)
   8 79FB          BZE   0  REN2 
  18 79_2          UPPLUS   2                   [SUCCESSFUL END OF MACRO 
   4 7=DL    REN2
   6 7=Y=          ENDCOM
   5 7?CW    XBREAK  
   8 7?XG          BZE   4  XGEO 
   8 7#C6          BNG   4  XGEO 
   4 7#FP ...[   
  20 7#J# ...[        THIS CURES B2129 WHICH MEANS IF MACRO EXIT WE FREE CPB-CUNI
   4 7#LX ...[   
   9 7#PG ...      VFREE    CPB,CUNI 
   4 7#S5 ...[   
  16 7#WQ          UP                           [MACRO BREAKIN   
   4 7#_W ...XGEO
   4 7*52 ...[   
  17 7*86 ...[    THIS IS A REQUESTED JOBWELL PHASE N  ENHANCEMENT   
  13 7*?= ...[        SEE R.J.D.  GRIMEWADE  (CENT)  
  17 7*BB ...[     DONE UNDER DEVELOPEMENT GEDIT   7.OCT.74   *CENT  
   4 7*FG ...[   
   7 7*JL ...#SKI     JSKI15 
   7 7*MQ ...       COMBRKIN 
   9 7*QW ...#SKI         JSKI15<1$1 
  21 7*W2          GEOERR   1,IP BRKIN          [OPERATORS'DIRECTIVES CANNOT BE BROKE
  14 7B*L    [              CONVERT NUMBER TO FORM A,B,C 
   6 7BFH ...#SKI  XIPEXT
   9 7BKD ...T6    IPUSEX   LDX   7,3
   7 7BP* ...#SKI  XIPEXT<1$1
  10 7BT=    T6    LDX   7  INSTPARAM(3) 
   8 7C#W          BZE   7  XNUM 
   9 7CSG          CALL  6  TEXTCON  
  16 7D#6          BRN      OUT                 [JOIN O/P ROUTINE
   5 7DRQ    OUTRANGE
   9 7F?B          LDX   0  AWORK3(2)
   8 7FR2          BZE   0  YFWD 
   7 7G=L          CLOSETOP  
   4 7G*9 ...[   
  20 7GCS ...[        THIS CURES B2129 WHICH MEANS IF MACRO EXIT WE FREE CPB-CUNI
   4 7GGC ...[   
   9 7GK2 ...      MFREE    CPB,CUNI 
   4 7GMK ...[   
   7 7GQ=          UPPLUS   1
   4 7H9W    YFWD
   8 7HPG          LDN   0  14   
  17 7J96          OUTBLOCK 0                   [SET UP PARAM BLOCK  
  16 7JNQ          BPZ   5  NO1                 [J IF NOT TRACE  
   7 7K8B          LDX   0  6
   8 7KN2          CALL  6  SYNTH
   7 7L7L          LDX   0  7
   8 7LM=          CALL  6  SYNTH
   8 7M6W          BRN      OPT1 
   4 7MLG    NO1 
  15 7N66          BZE   5  NUMX                [J IF NUMBER 
   8 7NKQ          BZE   6  NUMX 
   7 7P5B          LDX   5  7
  18 7PK2          LDX   7  6                   [CONTEXT TYPE PARAMETER  
  15 7Q4L          CALL  6  TEXTCON             [CONVERT MAX 
   7 7QJ=          LDX   7  5
   8 7R3W          BZE   7  NO2  
  15 7RHG          CALL  6  TEXTCON             [CONVERT MIN 
   8 7S36          BRN      OPT1 
   4 7SGQ    NUMX
  15 7T2B          OUTNUM 6,0                   [OUTPUT MAX  
   4 7TG2    NO2 
  18 7T_L          OUTNUM 7,0                   [AND MIN AS DEC NUMBERS  
   9 7WF=    OPT1  CALL  3  MISTAKE  
   7 7WYW          +IOUTRANGE
   6 7XDG    WOOPS CLOSE 
   8 7XY6          BRN      REND 
  11 7YCQ    WOE   COMERR   JPARMIS,JVALUE   
   5 94J2    SUNSET  
  10 953L          BXE   4  GSIGN,SSUNS  
   7 95H=          EXIT  7  1
   5 95MN ...SSUNS   
  17 95S6 ...[     THIS CURES B2928 - I.E. AS OUTPARAM MAY COORDINATE
  14 95YJ ...[         WE MUST RELITIVIZE THE LINK (X7)  
  15 9652 ...[             DONE    4TH   JUNE  75      :CENT 
   4 969D ...[   
   8 96*W ...      SBX   7  FX1  
  17 96GG          LDX   0  UNSET(1)            [CASE WHEN IP UNSET  
  10 9726          STO   0  ACOMMUNE5(2) 
  10 97FQ          LDX   0  UNSET+1(1)   
  10 97_B          STO   0  ACOMMUNE6(2) 
   7 98F2          LDN   4  7
  10 98NS ...      OUTPARAM 4,ACOMMUNE5  
   8 98YL ...      ADX   7  FX1  
   4 998D ...[   
   7 99D=          EXIT  7  0
  12 9JML    [     S/R TO CONVERT TRACE TYPE IPS 
   5 9K7=    SYNTH   
   8 9KLW          SBX   6  FX1  
   7 9L6G          SYNTHMON 0
   8 9LL6          ADX   6  FX1  
   7 9M5Q          EXIT  6  0
  10 9MKB         MENDAREA 30,K99INSTPARA
   4 9N52    #END
   8 ____ ...61651256000200000000
