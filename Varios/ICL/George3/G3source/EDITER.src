  10 22FL    #LIS  K0EDITER>K0ALLGEO>0   
  16 22_=    #SEG  EDITER                       [DICK RUSBRIDGE  
  16 2394 ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982.   
   7 23DW          8HEDITER  
  21 23YG    #     THIS SEGMENT OUTPUTS ERROR MESSAGES AND COMMENTS FOR THE EDITOR.  
  21 24D6 ...#     K1EDITER - OUTPUT FIXED MESSAGE IN COMERR OR COMMENT (EDERR MACRO)
  21 24XQ    #     K2EDITER - OUTPUT FIXED MESSAGE IN COMMENT CATEGORY. (EDCOM MACRO)
  17 25CB    #     K3EDITER - OUTPUT VARIABLE SYNTAX ERROR MESSAGE.  
  16 25JJ ...#     K4EDITER - OUTPUT VARIABLE EXECUTION MESSAGE  
  21 25PQ ...#     K5EDITER - OUTPUT FIXED MESSAGE IN COMERR CATEGORY. (EDCOMERR MACR
   4 25X2    #   
  11 26BL    #              ENTRY POINT TABLE
   4 26W=    #   
  16 27*W     SEGENTRY K1EDITER,N1EDITER        [EDERR MESSAGE   
  16 27TG     SEGENTRY K2EDITER,N2EDITER        [EDC6M MESSAGE   
  17 28*6     SEGENTRY K3EDITER,N3EDITER        [SYNTAX ERROR MESSAGE
  18 28SQ     SEGENTRY K4EDITER,N4EDITER        [EXECUTION ERROR MESSAGE 
  16 28W_ ... SEGENTRY K5EDITER,N5EDITER        [EDCOMERR MESSAGE
  14 28_8 ...               +0                  [SPACE   
  13 293C ...               +0                  [HERE
  13 295L ...               +0                  [FOR 
  14 297T ...               +0                  [EXTRA   
  15 29=4 ...               +0                  [SEGENTRYS   
   4 29#B    #   
   9 29S2    SYNT  14HSYNTAX ERROR:  
  10 2=?L    XECU  17HEXECUTION ERROR:   
   4 2=*3 ...#   
  17 2=BD ...#     THE FOLLOWING MESSAGES ARE SET UP WITH UNIVERSALS 
  15 2=CT ...#     WHICH ARE USED IN OTHER SEGMENTS, HENCE:- 
  16 2=F= ...#           1/ ANY NEW SEGENTRY MUST BE SET UP TO   
  16 2=GM ...#              REPLACE ONE OF THE SPARE WORDS ABOVE 
  17 2=J4 ...#           2/ ANY NEW MESSAGES MUST BE ENTERED IN SUCH 
  18 2=KF ...#              A WAY THAT THEY WILL NOT CHANGE ANY OF THE   
  17 2=LW ...#              EXISTING UNIVERSAL VALUES. E.G. INSERTED 
  10 2=N? ...#              AT THE END   
   4 2=PN ...#   
  20 2=R=     EDERM LWIN,53,L/W WILL BE INEFFECTIVE SINCE TRACING INHIBITS OUTPUT
  19 2?=W     EDERM MODT,47,MERGING OLDFILES OF DIFFERENT TYPES NOT ALLOWED  
  17 2?QG     EDERM YRTL,41,YOUR RECORD'S TOO LONG: I'VE TRUNCATED IT
  17 2#=6     EDERM YROB,40,YOU'VE RUN OFF THE BEGINNING OF THE FILE 
  17 2#PQ     EDERM ITON,40,INCOMPATIBLE TYPES FOR OLD AND NEW FILES 
  17 2*9B     EDERM YBSL,39,YOU'VE REACHED YOUR BACKING STORE LIMIT  
  17 2*P2     EDERM FQUE,38,REPETITION OF THIS KIND IS NOT ALLOWED   
  16 2B8L     EDERM TPEF,37,FILE IS OF THE WRONG TYPE FOR EDITING
  16 2BN=     EDERM BTNA,35,BACKWARDS TRANSCRIPTION NOT ALLOWED  
  16 2C7W     EDERM YROE,34,YOU'VE RUN OFF THE END OF THE FILE   
  15 2CMG     EDERM TWOF,33,F MAY NOT BE USED IN THIS CONTEXT
  15 2D76     EDERM IEYE,32,I'VE EXHAUSTED YOUR EDITING FILE 
  15 2DLQ     EDERM IEYJ,30,I'VE EXHAUSTED YOUR JOB SOURCE   
  14 2F6B     EDERM AFNA,30,AMORPHOUS OLDFILE NOT ALLOWED
  14 2FL2     EDERM YFYN,27,YOU'VE FILLED YOUR NEW FILE  
  14 2G5L     EDERM YHNI,27,YOU HAVE NO INPUT FILE OPEN  
  13 2GK=     EDERM TNFC,25,THERE IS NO FILE TO CLOSE
  13 2H4W     EDERM ITER,22,INSTRUCTION TERMINATED   
  13 2HJG     EDERM EFSF,22,TOO MANY EDITING FILES   
  12 2J46     EDERM TMIF,20,TOO MANY INPUT FILES 
  12 2JHQ     EDERM CHNF,19,CHARACTER NOT FOUND  
  11 2K3B     EDERM NUTL,16,NUMBER TOO LARGE 
  11 2KH2     EDERM IPMT,16,INPUT FILE EMPTY 
  11 2L2L     EDERM EDAB,14,EDIT ABANDONED   
   6 2L7S ...#UNS  EDLOOP
  20 2L*2 ... EDERM INCL,52,INSTRUCTION(S) NOT COMPLETED SINCE EDITOR WOULD LOOP 
  19 2LBQ ... EDERM YEDL,46,YOU'VE EXCEEDED THE INSTALLATION DEFAULT LIMIT   
  16 2LDG ... EDERM YESL,36,YOU'VE EXCEEDED YOUR SPECIFIED LIMIT 
   4 2LG=    #   
  14 2L_W    #     SETS COMERR IF NOT FROM MOP CONSOLE   
   4 2MFG    #   
  15 2M_6    SETCE LDN   0  #10                 [MOP CONTEXT.
  17 2NDQ          LDN   4  #40                 [IN COMMENT CATEGORY.
  16 2NYB          ANDX  0  CONTEXT(2)          [IF CURRENTLY MOP
  15 2PD2          BZE   0  SETS                [J IF NOT MOP
   9 2PK8 ...      LDXC  0  IEFT(2)  
  16 2PQB ...      BCS      SETX                [J IF NO EDITFILE
  10 2PXL          MHUNTW   3,CPB,CALAS  
   9 2QC=          LDXC  0  CPBFSD(3)
  16 2QWW          BCS      SETX                [JM IF NOT MACRO 
  19 2RBG    SETS  LDN   4  #20                 [RESET TO COMMERR CATEGORY   
  15 2RW6          SETCOMER                     [WHEN NOT MOP
  18 2S*Q    SETX  STO   4  AWORK1(2)           [SAVE MESSAGE CATEGORY   
   7 2STB          EXIT  6  0
   4 2T*2    #   
  20 2TSL    N1EDITER                           [MESSAGE IS IN COMERR CATEGORY   
   4 2W#=    #   
  18 2WRW          CALL  6  SETCE               [IF NOT MOP, SET COMERR  
   7 2X?G          BRN   X1  
   4 2XR6    #   
  20 2Y=Q    N2EDITER                           [MESSAGE IS IN COMMENT CATEGORY  
   4 2YQB    #   
   8 2_=2          LDN   4  #40  
   9 2_PL          STO   4  AWORK1(2)
   7 329=    X1    SMO   7   
   8 32NW          LDX   5  0(1) 
   7 338G          ADN   5  3
   7 33N6          SRL   5  2
  10 347Q          SETUPCORE 5,3,CPB,CUNI
   7 34MB          SMO   7   
   8 3572          LDN   2  0(1) 
   8 35LL          LDX   7  0(2) 
   7 366=          ADN   2  1
   7 36KW          LDX   1  5
   8 375G          ADN   3  A1   
   8 37K6          MOVE  2  0(1) 
   8 384Q    X2    LDX   2  FX2  
  16 38JB          LDX   6  ACOM6(2)            [PRESERVE ACOM6  
  16 3942          LDX   4  AWORK1(2)           [MESSAGE CATEGORY
  11 39HL          OUTMON   7,A1,4,CPB,CUNI  
   9 3=3=          STO   6  ACOM6(2) 
   9 3=GW          VFREEW   CPB,CUNI 
   5 3?2G          UP
   4 3?G6    #   
  19 3?_Q    RLIF                               [RELOCATE INSTRUCTION BLOCK  
   4 3#FB    #   
   9 3#_2          LDX   5  IEIM(2)  
   9 3*8S          LDX   3  IEIA(2)  
   8 3*DL          TESTMOVE 5,X3 
  17 3*Y=          MHUNTW   3,FILE,FRB          [OTHERWISE FIND IT   
   7 3BCW          PHOTO 5   
   9 3BXG          STO   3  IEIA(2)  
   9 3CC6          STO   5  IEIM(2)  
   7 3CWQ    X3    EXIT  4  0
   4 3DBB    #   
  17 3DW2    N3EDITER       [ 'SYNTAX ERROR:<ERRONEOUS INSTR4CTION>' 
   4 3F*L    #   
  18 3FT=          CALL  6  SETCE               [IF NOT MOP, SET COMERR  
   9 3G#W          LDX   3  IEPS(2)  
   9 3GSG          LDX   7  IEIP(2)  
   7 3H#6          SLC   3  2
   6 3HRQ    #SKI  JDIAG1
   4 3J?B    (   
   9 3JR2          LDX   4  IEIG(2)  
  15 3K=L          SLL   4  2                   [J IF IEPS IS
   4 3KQ=    )   
   7 3L9W    #SKI  JDIAG1<1$1
  15 3LPG          LDN   4  A1+2*4              [J IF IEPS IS
  15 3M96          BXL   3  4,X8                [RIDICULOUS  
   7 3MNQ          SLC   7  2
   7 3N8B          SBS   3  7
   7 3NN2          STO   7  3
   7 3P7L          SRL   3  2
   8 3PM=          LDN   4  #3763
   7 3Q6W          ADN   3  5
   8 3QLG          BXL   7  4,X4 
   7 3R66    X8    LDN   7  0
   8 3RKQ    X4    BNZ   7  X5   
   7 3S5B          LDN   3  5
  10 3SK2    X5    SETUPCORE 3,3,CPB,CUNI
   8 3T4L          LDN   6  A1(3)
   9 3TJ=          LDN   5  SYNT(1)  
   8 3W3W          BZE   7  X6   
   8 3WHG          MVCH  5  14   
   8 3X36    X7    CALL  4  RLIF 
   9 3XGQ          LDX   5  IEPS(2)  
   9 3Y2B          ADX   5  IEIA(2)  
   7 3YG2          LDX   3  7
   8 3YPS    X11   BZE   3  X9   
   8 3Y_L          MVCH  5  0(3) 
   7 3_49          SBN   3  1
   7 3_6S          SRL   3  9
   7 3_9C          SLL   3  9
   8 3_#2          BRN      X11  
   4 3_BK    X9  
   8 3_F=          ADN   7  14   
   7 3_YW          BRN   X2  
   8 42DG    X6    MVCH  5  12   
   8 42Y6          LDN   7  12   
   7 43CQ          BRN   X2  
   4 43XB    #   
  18 44C2    N4EDITER       [ 'EXECUTION ERROR:<ERRONEOUS INSTRUCTION>'  
   4 44WL    #   
   8 45B=          CALL  6  SETCE
   9 45TW          LDX   3  IEPS(2)  
   9 46*G          LDX   7  IEIP(2)  
   7 46T6          SLC   3  2
   6 47#Q    #SKI  JDIAG1
   4 47SB    (   
   9 48#2          LDX   4  IEIG(2)  
  15 48RL          SLL   4  2                   [ J IF IEPS  
   4 49?=    )   
   7 49QW    #SKI  JDIAG1<1$1
  15 4==G          LDN   4  A1+2*4              [JUMP IF IEPS
  16 4=Q6          BXL   3  4,X10               [IS RIDICULOUS   
   7 4?9Q          SLC   7  2
   7 4?PB          SBS   3  7
   7 4#92          STO   7  3
   7 4#NL          SRL   3  2
   8 4*8=          LDN   4  #3763
   7 4*MW          ADN   3  5
   8 4B7G          BXGE  7  4,X10
   8 4BM6          BZE   7  X10  
  10 4C6Q          SETUPCORE 3,3,CPB,CUNI
   8 4CLB          LDN   6  A1(3)
   9 4D62          LDN   5  XECU(1)  
   8 4DKL          MVCH  5  17   
   8 4F5=          CALL  4  RLIF 
   9 4FJW          LDX   5  IEPS(2)  
   9 4G4G          ADX   5  IEIA(2)  
   7 4GJ6          LDX   3  7
   8 4GRY    X12   BZE   3  X13  
   8 4H3Q          MVCH  5  0(3) 
   7 4H6*          SBN   3  1
   7 4H8Y          SRL   3  9
   7 4H?H          SLL   3  9
   8 4HB6          BRN      X12  
   4 4HDP    X13 
   8 4HHB          ADN   7  17   
   8 4J32          BRN      X2   
   5 4JGL    X10   UP
   4 4JK9 ...#   
  20 4JMS ...N5EDITER                           [MESSAGE IS IN COMERR CATEGORY   
   4 4JQC ...#   
   8 4JT2 ...      LDN   6  X1(1)
   8 4JXK ...      BRN      SETS 
   9 4K2=     MENDAREA 200,K100EDITER
   4 4KFW    #END
   8 ____ ...03732377000200000000
