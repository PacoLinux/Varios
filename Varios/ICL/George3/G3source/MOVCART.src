  16 22FL ...      SEG      MOVCART,865,FRANK BLUNDELL,MOVCART   
  19 23YG    #              THIS IS THE SEGMENT WHICH CONTROLS THE MOVING OF 
  13 24D6    #              FILESTORE/SWAP CARTRIDGES
   4 24XQ    #   
  17 25CB    #              XA1=CPPTR,+1=TYPE/GEOG,+2=CSN OR BSA ADDR
  12 25X2    #              +3=BSUN,+4=APIA ADDR 
   8 26BL    #DEF  XBSA=BSFAIL   
   6 26W=    #DEF  XREC=5
   6 27*W    #DEF  XA1=A1
  10 27TG    #DEF  XBUFSTART=2*XREC+XA1+1
  11 28*6    #DEF  XLENCART=XBUFSTART-XA1+128
   4 28SQ    #   
  12 29#B          SEGENTRY K1MOVCART,QK1MOVCART 
  18 29D? ...      FSHENTRY K2MOVCART,QK2MOVCART,QK2MOVCART,QK2MOVCART   
  18 29J8 ...      FSHENTRY K3MOVCART,QK3MOVCART,QK3MOVCART,QK3MOVCART   
  18 29N5 ...      FSHENTRY K4MOVCART,QK4MOVCART,QK4MOVCART,QK4MOVCART   
  13 29Q3 ...      FSHENTRY K5MOVCART,XBGONE,,XBGONE 
   4 29S2    #   
   9 2=?L    XMASK          #17770000
   9 2=R=    XMASK1         #777777  
   9 2?=W    #DEF  XSER=2*XREC+XA1   
  18 2?*5 ...#  THESE FIRST TWO MESSAGES MUST BE AT THE SAME OFFSET AS   
   6 2?C# ...# IN MOVCASH
   4 2?FH ...#   
   9 2?HQ ...XBMCG          +BMVCG   
   9 2?K_ ...XBMG3          +BMVG3   
   4 2?N8 ...#   
   9 2?QG    XBMV           +BMVINV  
   9 2#=6    XBMVC          +BMVINC  
   9 2#PQ    XBMCOM         +BMVINCOM
   9 2*9B    XBMSAME        +BMVSAME 
   9 2*P2    XBMFILE        +BMVFILE 
   9 2B8L    XBMFIL1        +BMVFILE1
   9 2CG= ...XBMFD          +BMVFD   
  20 2CSN ...XWAITSWIT      +EWAITSWIT           [UNIT IS WAITING TO BE SW OUT   
   9 2D76    XBMENT         +ENTIN30 
   8 2D7* ...      FSHCODE  AORB 
   4 2D7J ...(   
  12 2D7K ...#   LINK FOR UNANTICIPATED CLOSEDOWN
   4 2D7L ...#   
   5 2D7M ...SCDLINK 
   9 2D7N ...      PAIR     MOVCART,5
   4 2D7P ...#   
  18 2D7R ...XBMSH          +FSHBMSH            [SPINDLES MUST BE SHARED 
  21 2D82 ...XBMUN          +FSHBMUN            [SPINDLES MUST BE DEDICATED OR SHARED
  19 2D89 ...XBMIX          +FSHBMIX            [CANT HAVE RESIDENCES ON BOTH
   9 2D8D ...XMASK2         #77770000
   4 2D8M ...#   
   4 2D** ...)   
   4 2DG3 ...#   
   6 2DLQ    QK1MOVCART  
  19 2F6B ...      PARSORT  XBRKIN,ALL,GROPE    [CONVERT OPERATORS UNIT NOS. 
  18 2FL2          SETNCORE XLENCART,3,BSTB,CART,1  [CREATE BUFFER BLOCK 
   7 2G5L          LDN   4  2
   7 2GK=          LDN   5  0
  18 2H4W    X1    HUNTW    1,CPAR,JSCE         [HUNT FOR PARAMETER BLOCK
  21 2HJG ...      BNG   1  TIDYUP            [FINISH IF NO PARAM-MESSAGE O/P BY PARS
   9 2JHQ          LDN   6  JSCEP(1) 
  19 2K3B          SMO      5                   [MOVE DEVICE LIST POINTER AND
  20 2KH2          LDN   7  XA1(3)              [DEVICE TYPE/GEOGRAPHICAL NUMBER 
  16 2L2L          MOVE  6  2                   [TO BUFFER BLOCK 
   7 2LG=          SMO      5
  18 2L_W          STOZ     XA1+2(3)            [CLEAR SERIAL NUMBER WORD
  17 2MFG          FREECORE 1                   [FREE PARAMETER BLOCK
   8 2M_6          ADN   5  XREC 
  19 2NDQ          BCT   4  X1                  [LOOK FOR SECOND PARAMETER   
  19 2NYB          HUNTW    1,CPAR,JSCE         [IF ANY MORE PARAMETERS THE  
  18 2PD2          LDN   6  XBMV                [....COMMAND IS INCORRECT
  17 2PXL          BPZ   1  XINV                [COMMAND IS INVALID  
   9 2QC=          LDX   4  XA1+1(3) 
  16 2QWW          SRL   4  18                  [GET DEVICE TYPES
   9 2RBG          LDX   5  XA1+6(3) 
   8 2RW6          SRL   5  18   
  20 2S*Q          LDN   6  XBMCOM              [....DIFFERENT CHARACTERISTICS   
  20 2STB          TXU   4  5                   [MAKE SURE DEVICES OF SAME TYPE  
   9 2T*2          BCS      XINCOM   
   7 2TSL          SBN   4  6
  21 2W#= ...      BZE   4  X2                  [DEVICE TYPE MUST EITHER BE 6 OR 26  
   8 2WRW          SBN   4  20   
   8 2X7K ...      LDN   6  XBMVC
   9 2XH# ...      BNZ   4  XWRTYP   
  21 2XX3 ...X2    LPROP    XA1(3),4            [LOAD PROPERTIES AND CHECK DEVICES   
  16 2Y=Q ...      LPROP    XA1+5(3),5          [ARE SAME TYPE   
   9 32F4 ...      LDN   6  XBMCOM   
  17 32SR ...      TXU   4  5                   [COMMAND INVALID IF  
  15 338G          BCS      XINCOM              [DIFFERENT   
  17 33?3 ...      LDN   6  XBMFD               [CHECK DEVICES ARE   
  16 33?W ...      JPROP     XA1(3),XINCOM,XINCOM   [NOT FIXED   
   5 33#Q ...XCHEKST 
  16 33#R ...      LADDP    XA1(3),2               [PTR TO APIA  
  10 33#S ...      LDX   4  BSUNIT3(2)   
   7 33#T ...      LDX   5  4
   9 33#W ...      ANDN  5  BSWOUT   
  21 33#X ...      BNZ   5  XBSW                      [ERROR IF WAITING TO BE SW OUT 
  21 33#Y ...      ANDN  4  #10                    [CHECK IF STATUS CHANGE IN PROGRES
  18 33#_ ...      BZE   4  XNOS1                  [ON FIRST, _ IF NOT   
   9 33*2 ...      COOR3    KTSCTF   
   8 33*3 ...      BRN      XNOS2
  10 33*4 ...XNOS1 LADDP    XA1+5(3),2   
  21 33*5 ...      LDX   4  BSUNIT3(2)             [CHECK IF STATUS CHANGE IN PROGRES
   7 33*6 ...      LDX   5  4
   9 33*7 ...      ANDN  5  BSWOUT   
   8 33*8 ...      BZE   5  XNOS4
  21 33*9 ...      ADN   3  5                         [ERROR IF WAITING TO BE SW OUT 
   8 33*= ...      BRN      XBSW 
   5 33*? ...XNOS4   
  15 33*# ...      ANDN  4  #10                    [ON SECOND
  15 33** ...      BZE   4  XNOS3                  [_ IF NOT 
   9 33*B ...      COOR3    KTSCTF   
  10 33*C ...XNOS2 MHUNTW   3,BSTB,CART  
   9 33*D ...      BRN      XCHEKST  
   5 33*F ...XNOS3   
   7 33*N ...      FSHSKIP   
  19 347Q          LDN   6  XBMENT              [SPINDLES MUST BE DEDICATED  
   7 34MB          LDN   7  0
   2 3572
  18 35LL          LADDP    XA1(3),2            [GET POINTER TO APIA AND 
  18 366=          STO   2  XA1+4(3)            [REMEMBER IN BUFFER BLOCK
   9 36KW          LDX   4  BSUNIT(2)
   8 375G          ANDN  4  #7777
  16 37K6          STO   4  XA1+3(3)            [REMEMBER BSUN   
  19 384Q          ORX   7  BSUNIT3(2)          [B22 SET IF CG IN PROGRESS   
   2 38JB
   7 38PJ ...      FSHSKIP   
   4 38WQ ...(   
  17 3=3=          JDED     2,X25               [JUMP IF DEDICATED   
   8 3=GW          BRN      XCG  
   4 3?2G    X25 
   4 3?PY ...)   
  20 3?_Q          LADDP    XA1+5(3),2          [GET POINTER TO APIA FOR OTHER   
  20 3#FB          STO   2  XA1+9(3)            [DEVICE AND STORE IN BUFFER BLOCK
   9 3#_2          LDX   5  BSUNIT(2)
   8 3*DL          ANDN  5  #7777
  16 3*Y=          STO   5  XA1+8(3)            [REMEMBER BSUN   
  19 3BCW          ORX   7  BSUNIT3(2)          [B22 SET IF CG IN PROGRESS   
   2 3BXG
   7 3C4N ...      FSHSKIP   
   4 3C9W ...(   
  17 3DBB          JDED     2,X26               [JUMP IF DEDICATED   
   8 3DW2          BRN      XCG  
   4 3F*L    X26 
   4 3G54 ...)   
  17 3G#W          ANDN  7  BCGCOM              [CG IN PROGRESS BIT  
   8 3GSG          LDN   6  XBMCG
  19 3H#6          BNZ   7  XCG                 [J IF CG ALREADY IN PRORESS  
  19 3HRQ          LDN   6  XBMSAME             [....REFER TO THE SAME DEVICE
  20 3J?B          TXU   5  4                   [IF THE BSUNS ARE THE SAME THE   
  18 3JR2          BCC      XSAME               [COMMAND MUST BE INVALID 
  21 3K=L          LDN   6  XBMG3               [....CARTRIDGE CONTAINS GEORGE FILE  
  20 3K*9 ...      LDX   7  BRADSPLTBSU         [BSUN OF SCF ; =0 IF NOT PRESENT 
   7 3KCS ...      TXU   4  7
  16 3KGC ...      BCC      XG3                 [J IF EQUAL TO   
   7 3KK2 ...      TXU   5  7
  16 3KMK ...      BCC      XG3                 [EITHER DEVICE   
  18 3KQ=          LDN   7  1                   [BSUN OF GEORGE DEVICE   
   7 3L9W          TXU   4  7
  19 3LPG          BCC      XG3                 [INVALID IF CG ON GEORGE FILE
   7 3M96          TXU   5  7
   8 3MNQ          BCC      XG3  
  16 3N8B          LDN   7  0                   [ZEROISE MARKERS 
   7 3NN2          LDN   5  0
  18 3P7L    X6    SLL   4  12                  [MOVE BSUN TO BITS0-11   
  20 3PM=          LDX   2  ALFTP               [POINTER TO LOGICAL FILE TABLE   
  10 3Q6W          LDX   6  ALOGLEN(2)   
  19 3QLG    X3    BZE   6  X5                  [BRANCH IF NO MORE ENTRIES   
   8 3R66          LDX   0  A1(2)
  18 3RKQ          BNG   0  X4                  [BRANCH IF NO RESIDENCES 
   8 3S5B          SMO      FX1  
   8 3SK2          ANDX  0  XMASK
  19 3T4L          TXU   0  4                   [SEE IF ENTRY FOR SAME BSUN  
   8 3TJ=          BCC      X7   
   7 3W3W    X4    SBN   6  2
  17 3WHG          BDX   2  X3                  [LOOK AT NEXT ENTRY  
  21 3X36    X5    BNZ   5  X8                  [BRANCH IF WE HAVE LOOKED FOR BOTH   
  19 3XGQ    X9    LDX   4  XA1+8(3)            [BSUN'S - IF NOT LOOK AGAIN  
   8 3Y2B          ADN   5  XREC 
   8 3YG2          BRN      X6   
   8 3YJ9 ...      FSHCODE  AORB 
   4 3YLD ...(   
   7 3YNM ...X7    LDX   1  2
  19 3YQW ...      LDEX  2  A1(2)               [GET MODIFIER TO FDTABLE AND 
   4 3YT5 ...)   
   7 3YX# ...      FSHSKIP   
  19 3Y_L    X7    LDEX  2  A1(2)               [GET MODIFIER TO FDTABLE AND 
  18 3_F=          ADX   2  AFDTP               [ADD ADDRESS OF FDTABLE  
  21 3_YW          LDX   0  A1+5(2)             [LOAD SERIAL NUMBER FROM FDTABLE...  
   7 42DG          SMO      5
  19 42Y6          STO   0  XA1+2(3)            [...AND STORE IN BUFFER BLOCK
   7 43CQ          ADN   7  1
   8 43D= ...      FSHCODE  AORB 
   4 43DQ ...(   
  21 43F= ...      ADX   3  5                   [UPDATE POINTER FOR BORING BS MACRO  
   4 43FQ ...X7A 
   8 43G= ...      LDCT  0  #200 
   8 43GQ ...      ANDX  0  A1(1)
  18 43H= ...      BZE   0  NOTSWAP             [J IF FILESTORE RESIDENCE
  19 43J= ...      BS       3,BCGHOMESWAP       [SET SWAP BIT IN DEVICE ENTRY
   8 43JQ ...      BRN      NOTFS
   5 43LQ ...NOTSWAP 
  19 43M= ...      BS       3,BCGFILESTOR       [SET F/S BIT IN DEVICE ENTRY 
   5 43MQ ...NOTFS   
  19 43N= ...      SBN   6  2                   [PROCESS REST OF ALFTABLE IN 
  19 43NQ ...      BDX   1  /                   [CASE F/S+SWAP ON SAME DRIVE 
   9 43P= ...      BZE   6  XALFIN   
   8 43PQ ...      LDX   0  A1(1)
   8 43Q= ...      BNG   0  NOTFS
   8 43QQ ...      SMO      FX1  
   8 43R= ...      ANDX  0  XMASK
   7 43RQ ...      TXU   0  4
   8 43S= ...      BCC      X7A  
   8 43SQ ...      BRN      NOTFS
   5 43T= ...XALFIN  
  14 43TQ ...      SBX   3  5                   [RESET X3
   4 43W= ...)   
  17 43XB          BZE   5  X9                  [LOOK FOR OTHER BSUN?
  19 44C2    X8    LDN   6  XBMFILE             [....MUST CONTAIN A RESIDENCE
  19 44WL          BZE   7  XFILE               [BRANCH IF RESIDENCES ON NONE
   9 45B=          LDN   6   XBMFIL1 
   7 45TW          SBN   7  2
  19 46*G          BPZ   7  XFILE               [BRANCH IF RESIDENCES ON BOTH
   8 46GN ...      FSHCODE  AORB 
   5 46MW ...RESET   
   9 46T6          LDX   2  XA1+4(3) 
   9 47#Q          LDX   1  XA1+9(3) 
   9 47SB          LDN   5  BCGCOM   
  20 48#2          ORS   5  BSUNIT3(2)          [REMEMBER CG  NOW IN PROGRESS ON 
  16 48RL          ORS   5  BSUNIT3(1)          [THE TWO DEVICES.
   8 48RR ...      FSHCODE  AORB 
   4 48RY ...(   
   8 48S= ...      SMO      FX2  
  18 48SC ...      STOZ     AWORK1              [ZEROISE TRANSBEG MARKER 
  18 48SJ ...      LDX   5  3                   [SET UP SECOND POINTER   
  18 48SP ...      ADN   5  XREC                [TO POINT TO SECOND ENTRY
   8 48SW ...      LDN   6  XBMUN
  20 48T3 ...      JUNDE    2,XXCG              [DRIVES MUST NOT BE UNDEDICATED  
   9 48T8 ...      JUNDE    1,XXCG   
  18 48T* ...      JBS      XFS,3,BCGFILESTOR   [J IF 1ST DRIVE HAS F/S  
  15 48TG ...      JBC      XSWAP,5,BCGFILESTOR [J IF NOT F/S
   8 48TM ...XFS   LDN   6  XBMSH
  18 48TS ...      JDED     2,XXCG              [ERROR - DRIVE NOT SHARED
  16 48T_ ...      BS       3,BCGSHARED         [SET SHARED BIT  
  21 48W6 ...      JDED     1,XXCG              [ERROR AGAIN - DRIVE MUST BE SHARED  
  16 48W? ...      BS       5,BCGSHARED         [SET SHARED BIT  
   8 48WD ...      BRN      XCHKD
   5 48WK ...XSWAP   
  17 48WQ ...      JDED     2,XSETDD            [ALLOWABLE FOR SWAP  
  16 48WX ...      BS       3,BCGSHARED         [SET SHARED BIT  
   9 48X4 ...      BRN      XNXTSW   
   5 48X9 ...XSETDD  
  17 48XB ...      BS       3,BCGDEDIC          [SET DEDICATED BIT   
   5 48XH ...XNXTSW  
  16 48XN ...      JDED     1,XSETDE            [AGAIN ALLOWABLE 
  16 48XT ...      BS       5,BCGSHARED         [SET SHARED BIT  
   8 48Y2 ...      BRN      XCHKD
   5 48Y7 ...XSETDE  
  17 48Y# ...      BS       5,BCGDEDIC          [SET DEDICATED BIT   
  19 48YF ...      JDED     2,NOWAY             [BOTH DEDICATED! NO TRANSBEG 
   5 48YL ...XCHKD   
  16 48YR ...      JBC      NOWAY,,FSHRUNNING   [J IF B M/C DOWN 
   8 48YY ...      LDX   0  GSIGN
   8 48_5 ...      SMO      FX2  
  19 48_= ...      ORS   0  AWORK1              [SET MARKER -VE TRANSBEG REQD
   9 48_C ...      FSHCODE  A,XIAMB  
   4 48_D ...(   
  11 48_F ...      SETNCORE 1,3,FSH,FSHAMBLES
  10 48_G ...      LDX   0  SCDLINK(1)   
  10 48_H ...      STO   0  FSHCDLINK(3) 
  20 48_J ...      FSHACTPR XBDOWN              [SET UP PAIRED ACT IN OTHER M/C  
   4 48_K ...)   
   5 48_P ...XIAMB   
  20 48_W ...      SETNCORE 2*XREC+1,3,BSTB,HORSE [SET UP /HORSE AFORE THE /CART 
  18 4923 ...      MHUNTW   2,BSTB,CART         [REFIND THE /CART BLOCK  
   8 4928 ...      ADN   2  XA1  
   8 492* ...      ADN   3  XA1  
  19 492G ...      MOVE  2  2*XREC+1            [COPY DETAILS OF THE DRIVES  
  19 492M ...                                   [HEEEEEERE WEEEEEEE GOOOOO!!!
  18 492S ...      TRANSBEG FSHCGID,MOVCASH,2,BSTB,HORSE,ACOMMUNE1,XBDOWN
  15 492_ ...      BRN      XBERR               [ERROR JUMP  
  10 4936 ...      MHUNTW   3,BSTB,CART  
  10 493? ...      MHUNTW   2,BSTB,HORSE 
  19 493D ...      LDN   6  XBMIX               [MESSAGE - NO MIXTURE ALLOWED
  16 493K ...      LDX   4  XA1+3(3)            [GET SHFS BITS   
  17 493Q ...      ORX   4  XA1+3(2)            [FROM ALIEN M/C ALSO 
  17 493X ...      LDX   5  XA1+3+XREC(3)       [ALSO FOR OTHER DRIVE
  10 4944 ...      ORX   5  XA1+3+XREC(2)
  20 4949 ...      MBSXD    7,BCGHOMESWAP,BCGAWAYSWAP,BCGFILESTOR [SET UP MASK   
  15 494B ...      LDX   0  4                   [FIRST DRIVE!
   7 494H ...      ANDX  0  7
  18 494N ...      BNZ   0  RECHK1              [J IF RES ON 1ST DRIVE   
  15 494T ...      LDX   0  5                   [NONE ON 1ST 
   7 4952 ...      ANDX  0  7
  17 4957 ...      BNZ   0  RECHK2              [SO J IF ANY ON 2ND  
  19 495# ...      GEOERR   1,NORES             [NO RESIDENCES - IMPOSSIBLE  
   5 495F ...RECHK1  
   7 495L ...      LDX   0  5
   7 495R ...      ANDX  0  7
  20 495Y ...      BNZ   0  XAPIA               [ILL RES ON BOTH FROM EITHER M/C 
   5 4965 ...RECHK2  
   7 496= ...      ORX   4  5
  19 496C ...      MBSXD    7,BCGHOMESWAP,BCGAWAYSWAP [SET UP ANOTHER MASK   
   7 496J ...      LDX   0  4
  18 496P ...      ANDX  0  7                   [CHECK IF SWAP RESIDENCES
  18 496W ...      ERX   0  7                   [ON SAME FOR MORE CHECKS 
   9 4973 ...      BNZ   0  RECHK4   
   5 4978 ...RECHK3  
   8 497* ...      LDN   6  XBMSH
  18 497G ...      MBSXD    7,BCGDEDIC          [SET UP YET ANOTHER MASK 
   7 497M ...      LDX   0  4
   7 497S ...      ANDX  0  7
  20 497_ ...      BNZ   0  XAPIA               [ILLEGAL TO HAVE SWAP ON DEDIC   
   5 4986 ...RECHK4  
   8 498? ...      LDX   1  FX1  
   9 498D ...      LDX   0  XA1+3(3) 
  17 498K ...      ANDX  0  XMASK2(1)           [ISOLATE SHFS BITS   
  16 498Q ...      ORS   0  XA1+3(2)            [TO /HORSE BLOCK 
  10 498X ...      LDX   0  XA1+3+XREC(3)
  17 4994 ...      ANDX  0  XMASK2(1)           [ISOLATE SHFS BITS   
  16 4999 ...      ORS   0  XA1+3+XREC(2)       [TO /HORSE BLOCK 
  18 499B ...      TRANSBEG FSHCGID,MOVCASH,3,BSTB,HORSE,ACOMMUNE1,XBDOWN
   8 499H ...      BRN      XBERR
  10 499N ...      MHUNTW   3,BSTB,CART  
   9 499T ...      LDX   2  XA1+4(3) 
  10 49=2 ...      LDX   1  XA1+4+XREC(3)
   5 49=7 ...NOWAY   
   4 49=# ...)   
   9 49?=    X12   LDX   5  BSUNIT(2)
  21 49QW          BNG   5  X10                 [CO-ORDINATE IF GRUESOME IN TRANSIT  
   9 4==G          LDX   5  BSUNIT(1)
  16 4=Q6          BPZ   5  X11                 [ON EITHER DEVICE
   9 4?9Q ...X10   COOR3    ENDGRU   
  17 4?PB          MHUNTW   3,BSTB,CART         [REFIND BUFFER BLOCK 
   9 4#92          LDX   2  XA1+4(3) 
  18 4#NL          LDX   1  XA1+9(3)            [LOAD POINTERS TO APIAS  
   8 4*8=          BRN      X12  
   9 4*MW    X11   LDN   5  BNOENG   
  21 4B7G          ORS   5  BSUNIT3(2)          [TELL GRUESOME TO IGNORE ANY ENGAGE..
  19 4BM6          ORS   5  BSUNIT3(1)          [..BUTTONS FOR THE TIME BEING
   7 4C6Q          LDX   1  3
   9 4CLB          LDN   4  CGINPROG 
   7 4D62          LDN   5  2
  20 4DKL    X13   LDX   7  XA1+3(3)            [LOAD BACKING STORE UNIT NUMBER  
   8 4DQS ...      FSHCODE  AORB 
  17 4DY2 ...      ANDN  7  #7777               [MASK OUT SHFS BITS  
   9 4F5=          MPY   7  ASBSAB   
   7 4FJW          NGX   2  0
  17 4G4G          ADX   2  BBAC                [FIND RELEVANT BSA   
   9 4GJ6          ADX   2  ASBSAB   
  21 4H3Q          ORS   4  XBSA(2)             [TELL BSA TO HOLD UP ALL FILESTORE   
  15 4HHB          JDISENG  XA1(3),XDIS         [TRANSFERS   
  17 4J32    X22   JBUSY     XA1(3),X22         [WAIT UNTIL NON BUSY 
  19 4JGL          DISENGPER XA1(3)             [DISENGAGE DEVICE IF ENGAGED 
  10 4K2=    X23   JENG     XA1(3),X23   
  17 4KFW          LINT     XA1(3),0            [GET INTERNAL NUMBER 
   8 4K_G          LDN   4  24   
  19 4LF6          DVS   7  4                   [CONVERT INTERNAL NUMBER...  
  16 4LYQ          LDCT  4  256                 [...TO A BUTTON  
   7 4MDB          SMO      7
   7 4MY2          SRL   4  0
   7 4NCL          LDX   7  0
  18 4NX=          BUTTONUP 4,7                 [SET INTERRUPT BIT IN EBA
   9 4PBW          LDN   4  CGINPROG 
   9 4PWG    XDIS  LDX   0  XA1+2(3) 
  21 4Q25 ...      BNZ   0  X14                 [BRANCH IF CARTRIDGE CONTAINING RESID
   9 4Q5N ...      LDX   6  XA1(3)   
   8 4Q9? ...      FSHCODE   AORB
   4 4Q#W ...(   
   8 4QDF ...      SMO      FX2  
   9 4QJ4 ...      STO   6  AWORK3   
   4 4QMM ...)   
  16 4QR= ...      STO   2  XA1+2(3)            [POINTER TO BSA  
   8 4QWT ...X15   ADN   3  XREC 
  18 4R2D ...      BCT   5  X13                   [LOOK AT OTHER DEVICE  
   8 4R63 ...      BRN      X16  
  17 4R9L ...X14   STO   0   XSER(1)            [REMEMBER SERIAL NO  
   8 4R*B          LDCT  0  256  
   7 4RC_ ...#UNS  FSHTEST   
   4 4RGJ ...(   
  20 4RK7 ...      SMO      XA1+4(3)            [***  TESTING AID ON ONE M/C *** 
   9 4RMQ ...      ORS   0  BSUNIT2  
   4 4RQ* ...)   
  19 4RT2          SMO      XA1+4(3)            [CLEAR IN USE BY SYSTEM BIT  
   9 4S#L          ERS   0  BSUNIT2  
  16 4SS= ...      STO   2  XA1+2(3)              [POINTER TO BSA
  21 4T?W ...      ORS   0  XA1+2(3)               [MKR FOR BSA ORIGINALLY REFERRING 
   8 53M2          BRN      X15  
  16 53NY ...#   NOW OUTPUT MESSAGE REQUESTING RESIDENCE TO BE   
  12 53QW ...#      LOADED ON ALTERNATIVE DRIVE  
   4 53SS ...#   
  10 53WQ ...X16   GSCAN    XSER(1),CGCOM
  10 53YN ...XWAIT LONGSET   CGENG,XSET  
  17 542L ...      CALL  7  XOUTB                [CREATE PARAM BLOCK 
   9 544J ...      CALL  7  XOUTCSN  
  17 546G ...      LGEOG    6,4                  [OPS UNIT NO TO X4  
   9 548D ...      CALL  7  XOUTOPNO 
  15 54=B ...      DEMAND   1,BMVLOAD,XCANTDO,XBRKIN,XRENT,2 
   9 54## ...      JENG     6,XENG   
  10 54B= ...      LONGSET  CGENG,XSET   
  20 54D8 ...      BRN      XRENT                [RE-ENTER IF STILL NOT ENGAGED  
  18 54G6 ...XENG  CALL  7  XDQUIT                  [FREE DEMAND BLOCKS  
  10 54GQ ...      MHUNTW   3,BSTB,CART  
  15 54HB ...      LOCKC    3,XFIND              [LOCK BLOCK 
  17 54J4 ...      MHUNTW   3,BSTB,CART         [REFIND BUFFER BLOCK 
   8 54L=          LDN   4  128  
  17 555W          DCA      6,STO,4,COUNT       [COUNT TO DEVICE LIST
   7 55KG          LDN   4  1
  19 55PC ...      JENVNOT  XADDR,MEEP          [J IF 1900 OR DME ENVIRONMENT
  18 55T# ...      ORX   4  GSIGN               [SET BO OF ADDR FOR EDEM 
   5 55_9 ...XADDR   
  20 5656          DCA      6,STO,4,DTSA        [HARDWARE ADDRESS TO DEVICE LIST 
  10 56JQ          LDN   4  XBUFSTART(3) 
  20 574B          DCA      6,STO,4,CTSA        [BUFFER ADDRESS TO DEVICE LIST   
   7 57J2          LDN   4  0
  17 583L          DCA      6,DLA,4,MODE        [MODE TO DEVICE LIST 
  16 592W          LOCKC    2,/                 [LOCK ACTIVITY   
  14 59GG          GPERI    6,2                 [DO PERI 
  15 5=26          COOR1                        [WAIT FOR E0T
   7 5=FQ          UNLOCK   2
  17 5=_B          UNLOCK   3                   [UNLOCK BUFFER BLOCK 
   9 5?F2          LDCH  4  CPRW1(2) 
  18 5?YL          BZE   4  X17                 [BRANCH IF TRANSFER OK   
   8 5#D=          BPZ   4  X18  
  17 5#XW          GEOERR   1,CARTBUSY          [DEVICE STILL BUSY!  
  18 5*CG    X18   LGEOG    6,4                 [GET GEOGRAPHICAL NUMBER 
   9 5*X6 ...      CALL  7  XOUTOPNO 
  16 5CTL          INFORM   1,EWFAIL,1          [UNIT NN FAILED  
   8 5D*=          BRN      X20F 
   7 5DSW    X17   LDN   0  1
  10 5F#G          SBX   0  XBUFSTART(3) 
  17 5FS6          BNZ   0  X18                 [BLOCK ADDRESS OK?   
  11 5G?Q          LDX   0  XBUFSTART+1(3)   
  18 5GRB          BZE   0  X19                 [BRANCH IF NO CHECKSUM   
  10 5H?2          LDN   1  XBUFSTART(3) 
   8 5HQL          SUM   0  128  
  18 5J==          BNZ   0  X18                 [BRANCH IF CHECKSUM FAIL 
  11 5JPW    X19   LDX   7  XBUFSTART+5(3)   
   8 5K9G          SMO      FX1  
   9 5KP6          ANDX  7  XMASK1   
  17 5L8Q          TXU   7  XSER(3)             [CORRECT CARTRIDGE?  
   8 5LNB          BCS      X20  
   8 5LPF ...      FSHCODE  AORB 
   4 5LQJ ...(   
   9 5LRM ...      LDX   0  AWORK1(2)
  17 5LSQ ...      BPZ   0  RUNIN               [NO CHECKS REQUIRED  
  18 5LTT ...      STO   6  AWORK2(2)           [PRESERVE D.L. POINTER   
  20 5LXF ...      TRANSBEG FSHCGID,MOVCASH,4,BSTB,HORSE,ACOMMUNE1,UNIMP,AUT,SUP 
  18 5L_6 ...      LDX   6  AWORK2(2)           [REINSTATE D.L. POINTER  
   5 5M29 ...UNIMP   
  10 5M3# ...      MHUNTW   3,BSTB,CART  
  15 5M4C ...      LDX   7  XSER(3)             [RESET CSN   
   5 5M5G ...RUNIN   
   4 5M6K ...)   
  17 5M82          LDN   5  CGINPROG            [CG IN PROGRESS BIT  
   9 5MML          LDX   1  XA1+2(3) 
  20 5N7=          LDX   4  XA1+5(3)            [NOW TO SWOP OVER BORING CPPTRS  
   9 5NLW          STO   4  CPPTR(1) 
  20 5NQ2 ...      LDX   4  AWORK4(1)            [AND SWAP TRANSFER COUNTS TOO   
   9 5NT6 ...      SMO      XA1+7(3) 
   9 5NY= ...      LDX   0  AWORK4   
   9 5P3B ...      STO   0  AWORK4(1)
   7 5P4C ...#UNS  FSHTEST   
  20 5P5D ...      ORS   5  XBSA(1)             [***   TESTING AID ON ONE M/C ***
   9 5P6G    #SKI  K6MOVCART>199-199 
   4 5PL6    (   
  19 5Q5Q          LDX   0  XBSA(1)             [CHECK IN PROGRESS BIT SET   
   7 5QKB          ANDX  0  5
   8 5R52          BNZ   0  X20A 
   9 5RJL    X20B  GEOERR   1,CARTBIT
   4 5S4=    X20A
   4 5SHW    )   
   9 5SQ6 ...      LDN   0  BKARTWT  
  20 5SYB ...      ANDX  0  XBSA(1)             [CLEAR WT FOR KARTCONT BIT IF SET
   7 5T6L ...      ORS   5  0
  19 5T#W ...      ERS   0  XBSA(1)             [AND CG IN PROG BIT IN BSA   
   9 5TH6          LDX   1  XA1+7(3) 
   9 5TQY ...      STO   4  AWORK4(1)
   9 5W2Q          LDX   4  XA1(3)   
   9 5WGB          STO   4  CPPTR(1) 
   7 5WMJ ...#UNS  FSHTEST   
  20 5WSQ ...      ORS   5  XBSA(1)             [***   TESTING AID ON ONE M/C ***
   9 5X22    #SKI  K6MOVCART>199-199 
   4 5XFL    (   
   9 5X_=          LDX   0  XBSA(1)  
   7 5YDW          ANDX  0  5
   8 5YYG          BZE   0  X20B 
   4 5_D6    )   
   9 5_LB ...      LDN   0  BKARTWT  
  20 5_SL ...      ANDX  0  XBSA(1)             [CLEAR WT FOR KARTCONT BIT IF SET
   7 622W ...      ORS   5  0
  19 6296 ...      ERS   0  XBSA(1)             [AND CG IN PROG BIT IN BSA   
   6 62CB    #SKI  CRAB  
   4 62X2    (   
  21 63BL          LDX   0  XA1+5(3)            [FOR PM PURPOSES JSWAPDR POINTS TO   
  21 63W=          TXU   4  JSWAPDR             [ THE DEVICE RECORD FOR THE LEVICE   
  20 64*W          BCS      X30A                [ ON WHICH THE LARGEST SWAP FILE 
  19 64TG          STO   0  JSWAPDR             [ IS KEPT, SO BOTH CPPTRS ARE
  20 65*6          BRN      X31A                [  CHECKED TO ENSURE THAT JSWAPDR
  18 65SQ    X30A  TXU   0  JSWAPDR             [  STILL POINTS TO THE   
  17 66#B          BCS      X31A                [     CORRECT DEVICE 
  15 66S2          STO   4  JSWAPDR             [     RECORD 
   4 67?L    X31A
   4 67R=    )   
   2 68=W
  17 68QG          LADDP    6,1                 [GET POINTER TO APIA 
   8 69=6          LDCT  4  256  
  18 69PQ          ORS   4  BSUNIT2(1)          [SET IN USE BY SYSTEM BIT
  15 6=9B          STO   7  BSUNIT5(1)          [REMEMBER CSN
  10 6=P2          LDN   4  BCGCOM+BNOENG
   9 6?8L          LDX   1  XA1+4(3) 
  19 6?N=          LDX   5  XA1+8(3)            [SWOP OVER BSUNS IN APIAS... 
   8 6?TD ...      FSHCODE  AORB 
  17 6#2L ...      ANDN  5  #7777               [MASK OUT SHFS BITS  
  19 6#7W          DEX   5  BSUNIT(1)           [AND CLEAR MOVCART MARKERS   
   7 6#*4 ...#UNS  FSHTEST   
  20 6#G= ...      ORS  4  BSUNIT3(1)          [***   TESTING AID ON ONE M/C *** 
   9 6#MG    #SKI  K6MOVCART>199-199 
   4 6*76    (   
  10 6*LQ          LDX   0  BSUNIT3(1)   
  19 6B6B          ANDN  0  BCGCOM+BNOENG       [CHECK MOVCART MARKERS SET   
  10 6BL2          SBN   0  BCGCOM+BNOENG
   8 6C5L          BZE   0  X20C 
  10 6CK=    X20D  GEOERR   1,CGAPIABT   
   4 6D4W    X20C
   4 6DJG    )   
  10 6F46          ERS   4  BSUNIT3(1)   
   9 6FHQ          LINT     XA1(3),2 
   8 6G3B          LDN   7  24   
  18 6GH2          CALL  6  XBUTT               [LET GRUESOME INTERROGATE
   9 6H2L          LDX   1  XA1+9(3) 
   9 6HG=          LDX   5  XA1+3(3) 
   8 6HMD ...      FSHCODE  AORB 
  17 6HSL ...      ANDN  5  #7777               [MASK OUT SHFS BITS  
   9 6H_W          DEX   5  BSUNIT(1)
   7 6J74 ...#UNS  FSHTEST   
  20 6J#= ...      ORS   4  BSUNIT3(1)          [***   TESTING AID ON ONE M/C ***
   9 6JFG    #SKI  K6MOVCART>199-199 
   4 6J_6    (   
  19 6KDQ          LDX   0  BSUNIT3(1)          [CHECK MOVCART MARKERS SET   
  10 6KYB          ANDN  0  BCGCOM+BNOENG
  10 6LD2          SBN   0  BCGCOM+BNOENG
   8 6LXL          BNZ   0  X20D 
   4 6MC=    )   
  10 6MWW          ERS   4  BSUNIT3(1)   
  10 6NBG          LINT     XA1+5(3),2   
  18 6NW6          CALL  6  XBUTT               [LET GRUESOME INTERROGATE
   5 6P5Y ...TIDYUP  
  15 6P*Q          FON      CARTWAIT            [WAKE UP BSAS
  10 6PTB          MHUNTW   3,BSTB,CART  
  16 6Q*2          FREECORE 3                   [FREE CART BLOCK 
   8 6QB5 ...      FSHCODE  AORB 
   4 6QC8 ...(   
  10 6QD? ...      HUNTW    3,BSTB,HORSE 
   8 6QFB ...      BNG   3  XNOGG
   7 6QGF ...      FREECORE 3
   5 6QHJ ...XNOGG   
   4 6QR9 ...)   
  10 6QRC ...      FSHCODE  A,XNOPRINB   
   4 6QRK ...(   
  21 6QRR ...      TESTUNPR XNOPRINB            [CHECK IF THERE'S A PAIR IN M/C 'B'  
  18 6QR_ ...      FSHKILL                      [MURDER IT IF THERE IS ! 
   5 6QS7 ...XNOPRINB
   4 6QS* ...)   
  16 6QSL          ENDCOM                       [COMMIT SUICIDE  
  19 6R#=    XBUTT DVS   1  7                   [CONVERT INTERNAL NUMBER...  
  16 6RRW          LDCT  5  256                 [...FOR BUTTONUP 
   8 6S?G          SRL   5  0(1) 
   8 6SR6          BUTTONUP 5,2  
   7 6T=Q          EXIT  6  0
  18 6TQB    X20   LGEOG    6,4                 [GEOGRAPHICAL NO.TO X4   
  16 6W=2    X20F  DISENGPER 6                  [DISENGAGE DEVICE
   8 6WPL    X24   JENG     6,X24
   8 6X9= ...      CALL  7  XOUTB
   9 6XNW ...      CALL  7  XOUTOPNO 
   9 6Y8G ...      CALL  7  XOUTCSN  
  10 7=DL          INFORM   1,BMVRONG,1  
   8 7=DT ...      BRN      XWAIT
  11 7=F4 ...#      SET UP PARAMETER BLOCK   
   5 7=F? ...XOUTB   
   7 7=FG ...      LDN   0  4
   8 7=FK ...      SBX   7   FX1 
   8 7=FP ...      OUTBLOCK  0   
   8 7=FS ...      ADX   7   FX1 
   7 7=FY ...      EXIT  7  0
  10 7=G7 ...#      OUTPACK CSN PARAMETER
   5 7=GB ...XOUTCSN 
   8 7=GF ...      SBX   7   FX1 
  10 7=GK ...      MHUNTW   3,BSTB,CART  
  11 7=GS ...      OUTPACK   XSER(3),1,CSN   
   8 7=GX ...      ADX   7   FX1 
   7 7=H3 ...      EXIT  7  0
  12 7=H= ...#     OUTPACK GEOG UNIT NO PARAMETER
   5 7=HF ...XOUTOPNO
   8 7=HJ ...      SBX   7   FX1 
  10 7=HN ...      OUTPACK  4,1,GEOPER   
   8 7=HR ...      ADX   7   FX1 
   7 7=HX ...      EXIT  7  0
   4 7=J6 ...#   
   5 7=J* ...XDQUIT  
   7 7=JJ ...      DEMANDQUIT
   7 7=JR ...      LONGOFF   
   7 7=K2 ...      EXIT  7  0
   4 7=K9 ...#   
  17 7=KD ...#     HERE IF OPERATORS RESPOND TO DEMAND WITH CANTDO.  
  15 7=KM ...#     I.E. CARTRIDGE COMMAND MUST BE CANCELLED. 
   4 7=KW ...#   
   5 7=L5 ...XCANTDO 
  19 7=L# ...      CALL  7  XDQUIT                     [FREE DEMAND BLOCKS   
   9 7=LH ...      FSHCODE    AORB   
   4 7=LQ ...(   
  19 7=L_ ...      TRANSBEG  FSHCGID,MOVCASH,1,BSTB,HORSE,ACOMMUNE1,XUNIMP   
  16 7=M8 ...      BRN      XBERR                  [ERROR JUMP   
   5 7=MC ...XUNIMP  
   4 7=ML ...)   
  10 7=MT ...      MHUNTW     3,BSTB,CART
   7 7=N4 ...      LDN   4  2
   5 7=N? ...REPEAT  
   9 7=NG ...      LDN   6  CGINPROG 
  10 7=NP ...      LDN   7  BNOENG+BCGCOM
  16 7=NY ...      LDX   1  XA1+4(3)              [PTR TO APIA   
   8 7=P7 ...#UNS        FSHTEST 
  21 7=PB ...      ORS   7  BSUNIT3(1)                [*** TESTING IN ONE MACHINE ***
  21 7=PK ...      ERS   7  BSUNIT3(1)             [UNSET IGNORE ENGAGE EVENTS BIT AN
  18 7=PS ...#                                      [CG IN PROGRESS BIT  
  19 7=Q3 ...      LDX   2  XA1+2(3)                         [POINTER TO BSA 
  20 7=Q= ...      BPZ   2  NOTRES                  [_ IF NOT UNIT CONTAINING RES
   8 7=QF ...      LDCT  7  #400 
  20 7=QN ...      ORS   7  BSUNIT2(1)               [RESET IN USE BY SYSTEM BIT 
   9 7=QX ...      LDX   1  XA1(3)   
  21 7=R6 ...      LGEOG     1,7                    [PRESERVE GEOG UNIT NO FOR MESSAG
   8 7=R* ...      SMO      FX2  
   9 7=RJ ...      STO   7  ACOMMUNE1
   5 7=RR ...NOTRES  
   8 7=S2 ...#UNS        FSHTEST 
  21 7=S9 ...      ORS   6  XBSA(2)                   [*** TESTING IN ONE MACHINE ***
  20 7=SD ...      ERS   6  XBSA(2)                  [UNSET CG IN PROGRESS BIT   
   9 7=SM ...      LINT     XA1(3),2 
   8 7=SW ...      LDN   7  24   
   8 7=T5 ...      CALL  6  XBUTT
   8 7=T# ...      ADN   3  XREC 
   9 7=TH ...      BCT   4  REPEAT   
   8 7=TQ ...      CALL  7  XOUTB
   9 7=T_ ...      CALL  7  XOUTCSN  
   8 7=W8 ...      SMO      FX2  
   9 7=WC ...      LDX   4  ACOMMUNE1
   9 7=WL ...      CALL  7  XOUTOPNO 
  10 7=WT ...      INFORM   1,BMVCANC,1  
   9 7=X4 ...      BRN      TIDYUP   
   8 7=_5 ...      FSHCODE  AORB 
   4 7=_Y ...(   
   5 7?29 ...XBGONE  
  18 7?2G ...      FSHACCWK                     [ RESTORE ACCS AND AWORKS
   5 7?2R ...XBDOWN  
  12 7?2W ...      LDX   0  GSIGN               [
  20 7?2_ ...      SMO      FX2                 [  CLEAR 'NEED TO T/BEG' MARKER  
  12 7?34 ...      ERS   0  AWORK1              [
  17 7?37 ...      VFREEW   BSTB,HORSE          [ FREE M/C 'B' BLOCK 
  17 7?3= ...      MFREEW   FSH,FSHAMBLES       [ AND CLOSEDWN BLOCK 
  18 7?3* ...      BRN      NOWAY               [ CARRY ON REGARDLESS !!!
   5 7?3L ...XBERR   
  10 7?4F ...      MHUNTW   3,BSTB,CART  
  18 7?4Q ...      LDX   0  GSIGN            [SET MKR -VE TO INDICATE X6 
  21 7?53 ...      ORS   0  6                     [ALREADY CONTAINS MESSAGE ADDRESS  
   5 7?5# ...XAPIA   
  10 7?67 ...      HUNTW    2,BSTB,HORSE 
   8 7?72 ...      BNG   2  XNOH 
   7 7?7T ...      FREECORE 2
   4 7?8N ...XNOH
   9 7?9H ...      LDX   2  XA1+4(3) 
  10 7?=B ...      LDX   1  XA1+4+XREC(3)
   4 7??9 ...XXCG
   9 7?#4 ...      LDN   5  BCGCOM   
  18 7?#X ...      ERS   5  BSUNIT3(1)          [CLEAR CG IN PROGRESS BIT
  18 7?*Q ...      ERS   5  BSUNIT3(2)          [  ^   ^  ^     ^      ^ 
   4 7?BK ...)   
   4 7?CW    XG3 
   4 7?XG    XINV
   5 7#C6    XINCOM  
   5 7#WQ    XWRTYP  
   5 7*BB    XSAME   
   5 7*W2    XFILE   
  16 7B*L    XCG   FREECORE 3                   [FREE CART BLOCK 
   7 7BGS ...      LDXC  6  6
  21 7BN2 ...      BCS      XCG1               [_ IF ALREADY CONTAINS MESSAGE ADDRESS
   8 7BT=          ADX   6  FX1  
   7 7C#W          SMO      6
  17 7CSG          LDX   6  0                   [ADDRESS OF MESSAGE  
   4 7CTK ...XCG1
   9 7CWP ...      FSHCODE  A,XNOBPR 
   4 7CYY ...(   
   9 7D37 ...      TESTUNPR XNOBPR   
   7 7D5B ...      FSHKILL   
   5 7D7K ...XNOBPR  
   4 7D9S ...)   
   7 7D#6          COMERRX  6
  15 7DRQ    XFIND MHUNTW   2,BSTB,CART         [REFIND CART 
   7 7F?B          EXIT  1  0
   9 7F?N ...XBSW  LDEX  4  XA1+1(3) 
   9 7F#2 ...      CALL  7  XOUTOPNO 
  20 7F## ...      LDX   6  XWAITSWIT(1)              [UNIT WAITING TO BE SW OUT 
  10 7F#L ...      MHUNTW   3,BSTB,CART  
   8 7F#Y ...      LDX   0  GSIGN
   7 7F*= ...      ORS   0  6
   8 7F*J ...      BRN      XCG  
   6 7F*_ ...QK2MOVCART  
   6 7FDJ ...QK3MOVCART  
   6 7FH7 ...QK4MOVCART  
  18 7FKQ ...      GEOERR   1,MOVCASH               [CODE NOW IN MOVCASH 
  20 7FN* ...XSET  GEOERR   1,LONGSET               [LONG WAIT FIELDS ALREADY SET
  10 7FR2    XBRKIN GEOERR  1,BRKINOPS   
   4 7G=L    #   
  10 7GQ=          MENDAREA 50,K99MOVCART
   4 7H9W    #END
   6 ____ ...464677010001
