  15 22FL    #SEG  SETLIST70                    [JOHN BAILEY 
  11 22_=    #LIS  K0SETLIST>K0EMS>K0ALLGEO  
   7 23DW          8HSETLIST 
   4 23YG    #   
  12 24D6          SEGENTRY K1SETLIST,QK1SETLIST 
  12 24MY ...      SEGENTRY K2SETLIST,SWAPFREE   
   4 24XQ    #   
   5 25CB    #SKIP G4
   4 25X2    (   
  16 26BL     SEGENTRY K99SETLIST     [USED IN SWAPSPACE MACRO.  
   8 26W=                   +0   
   8 27*W    MAXLFN         +16  
  12 27TG    MAXBSA      #4001                  [
   4 28*6    )   
   7 28B9 ...#UNS ANOTALLOC  
   4 28C# ...(   
   7 28DC ...      FSHSKIP  B
   4 28FG ...(   
   4 28GK ...#   
  11 28HN ...#     TABLE FOR NOTALLOC MACRO  
   4 28JR ...#   
   9 28KW ...      FIXTRA   ANOTALRES
   8 28L_ ...               +0   
   8 28N4 ...               +0   
   8 28P7 ...               +0   
   4 28Q= ...)   
   4 28R* ...)   
   8 28RS ...MINWELL        +50  
   8 28S? ...MAXWELL        +500 
   6 28SQ    QK1SETLIST  
  21 29#B    #                                  CHECK SHEET SIZE, ALTER IF NECCESSARY
  16 29BK ...      JENVNOT  NOTMEEP,MEEP        [ J. IF NOT MEEP 
  15 29DS ...      LDX   0  BSSS                [ SHEET SIZE 
  15 29H3 ...      SBN   0  1024                [ CHECK IF 1K
  15 29K= ...      BZE   0  NOTMEEP             [ J. IF IT IS
  18 29L* ...      LDN   0  1024                [ ELSE RESET SHEETSIZE   
  17 29MD ...      STO   0  BSSS                [ TO BE 1K FOR MEEP  
  18 29NH ...      MONOUT   BMWRNGSHSZ          [ AND INFORM OPERATORS   
   5 29PN ...NOTMEEP 
   7 29S2          NGN   6  1
   7 2=?L          NGN   7  1
  17 2=R=          STOZ     AWORK1(2)           [USED TO HOLD LFN.   
  17 2?=W          LDX   3  ALFTP               [POINTER TO ALFTABLE.
  10 2?QG          LDX   5  ALOGLEN(3)   
   7 2#=6          ADX   5  3
   8 2#PQ    PA    LDX   1  A1(3)
  20 2*9B          BNG   1  PB                  [JUMP IF THIS FILE NON EXISTENT. 
   8 2*P2          LDCT  4  #200 
   8 2B8L          ANDX  4  A1(3)
  18 2BN=          BZE   4  PB                  [JUMP IF NOT SWAP FILE.  
   7 2C7W          LDN   6  0
   8 2CMG          ANDN  1  #7777
  20 2D76          ADX   1  AFDTP               [X1 POINTS TO FILE DESCRIPTION.  
   8 2DLQ          LDCH  0  A1(1)
  20 2F6B          LDX   4  A1+2(1)             [WORDS OR BLOCKS IN FILE IF DRUM 
   7 2FL2          SBN   0  9
  17 2G5L          BZE   0  PC                  [JUMP IF 1964 DRUM   
   8 2GK=          SBN   0  16   
  15 2H4W          BZE   0  PCA                 [JUMP IF 2851
  17 2HJG          LDX   4  A1+4(1)             [SEGMENTS/CYLINDER   
  16 2J46    PCA   SLL   4  7                   [CONVERT TO WORDS
   8 2JHQ    PC    TXL   4  BSSS 
  21 2K3B          BCC      PB                  [JUMP IF SHEET < FILE OR SHEET AREA. 
  21 2KH2          STO   4  BSSS                [OTHERWISE THIS IS NEW SHEET SIZE.   
  15 2L2L          LDN   7  0                   [SET  MARKER.
   9 2LG=          LDX   0  AWORK1(2)
  19 2L_W          STO   0  AWORK2(2)           [SAVE  LFN  TO OUTPUT LATER. 
  15 2MFG    PB    LDN   0  1                   [UPDATE L.F.N
   9 2M_6          ADS   0  AWORK1(2)
   7 2NDQ          ADN   3  2
   7 2NYB          TXL   3  5
  19 2PD2          BCS      PA                  [JUMP IF NOT END OF BLOCK.   
   8 2PXL          BZE   6  PD   
  10 2QC=          GEOERR   1,SWAPFILE   
   8 2QWW    PD    BNG   7  PE   
  10 2RBG    #   MESSAGE JSTLST1 OUTPUT  
   7 2RW6          LDN   4  4
  18 2S*Q          OUTBLOCK  4                  [CREATE PARAMETER BLOCK  
   8 2STB          LDX   4  BSSS 
   8 2T*2          SRL   4  10   
   8 2TSL          OUTNUM  4,0   
  13 2W#=          LDX   4  AWORK2(2)           [LFN 
   8 2WRW          OUTNUM  4,0   
  17 2X?G          INFORM  1,JSTLST1,1          [OUTPUT THE MESSAGE  
  16 2XR6    #     EACH LOGICAL FILE IS CHECKED TO ENSURE THAT:- 
  17 2Y=Q    #   1 FOR DRUM -FILE SIZE IS A MULTIPLE OF SHEET SIZE   
  21 2YQB    #   2 FOR EDS/FDS NUMBER OF WORDS/CYLINDER IS A MULTIPLE OF SHEET SIZE  
  20 2_=2    #     IF NOT THE FILE DESCRIPTION (FDTABLE) IS ALTERED AND A MESSAGE
  20 2_PL    #     IS OUTPUT TO TELL THE OPERATORS HOW MUCH OF THE FILE HAS BEEN 
   7 329=    #     REMOVED   
   4 32NW    #   
   8 338G    PE    LDX   7  BSSS 
   8 33N6          LDX   3  ALFTP
  10 347Q          LDX   5  ALOGLEN(3)   
   7 34MB          ADX   5  3
   9 3572          STO   5  AWORK1(2)
   9 35LL          STOZ     AWORK2(2)
   8 366=    XA    LDX   2  A1(3)
  18 36KW          BNG   2  XK                  [J. IF FILE NONEXISTENT. 
   8 375G          LDCT  4  #200 
   8 37K6          ANDX  4  A1(3)
  17 384Q          BZE   4  XK                  [J. IF NOT SWAP FILE.
   8 38JB          ANDN  2  #7777
  21 3942          ADX   2  AFDTP               [POINTER  TO FILE DESCRIPTION TABLE. 
   8 39HL          LDCH  6  A1(2)
   7 3=3=          SBN   6  9
  15 3=GW          BZE   6  XC                  [J. IF DRUM. 
   8 3?2G          SBN   6  16   
  15 3?G6          BZE   6  XCA                 [JUMP IF 2851
   9 3?_Q          LDX   4  A1+4(2)  
  16 3#FB          SLL   4  7                   [WORDS/SEEK AREA 
   7 3#_2    XB    SBX   4  7
  21 3*DL          BZE   4  XF                  [J. IF SHEET SIZE FACTOR OF FILEWIDTH
   8 3*Y=          BPZ   4  XB   
   7 3BCW          ADX   4  7
   7 3BXG          SRL   4  7
  19 3CC6          SBS   4  A1+4(2)             [UPDATE SEGMENTS/SEEK AREA   
  18 3CWQ          MPY   4  A1+3(2)             [NUMBER OF BLOCKS LOST   
  18 3DBB          SBS   5  A1+1(3)             [UPDATE BLOCKLETS COUNT  
   7 3DW2          LDX   0  5
  16 3F*L          SRL   0  3                   [X0= K WORDS LOST
   8 3FT=          BRN      XE1  
  16 3G#W    XCA   LDX   0  A1+2(2)             [BLOCKS IN FILE  
  14 3GSG          SLL   0  7                   [WORDS   
   7 3H#6          LDX   4  0
   7 3HRQ    XCB   SBX   0  7
  20 3J?B          BZE   0  XF                  [JUMP IF FILE SIZE  MULTIPLE OF  
   8 3JR2          BPZ   0  XCB  
  17 3K=L          ADX   0  7                   [X0 HOLDS WORDS LOST 
  19 3KQ=          SBS   0  4                   [X4 -NEW FILE SIZE IN WORDS  
  16 3L9W          SRL   4  7                   [BLOCKS IN FILE  
  16 3LPG          STO   4  A1+2(2)             [UPDATE FDTABLE  
  18 3M96          SRL   0  10                  [UNITS OF 1K FOR MESSAGE 
   8 3MNQ          BRN      XE   
  15 3N8B    XC    LDX   0  A1+2(2)             [DRUM PART.  
   7 3NN2    XD    SBX   0  7
  21 3P7L          BZE   0  XF                  [J. IF SHEET SIZE FACTOR OF FILE SIZE
   8 3PM=          BPZ   0  XD   
   7 3Q6W          ADX   0  7
  18 3QLG          SBS   0  A1+2(2)             [UPDATE WORDS/SEEK AREA  
  17 3R66          SRL   0  10                  [UNITS OF 1K WORDS.  
   9 3RKQ          LDX   4  A1+2(2)  
   7 3S5B          SRL   4  7
  20 3SK2    XE    STO   4  A1+1(3)             [UPDATE BLOCKETS CT. IN ALFTABLE.
   8 3T4L    XE1   SMO      FX2  
   9 3TJ=          STO   0  AWORK3   
  10 3W3W    #  MESSAGE JSTLST2  OUTPUT  
   7 3WHG          LDN   4  4
  18 3X36          OUTBLOCK  4                  [CREATE PARAMETER BLOCK  
  13 3XGQ          LDX   4  AWORK2(2)           [LFN 
   8 3Y2B          OUTNUM  4,0   
  17 3YG2          LDX   4  AWORK3(2)           [AMOUNT REDUCED IN K 
   8 3Y_L          OUTNUM  4,0   
  16 3_F=          INFORM  1,JSTLST2,1          [OUTPUT MESSAGE  
   8 3_YW    XF    LDX   7  BSSS 
   8 42DG    XK    LDX   2  FX2  
   7 42Y6          LDN   0  1
  15 43CQ          ADS   0  AWORK2(2)           [UPDATE LFN. 
   7 43XB          ADN   3  2
   9 44C2          TXL   3  AWORK1(2)
  18 44WL          BCS      XA                  [BACK TO DO IT ALL AGAIN.
  17 45B=          LDX   3  ALFTP               [POINTER TO ALF TABLE
   7 45TW          LDN   7  0
   9 46*G    #SKI  K6SETLIST>499-499 
   8 46T6          TRACE    3,ALF
   6 46XP ...#UNS FSHTEST
   4 472# ...#SKI
   4 474X ...(   
   7 477G ...      FSHSKIP  B
   4 47=5 ...(   
  18 47#Q ...T2    BXGE  7  ALOGLEN(3),ZSWSP    [J. IF END OF ALF TABLE. 
   4 47BN ...)   
   4 47DL ...)   
  10 47GJ ...      FSHCODE  B,XFSHBBSFREE
   4 47JG ...(   
  18 47LD ...T2    BXGE  7  ALOGLEN(3),SWAPFREE [J IF END OF /ALFTABLE   
   6 47NB ...XFSHBBSFREE 
   4 47Q# ...)   
   9 47SB    #SKI  K6SETLIST>599-599 
   9 48#2          TRACE    7,ALF MOD
   7 48RL          SMO      7
   8 49?=          LDX   4  A1(3)
  20 49QW          BNG   4  T6                  [J. IF LOGICAL FILE NON-EXISTENT.
   8 4==G          LDX   5  BSBS4
   6 4=?2 ...#UNS FSHTEST
   4 4=?G ...(   
  10 4=#2 ...      FSHCODE  A,NOTINMCA   
   4 4=#G ...(   
   7 4=*2 ...      LDX   0  7
  16 4=*G ...      SBN   0  8                   [ CHECK IF RES. 4
  21 4=B2 ...      BZE   0  T6                  [ OMIT RES 4 IM M/C A IN TEST ENVIR. 
   5 4=BG ...NOTINMCA
   4 4=C2 ...)   
   4 4=CG ...)   
  10 4=D2 ...      FSHCODE  B,XFSHBBS1A  
   4 4=DG ...(   
   8 4=F2 ...      LDCT  0  #200 
   7 4=FG ...      SMO      7
   8 4=G2 ...      ANDX  0  A1(3)
  16 4=GG ...      BNZ   0  XFSHBBS1            [J IF SWAP FILE  
  21 4=H2 ...      LDN   5  FSHBSBLOCKS         [64 BLOCKS FOR FILESTORE RESIDENCE   
   5 4=HG ...XFSHBBS1
   6 4=J2 ...#UNS FSHTEST
   4 4=JG ...(   
  16 4=K2 ...      BZE   0  XFSHBBS1A           [ J. IF F/S RES  
   7 4=KG ...      LDX   0  7
  16 4=L2 ...      SBN   0  8                   [ CHECK IF RES 4 
  21 4=LG ...      BNZ   0  T6               [ IGNORE SWAP RES IF NOT RES 4 IN M/C B 
   4 4=M2 ...)   
   6 4=MG ...XFSHBBS1A   
   4 4=N2 ...)   
   7 4=NG ...      FSHSKIP   
   4 4=P2 ...(   
  10 4=Q6          BSOFF    BSBITS,T2A   
   7 4?9Q          SMO      7
   9 4?PB          LDX   5  A1+1(3)  
   9 4#92    #SKI  K6SETLIST>399-399 
  10 4#NL          TRACE    5,FILE SIZ   
  16 4*8=          LDX   4  BSSS                [X4=WORDS/SHEET  
   8 4*MW          LDCT  0  #200 
   7 4B7G          SMO      7
   8 4BM6          ANDX  0  A1(3)
  17 4C6Q          BNZ   0  Q1                  [JUMP IF SWAP FILE   
  18 4CLB          LDX   4  BSBS                [OTHERWISE X4=WORDS/BLOCK
  18 4D62    Q1    SRL   4  7                   [BLOCKLETS/SHEET OR BLOCK
  21 4DKL          DVS   4  4                   [X5=NUMBER OF BLOCKS OR SHEETS IN FIL
   8 4F5=          LDN   1  24   
  15 4FJW          DVS   4  1                   [NO.OF WORDS 
   8 4G4G          BZE   4  T3   
  14 4GJ6          ADN   5  1                   [ROUND UP
   8 4H3Q    T3    SMO      BSBS4
   4 4H?J ...)   
   9 4HHB    T2A   ADN   5  BSRLEN-A1
   7 4J32          EMSCORE  5
   7 4JGL          FINDCORE 2
  10 4K2=          NAME     2,BSTB,BSLIST
   7 4KFW          SRL   7  1
  15 4K_G          STO   7  BACK1(2)            [LOG FILE NO 
   7 4LF6          SLL   7  1
   7 4LYQ          LDX   1  5
   7 4MDB    T3A   SMO      2
   9 4MY2          STOZ     A1-1(1)  
   8 4NCL          BCT   1  T3A  
   7 4NX=          NGN   4  1
  14 4PBW          STO   4  BACK2(2)            [SET UP  
  14 4PWG          STO   4  BSRLEN(2)           [EMPTY   
   7 4QB6          SMO      7
  15 4QTQ          LDX   5  A1+1(3)             [NO OF BKTS  
   8 4R*B          LDX   4  BSSS 
   8 4RT2          LDCT  0  #200 
   7 4S#L          SMO      7
   8 4SS=          ANDX  0  A1(3)
   8 4T?W          BNZ   0  Q2   
   8 4TRG          LDX   4  BSBS 
   7 4W?6    Q2    SRL   4  7
   7 4WQQ          DVS   4  4
  15 4X=B          STO   5  A1+3(2)             [NO OF BLKS  
   7 4XQ2          SMO      7
   8 4Y9L          LDX   4  A1(3)
  16 4YP=          ANDN  4  #7777               [MOD FOR FDTABLE 
  17 4_8W          LDX   1  AFDTP               [POINTER TO FD TABLE 
   7 4_NG          SMO      4
  15 5286          LDCH  5  A1(1)               [DEVICE TYPE 
  13 52MQ          SRL   5  1                   [TURN
  13 537B          ERN   5  1                   [INTO
  14 53M2          ANDN  5  3                   [LETHARGY
   9 546L          STO   5  A1+4(2)  
   7 546X ...#UNS ANOTALLOC  
   4 5478 ...(   
   7 547F ...      FSHSKIP  B
   4 547Q ...(   
   4 5483 ...#   
  17 548# ...#     MARK BSLIST BLOCKS FOR DO NOT ALLOCATE RESIDENCES 
   4 548K ...#   
  18 548W ...      FIXTRA   K100SETLIST         [USED BY NOTALLOC MACRO  
  20 5497 ...      BRN      NOTAL3              [BRANCH NULLED OUT BY NOTALLOC   
   4 549D ...#   
   6 549P ...#UNS FSHTEST
   4 54=2 ...(   
   9 54=? ...      FSHCODE  B,NOTINB 
   9 54=J ...      BRN      NOTAL3   
   5 54=T ...NOTINB  
   4 54?6 ...)   
  17 54?C ...      LDN   1  0                   [CLEAR WORD MODIFIER 
   9 54?N ...      LDX   4  BACK1(2) 
   8 54?_ ...      SBN   4  24   
  18 54#= ...      BNG   4  NOTAL1              [J IF RESIDENCE NO. < 24 
  18 54#H ...      ADN   1  1                   [INCREASE WORD MODIFIER  
   8 54#S ...      SBN   4  24   
  18 54*5 ...      BNG   4  NOTAL1              [J IF RESIDENCE NO. < 48 
  18 54*B ...      ADN   1  1                   [INCREASE WORD MODIFIER  
   8 54*M ...      SBN   4  24   
   5 54*Y ...NOTAL1  
  17 54B9 ...      ADN   4  24                  [ADJUST SHIFT FACTOR 
   8 54BG ...      SMO      FX1  
  20 54BR ...      LDX   6  ANOTALRES(1)        [PICK UP RELEVANT WORD IN TABLE  
   7 54C4 ...      SMO      4
  21 54C* ...      SLL   6  0                   [SHIFT UNTIL REQUIRED BIT IN BIT 0   
  16 54CL ...      BPZ   6  NOTAL3              [J IF BIT NOT SET
  16 54CX ...      LDCT  0  #200                [IS IT SWAPFILE  
   7 54D8 ...      SMO      7
   8 54DF ...      ANDX  0  A1(3)
  17 54DQ ...      BZE   0  NOTAL2              [NO -SO OK TO MARK   
  16 54F3 ...      LDN   5  2                   [YES -SO ERROR   
  15 54F# ...      LDX   4  BACK1(2)            [RESIDENCE NO
   7 54FK ...      OUTBLOCK 5
   8 54FW ...      OUTNUM   4,0  
  16 54G7 ...      INFORM   1,JSTLST3,1         [OUTPUT ERROR MSG
  16 54GD ...      MHUNT    2,BSTB,BSLIST       [RELOCATE BSLIST 
   9 54GP ...      BRN      NOTAL3   
   5 54H2 ...NOTAL2  
  21 54H? ...      LDCT  0  #200                [SET BIT 1 (BSMARK) TO MARK DO NOT   
  14 54HJ ...      ORS   0  A1+4(2)             [ALLOCATE
   5 54HT ...NOTAL3  
   4 54J6 ...#   
  10 54JC ...      FIXTRA   K101SETLIST  
   4 54JN ...#   
   4 54J_ ...)   
   4 54K= ...)   
  20 54L=          LDCT  0  #200                [J. IF NOT MARKED AS A SWAP FILE.
   7 555W          SMO      7
   8 55KG          ANDX  0  A1(3)
   8 5656          BZE   0  T4   
  19 56JQ          LDCT  0  #400                [SET MARKER IN LETHARGY WORD.
   9 574B          ORS   0  A1+4(2)  
   8 57BR ...      LDN   0  BWELL
  18 57P8 ...      STO   0  BSRESERVE(2)        [SET RESERVE TANK =BWELL 
   8 583L          BRN      T5   
   4 584? ...#   
  19 584Y ...#     ALLOCATE SIZE OF RESERVE TANK AS 1% OF SIZE OF RESIDENCE  
  14 585K ...#     WITH MIN SIZE 50 BLOCKS AND MAX 500   
   4 586= ...#   
   8 586X ...T4    LDX   1  FX1  
  15 587J ...      LDN   0  100                 [CALC 1% OF  
  17 5889 ...      LDX   5  BSTOT(2)            [SIZE OF RESIDENCE   
   7 588W ...      DVS   4  0
  15 589H ...      LDX   0  5                   [SAVE IN X0  
  17 58=8 ...      SBX   5  MINWELL(1)          [LESS THAN 50 BLOCKS 
  13 58=T ...      BPZ   5  TT6                 [NO  
  17 58?G ...      LDX   5  BSTOT(2)            [IS SIZE OF RESIDENCE
  17 58#7 ...      SBX   5  MINWELL(1)          [LESS THAN 50 BLOCKS 
  13 58#S ...      BPZ   5  TT4                 [NO  
   4 58*F ...#   
  11 58B6 ...#     RESIDENCE LESS THAN 25K   
   4 58BR ...#   
  16 58CD ...      LDN   5  2                   [OUTPUT WARNING  
   9 58D5 ...      LDX   4  BACK1(2) 
  13 58DQ ...      OUTBLOCK 5                   [MSG 
   8 58FC ...      OUTNUM   4,0  
  10 58G4 ...      INFORM   1,JSTLST4,1  
  10 58GP ...      MHUNT    2,BSTB,BSLIST
   4 58HB ...#   
  20 58J3 ...TT4   LDX   0  MINWELL(1)          [SET RESERVE TANK TO 50 BLOCKS   
  10 58JN ...TT5   STO   0  BSRESERVE(2) 
   9 58KF ...      FSHCODE  B,NOTFSHB
   4 58MN ...(   
  21 58PX ...      STOZ     A1(2)               [ BSPOI = 0 FOR F/S RES IN MACH. B   
  20 58S6 ...      NGX   0  BSTOT(2)            [SET BSFRE=BSTOT FOR FS RES IN   
   5 58W* ...NOTFSHB 
  19 58YJ ...)                                  [ MACHINE B, OTHERWISE NEGATE
  19 599H ...T5    NGS   0  BSFREE(2)           [MACHINE B OTHERWISE NEGATE  
   9 59GG          CHAIN    2,BMISC+1
   7 5=26    T6    ADN   7  2
   8 5=FQ          BRN      T2   
   4 5=H3 ...#   
  16 5=J# ...TT6   SBN   5  450                 [IS RESERVE TANK 
  18 5=KK ...      BNG   5  TT5                 [GREATER THAN 500 BLOCKS 
  18 5=LW ...      LDX   0  MAXWELL(1)          [YES SO SET TO 500 BLOCKS
   8 5=N7 ...      BRN      TT5  
   5 5=PJ ...SWAPFREE
   6 5=PY ...#UNS FSHTEST
   4 5=Q# ...(   
  10 5=QN ...      FSHCODE  A,NOTLOOP1   
   4 5=R4 ...(   
  17 5=RD ...      BS       2,BFSHACTB          [ SWAP ACT TO M/C B  
   6 5=RS ...      FPUT  
  18 5=S8 ...      COOR1                        [ TO GET CHAINS SWAPPED  
  19 5=SJ ...      LDN   7  0                   [ REINIT. ALFTABLE POINTER   
  19 5=SY ...      BRN      T2                  [ TO SET UP /BSLISTS IN M/C B
   5 5=T# ...NOTLOOP1
   4 5=TN ...)   
  10 5=W4 ...      FSHCODE  B,NOTLOOP2   
   4 5=WD ...(   
  18 5=WS ...      BC       2,BFSHACTB          [ SWAP ACT BACK TO M/C A 
   6 5=X8 ...      FPUT  
  20 5=XJ ...      COOR1                        [ AND GET CHAINS SWAPPED AGAIN   
   5 5=XY ...NOTLOOP2
   4 5=Y# ...)   
   4 5=YN ...)   
   4 5=_B    #   
  14 5?F2    #     FREE  ALL BACKING STORE  ON SWAP FILES
   4 5?YL    #   
  15 5#D=    T7    LDX   7  BSSS                [SHEET SIZE  
   8 5#XW          LDX   3  ALFTP
  10 5*CG          LDX   5  ALOGLEN(3)   
   7 5*X6          ADX   5  3
   9 5BBQ          STO   5  AWORK1(2)
   9 5BWB          STOZ     AWORK2(2)
  11 5CB2          SETNCORE 512,2,BSTB,FULLB 
   8 5CTL    YC    LDX   0  A1(3)
   8 5D*=          BNG   0  YA   
   8 5DSW          LDCT  4  #200 
   8 5F#G          ANDX  4  A1(3)
   8 5FS6          BZE   4  YA   
   8 5G?Q          SMO      FX2  
  16 5GRB          LDX   0  AWORK2              [LOGICAL FILE NO.
   9 5H?2          STO   0  A1+1(2)  
   6 5H?T ...#UNS FSHTEST
   4 5H#N ...(   
   9 5H*H ...      FSHCODE  A,NSMACA 
   4 5HBB ...(   
   9 5HC9 ...      LDX   0  A1+1(2)  
  20 5HD4 ...      SBN   0  4                   [ CHECK IF RESIDENCE 4 AND IF SO 
  21 5HDX ...      BZE   0  YA                  [ IGNORE IT IN M/C A IN TEST ENVIR.  
   5 5HFQ ...NSMACA  
   4 5HGK ...)   
   9 5HHD ...      FSHCODE  B,NSMACB 
   4 5HJ? ...(   
   9 5HK6 ...      LDX   0  A1+1(2)  
  20 5HK_ ...      SBN   0  4                   [ CHECK IF RESIDENCE 4 AND IF NOT
  21 5HLS ...      BNZ   0  YA                  [ IGNORE IT IN M/C B IN TEST ENVIR.  
   5 5HMM ...NSMACB  
   4 5HNG ...)   
   4 5HP* ...)   
  18 5HQL          SRL   7  7                   [SHEET SIZE IN BLOCKLETS.
  19 5J==          LDX   5  A1+1(3)             [NO. OF BLOCKLETS IN FILE.   
  19 5JPW          DVS   4  7                   [X5= NO. OF SHEETS IN FILE.  
   9 5K9G    #SKI  K6SETLIST>199-199 
  18 5KP6          BNZ   4  XER                 [ERROR IF A REMAINDER.   
   7 5L8Q          LDN   6  0
   8 5LNB    XG    LDN   4  510  
  19 5M82          STO   4  A1(2)               [NO. OF BLOCKS TO BE FREED.  
   7 5MML          TXL   4  5
   8 5N7=          BCS      XL   
  19 5NLW          STO   5  A1(2)               [IF LESS THAN 510 THIS TIME. 
   7 5P6G    XL    LDN   0  2
  17 5PL6          ADS   0  A1(2)               [UPDATE RECORD HEADER
  16 5Q5Q    XH    STO   6  A1+2(2)             [INSERT SHEET NO.
   7 5QKB          SBN   5  1
  17 5R52          BZE   5  XI                  [J. IF END OF FILE.  
   7 5RJL          ADN   2  1
  16 5S4=          ADX   6  7                   [UPDATE SHEET NO.
  19 5SHW          BCT   4  XH                  [J. IF BLOCK NOT YET FULL.   
   7 5T3G    XI    FREEBAX   
  10 5TH6          MHUNTW   2,BSTB,EMPTYB
  18 5W2Q          NAME     2,BSTB,FULLB        [OTHERWISE RENAME BLOCK  
   8 5WGB          BZE   5  YA   
  16 5X22          BRN      XG                  [AND START AGAIN.
   7 5XFL    YA    LDX   1  2
   8 5X_=          LDX   2  FX2  
   7 5YDW          LDN   0  1
  15 5YYG          ADS   0  AWORK2(2)           [UPDATE LFN. 
   7 5_D6          ADN   3  2
   9 5_XQ          TXL   3  AWORK1(2)
   8 62CB          BCC      YB   
   7 62X2          LDX   2  1
   8 63BL          LDX   7  BSSS 
   8 63W=          BRN      YC   
   7 64*W    YB    FREECORE 1
   5 64M= ...#SKIP G4
   4 64QB ...(   
  20 64TG    #     THIS SECTION CALCULATES THE TOTAL NO OF SHEETS AVAILABLE FOR  
  21 65*6    #     SWAP SPACE - NOT COUNTING THOSE RESERVED FOR FAILURES - CONVERTS  
  16 65SQ    #     IT TO A NO. OF PAGES & STORES IT IN EFSWAPSP  
   4 66#B    #   
  15 67R=          LDCT  4  0                   [INIT MAX LFN
  15 68=W          LDCT  5  #400                [INIT MIN LFN
   7 68QG          LDN   6  0
  19 69=6          LDN   3  BMISC               [BASE OF MISCELLANEOUS CHAIN 
  15 69PQ    ZS2   LDX   3  FPTR(3)             [NEXT BLOCK  
  20 6=9B          TXU   3  CXMI                [J WHEN WE HAVE BEEN RIGHT ROUND 
   8 6=P2          BCC      ZS4  
   9 6?8L          LDX   0  ATYPE(3) 
   8 6?N=          SRL   0  12   
  21 6#7W          SBN   0  BSTB+BSLIST         [IGNORE ALL EXCEPT BSTB/BSLIST BLKS  
   8 6#MG          BNZ   0  ZS2  
   9 6*76          LDX   0  A1+4(3)  
  21 6*LQ          BPZ   0  ZS2                 [IGNORE UNLESS IT REFERS TO SWAPFILE 
  17 6B6B          TXL   5  BACK1(3)            [COMPARE LFN WITH MIN
   8 6BL2          BCS      ZS3  
   9 6C5L          LDX   5  BACK1(3) 
  17 6CK=    ZS3   TXL   4  BACK1(3)            [COMPARE LFN WITH MAX
   8 6D4W          BCC      ZS3A 
   9 6DJG          LDX   4  BACK1(3) 
   4 6F46    ZS3A
  21 6FHQ          ADX   6  A1+1(3)             [ACC TOTAL NO OF SHEETS FOR SWAPPING 
  17 6G3B          BVS      (GEOERR)            [ERROR IF OVERFLOW   
  15 6GH2          LDX   7  A1+3(3)             [SIZE OF FILE
  17 6H2L          LDX   0  ASHEETSIZE          [CONVERT TO PAGE NO. 
   8 6HG=          SRL   0  10   
   7 6H_W          MPY   7  0
   9 6JFG          BNZ   7  (GEOERR) 
  21 6J_6          TXL   0  MAXBSA(1)           [ERROR IF FILE EXCEEDS PERMITTED SIZE
   9 6KDQ          BCC      (GEOERR) 
   8 6KYB          BRN      ZS2  
  10 6LD2    ZS4   LDX   0  ASHEETSIZE   
  18 6LXL          SRL   0  10                  [NO OF PAGES PER SHEET   
  21 6MC=          MPY   6  0                   [CONVERT TOTAL NO OF SHEETS TO PAGES 
   9 6MWW          BNZ   6  (GEOERR) 
  10 6NBG          LDX   0  K99SETLIST(1)
  19 6NW6          BZE   0  ZS5                 [ JUMP IF NO SWAPSPACE MACRO.
   7 6P*Q          LDX   6  7
  19 6PTB          MPY   6  0                   [ MULTIPLY BY PARAMETER, AND 
  16 6Q*2          SRA   67 7                   [ DIVIDE BY 128  
   8 6QSL          BNZ   6  ZSGR 
   8 6R#=          BZE   7  ZSGR 
   8 6RRW          BRN      ZS5  
   4 6S?G    ZSGR
  10 6SR6          GEOERR   1,SWAPSPAC   
   4 6T=Q    ZS5 
   9 6TQB          STO   7  EFSWAPSP 
  17 6W=2          BNG   5  (GEOERR)            [ERROR IF NO SWAPFILE
  21 6WPL          STO   5  EMINSSLFN           [STORE MIN LFN ALLOCATED AS SWAPSPACE
  19 6X9=          SBX   4  5                   [DIFFERENCE BETWEEN MAX & MIN
  17 6XNW          TXL   4  MAXLFN(1)           [ERROR IF TOO LARGE  
   9 6Y8G          BCC      (GEOERR) 
   4 6YN6    )   
   5 6YXY ...ZSWSP   
   6 6YY? ...#UNS FSHTEST
   4 6YYL ...(   
   9 6YY_ ...      FSHCODE  A,NIFINB 
   4 6Y_# ...(   
   8 6Y_M ...      LDX  2  FX2   
  17 6_22 ...      BS      2,BFSHACTB          [ PUT ACT INTO M/C B  
   6 6_2* ...      FPUT  
  19 6_2N ...      COOR1                       [ AND GET CHAINS ETC SWAPPED  
  19 6_33 ...      BRN      T7                 [ TO REPEAT FREEBAXES IN M/C B
   5 6_3B ...NIFINB  
   4 6_3P ...)   
   9 6_44 ...      FSHCODE  B,NIFINA 
   4 6_4C ...(   
   8 6_4Q ...      LDX   2  FX2  
  17 6_55 ...      BC       2,BFSHACTB         [ RETURN ACT TO M/C A 
   6 6_5D ...      FPUT  
  18 6_5R ...      COOR1                       [ TO SWAP CHAINS ETC AGAIN
   5 6_66 ...NIFINA  
   4 6_6F ...)   
   4 6_6S ...)   
   5 6_7Q          UP
   4 6_MB    #   
   9 7272    #SKI  K6SETLIST>199-199 
  10 72LL    XER   GEOERR   1,SHEETSIZ   
   4 736=    #   
  10 73KW          MENDAREA 20,K98SETLIST
   4 745G    #   
   4 74K6    #END
   6 ____ ...035443100003
