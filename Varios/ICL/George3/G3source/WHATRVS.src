  11 22FL     SEG WHATRVS,866,J.K.PROTHEROUGH
   4 22_=    [   
   4 23DW    [   
  16 23YG    [ (C) COPYRIGHT INTERNATIONAL COMPUTERS LIMITED 1982
   4 24D6    [   
  12 24XQ          SEGENTRY K1WHATRVS,X1WHATRVS  
   4 25CB    [   
  16 25X2    [IMPLEMENTS THE WHATRVS COMMAND TO OBTAIN A LIST OF 
  11 26BL    [OUTSTANDING RETRIEVE REQUESTS  
   4 26W=    [   
   4 27*W    [   
   4 27TG    [   
  10 28*6    [MONITORING FILE MESSAGES   
   9 28SQ    WRVBY           +JWVRVBY
   9 29#B    WFNAME          +JWVFIL 
   9 29S2    WTERM           +JWVTERM
   9 2=?L    WNOUT           +JWVNOUT
  10 2=R=    WCOUNTMESS       +JWVCOUNT  
   8 2?=W    MAXSELEC        +9  
   9 2?QG    MAGIC           7036875 
   9 2#=6    PSTRJOBNO       8HJOBNO 
   8 2#PQ    PSTRCOLON       4H: 
   9 2*9B    PSTRALL         8HALL   
   9 2*P2    PSTRFULL        8HFULL  
   9 2B8L    PSTRUSER        8HUSER :
   9 2BN=    PSTRHERE        8HHERE  
   9 2C7W    PSTRLIST        8HLIST  
  10 2CMG    PROPERTY        8HPROPERTY  
   8 2D76    PRJBRAC         1H) 
  10 2DLQ    PRJPROP         9H,PARAM(PR 
  17 2F6B    PRUNJOB         36HRJ WVLIST0000,:OPERATORS,WVLISTJDF   
   9 2FL2    P10000          +10000  
   8 2G5L    PVAL2           +2  
   8 2GK=    PVAL4           +4  
   8 2H4W    PFILEDES        10  
  10 2HJG    POPERATORS      12HOPERATORS
  10 2J46                    12HWHATRVS  
   8 2JHQ                    1   
  10 2K3B                    #20000001   
   8 2KH2                    4HB1
   2 2L2L
   4 2LG=    [   
   4 2L_W    [   
   4 2MFG    [   
   6 2M_6    X1WHATRVS   
   2 2NDQ
   4 2PXL    [   
   9 2QC=          CALL  7  SETBLOCKS
   2 2QWW
   4 2RBG    [   
   9 2RW6    [LOOK FOR JOBNO AS %A   
   4 2S*Q    [   
  10 2STB          MHUNT    2,CPB,CALAS  
  10 2T*2          LDX   6  APARANUM(2)  
   8 2TSL          IF       6,NZ 
   6 2W#=          THEN  
  11 2WRW             LDX   6  APARAFIR(2)   
  20 2X?G             ANDN  6  #7777                    [X6 = CHAR COUNT OF %A   
   9 2XR6             IF       6,NZ  
   7 2Y=Q             THEN   
  12 2YQB                LDCH   0  APARAFIR+1(2) 
   9 2_=2                SBN   0  #73
  16 2_PL                IF        EITHER,0,PZ          [IF > 'Z'
  11 329=                   ADN   0  #73-#41 
  16 32NW                OR        0,NG                 [OR < 'A'
  11 338G                   ADN   0  #41-#32 
  17 33N6                AND       0,NZ                 [AND .NE. '*'
  11 347Q                   ADN   0  #32-#12 
  20 34MB                AND       0,NZ                 [AND .NE. ':' THEN JOBNO 
   7 3572                THEN
  10 35LL                   SPARANOT 1   
   9 366=                   CHNUMCOX 
  12 36KW                   TESTRPN2 OK,ZENDCOM  
  12 375G                   LDX   5  ACOMMUNE1(2)
  17 37K6                   BNG   5  ZJOBNOERR          [JOBNO -VE   
  17 384Q                   BZE   5  ZJOBNOERR          [JOBNO ZERO  
  11 38JB                   MFREE    CPB,CUNI
  13 3942                   MHUNTW   3,ADATA,AWHATRVS
  12 39HL                   STO   5  AWVJOBNO(3) 
  18 3=3=                   BS       3,AWVBJOBNO        [SET JOBNO BIT   
  20 3=GW                   CALL  7  SEPARATE           [SET UP SELECTION DETAILS
  10 3?2G                   LDX   0  FX1 
  12 3?G6                   ADN   0  PSTRJOBNO   
  20 3?_Q                   MVCH  0  6                  [MOVE JOBNO TO SELECTION 
  18 3#FB                                               [DETAILS BLOCK   
  12 3#_2                   CALL  7  SUBADDNUM   
   7 3*DL                FI  
   7 3*Y=             ELSE   
   8 3BCW                PARAPASS
  11 3BXG                BRN      ZNULLPAR   
   6 3CC6             FI 
   5 3CWQ          FI
   4 3DBB    [   
  11 3DW2    [LOOK FOR :USER,JOB OR JOB,:USER
   4 3F*L    [   
   8 3FT=          LDX   1  FX1  
   7 3G#W          LDN   3  1
  10 3GSG          LDN   1  PSTRCOLON(1) 
   9 3H#6          CALL  7  SUBPARAM 
   9 3HRQ          LDX   7  ANUM(2)  
  18 3J?B          IF       7,PZ                        [IF COLON PRESENT
   6 3JR2          THEN  
  20 3K=L             JBS      ZPREVJOBNO,3,AWVBJOBNO   [ERROR IF PREVIOUS JOBNO 
  10 3KQ=             BZE   7  ZNULLUSER 
   8 3L9W             STO   3  7 
  19 3LPG             CHEKLFN2 ZNVALNAM,ZNVALNAM,2     [CHECK USERNAME FORMAT
   8 3M96             LDX   3  7 
  10 3MNQ             CALL  1  TRANSFER  
   8 3N8B             +AWVUSERNAM
  21 3NN2                                               [COPY USERNAME TO ADATA/AWHAT
  18 3P7L             BS       3,AWVBUSERNAM            [SET USERNAME BIT
  10 3PM=             CALL  7  SEPARATE  
  21 3Q6W             LDN   0  #12                      [ADD USERNAME TO SELECTION DE
   9 3QLG             DCH   0  0(1)  
   8 3R66             BCHX  1  / 
  10 3RKQ             CALL  7  SUBADDPAR 
   8 3S5B             FREECORE 2 
   8 3SK2             PARUNACC   
  21 3T4L                                               [GET JOBNAME (FIRST UNACC PAR
  10 3TJ=             MHUNT    2,CPB,CUNI
  13 3W3W             CHEKLFN2 ZNOJOBNAM,ZNVALNAM,2  
  19 3WHG                                               [CHECK JOBNAME FORMAT
  12 3X36             MHUNTW   3,ADATA,AWHATRVS  
  21 3XGQ             CALL  1  TRANSFER                 [COPY JOBNAME TO ADATA/AWHATR
   8 3Y2B             +AWVJOBNAM 
  18 3YG2             BS       3,AWVBJOBNAM             [SET JOBNAME BIT 
  21 3Y_L             CALL  7  SEPARATE                 [ADD JOBNAME TO SELECTION DET
  10 3_F=             CALL  7  SUBADDPAR 
   5 3_YW          FI
   7 42DG          FREECORE 2
   4 42Y6    [   
  12 43CQ    [LOOK FOR USERNAME GIVEN BY 'USER'  
   4 43XB    [   
   7 44C2          LDN   3  4
  10 44WL          LDN   1  PSTRUSER(1)  
   9 45B=          CALL  7  SUBPARAM 
   9 45TW          LDX   1  ANUM(2)  
  19 46*G          IF       1,PZ                        [IF 'USER' PRESENT   
   6 46T6          THEN  
  18 47#Q             BS       3,AWVBUSER                [SET USER BIT   
  17 47SB             JBSS     Z2USERS,3,AWVBUSERNAM    [PREVIOUS ':'
  21 48#2             JBS      Z2USERS,3,AWVBJOBNO      [OR PREVIOUS JOBNO THEN ERROR
  20 48RL                                               [ELSE SET USERNAME BIT   
  10 49?=             BZE   1  ZNULLUSER 
  10 49QW             LDCH  7  APARA(2)  
   9 4==G             SBN   7  #12   
  17 4=Q6             IF       7,ZE                     [IF 'USER' : 
   7 4?9Q             THEN   
  11 4?PB                LDX   1  JPARNUM(2) 
   2 4#92
  18 4#NL                PARALYSE #12,,1                [THEN SPLIT AT : 
  16 4*8=                TESTREP2 UNPAIR,ZNVALNAM,TOOMANY,ZMAXPAR
  20 4*MW                SPARANOT 2                     [GET USERNAME (2ND PARAM)
  17 4B7G                PARAFREE                       [FREE CMULTI 
  11 4BM6                MHUNT    2,CPB,CUNI 
   6 4C6Q             FI 
  20 4CLB             CHEKLFN2 ZNULLUSER,ZNVALNAM,2     [CHECK USERNAME FORMAT   
  12 4D62             MHUNTW     3,ADATA,AWHATRVS
   2 4DKL
  10 4F5=             CALL  1  TRANSFER  
   8 4FJW             +AWVUSERNAM
  10 4G4G             CALL  7  SEPARATE  
   9 4GJ6             LDX   0  FX1   
   2 4H3Q
  10 4HHB             ADN   0  PSTRUSER  
   8 4J32             MVCH  0  6 
  10 4JGL             CALL  7  SUBADDPAR 
   5 4K2=          FI
   7 4KFW          FREECORE 2
   4 4K_G    [   
   7 4LF6    [LOOK FOR 'ALL' 
   4 4LYQ    [   
   8 4MDB          LDN   3   3   
   9 4MY2          LDN  1  PSTRALL(1)
   9 4NCL          CALL  7  SUBPARAM 
   9 4NX=          LDX  1  ANUM(2)   
   8 4PBW          IF      1,PZ  
   6 4PWG          THEN  
  18 4QB6             JMBS     Z2USERS,3,AWVBUSERNAM,AWVBUSER,AWVBJOBNO  
  17 4QTQ             BS      3,AWVBALL                 [SET ALL BIT 
  10 4R*B             CALL  7  SEPARATE  
   2 4RT2
   2 4S#L
  21 4SS=             LDX   0  FX1                      [PUT ALL IN SELECTION DETAILS
  10 4T?W             ADN   0  PSTRALL   
   8 4TRG             MVCH  0  4 
   5 4W?6          FI
   7 4WQQ          FREECORE 2
   2 4X=B
   2 4XQ2
   4 4Y9L    [   
  11 4YP=    [CLOSE SELECTION DETAILS BLOCK  
   4 4_8W    [   
  11 4_NG          MHUNT    3,ADATA,CREADL   
   9 5286          LDX   1  A1+1(3)  
   8 52MQ          SBN   1  A1+2 
   2 537B
  18 53M2          IF       1,ZE                        [IF NO PARAMETERS
  17 546L          THEN                                 [FREE BLOCK  
   8 54L=             FREECORE 3 
   6 555W          ELSE  
  11 55KG             CALL  7  SUBENDETAIL   
   9 5656             ALTLENG   3,2  
   5 56JQ          FI
   4 574B    [   
  11 57J2    [SET ALL AS DEFAULT IF NECESSARY
   4 583L    [   
   4 58H=    [   
  11 592W          MHUNTW   3,ADATA,AWHATRVS 
  17 59GG          JMBS      NSOK,3,AWVBUSERNAM,AWVBUSER,AWVBJOBNO   
   9 5=26          BS      3,AWVBALL 
   4 5=FQ    NSOK
   4 5=_B    [   
  10 5?F2    [PROCESS ROUTING PARAMETER  
   4 5?YL    [   
   7 5#D=    [LOOK FOR 'HERE'
   4 5#XW    [   
  13 5*CG          PARABEG 1,PVAL4(1),PSTRHERE(1),,0 
   9 5*X6          MHUNT   2,CPB,CUNI
  19 5BBQ          IF      +ANUM(2),PZ                  [IF 'HERE' PRESENT   
   6 5BWB          THEN  
  10 5CB2             BNZ   0  ZDUFFPAR  
  12 5CTL             MHUNTW   3,ADATA,AWHATRVS  
  18 5D*=             BS       3,AWVBHERE               [SET 'HERE' BIT  
   5 5DSW          FI
   7 5F#G          FREECORE 2
   4 5FS6    [   
   7 5G?Q    [LOOK FOR 'LIST'
  19 5GRB    [LEAVE %(PR) IN CUNI, OR IF PROPERTY NOT GIVEN THEN NO CUNI LEFT
   4 5H?2    [   
  13 5HQL          SPARABEG 1,PVAL4(1),PSTRLIST(1),,0
  10 5J==          MHUNT    2,CPB,CUNI   
   9 5JPW          LDX   7  ANUM(2)  
  19 5K9G          IF       7,PZ                        [IF 'LIST' PRESENT   
   6 5KP6          THEN  
  12 5L8Q             MHUNTW    3,ADATA,AWHATRVS 
  19 5LNB             JBS      ZHL,3,AWVBHERE           [IF LIST & HERE,ERROR
  19 5M82             MBS      3,AWVBFULL,AWVBLIST      [SET FULL + LIST BITS
  19 5MML             IF       7,NZ                     [IF PROPERTY STRING  
   7 5N7=             THEN   
  11 5NLW                LDX   3  JPARNUM(2) 
   9 5P6G                PARALYSE ,,3
  16 5PL6                TESTREP2 UNPAIR,ZLISTWR,TOOMANY,ZMAXPAR 
   9 5Q5Q                PARANUMB 4  
  18 5QKB                BXU   4  PVAL2(1),ZLISTWR      [IF NOT 2 PARAMS 
  18 5R52                LDN   1  2                     [REMOVE BRACKETS 
   9 5RJL                PARALYSE ,,1
  12 5S4=                TESTREP2 TOOMANY,ZMAXPAR
   9 5SHW                LDN   7  8  
   7 5T3G                DO  
  19 5TH6                   SPARABEG 1,7,PROPERTY(1)    [LOOK FOR 'PR' PARAM 
  12 5W2Q                   MHUNT     2,CPB,CUNI 
  11 5WGB                   LDX   6  ANUM(2) 
  18 5X22                   IF       6,PZ               [IF 'PR' FOUND   
   8 5XFL                   THEN 
  12 5X_=                      BNZ   6  NXTLISFND
  18 5YDW                      BRN      ZWRQUAL         [ERROR IF NULL   
   8 5YYG                   FI   
  18 5_D6                   FREECORE 2                  [ELSE TRY AGAIN  
  17 5_XQ                   SRL   7  2                    [FOR 'PR'  
   2 62CB
  11 62X2                   BZE   7  ZWRQUAL 
   8 63BL                REPEAT  
   6 63W=    NXTLISFND   
  18 64*W                HUNT2J   2,CPB,CUNI,,(GEOERR)  [FIND 'LIST' CUNI
  16 64TG                FREECORE 2                     [FREE IT 
  13 65*6                MHUNT    3,ADATA,AWHATRVS   
  18 65SQ                BS       3,AWVBLISTPR          [SET PROPERTY BIT
  17 66#B                PARAFREE                       [FREE CMULTI 
   2 66S2
  10 67?L                BRN      NOTLIST
  18 67R=             FI                                [END 'PR' CODE   
  18 68=W          FI                                   [END 'LIST' CODE 
   7 68QG          FREECORE 2
   4 69=6    [   
   5 69PQ    NOTLIST 
   4 6=9B    [   
   4 6=P2    [   
  10 6?8L    [PROCESS OUTPUT LEVEL PARAM 
   4 6?N=    [   
  13 6#7W          PARABEG  1,PVAL4(1),PSTRFULL(1),,0
  10 6#MG          MHUNT    2,CPB,CUNI   
  11 6*76          MHUNTW   3,ADATA,AWHATRVS 
  19 6*LQ          IF       +ANUM(2),PZ                 [IF 'FULL' PRESENT   
   6 6B6B          THEN  
  10 6BL2             BNZ   0  ZDUFFPAR  
  18 6C5L             BS       3,AWVBFULL               [SET 'FULL' BIT  
   2 6CK=
  19 6D4W             IF       MBAC,3,AWVBLIST,AWVBHERE [IF NO ROUTING GIVEN 
   7 6DJG             THEN   
  18 6F46                BS     3,AWVBLIST              [SET 'LIST' BIT  
   6 6FHQ             FI 
   5 6G3B          FI
   7 6GH2          FREECORE 2
  11 6H2L    [ONLY SET HERE OR LIST IF 'FULL'
   4 6HG=    [   
  17 6H_W          IF       BC,3,AWVBFULL               [IF 'FULL'   
   6 6JFG          THEN  
  13 6J_6             MBC       3,AWVBLIST,AWVBHERE  
   5 6KDQ          FI
   4 6KYB    [   
   4 6LD2    [   
  12 6LXL    [ERROR IF ANY UNACCESSED PARAMETERS 
   4 6MC=    [   
   7 6MWW          PARUNACC  
  10 6NBG          MHUNT    2,CPB,CUNI   
   2 6NW6
   9 6P*Q          LDX   7  ANUM(2)  
   8 6PTB          IF       7,PZ 
   6 6Q*2          THEN  
  10 6QSL             BZE   7  ZNULLPAR  
  10 6R#=             BRN      ZUNRECOG  
   5 6RRW          FI
   7 6S?G          FREECORE 2
   4 6SR6    [   
   4 6T=Q    [   
   4 6TQB    [   
  10 6W=2          ACROSS   WHATRVSA,1   
   4 6WPL    [   
   4 6X9=    [   
   9 6XNW    [SUBROUTINE 'SEPARATE'  
   4 6Y8G    [   
   4 6YN6    [   
  20 6_7Q    [THIS SUBROUTINE FINDS THE SELECTION DETAILS (ADATA/CREADL) BLOCK,  
  20 6_MB    [ADDS A SEPARATOR CHARACTER AND LEAVES THE POINTERS READY TO ADD THE
   8 7272    [PARAMETER ITSELF   
   4 72LL    [   
   9 736=    [ENVIRONMENT REQUIRED   
   4 73KW    [   
  18 745G    [[  ADATA/CREADL EXISTS WITH PTR TO NEXT FREE CHAR IN A1+1  
   4 74K6    [   
   8 754Q    [ENVIRONMENT CHANGES
   4 75JB    [   
   7 7642    [   XO-DESTROYED
   8 76HL    [  X1-NEXT FREE CHAR
   8 773=    [  X3-HEAD OF BLOCK 
   6 77GW    [  X7-LINK  
   4 782G    [   
   5 78G6    SEPARATE
  11 78_Q          MHUNT    3,ADATA,CREADL   
   9 79FB          LDX   1  A1+1(3)  
   8 79_2          SBN   1  A1+2 
  18 7=DL          IF       1,ZE                        [IF FIRST PARAM  
   6 7=Y=          THEN  
  18 7?CW             LDN   0  #30                      [SEPARATOR IS '('
  10 7?XG             ADN   1  A1+2(3)   
   6 7#C6          ELSE  
  18 7#WQ             ADN   1  A1+2(3)                  [ELSE PUT IN (,) 
  18 7*BB             LDN   0  #34                      [FOLLOWED BY ' ' 
   9 7*W2             DCH   0  0(1)  
   2 7B*L
   8 7BT=             BCHX  1  / 
   9 7C#W             LDN   0  #20   
   5 7CSG          FI
  18 7D#6          DCH   0  0(1)                        [INSERT SEPARATOR
   7 7DRQ          BCHX  1  /
   7 7F?B          EXIT  7  0
   4 7FR2    [   
   4 7G=L    [   
   9 7GQ=    [SUBROUTINE 'SUBADDNUM' 
   4 7H9W    [   
  21 7HPG    [THIS SUBROUTINE CONVERTS A BINARY NUMBER TO DECIMAL,ADDING IT TO THE   
  21 7J96    [END OF THE SELECTION DETAILS BLOCK,STARTING WITH THE FIRST SIG. DECIMAL
   4 7JNQ    [   
   9 7K8B    [ENVIRONMENT REQUIRED   
   4 7KN2    [   
  11 7L7L    [   X1,X3 AS LEFT BY 'SEPARATE' 
   2 7LM=
  10 7M6W    [   X5 CONTAINS THE NUMBER  
   4 7MLG    [   
   8 7N66    [ENVIRONMENT CHANGES
   4 7NKQ    [   
  10 7P5B    [   X0,1,2,4,5,6 DESTROYED  
   2 7PK2
   6 7Q4L    [  X7-LINK  
  11 7QJ=    [   PTR WORD OF BLOCK UPDATED   
   4 7R3W    [   
   4 7RHG    [   
   6 7S36    SUBADDNUM   
   4 7SGQ    [   
   8 7T2B          SMO      FX1  
  21 7TG2          MPY   5  MAGIC                        [X56 FRACTION FOR CONVERSION
   2 7T_L
   7 7WF=          LDN   0  7
  20 7WYW          MODE     1                           [SUPPRESS LEADING ZEROS  
   8 7XDG          LDN   2  #20  
   7 7XY6          LDN   4  0
  20 7YCQ          DO                                   [FOR ALL NON-SPACES DO   
   8 7YXB             CBD   5  4 
   9 7_C2             IF      4,U,2  
   7 7_WL             THEN   
  17 82B=                DCH   4  0(1)                  [PUT IN BLOCK
   9 82TW                BCHX  1  /  
   6 83*G             FI 
   8 83T6          REPEAT  CT 0  
   7 84#Q          SBX   1  3
   9 84SB          STO   1  A1+1(3)  
   7 85#2          EXIT  7  0
   4 85RL    [   
   4 86?=    [   
   9 86QW    [SUBROUTINE 'SUBPARAM'  
   4 87=G    [   
  20 87Q6    [DOES A SPARABEG,HUNTS THE CUNI IN X2 AND THE ADATA/AWHATRVS IN X3  
   4 889Q    [   
   9 88PB    [ENVIRONMENT REQUIRED   
   4 8992    [   
   8 89NL    [   X3-LENGTH OF KEY
   6 8=8=    [   X1-KEY  
   6 8=MW    [   X7=LINK 
   4 8?7G    [   
   8 8?M6    [ENVIRONMENT CHANGES
  10 8#6Q    [   X2=CPB/CUNI JUST SET UP 
   2 8#LB
   9 8*62    [   X3=ADATA/AWHATRVS   
   4 8*KL    [   
   4 8B5=    [   
   5 8BJW    SUBPARAM
   4 8C4G    [   
   8 8CJ6          SBX   7  FX1  
  10 8D3Q          SPARABEG 1,3,0(1),,0  
  10 8DHB          MHUNT    2,CPB,CUNI   
  11 8F32          MHUNTW   3,ADATA,AWHATRVS 
   2 8FGL
   8 8G2=          ADX   7  FX1  
   7 8GFW          EXIT  7  0
   4 8G_G    [   
   4 8HF6    [   
   9 8HYQ    [SUBROUTINE 'TRANSFER'  
   4 8JDB    [   
  19 8JY2    [THIS SUBROUTINE MOVES A FILE LOCAL NAME FROM A CPB/CUNI TO THE 
   7 8KCL    [ADATA/AWHATRVS 
   4 8KX=    [   
   9 8LBW    [ENVIRONMENT REQUIRED   
   4 8LWG    [   
   8 8MB6    [   X2-PTS TO /CUNI 
   9 8MTQ    [   X3 PTS TO ?AWHATRVS 
   4 8N*B    [   
   8 8NT2    [ENVIRONMENT CHANGES
   4 8P#L    [   
   6 8PS=    [   X1-LINK 
   8 8Q?W    [  X4,5=DESTROYED   
   4 8QRG    [   
   8 8R?6    [PARAMETER REQUIRED 
  20 8RQQ    [DISPLACEMENT WITHIN /AWHATRVS AT WHICH THE TRANSFER SHOULD START   
   4 8S=B    [   
   4 8SQ2    [   
   5 8T9L    TRANSFER
   4 8TP=    [   
  19 8W8W          LDN   4  APARA(2)                       [X4 POINTS TO DATA
   7 8WNG          LDX   5  3
  21 8X86          ADX   5  0(1)                        [X5 POINTS TO RECEPTION AREA 
   9 8XMQ          SMO      ANUM(2)  
   7 8Y7B          MVCH  4  0
   7 8YM2          EXIT  1  1
   4 8_6L    [   
   4 8_L=    [   
   9 925W    [SUBROUTINE 'SUBADDPAR' 
   4 92KG    [   
  20 9356    [THIS SUBROUTINE ADDS THE CONTENTS OF A CPB?CUNI TO THE END OF THE  
   9 93JQ    [SELECTION DETAILS BLOCK
   4 944B    [   
   9 94J2    [ENVIRONMENT REQUIRED   
   4 953L    [   
  11 95H=    [   X1,3 AS LEFT BY 'SEPARATE'  
   2 962W
   7 96GG    [   X2-CPB/CUNI 
   4 9726    [   
   8 97FQ    [ENVIRONMENT CHANGES
   8 97_B    [   X0,1 DESTROYED  
   6 98F2    [   X7-LINK 
  10 98YL    [   PTR WD OF BLOCK UPDATED 
   4 99D=    [   
   4 99XW    [   
   6 9=CG    SUBADDPAR   
   4 9=X6    [   
   9 9?BQ          LDN   0  APARA(2) 
   9 9?WB          SMO      ANUM(2)  
   7 9#B2          MVCH  0  0
   2 9#TL
   7 9**=          SBX   1  3
   9 9*SW          STO   1  A1+1(3)  
   7 9B#G          EXIT  7  0
   4 9BS6    [   
   4 9C?Q    [   
  10 9CRB    [SUBROUTINE 'SUBENDETAIL'   
   4 9D?2    [   
  19 9DQL    [THIS SUBROUTINE CLOSES THE ADATA/CREADL SELECTION DETAILS BLOCK
   4 9F==    [   
   9 9FPW    [ENVIRONMENT REQUIRED   
   4 9G9G    [   
   8 9GP6    [   X3-ADATA/CREADL 
   8 9H8Q    [ENVIRONMENT CHANGES
   4 9HNB    [   
   7 9J82    [   X0-DESTROYED
  10 9JML    [   X1=CHAR CT OF MESSAGE   
   9 9K7=    [   X2=LOGLEN OF BLOCK  
   6 9KLW    [   X7-LINK 
   4 9L6G    [   
   4 9LL6    [   
   6 9M5Q    SUBENDETAIL 
   4 9MKB    [   
   9 9N52          LDX   1  A1+1(3)  
   8 9NJL          LDN   0  #31  
   7 9P4=          SMO      3
  17 9PHW          DCH   0  0(1)                        [INSERT ')'  
   7 9Q3G          BCHX  1  /
   7 9QH6          SLC   1  2
   9 9R2Q          SBN   1  A1+2*4   
  18 9RGB          STO   1  A1(3)                       [STORE CHAR COUNT
  20 9S22          LDN   2  11(1)                       [ADD 2 WORDS AND ROUND UP
   7 9SFL          SRL   2  2
   7 9S_=          EXIT  7  0
   4 9TDW    [   
   9 9TYG    [SUBROUTINE 'SETBLOCKS' 
   4 9WD6    [   
   2 9WXQ
  18 9XCB    [CREATE AND INITIALIZE ADATA/AWHATRVS AND ADATA/CREADL TO   
  10 9XX2    [CONTAIN SELECTION DETAILS  
   4 9YBL    [   
   4 9YW=    [   
   6 9_*W    SETBLOCKS   
   4 9_TG    [   
   8 =2*6          SBX   7  FX1  
  14 =2SQ          SETUPCORE  MAXSELEC(1),3,ADATA,CREADL 
   8 =3#B          LDN   0  A1+2 
  16 =3S2          STO   0  A1+1(3)                     [SET PTRS
  17 =4?L          SETNCORE  AWVJOBNO-AWVJOBNAM+1,3,ADATA,AWHATRVS   
   4 =4R=    [   
   9 =5=W    [CLEAR ADATA/AWHATRVS   
   4 =5QG    [   
   8 =6=6          LDX   0  ACES 
  10 =6PQ          STO   0  AWVJOBNAM(3) 
  10 =79B          LDN   5  AWVJOBNAM(3) 
  11 =7P2          LDN   6  AWVJOBNAM+1(3)   
   7 =88L          MOVE  5  5
  10 =8N=          STOZ     AWVBITS(3)   
  10 =97W          LDN   5  AWVBITS(3)   
  10 =9MG          LDN   6  AWVBITS+1(3) 
  11 ==76          MOVE  5  AWVJOBNO-AWVBITS 
   4 ==LQ    [   
  14 =?6B    [SET OPERATOR CONTEXT OR OPERATOR COMMAND   
   4 =?L2    [   
  11 =#5L          OPSCON   SETOPER,SETOPCOM 
   8 =#K=          BRN      NOTOP
   5 =*4W    SETOPER 
  10 =*JG          BS       3,AWVBOPER   
   8 =B46          BRN      NOTOP
   5 =BHQ    SETOPCOM
  10 =C3B          BS       3,AWVBOPCOM  
   5 =CH2    NOTOP   
   4 =D2L    [   
  18 =DG=    [SET REMOTE CONTEXT AND SOURCE CONSOLE PROPERTY AS REQUIRED 
   4 =D_W    [   
   8 =FFG          LDX   2  FX2  
  10 =F_6          LDX   6  JSOURCE3(2)  
   8 =GDQ          IF       6,NZ 
   6 =GYB          THEN  
  10 =HD2             BS       3,AWVBREM 
   2 =HXL
   9 =JC=             ANDX  6  BSP16 
  10 =JWW             STO   6  AWVCONS(3)
   5 =KBG          FI
   8 =KW6          ADX   7  FX1  
   7 =L*Q          EXIT  7  0
   4 =LTB    [   
   4 =M*2    [   
   6 =MSL    ZPREVJOBNO  
   5 =N#=    Z2USERS 
  21 =NRW    ZHL   COMERR   ASCOMBER                    [ILLEGAL PARAMETER COMBINATIO
   5 =P?G    ZDUFFPAR
   7 =PR6          LDX   3  2
  10 =Q=Q          SPARANOX   JPARNUM(3) 
   9 =QQB          COMERR   APFERR   
   5 =R=2    ZUNRECOG
   5 =RPL    ZLISTWR 
  20 =S9=          COMERR   APFERR                      [PARAMETER FORMAT ERROR  
   5 =SNW    ZMAXPAR 
   9 =T8G          COMERR   JMAXPAR  
   5 =TN6    ZWRQUAL 
  20 =W7Q          COMERR   BADQUAL                     [ERROR IN LIST QUALIFIER 
   6 =WMB    ZNULLUSER   
   5 =X72    ZNULLPAR
   9 =XLL          COMERR   JNULLPAR 
   5 =Y6=    ZENDCOM 
   6 =YKW          ENDCOM
   6 =_5G    ZJOBNOERR   
   9 =_K6          COMERR  FWHSTAT   
   5 ?24Q    ZNVALNAM
  19 ?2JB          COMERR   JNLFNF                      [INVALID NAME FORMAT 
   6 ?342    ZNOJOBNAM   
  18 ?3HL          COMERR   JPARMIS,JJOBNA              [JOBNAME MISSING 
   2 ?43=
   4 ?5G6    #END
   6 ____ ...177075170002
