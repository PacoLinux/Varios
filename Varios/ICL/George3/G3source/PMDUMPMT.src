   4 22FL    #   
   8 22_=    #OPT  K0PMDUMPMT=0  
  11 23DW    #LIS  K0PMDUMPMT>K0POST>K0ALLGEO
   4 23YG    #   
  15 24D6    #SEG  PMDUMPMT85                    [M.C.D. MANN
   4 24XQ    #   
  20 25CB    #     THIS SEGMENT ASKS THE OPERATOR FOR POSTMORTEM DUMP TAPE,CHECKS
  21 25X2    #     THAT IT IS A VALID TAPE AND IF SO POSITIONS THE TAPE READY FOR A  
   7 26BL    #     POST DUMP 
   4 26W=    #   
  10 27*W                   8HPMDUMPMT   
   4 27TG    #   
   9 28*6    #     ENTRY POINT TABLE 
   4 28SQ    #   
  11 29#B     SEGENTRY K1PMDUMPMT,XK1PMDUMPMT
  12 29S2     SEGENTRY  K2PMDUMPMT,XK2PMDUMPMT   
   4 2=?L    #   
   9 2=R=    MESSC          +2,2H_*  
  16 2?=W    MESSD          +27,27H_*DUMP TAPE TSN,DECK NO.?_*   
  21 2?QG    MESSE          +48,48H  _*TAPE ON DECK     NON-STANDARD HEADER LABEL_*  
  19 2#=6    MESSF          +42,42H_*TAPE            INCORRECT HEADER LABEL_*
  16 2#PQ    MESSG          +28,28H_*TAPE            LOAD WPR_*  
  18 2*9B    MESSH          37,37H_*PLEASE LOAD          ON UNIT     _*  
  14 2*P2    MESSI          +19,19H_*INVALID MESSAGE_*   
  14 2B8L    MESSJ          +22,22H  _*UNIT     FAIL   _*
  17 2BN=    MESSK          +34,34H_*           IS NOT ON DECK     _*
  16 2C7W    MESSL          +30,30H  _*START OF DUMP NUMBER    _*
  19 2CMG    MESSM          +42,42H_*TAPE            INCORRECT SUBFILE NAME_*
  15 2CX# ...MESSN          27,27H  _*UNIT     FORMAT ERROR_*
   8 2D76    TWEN4          +24  
   8 2DLQ    XFOUR          +4   
   8 2F6B    XFIVE          +5   
   8 2FL2    SEVEN          +7   
   8 2G5L    XEIGHT         +8   
   8 2GK=    THREE          +3   
   8 2H4W    NINE           +9   
   8 2HJG    TEN            +10  
   8 2J46    TWO            +2   
   8 2JHQ    ONE28          128:0
   8 2K3B    SIXTY3         +63  
  20 2KH2    XTWBIT         +0                  [MEMORY T/W ENGAGE BUTTON BIT?   
  21 2L2L    XMTBIT         +0                  [BIT POS CT/WORD REL EBA,ALSO MT BIT 
   9 2LG=    TSNX           #20202020
   9 2L_W                   #20202020
  16 2MFG    TSN            #20202020           [TO HOLD TSN IN  
  16 2M_6                   #20202020           [CHARACTER FORM  
   8 2NDQ    SPACE          #20  
   9 2NYB    SPACEFIL       #20202020
  15 2PD2    X1STORE        +0                  [ TO SAVE X1 
   8 2PXL    X3STORE        +0   
   8 2QC=    WLINK          +0   
  21 2QWW    XBOTIND        +0                  [SET POS. IF POSTDUMPS TO START AT   
  13 2RBG                                       [BOT 
  17 2RW6    XSCRIND        +0                  [SCRATCHING INDICATOR
  20 2S*Q    MIND           +0                  [SET 0 IF NOT NECESSARY TO OUTPUT
  17 2STB                                       ['LOAD WPR' MESSAGE  
   8 2T*2    PMCOUNT        +0   
  10 2TSL    POSTDUMP       12HPOSTDUMP  
   8 2W#=    TEA            #64  
  11 2WRW    SCRA           12HSCRATCH TAPE  
   9 2X?G    XMT            4HMT00   
   9 2XR6    XDA            4HDA00   
   9 2Y=Q    XBOT           4HBOT0   
   9 2YQB    XSCR           4HSCR0   
   8 2_=2    XCOMMA         #34  
   8 2_PL    XLINK2         +0   
   8 329=    XLINK3         +0   
   8 32NW    XLINK4         +0   
   8 338G    XLINK5         +0   
   4 33N6    #   
  13 347Q    #     BLOCK FORMAT OF MT HEADER LABEL   
   4 34MB    #   
  14 3572    THDDR          4HHDDR              [IDENTITY
  13 35LL                   +0                  [TSN 
  15 366=                   12HGEORGEPMTAPE     [TAPE NAME   
  16 36KW                   +0                  [REEL SEQ. NO.   
  16 375G                   +0                  [FILE GEN. NO.   
  20 37K6                   +999                [SECURITY BIT / RETENTION PERIOD 
  15 384Q                   +0                  [DATE WRITTEN
  18 38JB                   +0                  [BITS 0-8=GEOG NO OF DECK
   4 3942    #   
  12 39HL    #     MAG, TAPE PERI. CONTROL AREAS 
   4 3=3=    #   
  16 3=GW    MTREAD         261:0,+0,+512,+MTBUF    [READ A BLOCK
  19 3?2G    MTWRI          261:1,+0,+20,+MTBUF     [WRITE BLOCK (HDDR ONLY) 
  18 3?G6    MTBACK         261:3,+0,+0,+0          [BACKSPACE ONE BLOCK 
  18 3?_Q    MTSKIP         261:4,+0,+0,+0          [SKIP TO TAPE MARK   
  16 3#FB    MTREW          261:7,+0,+0,+0          [REWIND TAPE 
   4 3#_2    #   
   4 3*DL    #   
   4 3*Y=    #   
  19 3BCW    #     MAG TAPE PERI ROUTINE,REPLY WORD CHECKED FOR TAPE FAILURE 
  18 3BXG    #     IF MT FAILS CONSOLE OUTPUT-'UNIT NN FAIL N' AND EXIT 0
  10 3CC6    #     IF SUCCESSFUL EXIT 1  
  13 3CWQ    #     ENTRY    X3=START ADDRESS OF PCA  
  15 3DBB    #           ALWAYS ASSUMES 4 WORD CONTROL AREA  
   5 3DW2    # EXIT  
  12 3F*L    #              X0,X3,X5,X6=UNDEFINED
  11 3FT=    #                       X7=LINK 
   4 3G#W    #   
   5 3GSG    MTPERI  
  14 3H#6          LDX   6  0(3)                [GET MODE
   9 3HRQ          SMO      FXPMDR   
  16 3J?B          LDX   1  JMTPTR              [KEEP DEV REC PTR
  16 3JR2    MT2   JBUSY 1,MT2                  [LOOP TIL UNBUSY 
  15 3K=L          DCA   1,DLA,6,MODE           [STORE MODE  
   8 3KQ=          LDX   6  2(3) 
  15 3L9W          DCA   1,STO,6,COUNT          [STORE COUNT 
  17 3LPG          LDX   6  3(3)                [STORE BUFFER ADDRESS
   9 3M96          DCA   1,STO,6,CTSA
   9 3MNQ          SMO      FXPMDR   
  17 3N8B          LDN   3  JMTPTR-CPPTR        [GET 'BLOCK START'   
  14 3NN2          GPERI 1,3,NC                 [DO PERI 
   5 3P7L    MTERR   
   9 3PM=          SMO      FXPMDR   
   9 3Q6W          LDX   6  JMTPTR   
  15 3QLG ...MTRR  JBUSY    6,MTRR              [LOOP IF BUSY
  17 3R66 ...      JPMFAIL  6,MT1               [GO TO MT1 IF FAIL   
  18 3RKQ          EXIT  7  1                   [EXIT IF PERI SUCCESSFUL 
   9 3S5B    MT1   SMO      FXPMDR   
  17 3S*8 ...      LDX   6  JMTRW1              [LOAD THE REPLY WORD 
   8 3SG4 ...      SMO      FXPM1
  16 3SLY ...      STO   7  WLINK               [SAVE THE LINK   
  16 3SRS ...      SLC   6  5                   [LOOK AT BIT 5   
  19 3SYN ...      BNG   6  MT1A                [NO CODE IF DECK INOPERABLE  
  16 3T5J ...      SRC   6  2                   [LOOK AT BIT 3   
  20 3T=D ...      BPZ   6  MT1B                [J IF LONG BLOCK DETECTED (BIT 1)
   7 3TC# ...      SRC   6  3
   8 3TJ=          SMO      FXPM1
  19 3W3W          DCH   6  MESSJ+5             [DUMP FAIL NO.:- 0,1,2 OR 3  
   9 3WHG ...MT1A  SMO      FXPMDR   
   9 3X36          LDX   6  JMTGEOG  
  20 3XGQ ...      CALL  7  (JGNO1)             [CONVERT DECK GEOG. NO. TO CHARS.
   8 3Y_L          SMO      FXPM1
   9 3_F=          STO   5  MESSJ+3  
   8 3_YW          SMO      FXPM1
   8 42DG          LDN   3  MESSJ
  20 42Y6 ...      CALL  7  (JCONOUT)           [OUTPUT FAIL MESSAGE ON CONSOLE  
   8 43CQ          BRN      MT3  
   9 43F# ...MT1B  SMO      FXPMDR   
  18 43GW ...      LDX   6  JMTGEOG             [UNIT NUMBER OF MT DECK  
  19 43JD ...      CALL  7  (JGNO1)             [CONVERT IT TO CHARACTER FORM
   8 43L2 ...      SMO      FXPM1
  19 43MJ ...      STO   5  MESSN+3             [AND INSERT IT IN THE MESSAGE
   8 43P6 ...      SMO      FXPM1
  18 43QN ...      LDN   3  MESSN               [PRINT ON THE CONSOLE:   
  18 43S= ...      CALL  7  (JCONOUT)           [^UNIT NN FORMAT ERROR^  
   8 43TS ...      BRN      MT3  
   4 43XB    #   
  10 44C2    #     ROUTINE TO REWIND MT  
   6 44WL    #     EXIT 0
   8 45B=    #           X7=LINK 
   9 45TW    #     X0,X3,X6=UNDEFINED
   4 46*G    #   
   5 46T6    MTREWIND
   8 47#Q          SMO      FXPM1
   8 47SB          STO   7  WLINK
   9 48#2    MT3   SMO      FXPMDR   
  14 48RL          LDX   1  JMTPTR              [GET PTR,
   7 49?=          LDN   6  7
   9 49QW          DCA   1,DLA,6,MODE
   9 4==G          SMO      FXPMDR   
  15 4=Q6          LDN   3  JMTPTR-CPPTR        [BLOCK START 
  15 4?9Q          GPERI 1,3,NC                 [DO REWIND   
   8 4?PB          SMO      FXPM1
   8 4#92          LDX   7  WLINK
  14 4#NL          EXIT  7  0                   [EXIT 0  
   4 4*8=    #   
  15 4*MW    #      ROUTINE TO EXTRACT A SPECIFIED PARAMETER 
  18 4B7G    #      FROM A GIVEN FIELD OF PARAMETERS ADDRESSED BY JCCBUF 
   4 4BM6    #   
  15 4C6Q    #      ON ENTRY  X5 CONTAINS NUMBER OF PARAMETER
  18 4CLB    #                X6 CONTAINS TOTAL NUMBER OF CHARS IN FIELD 
   4 4D62    #   
  20 4DKL    #      ON EXIT   X3 CONTAINS MODIFIER POINTING TO FIRST CHARACTER   
  14 4F5=    #                   OF REQUIRED PARAMETER   
  19 4FJW    #                X7 CONTAINS NUMBER OF CHARACTERS IN PARAMETER  
  14 4G4G    #                   IF ZERO  NULL PARAMETER 
   4 4GJ6    #   
   7 4H3Q    #      LINK X0  
   4 4HHB    #   
  11 4J32    #      ACCS  1,2,4,5 UNCHANGED  
   4 4JGL    #   
  17 4K2=    PMPAR STO   5  XLINK4(1)           [ PARAMETER NUMBER   
  16 4KFW          STO   6  XLINK2(1)           [ CHARS IN FIELD 
   9 4K_G          STOZ     XLINK5(1)
   7 4LF6          LDN   7  1
  16 4LYQ    PAR6  LDCH  6  0(3)                [ GET NEXT CHAR  
   9 4MDB          TXU   6  SPACE(1) 
  15 4MY2          BCC      PAR                 [ J IF SPACE 
   9 4NCL          STO   3  XLINK3(1)
   9 4NX=    PAR3  TXU   6  XCOMMA(1)
  15 4PBW          BCC      PAR1                [ J IF COMMA 
  18 4PWG          ADS   7  XLINK5(1)           [ INCREMENT CHAR. COUNT  
   7 4QB6          BCHX  3  /
   9 4QTQ    PAR5  SBS   7  XLINK2(1)
   9 4R*B          LDX   6  XLINK2(1)
  20 4RT2          BZE   6  PAR2                [ ALL FIELD EXAMINED ? J IF YES  
  16 4S#L          LDCH  6  0(3)                [ LOAD NEXT CHAR 
  16 4SS=          BRN      PAR3                [ LOAD NEXT CHAR 
   9 4T?W    PAR1  SBS   7  XLINK4(1)
   9 4TRG          LDX   6  XLINK4(1)
  18 4W?6          BZE   6  PAR4                [ J IF PARAMETER WANTED  
  21 4WQQ          STOZ     XLINK5(1)           [ INITIALISE CHARS. IN THIS PARAMETER
   7 4X=B          BCHX  3  /
   9 4XQ2          STO   3  XLINK3(1)
  20 4Y9L          BRN      PAR5                [ COUNT CHARS. IN THIS PARAMETER 
   7 4YP=    PAR   BCHX  3  /
   9 4_8W          SBS   7  XLINK2(1)
   9 4_NG          LDX   6  XLINK2(1)
  19 5286          BNZ   6  PAR6                [ J IF MORE CHARS TO CHECK   
   9 52MQ    PAR2  SBS   7  XLINK4(1)
   9 537B          LDX   6  XLINK4(1)
  18 53M2          BZE   6  PAR4                [ J IF PARAMETER WANTED  
   9 546L          STOZ     XLINK3(1)
   9 54L=          STOZ     XLINK5(1)
  18 555W    PAR4  LDX   7  XLINK5(1)           [ SET UP EXIT CONDITIONS 
   9 55KG          LDX   3  XLINK3(1)
  15 5656          EXIT  0  0                   [ OUT AGAIN. 
   4 56JQ    #   
   4 574B    #   
   4 57J2    #   
  21 583L    #     ROUTINE TO CHECK VALIDITY OF PARAMETERS IN CONSOLE INPUT MESSAGE  
  18 58H=    #     IF NO. VALID :NO. STORED IN OCTAL FORMAT IN THDDR+1   
  19 592W    #                       STORED IN CHAR. FORMAT IN TSN AND TSN+1 
  10 59GG    #                   EXIT 0 1
  10 5=26    #     IF INVALID :  EXIT 0 0
  13 5=FQ    #     ENTRY  X3=START ADD. OF MESSAGE   
  13 5=_B    #            X5=NO. OF CHARS IN MESSAGE 
  13 5?F2    #     EXIT   X2,X3,X4,X5,X6,X7=UNDEFINED
   8 5?YL    #            X0=LINK
   4 5#D=    #   
   5 5#XW    VALID   
   8 5*CG          SMO      FXPM1
  15 5*X6          STO   0  WLINK               [STORE LINK  
   8 5BBQ          SMO      FXPM1
   9 5BWB          STO   3  X3STORE  
   8 5CB2          SMO      FXPM1
   9 5CTL          STOZ     XSCRIND  
   8 5D*=          SMO      FXPM1
   9 5DSW          STOZ     XBOTIND  
   8 5F#G          SMO      FXPM1
   9 5FS6          LDX   7  SPACEFIL 
   8 5G?Q          SMO      FXPM1
   8 5GRB          STO   7  TSNX 
   8 5H?2          SMO      FXPM1
   9 5HQL          STO   7  TSNX+1   
  15 5J==          STO   5  2                   [FOR PMPAR.  
  15 5JPW          LDN   4  1                   [MEANS PARA 1
   7 5K9G    V5    LDX   5  4
   7 5KP6          LDX   6  2
   8 5L8Q          LDX   1  FXPM1
  10 5LNB          LDX   3  X3STORE(1)   
  17 5M82          CALL  0  PMPAR               [GET NEXT PARAMETER  
   7 5MML          SBN   4  1
  17 5N7=          BZE   4  V1                  [J TO VALIDATE PARA 1
   7 5NLW          SBN   4  1
  17 5P6G          BZE   4  V2                  [J TO VALIDATE PARA 2
   4 5PL6    #   
  18 5Q5Q          BZE   7  V3                  [ELSE J IF PARA 3 NULL   
   8 5QKB          SMO      FXPM1
  16 5R52          TXU   7  THREE               [INVALID IF NOT  
  15 5RJL          BCS      XEXIT               [3 CHARACTERS
   7 5S4=          LDN   6  0
  16 5SHW          LDN   4  6                   [ELSE CHECK FOR  
  13 5T3G          MVCH  3  3                   [BOT 
   8 5TH6          SMO      FXPM1
   8 5W2Q          TXU   6  XBOT 
   8 5WGB          BCC      V3   
   8 5X22          SMO      FXPM1
  14 5XFL          TXU   6  XSCR                [OR SCR  
  17 5X_=          BCS      XEXIT               [INVALID IF NEITHER  
   8 5YDW          SMO      FXPM1
  17 5YYG          STO   7  XSCRIND             [SET SCR INDICATOR   
   8 5_D6          BRN      V7   
   8 5_XQ    V3    SMO      FXPM1
  16 62CB          STO   7  XBOTIND             [SET INDICATOR   
   8 62X2    V7    SMO      FXPM1
   8 63BL          LDN   3  MESSC
  16 63W=          CALL  7  (JCONOUT)           [PRINT A NEW LINE
   8 64*W          SMO      FXPM1
  13 64TG          LDN   6  TSNX                [COPY
   8 65*6          SMO      FXPM1
  13 65SQ          LDN   7  TSN                 [TSN 
   7 66#B          MOVE  6  2
   8 66S2          SMO      FXPM1
  16 67?L          LDX   0  WLINK               [AND EXIT VALID  
   7 67R=          EXIT  0  1
   4 68=W    #   
   4 68QG    #   
  17 69=6    V1    BZE   7  XEXIT               [EXIT IF PARA 1 NULL 
   8 69PQ          SMO      FXPM1
  17 6=9B          TXL   7  NINE                [CHECK NO OF CHARS<9 
  16 6=P2          BCC      XEXIT               [INVALID IF NOT  
   8 6?8L          LDCH  4  0(3) 
   9 6?N=          TXL   4  XEIGHT(1)
   8 6#7W          BCS      V1A  
   7 6#MG          SBN   7  3
  21 6*76          BPZ   7  XEXIT               [J IF MORE THAN 4 CHARACTERS: ERROR  
   7 6*LQ          ADN   7  3
   7 6B6B          LDN   5  0
   7 6BL2          LDN   4  5
   7 6C5L          SMO      7
  18 6CK=          MVCH  3  0                   [MOVE INTO THE TOP OF X5 
   9 6D4W          TXU   5  XDA(1)   
  21 6DJG          BCC      XDADUMP             [J IF ^DA^ : A DISC DUMP IS WANTED   
   9 6F46          TXU   5  XMT(1)   
  17 6FHQ          BCS      XEXIT               [ERROR IF NOT ^MT^   
   7 6G3B          SBN   2  3
  21 6GH2          LDX   3  X3STORE(1)          [IF THE FIRST PARAMETER IS ^MT^ ...  
   7 6H2L          SLC   3  2
  21 6HG=          ADN   3  3                   [ ...  WE SIMPLY MAKE ^MT^ DISAPPEAR 
   7 6H_W          SRC   3  2
  20 6JFG          STO   3  X3STORE(1)          [ ...  FROM THE INPUT MESSAGE!   
  18 6J_6          BNG   2  XEXIT               [ERROR IF THERE ARE  ... 
  20 6KDQ          BZE   2  XEXIT               [   ...  NO MORE CHARACTERS LEFT 
   7 6KYB          LDN   4  1
   8 6LD2          BRN      V5   
   8 6LXL    V1A   SMO      FXPM1
  14 6MC=          LDN   4  TSNX                [COPY TSN
  15 6MWW          STO   3  6                   [SAVE ADDRESS
   7 6NBG          SMO      7
   7 6NW6          MVCH  3  0
  17 6P*Q          LDX   3  6                   [RESET START ADDRESS 
   7 6PTB          LDN   5  0
   8 6Q*2    V4    LDCH  6  0(3) 
   8 6QSL          SMO      FXPM1
   9 6R#=          TXL   6  XEIGHT   
  16 6RRW          BCC      XEXIT               [EXIT IF INVALID 
  19 6S?G          SRC   6  3                   [MOVE OCTAL CHAR TO M.S END  
  17 6SR6          SLL   56 3                   [AND SET UP NO IN X5 
   7 6T=Q          BCHX  3  /
  16 6TQB          BCT   7  V4                  [LOOP TIL DONE   
   8 6W=2          SMO      FXPM1
  16 6WPL          STO   5  THDDR+1             [SET TSN IN HDDR,
  15 6X9=          LDN   4  2                   [SET PARA NO 
  16 6XNW          BRN   V5                     [TO 2 AND GET IT.
   4 6Y8G    #   
  19 6YN6    V2    BZE   7  XEXIT               [EXIT INVALID IF PARA 2 NULL 
   8 6_7Q          SMO      FXPM1
  18 6_MB          TXL   7  XFOUR               [INVALID IF NOT <4 CHARS 
  16 7272          BCC   XEXIT                  [ EXIT INVALID   
   4 72LL    #   
  10 736=    #     NOW CONVERT TO BINARY 
   4 73KW    #   
   7 745G          LDN   4  0
  16 74K6          LDN   5  0                   [RECEIVES ANSWER 
   8 754Q    V6    CDB   4  0(3) 
  16 75JB          BCS   XEXIT                  [EXIT IF INVALID 
  15 7642          BCHX  3  /                   [STEP ADDR   
  16 76HL          BCT   7  V6                  [LOOP TIL DONE   
   9 773=          SMO      FXPMDR   
  15 77GW          STO   5  JMTGEOG             [STORE ANSWER
   7 782G          LDN   4  3
   7 78G6          BRN   V5  
   5 78_Q    XEXIT   
   8 79FB          SMO      FXPM1
   8 79_2          LDX   0  WLINK
   7 7=DL          EXIT  0  0
   4 7=Y=    #   
  17 7?CW    #   DA DISCOVERED IN INPUT.SINCE WE ARE NOT WANTED,WE   
   9 7?XG    #   YIELD TO PMDUMPDA.  
   4 7#C6    #   
   5 7#WQ    XDADUMP 
   9 7*BB          LDN   4  64+128   
   9 7*W2          SMO      FXPMDR   
   9 7B*L          ERS   4  ALLPOST  
  11 7BT=          PMOVE    PMDUMPDA,FXPM1,2 
   4 7C#W    #   
   4 7CSG    #   
   6 7D#6    XK1PMDUMPMT 
   4 7DRQ    #   
  18 7F?B    #     CODING REQUESTING DUMP TAPE NO. AND DECK NO OF SAME   
  12 7FR2    #     VALIDITY OF REPLY ALSO CHECKED
   4 7G=L    #   
   8 7GQ=          LDX   1  FXPM1
  20 7H9W          ADS   1  MTREAD+3(1)         [DATUMISE TAPE PERI CONTROL AREAS
  10 7HPG          ADS   1  MTWRI+3(1)   
   8 7J96    T     SMO      FXPM1
   8 7KN2          LDN   3  MESSD
  17 7L7L          CALL  7  (JCONOUT)           [ASK FOR TSN/DECK NO 
  14 7LM=    TZ2   CALL  7  (JCTWIN)            [READ I/P
  14 7M6W          BRN   TZ2                    [NOT OK  
  16 7MLG    TZ1   LDN   3  FCINBUF             [SET X3 FOR VALID
  14 7N66          ANDN  6  #777                [COUNT   
  15 7NKQ          NGX   5  6                   [CHARS READ  
  14 7P5B          ADN   5  64                  [IN X5   
  15 7PK2          BZE   5  T                   [J IF INVALID
   9 7Q4L    TZ3      LDX   1  FXPM1 
  16 7QJ=          CALL  0  VALID               [IS REPLY VALID? 
  15 7R3W          BRN      T                   [J IF INVALID
   8 7R94 ...      SMO      FXPM1
  19 7RB= ...      STOZ     MIND                [STOZ MESSAGE O/P INDICATOR  
  17 7RHG          LDX   1  JCTWPTR             [NOW REQUIRE TO UNSET
  18 7S36          LINT  1,6                    [T/W ENGAGE BUTTON BIT   
   8 7SGQ          SMO      FXPM1
  17 7T2B          DVS   5  TWEN4               [SO CALCULATE POSN   
  14 7TG2          STO   5  0                   [REMEMBER
  14 7T_L          STO   6  2                   [RESULT  
  20 7WF=          SLL   6  9                   [SET RESULT IN  REMAINDE/QUOTIENT
  16 7WYW          SRL   56 9                   [FORM IN XTWBIT  
   8 7XDG          SMO      FXPM1
   9 7XY6          STO   6  XTWBIT   
  21 7YCQ          LDCT  6  #400                [SET UP MASK IN ORDER TO UNSET BIT   
  18 7YXB          BZE   0  TS1                 [J WHEN GOT RIGHT POSN   
   7 7_C2    TS2   SRL   6  1
   8 7_WL          BCT   0  TS2  
  14 82B=    TS1   ORS   6  EBA(2)              [UNSET   
  14 82TW          ERS   6  EBA(2)              [T/W BIT 
   4 83*G    #   
  17 83T6    #     CODING TO CHECK SPECIFIED DECK FOR SPECIFIED TAPE,
  13 84#Q    #     REWIND IT AND LOOK AT HEADER LABEL
   4 84SB    #   
   9 85#2    T3    SMO      FXPMDR   
  17 85RL          LDX   5  JMTGEOG             [GET DECK GEOG NO.   
  19 86?=          FINDPEREC 1,APGEOG,5         [AND FIND RELEVANT DEV RECORD
  19 86QW          BNG   1  T                   [REPEAT MESS IF NON-EXISTENT 
  14 87=G          LTYPE 1,5                    [CHECK   
   8 87Q6          SMO      FXPM1
  14 889Q          TXU   5  XFIVE               [ITS A MT
  15 88PB          BCS   T                      [TCH! TCH!   
   9 8992          SMO      FXPMDR   
  18 89NL          LDX   5  JMTPTR              [IS THIS THE FIRST MT?   
  14 8=8=          BZE   5  T2                  [J IF SO 
  19 8=MW          TXU   5  1                   [ELSE IS IT DIFFERENT TO LAST
  16 8?7G          BCC   T1                     [TIME, J IF SAME 
   9 8?M6          SMO      FXPMDR   
  18 8#6Q          LDN   4  JMTREC              [ELSE UNDUMP OLD DEV REC 
   9 8#LB          MOVE  4  K53-K50  
   4 8*62    #   
   9 8*KL    T2    SMO      FXPMDR   
  16 8B5=          STO   1  JMTPTR              [STORE NEW PTR   
   9 8BJW          SMO      FXPMDR   
  16 8C4G          LDN   2  JMTREC              [AND NEW DEV REC 
   9 8CJ6          MOVE  1  K53-K50  
   4 8D3Q    #   
   9 8DHB    T1    SMO      FXPMDR   
  18 8F32          LDX   1  JMTPTR              [CHECK IF DECK ENGAGED   
  14 8FGL          JENG  1,M1                   [J IF SO 
   8 8G2=          SMO      FXPM1
  19 8GFW          LDX   6  MIND                [ELSE IF INDICATOR NOT SET   
   8 8G_G          BNZ   6  T1   
   8 8HF6          SMO      FXPM1
  20 8HYQ          LDN   5  TSN                 [ASK FOR OUR TAPE TO BE LOADED   
   8 8JDB          LDCT  6  #400 
   8 8JY2          SMO      FXPM1
   9 8KCL          ORN   6  MESSH+4  
  18 8KX=          MVCH  5  8                   [MOVE TSN INTO MESSAGE   
   9 8LBW          SMO      FXPMDR   
  18 8LWG          LDX   6  JMTGEOG             [PICK UP THE DECK NUMBER 
  18 8MB6          CALL  7  (JGNO1)             [CONVERT IT TO CHARACTERS
   7 8MTQ          LDN   6  5
   8 8N*B          LDCT  7  #600 
   8 8NT2          SMO      FXPM1
  19 8P#L          ORN   7  MESSH+8             [AND PUT IT INTO THE MESSAGE 
   7 8PS=          MVCH  6  4
   8 8Q?W          SMO      FXPM1
   8 8QRG          LDN   3  MESSH
   9 8R?6          CALL  7  (JCONOUT)
  17 8RQQ          LDN   7  7                   [AND SET INDICATOR   
   8 8S=B          SMO      FXPM1
   8 8SQ2          STO   7  MIND 
  17 8T9L          BRN   T15                    [J CHECK T/W AND MT  
   4 8TP=    #   
   8 8W8W    M1    SMO      FXPM1
  16 8WNG          STOZ  MIND                   [UNSET INDICATOR 
   8 8X86          SMO      FXPM1
   8 8XMQ          LDN   3  MTREW
  15 8Y7B          CALL  7  MTPERI              [REWIND MT   
  17 8YM2          BRN   T                      [FAIL SO START AGAIN.
   8 8_6L          SMO      FXPM1
  15 8_L=          LDN   3  MTREAD              [READ HEADER 
   9 925W          CALL  7  MTPERI   
  14 92KG          BRN   T                      [FAIL.   
   4 9356    #   
  10 93JQ    #     NOW CHECK HEADER...   
   8 944B          SMO      FXPM1
   8 94J2          LDX   6  MTBUF
   8 953L          SMO      FXPM1
   8 95H=          TXU   6  THDDR
  18 962W          BCC      T17                 [J IF STANDARD HDDR LABEL
   9 96GG          SMO      FXPMDR   
  19 9726          LDX   6  JMTGEOG             [GET DECK NO AND CONVERT TO  
  15 97FQ          CALL  7  (JGNO1)               [CHARACTERS
   8 97_B          SMO      FXPM1
   9 98F2          STO   5  MESSE+5  
   8 98YL          SMO      FXPM1
  18 99D=          LDN   3  MESSE              [-STANDARD HEADER LABEL   
  21 99XW          CALL  7  (JCONOUT)              [CONSOLE MESSAGE 'TAPE ON DECK NN 
   9 9=CG          SMO      FXPMDR   
  14 9=X6          LDX   1  JMTPTR              [SET UP  
  13 9?BQ          LDN   6  7                   [C.A 
  15 9?WB          DCA   1,DLA,6,MODE           [TO REWIND   
   9 9#B2          SMO      FXPMDR   
  14 9#TL          LDN   2  JMTPTR-CPPTR        [OUR MT  
  14 9**=          GPERI 1,2,NC                 [DO IT.  
  16 9*SW          BRN   T                      [START OVER AGAIN
   8 9B#G    T17   SMO      FXPM1
   9 9BS6          LDX   6  MTBUF+1  
   8 9C?Q          SMO      FXPM1
   9 9CRB          TXU   6  THDDR+1  
  16 9D?2          BCC      T18                 [J IF TSN CORRECT
   8 9DQL    T60   SMO      FXPM1
  16 9F==          LDN   5  TSN                 [OUTPUT MESSAGE: 
   8 9FPW          SMO      FXPM1
  18 9G9G          LDN   6  MESSK+2             ['TSN NOT ON DECK N'...  
   7 9GP6          MOVE  5  2
   9 9H8Q          SMO      FXPMDR   
   9 9HNB          LDX   6  JMTGEOG  
   9 9J82          CALL  7  (JGNO1)  
   8 9JML          SMO      FXPM1
   9 9K7=          STO   5  MESSK+8  
   8 9KLW          SMO      FXPM1
   8 9L6G          LDN   3  MESSK
   9 9LL6          CALL  7  (JCONOUT)
   8 9M5Q          SMO      FXPM1
   8 9MKB          LDN   3  MTREW
  15 9N52          CALL  7  MTPERI              [REWIND MT   
   8 9NJL                   NULL 
  15 9P4=          BRN   T                      [TRY AGAIN   
   4 9PHW    T18 
   8 9Q3G          SMO      FXPM1
   9 9QH6          LDX   5  XSCRIND  
  17 9R2Q          BNZ   5  T13                 [J IF SCR SPECIFIED  
   8 9RGB          SMO      FXPM1
   9 9S22          LDX   4  MTBUF+2  
   8 9SFL          SMO      FXPM1
   9 9S_=          LDX   5  MTBUF+3  
   8 9TDW          SMO      FXPM1
   9 9TYG          LDX   6  MTBUF+4  
   8 9WD6          SMO      FXPM1
   9 9WXQ          TXU   4  THDDR+2  
   8 9XCB          SMO      FXPM1
   9 9XX2          TXU   5  THDDR+3  
   8 9YBL          SMO      FXPM1
   9 9YW=          TXU   6  THDDR+4  
  21 9_*W          BCS      T14                 [J IF HDDR LABEL NOT 'G3POSTMORTEM'  
   9 9_TG          SMO      FXPMDR   
   9 =2*6          LDX   1  JMTPTR   
  17 =2SQ          JWPR  1,T20                  [J IF OUR MT HAS WPR 
   4 =3#B    T15 
   9 =3S2          SMO      FXPMDR   
  14 =4?L          LDX   1  JMTPTR          [NOW FIND MT 
  16 =4R=          LINT  1,6                [ENGAGE BUTTON BIT   
   8 =5=W          SMO      FXPM1
  15 =5QG          DVS   5  TWEN4           [AND UNSET IT.   
   7 =6=6          STO   5  0
   7 =6PQ          STO   6  2
   7 =79B          SLL   6  9
   7 =7P2          SRL   56 9
   8 =88L          SMO      FXPM1
  16 =8N=          STO   6  XMTBIT              [REMEMBER POSN   
   8 =97W          LDCT  6  #400 
   8 =9MG          BZE   0  TT1  
   7 ==76    TT2   SRL   6  1
   8 ==LQ          BCT   0  TT2  
   9 =?6B    TT1   ORS   6  EBA(2)   
   9 =?L2          ERS   6  EBA(2)   
   8 =#5L          SMO      FXPM1
  17 =#K=          LDX   5  MIND                [MESSAGE ALREADY O/P?
  14 =*4W          BNZ   5  T30                 [J IF SO 
   8 =*JG          SMO      FXPM1
  14 =B46          LDN   5  TSN                 [ELSE WE 
   8 =BHQ          SMO      FXPM1
   9 =C3B          LDN   6  MESSG+3  
  20 =CH2          MOVE  5  2                   [INSERT TSN IN CONSOLE MESSAGE   
   8 =D2L          SMO      FXPM1
  18 =DG=          LDN   3  MESSG              [OUTPUT 'LOAD WPR' MESSAGE
   9 =D_W          CALL  7  (JCONOUT)
   8 =FFG          BRN      T30  
   8 =F_6    T14   SMO      FXPM1
   8 =GDQ          TXU   4  SCRA 
   8 =GYB          SMO      FXPM1
   9 =HD2          TXU   5  SCRA+1   
   8 =HXL          SMO      FXPM1
   9 =JC=          TXU   6  SCRA+2   
  17 =JWW          BCC      T13                 [J IF SCRATCH TAPE   
   9 =KBG          SMO      FXPMDR   
  14 =KW6          LDX   1  JMTPTR              [REWIND  
  13 =L*Q          LDN   6  7                   [THE 
  13 =LTB          DCA   1,DLA,6,MODE           [MT  
   9 =M*2          SMO      FXPMDR   
  10 =MSL          LDN   2  JMTPTR-CPPTR 
  14 =N#=          GPERI 1,2,NC                 [PERI.   
   8 =NRW          SMO      FXPM1
   8 =P?G          LDN   5  TSN  
   8 =PR6          SMO      FXPM1
   9 =Q=Q          LDN   6  MESSF+3  
  20 =QQB          MOVE  5  2                   [INSERT TSN IN CONSOLE MESSAGE   
   8 =R=2          SMO      FXPM1
  18 =RPL          LDN   3  MESSF              [INCORRECT HEADER LABEL   
  21 =S9=          CALL  7  (JCONOUT)              [CONSOLE MESSAGE 'TAPE NNNNNNNN   
  16 =SNW          BRN   T                      [REQUEST NEW MT  
   9 =T8G    T13   SMO      FXPMDR   
   9 =TN6          LDX   1  JMTPTR   
  14 =W7Q          JWPR  1,M3                   [J IF WPR
   7 =WMB          BRN   T15 
   8 =X72    M3    SMO      FXPM1
   8 =XLL          LDN   3  MTREW
  15 =Y6=          CALL  7  MTPERI              [REWIND MT   
  17 =YKW          BRN   T                      [START AGAIN IF FAIL 
   9 =_5G          SMO      FXPMDR   
   9 =_K6          LDX   6  JMTGEOG  
   7 ?24Q          SRC   6  9
   8 ?2JB          SMO      FXPM1
  19 ?342          STO   6  THDDR+9             [WORD 9 OF TAPE HEADER LABEL 
  17 ?3HL          LDX   6  EDATE               [STORE 'DATE WRITTEN'
   8 ?43=          SMO      FXPM1
  15 ?4GW          STO   6  THDDR+8             [IN HEADER   
   8 ?52G          SMO      FXPM1
   8 ?5G6          LDN   5  THDDR
   8 ?5_Q          SMO      FXPM1
  21 ?6FB          LDN   6  MTBUF               [MOVE HDDR LABEL INTO MT WRITE BUFFER
   8 ?6_2          MOVE  5  10   
   8 ?7DL          SMO      FXPM1
   8 ?7Y=          LDN   3  MTWRI
  16 ?8CW          CALL  7  MTPERI              [WRITE HDDR LABEL
  18 ?8XG          BRN   T                      [FAIL SO REQUEST NEW MT. 
  13 ?9C6                                       [NO  
   8 ?9WQ          SMO      FXPM1
   9 ?=BB          STOZ     PMCOUNT  
   7 ?=W2          BRN      W
   4 ??*L    #   
  20 ??T=    #     CODING TO POSITION 'G3POSTMORTEM' TAPE BEFORE TRAILER LABEL   
   4 ?##W    #   
   4 ?#SG    T20 
   8 ?*#6          SMO      FXPM1
   9 ?*RQ          STOZ     PMCOUNT  
   8 ?B?B          SMO      FXPM1
  19 ?BR2          LDX   0  XBOTIND             [J IF POSTDUMP TO BE WRITTEN 
  15 ?C=L          BNZ   0  W                   [AFTER BOT   
   8 ?CQ=    M4    SMO      FXPM1
   9 ?D9W          LDN   3  MTSKIP   
  15 ?DPG          CALL  7  MTPERI              [SKIP TO TM  
   7 ?F96          BRN   T   
   8 ?FNQ          SMO      FXPM1
   9 ?G8B          LDN   3  MTREAD   
  16 ?GN2          CALL  7  MTPERI              [READ NEXT BLOCK 
   7 ?H7L          BRN   T   
   8 ?HM=          SMO      FXPM1
   8 ?J6W          LDCH  6  MTBUF
   7 ?JLG          SBN   6  6
  20 ?K66          BNZ   6  M2                  [J IF NOT START OF S/F SENTINEL  
   7 ?KKQ          LDN   3  3
   8 ?L5B    SSFS  SMO      FXPM1
  14 ?LK2          LDX   4  MTBUF+1(3)          [S/F NAME
   8 ?M4L          SMO      FXPM1
  10 ?MJ=          TXU   4  POSTDUMP-1(3)
  18 ?N3W          BCS      M5                  [J IF NOT 'POSTDUMP' S/F 
   8 ?NHG          BCT   3  SSFS 
   7 ?P36          LDN   4  1
   8 ?PGQ          SMO      FXPM1
  17 ?Q2B          ADS   4  PMCOUNT             [UPDATE DUMP COUNT   
   8 ?QG2          BRN      M4   
   7 ?Q_L    M2    SBN   6  1
  18 ?RF=          BNZ   6  M4                  [J IF NOT TRAILER LABEL  
   8 ?RYW          SMO      FXPM1
   9 ?SDG          LDN   3  MTBACK   
  15 ?SY6          CALL  7  MTPERI              [BACKSPACE   
  13 ?TCQ          BRN   T                      [PAST
   8 ?TXB          SMO      FXPM1
  15 ?WC2          LDN   3  MTBACK              [PRECEDING   
  13 ?WWL          CALL  7  MTPERI              [TM  
   7 ?XB=          BRN   T   
   8 ?XTW          SMO      FXPM1
   9 ?Y*G          LDN   3  MTBACK   
  16 ?YT6          CALL  7  MTPERI              [BACKSPACE AND   
  18 ?_#Q          BRN   T                      [THEN READ A BLOCK- UNDER
   8 ?_SB          SMO      FXPM1
  21 #2#2          LDN   3  MTREAD              [GEORGE EXEC A WRITE CANNOT FOLLOW   
  20 #2RL          CALL  7  MTPERI              [IMMEDIATELY AFTER A BACKSPACE   
   7 #3?=          BRN   T   
   7 #3QW          BRN      W
   8 #4=G    M5    SMO      FXPM1
   9 #4Q6          LDN   6  MESSM+2  
   8 #59Q          SMO      FXPM1
   8 #5PB          LDN   5  TSN  
  19 #692          MOVE  5  2                   [STORE TSN IN CONSOLE MESSAGE
   8 #6NL          SMO      FXPM1
  18 #78=          LDN   3  MESSM              [INCORRECT SUBFILE NAME'  
  21 #7MW          CALL  7  (JCONOUT)              [CONSOLE MESSAGE 'TAPE NNNNNNNN   
  15 #87G          CALL  7  MTREWIND            [REWIND MT   
  16 #8M6          BRN      T                   [REQUEST NEW MT  
   4 #96Q    #   
  20 #9LB    #     CODING TO LOOK AT PERIPHERAL ENGAGE BUTTON AREA TO CHECK IF   
  16 #=62    #     CONSOLE T/W ENGAGED BIT SET OR IF MT ENGAGED  
   4 #=KL    #   
   4 #?5=    T30 
   7 #?JW          LDN   4  0
   8 ##4G          SMO      FXPM1
   9 ##J6          LDX   5  XTWBIT   
  18 #*3Q          LDCH  2  5                   [GET EBA MODIFIER IN X2  
  19 #*HB          SLL   45 9                   [GET COUNT OF BIT POSN IN X4 
  20 #B32          LDCT  3  #400                [SET BIT IN X3 TO REQD POSN...   
   8 #BGL          BZE   4  T31  
   7 #C2=    T35   SRL   3  1
   8 #CFW          BCT   4  T35  
  16 #C_G    T31   ANDX  3  EBA(2)              [CHECK T/W BIT   
  14 #DF6          BNZ   3  T32                 [J IF SET
   8 #DYQ          SMO      FXPM1
  17 #FDB          LDX   5  XMTBIT              [ELSE CHECK MT BIT   
   7 #FY2          LDCH  2  5
   7 #GCL          SLL   45 9
   8 #GX=          LDCT  3  #400 
   8 #HBW          BZE   4  T33  
   7 #HWG    T34   SRL   3  1
   8 #JB6          BCT   4  T34  
   9 #JTQ    T33   ANDX  3  EBA(2)   
  18 #K*B          BZE   3  T30                 [RESUME SEARCH IF NOT SET
  16 #KT2          ERS   3  EBA(2)              [ELSE UNSET IT   
  17 #L#L          BRN   M1                     [AND RESUME SEARCH   
   4 #LS=    #   
  14 #M?W    #     T/W BIT SET SO CLEAR AND READ INPUT   
   4 #MRG    #   
  14 #N?6    T32   ERS   3  EBA(2)              [CLEAR IT
  15 #NQQ    T38   CALL  7  (JCTWIN)              [READ I/P  
  15 #P=B          BRN   T38                    [LOOP IF NOT 
  14 #PQ2          ANDN  6  #777                [GET NO. 
  13 #Q9L          NGX   5  6                   [OF  
  14 #QP=          ADN   5  64                  [CHARS   
  15 #R8W          LDN   3  FCINBUF             [READ IN X5  
  21 #RNG          BZE   5  T30                 [IGNORE MESSAGE IF NONE OR ALL SPACES
   8 #S86          BRN      TZ3  
   4 #SMQ    #   
   4 #T7B    #   
  18 #TM2    #   PMDUMPDA WAS RUDELY INTERRUPTED & SO WE ARE TAKING OEVR 
   4 #W6L    #   
   6 #WL=    XK2PMDUMPMT 
   8 #X5W          LDX   1  FXPM1
  20 #XKG          ADS   1  MTREAD+3(1)         [DATUMISE TAPE PERI CONTROL AREAS
  10 #Y56          ADS   1  MTWRI+3(1)   
  19 #YJQ          STOZ     MIND(1)             [STOZ MESSAGE O/P INDICATOR  
   9 #_4B          SMO      FXPMDR   
  19 #_J2          LDX   6  JPCONREPLY             [LOAD CONSOLE REPLY WORD  
   8 *23L          BRN      TZ1  
   4 *2H=    #   
  21 *32W    #     CODING TO INITIATE TRANSFER TO #SEG PMDUMPA WHERE THE POSTMORTEM  
  10 *3GG    #     DUMP WILL TAKE PLACE  
   4 *426    #   
   8 *4FQ    W     SMO      FXPM1
   9 *4_B          LDX   6  PMCOUNT  
  19 *5F2          ADN   6  1                   [NO. INCLUDING PRESENT DUMP  
  17 *5YL          CALL  7  (JGNO1)               [CONVERT TO DECIMAL
   8 *6D=          SMO      FXPM1
  18 *6XW          STO   5  MESSL+7             [STORE COUNT IN MESSAGE  
   8 *7CG          SMO      FXPM1
   8 *7X6          LDN   3  MESSL
   9 *8BQ          CALL  7  (JCONOUT)
  17 *8WB          PMOVE     PMDUMP,FXPM1,1            [ OFF WE GO   
   4 *9B2    #   
  16 *9TL    MTBUF                              [MAG. TAPE BUFFER
   4 *=*=    #END
   6 ____ ...554711620005
