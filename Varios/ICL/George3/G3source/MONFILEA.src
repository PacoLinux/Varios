  16 22FL ...#SEG  MONFILEA                     [RICHARD GRIMWADE
   8 22PD ...#OPT  K0MONFILEA=0  
  15 22_= ...#LIS  K0MONFILEA>K0MONCHAPS>K0ALLGEO>K0GREATGEO 
   7 23DW ...      8HMONFILEA
  12 23YG ...      SEGENTRY K1MONFILEA,SENTRY1   
  16 24D6 ...      FSHENTRY K2MONFILEA,SENTRY2,SENTRY2,SENTRY2   
  16 24XQ ...      FSHENTRY K3MONFILEA,SENTRY3,SENTRY3,SENTRY3   
   4 28SQ    #   
  11 29#B    # OVERFLOW SEGMENT FOR MONFILE  
  12 29S2    # PRINCIPLE DUTY AS MESSAGE ROUTER. 
  17 2=?L    # MESSAGE PARAMETERS ARE IN ASET BLOCK (BUT NOTE 'KNOWN'
  16 2=R=    # PDC PARAMS),FINAL MESSAGE ASSEMBLED IN AOUT AND IS
  17 2?=W    # THEN SENT TO APPROPRIATE DESTINATIONS IN STRICT ROTA  
   4 2?QG    #   
   7 2#=6    # DESTINATIONS: 
   4 2#PQ    #   
   7 2*9B    # SYSTEM JOURNAL
   9 2*P2    # MONITORING FILE  (!)  
   7 2B8L    # MOP CONSOLES  
   8 2BN=    # OPERATORS CONSOLES
   7 2C7W    # GSCAN BUFFER  
   7 2CMG    # REPLY 'BUFFER'
   9 2D76    # RETAIN BLOCK FACILITY 
  14 2DLQ    # TABLE FOR GOING ACROSS TO GET MESSAGE HELD
   9 2F6B    # IN ANOTHER SEGMENT:   
   4 2H4W    #   
   4 2HJG    #   
   7 2J46     MFORDER MFSETAB
   5 2JHQ     MFDEFS 
   8 2JKN ...       FSHCODE  AORB
   4 2JML ...(   
   5 2JPJ ...SLINK2  
  10 2JRG ...      LINK     MONFILEA,2   
   5 2JTD ...SLINK3  
  10 2JXB ...      LINK     MONFILEA,3   
   4 2J_# ...)   
   9 2K3B    WJOUR CATMASK  JOURNAL  
   9 2KH2    WCLUS CATMASK  CLUSTER  
   9 2L2L    WPRIN CATMASK  FORCEPRI 
  17 2LG=    SCAMSK CATMASK OL,PM,LS            [FORBIDDEN GSCAN'S   
   4 6QSL    #   
  15 6R#=    #     THIS PART DEALS WITH OUTPUT OF THE BASIC  
  14 6RRW    #     MESSAGE WHICH HAS BEEN COMPILED INTO  
   8 6S?G    #      A BOUT BLOCK.
  16 6SR6    #     THE DESTINATIONS ARE HELD AS BIT POSITIONS IN 
  12 6T=Q    #     THE WORD GDESTINY OF THE ACTY.
  16 6TQB    #     OUTPUT IS IN STRICT ROTA, BUT THIS ROTA MAY   
  17 6W=2    #     BE TAMPERED WITH USING 'MFSETAB' IN MONFILE, BUT  
  16 6WPL    #     MONFILE O/P SHOULD BE PREVIOUS TO ANY ROUTINE 
  10 6X9=    #     WHICH USES 'WRITE'.   
  12 6XNW    #     AND DITTO 'MOP' AND 'SCANB'   
   9 6Y8G    OUTAB [DESTINATION ROTA 
   4 6YN6    #   
   9 6_7Q          MFORDER  SHUFFLE  
  17 6_MB ...SENTRY1     [ENTERED FROM MONFIL, MONOLITH AND K4MONFILE
  16 72LL    #     SOME EXCLUSIVE DESTINIES MUST BE DEALT WITH:  
  10 736=          LDX   7  MDESTINY(2)  
   7 738F ...#SKI  JWPHASE4  
   4 73=N ...(   
   8 73#X ...      SRL   7  11   
   7 73C6 ...      SRC   7  1
   4 73F* ...)   
   8 73HJ ...#SKI  JWPHASE4<1$1  
  17 73KW          SRL   7  12                  [SINCE MARKS=MDESTINY
   8 745G          LDN   0  MALL 
   7 74K6          ANDX  0  7
  17 754Q          BZE   0  OPUS1               [J IF ROUTE NOT ALL  
   7 75JB          NGN   0  1
  11 7642          ERN   0  OP+REMOTE+MBOTH  
  17 76HL          ANDS  0  7                   [REMOVE OTHER ROUTES 
   8 773=          BRN      OPEND
   8 77GW    OPUS1 LDN   0  MBOTH
   7 782G          ANDX  0  7
  16 78G6          BZE   0  OPUS2               [J IF NOT 'BOTH' 
   7 78_Q          NGN   0  1
   9 79FB          ERN   0  REMOTE+OP
   7 79_2          ANDX  7  0
   8 7=DL          BRN      OPEND
   9 7=Y=    OPUS2 LDN  0   OP+REMOTE
   7 7?CW          ANDX  0  7
   9 7?XG          ERN   0  OP+REMOTE
  17 7#C6          BNZ   0  OPEND               [J IF NOT OP&REMOTE  
  16 7#WQ          ERN   7  OP+REMOTE+MBOTH     [ELSE SET (BOTH) 
   8 7*BB    OPEND SLC   7  12   
  10 7*W2          STO   7  MDESTINY(2)  
   9 7B*L          TRACE    7,DESTINY
   4 7BT=    #   
  10 7C#W          MHUNTW   1,GMON,BOUT  
   8 7CSG          LDCT  0  #100 
  10 7D#6          ANDX  0  ARECB+1(1)   
   7 7DRQ          SLL   0  2
  15 7F?B          STO   0  AWORK1(2)           [SET MARKER  
   7 7F*# ...#SKI  JWPHASE4  
   4 7FC= ...(   
   8 7FF8 ...      ANDN  7  #4000
  16 7FH6 ...      BZE   7  XECUTE              [J IF NOT HLS&PA 
  20 7FK4 ...      ERS   7  GDESTINY(2)         [REMOVE HLS&UNPACKED DESTINY IND.
   9 7FM2 ...      STOZ     AWORK4(2)
   8 7FNY ...      BRN      ZHLS 
   4 7FP_ ...)   
   5 7FR2    XECUTE  
   8 7G=L          LDX   1  FX1  
  18 7GQ=    [ WE PRESUME THAT SJPAK IS B0 AND HAS BEEN O/P PREVIOUSLY   
   8 7H9W          LDX   2  FX2  
  10 7HPG          LDXC  0  GDESTINY(2)  
  17 7J96          LDEX  4  0                   [OUTPUT ROUTINE NO   
   7 7JNQ          ERX   0  4
  15 7K8B          BZE   0  OFF                 [FINISHED !  
  17 7KN2    ROUND BNG   0  NEXT                [NEXT SIG BIT FOUND  
   7 7L7L          SLL   0  1
   8 7LM=          BUX   4  ROUND
   9 7M6W    OFF   MFREEW   GMON,BOUT
   9 7MLG          LDEX  0  AWORK1(2)
  15 7N66          BZE   0  ZUP                 [J IF NO AOUT
   9 7NKQ          MFREEW   GMON,AOUT
   4 7P5B    ZUP 
   5 7PK2          UP
  15 7Q4L    NEXT  STOZ     AWORK4(2)           [INITIALIZE  
   7 7QJ=          DEX   4  0
  10 7R3W          STO   0  GDESTINY(2)  
   9 7RHG          ADN   4  OUTAB(1) 
  17 7S36          EXIT  4  0                   [OFF TO O/P ROUTIE   
  11 7SGQ    WALMS CATMASK  CENTRAL,CLUSTER  
  10 7T2B    TRALCEN     CATMASK CENTRAL 
  16 7TG2    MASCTAB        [USED BY WMASC - ROUTING PARAM ORDER 
  16 7T_L          BRN      WSJ                 [1 SYSTEM JOURNAL
  16 7WF=          BRN      WSCAN               [2 SCAN BUFFER   
  15 7WYW          BRN      WMONF               [3 MONFILE   
  14 7XDG          BRN      WMOP                [4 MOP   
  18 7XY6          BRN      WALL                [5 O/P TO ALL CONSOLES!  
  21 7YCQ          BRN      WCONS               [6 O/P TO CENTRAL     !KEEP TOGETHER 
  15 7YXB          BRN      WCLS                [7 O/P TO OWN
  14 7_C2          BRN      WREPL               [9  REPLY
   7 7_J8 ...#SKI  JWPHASE4  
  16 7_PB ...      BRN      WHLS                [9 HLSTEMP OUTPUT
   9 7_WL    #DEF  XEND=0?-MASCTAB+1 
   4 82B=    #   
  12 82TW    #     S/R TO FIND NEXT RELEVANT PART
  11 83*G    #     FROM ROUTE NUMBER IN X7   
  17 83T6    #     PTR GIVEN IN X3 AND AWORK4->NEXTPART (OR 0 OR -VE)
   4 84#Q    #   
  15 84SB    #     AWORK3 : TOP 6 BITS RESERVED FOR 'WLEN'   
  14 85#2    #              LAST 9 BITS 2_(PART NUMBER)  
   4 85RL    #   
   8 86?=    WMASC LDX   2  FX2  
   8 86QW          SBN   7  XEND 
  14 87=G          BPZ   7  (GEOERR)            [ESURE OK
   8 87Q6          ADN   7  XEND 
   9 889Q    WM1   LDXC  3  AWORK4(2)
  17 88PB          BCS      (6)                 [+0 IF NO NEXT PART  
  17 8992          BNZ   3  WM2                 [J IF 2ND TIME THRU  
  10 89NL          MHUNTW   3,GMON,BOUT  
  16 8=8=          ADN   3  A1                  [PTR TO 1ST PART 
   7 8=MW          LDN   0  1
  15 8?7G          STO   0  AWORK3(2)           [INITIALIZE  
   8 8?M6    WM2   LDCT  0  #100 
   8 8#6Q          ANDX  0  1(3) 
   8 8#LB          BNZ   0  WM3  
  18 8*62          NGN   0  4095                [SET -VE IF NOT CONTINUED
   8 8*KL          BRN      WM4  
   8 8B5=    WM3   LDX   0  0(3) 
  15 8BJW ...      ANDN  0  #3777               [REC HEADER  
   7 8C4G          ADN   0  3
   7 8CJ6          SRL   0  2
  15 8D3Q          ADN   0  2(3)                [->NEXT PART 
   9 8DHB    WM4   STO   0  AWORK4(2)
   9 8F32          LDEX  0  AWORK3(2)
  17 8FGL          ADS   0  AWORK3(2)           [DOUBLE 'PART-INDIC' 
   7 8G2=          SMO      7
  16 8GFW          BRN      MASCTAB-1           [TO APPR ROUTINE 
   8 8G_G    #     SYSTEM JOURNAL
   8 8HF6    WSJ   SMO      FX1  
   8 8HYQ          LDX   0  WJOUR
   8 8JDB    WM5   ANDX  0  1(3) 
  16 8JY2          BZE   0  WM1                 [J IF NOTJOURN   
   7 8KCL          EXIT  6  1
   8 8KX=    #     SCAN BUFFER   
   8 8LBW    WSCAN SMO      FX1  
   9 8LWG          LDX   0  SCAMSK   
   8 8MB6          ANDX  0  1(3) 
  16 8MTQ          BNZ   0  WM1                 [J IF 'NOSCAN'   
   7 8N*B          EXIT  6  1
   8 8NT2    #     MONITOR FILE  
   9 8P#L    WMONF LDX   0  AMFMSK(2)
  16 8PS=          BZE   0  WM1                 [J IF TRACE NONE 
  15 8Q?W          LDCT  0  #20                 [FORCEPRINT  
   9 8QRG          ORX   0  AMFMSK(2)
   8 8R?6          BRN      WM5  
   7 8RQQ    #     MOP OUTPUT
  15 8S=B    WMOP  LDCT  0  2                   [FORCETYPE   
   9 8SQ2          ORX   0  AMXMSK(2)
   8 8T9L          BRN      WM5  
   8 8TP=    #     CONSOLE  O/P  
  14 8W8W    WCONS LDN   0  #4000               [CENTRAL 
  15 8WNG          ORX   0  IOPTRACE            [AND OPTRACE 
   8 8X86          BRN      WM5  
   8 8XMQ    #     CLUSTER  O/P  
   8 8Y7B    WCLS  LDN   0  #4000
  16 8YM2          SLL   0  1                   [REMOTE BIT 'OP' 
  13 8_6L          ORX   0  IOPTRACE            [?   
   8 8_L=          BRN      WM5  
  18 925W    #     O/P TO ALL - ASSUME THE MESSAGE IS SET EQUAL FOR ALL  
   8 92KG    WALL  SMO      FX1  
  14 9356          LDX   0  WALMS               [CL&OP   
   9 93JQ          ORX   0  IOPTRACE 
   8 944B          BRN      WM5  
   4 94J2    #   
  11 953L    #     REPLY : TAKE CATEGORIES:  
  11 95H=    #     LG,PM,FL,CT,CE,OJ,LS,DP   
  13 962W    RMASK CATMASK  LG,PM,FL,CT,CE,OJ,LS,DP  
   8 96GG    WREPL SMO      FX1  
   8 9726          LDX   0  RMASK
   8 97FQ          BRN      WM5  
   7 97HF ...#SKI  JWPHASE4  
   4 97K8 ...(   
   9 97LX ...#     HLSTEMPQ OUTPUT   
   8 97NL ...WHLS  LDN   0  #4000
   8 97Q* ...      ANDX  0  0(3) 
  15 97S4 ...      BZE   0  WM1                 [J IF NOT HLS
   7 97TR ...      EXIT  6  1
   4 97XG ...)   
  14 97_B    #     FINDS THE NO OF CHARS IN MESSAGE PARTS
  16 98F2    #     TO GIVEN ROUTE(IN X7  [CF WMASC]) RESULT X4   
  11 98YL    #     AWORK3 UPDATED FOR 'WRITE'
  13 99D=    #     HMON1 IS THE UNION OF CATE_ORIES  
   7 99XW    WLEN  LDN   4  0
   8 9=CG          SMO      FX2  
   8 9=X6          STO   4  HMON1
   8 9?BQ    WL1   CALL  6  WMASC
  17 9?WB          BRN      WL2                 [OUT IF NO MORE PARTS
   8 9#B2          LDX   0  0(3) 
   7 9#FX ...#SKI  JWPHASE4  
   8 9#KS ...      ANDN  0  #3777
   8 9#PP ...#SKI  JWPHASE4<1$1  
   8 9#TL          ANDN  0  4095 
   7 9**=          ADX   4  0
   9 9*SW          LDCH  0  AWORK3(2)
  17 9B#G          ORX   0  AWORK3(2)           [ADD THIS PART-INDIC 
   9 9BS6          DCH   0  AWORK3(2)
   8 9C?Q          LDX   0  1(3) 
  16 9CRB          ORS   0  HMON1(2)            [ADD CATEGORIES  
   8 9D?2          BRN      WL1  
   9 9DQL    WL2   STOZ     AWORK4(2)
   7 9F==          EXIT  5  0
  13 9FPW    ZMONFILE       [OUTPUT TO MONITOR FILE  
   7 9G9G          LDN   7  3
   8 9GP6          CALL  5  WLEN 
   8 9GQT ...      FSHCODE  AORB 
   4 9GSJ ...(   
  18 9G_P ...      LDX   5  SLINK2(1)           [LINK TO LABEL 'SENTRY2' 
  21 9H32 ...      ALIEN    XHOMETRAN,2         [J IF ON AWAY MACHINE - TRANSFER HOME
  21 9H4? ...      TRANCHK FSHMESSAGE,EQUAL,XHOMENEST,SENTRY2 [NEST IF ALREADY TRAN'D
   5 9H5J ...SENTRY2 
   4 9H6W ...)   
  17 9H8Q          LDN   0  2000                [MAX CHARS PER RECORD
   7 9HNB          TXL   4  0
   8 9J82          BCS      MON9 
  17 9JML          LDX   4  0                   [ENFORCE MAX IF NEC  
  15 9K7=    MON9  NGX   5  4                   [SAVE IN X5  
   7 9KLW          LDX   2  4
  14 9L6G          ADN   2  11                  [+2 WORDS
  15 9LL6          SRL   2  2                   [NO OF WORDS 
   7 9M5Q          LDX   6  2
  11 9MKB          STEP     -1,0(2),FORCED   
  10 9N52          TESTRPN2 FILEFULL,MON6
   4 9NJL    #   
  15 9P4=    #     FILEFULL SO WE SET NOTRACE AT ALL LEVELS  
  11 9PHW    #     AND SET 'FULLTRACE' MARKER
   4 9Q3G    #   
   8 9Q5P ...      FSHCODE  AORB 
   4 9Q7Y ...(   
  20 9Q93 ...      TRANCHKN FSHMESSAGE,EQUAL,MONFULL,MONFULL [J IF NOT TRANSBEG'D
  19 9Q=7 ...      LDN   2  1                   [OFFSET TO LABEL 'MONFULL'   
  21 9Q#B ...      CALL  4  TRANSFIN            [RETURN TO ORIGINAL MACHINE OR UNNEST
   5 9QBK ...MONFULL 
   4 9QDS ...)   
   8 9QH6          LDCT  0  #40  
  15 9R2Q          ORS   0  CLONG1(2)           [FULLTRACE   
  16 9RGB          STOZ     AMFMSK(2)           [NONE THIS LEVEL 
  11 9S22    MON8  HUNT2J   2,CPB,CALAS,,MON7
  17 9SFL          STOZ     CMFMSK(2)           [AND AT HIGHER LEVELS
   8 9S_=          BRN      MON8 
   4 9TDW    MON6
  15 9TYG          STO   6  0(3)                [RECORD HDDR 
  15 9WD6          LDN   4  2(3)                [->NEXT WORD 
  15 9WXQ          LDN   1  1(3)                [->CATWORD   
   8 9XCB          STOZ     0(1) 
   8 9XX2    MON3  CALL  6  WMASC
   8 9YBL          BRN      MON5 
   8 9YW=          LDX   2  0(3) 
   7 9_27 ...#SKI  JWPHASE4  
   8 9_64 ...      ANDN  2  #3777
   8 9_9_ ...#SKI  JWPHASE4<1$1  
  14 9_*W          ANDN  2  4095                [CHAR CNT
   7 9_TG          ADX   5  2
   8 =2*6          BNG   5  MON10
  16 =2SQ          SBX   2  5                   [TRUNCATE IF NEC 
   5 =3#B    MON10   
  14 =3S2          LDX   0  1(3)                [CATBITS 
  14 =4?L          ORS   0  0(1)                [UNION   
   7 =4R=          ADN   3  2
   8 =5=W    MON2  SBNC  2  512  
  16 =5QG          BCS      MON1                [J IF < 128 CHARS
   7 =6=6          MVCH  3  0
   8 =6PQ          BRN      MON2 
   8 =79B    MON1  ANDN  2  #777 
   8 =7P2          BZE   2  MON11
  14 =88L          MVCH  3  0(2)                [LAST IN 
  16 =8N=    MON11 BNG   5  MON3                [J IF MORE ROOM  
   7 =97W    MON5  LDX   5  4
   7 =9MG          SLC   5  2
   7 ==76          ANDN  5  3
  17 ==LQ          BZE   5  MON4                [J IF NO SPACEFILL   
   8 =?6B          LDN   3  ACES 
   7 =?L2          NGX   5  5
  15 =#5L          ADN   5  4                   [CHARS TO GO 
   7 =#K=          SMO      5
  15 =*4W          MVCH  3  0                   [SPACEFILL   
   8 =*8R ...      FSHCODE  AORB 
  21 =*#N ...MON4  TESTREPN FNEARLY,XRETURN     [J IF NOT MONITORING FILE NEARLY FULL
   7 =*DK ...      FSHSKIP   
  11 =*JG    MON4  TESTREPN FNEARLY,XECUTE   
  18 =B46    MON7  DOWN     PROCONTX,15         [ABANDON IF NEARLY FULL  
   8 =B64 ...      FSHCODE  AORB 
   4 =B82 ...(   
   5 =B9Y ...XRETURN 
  20 =B?W ...      TRANCHKN FSHMESSAGE,EQUAL,XECUTE,XECUTE [J IF NOT TRANSBEG'D  
  18 =B*S ...      LDN   2  0                   [OFFSET TO LABEL 'XECUTE'
  21 =BCQ ...      CALL  4  TRANSFIN            [RETURN TO OTHER MACHINE OR UNNEST   
   4 =BFN ...)   
   9 =BHQ          BRN      XECUTE   
   8 =BK_ ...      FSHCODE  AORB 
   4 =BN8 ...(   
   5 =BQC ...TRANSFIN
  21 =BSL ...      TRANSFIN 2                   [RETURN TO OTHER M/C OR DROP THROUGH 
  21 =BWT ...      EXIT  4  0                   [RETURN TO AFTER CALL - 1 LEVEL LESS 
   4 =B_4 ...)   
   8 =C3B    #     MOPOUT TABLE  
   8 =CH2    MOPOUT         +0   
  19 =D2L          BRN      MREAD               [TAPE READER -TO OPS CONSOLE 
   8 =DG=                   +3   
  17 =D_W          BRN      MREAD               [CARD READER - DITTO 
   8 =FFG                   +7   
  14 =F_6          BRN      MFAIL               [MOP FAIL
   8 =GDQ                   +10  
  15 =GYB          BRN      TOMOP               [MOP CONSOLE 
   8 =HD2                   +11  
  15 =HXL          BRN      TOMOP               [MOP CONSOLE 
   8 =KBG                   +24  
  13 =KW6          BRN      MIPB                [IPB 
   8 =L*Q                   +60  
  16 =LTB          BRN      MCOMI               [COMMAND ISSUER  
   8 =M*2                   +63  
  17 =MSL          BRN      MREAD               [LOGGEDIN OPSCONSOLE 
   8 =N#=                   +5   
  15 =NRW          BRN      MAGTP               [MAGTAPE !   
   9 =P?G    #DEF  MOPNO=0?-MOPOUT/2 
   8 =PR6    #DEF  MAGTP=MREAD   
   8 =PTY ...      FSHCODE AORB  
   4 =PYQ ...(   
  12 =Q3J ...#      TABLE USED BY ZMOP IN SHFS   
  15 =Q6B ...MOPTOOP        +0                  [TAPE READER 
  15 =Q98 ...               +3                  [CARD READER 
  15 =Q#2 ...               +63                 [OPS CONSOLE 
  15 =QBS ...               +5                  [MAG TAPE!   
   9 =QFL ...#DEF  MOPOPNO=0?-MOPTOOP
   4 =QJD ...)   
   4 =QM= ...#   
   4 =QQB    #   
  17 =R=2    NCONS          +3                  [NO OF CONSOLE ROUTES
  14 =RPL    TESTAB         [CATEGORY BITS FOR  ROUTE:   
  13 =S9=          CATMASK  CENTRAL,CLUSTER     [5   
  13 =SNW          CATMASK  CENTRAL             [6   
  13 =T8G          CATMASK  CLUSTER             [7   
   9 =TN6    #     FOR USE OF 'WRITE'
  17 =W7Q    #     TEST IF JOBNAME TO BE PREFI XED (USE AFTER WLEN)  
   7 =WMB    TESTJ LDX   1  7
   7 =X72          SBN   1  5
   8 =XLL          SMO      FX1  
   8 =Y6=          TXL   1  NCONS
  18 =YKW          BCC      TS1                 [J IF NOT A CONSOLE ROUTE
   8 =_5G          ADX   1  FX1  
   9 =_K6          LDX   0  TESTAB(1)
   9 ?24Q          ANDX  0  HMON1(2) 
  16 ?2JB          BNZ   0  TS1                 [OR 'E[PECTED'   
   9 ?342          LDX   0  JOBNO(2) 
  15 ?3HL          BZE   0  TS1                 [J IF NO-USER
   8 ?43=    #SKI  ANOUSER<1$1   
   4 ?4GW    (   
   9 ?52G          LDCH  0  ATYPE(2) 
   9 ?5G6          SBN   0  CPAT/64  
  17 ?5_Q          BNZ   0  TS2                 [OUT IF NOT A CPAT   
  15 ?6FB          BRUSEN   TS1,2               [OR NO-USER  
   4 ?6_2    TS2 
   4 ?7DL    )   
   4 ?7Y=    #   
  11 ?8CW    #     ELSE OPTRACE ROUTING ALONE
   8 ?8XG          LDCT  0  #10  
  16 ?9C6          ORS   0  AWORK3(2)           ['OPTRACE' PART  
  15 ?9WQ          ADN   4  7                   [MAX INCREASE
   7 ?=BB          EXIT  6  0
   7 ?=W2    TS1   EXIT  6  1
  13 ??*L    #     S/R TO SET UP AN AOUT IF NECESSARY
  11 ??T=    #     USES AWORK1 AS MARKER WORD
  14 ?##W    #     X1->AOUT ON EXIT, X7 USED AS FOR WMASC
   4 ?#SG    #   
  14 ?*#6    #     AWORK 1 HAS: B0 SET IF MULTIPART BOUT 
  16 ?*RQ    #                LAST 9 BITS SET IF AN AOUT EXISTS  
   5 ?B?B    WRITE   
  19 ?BR2          CALL  5  WLEN                [SETUP X4,AWORK3,ACOMMUNE1   
  15 ?C=L          LDX   5  1                   [KEEP LINK   
   8 ?CQ=          ADN   4  11   
   7 ?D9W          SRL   4  2
  19 ?DPG          CALL  6  TESTJ               [CHECK IF JOBNAME PREFIX...  
  15 ?F96          LDN   6  0                   [...REQUIRED 
  18 ?FNQ          LDCH  0  AWORK3(2)           [PARTS REQUIRED THIS TIME
  17 ?G8B          LDEX  1  AWORK1(2)           [PARTS USED LAST TIME
  18 ?GN2          DEX   0  AWORK1(2)           [REMEMBER FOR NEXT TIME  
  15 ?H7L          BZE   1  NOOUT               [J IF NO AOUT
   7 ?HM=          TXU   0  1
  16 ?J6W          BCS      NEWOUT              [NEED A NEW AOUT 
  18 ?JLG          MHUNTW   1,GMON,AOUT         [...ELSE THIS ONE WILL DO
   7 ?K66          EXIT  5  0
   5 ?KKQ    NEWOUT  
  17 ?L5B          MFREE    GMON,AOUT           [FREE THE OLD AOUT   
   5 ?LK2    NOOUT   
   8 ?M4L          SBX   5  FX1  
  11 ?MJ=          SETUPCORE 4,1,GMON,AOUT   
   8 ?N3W          ADX   5  FX1  
   9 ?NHG          LDX   0  HMON1(2) 
  16 ?P36          STO   0  ARECB+1(1)          [CATEGORY UNION  
  14 ?PGQ          LDN   4  ARECB+2(1)          [CHAR PTR
  18 ?Q2B          BZE   6  PUTIN               [J IF TO PUT JOBNAME IN  
   8 ?QG2    WR1   CALL  6  WMASC
  18 ?Q_L          BRN      WTIDY               [TIDY UP IF NO MORE PARTS
   8 ?RF=          LDX   0  0(3) 
   7 ?RK7 ...#SKI  JWPHASE4  
   8 ?RP4 ...      ANDN  0  #3777
   8 ?RS_ ...#SKI  JWPHASE4<1$1  
  15 ?RYW          ANDN  0  4095                [CHAR-COUNT  
  17 ?SDG          ADN   3  2                   [SKIP RECORD WORDS   
   8 ?SY6    WR6   BZE   0  WR1  
   8 ?TCQ          SBNC  0  512  
   8 ?TXB          BCS      WR7  
  19 ?WC2          MVCH  3  512                 [MOVE IN BLOCKS OF 512 CHARS 
   8 ?WWL          BRN      WR6  
  15 ?XB=    WR7   SMO      0                   [PUT LAST IN 
   7 ?XTW          MVCH  3  0
   8 ?Y*G          BRN      WR1  
   8 ?YT6    WTIDY LDN   3  ACES 
   7 ?_#Q          LDX   0  4
   7 ?_SB          SLC   0  2
   7 #2#2          NGX   0  0
  15 #2RL          ANDN  0  3                   [CHARS TO GO 
   8 #3?=          BZE   0  WR2  
   7 #3QW          SMO      0
  15 #4=G          MVCH  3  0                   [SPACEFILL   
   9 #4Q6    WR2   SBN   4  ARECB(1) 
  15 #59Q          STO   4  ARECB(1)            [RECORD HDDR 
   7 #5PB          EXIT  5  0
  14 #692    #     PREFIX THE JOBNAME :Q 'FRED,:JACK: '  
   8 #6NL    PRESET      4H,::   
  10 #78=    PUTIN FCJOB 1,2,PCA,CPA,OLPA
   8 #7MW    #SKI  ANOUSER<1$1   
   4 #87G    (   
  10 #8M6          LDX   0  ALOGLEN(1)   
   9 #96Q          SBN   0  ASTJOB   
  16 #9LB          BZE   0  PUT3                [OUT IF NO-USER  
   4 #=62    )   
   8 #=KL          SMO      FX1  
   9 #?5=          LDN   6  PRESET   
   9 #?JW          LDN   3  JNAME(1) 
   8 ##4G    PUT2  LDN   2  3(3) 
   7 ##J6    PUT1  SLC   2  2
   7 #*3Q          SBN   2  1
   7 #*HB          SRC   2  2
   8 #B32          LDCH  0  0(2) 
   8 #BGL          SBN   0  #20  
  15 #C2=          BZE   0  PUT1                [BACKSPACE   
  15 #CFW          SBX   2  3                   [X3--CHAR 0  
   7 #C_G          SLC   2  2
  14 #DF6          MVCH  3  1(2)                [PUT IN  
   7 #DYQ          LDX   3  6
   7 #FDB          MVCH  3  2
  16 #FY2          BNG   6  PUT3                [OUT ON 2ND PASS 
   7 #GCL          LDX   6  3
   9 #GX=          LDN   3  JUSER(1) 
   8 #HBW          BRN      PUT2 
  15 #HWG    PUT3  MHUNTW   1,GMON,AOUT         [RESTORE X1  
   8 #JB6          BRN      WR1  
   4 #JTQ    #   
   9 #K*B    ZSJPAK GEOERR  1,SJDONE!
   4 #KT2    #   
   7 #KX9 ...#SKI  JWPHASE4  
   4 #K_D ...(   
   4 #L3M ...ZHLS
  17 #L5W ...      LDN   7  9                   [ROUTE 9 FOR WMASC   
   8 #L85 ...      BRN      SJ0  
   4 #L=# ...)   
   5 #L#L    ZSJNPK  
  17 #LS=          LDN   7  1                   [ROUTE 1 FOR WMASC   
   7 #L_D ...#SKI  JWPHASE4  
   4 #M6L ...SJ0 
  16 #M?W          CALL  5  WLEN                [CHARS IN MESSAGE
  16 #MRG          LDX   5  4                   [SAVE CHAR COUNT 
  13 #N?6          ADN   4  JMESSAGE-JWAITER+1*4+3   
  17 #NQQ          SRL   4  2                   [NO OF WORDS REQID   
  11 #P=B          SETUPCORE 4,1,GMON,JRNAL  
   9 #PQ2          STOZ     JCATS(1) 
  10 #Q9L          STOZ     JWAITER(1)   
  10 #QP=          MHUNT    3,GMON,BOUT  
   8 #R8W          LDX   0  A1(3)
   9 #RNG          ANDX  0  HALFTOP  
  16 #S86          STO   0  JPACKORG(1)         [MESSNO/NO PDCS  
  10 #SMQ          LDX   0  ACOMMUNE2(2) 
   9 #T7B          STOC  0  JJOBNO(1)
  16 #TM2          BCC      SJ1                 [J IF AUTONOMOUS 
  15 #TWS ...      BSON     EMSBIT,SJ1          [J IF IN EMS 
   9 #W6L          LDX   0  ACTNUM(2)
  10 #WL=          STO   0  JWAITER(1)   
  10 #X5W    SJ1   LDN   4  JMESSAGE(1)  
   8 #XKG    SJ3   CALL  6  WMASC
  17 #Y56          BRN      SJ2                 [OUT IF NO MORE PARTS
   8 #YJQ          LDX   0  1(3) 
  15 #_4B          ORS   0  JCATS(1)            [ADD CATGORY 
  14 #_J2          LDEX  0  0(3)                [CHARS   
   8 *23L          BZE   0  SJ3  
   7 *2H=          ADN   3  2
   7 *32W          SMO      0
  15 *3GG          MVCH  3  0                   [CONCATENATE 
   8 *426          BRN      SJ3  
   7 *4FQ    SJ2   LDX   4  1
   7 *4GF ...#SKI  JWPHASE4  
   4 *4H8 ...(   
  17 *4HX ...      GMONTIDY 4,5,UNPACKED        [TIDY UP GMON/JRNAL  
   7 *4JL ...      SBN   7  1
  16 *4K* ...      BZE   7  SJ6                 [J IF JOURNAL O/P
   7 *4L4 ...      LDX   1  4
  10 *4LR ...      LDX   3  ALOGLEN(1)   
   7 *4MG ...      SBN   3  4
   8 *4N9 ...      LDN   2  A1(1)
   9 *4NY ...      LDN   1  A1+3(1)  
  16 *4PM ...      MOVE  1  0(3)                [WORDS OF......  
  15 *4QB ...      ALTLEN   4,3                 [RED TAPE.   
  10 *4R5 ...      NAME     4,ADATA,ASUP 
  15 *4RS ...      HLSINFORM XECUTE,MESSAGE     [O/P TO TEMPQ
  10 *4SH ...      VFREEW   ADATA,ASUP   
   9 *4T= ...      BRN      XECUTE   
   4 *4T_ ...SJ6 
   7 *4WN ...      SJCHAINY 4
   4 *4XC ...)   
   8 *4Y6 ...#SKI  JWPHASE4<1$1  
  13 *4_B          SJCHAIN  4,5                 [O/P 
   9 *5F2          BRN      XECUTE   
   5 *5YL    ZSCANB  
   7 *6D=          LDN   7  2
   8 *6XW    WSC1  CALL  6  WMASC
   9 *7CG          BRN      XECUTE   
   9 *7X6          LDXC  0  AWORK1(2)
  16 *8BQ          BCS      WSC3                [J IF MANY PART  
   7 *8WB          NGN   4  1
   8 *9B2          BRN      WSC2 
   4 *9TL    WSC3
  10 *=*=          MHUNTW   1,GMON,BOUT  
   7 *=SW          LDX   5  1
   7 *?#G          PHOTO    4
   8 *?S6    WSC2  LDEX  2  0(3) 
   7 *#?Q          ADN   2  3
   7 *#RB          SRL   2  2
  12 **?2          GSCAN    1(3),MONF OUT,1(2)   
  16 **QL          BNG   4  XECUTE              [OUT IF ONEPART  
   9 *B==          TESTMOVE 4,WSC1   
  10 *BPW          MHUNTW   1,GMON,BOUT  
  16 *C9G          SBX   1  5                   [DISTANCE MOVED  
   9 *CP6          ADS   1  AWORK4(2)
   8 *D8Q          BRN      WSC1 
   4 *DNB    #   
  14 *F82    #     MOP O/P-INCLUDES READER-JOB MESSAGES  
   7 *FML    ZMOP  LDN   7  4
   8 *G7=          CALL  5  WLEN 
  16 *GLW          STO   4  ACOMMUNE1(2)        [SAVE CHARLENGTH 
   8 *GMR ...      FSHCODE  AORB 
   4 *GNN ...(   
  17 *GNX ...      LDCH  0  JSOURCE1(2)          [IF MOP OUTPUT TO BE
  18 *GP6 ...      LDN   3  MOPTOOP(1)           [DIRECTED TO OPS CONSOLE
  17 *GP* ...      LDN   4  MOPOPNO              [DON'T TRANSFER HOME
   5 *GPJ ...XLOOP   
   8 *GPR ...      TXU   0  0(3) 
   9 *GQ2 ...      BCC      SENTRY3  
   7 *GQ9 ...      ADN   3  1
   8 *GQD ...      BCT   4  XLOOP
  21 *GQM ...      SAWCEN   CENTRAL,SENTRY3      [DON'T TRANSFER HOME IF SOURCE NOT  
  20 *GQW ...                                    [CENTRAL - OUTPUT DIRECTED TO OP
  18 *GRC ...      LDX   5  SLINK3(1)           [LINK TO LABEL 'SENTRY3' 
  21 *GRQ ...      ALIEN    XHOMETRAN,2         [J IF ON AWAY MACHINE,TRANSFER HOME  
  21 *GS5 ...      TRANCHK FSHMESSAGE,EQUAL,XHOMENEST,SENTRY3 [NEST IF ALREADY TRAN'D
  18 *GSD ...      BRN      SENTRY3             [DON'T NEED TO TRANSBEG  
   6 *GSR ...XHOMETRAN   
  16 *GT9 ...      LDN   1  FSHMESSAGE          [ID = FSHMESSAGE 
   6 *GTM ...XTRANSBEG   
  20 *GW6 ...      LDN   2  GMON+BOUT           [TERMINATING BLOCK = GMON/BOUT   
   8 *GX3 ...      SLL   2  12   
  13 *GXY ...      TRANSBEX 1,5,2,ACOMMUNE3,XIPBDOWN 
  15 *GYT ...      BRN      XECUTE              [OK RETURN   
  19 *G_Q ...      BRN      MONFULL             [MONITORING FILE FULL RETURN 
   5 *H2M ...XIPBDOWN
  17 *H3J ...      TRANSDIE                     [KILL OFF ACTIVITY   
   6 *H3X ...XHOMENEST   
  20 *H4= ...      LDN   1  FSHMESSAGE+FSHZNEST [ID = FSHMESSAGE,NESTED TRANSBEG 
   9 *H4K ...      BRN      XTRANSBEG
   5 *H4Y ...SENTRY3 
   4 *H5B ...)   
  15 *H6G          CALL  1  WRITE               [GET X1->AOUT
   9 *HL6    #     HERE  X1 -> AOUT  
  15 *J5Q    WMOP3 LDCH  0  JSOURCE1(2)         [PERI-TYPE   
   8 *JKB          SMO      FX1  
  14 *K52          LDN   3  MOPOUT              [TABLE   
  15 *KJL          LDN   4  MOPNO               [NO OF TYPES 
   8 *L4=    MLOP  TXU   0  0(3) 
   8 *LHW          BCC      MFND 
   7 *M3G          ADN   3  2
   8 *MH6          BCT   4  MLOP 
  10 *N2Q          GEOERR   1,RONGTYPE   
  15 *NGB    MFND  LDX   7  ACOMMUNE1(2)        [CHAR LENGTH 
  14 *P22          EXIT  3  1                   [BRANCH  
   6 *PFL    #     MOPOUT
  18 *P_=    TOMOP SAWCEN   CENTRAL,MREAD       [J IF SAWCE NOT CENTRAL  
   6 *QDW ...#SKI  JSKI31
   4 *QYG    (   
  17 *RD6 ...      JBC      MOP1,2,EXQUIET         [J IF NOT QUIET   
  14 *RXQ ...      LDEX  0  ARECB+1(1)          [CATEGORY
   8 *SCB ...      ERN   0  #20  
  16 *SX2 ...      BNZ   0  MOP1                [J IF NOT COMMERR
   7 *TBL ...      LDN   0  1
  14 *TW= ...      STO   0  ACOMMUNE1(2)        [MOP IND.
   9 *W*W ...      DOWN     PNTLAST,4
   8 *WTG ...      FSHCODE  AORB 
   9 *X*6 ...      BRN      XRETURN  
   7 *XSQ ...      FSHSKIP   
   9 *_R=          BRN      XECUTE   
   4 B2=W    MOP1
   4 B2QG    )   
  17 B3=6          MOPOUT   7,NOIPB             [OUT TO MOP-NO IPB   
   8 B3B3 ...      FSHCODE  AORB 
   9 B3FY ...      BRN      XRETURN  
   7 B3KT ...      FSHSKIP   
   9 B3PQ          BRN      XECUTE   
   8 B49B    #     MOP FAILURE   
  10 B4P2    MFAIL DOWN     MULTIPLX,4   
   8 B4SX ...      FSHCODE  AORB 
   9 B4YS ...      BRN      XRETURN  
   7 B54P ...      FSHSKIP   
   9 B58L          BRN      XECUTE   
   4 B776    #   
   6 B789 ...#SKI  JSKI31
   4 B79# ...(   
  16 B7=C ...MIPB  JBC      XQNOT,2,EXQUIET     [J IF NOT QUIET  
  10 B7?G ...      LDEX  0  ARECB+1(1)   
   8 B7#K ...      ERN   0  #20  
  18 B7*N ...      BNZ   0  XQNOT               [J IF NOT COMMAND ERROR  
   7 B7BR ...      LDN   0  2
  15 B7CW ...      STO   0  ACOMMUNE1(2)           [IPB IND. 
  15 B7D_ ...      DOWN     PNTLAST,4           [O/P 'ERROR  
   8 B7F9 ...      FSHCODE  AORB 
   9 B7FF ...      BRN      XRETURN  
   7 B7FP ...      FSHSKIP   
   9 B7G4 ...      BRN      XECUTE   
   7 B7H7 ...XQNOT IPBOUT   7
   4 B7J= ...)   
   7 B7K* ...#SKI  JSKI31<1$1
   7 B7LQ    MIPB  IPBOUT   7
   8 B7QM ...      FSHCODE  AORB 
   9 B7WJ ...      BRN      XRETURN  
   7 B82F ...      FSHSKIP   
   9 B86B          BRN      XECUTE   
   4 B8L2    #   
   6 B8M8 ...#SKI  JSKI31
   4 B8NB ...(   
  17 B8PJ ...MCOMI JBC      MCIEX,2,EXQUIET        [J IF NOT QUIET   
  10 B8QQ ...      LDEX  0  ARECB+1(1)   
   8 B8RY ...      ERN   0  #20  
  16 B8T6 ...      BNZ   0  MCIEX               [J IF NOT COMERR 
  10 B8W# ...      STOZ     ACOMMUNE1(2) 
  16 B8XG ...      DOWN     PNTLAST,4             [O/P 'ERROR'   
   8 B8XR ...      FSHCODE  AORB 
   9 B8Y4 ...      BRN      XRETURN  
   7 B8Y* ...      FSHSKIP   
   9 B8YN ...      BRN      XECUTE   
   7 B8_W ...MCIEX CIOUT    7
   4 B934 ...)   
   7 B94= ...#SKI  JSKI31<1$1
   7 B95L    MCOMI CIOUT    7
   8 B99H ...      FSHCODE  AORB 
   9 B9*D ...      BRN      XRETURN  
   7 B9F* ...      FSHSKIP   
   9 B9K=          BRN      XECUTE   
  10 B=4W    #     REPORT TO ONE SOURCE  
  10 B=JG    MREAD LDX   5  GDESTINY(2)  
   7 B?46          LDEX  3  5
  14 B?HQ          SRC   5  12(3)               [RESET   
   7 B#3B          LDX   6  5
  12 B#H2          ANDN  6  OP+REMOTE+MBOTH+MALL 
  19 B*2L          BZE   6  SOLE                [TO CLUSTER IF NOT OPROUTE   
   8 B*G=          ANDN  6  OP   
  17 B*_W          BZE   6  XECUTE              [OUT IF NOT CENTRAL  
  18 BBFG          ERN   5  OP+MBOTH            [ELSE SET 'BOTH' INSTEAD 
   8 BB_6          SLC   5  12(3)
  10 BCDQ          STO   5  GDESTINY(2)  
   9 BCYB          BRN      XECUTE   
   4 BDD2    #   
   7 BDXL    #     O/P TO ALL
   7 BFC=    ZMALL LDN   7  5
   8 BFWW          CALL  1  WRITE
   7 BGBG          LDN   6  3
   8 BGW6          BRN      SOLE 
   4 BH*Q    #   
   8 BHTB    #     O/P TO 'OWN'  
   5 BJ*2    ZREMOTE 
   7 BJSL          LDN   7  7
   8 BK#=          CALL  1  WRITE
   7 BKRW          LDN   6  0
   8 BL?G          BRN      SOLE 
   4 BLR6    #   
   8 BM=Q    #     O/P TO CENTRAL
   7 BMQB    ZOP   LDN   7  6
   8 BN=2          CALL  1  WRITE
   7 BNPL          LDN   6  1
   8 BP9=          BRN      SOLE 
   4 BPNW    #   
  11 BQ8G    #     O/P TO BOTH OWN+CENTRAL   
   5 BQN6    ZMBOTH  
   7 BR7Q          LDN   7  5
   8 BRMB          CALL  1  WRITE
   7 BS72          LDN   6  2
   8 BS=X ...      FSHCODE  AORB 
   4 BS#T ...(   
  14 BSBS ...SOLE  CONSOLE  6                   [O/P !   
   4 BSFB ...)   
   7 BSGP ...      FSHSKIP   
   4 BSJM ...(   
  14 BSLL    SOLE  CONSOLE  1,6                 [O/P !   
   4 BT6= ...)   
   9 BW5G          BRN      XECUTE   
   4 BW=N ...#SKI
   4 BWCW ...(   
   8 BWK6    #     RETAIN BLOCK  
  16 BX4Q    #     AN AOUT HOLDING ALL RELEVANT PARTS IS RETAINED
  11 BXJB    #     THIS IS A TERMINAL ROUTINE
   5 BY42    ZRETAIN 
   9 BYHL          LDEX  0  AWORK1(2)
  15 B_3=          BZE   0  RET1                [J IF NO AOUT
   9 B_GW          MFREEW   GMON,AOUT
   4 C22G    RET1
   8 C2G6    #SKI  AORBOUT<1$1   
   4 C2_Q    (   
  10 C3FB          MHUNTW   1,GMON,BOUT  
  10 C3_2          NAME     1,GMON,AOUT  
   4 C4DL    )   
   5 C4Y=          UP
   4 C5CW    #   
  16 C5XG    #     A MESSAGE HAS BEEN ASSEMBLED (BROADCAST ETC)  
   5 C6C6    ZSEMBL  
  10 C6WQ          MHUNTW   1,GMON,BOUT  
  10 C7BB          NAME     1,GMON,AOUT  
   5 C7W2          UP
   4 C8*L    #   
  19 C8T=    #     REPLY: ALL THOSE PARTS WHICH ARE NOT'ONLINE'OR'COMMAND'   
  19 C9#W    #     YET COULD CONCEIVABLY BE FULLTRACED ARE LEFT AS A BREPLY  
   6 C9SG    #     BLOCK 
   5 C=#6    ZREPLY  
   7 C=RQ          LDN   7  8
   8 C??B          CALL  1  WRITE
   7 C?R2          LDX   5  1
  10 C#=L          VFREEW   ADATA,BREPLY 
   9 C#Q=          MFREEW   GMON,BOUT
  11 C*9W          NAME     5,ADATA,BREPLY   
   5 C*PG          UP
   4 C*PW ...)   
   8 C*Q= ...#     RETAIN BLOCK  
  16 C*QL ...#     AN AOUT HOLDING ALL RELEVANT PARTS IS RETAINED
  11 C*R2 ...#     THIS IS A TERMINAL ROUTINE
   5 C*RB ...ZRETAIN 
   9 C*RQ ...      LDEX  0  AWORK1(2)
  15 C*S6 ...      BZE   0  RET1                [J IF NO AOUT
   9 C*SG ...      MFREEW   GMON,AOUT
   4 C*SW ...RET1
   8 C*T= ...#SKI  AORBOUT<1$1   
   4 C*TL ...(   
  10 C*W2 ...      MHUNTW   3,GMON,BOUT  
   9 C*WB ...      BRN      NAMEAOUT 
   4 C*WQ ...)   
   7 C*X6 ...#SKI  AORBOUT   
   5 C*XG ...      UP
   4 C*XW ...#   
  16 C*Y= ...#     A MESSAGE HAS BEEN ASSEMBLED (BROADCAST ETC)  
   5 C*YL ...ZSEMBL  
  10 C*_2 ...      MHUNTW   3,GMON,BOUT  
   5 C*_B ...NAMEAOUT
   9 C*_Q ...      CALL  2  NAMEUP   
   8 CB26 ...#HAL  +GMON+AOUT,+0 
   4 CB2G ...#   
  19 CB2W ...#     REPLY: ALL THOSE PARTS WHICH ARE NOT'ONLINE'OR'COMMAND'   
  19 CB3= ...#     YET COULD CONCEIVABLY BE FULLTRACED ARE LEFT AS A BREPLY  
   6 CB3L ...#     BLOCK 
   5 CB42 ...ZREPLY  
   7 CB4B ...      LDN   7  8
   8 CB4Q ...      CALL  1  WRITE
   7 CB56 ...      LDX   3  1
  10 CB5G ...      VFREEW   ADATA,BREPLY 
   9 CB5W ...      MFREEW   GMON,BOUT
   9 CB6= ...      CALL  2  NAMEUP   
   9 CB6L ...#HAL  +ADATA+BREPLY,+0  
   4 CB72 ...#   
   5 CB7B ...NAMEUP  
   8 CB7Q ...      LDX   0  0(2) 
   7 CB86 ...      NAMEX    3
   5 CB8G ...      UP
   4 CB96    #   
   4 CC8B    #END
   8 ____ ...77552205001000000000
