   8 22FL    #OPT  K0LISTINIT=0  
   9 22_= ...#OPT           ATPOLTR=0
  18 23YG    #LIS  K0LISTINIT>K0LISTFILE>K0ALLGEO>K0GREATGEO>K0COMMAND   
   7 24D6    #SEG  LISTINIT  
  10 24XQ                   8HLISTINIT   
   4 25CB    [   
  19 25X2    [THIS SEGMENT HAS THE FOLLOWING FUNCTIONS(LINE PRINTERS ONLY)   
   4 26BL    [   
  16 26W=    [1.IT OUTPUTS DEBATCHERS AND HEADINGS FOR LISTFILE. 
  16 27*W    [2.IT OUTPUTS DEBATCHERS AND HEADINGS FOR ONLINE.   
  16 27TG    [3.IT OUTPUTS TERMINATING DEBATCHERS FOR LISTFILE.  
  15 28*6    [4.IT OUTPUTS TERMINATING DEBATCHERS FOR ONLINE.
  18 28SQ    [5.IT ACTIVATES A PRINTER AFTER AN ENGAGE BUTTON INTERRUPT. 
  17 29#B    [6.IT OUTPUTS LINE NUMBER OR FILE EMPTY ERROR MESSAGES  
  14 29S2    [HERE IS A LIST OF ENTRY POINTS AND BRANCHES
   4 2=?L    [   
  11 2=R=          SETENTRY LISTINIT,POINT,6 
  14 2?5M ...      FSHENTRY K20LISTINIT,POINT20,,POINT20 
  16 2?D4 ...      FSHENTRY K21LISTINIT,POINT21,POINT21,POINT21  
  15 2?K? ...      FSHENTRY K22LISTINIT,XBFAILED,,XBFAILED   
  11 2?MW ...      SEGENTRY K8LISTINIT,POINT8
  21 2?QG    [***********************************************************************
   6 2#=6    [DATA AREA  
  10 2#PQ    [HEADINGS USED BY LISTFILE  
   4 2*9B    [   
   8 2*F8 ...SHASH          4H###
   8 2*P2    PRINT          +1   
   5 2B8L    THEAD   
  11 2BN=                   12HPRODUCED ON   
  10 2C7W                   12H        AT
   5 2CMG    SHEAD   
   9 2D76                    4H#OUT  
  12 2DLQ                    SEGENTRY K40LISTINIT
  13 2F6B                    24HPUT BY LISTFILE IN ':
  12 2FL2    ONAT           16H' ON         AT   
  10 2G5L    SDOC           12HDOCUMENT  
  12 2GQC    RCON           16H#CONTINUATION OF  
  15 2GS6    RFOL           28H FOLLOWING RESTART ACTION OF  
   9 2GTT    RPAGE          8H 'ALL' 
   9 2GXJ                   8H 'PAGE'
   9 2H32    UNLFON         8H USING 
  12 2HB* ...SEMPT          16HSETUP FILE EMPTY  
  14 2HPN    REVF           24H  A COPY OF THIS FILE HA  
  14 2HWW                   24HS BEEN LOST BY REVERSION  
   4 2J46    [   
  15 2JHQ    [HEADINGS FOR ONLINE OBJECT PROGRAM PERIPHERAL  
   4 2K3B    [   
   5 2KH2    OHEAD   
  14 2L2L                   28H#OUTPUT ON-LINE BY JOB ': 
   4 2LG=    [   
   9 2L_W    XAST           4H****   
  18 2MFG    PASTZ          #72                 [TEST WHETHER DEBATCHING 
  19 2M_6                                       [CHARACTER OVERRUN ALPHABET  
  20 2NDQ    N31            #60000001           [CHAR POS3 +1 FOR TIME IN #LIST  
   9 2NYB    SETUPBIT       #10000000
   4 2PD2    [   
  13 2PXL    SCOLON         #12                 [:   
  13 2QC=    SHRIEK         #21                 [!   
   4 2QF8    [   
  17 2QM2    RESTARTBIT     #4000               [RESTART TAKING PLACE
  18 2QNY    RESTACTN       #300000             [RESTART ACTION SPECIFIED
   9 2QQW                   #200000  
   4 2QWW    [   
  12 2RBG    [TABLE OF JUMPS FOR PERI SUBROUTINE 
   4 2RW6    [   
   5 2S*Q    STABLE  
  19 2STB ...      BRN      YAB                 [INTERRUPT DURING HEADINGS   
  19 2T*2          BRN      SBRK2               [INTERRUPT DURING TERMINATORS
  12 2TSL          BRN      YABANL              [
  21 2W#=    [***********************************************************************
   7 2WRW    [SUBROUTINE AREA
   4 2X?G    [   
  21 2XR6    [THIS SUBROUTINE INITIALISES THE COUNT IN THE DEVICE LIST CONTROL AREA  
  13 2Y=Q    [ACCORDING TO THE WIDTH OF THE PRINTER. 
   5 2YQB    SETUPCA 
   6 2YR9 ...#SKI  ANIP  
   4 2YS4 ...(   
   9 2YSX ...      LDX   0  CPPTR(2) 
  17 2YTQ ...      BZE   0  (7)                 [EXIT AT ONCE IF NIP 
   4 2YWK ...)   
  14 2YXJ          LDN   4  0                   [SET MODE
   9 2_4Q          DCA   ,DSA,4,MODE 
  16 2_=2          LDCT  0  #600                [B0 AND B1 SET   
  17 2_PL          DCA   ,STO,0,CTSA            [IN ADDRESS FOR LP   
  17 329=          LADDP    ,4                  [GET POINTER TO APIA 
   7 32NW          SMO      4
   9 338G          LDEX  5  AREPERT  
  16 38JB          ADN   5  1                   [ADD ONE FOR PFCC
   7 3942    #SKI  ATPOLTR   
  10 39HL          TRACEVER 5,INITPWID   
  15 3=3=          DCA   ,STO,5,COUNT           [SET UP COUNT
   7 3=GW          EXIT  7  0
   4 3?2G    [   
  21 3?G6    [THIS ROUTINE PERFORMS THE PERI ON THE DEVICE AND CHECKS THE REPLY FOR  
  21 3?_Q    [DISENGAGED OR INOPERABLE STATES,TAKING APPROPRIATE ACTION IF NECESSARY 
   4 3#FB    [   
   6 3#GY ...#SKI  ANIP  
   4 3#JG ...(   
  16 3#L4 ...[ NEW ENTRY POINT FOR NIP TO OUTUT END OF LISTING   
  17 3#ML ...PERI4 STOZ     GZDUMP(2)           [MARK END OF LISTING 
   8 3#P8 ...PERI6 SBX   7  FX1  
  14 3#QQ ...PERI5 DOWN     LISTPREP,4          [DO PERI 
   8 3#S# ...      ADX   7  FX1  
   7 3#TW ...      EXIT  7  0
   4 3#XD ...)   
   4 3#_2    PERI
   8 3*DL          LDN   0  #41  
   5 3*Y=    PERI2   
   9 3BCW          STO   0  GZDATA(2)
   5 3BXG    PERI3   
   8 3CC6          SBX   7  FX1  
   6 3CDH ...#SKI  ANIP  
   4 3CFY ...(   
  13 3CH* ...[ IF NIP GO DOWN TO LISTPREP TO DO PERI 
   9 3CJQ ...      LDX   0  CPPTR(2) 
  14 3CL7 ...      BNZ   0  RPTPERI             [NOT NIP 
  18 3CMJ ...      LDEX  0  JSOURCE3(2)         [NO OF PRINT POSITIONS   
  14 3CN_ ...      ADN   0  1                   [FOR PFCC
  18 3CQB ...      STO   0  GZDUMP(2)           [CHAR COUNT OF TRANSFER  
  16 3CRR ...      BRN      PERI5               [GO TO LISTPREP  
   4 3CT8 ...)   
   5 3CWQ    RPTPERI 
   9 3DBB          LDX   0  FINISH   
   7 3DW2          SLL   0  2
  16 3F*L          BNG   0  SFINI               [J IF FINISH NOW 
   9 3FT=          ERX   0  FINISH   
   7 3G#W          SLL   0  1
  16 3GSG          BPZ   0  NFIN                [J IF NOT FINISH 
   5 3H#6    SFINI   
   9 3HRQ          TXU   5  PRINT(1) 
  17 3J?B          BCS      SBRKIN              [J IF NOT TERMINATORS
   4 3JR2    NFIN
  16 3K=L          LOCKC    FX2,SLOCKC          [LOCK FOR EXEC   
   9 3KQ=          LDN   4  GZDATA(2)
  20 3L9W          DCA      ,ADS,4,CTSA         [BUFFER ADDRESS INTO CONTROL AREA
   7 3LPG          GPERI ,2  
   6 3M96          COOR1 
  10 3MNQ          DCA      ,SBS,4,CTSA  
   7 3N8B          UNLOCK   2
   8 3NN2          LDCT  4  #50  
   9 3P7L          ANDX  4  CPRW1(2) 
  20 3PM=          BNZ   4  NOTENG              [J IF ERROR/EXCEPTION CONDITION  
   8 3Q6W          LDCT  4  #100 
   9 3QLG          ANDX  4  CPRW1(2) 
  16 3R66          BNZ   4  PLOW                [J IF PAPER LOW  
   8 3RKQ          ADX   7  FX1  
   7 3S5B          EXIT  7  0
   6 3S5Q ...#UNS FLLP4B 
   4 3S66 ...(   
   4 3S6G ...[   
  18 3S6W ...[ THIS SUBROUTINE SENDS SOFT FORMAT LOOP TO LP IF NECESSARY 
   4 3S7= ...[   
   5 3S7L ...SFLOOP  
   8 3S82 ...      SBX   6  FX1  
  11 3S8B ...      MHUNTW  3,ADATA,FORMLOOP  
   6 3S8Q ...#UNS FLLP4BC
   8 3S96 ...   TRACEVER 3,LI1   
  17 3S9G ...      IF  3,PZ           [ IF THERE IS A FORMLOOP BLOCK 
  16 3S9W ...      THEN               [ THEN ITS AN LP4B PRINTER!
  18 3S== ...      LDN   4  #30       [ SO SEND A FORMLOOP WITH MODE #30 
   6 3S=L ...#UNS FLLP4BC
   8 3S?2 ...       LDN   4  0   
   9 3S?B ...       DCA  ,DSA,4,MODE 
  10 3S?Q ...       LDX   4  FORMCHAR(3) 
   6 3S#6 ...#UNS FLLP4BC
   9 3S#G ...   TRACEVER 4,LICOUNT   
   9 3S#W ...       DCA  ,STO,4,COUNT
   6 3SC6 ...#UNS FLLP4BC
   9 3SCG ...   TRACEVER 2,LI2PERI?  
   8 3SCW ...      LDX   2  FX2  
  10 3SD6 ...      LDN   4  FORMDATA(3)  
  21 3SDB ...[ PERI FROM ADATA/FORMLOOP SINCE LENGTH CAN BE GREATER THAN ACTLF BUFFER
  18 3SDL ...      DCA  ,STO,4,CTSA       [ BUFFER IS ON WORD BOUNDARY   
   8 3SF2 ...      GPERI  ,2,NC  
  18 3SFB ...      CALL  7  SETUPCA       [ NO NEED TO COORDINATE HERE   
   5 3SFQ ...      FI
   8 3SG6 ...      LDX   1  FX1  
   8 3SGG ...      ADX   6  FX1  
   7 3SGW ...      EXIT  6  0
   3 3SH= ...
   4 3SHL ...)   
   4 3SK2    [   
  21 3T4L    [THIS SUBROUTINE PRINTS A LINE OF ASTERISKS WITH UPWARD ARROWS AT ENDS  
   4 3TJ=    [   
   4 3W3W    SHAM
   8 3WHG          SBX   6  FX1  
  17 3X36          STO   5  ACOMMUNE7(2)        [PRESERVE EXIT SWITCH
   9 3XGQ          LDX   4  XAST(1)  
  19 3Y2B          CALL  0  SPFL2               [FILL BUFFER WITH ASTERISKS  
  17 3YG2          CALL  7  TEST48              [48 CHAR REPERTOIRE? 
  18 3Y_L          BNG   0  TO48CHARBD          [J IF 48 CHAR REPERTOIRE 
   8 3_F=          LDN   0  #76  
   5 3_YW    TOSHAM  
   7 42DG    #SKI  ATPOLTR   
  10 42Y6          TRACEVER 0,ASTSLASH   
  20 43CQ          DCH   0  GZDATA+1(2)         [UPWARD POINTING ARROW AT START  
   6 43FF ...#SKI  ANIP  
   4 43H8 ...(   
  11 43JX ...[ PRINT WIDTH KEPT IN JSOURCE3  
  10 43LL ...      LDEX  1  JSOURCE3(2)  
   7 43N* ...      ADN   1  1
   4 43Q4 ...)   
   6 43RR ...#UNS  ANIP  
   4 43TG ...#SKI
  16 43XB          DCA      ,LDX,1,COUNT        [GET PRINT WIDTH 
   7 44C2          ADN   1  2
   7 44WL          SRC   1  2
   8 45B=          SMO      FX2  
  19 45TW          DCH   0  GZDATA(1)           [UPWARD POINTING ARROW AT END
   8 46*G          LDX   1  FX1  
   8 46T6          LDN   0  #40  
   6 472# ...#UNS FLLP4BC
   8 477G ...      LDN   0  #41  
  16 47#Q          LDX   5  ACOMMUNE7(2)        [RETURN SWITCH   
  18 47SB          CALL  7  PERI2               [PRINT ON CURRENT LINE   
   8 48#2          ADX   6  FX1  
   7 48RL          EXIT  6  0
   6 49?=    TO48CHARBD  
  21 49QW          LDN   0  #37                 [SLASH TO BOUND ASTERISKS FOR 48 CHAR
   9 4==G          BRN      TOSHAM   
   4 4=Q6    [   
  19 4?9Q    [THIS SUBROUTINE UPDATES THE DEBATCHING CHARACTER OF THE PRINTER
   4 4?PB    [   
   6 4#92    SETUPALPHAS 
   6 4#?K ...#UNS  ANIP  
  18 4#B8 ...      LDCH  0  JSOURCE3(2)          [DEBATCHING CHAR IF NIP 
   6 4#DR ...#UNS  ANIP  
   4 4#HB ...#SKI
   4 4#K_ ...(   
   7 4#NL          LDN   3  0
   9 4*8=          SMO      CPPTR(2) 
   9 4*MW          LDCH  0  K52+4(3) 
   4 4*XN ...)   
  18 4B7G          TXU   0  PASTZ(1)            [WAS LAST CHARACTER Z?   
  14 4BM6          BCS      NOTZ                [J IF NOT
  18 4C6Q          LDN   0  #40                 [ELSE RESTART ALPHABET   
  16 4CLB    NOTZ  ADN   0  1                   [UPDATE CHARACTER
   6 4CMR ...#UNS  ANIP  
   4 4CP8 ...(   
  18 4CQK ...      DCH   0  JSOURCE3(2)         [DEBATCHING CHAR IF NIP  
   9 4CS2 ...      LDX   3  CPPTR(2) 
  16 4CTC ...      BZE   3  (7)                 [J IF NIP ACTLF  
  18 4CWS ...      DCH   0  K52+4(3)            [STORE IN DEVICE LIST TOO
   4 4CY9 ...)   
   6 4C_L ...#UNS  ANIP  
   4 4D33 ...#SKI
   4 4D4D ...(   
  13 4D62          SMO      CPPTR(2)            [IN  
  15 4DKL          DCH   0  K52+4(3)            [DEVICE LIST 
   4 4DTD ...)   
   7 4F5=          EXIT  7  0
   4 4FJW    [   
  19 4G4G    [THIS SUBROUTINE FILLS THE ACTLF PRINT BUFFER WITH THE CURRENT  
   9 4GJ6    [DEBATCHING CHARACTER.  
   4 4H3Q    [   
   6 4HHB    SETUPBUFFER 
   6 4HK_ ...#UNS  ANIP  
  10 4HNJ ...      LDCH  4  JSOURCE3(2)  
   6 4HR7 ...#UNS  ANIP  
   4 4HTQ ...#SKI
   4 4HY* ...(   
   7 4J32          LDN   3  0
   9 4JGL          SMO      CPPTR(2) 
  16 4K2=          LDCH  4  K52+4(3)            [GET CHARACTER   
   4 4K=4 ...)   
  15 4KFW          LDX   5  4                   [AND INSERT  
  13 4K_G          LDN   6  3                   [INTO
  13 4LF6    W     SLL   4  6                   [EACH
  17 4LYQ          ADX   4  5                   [CHARACTER POSITION  
  14 4MDB          BCT   6  W                   [OF X4   
  19 4MY2          CALL  0  SPFL2               [FILL BUFFER WITH CHARACTER  
   7 4NCL          EXIT  7  0
   4 4NX=    [   
  21 4PBW    [THIS SUBROUTINE SPACEFILLS THE FIRST 15 WORDS OF THE ACTLF PRINT BUFFER
   4 4PWG    [   
   6 4QB6    SPACEBUFFER 
  17 4QTQ          LDX   4  ACES                [ACES HOLDS SPACES   
  10 4R*B          STO   4  GZDATA+1(2)  
  10 4RT2          LDN   3  GZDATA+1(2)  
  10 4S#L          LDN   4  GZDATA+2(2)  
  15 4SS=          MOVE  3  14                  [SPACEFILL   
   7 4T?W          EXIT  7  0
   4 4TRG    [   
  15 4W?6    [ THIS SUBROUTINE ACCOMODATES THE FILENME MACRO 
  13 4WQQ    [ NOTE THAT X3 IS THE LINK ACCUMULATOR  
   4 4X=B    [   
   5 4XQ2    SFLENME 
  10 4Y9L          FLENME GUSER,GJOBN,2  
   7 4YP=          EXIT  3  0
   4 4_8W    [   
  16 4_NG    [ THIS SUBROUTINE SPACEFILLS THE ACTLF PRINT BUFFER 
   4 5286    [   
   4 52MQ    SPFL
   8 537B          LDX   4  ACES 
  21 53M2    SPFL2                              [ENTER HERE IF OTHER CHARACTER WANTED
  10 546L          LDX   1  ALOGLEN(2)   
  17 54L=          SBN   1  GZDATA-A1+1         [GET BUFFER LENGTH   
  10 555W          STO   4  GZDATA+1(2)  
  10 55KG          LDN   3  GZDATA+1(2)  
  10 5656          LDN   4  GZDATA+2(2)  
  15 56JQ          MOVE  3  0(1)                [FILL BUFFER 
   8 574B          LDX   1  FX1  
   7 57J2          EXIT  0  0
   4 583L    [   
   4 58H=    [   
  20 592W    [THIS ROUTINE ABANDONS AN OPERATOR TERMINATED LISTING,FREEING BLOCKS
   4 59GG    TIDY
  11 5=26          HUNTW    1,ADATA,CREADL   
   8 5=FQ          BNG   1  TIDY1
   7 5=_B          FREECORE 1
   5 5?F2    TIDY1   
  10 5?YL          HUNTW    1,FILE,ENT   
   8 5#D=          BNG   1  TIDY2
   7 5#XW          FREECORE 1
   5 5*CG    TIDY2   
  11 5*X6 ...      HUNT     1,GOUT,INFOLIST  
   8 5BBQ          BNG   1  TIDY4
   7 5BWB          FREECORE 1
   5 5CB2    TIDY4   
  11 5CTL ...      HUNT     1,GOUT,INFOSET   
   8 5D*=          BNG   1  TIDY5
   7 5DSW          FREECORE 1
   5 5F#G    TIDY5   
   7 5FS6          EXIT  3  0
   4 5G?Q    [   
  10 5GRB    [FOR USE BY LOCKC ROUTINE   
   5 5H?2    SLOCKC  
   8 5HQL          LDX   2  FX2  
   7 5J==          EXIT  1  0
   4 5JPW    [   
   5 5MML    TEST48  
  20 5N7=    [THIS ROUTINE SETS X0 NEGATIVE IF 48 CHARACTER REPERTOIRE IS IN USE 
   6 5N8M ...#UNS  ANIP  
   4 5N=4 ...(   
  16 5N?F ...[ REPERTOIRE KEPT IN JSOURCE3 ONCE NIP IMPLEMENTED  
   8 5N#W ...      LDN   0  #7000
  10 5NB? ...      ANDX  0  JSOURCE3(2)  
   8 5NCN ...      SLL   0  12   
   4 5NF5 ...)   
   6 5NGG ...#UNS  ANIP  
   4 5NHX ...#SKI
   4 5NK# ...(   
   8 5NQ# ...      LADDP    ,0   
   7 5NX# ...      SMO      0
   9 5P4# ...      LDX   0  AREPERT  
   8 5P9# ...      SLL   0  13   
   4 5PB# ...)   
   7 5PL6          EXIT  7  0
   4 5PLG ...#   
   6 5Q5M ...#UNS  ANIP  
   4 5Q68 ...(   
   4 5Q6P ...[   
  21 5Q7= ...[***********************************************************************
   4 5Q7R ...[   
   5 5Q8# ...SETUPNIP
   4 5Q8T ...[   
  21 5Q9B ...[***********************************************************************
   4 5Q9X ...[   
  21 5Q=D ...[ TO SET UP REPERTOIRE INFO IN JSOURCE3 FOR COMPATIBILITY WITH NIP ACTLF
   7 5Q=_ ...[ CALLED ON X7  
   4 5Q?G ...[   
   8 5Q#3 ...      LDX   2  FX2  
   8 5Q#F ...      LADDP    ,4   
   7 5Q#X ...      SMO      4
   9 5Q** ...      LDX   0  AREPERT  
   7 5Q*R ...      SLL   0  1
   8 5QB9 ...      ANDN  0  #6000
  20 5QBN ...      STO   0  JSOURCE3(2)         [STORE 48,64 OR 96 CHAR REPERT   
   7 5QCQ ...      SMO      4
   9 5QD? ...      LDEX  0  AREPERT  
  17 5QDS ...      DEX   0  JSOURCE3(2)         [STORE PRINT WIDTH   
   9 5QF* ...      LDX   3  CPPTR(2) 
   9 5QFW ...      LDCH  0  K52+4(3) 
  18 5QGC ...      DCH   0  JSOURCE3(2)         [STORE DEBATCHING CHAR   
   7 5QGY ...      EXIT  7  0
   4 5QHF ...[   
   4 5QJ2 ...)   
  21 5QJH ...[***********************************************************************
   9 5QKB    [MAIN BODY OF SEGMENT   
  21 5R52    [***********************************************************************
   5 5RJL    POINT1  
  19 5S4=    [THIS ENTRY POINT OUTPUTS DEBATCHERS AND HEADINGS FOR LISTFILE  
  21 5SHW    [***********************************************************************
   5 5T3G    POINT2  
  18 5TH6    [THIS ENTRY POINT OUTPUTS DEBATCHERS AND HEADINGS FOR ONLINE
   4 5W2Q    [   
   4 5WGB    [   
  16 5WQ8          STO   2  AWORK1(2)           [ NOT EB ENTRY   
  18 5WR# ...REG1  JNRGROP  ,REG3            [JUMP IF REGROPE BIT NOT SET
  16 5WSD ...      JENG     ,REG2               [JUMP IF ENGAGED 
  18 5WTJ ...      ENGPER   YAB,,YAB             [CD &BRKIN EXIT TO YAB  
   8 5WWN ...REG2  COOR3    #62  
   8 5WXS ...      BRN      REG1 
  17 5WYY ...REG3  CALL  7  SETUPCA             [SET UP CONTROL AREA 
   6 5X29 ...#UNS  ANIP  
  17 5X2D ...      CALL  7  SETUPNIP            [INITIALISE JSOURCE3 
   7 5X2P ...#UNS G3SIGNET   
   4 5X3D ...(   
   9 5X47 ...      LDX   0  CPPTR(2) 
  19 5X4W ...      IF  0,E,ASIGNETPTR           [ IF THIS IS A SIGNET LISTING
  19 5X5K ...      THEN                         [ THEN FIND ITS DESTINATION  
  18 5X6# ...      MHUNTW   3,APERI,APROPNOS    [ FROM THE APROPNOS BLOCK
   9 5X73 ...      LDX   5  ADTEMP(3)
   7 5X7Q ...      SMO      5
   9 5X8F ...      LDX   0  FPTR(3)  
  10 5X98 ...      STO   0  GZDATA+2(2)  
   9 5X9X ...      LDX   0  SHASH(1) 
  10 5X=L ...      STO   0  GZDATA+1(2)  
   7 5X?* ...      LDN   5  9
   9 5X#4 ...      DCA  ,STO,5,COUNT 
  19 5X#R ...      DISENGPER                    [ WE MUST DISENGAGE BEFORE   
  17 5X*G ...      LDN   5  0                   [ ISSUING THE PERI   
   8 5XB9 ...      CALL  7  PERI 
  19 5XBY ...      CALL  7  SETUPCA             [ AND RESET THE CONTROL AREA 
   5 5XCM ...      FI
   4 5XDB ...)   
   7 5XDR ...#UNS   FLLP4B   
   9 5XF8 ...      CALL  6  SFLOOP   
   6 5XF# ...#UNS  ANIP  
  17 5XFD ...XNLP                               [NIP ACTLF JOINS HERE
  17 5XFL          LDN   5  0                   [START OF HEADINGS   
   9 5X_=          STOZ     GZDATA(2)
   7 5YDW          LDN   6  3
   8 5YYG    NULLS CALL  7  PERI3
   8 5_D6          BCT   6  NULLS
  10 5_MY          SEGENTRY K80LISTINIT  
   8 5_XQ          LDN   0  #11  
  18 62CB          CALL  7  PERI2               [THROW TO HEAD OF FORM   
  18 62X2          CALL  6  SHAM                [PRINT LINE OF ASTERISKS 
  16 63BL    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
  11 63W=    [PART OF SPECIAL DEBATCHING AID 
   5 64*W    [S71831A
  10 64N? ...      SEGENTRY K50LISTINIT  
   9 652N ...      BRN      NODEBS   
  16 65*6    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
   8 65SQ          LDN   0  #01  
  16 66#B          CALL  7  PERI2               [THROW ONE LINE  
  19 66S2          CALL  7  SETUPALPHAS         [UPDATE DEBATCHING CHARACTER 
  16 67?L          CALL  7  SETUPBUFFER         [AND FILL BUFFER 
   7 67HD          LDN   5  0
  16 67R=          CALL  7  PERI                [PRINT FIRST LINE
  20 68=W          CALL  7  SPACEBUFFER         [SPACEFILL FIRST 15 BUFFER WORDS 
  12 68QG          CALL  7  PERI                [
  12 69=6          CALL  7  PERI                [
  10 69PQ          LDN   7  GZDATA+4(2)  
  10 6=9B ...      MHUNTW   2,GOUT,INFO  
  20 6=P2          CALL  3  SFLENME             [:USERNAME.JOBNAME INTO BUFFER   
  14 6?8L          LDX   1  FX1                 [RESET   
  15 6?N=          LDN   5  0                   [OVERWRITTEN 
  15 6#7W          LDX   2  FX2                 [ACCUMULATORS
  14 6#MG          LDX   0  ACES                [WIPE OUT
  16 6*76          STO   0  GZDATA+4(2)         [CHARACTER COUNT 
  10 6*FH ...      SEGENTRY K57LISTINIT  
   9 6*RY ...      BRN      NOLFPRINT
  19 6B6B          DOWN     LISTEXCP,1          [SPACEFILL REQUIRED CHARACTRS
  10 6BL2          SEGENTRY K67LISTINIT  
   6 6C5L    NOLFPRINT   
   4 6CK=    [   
   9 6D4W          CALL  7  TEST48   
   7 6DJG    #SKI  ATPOLTR   
   9 6F46          TRACEVER 0,48ORNOT
  19 6FHQ          BPZ   0  NOT48A              [J IF NOT 48 CHAR REPERTOIRE 
  16 6G3B          LDN   0  #20                 [ELSE SPACEFILL  
  14 6GH2          DCH   0  GZDATA+5(2)         [COLON   
   5 6H2L    NOT48A  
  18 6HG=          CALL  7  PERI                [PRINT HIGHLIGHTING LINE 
  20 6H_W          CALL  7  SPACEBUFFER         [SPACEFILL FIRST 15 WORDS AGAIN  
   8 6JFG          CALL  7  PERI 
   8 6J_6          CALL  7  PERI 
  10 6KDQ          CALL  7  SETUPBUFFER  
   7 6KNJ          LDN   5  0
   8 6KYB          CALL  7  PERI 
   4 6LD2    [   
  15 6LXL    [DEBATCHERS HAVE BEEN OUTPUT-NOW FOR HEADINGS   
  11 6MC=    [FIRST HEADING-'PRODUCED ON ETC'
   5 6MWW    NODEBS  
  10 6NBG          SEGENTRY K60LISTINIT  
  16 6NW6          CALL  0  SPFL                [SPACEFILL BUFFER
  10 6P*Q ...      MHUNTW   3,GOUT,INFO  
   9 6PTB          LDX   4  GMODE(3) 
  15 6Q*2          BNG   4  TONLINE             [J IF ONLINE 
  20 6QSL          MHUNTW   3,ADATA,CREADL      [FIND BLOCK WITH'#LISTING ETC'   
   9 6R#=          LDX   1  A1+FRH(3)
  16 6RRW          LDX   5  1                   [LOAD NO OF CHARS
   7 6S?G          ANDN  5  3
   7 6SR6          ERN   5  3
  18 6T=Q          ADN   5  1                   [X5 IS COMPLEMENT WRT 4  
  15 6TQB          SRC   1  2                   [SPACEFILL   
  14 6W=2          LDN   0  #20                 [BLOCK   
  14 6WPL    XSP   SMO      3                   [BUFFER  
  14 6X9=          DCH   0  CPDATA(1)           [BEYOND  
  14 6XNW          BCHX  1  /                   [ENTRANT 
  13 6Y8G          BCT   5  XSP                 [NAME
   9 6YN6          LDN   5  CPDATA(3)
  10 6_7Q          LDN   6  GZDATA+1(2)  
  20 6_MB          MOVE  5  0(1)                [MOVE IN #LISTING OF ENTRANT NAME
   8 7272          SMO      FX1  
   8 72LL          LDN   5  THEAD
   7 736=          ADX   6  1
  19 73KW          MOVE  5  6                   [MOVE'PRODUCED ETC'TO BUFFER 
  17 745G          ADN   6  3                   [POSITION AFTER 'ON' 
   8 74K6          SBX   6  FX2  
  10 754Q          MHUNTW   3,FILE,ENT   
  21 75JB          LDX   4  EWRITDAY(3)         [GET DATA AND TIME FILE LAST WRITTEN 
  15 7642          LDX   5  EWRITTIME(3)        [FOR HEADING 
  15 773=          DATECON  4                   [CONVERT DATE
  19 77GW          LDX   1  ACOMMUNE1(2)        [GET PART OF CONVERTED DATE  
   8 782G          ADX   6  FX2  
   7 78G6          SMO      6
  16 78_Q          STO   1  0                   [STORE BEFORE'AT'
   7 79FB          ADN   6  1
  19 79_2          LDX   1  ACOMMUNE2(2)        [GET REST OF CONVERTED DATE  
   7 7=DL          SMO      6
  16 7=Y=          STO   1  0                   [STORE BEFORE'AT'
   8 7?CW          SBX   6  FX2  
  15 7?XG          TIMECON  5                   [CONVERT TIME
   8 7#C6          ADX   6  FX2  
  14 7#WQ          ADX   6  N31(1)              [ADD 1.3 
  10 7*BB          LDN   5  ACOMMUNE1(2) 
  16 7*W2          MVCH  5  8                   [STORE AFTER'AT' 
   4 7B*L    [   
   9 7BT=          CALL  7  TEST48   
  19 7C#W          BPZ   0  NOT48B              [J IF NOT 48 CHAR REPERTOIRE 
  18 7CSG          LDN   0  #32                 [ELSE REPLACE HASH WITH  
  14 7D#6          DCH   0  GZDATA+1(2)         [ASTERISK
  18 7DRQ          LDN   0  #20                 [AND REPLACE COLON WITH  
  14 7F?B          DCH  0   GZDATA+4(2)         [SPACE   
   5 7FR2    NOT48B  
   8 7G=L          LDN   0  #42  
   7 7GQ=          LDN   5  0
   8 7H9W          CALL  7  PERI2
   4 7HPG    [   
  17 7J96    [NOW SET UP AND PRINT SECOND HEADING,IE 'OUTPUT BY' ETC 
   4 7JNQ    [   
  16 7K8B          CALL  0  SPFL                [SPACEFILL BUFFER
  16 7KN2          LDX   3  FX2                 [PRESERVE POINTER
  10 7L7L ...      MHUNTW   2,GOUT,INFO  
  15 7LM=          LDN   4  SHEAD(1)            [NEXT HEADING
   5 7M6W    TONLINE2
  10 7MLG          LDN   5  GZDATA+1(3)  
   7 7N66          MOVE  4  6
  10 7NKQ          LDN   7  GZDATA+6(3)  
   9 7P5B          CALL  3  SFLENME  
   8 7PK2          LDX   1  FX1  
   9 7Q4L          LDX   4  GMODE(2) 
  15 7QJ=          BNG   4  ZON                 [J IF ONLINE 
  10 7R3W          LDX   4  SHEAD+5(1)   
   8 7RHG          BRN      XBAC 
   4 7S36    ZON 
  10 7SGQ          LDX   4  OHEAD+5(1)   
   8 7T2B    XBAC  SMO      FX2  
   9 7TG2          STO   4  GZDATA+6 
  18 7T_L          LDN   6  ONAT(1)             [STORE REST OF HEADING   
   8 7WF=          MVCH  6  16   
  14 7WYW          LDX   4  CLEANCT             [GET TIME
   8 7XDG          SBX   7  FX2  
  15 7XY6          TIMECON  4                   [CONVERT TIME
   8 7YCQ          ADX   7  FX2  
  17 7YXB          LDN   6  ACOMMUNE1(2)        [PUT TIME IN HEADING 
   7 7_C2          MVCH  6  8
   8 7_WL          SBX   7  FX2  
  14 82B=          DATECON  EDATE               [GET DATE
   8 82TW          ADX   7  FX2  
  18 838=          STO   7  ACOMMUNE4(2)        [RETAIN BUFFER POINTER   
  17 83*G          LDN   6  ACOMMUNE1(2)        [PUT DATE IN HEADING 
   7 83T6          SLC   7  2
   8 84#Q          SBN   7  19   
   7 84SB          SRC   7  2
  16 85#2          MVCH  6  7                   [MOVE TO BUFFER  
   9 85BP          LDN   6  UNLFON(1)
  10 85CL          LDX   7  ACOMMUNE4(2) 
  16 85DH          MVCH  6  8                   [MOVE IN 'USING' 
  21 85FD          LDN   5  JSOURCE1(2)         [MOVE JSOURCE WORDS TO ACOM 1 AND 2  
  10 85G*          LDN   6  ACOMMUNE1(2) 
   7 85H=          MOVE  5  2
   8 85J7          SBX   7  FX2  
  18 85K4          OUTPERA                      [GET UNIT NO. IN ACOM2   
   8 85K_          ADX   7  FX2  
  21 85LW          LDN   6  ACOMMUNE2(2)        [AND NO. OF CH. IN 'UNIT NO' IN AC1  
  10 85MR          LDX   5  ACOMMUNE1(2) 
   7 85NN          SMO      5
  16 85PK          MVCH  6  0                   [MOVE IN UNIT NO.
  16 85RL          LDN   5  0                   [DURING HEADINGS 
   4 86?=    [   
   9 86QW          CALL  7  TEST48   
  19 87=G          BPZ   0  NOT48C              [J IF NOT 48 CHAR REPERTOIRE 
  18 87Q6          LDN   0  #32                 [ELSE REPLACE HASH WITH  
  14 889Q          DCH   0  GZDATA+1(2)         [ASTERISK
  18 88PB          LDN   0  #20                 [AND REPLACE COLON WITH  
  14 8992          DCH   0  GZDATA+7(2)         [SPACE   
   5 89NL    NOT48C  
  17 8=8=          LDN   0  #42                 [NOT SET UP IF ONLINE
  16 8=MW          CALL  7  PERI2               [OUTPUT HEADING  
   4 8?7G    [   
  17 8?M6    [OUTPUT THIRD HEADING,IE'DOCUMENT' OR 'CONTINUATION' ETC
   4 8#6Q    [   
  16 8#LB          CALL  0  SPFL                [SPACEFILL BUFFER
  10 8#LC          HUNTW    3,FILE,ENT   
  15 8#LD          BNG   3  NOREVFIL1           [ J IF ONLINE
  21 8#LF          JBC NOREVFIL,3,BELOSE        [JUMP IF FILE NOT REVERTED BY CLOSE  
   7 8#LG          FREECORE 3
  19 8#LH          LDN   4  REVF(1)             [PICK UP COPY LOST MESSAGE   
  10 8#LJ          LDN   5  GZDATA+1(2)  
  17 8#LK          MOVE  4  12                  [AND PUT IT IN BUFFER
   8 8#LL          LDN   0  #42  
   7 8#LM          LDN   5  0
   8 8#LN          CALL  7  PERI2
   8 8#LP          CALL  0  SPFL 
   9 8#LQ          BRN      NOREVFIL1
   5 8#LR    NOREVFIL
   7 8#LS          FREECORE 3
   6 8#LT    NOREVFIL1   
  18 8#M? ...      MHUNTW   3,GOUT,INFO         [CONTINUATION CODE!!!!   
  10 8#MQ    [START OF CONTINUATION CODE 
  10 8#N5          LDX   0  RESTARTBIT(1)
   9 8#ND          ANDX  0  GMODE(3) 
  17 8#NR          BZE   0  NOTRESTA            [ J IF NOT RESTART   
   9 8#P6          LDN   4  RCON(1)  
  10 8#PF          LDN   5  GZDATA+1(2)  
  20 8#PS          MOVE  4  4                   [MOVE 'CONTINUATION' INTO BUFFER 
  11 8#Q7          MHUNTW   2,ADATA,CREADL   
  19 8#QG          LDX   4  A1+FRH(2)           [NO. OF CH. IN FILENAME+12   
   8 8#QT          LDCT  5  #600 
  10 8#R8          ADN   5  CPDATA+2(2)  
   8 8#RH          LDX   2  FX2  
  10 8#RW          LDN   6  GZDATA+5(2)  
  17 8#S9          SBN   4  11                  [REMOVE #LISTING OF  
   7 8#SJ          SMO      4
  18 8#SX          MVCH  5  0                   [MOVE FILENAME IN BUFFER 
   9 8#T=          LDX   0  GMODE(3) 
  10 8#TK          ANDX  0  RESTACTN(1)  
  20 8#TY          BZE   0  NORESACT            [ J IF NO RESTART ACTION SPEC.   
   9 8#W?          LDN   5  RFOL(1)  
   7 8#WL          ADN   6  1
  20 8#W_                                       [X6 STILL CONTAINS BUFFER POINTER
  20 8#X#          MOVE  5  7                   [MOVE 'FOLLOWING' ETC IN BUFFER  
  10 8#XM          ANDX  0  RESTACTN+1(1)
  17 8#Y2          ADN   6  7                   [RESET BUFFER POINTER
  17 8#Y*          SRL   0  15                  [ J IF ALL SPECIFIED 
   7 8#YN          SMO      0
   9 8#_3          LDN   5  RPAGE(1) 
  18 8#_B          MOVE  5  2                   [MOVE IN 'PAGE OR 'ALL'  
   5 8#_P    NORESACT
  17 8#_Q ...      CALL  7  TEST48             [J. IF NOT 48 CHAR REP
  19 8#_R ...      BPZ   0  NOT48E                 [ELSE REPLACE HASH WITH   
  15 8#_S ...      LDN   0  #32                    [ASTERISK 
  19 8#_T ...      DCH   0  GZDATA+1(2)            [AND REPLACE COLON WITH   
  14 8#_W ...      LDN  0  #20                   [SPACE  
  10 8#_X ...      DCH   0  GZDATA+5(2)  
   5 8#_Y ...NOT48E  
   8 8*24          LDN   0  #42  
   7 8*2C          LDN   5  0
   8 8*2Q          CALL  7  PERI2
   8 8*35          CALL  0  SPFL 
   5 8*3D    NOTRESTA
  10 8*3R    [END OF CONTINUATION CODE!!!
   9 8*46    [OUTPUT 'DOCUMENT ETC.  
  10 8*54 ...      MHUNTW   3,GOUT,INFO  
   9 8*62          LDN   4  SDOC(1)  
  10 8*KL          LDN   5  GZDATA+1(2)  
  17 8B5=          MOVE  4  3                   [MOVE IN 'DOCUMENT'  
   9 8C4G          LDEX  4  GSUSE(3) 
   7 8CJ6          ADN   4  3
   7 8D3Q          SRL   4  2
  10 8DHB          ADN   4  GZDATA-A1+4  
  21 8F32          TXU   4  ALOGLEN(2)          [TEST AND J IF BUFFER LARGE ENOUGH   
   8 8FGL          BCC      RIGHT
  10 8G2=          TXL   4  ALOGLEN(2)   
   8 8GFW          BCS      RIGHT
  20 8G_G          LDX   6  ALOGLEN(2)          [ELSE WE MOVE AS MUCH AS POSSIBLE
  10 8HF6          SBN   6  GZDATA-A1+4  
   7 8HYQ          SLL   6  2
   8 8JDB          BRN      RIG  
   5 8JY2    RIGHT   
   9 8KCL          LDEX  6  GSUSE(3) 
   4 8KX=    RIG 
  19 8LBW          LDN   4  GHEAD(3)            [COPY ACROSS USER'S HEADER   
  10 8LWG    TAG2  LDN   5  GZDATA+4(2)  
   7 8MB6          SMO      6
   7 8MTQ          MVCH  4  0
   7 8N*B          LDN   5  0
   4 8NT2    [   
  19 8P#L          CALL  7  TEST48              [IS IT 48 CHAR REPERTOIRE?   
  19 8PS=          BPZ   0  NOT48D              [J IF NOT 48 CHAR REPERTOIRE 
  10 8Q?W          LDCH  0  GZDATA+4(2)  
  16 8QRG          TXU   0  SCOLON(1)           [IS IT A COLON?  
  15 8R?6          BCS      NOTCOLON            [IF NOT JUMP 
  16 8RQQ          LDN   0  #20                 [ELSE REPLACE BY 
  14 8S=B          DCH   0  GZDATA+4(2)         [SPACE   
   9 8SQ2          BRN      NOT48D   
   5 8T9L    NOTCOLON
  15 8TP=          TXU   0  SHRIEK(1)           [IS IT A !   
  15 8W8W          BCS      NOT48D              [IF NOT,JUMP 
  17 8WNG          LDN   0  #27                 [ELSE REPLACE WITH   
  15 8X86          DCH   0  GZDATA+4(2)         [APOSTROPHE  
   8 8XMQ          CALL  7  PERI3
  16 8Y7B          CALL  0  SPFL                [SPACEFILL BUFFER
  16 8YM2          LDN   0  #36                 [NOW OVERPRINT   
  15 8_6L          DCH   0  GZDATA+4(2)         [WITH POINT  
   8 8_L=          LDN    0  #40 
   8 925W          CALL  7  PERI2
   9 92KG          BRN      NOMORE   
   5 9356    NOT48D  
  17 93JQ          CALL  7  PERI3               [PRINT THIRD HEADING 
   5 944B    NOMORE  
   8 94J2          LDN   0  #02  
  20 953L          CALL  7  PERI2               [THROW TWO LINES WITHOUT PRINTING
  11 95H=          HUNTW    1,ADATA,CREADL   
  19 962W          BNG   1  NFR                 [THERE ISN'T ONE IF ONLINE   
   7 96GG          FREECORE 1
   4 9726    NFR 
  10 97#H ...      MHUNTW   3,GOUT,INFO  
  17 97LY ...      JBS  XFRTOERR,3,BLFFRTOER    [ J IF ERROR FOUND   
   9 97_B          LDX   4  GMODE(3) 
   8 98F2          SMO      FX1  
   9 98YL          LDX   5  SETUPBIT 
   7 99D=          ANDX  5  4
  19 99XW          BNZ   5  XCROSS              [J IF THERE IS A SETUP FILE  
  16 9=CG          BPZ   4  XCROSS              [J IF NOT ONLINE 
   6 9=X6 ...XFRTOEXIT   
   6 9?BQ          LFEND 
   5 9?WB    XCROSS  
  10 9#B2          ACROSS   LISTPREP,1   
   4 9#TL    [   
   4 9**=    [   
   4 9*SW    [   
  21 9B#G    [***********************************************************************
   5 9BS6    POINT3  
  20 9C?Q    [THIS ENTRY POINT OUTPUTS THE TERMINATING DEBATCHERS FOR LISTFILE   
   9 9CRB          STO   2  AWORK1(2)
   9 9D?2          BRN      ZLFTERMS 
  21 9DQL    [***********************************************************************
   5 9F==    POINT4  
  19 9FPW    [THIS ENTRY POINT OUTPUTS THE TERMINATING DEBATCHERS FOR ONLINE 
   9 9G9G          NGS   2  AWORK1(2)
   6 9G?9 ...#UNS  ANIP  
   9 9G#Y ...      CALL  7  SETUPNIP 
   5 9GP6    ZLFTERMS
   6 9GRP ...#UNS  ANIP  
   4 9GW# ...(   
  17 9GYX ...[ IF NIP CANNOT USE MODE WORD TO TELL IF ACTLF EXTENDED 
   8 9H3G ...      LDX   2  FX2  
  10 9H65 ...      LDX   0  ALOGLEN(2)   
   8 9H8N ...      SBN   0  120  
   9 9H?? ...      LDX   7  CPPTR(2) 
   8 9H*W ...      BNZ   7  NOTM4
   8 9HDF ...      SBN   0  70   
   8 9HH4 ...      BNG   0  NOTM4
   9 9HKM ...      ALTLEND  FX2,187  
   8 9HN= ...      BRN      NOTM4
   5 9HQT ...NOTNIP1 
   4 9HTD ...)   
   6 9HY3 ...#UNS  ANIP  
   4 9J2L ...#SKI
   4 9J59 ...(   
  10 9J82          DCA      ,LDEX,0,MODE 
  16 9JML          BZE   0  NOTM4               [J IF NOT MODE 4 
   4 9JXD ...)   
   9 9K7=          ALTLEND  FX2,100  
   5 9LL6    NOTM4   
  17 9MKB          CALL  7  SETUPCA             [SET UP CONTROL AREA 
   7 9N52          LDN   5  1
   6 9N6? ...#UNS  ANIP  
   4 9N7J ...(   
  15 9N8T ...[ IF NIP OUTPUT REPLY CHANGED BEFORE TERMINATORS
   9 9N=6 ...      LDX   0  CPPTR(2) 
   8 9N?C ...      BNZ   0  NOTNP
   9 9N#N ...      KEEPREP  0,CHANGED
   8 9N*_ ...      ORX   0  GSIGN
   9 9NC= ...      STO   0  GZDUMP(2)
   8 9NDH ...      CALL  7  PERI6
   5 9NFS ...NOTNP   
   4 9NH5 ...)   
  14 9NJL    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   
  13 9P4=    [SPECIAL TO THROW THOF BEFORE DEBATCHERS
   5 9PHW    [S71102A
  14 9Q3G    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   
   5 9QH6    [S71831B
  10 9QTH ...      SEGENTRY K52LISTINIT  
   8 9R7Y ...      BRN      NOT  
  14 9RGB    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   
   8 9S22    [SPECIAL DEBATCHERS 
   7 9SFL          LDN   6  3
   9 9S_=          STOZ     GZDATA(2)
   5 9TDW    NULL2   
   8 9TYG          CALL  7  PERI3
   8 9WD6          BCT   6  NULL2
  14 9WXQ    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!! 
   4 9XCB    NOT 
  10 9XX2          SEGENTRY K62LISTINIT  
   8 9YBL          LDN   0  #02  
  10 9YLD ...      SEGENTRY K30LISTINIT  
   8 9YW= ...      BRN      SPER 
  10 9_64          SEGENTRY K81LISTINIT  
   8 9_*W          LDN   0  #11  
   4 9_TG    SPER
  10 =2*6          SEGENTRY K35LISTINIT  
  15 =2SQ          CALL  7  PERI2               [THROW THOF  
  14 =3#B    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   
   8 =3S2    [SPECIAL DEBATCHERS 
   5 =4?L    [S71831C
  10 =4L3 ...      SEGENTRY K54LISTINIT  
   8 =4YD ...      BRN      SHAMP
  21 =5=W          CALL  7  SETUPBUFFER         [FILL BUFFER WITH CURRENT CHARACTER  
   7 =5QG          LDN   5  1
   8 =6=6          LDN   0  #40  
  16 =6PQ          CALL  7  PERI2               [PRINT FIRST LINE
   7 =79B          LDN   6  6
  17 =7P2    REPT  CALL  7  PERI                [PRINT OTHER 6 LINES 
   8 =88L          BCT   6  REPT 
   8 =8N=    SEB   LDN   0  #02  
  16 =97W          CALL  7  PERI2               [THROW TWO LINES 
   5 =9MG    SHAMP   
  10 ==76          SEGENTRY K64LISTINIT  
  14 ==LQ    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   
  21 =?6B          CALL  6  SHAM                [PRINT LINE OF * WITH _ AT EACH END  
  10 =?L2          SEGENTRY K18LISTINIT  
   6 =#5L          NULL  
   6 =#K=          NULL  
  10 =#T4          SEGENTRY K82LISTINIT  
   8 =*4W          LDN   0  #11  
   9 =*JG          LDX   7  AWORK1(2)
   8 =B46          BZE   7  SX1  
  10 =BHQ          SEGENTRY K70LISTINIT  
   6 =C3B          NULL  
  10 =CH2          SEGENTRY K71LISTINIT  
   4 =CQS    SX1 
  15 =D2L          CALL  7  PERI2               [THROW THOF  
   6 =D43 ...#UNS  ANIP  
   4 =D5D ...(   
  14 =D6T ...[ IF NIP ACTLF MUST OUTPUT END OF LISTING   
  19 =D8= ...[ WILL NOT RETURN UNLESS A NIPENDLF OR NIPREL MACRO IS ISSUED   
   9 =D9M ...      LDX   0  CPPTR(2) 
   8 =D?4 ...      BNZ   0  XNL  
   8 =D#F ...      CALL  7  PERI4
   8 =D*W ...      BRN      SBRK3
  21 =DC? ...XNL   STOZ     JSOURCE3(2)         [NOT NIP - CLEAR OUT REPERTOIRE INFO 
   4 =DDN ...)   
   5 =DG=    SBRK2   
   7 =DHS ...#UNS G3SIGNET   
   4 =DKB ...(   
   9 =DLY ...     LDX   0  CPPTR(2)  
   9 =DNG ...     IF 0,E,ASIGNETPTR  
   6 =DQ4 ...     THEN   
   7 =DRL ...       DISENGPER
   5 =DT8 ...     FI 
   6 =DWQ ...SIGNETKICK  
   4 =DY# ...)   
   9 =F8X ...SBRK3 LDX   0  AWORK1(2)
  19 =FFG          BZE   0  TIDYPER             [J IF EBI OR OL TERMINATORS  
   9 =F_6          BNG   0  TIDYPER  
   7 =G6# ...      SBN   0  1
  17 =G?G ...      BZE   0  XFRTOEXIT           [ J IF FROM/TO ERROR 
   5 =GDQ          UP
   5 =GYB    TIDYPER 
  10 =HD2          SEGENTRY K19LISTINIT  
   9 =HXL          LDX   0  FINISH   
   7 =JC=          SLL   0  1
   9 =JWW          BPZ   0  NFINISH  
  10 =KBG          ACROSS   LISTNEXT,6   
   5 =KW6    NFINISH 
   4 =L?6 ...#   
   5 =L?Y ...POINT20 
   4 =L#Q ...#   
  14 =L*Q          LFBITSET F,2                 [ACTLF   
  13 =LTB          LDN   1  JOBRING(2)          [CAN 
  13 =M*2          POINTACT OUT,2               [BE  
  15 =MSL          ADN   2  JOBRING             [CONSIDERED  
  13 =N#=          ENRING   1,2                 [FOR 
  14 =NRW          LFEND    EB                  [LISTING 
   4 =P?G    [   
  21 =PR6    [***********************************************************************
   5 =Q=Q    POINT5  
  16 =QQB    [THIS ENTRY POINT IS FROM ENGAGE AFTER EB INTERRUPT 
   4 =QRJ ...#   
   4 =R8L ...#   
   9 =R=2          STOZ     AWORK1(2)
   7 =R#K ...#UNS G3SIGNET   
   4 =RC8 ...(   
   9 =RFR ...      LDX   0  CPPTR(2) 
  12 =RJB ...      BXE   0  ASIGNETPTR,SIGNETKICK
   4 =RL_ ...)   
   8 =RPJ ...REG4  JNRGROP  ,REG6
   8 =RS7 ...      JENG     ,REG5
   9 =RWQ ...      BRN      TIDYPER  
   8 =R_* ...REG5  COOR3    #62  
   8 =S3Y ...      BRN      REG4 
  17 =S6H ...REG6  CALL  7  SETUPCA             [SET UP CONTROL AREA 
  15 =S9=          LDN   5  1                   [TERMINATORS 
  19 =SF4 ...      JWRONG   ,TIDYPER            [J. DEVICE HAS BEEN WRONGED. 
   8 =SNW          BRN      SEB  
  21 =T8G    [***********************************************************************
   5 =TN6    POINT6  
  19 =W7Q    [THIS ENTRY OUTPUTS AN ERROR MESSAGE ON PERIPHERAL INDICATING   
  11 =WMB    [THAT THE SETUP FILE IS EMPTY   
   4 =X72    [   
  15 =XLL          STO   2  AWORK1(2)           [ NOT EB PATH
  16 =_5G          CALL  0  SPFL                [SPACEFILL BUFFER
   8 =_K6 ...      LDX   1  FX1  
  20 ?24Q          LDN   4  SEMPT(1)            [LOAD ADDRESS OF EMPTY MESSAGE   
  16 ?2JB          LDN   5  GZDATA+1(2)         [COPY INTO BUFFER
   7 ?342 ...      MOVE  4  4
  17 ?8CW          CALL  7  SETUPCA             [SET UP CONTROL AREA 
  13 ?8XG          LDN   0  #42                 [PFCC
   7 ?9C6          LDN   5  2
  16 ?9WQ          CALL  7  PERI2               [OUTPUT MESSAGE  
   5 ?=BB          UP
   4 ?=G? ...[   
  14 ?=L8 ...[ OUPUT MESSAGE SET UP BY CENTRAL ROUTINES  
   4 ?=Q5 ...[   
   5 ?=W2    XFRTOERR
   7 ??38 ...      LDN   0  1
  17 ??8B ...      STO   0  AWORK1(2)           [ EXIT PATH MARKER   
   8 ??*L          CALL  0  SPFL 
  11 ??T= ...      MHUNTW   3,ADATA,HEADING  
  10 ?##W ...      LDN   4  HEADPFCC(3)  
   9 ?#SG          LDN   5  GZDATA(2)
  10 ?*#6 ...      SMO      HEADCNT(3)   
   7 ?*RQ ...      MVCH  4  3
   7 ?B?B          FREECORE 3
   9 ?BBT ...      CALL  7  SETUPCA  
   7 ?BG# ...      LDN   5  1
   8 ?BKR ...      CALL  7  PERI3
  10 ?BP= ...      MHUNT    1,GOUT,INFO  
  17 ?BSP ...      JBC      ZLFTERMS,1,BLFSETUP [ J IF NO SETUP FILE 
  20 ?BY8 ...      FILEMOVE 0,1                 [ INTERCHANGE SO MAIN FILE AT TOP
  17 ?C3M ...      BRN      ZLFTERMS            [ TERMINATE LISTING  
   4 ?C76 ...[   
   4 ?C=L    [   
  21 ?CQ=    [***********************************************************************
  19 ?D9W    [CODE FOR DEALING WITH FAILED/DISENGAGED PRINTERS OR PAPER LOW  
   5 ?DPG    NOTENG  
   8 ?DTC          SMO      FX2  
   9 ?D_#          LDX   0  AWORK1   
  16 ?F59          BZE   0  TIDYPER             [ J IF EB PATH   
  11 ?F96          ENGPER   SBRKIN,,SBRKIN   
   9 ?FNQ          BRN      RPTPERI  
   5 ?G8B    SBRKIN  
   8 ?GN2          LDX   4  FX1  
   7 ?H7L          ADX   4  5
   9 ?HM=          EXIT  4  STABLE   
   4 ?J6W    PLOW
  10 ?JLG          DOWN     LISTNEXT,10  
   9 ?K66          BRN      SBRKIN   
   5 ?KKQ    YABANL  
   7 ?L5B          FREELINK  
   4 ?LK2    YAB 
   7 ?M4L          LDN   4  0
   8 ?MJ=          CALL  3  TIDY 
   8 ?N3W          LFEND    CD   
   4 ?Q2B    [   
  21 ?QG2    [***********************************************************************
  10 ?Q_L    [SECOND HEADING FOR ONLINE  
   5 ?RF=    TONLINE 
   9 ?RYW          LDN   4  OHEAD(1) 
  16 ?SDG          LDX   2  3                   [INTERFACE FOR   
  17 ?SY6          LDX   3  FX2                 [SFLENME SUBROUTINE  
   9 ?TCQ          BRN      TONLINE2 
   4 ?TDC ...[   
  21 ?TF4 ...[***********************************************************************
   5 ?TFP ...POINT21 
   4 ?TGB ...#   
   4 ?TW9 ...[   
   4 ?TW= ...[   
  21 ?TW? ...[***********************************************************************
   4 ?TW# ...[   
   5 ?TW* ...POINT8  
   4 ?TWB ...[   
  21 ?TWC ...[***********************************************************************
   4 ?TWD ...[   
   6 ?TWF ...#UNS  ANIP  
   4 ?TWG ...(   
  17 ?TWH ...[ ENTRY POINT OF NIPLINES MACRO ON FIRST CALL OF LISTING
  17 ?TWJ ...[ FLAG Q (BEFORE FIRST LINE OF LISTING) SHOULD BE SET   
  20 ?TWK ...[ IT MUST BE CLEARED IMMEDIATELY S THAT THIS ENTRY IS NOT USED AGAIN
  21 ?TWL ...[ EVEN IF THERE IS A BREAKIN - AND NIPREL WILL KNOW TO FORCE CLOSEDOWN  
  10 ?TWM ...[ NOT ENTER LISTNEXT DIRECT 
   4 ?TWN ...[   
  20 ?TWP ...      STO   2  AWORK1(2)            [NOT EB AND OUTPUTTING HEADERS  
   8 ?TWQ ...      LFBITCLR Q,2  
  17 ?TWR ...      BRN      XNLP                 [JOIN MAIN CODE PATH
   4 ?TWS ...[   
   4 ?TWT ...)   
   4 ?TXB    #END
   8 ____ ...47115546000100000000
