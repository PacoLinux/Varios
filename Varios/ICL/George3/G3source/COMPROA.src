  14 22FL ...      SEG  COMPROA,,CENT(COMMAND PROCESSOR) 
  15 24D6    [ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 25CB    [   
  18 25X2    [     ENTRY POINTS - MUST BE KEPT FIXED WITHIN THE SEGMENT  
   4 26BL    [   
  16 26W=          SEGENTRY K1COMPROA,QENTRY1   [INITIALISE CPAT 
  19 27*W          SEGENTRY K2COMPROA,QENTRY2   [SET UP CPB/CALAS/CLB PAIR   
  18 27TG          SEGENTRY K3COMPROA,QENTRY3   [ISSUCOM,ISSUCOML MACROS 
  16 28*6          SEGENTRY K7COMPROA,QENTRY7   [ISSUCOMJ MACRO  
  16 28SQ          SEGENTRY K9COMPROA,QENTRY9   [ISSUCOMB MACRO  
   4 29#B    [   
   4 29S2    [   
  19 2=?L    [     THIS CHAPTER IS PART OF THE COMMAND PROCESSOR AND CONTAINS
  20 2=R=    [     1)  CODE TO INITIALISE A CPAT - ENTERED WHEN CPAT FIRST SET UP
  18 2?=W    [                                   ENTRY POINT K1COMPROA   
  20 2?QG    [     2)  COMMAND ISSUER CODE ENTERED BY COMMAND ISSUER MACROS AT   
  18 2#=6    [                                   ENTRY POINTS K3COMPROA  
  18 2#PQ    [                                                K7COMPROA  
  18 2*9B    [                                                K9COMPROA  
  20 2*P2    [     COMPROB ENTERS COMPROA AT K2COMPROA WHEN IT REQUIRES TO SET   
  16 2B8L    [     UP A NEW CPB/CALAS/CLB PAIR FOR BREAKIN LEVEL 
   4 2BN=    [   
   7 2C7W    #DEF  PERTY=#10 
  21 2CMG    MCONTX         #20402              [CONTEXT=NOT BREAKIN,NO CORE IMAGE,  
  14 2D76                                       [ NO USER
   9 2DLQ    READERREP      #00000061
   8 2F6B    OPCONSOLE      +63  
   8 2FL2    XMOPCONSOLE    +11  
   8 2G5L    YMOPCONSOLE    +10  
  19 2GK=    XIPB           +24                 [PERI. TYPE FOR TELEPROCESSOR
   8 2H4W    SMOPCONSOLE    +60  
   8 2HJG    ZLINOP         +7   
   8 2J46    ZCR            +3   
   9 2JHQ    SYSTISSUE      #60000000
   4 2K3B    [   
  16 2KH2    [ XGROT IS JUMPED TO IF THE PERIPHERAL IS DISENGAGED
   5 2L2L    XGROT   
  10 2LG=          LDX   7  CONTEXT(2)   
   8 2L_W          ANDN  7  #210 
   9 2MFG          BZE   7  XGROT1   
   7 2M_6          MOPCLOSE  
   5 2NDQ    XGROT1  
   7 2NYB          SUICIDE   
  13 2PD2    [              SET UP ENTRY IN JOB QUEUE
   5 2PXL    QENTRY1 
   6 2QC=    #SKI  ICT   
   4 2QWW    (   
  10 2RBG          JBS      XICT,,ICTSW  
  10 2RW6 ...      APCOMPRO K80COMPROA   
   4 2S*Q    XICT
   4 2STB    )   
  10 2T*2          LDX   7  JOBEVENTS(2) 
   8 2TSL          LDCT  0   #40 
  10 2W#=          STO   0  JOBEVENTS(2) 
   8 2WRW          JOBCONS  XGROT
  10 2X?G          STO   7  JOBEVENTS(2) 
  21 2XR6          LDN   4  PERTY               [SET CONTEXT BITS TO PERIPHERAL TYPE 
  10 2Y=Q          SAWCEN   CENTRAL,STOR 
  18 2YQB          LDCH  7  JSOURCE1(2)         [GET TYPE OF PERIPHERAL  
  21 2_=2          TXU   7  XMOPCONSOLE(1)      [TEST FOR MOP CONSOLE AND BRANC IF IS
   8 2_PL          BCC      STOR 
  11 329=          TXU   7  YMOPCONSOLE(1)   
   8 32NW          BCC      STOR 
   9 338G          TXU   7  XIPB(1)  
   8 33N6          BCC      STOR 
  19 347Q          TXU   7  SMOPCONSOLE(1)      [TEST FOR SIH. MOP CONSOLE   
  16 34MB          BCC      STORE               [BRANCH IF IT IS 
  19 3572          TXU   7  OPCONSOLE(1)        [BRANCH IF OPERATOR CONSOLE  
   8 35LL          BCC      SHIF1
   8 366=          LDCT  0  #600 
  10 36KW          ANDX  0  CONTEXT(2)   
   8 375G          BZE   0  NSI  
  18 37K6          LDN   4  0                   [UNSET PERIPHERAL CONTEXT
  14 384Q          BNG   0  SETM       [J IF SYS. ISS.   
  16 38JB          LDN   4  #20        [MAKE SYS. STAR. LOOK...  
  13 3942    #                         [LIKE OPERATOR
   8 39HL          BRN      SETM 
   4 3=3=    NSI 
  16 3=GW          BZE   7  SHIF2               [BRANCH IF PTR   
  15 3?2G          TXU   7  ZCR(1)              [BRANCH IF CR
   8 3?G6          BCC      SHIF2
  17 3?_Q          TXU   7  ZLINOP(1)           [TEST FOR LINE INOP. 
  16 3#FB          BCC      ZABAND              [BRANCH IF IT IS 
  10 3#_2          GEOERR   1,WRPERENG   
   5 3*DL    ZABAND  
  16 3*Y=          ACROSS   LOGOUT,7            [ABANDON THE JOB 
  14 3BCW    SHIF2 SLL   4  1                   [TR OR CR
  16 3BXG    SHIF1 SLL   4  1                   [OPERATOR CONSOLE
   4 3CC6    SETM
  10 3CWQ          LDX   5  READERREP(1) 
  19 3DBB          STO   5  AMXMSK(2)           [SET INITIAL READER REPORT   
  18 3DW2    STORE ORX   4  MCONTX(1)           [NOT BROKEN IN,NO USER   
  10 3F*L          LDX   0  SYSTISSUE(1) 
  10 3FT=          ANDS  0  CONTEXT(2)   
  10 3G#W          ORS   4  CONTEXT(2)   
  17 3GSG    [              SET UP LINE BUFFER AND PARAMETER BLOCK   
   4 3H#6    STOR
   8 3HRQ          LDCT  4  #400 
   7 3J?B          LDN   5  0
   6 3JDJ ...#UNS G400   
  19 3JKQ ...      LDN   7  1                  [MARK AS NORMAL COMPROA PATH  
  17 3JR2    QENTRY2                           [ENTRY FROM COMPROB   
   5 3K=L    WBREAK  
  19 3KQ=          LDN   0  2                   [PRESET AN UPPLUS 2 RE-ENTRY 
   9 3L9W          DCH   0  EXT+10(2)
   6 3LPG    XGETCLBCPB  
   6 3LWN ...#UNS G400   
   7 3M3W ...      STO   7  3
   7 3M96          GETCLB   2
   7 3MNQ          GETCPB   2
   6 3MTY ...#UNS G400   
   7 3N36 ...      LDX   7  3
   9 3N8B          STO   4  CPBFSD(2)
  10 3NN2          STO   5  CPBCPLEV(2)  
   9 3P7L          STOZ     CMXMSK(2)
  10 3PM= ...      SEGENTRY K70COMPROA   
  20 3Q6W ...      BRN      XK71                [MENDED TO NULL IF REPORTNEST ON 
   8 3QLG          LDN   3  #10  
   8 3R66          SMO      FX2  
   9 3RKQ          ANDX  3  CONTEXT  
  16 3S5B ...      BZE   3  XK71                [BR IF NOT MOP   
   8 3SK2          SMO      FX2  
   9 3T4L          LDX   3  AMXMSK   
  20 3TJ=          STO   3  CMXMSK(2)           [SET REPORTING MASK AT NEW LEVEL 
   4 3W3W ...XK71
  10 3WHG ...      SEGENTRY K71COMPROA   
   6 3WJN ...#UNS G400   
   4 3WKW ...(   
  10 3WM4 ...      MHUNT    2,CPB,CALAS  
  18 3WN= ...      BNZ   7  XDSKA               [J IF NORMAL COMPROA PATH
  17 3WPD ...      UNSETHK                      [HERE FROM COMPROB   
   7 3WQL ...      DOUPMON   
   8 3WRS ...      BRN      XDSKB
   5 3WT2 ...XDSKA   
  16 3WW8 ...      CPHOOK    2                   [COMPROA PATH   
  17 3WXB ...XDSKB                               [COMMON CODE AGAIN  
  10 3WYJ ...      MHUNT    2,CPB,CALAS  
   4 3W_Q ...)   
   8 3X36          SMO      FX2  
   9 3XGQ          LDX   3  AMFMSK   
  16 3Y2B          STO   3  CMFMSK(2)           [SET TRACING MASK
   8 3YG2          LDX   2  FX2  
   9 3Y_L          LDCH  0  EXT+10(2)
   8 3_F=          SBN   0  #77  
   9 3_YW          ACROSS   COMPRO,2 
   8 42DG          LDCT  0  #770 
   9 42Y6          ERS   0  EXT+10(2)
   9 43CQ          ACROSS   COMPRO,4 
   4 43XB    #   
  12 44C2    [   ENTRY POINTS FOR ISSUCOM MACROS 
   4 44WL    #   
   9 45B=    [ ISSUCOMB AT K9COMPRO  
   4 45TW    #   
   9 46*G    [ ISSUCOMJ AT K7COMPRO  
   4 46T6    #   
  12 47#Q    [ ISSUCOM AND ISSUCOML AT K3COMPRO  
   4 47SB    #   
   5 48#2    QENTRY9 
   8 48RL          LDX   4  GSIGN
   8 49?=          LDCT  5  #600 
   8 49QW          BRN      Z1   
   5 4==G    QENTRY7 
   8 4=Q6          LDCT  4  #300 
   9 4?9Q          ANDX  4  EXT+3(2) 
   7 4?PB          BRN      Z
   5 4#92    QENTRY3 
   7 4#NL          LDN   4  0
   8 4*8=    Z     ORX   4  GSIGN
   8 4*MW          LDX   5  GSIGN
   4 4B7G    Z1  
   7 4BM6          SAVERBIT  
   8 4C6Q          LDCT  7  128  
   9 4CLB          ORS   7  EXT+11(2)
   7 4D62          LDN   7  1
   9 4DKL          ADS   7  CPLEV(2) 
   9 4F5=          ADX   5  CPLEV(2) 
   8 4FJW          LDN   0  #77  
   9 4G4G          DCH   0  EXT+10(2)
  10 4GJ6          BRN      XGETCLBCPB   
   4 4H3Q    #END
   6 ____ ...646051100003
