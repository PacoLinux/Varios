   8 22FL    #OPT  K0HLSEVENT=0  
  14 22_=    #LIS  K0HLSEVENT>K0HLS>K0ALLGEO>K0GREATGEO  
  15 23DW    #SEG  HLSEVENT                     [CENT SECTION
  15 23YG ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   7 24D6          8HHLSEVENT
   4 24XQ    #   
  18 25CB    #     THIS SEGMENT HANDLES THE HLSINFORM MACRO. ON ENTRY:-  
  11 25X2    #     ACOMMUNE1= EVENT NUMBER.  
  10 26BL    #     ACOMMUNE2= JOB NUMBER.
  14 26W=    #     ACOMMUNE3= COPY OF JLSTAT IF WELL JOB.
  17 27*W    #     ACOMMUNE4= HOLDS (IN BITS 12-23) SUBSIDUARY INFO. 
   4 27TG    #   
  11 28*6    #     AND FOR SAVEABLE ENTRIES  
  12 28SQ    #     ACOMMUNE5= NORMAL RETURN LINK 
   4 29#B    #   
  11 29S2          SEGENTRY K1HLSEVENT,XK1   
  11 2=?L          SEGENTRY K2HLSEVENT,XK2   
  11 2=R=          SEGENTRY K3HLSEVENT,XK3   
  11 2?=W          SEGENTRY K4HLSEVENT,XK4   
  11 2?QG          SEGENTRY K5HLSEVENT,XK5   
  12 2#=6          FSHENTRY K7HLSEVENT,XK7,,XK7  
  12 2#PQ          FSHENTRY K8HLSEVENT,XK8,,XK8  
  12 2*9B          FSHENTRY K9HLSEVENT,,XK9,XK9  
  11 2*P2          SEGENTRY K10HLSEVENT,XK10 
  12 2B8L          SEGENTRY K11HLSEVENT,XIGNORE  
   4 2BN=    #   
  10 2C7W     SEGENTRY      K90HLSEVENT  
   4 2CMG    #   
  16 2D76    #     THERE NOW FOLLOWS A TABLE OF 2 WORD ENTRIES.  
  18 2DLQ    #     EACH ENTRY IS SET UP BY HLSEVENT AND HAS THE FOLLOWING
   7 2F6B    #     FORMAT.   
  18 2FL2    #     WORD 0   BIT 0=1 IF EVENT REQUIRED FOR OFFLINE JOBS   
  17 2G5L    #                  1=1 IF EVENT REQUIRED FOR MOP JOBS   
  14 2GK=    #                  2=1 IF HLS TO BE KICKED  
  18 2H4W    #                  3=1 IF JOB TO BE MADE TENTATIVELY STARTED
  19 2HJG    #                  4=1 IF JOB TO LONGSTOP AFTER INFORMING HLS   
  20 2J46    #                  5=1 IF EVENT TO BE IGNORED IF JOB FULLY STARTED  
  15 2JHQ    #                  6=1 IF USER MUSTN'T SET BIT 4
  17 2K3B    #                  7=1 IF JOB IS SAVEABLE AT THIS POINT 
  12 2KH2    #                  8-17 UNDEFINED   
  19 2L2L    #                  18-23 NUMBER OF OPTIONS IF LONGSTOP APPLIES  
   4 2LG=    #   
  20 2L_W    #     WORD 1   BIT SETTINGS TO BE PUT IN HLSYONQ IN HLSTEMPQ BLOCK  
   4 2MFG    #   
   5 2M_6    TABLE   
  15 2NDQ          HLSEVENT FSTART,REQ,IGNOREFS,#1,SAVEABLE  
   9 2NYB          HLSEVENT REGEN,OFF
  12 2PD2          HLSEVENT LOGOUT,#2,NEVERREQ   
  15 2PXL          HLSEVENT STOPPED,REQ,MAKETS,#4,SAVEABLE   
  12 2QC=          HLSEVENT CONNECT,MAKETS,#20   
  10 2QWW          HLSEVENT JOBDATA,OFF  
  10 2RBG          HLSEVENT WAITCOM,OFF  
  10 2RW6          HLSEVENT DELETE,OFF   
  12 2S*Q          HLSEVENT ONLINE,OFF,NEVERREQ  
  10 2STB          HLSEVENT XONLINE,OFF  
  12 2T*2          HLSEVENT RELEASE,OFF,NEVERREQ 
  10 2TSL          HLSEVENT JOBTIME,OFF  
  10 2W#=          HLSEVENT MAXSIZE,OFF  
  10 2WRW          HLSEVENT MAXQUOTA,OFF 
  10 2X?G          HLSEVENT GMQUOTA,OFF  
  10 2XR6          HLSEVENT PERFORM,OFF  
  10 2Y=Q          HLSEVENT MESSAGE,OFF  
  10 2YQB          HLSEVENT DEWELL,OFF   
  10 2_=2          HLSEVENT ENWELL,OFF   
   9 2_PL          HLSEVENT SCHEDULE 
   9 329=          HLSEVENT MOP,OFF  
   9 32NW          HLSEVENT LIMBO,OFF
  10 338G          HLSEVENT RESUME,OFF   
   9 33N6          HLSEVENT HALT,OFF 
  10 347Q          HLSEVENT WAITIN,OFF   
  10 34MB          HLSEVENT WAITOUT,OFF  
   9 3572          HLSEVENT LOAD,OFF 
  12 35LL          HLSEVENT NONCAND,OFF,NEVERREQ 
   6 366=    #UNS  ECS   
  10 36KW          HLSEVENT REALTIME,OFF 
   7 375G    #UNS  EVNOTLOAD 
  10 37K6          HLSEVENT NOTLOAD,OFF  
  11 384Q          HLSEVENT SWITCH,OFF   [CME
  11 38JB          HLSEVENT GEOGNO,OFF   [CME
   4 3942    #   
   9 39HL    #ALT  HLSBITS1=MHLSBITS1
   9 3=3=    #ALT  HLSBITS2=MHLSBITS2
   4 3=GW    #   
  14 3?2G    #     THERE NOW FOLLOWS A TABLE OF LINKS FOR
  15 3?G6    #     USE WHEN UNSAVING JOBS UNDER A DIFFERENT  
  14 3?_Q    #     REGIME AS THAT THEY WERE SAVED UNDER  
   5 3#FB    XCONVTAB
   9 3#_2                   +EVFSTART
   9 3*DL          LINK     ALASB,90 
  10 3*Y=                   +EVSTOPPED   
  10 3BCW          LINK     CONTINUE,21  
   8 3BXG                   -1   
   4 3CC6    #   
   8 3CWQ    STYLE          #31  
   8 3DBB                   #32  
   9 3DW2    TMESS          +CMOPL   
   9 3F*L                   +CNOPER  
   9 3FT=                   +CNOURG  
   9 3G#W    TMESSGEN       +CNOSTART
   8 3GSG    X4             +4   
   8 3H#6    X5             +5   
   5 3HRQ    XHLSCAT 
   8 3J?B          CATMASK  CT   
   9 3JR2    MASKWF         #77776777
   9 3K=L    STOP           #50000001
   9 3KQ=    ZFNAB          #50000002
   8 3L9W    X24            +24  
   9 3LPG    TOP9           #77700000
  10 3M96    XCONNECT       +EVCONNECT   
   4 3MNQ    #   
   5 3N8B    MONOUTS 
   8 3NN2          SBX   7  FX1  
   7 3P7L          SRC   4  6
   7 3PM=          OUTBLOCN 8
  11 3Q6W          OUTPAR   TIMENOW,JOBMILL  
  10 3QLG          OUTPACK  4,1,URGENCY  
   8 3R66          LDN   0  #1000
  10 3RKQ          ANDX  0  CONTEXT(2)   
  17 3S5B          BZE   0  XMON                [J IF NO CORE IMAGE  
   9 3SK2          OUTPAR   PROGMILL 
   9 3T4L    XMON  MONOUT   JUSURG   
   7 3TJ=          PERCOUNT  
  19 3W3W          BZE   6  NOMON2              [J IF OMMITTING F/S MESSAGE  
   9 3WHG          OUTPAR   TIMENOW  
   9 3X36          MONOUT   JSTART   
   5 3XGQ    NOMON2  
   5 3Y2B    X7XIT   
   8 3YG2          ADX   7  FX1  
   7 3Y_L          EXIT  7  0
   4 3_F=    #   
   5 3_YW    XHLSOUTW
   7 42DG          NGNC  0  1
   5 42Y6    XHLSOUT 
   7 43CQ          LDN   6  0
   8 43XB          SBX   7  FX1  
   5 44C2    TRYAGAIN
  18 44WL          BXE   4  X4(1),VARMESS       [J IF VARIABLE MESSAGE   
  21 45B=          BXE   4  X5(1),VARPAR       [J IF VARIABLE PARAMETERTO CNOSTART   
   7 45TW          SMO      4
  10 46*G          LDX   4  TMESS-1(1)   
   5 46T6    XMNOUT  
   7 47#Q          MONOUTX  4
   8 47SB          BRN      VTRY 
   5 48#2    VARMESS 
  10 48RL          HUNT    3,GMON,HLSOUT 
   8 49?=          LDX   4  A1(3)
   7 49QW          SLL   4  2
  14 4==G          OUTMON   4,A1+2,XHLSCAT(1),GMON,HLSOUT
   4 4=Q6    VTRY
  10 4?9Q          MFREE    GMON,HLSOUT  
   9 4?PB    [LOOK FOR NEW MESS NO   
  10 4#92          HUNT     3,GMON,HLSOUT
   9 4#NL          BNG   3  X7XIT1   
  15 4*8=          LDX   4  A1+1(3)             [NEW MESS NO 
   9 4*MW          BRN      TRYAGAIN 
   4 4B7G    [   
  12 4BM6    [ IF USER (VIZ NOT DEWELL MESSAGE)  
  20 4C6Q    [ ZEROISE JMESS AS IF SECOND MESS XCODE HAS COME ALONG DURING OUTPUT
  19 4CLB    [ WE WOULD HAVE PICKED UP GMON,HLSOUT BLOCK &SO JMESSLEFT SET   
  18 4D62    [ WHICH COULD CAUSE PROBLEMS IN EVENT OF SPURIOUS WAKE UPS  
  12 4DKL    [ AS NO BLOCK PRESENT WHEN JMESS SET
   5 4F5=    X7XIT1  
  10 4FJW         JBC   X7XIT,2,CXTUSER  
   8 4G4G          FJOCA     3,2 
   9 4GJ6          STOZ     JMESS(3) 
   8 4H3Q          BRN      X7XIT
   5 4HHB    VARPAR  
   8 4J32          BNZ   6  XNULL
   8 4JGL          OUTMESS  FULLY
   8 4K2=          BRN      VARP 
   5 4KFW    XNULL   
   7 4K_G          OUTNULL   
  10 4LF6    VARP  MHUNT    3,GMON,HLSOUT
   8 4LYQ          LDX   4  A1(3)
   7 4MDB          SLL   4  2
  12 4MY2          OUTPARAM 4,A1+2,GMON,HLSOUT   
  10 4NCL          LDX   4  TMESSGEN(1)  
   9 4NX=          BRN      XMNOUT   
   4 4PBW    #   
   4 4PWG    XK1 
   9 4QB6          LDX   0  AWORK3(2)
  17 4QTQ          STO   0  ACOMMUNE6(2)    [=SC BITS FOR SC COMMAND 
  10 4R*B          LDX   0  ACOMMUNE5(2) 
  15 4RT2          STO   0  AWORK2(2)         [NORMAL LINK   
   9 4S#L          LDN   0  GEORGE   
   7 4SS=          SRC   0  6
   9 4T?W          ORX   0  GVERSION 
  15 4TRG          STO   0  AWORK3(2)      [TO COMPARE LATER 
  10 4W?6          LDX   0  ACOMMUNE1(2) 
   9 4WQQ          STO   0  AWORK1(2)
  15 4X=B          LDX   4  ACOMMUNE2(2)        [JOB NUMBER  
   8 4XQ2          LDCT  6  #400 
   7 4Y9L          LDN   5  0
  16 4YP=          BZE   4  TEST            [J IF NO USER EVENT  
   9 4_8W          FINDJOBQ 3,4,SHLSQ
   9 4_NG          LDX   5  JMISC(3) 
   9 5286          LDX   6  JURGE(3) 
   8 52MQ          BRN      TEST 
   9 537B    SHLSQ SHLSQ    4,3,SJL  
  10 53M2          LDX   5  HLSSTAT(3)   
  10 546L          LDX   6  HLSYONQ(3)   
   7 54L=          SLL   6  7
   8 555W          ERX   6  GSIGN
   8 55KG          BRN      TEST 
  19 5656    SJL   LDX   0  ACOMMUNE3(2)        [SHOULD= JLSTAT FOR WELL JOB 
  20 56JQ          BZE   0  XIGNORE             [JOB HAS LEFT SYSTEM - I THINK   
   7 574B          LDN   6  0
   7 57J2          LDN   5  0
  14 583L          ANDN  0  1                   [JLBMOP  
   8 58H=          BNZ   0  TEST 
  17 592W          LDCT  5  #400                [MARK AS BACKGROUND  
  15 59GG    TEST  LDX   7  ACOMMUNE1(2)        [EVENT NUMBER
  11 5=26          BXU   7  XCONNECT(1),NCON 
  16 5=FQ          ERX   5  GSIGN     [INVERT TYPE IF CN EVENT   
   4 5=_B    NCON
   7 5?F2          SLL   7  1
   7 5?YL          SMO      7
   9 5#D=          LDX   4  TABLE(1) 
  16 5#XW          STO   4  AWORK4(2)        [REMEMBER FOR LATER 
   7 5*CG          SMO      7
  17 5*X6          LDX   7  TABLE+1(1)          [PICK UP HLSYONQ BITS
   8 5BBQ          ORX   7  BIT10
  10 5BWB          LDX   0  ACOMMUNE2(2) 
   9 5CB2          BZE   0  NTESTFS  
   8 5CTL          LDCT  0  #400 
   8 5D*=          BNG   5  XBCK 
   8 5DSW          LDCT  0  #200 
   7 5F#G    XBCK  ANDX  0  4
  18 5FS6          BZE   0  XIGNORE             [J IF EVENT NOT REQUIRED 
   8 5G?Q          LDCT  0  #010 
   7 5GRB          ANDX  0  4
  20 5H?2          BZE   0  NTESTFS             [J IF NO TEST ON FULLY STARTED   
  17 5HQL          BNG   6  XIGNORE             [J IF FULLY STARTED  
   5 5J==    NTESTFS 
  10 5JPW          LDX   0  ACOMMUNE1(2) 
  18 5K9G          BNZ   0  NOTFSREQ            [J IF NOT FSTART ENTRY   
  10 5KP6          LDX   3  CONTEXT(2)   
   7 5L8Q          ANDN  3  4
   7 5LNB          SRL   3  2
  15 5M82          BNZ   3  XOFL                [J IF NOT MOP
  11 5MML          JBS      NOTFSREQ,,HLSMFS 
   8 5N7=          BRN      XPRE 
  10 5NLW    XOFL  JBC      XPRE,,HLSBFS 
   5 5P6G    NOTFSREQ
   9 5PL6          LDN   0  EVXONLINE
  18 5Q5Q          BXE   0  ACOMMUNE1(2),XONCHECK     [J IF XONLINE EVENT
   4 5QKB    XK10
   5 5R52    XONRET  
  12 5_D6          SETNCORE AHLS,3,ADATA,JUNSWAP 
  19 5_XQ          STO   3  ACOMMUNE9(2)           [REMEMBER TEMPQ ADDRESS   
   8 62CB          STOZ     A1(3)
   8 62X2          LDN   0  A1(3)
   9 63BL          LDN   1  A1+1(3)  
   9 63W=          MOVE  0  AHLS-1   
  10 64*W          LDX   4  ACOMMUNE2(2) 
  10 64TG          STO   4  HLSJONO(3)   
  10 65*6          LDX   4  ACOMMUNE1(2) 
   7 65SQ          LDX   1  3
  10 66#B          LDN   0  EVSCHEDULE   
  17 66S2          BXU   0  ACOMMUNE1(2),NOTSC     [J IF NOT SC EVENT
  10 67?L          ORX   7  ACOMMUNE6(2) 
  11 67R=          HUNT     3,ADATA,CSTORE   
  15 68=W          BNG   3  NOTSC           [J IF NO MESSAGE 
   9 68QG          NGX   0  ANUM(3)  
  16 69=6          BPZ   0  NOTSC           [J IF NULL MESSAGE   
   9 69PQ          LDEX  0  ANUM(3)  
   8 6=9B          SMO      FX1  
   9 6=P2          BXL   0  X24,SC24 
   8 6?8L          LDN   0  24   
   9 6?N=    SC24  LDN   2  APARA(3) 
  10 6#7W          LDN   3  HLSJNAM(1)   
   7 6#MG          SMO      0
   7 6*76          MVCH  2  0
   8 6*LQ          LDX   2  FX2  
   5 6B6B    NOTSC   
  10 6BL2          HUNTW    3,ADATA,ASUP 
   8 6C5L          BNG   3  NOSUP
   8 6CK=          ORN   4  #1000
   5 6KYB    NOSUP   
  10 6LD2          STO   4  HLSYONQA(1)  
  10 6LXL          LDX   0  ACOMMUNE4(2) 
   8 6MC=          SLL   0  12   
  18 6MWW          ORS   0  HLSYONQA(1)         [SUBSIDUARY INFORMATILN  
   8 6SR6          ANDX  6  GSIGN
   8 6T=Q          ERX   6  GSIGN
   7 6TQB          SRL   6  7
  17 6W=2          ORS   6  HLSYONQ(1)          [SET (OR NOT) T/S BIT
  10 6WPL          ORS   7  HLSYONQ(1)   
   9 6Y8G          ORS   7  HLSWAIT  
   4 6YN6    #   
   4 6_7Q    XB8 
  16 7272          STO   5  HLSSTAT(1)          [COPY OF JMISC   
   5 72LL    SUPCH   
   9 79_2          LDN   2  BHTEMP   
   9 7=DL          LDX   2  BPTR(2)  
  18 7=Y=          CHAIN    1,2                 [CHAIN ON END OF TEMPQ   
   7 7?CW          LDX   1  3
   7 7?XG          NGN   3  1
  19 7#C6          BPZ   1  SUPCH               [J TO CHAIN ASUP IF PRESENT  
   8 7#WQ          LDCT  3  #040 
  15 7*BB          ORS   3  HLSWAIT         [SET NOKICK BIT  
   9 7*W2          LDX   4  AWORK4(2)
   7 7B*L          SLL   4  2
   9 7BT=          BPZ   4  NOKICK   
  16 7C#W          ERS   3  HLSWAIT          [CLEAR NOKICK BIT   
  15 7CSG          FON      #33                 [ROUSE HLS   
   5 7D#6    NOKICK  
   8 7DRQ          LDX   1  FX1  
   9 7F?B          LDX   0  JOBNO(2) 
  15 7FR2          BZE   0  UP1                 [J IF NO-USER
  18 7G=L          BXU   0  ACOMMUNE2(2),UP1    [J IF JOB NOT CURRENT JOB
   9 7GQ=          LDCH  0  ATYPE(2) 
   9 7H9W          SBN   0  CPAT/64  
  16 7HPG          BNZ   0  UP1                 [J IF NOT CPAT   
   7 7J96          SLL   4  1
  20 7JNQ          BPZ   4  NOTS                [J IF NOT REQUIRED TO BECOME T/S 
  10 7K8B          LDXC  0  CONTEXT(2)   
  17 7KN2          BCS      NOTS                [J IF SYSTEM ISSUED  
  16 7L7L          BNZ   6  NOTS                [J IF T/S ALREADY
   8 7LM=          LDN   0  #6000
  16 7M6W          ERS   0  CONTEXT(2)          [FLIP CONTEXTS   
   8 7MLG          FJOCA    3,2  
   7 7N66          NGNC  0  1
   9 7NKQ          ANDS  0  JURGE(3) 
   7 7P5B          LDN   3  0
  14 7PK2          BPZ   5  XSBS                [J IF MOP
   7 7Q4L          LDN   3  1
   4 7QJ=    XSBS
  10 82B=          LDX   0  HLSCOUNT(3)  
   7 82TW          SBN   0  1
   8 83*G          BPZ   0  XSTO 
  10 83T6          GEOERR   0,HLSCOUNT   
  10 84#Q    XSTO  STO   0  HLSCOUNT(3)  
   4 84SB    #   
   5 85#2    XMACHB2 
   4 85RL    #   
  17 86?=          BNZ   3  XBK1                [J IF BACKGROUND JOB 
  20 86QW          JBS      NOFON,,HLSMFS       [J IF HLS HAS CONTROL OF MOP F/S 
   8 87=G          BRN      XFON 
  20 87Q6    XBK1  JBS      NOFON,,HLSBFS       [J IF HLS HAS CONTROL OF BACK F/S
   8 889Q    XFON  SMO      FX1  
   9 88PB          LDX   0  STYLE(3) 
  20 8992          FOX      0                   [ KICK WATING JOBS OF SAME TYPE  
   5 89NL    NOFON   
   8 8=8=    NOTS  LDX   1  FX1  
   4 8=MW    #   
  21 8?7G    #     SPECIAL CASE: THE 'WAITIN' EVENT IS NOTIFIED FROM THE COORDINATOR 
  21 8?M6    #     BY A 'FIDDLED' DOWN INTO HLSEVENT WHICH RESULTS IN HLSEVENT GOING 
  20 8#6Q    #     BACK UP INTO THE ORIGINAL CALLING SEQUENCE TO THE WAIT ROUTINE
   4 8#LB    #   
  21 8*62    #     TO BE SURE THAT THIS INTERFACE WORKS, HLSEVENT MUST GO 'UP' NOT   
   9 8*KL    #     'UPPLUS' 1 OR 2!! 
   4 8B5=    #   
   9 8BJW          LDN   0  EVWAITIN 
  21 8C4G          BXE   0  ACOMMUNE1(2),UP     [JIF DEALING WITH THE 'WAITIN' EVENT 
   7 8CJ6          SLL   4  1
  19 8D3Q          BPZ   4  UP1                   [J IF LONGWAIT NOT REQUIRED
   8 8DHB          FJOCA    3,2  
   9 8F32          LDX   5  JURGE(3) 
   9 8FGL          LDX   7  JMISC(3) 
   7 8G2=          SLC   7  1
  18 8GFW          ANDN  7  1                   [X7=0 FOR MOP,1 FOR BACK 
  11 8G_G          SPRIVJUM JSISSUE,3,XSIS   
  11 8HF6          SPRIVJUM JSSTART,3,XSSTA  
   7 8HYQ          SMO      2
   9 8JDB          LDCH  0  AWORK4   
  16 8JY2          STO   0  EXT+1(2)        [SET NO. OF OPTIONS  
  17 8KCL          LDX   3  ACOMMUNE9(2)     [ADDRESS OF TEMPQ BLOCK 
   8 8KX=          LDCT  0  #001 
  15 8LBW          ORS   0  HLSYONQ(3)     [SET REQUEST BIT  
   7 8LWG          SMO      7
   9 8MB6          LDX   3  STYLE(1) 
   9 8MTQ          LDX   6  JOBNO(2) 
  10 8N*B          LDX   0  JOBEVENTS(2) 
  17 8NT2          BNG   0  XB1             [J IF BEING BROKEN IN ON 
  11 8P#L          LONGSET  0(3),(GEOERR),6  
   9 8PS=    WAIT  LDX   7  AWORK3(2)
   7 8Q?W          STO   5  4
   9 8QRG          NGX   5  AWORK1(2)
  10 8R?6          LDX   3  CONTEXT(2)   
   7 8RQQ          ANDN  3  4
   7 8S=B          SRL   3  2
   4 8SQ2    XK4 
  13 8T9L          LONGSTOP XBREAK,SPUR,,,BITS22LS   
   5 8TP=    XBREAK  
   9 8W8W          STO   7  AWORK3(2)
   7 8WNG          NGX   5  5
  18 8X86          BXE   5  AWORK1(2),WKOK       [DIFFERENT IF CONVERTING
   8 8XMQ          BZE   5  STWK 
   9 8Y7B          LDN   5  EVSTOPPED
   9 8YM2    STWK  STO   5  AWORK1(2)
   7 8_6L    WKOK  LDX   5  4
   4 8_L=    XB1 
   8 925W          FJOCA    3,2  
   9 92KG          LDX   4  JMESS(3) 
   8 9356          BNZ   4  SPOK 
   9 93JQ          LDX   4  JURGE(3) 
  17 944B          BXE   4  5,NEITHER              [J IF NO CHANGE   
  18 94J2          BNG   5  XALR                [J IF JOB WAS ALREADY F/S
   7 953L          LDN   6  1
  18 95H=          BNG   4  XBOTH               [BOTH MONOUTS REQUIRED   
   7 962W    XALR  LDCH  0  4
   7 96GG          LDCH  6  5
   7 9726          SBX   6  0
  17 97FQ          BZE   6  NEITHER             [NO MONOUT REQUIRED  
   7 97_B          LDN   6  0
   5 98F2    XBOTH   
   9 9*SW          CALL  7  MONOUTS  
   5 9B#G    NEITHER 
  15 9BS6          LDX   5  EXT+1(2)       [OPTION REQUIRED  
  10 9C?Q          LDX   0  JOBEVENTS(2) 
  16 9CRB          BNG   0  XBRK1               [J IF BREAK IN   
   8 9D?2          BRN      UP5  
   4 9DQL    SPUR
   9 9F==          STO   7  AWORK3(2)
   7 9FPW          NGX   5  5
  11 9G9G          BXE   5  AWORK1(2),SPOK   
   8 9GP6          BZE   5  SPOK1
   9 9H8Q          LDN   5  EVSTOPPED
   9 9HNB    SPOK1 STO   5  AWORK1(2)
   4 9J82    SPOK
   7 9JML          LDX   5  4
   8 9K7=          FJOCA    3,2  
   9 9KLW          LDX   4  JMESS(3) 
  19 9L6G          BZE   4  WAITB               [RE-ENTER WAIT IF NO MESSAGE 
   9 9LL6          STOZ     JMESS(3) 
  10 9M5Q          HUNTW    3,GMON,HLSOUT
  15 9MKB          BNG   3  WAITB1              [JIF NO BLOCK
  16 9N52          LDX   4  A1+1(3)             [PICK UP MESS NO 
   5 9NJL    WAITB1  
   9 9P4=          CALL  7  XHLSOUT  
  10 9PHW    WAITB LDX   0  JOBEVENTS(2) 
  15 9Q3G          BNG   0  XB1               [J IF BREAK IN 
   8 9QH6          BRN      WAIT 
  10 9R2Q    UP1B  LDX   0  JOBEVENTS(2) 
   8 9RGB          BNG   0  XBRK1
   8 9S22          BRN      UP1  
   5 9SFL    XIGNORE 
  10 9S_=          VFREEW   ADATA,ASUP   
   8 9TDW          LDX   1  FX1  
   8 9TYG          BRN      UP1  
   7 9WD6    XBRK1 LONGOFF   
   8 9WXQ          LDN   7  #10  
   9 9XCB          LDX   4  JOBNO(2) 
   8 9XX2          TEMPQ    7,4  
   8 9YBL          BRN      UP   
   5 9YW=    XSSTA   
   7 9_*W          LDX   3  7
   4 9_TG    XSST
  10 =2*6          LDX   0  CONTEXT(2)   
   8 =2SQ          ANDN  0  #4000
  14 =3#B          BNZ   0  UP1       [J IF ALREADY FS   
   9 =3S2          BRN      MAKEFS   
  10 =4?L    XSIS  LDX   0  CONTEXT(2)   
   8 =4R=          ANDN  0  #4000
  14 =5=W          BNZ   0  UP1       [J IF ALREADY FS   
   9 =5QG          BRN      MAKEFS1  
   5 =6=6    MAKEFS  
   4 =6PQ    MKFS
   7 =B46          LDN   0  1
  10 =BHQ          ADS   0  HLSCOUNT(3)  
   5 =C3B    MAKEFS1 
   8 =CH2          FJOCA    3,2  
   8 =D2L          LDX   0  GSIGN
  18 =DG=          ORS   0  JURGE(3)            [SET F/SBIT IN JOB BLOCK 
   8 =D_W          LDN   0  #6000
  18 =FFG          ERS   0  CONTEXT(2)          [FLIP CONTEXTS IN CPAT   
   4 =F_6    #   
   5 =GDQ    MAKEFS2 
   9 =M*2          OUTPAR   TIMENOW  
   9 =MSL          MONOUT   JSTART   
   8 =N#=          BRN      UP1  
   4 =NRW    XPRE
   4 =YKW    #   
   4 =_5G    XK8 
   8 =_K6          FJOCA    1,2  
  11 ?24Q          SPRIVJUM JSISSUE,1,XSIS   
  11 ?2JB          SPRIVJUM JSSTART,1,XSST   
   7 ?342          LDN   5  0
   9 ?3HL    XPRE1 LDX   0  FINISH   
  15 ?43=          BNG   0  XFIN         [J IF FINISH ISSUED 
  10 ?F96          LDX   0  HLSCOUNT(3)  
  18 ?FNQ          BXL   0  IMOPLIMIT(3),MKFS   [MAKE F/S IF CONT<LIMIT  
   8 ?H7L          FJOCA    1,2  
  20 ?HM=          JBS      MKFS,1,JBEXPREQ         [J IF JOB HAS BEEN EXPRESSED 
  19 ?J6W          BNZ   5  NMESS               [SKIP MESSAGE IF ALREADY O/P 
   8 ?JLG          MONOUT   JWAIT
   7 ?K66          LDN   5  1
   5 ?KKQ    NMESS   
   8 ?L5B          BZE   3  MOPLW
   9 ?LK2          LONGWAIT XBR,#32  
   8 ?M4L          BRN      XPRE1
   9 ?MJ=    MOPLW LONGWAIT XBR,#31  
   8 ?N3W          BRN      XPRE1
   4 ?NHG    XBR 
   7 ?P36    UP    LDN   5  0
   8 ?PGQ          BRN      UPX  
   7 ?Q2B    UP1   LDN   5  1
   8 ?QG2          BRN      UPX  
   7 ?Q_L    UP5   ADN   5  1
   4 ?RF=    UPX 
   9 ?Y*G          LDX   0  AWORK1(2)
   7 ?YT6          SLL   0  1
   7 ?_#Q          SMO      0
   9 ?_SB          LDX   4  TABLE(1) 
   7 #2#2          SLL   4  7
  16 #2RL          BPZ   4  UPY       [J IF NOT SAVEABLE ENTRY   
   9 #3?=          LDX   3  AWORK2(2)
   9 #3QW          LDN   0  GEORGE   
   7 #4=G          SRC   0  6
   9 #4Q6          ORX   0  GVERSION 
  17 #59Q          BXE   0  AWORK3(2),NOCONV       [J IF SAME GEORGE 
   7 #5PB          LDN   6  0
   7 #692    XLOOK SMO      6
  10 #6NL          LDX   7  XCONVTAB(1)  
   9 #78=          BNG   7  (GEOERR) 
  11 #7MW          BXE   7  AWORK1(2),THIS   
   7 #87G          ADN   6  2
   8 #8M6          BRN      XLOOK
   7 #96Q    THIS  SMO      6
  10 #9LB          LDX   3  XCONVTAB+1(1)
   5 #=62    NOCONV  
   8 #=KL          ACROSSVAR  3  
   7 #?5=    UPY   SMO      5
   7 #?JW          UPPLUS   0
   4 ##4G    #   
   4 ##J6    XFIN
   8 #*3Q          LDX   1  FX1  
  19 #*HB          BBS      14,EXT+11(2),Z3     [J IF WE FN HAS BEEN ISSUED  
  14 #B32          BZE   3  Z2                  [J IF MOP
   9 #BGL          LDX   0  ZFNAB(1) 
  17 #C2=          STO   0  JOBEVENTS(2)        [FINISH/ABANDON BITS 
   8 #CFW    Z2    MONOUT   HNOFS
  16 #C_G          BNZ   3  XBR                 [ JUMP IF NOT MOP
   9 #DF6    ZWAIT LONGWAIT XBR,#31  
   8 #DYQ          BRN      XPRE1
   9 #FDB    Z3    LDX   7  STOP(1)  
  16 #FY2          STO   7  JOBEVENTS(2)        [WE/FINISH BITS  
   9 #GCL          LDX   7  MASKWF(1)
  16 #GX=          ANDS  7  EXT+11(2)           [CLEAR WE/FN BIT 
   8 #HBW          BRN      UP1  
   4 #HWG    #   
  15 #JB6    #     ROUTINE TO PRODUCE FULLY ST'ED MESSAGES   
   4 #JTQ    XK2 
   8 #K*B          FJOCA    3,2  
   9 #KT2          LDXC  4  JURGE(3) 
   7 #L#L          LDN   6  0
   9 #LS=          CALL  7  MONOUTS  
   5 #M?W          UP
   4 #MRG    #   
  12 #N?6    #     ROUTINE TO OUTPUT HLS MESSAGE 
   4 #NQQ    XK3 
  10 #P=B          MHUNT    3,GMON,HLSOUT
   8 #PQ2          LDXC  4  A1(3)
  16 #Q9L          BCC      XK3OUT         [J IF MESSAGE GIVEN   
   7 #QP=          FREECORE 3
   5 #R8W    XK3OUT  
  16 #RNG          LDX   4  A1+1(3)        [PICK UP MESSAGE NO   
   9 #S86          CALL  7  XHLSOUTW 
   5 #SMQ          UP
   4 #T7B    #   
  16 #TM2    #     THIS ENTRY POINT IMPLEMENTS THE HLSXONCH MACRO
   4 #W6L    XK5 
   9 #WL=          LDN   0  EVXONLINE
  10 #X5W          STO   0  ACOMMUNE1(2) 
   9 #XKG          LDX   0  JOBNO(2) 
  10 #Y56          STO   0  ACOMMUNE2(2) 
   8 #YJQ          BRN      XK1  
   5 #_4B    XONCHECK
  10 #_J2          ACROSS   HLSOCCUR,1   
   4 *23L    #   
   4 *MH6    #END
   6 ____ ...222725020002
