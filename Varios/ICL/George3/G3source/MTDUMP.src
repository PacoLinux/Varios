  13 227P ...      SEG   MTDUMP,860,FILESTORE,INCDUMP
   4 22*D ...#   
  10 22H7 ...      SEGENTRY K1MTDUMP,X1  
  10 22NW ...      SEGENTRY K2MTDUMP,X2  
  10 22WK ...      SEGENTRY K3MTDUMP,X3  
  10 234# ...      SEGENTRY K4MTDUMP,X4  
   4 23=3 ...#   
  19 23CQ ...#  THIS VERSION OF MTDUMP IS INTENDED FOR USE WITH THE PARALLEL 
   9 23KF ...#  VERSION OF DUMPER.   
   4 23R8 ...#   
  20 23YX ...#  ENTRY 1 IS USED WHEN A DUMP TAPE ACTIVITY FINISHES POSITIONING   
  20 246L ...#  ITS TAPE IN RESPONSE TO A DUMPER REQUEST. IT INITIALISES THE DTA.
   4 24#* ...#   
  21 24G4 ...#  ENTRY 2 IS ENTERED BY THE DUMPER CPAT WHEN A NONAUTONOMOUS TRANSFER  
  20 24MR ...#  IS TO BE CARRIED OUT. ON ENTRY ACOMMUNE1=MODE, 2=COUNT, 3=CTSA.  
   4 24TG ...#   
  21 2539 ...#  ENTRY 3 IS AS FOR ENTRY 2 EXCEPT THAT THE TRANSFER IS TO BE AUTONOMOU
   4 258Y ...#   
  20 25BM ...#  ENTRY 4 IS WHAT THE K2+3 ENTRIES LEAVE THE DTAS LINKSET TO. IT   
  20 25JB ...#  INFORMS THE CPAT OF THE END OF TRANSFER, AND SETS BITS 0-1 OF THE
  21 25Q5 ...#  INCACT WORD IN THE SELECTED FINC TO INDICATE THE OCCURRENCE OF FAILS 
  11 25XS ...#  AND EOT OR TM RESPECTIVELY   
   4 265H ...#   
   5 26?= ...X4ENT   
   9 26D_ ...      PAIR     MTDUMP,4 
   4 26LN ...#   
  20 26SC ...#  THE FOLLOWING SUBROUTINE SETS X3-> CURRENTLY SELECTED FINC BLOCK.
  14 2726 ...#  ON ENTRY X3->BLOCK FROM WHICH TO SEARCH. 
   5 277T ...XLOCFINC
   8 27*J ...      SMO      FX2  
   9 27H? ...      STO   0  ACOMMUNE4
  10 27P2 ...      HUNT2    3,FILE,FINC  
  11 27WP ...      JBC      NPLL,,BFILEINC   
  10 284D ...      HUNT2    3,FILE,FINC  
   4 28=7 ...NPLL
   8 28CW ...      SMO      FX2  
   9 28KK ...      LDX   0  ACOMMUNE4
   7 28R# ...      EXIT  0  0
   4 28_3 ...#   
   4 296Q ...#   
   4 29#F ...X1  
  20 29G8 ...      STOZ     AWORK1(2)           [MARKER WORD TO REMEMBER FAILS   
  14 29K3 ...      LONGON   FGOTWAIT    [ WAKE UP DUMPER 
   6 29MX ...SCOOR COOR1 
  21 29TL ...      GEOERR   BRIEFPM,DTA FPUT    [DONT EXPECT TO BE WOKEN FROM THIS   
  14 2=3* ...                                   [COOR.   
   4 2=94 ...#   
   4 2=BR ...#   
   4 2=JG ...X4  
   9 2=Q9 ...#SKI  K6MTDUMP>499-499  
   4 2=XY ...(   
   4 2?5M ...#   
  20 2??B ...#  THIS CODE IS USEFUL FOR TESTING. IT SIMULATES EOT IF THE FIRST   
  21 2?F5 ...#  ENT BLOCK ENCOUNTERED IN THE ACT CHAIN AFTER THIS ACT IS FOR A FILE  
  21 2?LS ...#  'ZZZZ22220000' (NOTE THAT THIS ALWAYS COMES LAST IN ANY DIRECTORY.   
   4 2?SH ...#   
   7 2#2= ...      LDX   3  2
  11 2#7_ ...      FINDACTFB 3,,CPAT,,,FPTR  
  10 2#*N ...      HUNT2OPT 3,FILE,ENT   
   8 2#HC ...      BNG   3  NOPE 
   9 2#P6 ...      LDX   0  XEOT(1)  
  10 2#WT ...      BXU   0  ELOC1(3),NOPE
   9 2*4J ...      LDX   0  XEOT+1(1)
  10 2*=? ...      BXU   0  ELOC2(3),NOPE
   9 2*D2 ...      LDX   0  XEOT+2(1)
  10 2*KP ...      BXU   0  ELOC3(3),NOPE
   9 2*RD ...      BS       2,BRWEOT 
   8 2*_7 ...      BRN      NOPE 
  11 2B6W ...XEOT           12HZZZZ22220000  
   4 2B#K ...NOPE
   4 2BG# ...)   
   8 2BN=          LDCT  6  #060 
   9 2C7W          ANDX  6  CPRW1(2) 
  21 2CMG          SLL   6  3                   [GET FAIL AND EOT BITS FOR THIS WRITE
  15 2C_* ...      STO   6  AWORK1(2)           [KEEP REPLY  
  21 2F6B          FINDACTFB 3,2,CPAT,,,FPTR    [FIND DUMPER'S ACT. (1ST CPAT ALONG C
  17 2FL2          LDX   4  3                   [X4-> DUMPER'S ACT.  
  18 2G5L ...      CALL  0  XLOCFINC            [X3-> DUMPER'S FINC BLOCK
  21 2GK=          ORS   6  INCACT(3)           [TRANSFER BITS 3-4 OF REPLY TO BITS  
  16 2H4W                                       [B 0-1 OF INCACT 
  19 2HJG          LDEX  0  INCACT(3)           [ COUNT OF CURRENTLY ACTIVE  
  20 2J46                                       [DUMPER PROCESSES (DUMPER+DTA'S) 
  15 2JHQ          SBN   0  1                   [REDUCE COUNT
  15 2K3B          DEX   0  INCACT(3)           [PUT IT BACK 
  17 2KH2 ...      BNZ   0  SLEEP               [J IF NOT LAST DTA   
  18 2L2L                                       [OR DUMPER STILL GOING   
   7 2LG=          LDX   2  4
  16 2L_W          FPUT                         [WAKE UP DUMPER  
   5 2MFG ...SLEEP   
  16 2M_6          UNLOCK   2                   [UNLOCK THIS DTA 
   8 2NDQ          BRN      SCOOR
   4 2NYB ...#   
   4 2PD2 ...#   
   4 2PXL ...X2  
  17 2QC= ...      STOZ     AWORK1(2)           [SHOW NONAUT ENTRY   
   5 2QWW ...PERIS   
   8 2RBG ...      LDX   3  FX2  
  14 2RW6 ...      CALL  0  XLOCFINC            [X3->FINC
   7 2S*Q ...      LDN   0  1
  20 2STB ...      ADS   0  INCPERIS(3)           [UPDATE COUNT OF ISSUED PERIS  
  10 2T*2 ...      LDX   5  ACOMMUNE1(2) 
  10 2TSL ...      LDX   6  ACOMMUNE2(2) 
  10 2W#= ...      LDX   7  ACOMMUNE3(2) 
  15 2WRW ...      LDX   4  INCCOPIES(3)        [NO OF TAPES 
  19 2X?G ...      SLL   4  1                   [LENGTH OF WORDS IN TAPELIST 
   7 2XR6 ...      LDN   0  1
  20 2Y=Q ...      DEX   0  INCACT(3)           [SET 1 IN COUNT OF RUNNING ACTS  
  20 2YQB ...                                   [FOR DUMPER CPAT, IN CASE DTAS   
  20 2_=2 ...                                   [FINISH BEFORE WE GO TO  SLEEP   
   5 2_PL ...NEXTACT 
  13 329= ...      FINDACTFB 2,,ACTDOC,ADUMPT,,BPTR  
  20 32NW ...      LDX   0  ADTSN(2)            [PICK UP TSN OF NEXT BLOCK ALONG 
  18 338G ...      SMO      4                   [J IF NOT DTA ON OUR LIST
  14 33N6 ...      BXU   0  FGETSER-FGETLEN(3),NEXTACT   
  20 347Q ...      LDX   0  AWORK1(2)           [IGNORE IT IF ITS A FAILED TAPE  
   9 34MB ...      BNG   0  YFAILED  
   7 3572 ...      LDN   0  1
  19 35LL ...      ADS   0  INCACT(3)           [INCREMENT RUNNING ACT COUNT 
  19 366= ...      CHANGEACT 2                  [CHANGE TO DTA TO ISSUE PERI 
  21 36B4 ...      LOCK     2                   [LOCK DOWN THIS DTA. CANT USE LOCKC  
  20 36KW ...                                   [IN THESE CIRCS UNTIL CENT BUG   
  15 36TN ...                                   [2231 FIXED  
  17 375G ...      LDX   1  CPPTR(2)            [X1-> DEVLIST ENTRY  
   9 37K6 ...      DCA   1,STO,5,MODE
  10 384Q ...      DCA   1,STO,6,COUNT   
  16 38JB ...      DCA   1,STO,7,CTSA           [DETAILS SET UP  
  15 3942 ...      GPERI    ,2                  [ISSUE PERI  
   8 39HL ...      LDX   1  FX1  
  20 3=3= ...      LINKSET  2,X4ENT(1)           [ENSURE ACT WAKES UP ON X4 ENTRY
  17 3=GW ...      FINDACTFB 2,,CPAT,,,FPTR      [LOCATE DUMPER CPAT 
  17 3?2G ...      CHANGEACT 2                  [CHANGE BACK TO IT   
   5 3?G6 ...YFAILED 
   8 3?_Q ...      LDX   2  FX2  
   7 3#FB ...      LDX   3  2
  14 3#_2 ...      CALL  0  XLOCFINC            [X3->FINC
  20 3*DL ...      SBN   4  FGETLEN             [DECR NO OF WORDS IN TSN LIST TO 
  14 3*Y= ...                                   [LOOK AT 
  19 3BCW ...      BNZ   4  NEXTACT             [CARRY ON WITH LOOP IF MORE  
   9 3BXG ...      LDX   0  AWORK1(2)
  18 3CC6 ...      BNZ   0  UP                  [GO UP IN NONAUT TRANS   
  19 3CWQ ...      LDEX   0  INCACT(3)         [DECR COUNT OF RUNNING ACTS   
   7 3DBB ...      SBN   0  1
   9 3DW2 ...      DEX   0  INCACT(3)
   8 3F*L ...      BZE   0  UP   
  18 3FT= ...      COOR1                        [SLEEP TILL DTAS WAKE US 
   5 3G#W ...UP    UP
   4 3GSG ...#   
   4 3H#6 ...#   
   4 3HRQ ...#   
   4 3J?B ...X3  
   7 3JR2 ...      LDN   0  1
   9 3K=L ...      STO   0  AWORK1(2)
   8 3KQ= ...      BRN      PERIS
   4 3L9W ...#   
   4 3LPG ...#   
  10 3M96 ...      MENDAREA 20,K99MTDUMP 
   4 3MNQ ...#   
   4 3N8B ...#END
   8 ____ ...01532407000300000000
