  18 2256 ...      SEG      CONNECT,865,SECTION CENT,,G3UGSI,G551,G400   
   4 228= ...[   
  15 22?B ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  13 22BG ...[     THIS EXCLUDES CODE UNDER #SKI G551
  14 22FL ...[                    AND UNDER #SKI G3UGSI  
   4 22JQ ...[   
   6 22KR ...#UNS G400   
  15 22LS ...#     THIS CHAPTER IS MODIFIED FOR G3PLUS-IH MK2
   7 22MW ...#OPT G551 = 0   
   6 22R2 ...#SKI G551&1 
  21 22W6 ...# WITH G3UG EDIT M551 (SELECTIVE PASSWORD CHECKS FOR CENTRAL AND REMOTE)
   4 22_=    #   
   7 232_ ...#OPT G3UGSI=0   
   4 234N ...[   
   6 236C ...#SKI  G3UGSI
   4 2386 ...(   
  16 239T ...# MACRO TO PREVENT M551 AND SECURITY INTERFACE CLASH
   7 23?J ...      SINM551   
   4 23*? ...)   
   4 23C2 ...[   
  18 23DW          SEGENTRY K1CONNECT,XK1       [CONNECT COMMAND ENTRY   
  20 23YG          SEGENTRY K2CONNECT,XK2       [IMPLICIT CONNECT FROM DISCONNECT
  13 24*R ...      FSHENTRY K3CONNECT,RETRY,,RETRY   
  12 24R4 ...      FSHENTRY K4CONNECT,,XK4,XK4   
  12 258* ...      FSHENTRY K5CONNECT,XK5,,XK5   
  19 25KL ...      FSHENTRY K7CONNECT,,XK7,XK7        [ENTRY FOR LIMBO CHECK 
   4 262X ...[   
   7 26BL    #OPT  WELLTEST=0
   4 26W=    #   
  10 285#    PCON              #700000   
   7 28*6    STRING      +1  
   7 28SQ                4H: 
   4 2#PQ    #   
  21 2*9B    #     THIS SEGMENT INPLEMENTS THE CONNECT COMMAND, WHICH BREAKS IN ON A 
  21 2*P2    #     SPECIFIED OFFLINE JOB,AND CONVERTS IT TO AN ONLINE JOB ATTACHED TO
  18 2B8L    #     CONSOLE, AND TERMINATING THE CURRENT MOP ACTIVITY .   
   4 2BN=    #   
  21 2C7W    #     IN THE COMMENT CONTAINED IN THIS SEGMENT, 'Y' IS THE JOB WHICH HAS
  18 2CMG    #     ISSUED THE CONNECT, AND 'Z' IS THE JOB BEING CONNECTED
   4 2D76    #   
   9 2D87    [     USE OF AWORK WORDS
   4 2D98    [   
  17 2D=9    [     ON ENTRY,AWORK1 HOLDS CONTEXT WORD (FROM VERBCLAS)
  17 2D?=    [     AWORK1-3 HOLD USER NAME DURING JOB & HLSQ SEARCHES
  14 2D#?    [     AWORK4 USED FOR VARIOUS INDICATORS:   
  18 2D*#    [       B6-8  SET RESPECTIVELY IF CONNECT IS RESTRICTED BY  
  17 2DB*    [            BY CONTEXT PRIVILEGES. (FOR NO-USER CASE)  
  15 2DCB    [       B17 - NUMBER   - SET IF JOB NO. GIVEN   
  15 2DDC    [       B18 - XADDJOBJ - SET IF ADDJOBJ OBEYED  
  15 2DFD    [       B19 - XCHECKPW - SET IF CHEKPASWD OBEYED
  15 2DGF    [       B20 - XADDJOB  - SET IF ADDJOB OBEYED   
  19 2DHG    [       B21 - SRCALL   - SET IF XADDCHEK S/R NEEDN'T BE REOBEYED
  20 2DJH    [       B22 - MESSOP   - SET IF MESSAGE ALREADY O/P IF WAIT REQ'D   
  16 2DKJ    [       B23 - NOTDC    - SET IF CN, AS OPPOSED TO DC
   4 2DLQ    [   
  15 2F6B    [ SWITCHES FOR KEEPING TRACK OF JOBCOUNTS ETC   
  17 2FDR          BITDEFS AWORK4,17,NUMBER,XADDJOBJ,XCHECKPW,XADDJOB
  14 2FR8          BITDEFS AWORK4,21,SRCALL,MESSOP,NOTDC 
  11 2G5L    [ SUBROUTINE TO KEEP JOBCONTS OK
  12 2GK=    TIDY  JMBAC   (5),2,XADDJOB,XADDJOBJ
   8 2GT4          SBX   5  FX1  
  11 2H4W          JBCC   NOADDJOB,2,XADDJOB 
  15 2HJG          SUBJOB                      [UNDO ADDJOB  
   8 2HS#          ADX   5  FX1  
   7 2J46          EXIT  5  0
   5 2JHQ    NOADDJOB
  10 2K3B          BC       2,XADDJOBJ   
  15 2KH2          SUBJOBJ                      [UNDO ADJOBJ 
   8 2KQS          ADX   5  FX1  
   7 2L2L          EXIT  5  0
   4 2LG=    [   
   5 2L_W    SEEKJBLK
  17 2MFG          CJOBLOCK 3                   [ LOOK FOR JOBBLOCK  
   7 2M_6          EXIT  5  0
   4 2NDQ    [   
   6 2NFT    SKJOBYNUM   
  13 2NGY    [     SUBROUTINE TO FIND JOB BLOCK OF Z 
   9 2NJ3    [     AWORK1= JOB NUMBER
   7 2NK6    [     X7 IS LINK
  10 2NL9    [     EXIT 0 IF NOT FOUND   
  13 2NM#    [     EXIT 1 IF FOUND,WITH X3= POINTER  
  11 2NNC    [     ON ENTRY, EXPECTS X2=FX2  
  10 2NPG          JOBLOCK  AWORK1(2),3  
   8 2NQK          BNG   3  SKJ10
   7 2NRN          EXIT  7  1
   5 2NSR    SKJ10   
   7 2NTW          EXIT  7  0
   4 2NW_    [   
   4 2NYB    [   
   4 2PD2    #   
  21 2PXL    #     ENTRY FOR DISCONNECT WITH IMPALICITY CONNE.T ENTRY IS MADE FROM   
  17 2QC=    #     LOGIN WHEN THE JOB NAME IS FOUND TO BE NOT UNIQUE 
   4 2QWW    #   
   4 2RBG    XK2 
   9 2YQB          STOZ     AWORK4(2)
   9 2_=2          BRN      XSTART   
   4 2_PL    XK1 
   9 329=          STOZ     AWORK4(2)
   9 32NW          BS       2,NOTDC  
   9 338G          LDX   4  CPLEV(2) 
  20 33N6          BNZ   4  NOTCON              [ERROR NOT DIRECTLY FROM CONSOLE 
  15 33S3          LDX   4  AWORK1(2)          [CONTEXT WORD 
  20 33XY          ANDX  4  ICONTEXT           [MASK OUT CONTEXT PRIVS NOT I USE 
  19 343T          ORS   4  AWORK4(2)          [SET RELEVANT BITS IN AWORK4  
   4 347Q    [   
   7 3DW2          LDN   7  1
  10 3F*L          ANDX  7  CONTEXT(2)   
  18 3FT=          BNZ   7  USERA               [B. IF IN USER CONTEXT   
  16 3G#W          SPARABEG 1,STRING(1)         [GET USER NAME   
  20 3GSG          CHEKLFN2 NUMB,NLF           [CHECK FORMAT, IF MISSING GO NUMB 
  10 3H#6          LDX   6  JPARNUM(3)   
  21 3HRQ          SBN   6  3                   [CHECK ONE OF FIRST TWO PARAMETERS   
   8 3J?B          BPZ   6  NUSEN
  16 3JR2          LDX   6  ACES                [STORE USER NAME 
   9 3K=L          STO   6  AWORK1(2)
   9 3KQ=          STO   6  AWORK2(2)
   9 3L9W          STO   6  AWORK3(2)
   9 3LPG          LDN   4  APARA(3) 
   9 3M96          LDN   5  AWORK1(2)
   9 3MNQ          SMO      ANUM(3)  
   7 3N8B          MVCH  4  0
   8 3NN2          MFREE CPB,CUNI
   5 3P7L    USERA   
  15 3PM=          SPARUNAC                     [GET JOB NAME
  16 3Q6W          CHEKLFN2 NOJOBNA,NUMB20         [CHECK FORMAT 
   8 3QLG          BNZ   7  USER 
   4 3QQC    [   
  18 3QW#          LDX   5  AWORK1(2)           [COPY USERNME INTO X5,6,7
   9 3R29          LDX   6  AWORK2(2)
   9 3R66          LDX   7  AWORK3(2)
   4 3R=3    [   
   5 3R*Y    NOTUSER 
  12 3RFT    [     SET UP FABSNB WITH USER NAME  
  11 3RKQ          SETNCORE 4,3,FILE,FABSNB  
  19 3S5B          STO   5  A1+1(3)             [MOVE USERNAME INTO FABSNB   
   9 3SK2          STO   6  A1+2(3)  
   9 3T4L          STO   7  A1+3(3)  
   7 3TJ=          LDN   0  4
   8 3W3W          STO   0  A1(3)
  19 3WHG          OPENBUDO WRONGUN             [GET A FILE/ADICTENT BLOCK   
   6 3X36          CLOSE 
   8 3XGQ          BRN   RETRY   
   4 3XG_    [   
   5 3XH8    WRONGUN 
  13 3XHC    [     USERNAME NOT FOUND IN DICTIONARY  
  15 3XHL    [     IF NAME WAS INPUT BY USER, GO TO XCHEK.   
  17 3XHT    [     THIS WILL RESULT IN CHEKPASSWD GIVING AN ERROR.   
  11 3XJ4          JBC      XCHEK,2,NUMBER   
  19 3XJ?    [     OTHER CASE IS WHEN ONLY JOB NO. WAS INPUT, AND USERNAME   
  20 3XJG    [     WAS FOUND IN JOB BLOCK.  THIS MEANS THAT NAME HAS DISAPPEARED 
  15 3XJP    [     FROM DICTIONARY, SO FIND OUT WHAT HAPPEND 
  17 3XJY          CALL  7  SKJOBYNUM           [GET JOB BLOCK AGAIN 
  20 3XK7          BRN      XCHEK                [MISSING - USER HAS BEEN ERASED 
  16 3XKB          GEOERR   1,USERLOST          [OTHERWISE BREAK 
   4 3XKK    [   
   4 3XKS    [   
   5 3XL3    SHLSR   
  13 3XL=    [     SUBROUTINE TO SEARCH HLSQ FOR JOB 
  11 3XLF    [     WHOSE NUMBER IS IN AWORK1 
  13 3XLN    [     IF BLOCK NOT FOUND, GOES TO XNOJOB
  13 3XLX    [     EXIT 0 IF FOUND, WITH X3=POINTER  
   7 3XM6    [     X7 IS LINK
  11 3XM*    [     ON ENTRY, EXPECTS X2=FX2  
  12 3XMJ          SHLSQ    AWORK1(2),3,XNOJOB   
   7 3XMR          EXIT  7  0
   4 3XN2    [   
   4 3XN9    [   
   4 3XND    NUMB
   8 3XNM    [     JOB NO INPUT  
   9 3XNQ          MFREE    CPB,CUNI 
  16 3XNW          SPARAPAS                     [GET 1ST PARAM   
  10 3XNX          MHUNT    3,CPB,CUNI   
  21 3XNY          LDX   4  ANUM(3)              [J IF 1ST PARAM NULL OR NON-EXISTING
   9 3XN_          BNG   4  NOJOBNA  
   9 3XP2          BZE   4  NOJOBNA  
   5 3XP5    NUMB20  
   9 3XP#    [     CONVERT JOB NUMBER
   7 3XPH          CHNUMCOD 1
  18 3XPQ          TESTRPN2 OK,NUMERR           [J IF NOT VALID NUMBER   
  10 3XP_          MHUNT    3,CPB,CUNI   
  10 3XQ8          LDX   4  JPARNUM(3)   
   7 3XQC          SBN   4  1
  17 3XQL          BNZ   4  NOJOBNA             [J IF NOT 1ST PARAM  
   4 3XQT    [   
  10 3XR4          LDX   4  ACOMMUNE1(2) 
  19 3XR?          STO   4  AWORK1(2)           [STORE JOB NUMBER IN AWORK1  
  19 3XRG          BS       2,NUMBER            [AND SET NUMBER BIT IN AWORK4
   4 3XRP    [   
  10 3XRY    [     NOW LOOK FOR JOB BLOCK
   9 3XS7          CALL  7  SKJOBYNUM
  16 3XSB          BRN      NONUMB              [J IF NOT FOUND  
  16 3XSK          LDX   5  JUSER(3)            [PICK UP USERNAME
  10 3XSS          LDX   6  JUSER+1(3)   
  10 3XT3          LDX   7  JUSER+2(3)   
   4 3XT=    [   
   5 3XTF    NUMFOUND
   7 3XTN          LDN   4  1
  10 3XTX          ANDX  4  CONTEXT(2)   
  17 3XW6          BNZ   4  USER                [J IF IN USER CONTEXT
   9 3XW*          BRN      NOTUSER  
   4 3XWJ    [   
   5 3XWR    NONUMB  
  11 3XX2    [     JOB NOT FOUND IN JOB QUEUE
  17 3XX9          BSON     HLSBIT,XNOJOB       [J IF HLS NOT IN USE 
  15 3XXD          CALL  7  SHLSR               [LOOK IN HLSQ
  16 3XXM          LDX   5  HLSUNAM(3)          [PICK UP USERNAME
  10 3XXW          LDX   6  HLSUNAM+1(3) 
  10 3XY5          LDX   7  HLSUNAM+2(3) 
   9 3XY#          BRN      NUMFOUND 
   4 3XYH    [   
   4 3XYQ    [   
   5 3Y2B    XOUTPACK
   8 3YG2          SBX   7  FX1  
   8 3Y_L          OUTPACKX 3,4,5
   8 3_F=          ADX   7  FX1  
   7 3_YW          EXIT  7  0
   4 428N    [   
   5 42DG    XADDCHEK
  10 42Y6          JBSS   (7),2,SRCALL   
  10 43CQ          JBC      XDC,2,NOTDC  
  20 43XB          LDX   0  CONTEXT(2)          [DON'T PROCEED IN USER CONTEXT   
   7 44C2          ANDN  0  2
   8 44WL          BZE   0  (7)  
  18 45B=          BPZ   4  XCHEK2              [LIMBO CALL DON'T ADDJOB 
  10 46T6          ADDJOB XDEAD,XBROKE   
   9 47#Q          BS       2,XADDJOB
   9 47SB          BRN      XCHEK2   
  15 48#2    XDC   BNG   4  (7)               [J IF NOT LIMBO
  19 48RL          ADDJOBJ   XDEAD,XBROKE          [ADD BACK JOB IF DC&LIMBO 
   9 49?=          BS      2,XADDJOBJ
   5 49QW    XCHEK2  
  10 4==G          JBS   RETRY,2,XCHECKPW
  19 4=G#          JBC   RETRY,2,NOTDC          [NO CHECK IF ALREADY MOP JOB 
   4 4=GS ...[   
   6 4=H# ...#SKI G551&1 
   4 4=HS ...(   
  11 4=J# ...[     MEND EXCHANGE SCHEME CODE 
   7 4=JS ...      LDN   7  1
  10 4=K# ...      DOWN     INITJOB,20   
   9 4=KS ...      BRN      XBROKE   
   8 4=L# ...      BRN      MESSU
   8 4=LS ...      BRN      PWOK 
   8 4=M# ...      BRN      MESSU
   4 4=MS ...PWOK
   4 4=N# ...)   
   9 4=NS ...#SKI  G551_G3UGSI<1$1   
   4 4=P# ...(   
  19 4=Q6          HUNT  3,FILE,ADICTENT        [CHECK USERS PASSWORD UNLESS 
  16 4?9Q          SECHECK 3,RETRY,XCHEK        [SECURITY IS LOW 
   5 4?PB    XCHEK   
  11 4*8=          CHEKPASWRD XBROKE,MESSU   
   4 4*9D ...)   
   6 4*=L ...#SKI  G3UGSI
   4 4*?S ...(   
  16 4**2 ...[     THIS SECTION COMPILED IF USER GROUP SECURITY  
  11 4*B8 ...[     INTERFACE IS SWITCHED ON  
  11 4*CB ...      SINMAC2  XDEAD,XBROKE,5   
   8 4*DJ ...      BRN      XK31 
   5 4*FQ ...XCHEK   
  20 4*GY ...      BRN      MESSU               [ERROR IN USERNAME OR PASSWORD   
   4 4*J6 ...XK31
   4 4*K# ...)   
   4 4*LG ...[   
  10 4*MW          BS       2,XCHECKPW   
  16 4B7G          BRN      RETRY               [REFIND JOB BLOCK
   4 4B#N    [   
   4 4BFW    [   
   4 4BM6    USER
  20 4C6Q          CANAMONST    XDEAD           [ANALYSE MONITORING STYLE PARAMS 
   5 4CLB    XSTART  
  15 4CRJ          STO   5  ACOMMUNE4(2)        [PRESERVE X5 
  11 4CYQ    [     GET NAME OF PROPER USER   
   9 4D62          CALL  5  SEEKJBLK 
  17 4D*S          JBS      USERCHEK,2,NUMBER   [J IF JOB NO. INPUT  
   9 4DKL          LDN   5  JUSER(3) 
   9 4F5=          LDN   6  AWORK1(2)
  20 4FJW          MOVE  5  3                   [STICK USERNAME IN AWORK WORDS   
   8 4FL4          BRN      RETRY
   4 4FM=    [   
   5 4FND    USERCHEK
  18 4FPL    [     COMPARE PROPER USER NAME WITH THAT IN JOB BLOCK FOR Z 
  10 4FQS          LDX   5  ACOMMUNE4(2) 
   9 4FS2          TXU   5  JUSER(3) 
  10 4FT8          TXU   6  JUSER+1(3)   
  10 4FWB          TXU   7  JUSER+2(3)   
  17 4FXJ          BCC      RETRY               [J IF THEY ARE SAME  
   8 4FYQ          BRN      XUSER
   4 4F_Y    [   
   4 4G36    [   
   5 4G4G    RETRY   
  18 4N7S          JBS      RETNUM,2,NUMBER     [J IF JOB NO. WAS INPUT  
  10 4NCL          HUNT     1,CPB,CUNI   
  21 4NX=          LDX   4  ANUM(1)             [MOVE JOB NAME TO COMMUNE WORDS AND  
  15 4PBW          LDN   5  12                  [SPACEFILL   
   9 4PWG    RET1  LDCH  6  APARA(1) 
  10 4QB6          DCH   6  ACOMMUNE1(2) 
   7 4QTQ          BCHX  1  /
   7 4R*B          BCHX  2  /
   7 4RT2          SBN   5  1
   8 4S#L          BCT   4  RET1 
   8 4SS=          BZE   5  RET3 
   8 4T?W          LDN   6  #20  
  10 4TRG    RET2  DCH   6  ACOMMUNE1(2) 
   7 4W?6          BCHX  2  /
   8 4WQQ          BCT   5  RET2 
  15 4X=B    RET3  LDX   2  FX2                 [RESTORE X2  
  17 4XQ2    #                                  FIND JOB QUEUE ENTRY 
   8 4Y9L          LDN   3  BJOBQ
  17 4YP=    NEXTB LDX   3  FPTR(3)             [GET NEXT IN CHAIN   
  18 4_8W          TXU   3  CXJO                [CHECK FOR END OF JOBQ   
  18 4_NG          BCC      NOJOB               [J IF 'Z' NON-EXISTENT   
   7 55KG          LDN   7  3
   7 5656    NEXTW SMO      7
  10 56JQ          LDX   4  JNAME-1(3)   
   7 574B          SMO      7
  16 57J2          TXU   4  ACOMMUNE1-1(2)      [COMPARE JOBNAME 
  18 583L          BCS      NEXTB               [J THIS JOBLOCK NOT 'Z'  
  20 58H=          BCT   7  NEXTW               [COMPARE WHOLE OF JOB/USER NAME  
   7 592W          LDN   7  3
   7 59GG    NEXTX SMO      7
  10 5=26          LDX   4  JUSER-1(3)   
   7 5=FQ          SMO      7
  16 5=_B          TXU   4  AWORK1-1(2)         [COMPARE USERNAME
  15 5?F2          BCS      NEXTB               [BRN IF FAIL 
   8 5?YL          BCT   7  NEXTX
  15 5?_D          BRN      TESTJB              [JOB FOUND!  
   4 5#2=    [   
   5 5#34    RETNUM  
  15 5#3W    [     JOB NO. WAS INPUT, HENCE FIND JOB BLOCK   
   9 5#4N          CALL  7  SKJOBYNUM
  16 5#5G          BRN      NOJOB               [J IF NOT FOUND  
   4 5#6#    [   
   5 5#76    TESTJB  
  17 5#7Y          LDCH  4  JSOURCE1(2)         [GET SOURCE OF JOB Y 
   8 5#8Q          SBN   4  #74  
  15 5#9J          BNZ   4  TESTJB10            [J IF NOT CI 
  10 5#=B          MHUNTW   1,AMXOR,CILCB
  17 5#?8          LDX   4  CIJOBNUM(1)         [GET JOB NO OF CI JOB
   8 5#*L          LDX   1  FX1  
   9 5#*W          TXU   4  JOBNUM(3)
  17 5#B6          BCC      SAMEJOB             [J IF SAME AS JOB Y  
   4 5#BD    [   
   5 5#C=    TESTJB10
   7 5#D=          LDN   6  0
   9 5#XW          LDX   4  JMISC(3) 
  16 5*CG          BNG   4  XNOTLIMBO           [BACKGROUND JOB  
   7 5*X6          FCAJO 2,3 
   4 5B2=    [   
   7 5D3W          LIMBO 6,2 
   8 5D72          LDX   2  FX2  
  18 5D=6          BZE   6  ONLINE              [ERROR CN TO MOP JOB!!!! 
   6 5D*=    XNOTLIMBO   
  13 5DBM    [     CHECK CONTEXT PRIVS IF NECESSARY  
   8 5DC*          LDX   1  FX1  
   9 5DD4          LDX   7  AWORK4(2)
   9 5DFF          ANDX  7  PCON(1)  
  17 5DGW          BZE   7  NOTLIMB10          [J IF NOT NECESSARY   
  19 5DJ?          LDX   5  JPRIV(3)           [PRIVILEGE WORD IN JOB BLOCK  
   7 5DKN          ANDX  5  7
   7 5DM5          ERX   5  7
  20 5DNG          BNZ   5  NOPRIV             [J IF REQUIRED PRIV IS NOT HELD   
   4 5DPX    [   
   6 5DR#    NOTLIMB10   
  19 5MML          CALL  7  XADDCHEK            [CHECK JOBCOUNTS & PASSWORD  
  10 5P6G          INHIBITBRK   XBROKE   
  15 5PL6          BSON     HLSBIT,XNBSAVED     [J IF PRE-HLS
   7 5Q5Q          LDCT  4  1
   9 5QKB          ANDX  4  JMISC(3) 
  21 5R52          BZE   4  XNBSAVED            [TAKE NO ACTION IF NOT BEING SAVED   
   9 5RJL          LDX   4  JOBNUM(3)
   9 5S4=          SHLSQ    4,2,US2  
  12 5SHW          HLSAV 2,CLEAR,SAVING,HLSQ,US6 
  20 5T3G          LDCT  5  #020                [MARK TO REVERSE SAVING PROCESS  
  10 5TH6          ORS   5  HLSYONQ(2)   
  21 5W2Q    US7   COOR3    #34                 [WAIT FOR SAVING PROCESS TO BE REVERS
   9 5WGB          SHLSQ    4,2,US2  
  10 5X22          LDX   5  HLSYONQ(2)   
  15 5XFL          BNG   5  US6                 [J IF SAVED  
   7 5X_=          SLL   5  1
  18 5YDW          BNG   5  US7                 [J IF STILL BEING SAVED  
   5 5YYG    XNBSAVED
  18 5_D6          LDX   4  6                   [REMEMBER 'LIMBO' STATE  
  19 5_XQ          LDX   6  JOBNUM(3)           [& REMEMBER JOB NUMBER OF 'Z'
  20 66S2          DOWN     PROCONTX,13                 [STOP 'Z' (JOBNO IN X6)  
  18 67?L          BRN      NOTSTOPPED                  [J NOT STOPPABLE 
   8 67R=          BRN   XNOTCON 
  21 68=W    #                                  CLOSE AND O/P MONITORING FILE OF 'Y' 
   4 69PQ    [   
  17 6=9B    [     UPDATE JWELL AND CHANGE JLSTAT IN JOBLIST ENTRY...
   4 6=P2    [   
   9 6?8L          BNZ   4  WASLIMBO 
   9 6?N=          DOWN     DSCNCT,3 
   5 6#7W    WASLIMBO
   4 6#MG    [   
   4 6*76    [   
   9 6B6B          STO   6  AWORK3(2)
   7 6BL2          LDN   0  1
  10 6C5L          ANDX  0  CONTEXT(2)   
   8 6CK=          BZE   0  NOUSE
  10 6D4W          JBC      NOUSE,2,NOTDC
   8 6DJG          FILECOUNT 3   
   7 6F46          SBN   3  1
   9 6FHQ          BZE   3  NCLOSE   
   6 6G3B          CLOSE 
   5 6GH2    NCLOSE  
  21 6H2L          LDN   4  1                   [OUTPUT MAX. ONLINE BACKSTORE USED   
   9 6HG=          CALL  5  SEEKJBLK 
  15 6H_W          LDX   6  GUBS(3)             [GET GUBS/2  
   7 6JFG          SRA   6  1
  17 6J_6          LDN   3  6                   [SET PTR TO RESULT   
   9 6KDQ          LDN   5  JPDNUMA  
   9 6KYB          CALL  7  XOUTPACK 
   9 6LD2          MONOUT   GUBSOUT  
  13 6LXL          OUTPAR USERNAME,JOBNAME,TIMENOW   
   7 6MC=          OUTMILL   
  12 6MWW          LFEJCNT  AWORK1(2),AWORK2(2),6
   7 6NBG          LDN   4  1
  11 6NW6          MHUNT    3,ADATA,AREPORT  
   8 6P*Q          LDX   0  A1(3)
  16 6PTB          BZE   0  NOMFLF              [J IF CN ,NONE   
  19 6Q*2          ADS   4  AWORK1(2)           [ALLOW FOR MONITORING FILE   
   5 6QSL    NOMFLF  
   9 6R#=          LDN   3  AWORK1(2)
   9 6RRW          LDN   5  JPDNUMA  
   9 6S?G          CALL  7  XOUTPACK 
   9 6SR6          LDX   0  AWORK2(2)
   9 6T=Q          BZE   0  XNONEA   
   9 6TQB          LDN   3  AWORK2(2)
   9 6W=2          LDN   5  JPDNUMB  
   9 6WPL          CALL  7  XOUTPACK 
   8 6X9=          BRN      XAON 
   5 6XNW    XNONEA  
   8 6Y8G          OUTPAR SKIP   
   8 6YN6    XAON  BZE   6  XBON 
   7 6_7Q          LDN   3  6
   9 6_MB          LDN   5  JPDNUMC  
   9 7272          CALL  7  XOUTPACK 
   4 72LL    XBON
  10 736=          MONOUT   JOBT,,NONAUT 
   4 73KW    #   
   6 73MS ...#UNS G400   
   4 73PQ ...(   
  10 73RN ...      SEGENTRY K60CONNECT   
  15 73TL ...      BRN      NOHOOK         [HOOK NOT REQUIRED
  15 73XJ ...      DOWN     DOMONOP,4      [CALL JOBEND HOOK 
   5 73_G ...NOHOOK  
   4 743D ...)   
  13 745G    #              PROCESS MONITORING FILE  
  21 74K6          SEGENTRY K50CONNECT     [USED BY BILA MACRO (TO MEND IN/OUT BILA) 
   7 754Q          LOGANAL   
   4 75JB    #   
  21 7=DL          FILELOG                      [DELETE TEMPORARY FILES AND DIRECTORY
   9 7=Y=          CALL  5  SEEKJBLK 
  15 7?CW          LDX   6  CUBS(3)             [GET CUBS/2  
   7 7?XG          SRA   6  1
  21 7#C6          BZE   6  XCUBSOK             [CUBS SHOULD HAVE RETURNED TO ZERO   
  21 7#WQ          OUTPAR USERNAME,JOBNAME      [OUTPUT USERNAME & JOBNAME PARAMETERS
  17 7*BB          LDN   3  6                   [SET PTR TO RESULT   
   7 7*W2          LDN   4  1
   9 7B*L          LDN   5  JPDNUMA  
   9 7BT=          CALL  7  XOUTPACK 
   9 7C#W          MONOUT   CUBSWRONG
   5 7CSG    XCUBSOK 
   5 7D#6    NOUSE   
  10 7J96          ACROSS   CONNECTA,1   
   4 7JNQ    #   
   4 7K8B    #   
   5 7LM=    XBROKE  
  14 7M6W          CALL  5  TIDY        [KEEP COUNT OK   
   7 7R3W    NSJ   COMBRKIN  
  10 7T2B    PJXCONSOLE     +JXCONSOLE   
   9 7TG2    PJPARMIS       +JPARMIS 
   9 7T_L    PJJOBNA        +JJOBNA  
   9 7WF=    PJUSNA         +JUSNA   
   6 7WLD ...#SKI G551&1 
   9 7WRL ...PERNOUSER      +ERNOUSER
   9 7WYW    PIMPASSE       +IMPASSE 
   9 7XDG    PJXEXIST       +JXEXIST 
   9 7XY6    PHNOTCON       +HNOTCON 
   9 7YCQ    PJNOTOFF       +JNOTOFF 
   9 7YXB    PJNLFNF        +JNLFNF  
   9 7_C2    PHSAMLN        +HSAMLN  
   9 7_G6    PGENERR        +GENERR  
   9 7_K=    PHWSUSER        +HWSUSER
   9 7_NB    PCNSELF        +CNSELF  
   9 7_RG    PJINVJNO       +JINVJNO 
   9 7_T4    PANOPRIV       +ANOPRIV 
  19 7_WL    #                                      JOB 'Z' DOES NOT EXIST   
   5 82B=    NOJOB   
  10 82TW          BSON     HLSBIT,XNOJOB
  18 83*G    #     NO JOB BLOCK. MAY BE SAVED SO SEARCH FOR HLSQ BLOCK   
   4 83T6    #   
  10 844Y          JBS      SNUM,2,NUMBER
   8 84#Q          LDN   3  BHLSQ
   5 84SB    SHLSQ   
   9 85#2          LDX   3  FPTR(3)  
   8 85RL          TXU   3  CXHL 
  15 86?=          BCC      XNOJOB              [NOT IN HLSQ 
   7 86QW          LDN   7  3
   5 87=G    XNEXTW  
   7 87Q6          SMO      7
  10 889Q          LDX   4  HLSJNAM-1(3) 
   7 88PB          SMO      7
  11 8992          TXU   4  ACOMMUNE1-1(2)   
  18 89NL          BCS      SHLSQ               [J IF NOT CORRECT JOBNAME
   9 8=8=          BCT   7  XNEXTW   
   7 8=MW          LDN   7  3
   5 8?7G    XNEXTX  
   7 8?M6          SMO      7
  10 8#6Q          LDX   4  HLSUNAM-1(3) 
   7 8#LB          SMO      7
  10 8*62          TXU   4  AWORK1-1(2)  
  19 8*KL          BCS      SHLSQ               [J IF NOT CORRECT USER NAME  
   9 8B5=          BCT   7  XNEXTX   
  15 8B78          BRN      SFOUND              [JOB FOUND   
   4 8B96    [   
   4 8B?4    SNUM
  10 8B*2    [     SEARCH HLSQ BY JOB NO.
   8 8BBY          CALL  7  SHLSR
   4 8BDW    [   
   5 8BGS    SFOUND  
   7 8BJW          NGN   4  1
  19 8C4G          CALL  7  XADDCHEK            [CHECK JOBCOUNT & PASSWORD   
  10 8CJ6          LDX   4  HLSJONO(3)   
   7 8MB6    US6   UNSAVE   4
  17 8MTQ          BRN      XNOJOB              [UNSUCCESSFUL UNSAVE 
   8 8N*B          BRN      RETRY
   5 8NT2    XNOJOB  
   9 8P#L          JBS   XIST,2,NOTDC
  13 8PS=    #              FILE WITH SAME LOCAL NAME
   7 8Q?W          SMO   FX1 
   9 8QRG          LDX   7  PHSAMLN  
   7 8R?6          BRN   NGN 
   4 8RQQ    XIST
   8 8S=B          SMO      FX1  
   9 8SQ2          LDX   7  PJXEXIST 
   8 8T9L          BRN      NGN  
  14 8TP=    US2   GEOERR                       1,NOHLSQ 
   4 8W8W    #   
   6 8WNG    NOTSTOPPED  
   7 8X86          PERMITBRK 
  16 8XMQ    [TO COVER CASE OF CHANGE OF LIMBO STATE OVER WAIT   
  20 8Y7B    [WE ENSURE XADDCHEK WILL DO ADDJOB IF NECESSARY-THIS MEANS CLEARING 
  20 8YM2    [B21 &20 AND DOING SUBJOB IF ADDJOB DONE.THEN ON TRYING AGAIN @RETRY
  16 8_6L    [XADDCHEK WILL REPEAT ALL BUT CHEKPW AS BIT 19 LEFT 
   8 8_L=          CALL  5  TIDY 
   9 925W          BC       2,SRCALL 
  11 92KG          JBSS  XNOMESTP,2,MESSOP   
  18 944B          LONGWAIT XBROKE,#62,JCONWAIT,XNOMESTP   [WAIT  AWHILE 
   8 94J2          BRN      RETRY
   5 953L    XNOTCON 
   7 95H=          PERMITBRK 
   8 962W          SMO      FX1  
  16 96GG          LDX   7  PHNOTCON            [NOT CONNECTABLE 
   8 9726          BRN      NGN  
   5 97FQ    NOTCON  
  17 97_B          LDX   7  PJXCONSOLE(1)       [NOT FROM A CONSOLE  
   8 98F2          BRN      NGN  
   5 98YL    NOJOBNA 
  16 99D=          LDX   7  PJPARMIS(1)         [JOB NAME MISSING
  10 99XW          LDX   6  PJJOBNA(1)   
   8 9=CG          BRN      XERR 
   6 9=G5 ...#SKI G551&1 
   4 9=JN ...(   
  10 9=M? ...XCHEK LDX   7  PERNOUSER(1) 
   8 9=PW ...      BRN      NGN  
   4 9=SF ...)   
   5 9=X6    ONLINE  
   8 9?BQ          SMO      FX1  
  18 9?WB          LDX   7  PJNOTOFF            [JOB Z IS ALREADY ONLINE 
   8 9#B2          BRN      NGN  
  16 9#TL    NLF   LDX   7  PJNLFNF(1)          [NOT A LOCAL NAME
   8 9**=          BRN      NGN  
  17 9*SW    NUSEN LDX   7  PJPARMIS(1)         [USER NAME MISSING   
   9 9B#G          LDX   6  PJUSNA(1)
   8 9BS6          BRN      XERR 
   4 9BSY    [   
   5 9BTQ    NUMERR  
   9 9BWJ    [     INVALID JOB NUMBER
  10 9BXB          LDX   7  PGENERR(1)   
  10 9BY8          LDX   6  PJINVJNO(1)  
   8 9B_2          BRN      XERR 
   4 9B_S    [   
   5 9C2L    XUSER   
  16 9C3D    [     SPECIFIED JOB DOES NOT BELONG TO PROPER USER  
  10 9C4=          LDX   7  PHWSUSER(1)  
   8 9C54          BRN      NGN  
   4 9C5W    [   
   5 9C6N    SAMEJOB 
  13 9C7G    [     CI JOB TRYING TO CONNECT TO ITSELF
   9 9C8#          VFREE    CPB,CUNI 
  10 9C96          LDX   7  PCNSELF(1)   
   8 9C9Y          BRN      NGN  
   4 9C=Q    [   
   5 9C=X    NOPRIV  
  14 9C?4    [     USER HAS NOT GOT REQUIRED PRIVILEGE   
  10 9C?9          LDX   7  PANOPRIV(1)  
   8 9C?B          BRN      NGN  
   4 9C?H    [   
  17 9C?Q    MESSU LDX   7  PIMPASSE(1)         [INCORRECT PASSWORD  
   7 9CRB    NGN   NGN   6  1
   8 9D?2    XERR  ERRORX   7,6  
   5 9FPW    XDEAD   
   8 9G9G          CALL  5  TIDY 
   6 9L6G    NSJ1  ENDCOM
   4 =7P2    #END
   6 ____ ...343013330005
