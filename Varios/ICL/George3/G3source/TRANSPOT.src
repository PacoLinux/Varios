  17 22FL    #LIS           K0TRANSPOT>K0ALLGEO>K0GREATGEO>K0LIBRARY 
  12 22_=          SEG      TRANSPOT,65,N.R.BOULT
  12 23DW          SEGENTRY K1TRANSPOT,QK1TRANS  
  12 23YG          SEGENTRY K2TRANSPOT,QK2TRANS  
  21 24D6    #              THERE ARE 2 ENTRY POINTS TO THIS SEGMENT. WHIDH DEALS WIT
  21 24XQ    #           FINDING OUT ABOUT THE AVAILABILITY OF DECKS FOR LOADING A TA
  21 25CB    #     K1TRANSPOT IS THE ENTRY POINT FOR THEE SEGMENT IN THE TRANSPORTAS 
  21 25X2    #           CASE. X4 IS SET TO ZERO TO SIGNIFY THIS ENTRY POINT. THE NET
  21 26BL    #           OF THIS ENTRY IS A MESSAGE TO THE OPERATORS CONSOLE STATING 
  21 26W=    #           UNITS AVAILABLE ARE :- %A%B%C %C WHERE THE PARAMETERS ARE   
  21 27*W    #           GEOGRAPHICAL NUMBERS OF THE FOUR DECKS (OR LESS) THAT ARE NO
  21 27TG    #           ALLOCATED OR ENGAGED & WHICH HAVE THE LOWEST FAIL COUNT OUT 
  14 28*6    #           THE TOTAL OF DECKS AVAILABLE.   
  21 28SQ    #     K2TRANSPOT IS THE ENTRY FROM LIBREQ. IT WILL SIMILARLY PRODUCE AVA
  21 29#B    #           DECK INFORMATION TO BE INCLUDED IN THE LOAD MT MESSAGE. X4 I
  12 29S2    #           NON-ZERO FOR THIS ENTRY.
   8 2=?L    MTYPE          +5   
   8 2=R=    OCT            +8   
   8 2?=W    QUART          +4   
   8 2?QG    PENRZI         #201 
   5 2#=6    QK1TRANS
  19 2#PQ          LDN   4  0                   [ENTRY FOR TRANSPORTS COMMAND
   7 2*9B          SPARAPAS  
  10 2*P2          MHUNT    3,CPB,CUNI   
   9 2B8L          NGX   0  ANUM(3)  
  21 2BN=          BPZ   0  T1A                 [BRANCH IF NULL OR MISSING PARAMETER 
   9 2C7W          LDX   0  APARA(3) 
   8 2CMG          SRL   0  12   
   8 2D76          SBN   0  #6062
  18 2DLQ          BZE   0  T1B                 [BRANCH IF PR PARAMETER  
   7 2F6B          LDN   6  5
   7 2FL2          LDN   7  0
   8 2G5L          QUALCON  6,7  
   8 2GK=          TESTREP  OK,T1
   9 2H4W          COMERR   APFERR   
   4 2HJG    T1  
   9 2J46          LDN   0  GLIBTRA7 
   9 2JHQ          STO   0  AWORK2(2)
   9 2K3B          STOZ     AWORK1(2)
   8 2KH2          BRN      T9   
   4 2L2L    T1A 
   7 2LG=          LDN   5  0
   8 2L_W          BRN      T1C  
   4 2MFG    T1B 
  19 2M_6          PROPERTY T22,2,,,X           [ANALYSE PROPERTY PARAMETER  
  17 2NDQ          LDN   4  0                    [RESET COMMAND FLAG 
  21 2NYB          MTEXPROP DECK                [SET UP COMPACT PROPERTY DESCRIPTION 
  10 2PD2          LDX   5  ACOMMUNE1(2) 
   9 2PXL          TESTREP  OK,T1C   
  12 2QC=          ERRTEST  3,T22,ILLMODE,ILLCOMB
  10 2QWW          GEOERR   1,TRANSPOT   
   4 2RBG    T1C 
  20 2RW6          MTCHAR   5                   [SET UP HARDWARE CHARACTERISTIC  
  10 2S*Q          LDX   0  ACOMMUNE1(2) 
   9 2STB          STO   0  AWORK1(2)
   9 2T*2          STO   5  AWORK2(2)
  18 2TSL          BNZ   4  T9                  [J IF ENTRY FROM LIBREQ  
   9 2W#=          ANDN  5  GLIBMODE 
   8 2WRW          BZE   5  T9   
  11 2X?G          SETNCORE 3,3,CPAR,GPERCON 
   7 2XR6          LDCT  0  5
   8 2Y=Q          STO   0  A1(3)
   9 2YQB          LDX   0  AWORK2(2)
   8 2_=2          SRL   0  18   
   9 2_PL          STO   0  A1+1(3)  
   8 329=          LDN   0  #74  
   9 32NW          STO   0  A1+2(3)  
   8 338G          BRN      T9   
   5 33N6    QK2TRANS
  17 347Q          LDN   4  1                   [ENTRY FROM LIBREQ   
  10 34MB          LDX   5  ACOMMUNE1(2) 
   8 3572          BRN      T1C  
  21 35LL    T9    SETNCORE 9,2,AONBS,GTRAN     [FOR TEMPORARY STORAGE OF GEOG NOS   
  20 366=          STOZ     A1(2)               [COUNT OF UNITS FREE (UP TO 4)   
  13 36KW          FINDPEREC 3,APFOTY,MTYPE(1),REENT 
   8 375G          BNG   3  T2   
  20 37K6          JALLOC   3,REENT             [RE-ENTER FINDPEREC IF ALLOCATED 
  15 384Q          JWRONG   3,REENT             [OR WRONGED  
   7 38JB          SMO   FX2 
   9 3942          LDX   0  AWORK1   
  18 39HL          BNG   0  T9F                 [J IF NO SPECIAL CHAR.   
   8 3=3=          LPROP    3,5  
  11 3=GW          BXL   0  PENRZI(1),T9FA   
   8 3?2G          ANDN  5  #203 
   8 3?G6          BRN      T9FB 
   4 3?_Q    T9FA
   8 3#FB          ANDN  5  #200 
   4 3#_2    T9FB
   8 3*DL          SMO      FX2  
  10 3*Y=          BXU   5  AWORK1,REENT 
   4 3BCW    T9F 
  11 3BXG          HUNT     2,CPAR,GPERCON   
  17 3CC6          BNG   2  T5                  [J IF NO PROPERTIES  
   9 3CWQ          LDX   0  A1+2(2)  
  18 3DBB          BZE   0  T5                  [J. IF NO PROPERTY BITS  
   7 3DW2          LDN   5  1
  17 3F*L          BXE   0  5,T5                [J. IF ONLY WPR BIT  
  15 3FT=          MOCH     3                   [CHECK MODE  
   9 3G#W          TESTREPN OK,REENT 
  20 3GSG    T5    LGEOG    3,5                 [PICK UP GEOGRAPHIC UNIT NUMBER  
   8 3H#6          ANDN  5  #777 
   8 3HRQ          LPROP    3,0  
   7 3J?B          SLL   0  9
  17 3JR2          ORS   0  5                   [STORE PROPERTY CODE 
  10 3K=L          HUNT     2,AONBS,GTRAN
  19 3KQ=          LDX   0  A1(2)               [PICK UP COUNT OF UNITS FOUND
   8 3L9W          SMO      FX1  
   8 3LPG          TXU   0  QUART
  19 3M96          BCC      T14                 [J IF 4 UNITS ALREADY FOUND  
  18 3MNQ          LDN   6  1                   [UPDATE COUNT OF UNITS   
   8 3N8B          ADS   6  A1(2)
   7 3NN2          SLL   0  1
   7 3P7L    T15   SMO      0
  18 3PM=          STO   5  A1+1(2)             [DUMP UNIT NO IN BLOCK   
   8 3Q6W          LFAILS   3,5  
   7 3QLG          SMO      0
  16 3R66          STO   5  A1+2(2)             [& FAILURE COUNT 
   8 3RKQ          LDX   1  FX1  
  21 3S5B          BRN      REENT               [THEN LOOK FOR NEXT AVAILABLE DECK   
   4 3SK2    T14 
   7 3T4L          LDX   6  5
   8 3TJ=          SRL   6  14   
  18 3W3W          ANDN  6  3                   [SPEED OF CURRENT DECK   
   7 3WHG          LDN   0  0
   4 3X36    T14A
   8 3XGQ          SMO      FX1  
  18 3Y2B          BXGE  0  OCT,T14F            [BRANCH IF END OF GTRAN  
   7 3YG2          SMO   0   
   9 3Y_L          LDX   1  A1+1(2)  
   8 3_F=          SRL   1  14   
  20 3_YW          ANDN  1  3                   [EXTRACT SPEED OF DECK IN LIST   
   8 42DG          SMO      FX2  
   9 42Y6          LDX   7  AWORK2   
   9 43CQ          ANDN  7  GLIBFAST 
  19 43XB          BZE   7  T14C                [BRANCH UNLESS FAST PROPERTY 
   9 44C2          BXL   6  1,T14E   
   4 44WL    T14B
   8 45B=          BXU   6  1,T15
   4 45TW    T14D
   8 46*G          LFAILS   3,1  
   7 46T6          SMO      0
  21 47#Q          BXL   1  A1+2(2),T15         [BRANCH IF FEWER FAILS ON CURRENT DEC
   4 47SB    T14E
   7 48#2          ADN   0  2
   8 48RL          BRN      T14A 
   4 49?=    T14C
   9 49QW          LDN   7  GLIBSLOW 
   8 4==G          SMO      FX2  
   9 4=Q6          ANDX  7  AWORK2   
   8 4?9Q          BZE   7  T14D 
   9 4?PB          BXL   1  6,T14E   
   8 4#92          BRN      T14B 
   4 4#NL    T14F
   8 4*8=          LDX   1  FX1  
   8 4*MW          BRN      REENT
   4 4B7G    T2  
  10 4BM6          HUNT     2,AONBS,GTRAN
  17 4C6Q          LDX   7  A1(2)               [COUNT OF UNITS FOUND
  18 4CLB          BZE   7  T17                 [J IF NO UNITS AVAILABLE 
  20 4D62          STOZ     6                   [FOR COUNT OF PARAMETERS OUTNUMED
  10 4DKL    T18   HUNT     2,AONBS,GTRAN
   7 4F5=          SMO      6
   9 4FJW          LDEX  1  A1+1(2)  
  16 4G4G          OUTNUM   1,0                 [OUTPUT UNIT NO  
   7 4GJ6          ADN   6  2
   7 4H3Q          STO   6  0
   7 4HHB          SRL   0  1
  21 4J32          BXU   0  7,T18               [J IF ALL UNITS HAVE NOT BEEN OUTNUME
   7 4JGL          SBN   7  4
  21 4K2=    T20   BZE   7  T19                 [J IF ALL PARAMETERS ACCOUNTES FOR   
   7 4KFW          OUTNULL   
   7 4K_G          ADN   7  1
   7 4LF6          BRN   T20 
  19 4LYQ    T17                                [HERE IF NO UNITS AVAILABLE  
  15 4MDB          BZE   4  T3                  [J IF COMMAND
   8 4MY2          SETREP   NOTAV
   8 4NCL          BRN      T21  
   8 4NX=    T3    MTINFORM HNOUN
   8 4PBW          BRN      T21  
   4 4PLN ...T19 
   8 4PWG ...      SETREP   OK   
   8 4Q6# ...      BNZ   4  T21  
   9 4QB6          MTINFORM ATRANS   
  10 4QTQ    T21   MFREE    AONBS,GTRAN  
   8 4R*B          BZE   4  T22  
   5 4RT2          UP
   6 4S#L    T22   ENDCOM
  11 4SS=          MENDAREA 50,K99TRANSPOT   
   4 4T?W    #END
   8 ____ ...43017175000400000000
