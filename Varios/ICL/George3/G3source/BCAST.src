   7 22FL    #OPT  K0BCAST=0 
  11 22_=    #LIS  K0BCAST>K0COMMAND>K0ALLGEO
  12 23=L ...      SEG   BCAST,867,SECTION CENT  
   4 23J2 ...[   
  16 23TB ...[  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD  1982 
   4 246Q ...[   
   4 24D6    #   
  17 24XQ          SEGENTRY K1BCAST,XK1         [COMMAND ENTRY POINT 
  17 25CB          SEGENTRY K2BCAST,XK2         [MACRO ENTRY POINT   
  14 25H?    #  SHARED FILESTORE ENHANCEMENT ENTRY POINT 
  21 25M8          FSHENTRY K3BCAST,,XK3,XK3    [TRANSBEG ENTRY POINT IF ACTIVITY IS 
  17 25R5                                       [  INERT IN MACHINE A
  21 25T3          FSHENTRY K4BCAST,XK4,,XK4    [UNANTICIPATED CLOSEDOWN ENTRY POINT 
   4 25TC    #   
  19 25TR    #     ENTRY POINT REQUIRED BY BROADCAST IMPROVEMENTS ENHANCEMENT
   7 25W7    #UNS BCMILLEN   
  21 25WH          SEGENTRY K5BCAST,XK5         [ENTRY PT TO BC WHILE PROGRAM STOPPED
   4 25X2    #   
  19 26BL    #     THIS SEGMENT IMPLEMENTS THE BROADCAST (BC) COMMAND AND THE
  21 26W=    #     BROADCAST MACRO TO OUTPUT A SPECIFIED TEXT TO ALL OR A SELECTION  
  20 27*W    #     OF CPA'S WHICH SATISFY A CERTAIN CONDITION. (I.E. THOSE IN MOP
   7 27TG    #     CONTEXT.) 
   4 28*6    #   
   9 28SQ    #     COMMAND FORMATS:  
   8 29#B    #     1) BC TEXT,ALL
  12 29S2    #     2) BC TEXT,JOBNAME,:USERNAME  
  11 2=?L    #     3) BC TEXT,JOB NUMBER LIST
  16 2=R=    #     4) BC TEXT      IN WHICH CASE ^ALL^ IS ASSUMED
   4 2?=W    #   
   8 2?QG    #     MACRO USAGE:  
  12 2#=6    #     ACOMMUNE1=MESSAGE IDENTIFIER  
  15 2#PQ    #     ACOMMUNE2=JOBNUMBER OR ZERO  (ZERO => ALL)
   4 2*9B    #   
   4 2*P2    #   
   7 2*QJ    #UNS BCMILLEN   
   4 2*S6    (   
   9 2*TN          SEGENTRY K55BCAST 
  21 2*X=    SBC            +0                  [SWITCH FOR BC ENHANCEMENT,0=ON,1=OFF
   5 2*YS    SLINK5  
   9 2B2B          LINK     BCAST,5  
   4 2B3Y    #   
   4 2B5G    #   
   4 2B74    )   
   7 2B8L    #DEF  TESTING=0 
   8 2BN=    XALL           4HALL
   8 2C7W    XCOLON         1,4H:
   9 2CMG    XBCAST         +BCAST   
   8 2D76    X51            51   
   4 2DLQ    #   
  20 2F6B    #     THE FOLLOWING ROUTINE CHECKS THAT THE ACTIVITY WHOSE ACTRING  
  20 2FL2    #     IS POINTED TO BY X2 MAY BE BROADCASTED TO. EXITS 6 0 IF NOT,  
  11 2G5L    #     EXITS 6 1 IF IT CAN BE.   
   4 2GK=    #   
   9 2H4W    OKACT SBN   2  ACTRING  
   9 2HJG          LDX   0  ATYPE(2) 
   8 2J46          SRL   0  12   
   8 2JHQ          ERN   0  CPAT 
   8 2K3B          BNZ   0  NO   
   8 2KH2          LIMBO    0,2  
   9 2L2L    NO    ADN   2  ACTRING  
   8 2LG=          BNZ   0  (6)  
  12 2L_W          LDX   0  CONTEXT-ACTRING(2)   
   8 2MFG          ANDN  0  #10  
  15 2M_6          BZE   0  (6)                 [J IF NOT MOP
   7 2NDQ          EXIT  6  1
   4 2NYB    #   
  21 2PD2    #     THE FOLLOWING ROUTINE PASSES THE NEXT BC PARAMETER,CHECKING FOR   
  21 2PXL    #     ITS EXISTENCE AND NON-NULLITY. EXITS 4 0 IF NON-EXISTENT:4 1 IF   
  19 2QC=    #     NULL:4 2 IF PRESENT AND NOT NULL WITH X5=CHAR COUNT AND X3
  18 2QWW    #     POINTING TO CPB/CUNI. FIRST TWO EXITS FREE CPB/CUNI.  
   4 2RBG    #   
   8 2RW6    PASS  SBX   4  FX1  
   7 2S*Q          SPARAPAS  
   8 2STB          ADX   4  FX1  
   5 2T58    PASSED  
  10 2T*2          MHUNT    3,CPB,CUNI   
   9 2TSL          LDX   5  ANUM(3)  
  15 2W#=          BPZ   5  PRES                [J IF PRESENT
   7 2WRW    XFR   FREECORE 3
  17 2X?G          LDN   5  0                   [SEE CALL AT ^XP2^   
   7 2XR6          EXIT  4  0
   7 2Y=Q    PRES  ADN   4  1
  15 2YQB          BZE   5  XFR                 [J IF NULL   
   8 2_=2          ANDN  5  #7777
   7 2_PL          EXIT  4  1
   4 329=    #   
  19 32NW    #     THE NEXT ROUTINE FINDS ALOGLEN OF THE GMON/AOUT AND STORES
  16 338G    #     IT IN AWORK3. IT USES X0 AND 3. LINK IN X6.   
   4 33N6    #   
   5 347Q    SETLEN  
  10 34MB          MHUNTW   3,GMON,AOUT  
  10 3572          LDX   0  ALOGLEN(3)   
   9 35LL          STO   0  AWORK3(2)
   7 366=          EXIT  6  0
   4 36KW    #   
  11 375G    #     NOW FOLLOWS THE MAIN CODE 
   4 37K6    #   
  17 384Q    XK1                                [COMMAND ENTRY POINT 
   7 38JB    #SKI  TESTING   
   7 3942          RGTRACE  1
  17 39HL          STOZ     AWORK1(2)           [COMMAND INDICATOR   
  17 3=3=          STOZ     AWORK2(2)           [HOLDS JPARNUM LATER 
   9 3=5T          SEGENTRY K52BCAST 
   7 3=8D          LDN   0  0
   8 3=?3          ANDN  0  #12  
   8 3=*L          ERN   0  #12  
  20 3=D9          BZE   0  NOTALLOWED          [J IF BC NOT OK IN NO-USER-MOP   
  17 3=GW          CALL  4  PASS                [PASS TEXT PARAMETER 
   9 3?2G          BRN      NOPARS   
  20 3?G6          BRN      NOPARS              [ERROR IF NULL OR NON-EXISTENT   
   7 3?_Q    #SKI  TESTING   
   7 3#FB          RGTRACE  2
   9 3#_2          OUTPAR   TIMENOW  
  17 3*DL          BXL   5  X51(1),SH           [J IF SHORT ENOUGH   
  14 3*Y=          LDN   5  50                  [TRUNCATE
  11 3BCW    SH    OUTPARAM 5,APARA,CPB,CUNI 
   9 3BXG          MFREE    CPB,CUNI 
  17 3CC6          ASSMESS  XBCAST(1)           [ASSEMBLE GMON/AOUT  
   7 3CWQ    #SKI  TESTING   
   7 3DBB          RGTRACE  3
   4 3DW2    [   
  17 3F*L          SPARABEG 1,XCOLON(1)         [LOOK FOR USEREPNAME 
  18 3FT=          CALL  4  PASSED              [FINDS & CHECKS CPB-CUNI 
  16 3G#W          BRN      NOCOLON             [J IF NO USERNAME
  15 3GSG          BRN      USNA                [J IF NULL   
   4 3H#6    [   
  11 3HRQ    [     USERNAME HAS BEEN FOUND   
   5 3J?B    USERNM  
  18 3JR2          CHEKLFN2 USNA,USFORM,3       [CHECK FORMAT OF USERNAME
  18 3K=L          LDX   6  ACES                [SPACE-FILL ACOMMUNE1-6  
  10 3KQ=          STO   6  ACOMMUNE1(2) 
  10 3L9W          LDN   6  ACOMMUNE1(2) 
  10 3LPG          LDN   7  ACOMMUNE2(2) 
   7 3M96          MOVE  6  5
  13 3MNQ    [     MOVE USERNAME INTO ACOMMUNE4,5,6  
   9 3N8B          LDN   6  APARA(3) 
  10 3NN2          LDN   7  ACOMMUNE4(2) 
  21 3P7L          SMO      5                   [X5 SET BY SUBROUTINE PASS(PASSED)   
   7 3PM=          MVCH  6  0
   7 3Q6W          FREECORE 3
   4 3QLG    [   
  10 3R66    [     NOW LOOK FOR JOBNAME  
   7 3RKQ          SPARUNAC  
  10 3S5B          MHUNT    3,CPB,CUNI   
  16 3SK2          CHEKLFN2 USNA,RONGJOB,3       [CHECK FORMAT   
   9 3TJ=          LDN   4  APARA(3) 
  10 3W3W          LDN   5  ACOMMUNE1(2) 
   9 3WHG          SMO      ANUM(3)  
  15 3X36          MVCH  4  0                   [MOVE JOBNAME
   7 3XGQ    #SKI  TESTING   
   7 3Y2B          RGTRACE  5
   8 3YG2          LDN   3  BJOBQ
  21 3Y_L    XJQ   LDX   3  FPTR(3)             [GO ROUND JOBQ LOOKING FOR THIS JOB  
   8 3_F=          TXU   3  CXJO 
  18 3_YW          BCC      XJEND               [ERROR IF JOB NOT FOUND  
   7 42DG          LDN   7  6
   7 42Y6    TEST  SMO      7
  11 43CQ          LDX   5  ACOMMUNE1-1(2)   
   7 43XB          SMO      7
  10 44C2          TXU   5  JNAME-1(3)   
  17 44WL          BCS      XJQ                 [J IF NOT THIS JOB   
   8 45B=          BCT   7  TEST 
  20 45TW          LDX   7  JOBNUM(3)           [GET SPECIFIED JOB'S JOBNUMBER   
  15 46*G          MFREE    CPB,CUNI            [FREE JOBNAME
   8 46T6          BRN      XHUNT
   4 46TC    [   
   4 46TN    [   
   5 46T_    NOCOLON 
  18 46W=    [ NOT USERNAME,JOBNAME SO CHECK FOR ^ALL^ OR JOB NO. LIST   
  17 46WH          LDN   7  0                   [SET ^ALL^ INDICATOR 
   8 46WS    XP    CALL  4  PASS 
   9 46X5          SEGENTRY K50BCAST 
  15 46XB          BRN      XHUNT               [BC TO ALL   
  17 46XM          BRN      XP                  [SKIP NULL PARAMETERS
   7 46XY    #SKI  TESTING   
   7 46Y9          RGTRACE  4
   7 46YG          SBN   5  3
  21 46YR          BNZ   5  XJLIST                [CAN'T BE ^ALL^ UNLESS IT'S 3 CHARS
   9 46_4          LDX   5  APARA(3) 
  16 46_*          BXU   5  XALL(1),XJLIST        [J IF NOT ^ALL^
   7 46_L          FREECORE 3
   8 46_X          BRN      XHUNT
   4 47BD    [   
   4 47KB    [   
  17 47SB    XK2                                [MACRO ENTRY POINT   
   7 48#2    #SKI  TESTING   
   7 48RL          RGTRACE  6
  16 49?=          STO   1  AWORK1(2)           [MACRO INDICATOR 
  15 49QW          LDX   7  ACOMMUNE2(2)        [JOBNUMBER   
  10 4==G          ASSMESS  ACOMMUNE1(2) 
   9 4=G#          SEGENTRY K53BCAST 
  19 4=Q6    XHUNT CALL  6  SETLEN              [SET LENGTH OF AOUT IN AWORK3
  21 4=X#          LDN   0  #7777               [TOP HALF OF WORD MAY HOLD MESSAGE NO
  21 4?4G          ANDS  0  A1(3)               [ SO MASK OUT TO PREVENT ANY TROUBLE!
   7 4?9Q    #SKI  TESTING   
   7 4?PB          RGTRACE  7
   7 4#92          LDN   5  0
  19 4#NL          BNZ   7  REN1                [J IF ONLY ONE JOB TO BC TO  
  21 4*8=    #     WE MUST NOW MAKE A NOTE OF ALL THE ACTIVITIES TO BC TO BY PUTTING 
  21 4*MW    #     THEIR ACTNUM'S IN AN ADATA/CSTORE BLOCK. WE GO ROUND THE ACT. LIST
  17 4B7G    #     FIRST TO SEE HOW BIG A BLOCK WE'RE GOING TO NEED. 
   7 4BM6          LDN   4  0
  10 4C6Q          LDN   7  ACTRING(2)   
   7 4CLB          LDX   2  7
   8 4D62    ROUND LDX   2  0(2) 
  18 4DKL          BXE   2  7,XEND              [J IF WE'RE BACK AT FX2  
  19 4F5=          CALL  6  OKACT               [SEE IF ACT. IS BC'ABLE TO   
  14 4FJW          BRN      ROUND               [J IF NOT
  18 4G4G          BUX   4  ROUND               [   ELSE INCREMENT COUNT 
  19 4GJ6    XEND  BZE   4  NUN                 [J IF NO SUITABLE ACTIVITIES 
   7 4H3Q    #SKI  TESTING   
   7 4HHB          RGTRACE 71
  11 4J32          SETUPCORE 4,1,ADATA,CSTORE
   7 4JGL    RETRY LDX   3  1
  10 4K2=          LDN   7  ACTRING(2)   
   7 4KFW          LDX   2  7
   8 4K_G    ROUNE LDX   2  0(2) 
  15 4LF6          BXE   2  7,YEND              [J IF HOME   
   8 4LYQ          CALL  6  OKACT
   8 4MDB          BRN      ROUNE
  19 4MY2          BZE   4  MORE                [J IF NO ROOM IN ADATA/CSTORE
  11 4NCL          LDX   6  ACTNUM-ACTRING(2)
   8 4NX=          STO   6  A1(3)
   7 4PBW          SBN   4  1
   8 4PWG          BUX   3  ROUNE
  10 4QB6    MORE  LDX   4  ALOGLEN(1)   
   7 4QTQ          ADN   4  5
  17 4R*B          ALTLEN   1,4                 [ENLARGE DATA BLOCK  
   7 4RT2    #SKI  TESTING   
   7 4S#L          RGTRACE 72
  11 4SS=          MHUNTW   1,ADATA,CSTORE   
   8 4T?W          BRN      RETRY
  10 4TRG    YEND  LDX   5  ALOGLEN(1)   
  20 4W?6          SBX   5  4                   [SET X5=COUNT OF ACTS TO BC TO   
   7 4WD#    #UNS BCMILLEN   
  19 4WKG          STO   5  ALOGLEN(1)          [REMEMBER COUNT OF ACTIVITIES
  16 4WQQ          BZE   5  NUN1                [J IF NOW NONE   
   7 4X=B    #SKI  TESTING   
   7 4XQ2          RGTRACE  8
  16 4Y9L          LDN   7  0                   [INDICATE ^ALL^  
   8 4YP=          LDX   2  FX2  
   7 4YPS    #UNS BCMILLEN   
   4 4YQB    (   
   8 4YQY          LDX   1  FX1  
   9 4YRG          LDX   0  SBC(1)   
  19 4YS4          BNZ   0  NOENH1              [J IF MILLENHANCEMENT NOT ON 
  21 4YSL          ORX   5  GSIGN               [INDICATE 1ST PASS THRU ADATA-CSTORE 
   5 4YT8    NOENH1  
   4 4YTQ    )   
   4 4_8W    #   
  17 4_NG    #     AT THIS POINT THE FOLLOWING WORDS ARE SIGNIFICANT 
   4 5286    #   
  15 52MQ    #     AWORK1=0 IF COMMAND: =NON ZERO IF MACRO   
  12 537B    #     X7=0 IF ^ALL^,ELSE = JOBNUMBER
  12 53M2    #     AWORK3=ALOGLEN OF GMON/AOUT   
  16 546L    #     IF X7=0 THEN X5=COUNT OF ACTIVITIES TO BC TO  
   4 54L=    #   
   7 54MD    #UNS BCMILLEN   
   4 54NL    (   
   4 54PS    #   
  21 54R2    #     BC IMPROVEMENTS ENHANCEMENT: IF THIS IS SWITCHED ON, AN ATTEMPT IS
  17 54S8    #     MADE TO STOP A JOB RUNNING A PROGRAM, SO AS TO BC.
  21 54TB    #     IN THE ^ALL^ CASE THERE CAN BE 2 PASSES THROUGH ADATA/CSTORE. IN  
  21 54WJ    #     THE 1ST, THE ACTIVITY NUMBER OF ANY JOB WHOSE PROGRAM CANNOT BE   
  21 54XQ    #     STOPPED IS LEFT UNZEROISED IN CSTORE; IN THE 2ND, ANOTHER ATTEMPT 
  12 54YY    #     IS MADE, THIS TIME WAITING.   
  16 5526    #     ^NON-ALL^ CASES ARE TREATED AS IF 2ND PASS.   
  16 553#    #       B0 OF X5 = 1 FOR 1ST PASS, 0 FOR 2ND PASS.  
   4 554G    )   
  20 555W    #     N.B. TRYING TO BE CLEVER,WE RENAME THE GMON/AOUT AMXOR/BBC FOR
   9 55KG    #     THE LAST ACTIVITY.
   4 5656    #   
  18 56JQ    NXT   BCT   5  NEXT                [J UNLESS LAST ACTIVITY  
   7 56PY    #UNS BCMILLEN   
  21 56X6          BNG   5  NEXT                [J IF 1ST PASS THROUGH ADATA/CSTORE  
  10 574B    REN   MHUNTW   3,GMON,AOUT  
  10 57J2    REN1  NAME     3,AMXOR,BBC  
   8 583L          BRN      XCHN 
   8 58H=    NEXT  LDX   2  FX2  
  13 592W          SETUPCORE AWORK3(2),3,AMXOR,BBC   
  10 59GG          MHUNTW   1,GMON,AOUT  
   8 5=26          LDN   0  A1(1)
   8 5=FQ          LDN   1  A1(3)
   9 5=_B          SMO      AWORK3(2)
   7 5?F2          MOVE  0  0
   4 5?YL    XCHN
   7 5#D=    #SKI  TESTING   
   7 5#XW          RGTRACE  9
  20 5*CG          BNZ   7  XJOB                [J IF X7 SPECIFIES A JOB NUMBER  
  11 5*X6          MHUNTW   1,ADATA,CSTORE   
   7 5BBQ          LDX   4  1
   7 5BWB          ADX   1  5
   7 5BY_    #UNS BCMILLEN   
   4 5C3J    (   
  14 5C67          SLL   1  1                   [LOSE B0 
   7 5C8Q          SRL   1  1
   4 5C?*    )   
  10 5CB2          FINDACTN 1,A1(1),NFND 
  10 5CTL    XBACK LDN   2  ACTRING(1)   
   8 5D*=          CALL  6  OKACT
  19 5DSW          BRN      NBC                 [J IF ACTIVITY NOT BC'ABLE TO
   7 5F?*    #UNS BCMILLEN   
   4 5F?B    (   
   8 5F?C          LDX   1  FX1  
   9 5F?D          LDX   0  SBC(1)   
  18 5F?F          BNZ   0  XCHAIN              [J IF ENHANCEMENT NOT ON 
   9 5F?G          SBN   2  ACTRING  
  16 5F?H          LDX   1  JOBRING+FPTR(2)     [FIND JOB BLOCK  
   9 5F?J          SBN   1  AJBRING  
   9 5F?K          LDCH  0  ATYPE(1) 
   9 5F?L          SBN   0  JOBQE/64 
  16 5F?M          BNZ   0  XCHAD              [J IF NOT FOUND   
  19 5F?N          JBC      XCHAD,1,JBPLUG       [J IF NO PLUGGED-IN PROGRAM 
  14 5F?P          FPCAJO   2,1                 [FIND PCA
  21 5F?Q          LDX   4  IREVIEW             [SET WAIT TIME AT -CURRENTLY- 2 SECS 
   9 5F?R          ADX   4  IREVIEW  
   9 5F?S          LDX   6  JOBNO(2) 
  17 5F?T          BS       2,ASTOPBC           [SET STOP BIT FOR BC 
   5 5F?W    TRYSTOP 
   7 5F?X          STOPIT   2
   9 5F?Y          LA       ASTOPBC,0
  11 5F?_          BXE   0  ASTOP(2),STOPPED 
   5 5F#2    TPASS   
  18 5F#3          BNG   5  NOSTOP              [J IF 1ST PASS, DONT WAIT
  18 5F#4          COOR3    #41                 [WAIT FOR EVENT TO CLEAR 
   9 5F#5          FINDJOBQ 1,6,NOJB 
  20 5F#6          JBC      NOJBPL,1,JBPLUG     [J IF NOW NO PLUGGED-IN PROGRAM  
  14 5F#7          FPCAJO   2,1                 [FIND PCA
   9 5F#8          BCT   4  TRYSTOP  
   9 5F#9          BC       2,ASTOPBC
   4 5F#=    XFCA
  15 5F#?          FCAJO    2,1                 [REFIND CPA  
   5 5F##    XCHAD   
   9 5F#*          ADN   2  ACTRING  
  10 5F#B          MHUNTW   3,AMXOR,BBC  
   5 5F#C    XCHAIN  
   4 5F#D    )   
   7 5F#G          LDX   6  2
   8 5FS6          LDX   2  0(2) 
   9 5G?Q          SBN   2  ACTRING  
   9 5GRB          CHAIN    3,BPTR(2)
   7 5H?2          LDX   2  6
   9 5HQL          SBN   2  ACTRING  
   8 5J==          LDCT  0  #10  
  15 5JPW          ORS   0  CLONG1(2)           [SET BC BIT  
   8 5K9G          LDN   0  #2000
   9 5KP6          ANDX  0  ATYPE(2) 
   7 5KS=    #UNS BCMILLEN   
  19 5KXB          BZE   0  ZOK             [J IF ACTIVITY NOT IN LONGWAIT   
   7 5L2G    #UNS BCMILLEN   
   4 5L5L    #SKI
  20 5L8Q          BZE   0  NOWT                [J IF ACTIVITY NOT IN LONGWAIT   
   7 5LNB          UNQUEUE   
   6 5M82          FPUT  
   7 5M8=    #UNS BCMILLEN   
   4 5M8G    (   
   4 5M8Q    ZOK 
  18 5M92          LDN   4  0                   [SET ^SUCCESSFUL^ MARKER 
   4 5M9=    )   
   4 5MLL    NOWT
   7 5N3Q    #UNS BCMILLEN   
   4 5N3W    (   
  15 5N42          BNZ   7  TDONE               [J IF NOT ALL
  17 5N46          BNZ   4  TDONE               [J IF NOT SUCCESSFUL 
  11 5N4=          MHUNTW   1,ADATA,CSTORE   
   7 5N4B          SMO      5
  19 5N4G          STOZ     A1(1)               [ZEROISE ACT.NUMBER IN CSTORE
  21 5N4L                                        [IF ^SUCCESSFUL- IE NO 2ND TRY NEEDE
   5 5N4Q    TDONE   
   4 5N4W    )   
  16 5N5=          BZE   5  XDONE               [J IF ALL DONE   
   7 5N7=    #SKI  TESTING   
   8 5NLW          RGTRACE  10   
   7 5NNS    #UNS BCMILLEN   
   4 5NQQ    #SKI
  15 5NSN          BZE   7  NXT                 [J IF ^ALL^  
   7 5NWL    #UNS BCMILLEN   
   4 5NYJ    (   
  15 5P2G          BZE   7  PASSTST             [J IF ALL^   
   4 5P4D    )   
  15 5P6G          BZE   7  NXT                 [J IF ^ALL^  
  21 5PL6    XCH   CHNUMCOD                     [CONVERT THE ALREADY PASSED APRAMETER
  10 5Q5Q          MHUNT    1,CPB,CUNI   
  10 5QKB          LDX   0  JPARNUM(1)   
  18 5R52          STO   0  AWORK2(2)           [SAVE FOR ERROR ROUTINE  
   7 5RJL          FREECORE 1
   9 5S4=          TESTREP2 OK,XOK   
   8 5SHW    XP1   CALL  4  PASS 
   8 5T3G          BRN      XFIN 
   8 5TH6          BRN      XP1  
   8 5W2Q          BRN      XCH  
  18 5WGB    XOK   LDX   7  ACOMMUNE1(2)        [JOBNUMBER FROM CHNUMCOD 
   8 5X22          BNZ   7  XP2  
  19 5XFL          LDCT  7  #400                [X7=0 WOULD LOOK LIKE ^ALL^  
   8 5X_=    XP2   CALL  4  PASS 
  18 5YDW          BRN      REN                 [J IF NO MORE PARAMETERS 
   8 5YYG          BRN      XP2  
   7 5_D6          LDN   5  1
   8 5_XQ          BRN      NEXT 
   5 62CB    XJLIST  
  19 62X2          CALL  6  SETLEN              [SET LENGTH OF AOUT IN AWORK3
   8 63BL          BRN      XCH  
   7 63BT    #UNS BCMILLEN   
   4 63C4    (   
   5 63C?    PASSTST 
   8 63CG          TXU   5  GSIGN
  17 63CP          BCC      SECCHK             [J IF END OF 1ST PASS 
   8 63CY          BRN      NXT  
   4 63D7    #   
  20 63DB    # NOW COUNT THE NUMBER OF NON-ZERO ACTIVITY NUMBERS IN ADATA/CSTORE,
  20 63DK    #     I.E. THOSE WHOSE PROGRAMS NOT YET STOPPED; X5 WILL CONTAIN THE
  21 63DS    #     COUNT AND INDICATE 2ND PASS. ACTIVITY NUMBERS ARE MOVED TO A1 ETC 
  15 63F3    #     OF CSTORE, LOSING ANY INTERVENING ZEROES. 
   4 63F=    #   
   5 63FF    SECCHK  
  11 63FN          MHUNTW   1,ADATA,CSTORE   
   7 63FX          LDN   6  0
   7 63G6          LDN   5  0
   5 63G*    SECSTRT 
   7 63GJ          SMO      6
   8 63GR          LDX   0  A1(1)
   9 63H2          BZE   0  SECADD   
   7 63H9          SMO      5
   8 63HD          STO   0  A1(1)
   7 63HM          ADN   5  1
   5 63HW    SECADD  
   7 63J5          ADN   6  1
  10 63J#          TXU   6  ALOGLEN(1)   
   9 63JH          BCC      SECFIN   
   9 63JQ          BRN      SECSTRT  
   5 63J_    SECFIN  
  17 63K8          BZE   5  XDONE              [2ND PASS NOT NEEDED  
   8 63KC          BRN      NXT  
   4 63KL    )   
   7 63L?    #UNS BCMILLEN   
   4 63LG    (   
   5 63LP    STOPPED 
  18 63LY          BS       2,ASTOPEXEC         [SET B0 OF ASTOP IN PCA  
  15 63M7          LDX   4  2                   [REMEMBER PCA
  14 63MB          FCAJO    2,1                 [FIND CPA
   8 63MK          LDCT  0  #10  
   9 63MS          ORS   0  CLONG1(2)
  15 63N3    #                                    [SET BC BIT
   9 63N=          ADN   2  ACTRING  
  15 63NF          MHUNTW   3,AMXOR,BBC         [REFIND BBC  
   8 63NN          LDX   2  0(2) 
   9 63NX          SBN   2  ACTRING  
  17 63P6          CHAIN    3,BPTR(2)           [CHAIN BBC AFTER CPA 
  13 63P*          LDX   2  4                   [PCA 
   8 63PJ          LDX   1  FX1  
   9 63PR          LDX   0  SLINK5(1)
  16 63Q2          STO   0  ALINK1(2)           [SET LINK IN PCA 
   6 63Q9          FPUT  
   8 63QD          BRN      ZOK  
   4 63QM    #   
  21 63QW    XK5                                [NEW ENTRY POINT FOR MILL ENHANCEMENT
  11 63R5          TRACE   JOBNO(2),BCAST5   
   6 63R#          UNPLUG
   9 63RH          BC    2,ASTOPBC   
   6 63RQ          CAPCA 
  14 63R_          DOWN     MULTIPLX,1          [DO BC   
   7 63SC          RUNPROG   
   4 63SL    #   
   4 63ST    )   
   7 63W=    NBC   LDX   6  1
  16 64*W          BNZ   7  XNBC                [J IF NOT ^ALL^  
   4 64FR    NFND
  16 6599          FREECORE 3                   [FREE AMXOR/BBC  
   7 659T    #UNS BCMILLEN   
   4 65=F    #SKI
   8 65?5          BRN      NOWT 
   7 65?P    #UNS BCMILLEN   
   8 65#*          BRN      ZOK  
   8 65*6          BRN      NOWT 
   9 65SQ    XJOB  FINDJOBQ 1,7,NOJOB
  20 66#B          JBS      NBC,1,JBSAVING      [IF BEING SAVED-A NON CANDIDATE  
   7 66S2          FCAJO    1
   8 67?L          BRN      XBACK
  16 67R=    XDONE BNZ   7  NOCS                [J IF NOT ^ALL^  
  11 6833          MHUNTW   2,ADATA,CSTORE   
   7 685L          LDX   4  2
  17 68=W          FREECORE 4                   [FREE ADATA/CSTORE   
   8 68QG    NOCS  LDX   2  FX2  
   7 69=6    #SKI  TESTING   
   8 69PQ          RGTRACE  11   
   9 6=9B          LDX   4  AWORK1(2)
  15 6=?K          BNZ   4  UPP                 [J IF MACRO  
   4 6=*S    XFIN
   6 6=XJ          ENDCOM
   5 6#7W    NOPARS  
   8 6#MG          CALL  3  XERR 
  12 6*76                   +JPARMIS,+JAMESSAGE  
   9 6*85          SEGENTRY K51BCAST 
   8 6*94          CALL  3  XERR 
  10 6*=3                   +JPARMIS,-1  
   6 6*?3    NOTALLOWED  
   8 6*BY          CALL  3  XERR 
  10 6*GT                   +JNOMANCON,-1
   4 6*LQ    USNA
   8 6B6B          CALL  3  XERR 
  11 6BL2                   +JPARMIS,+JUSNA  
   5 6C5L    USFORM  
   8 6CK=          CALL  3  XERR 
  11 6D4W                   +ERUSERFORM,-1   
   5 6D7F    RONGJOB 
   9 6D=4    [     INVALID JOBNAME   
   8 6D#M          CALL  3  XERR 
   9 6DC=                   +JOBIN,-1
   4 6DFT    [   
   5 6DJG    XJEND   
   8 6F46          CALL  3  XERR 
  10 6FHQ                   +EWHSTAT,-1  
   7 6G3B    NUN1  FREECORE 1
   9 6GH2    NUN   MFREE   GMON,AOUT 
   8 6H2L          CALL  3  XERR 
  10 6HG=                   +HNOMOP,-1   
   7 6H_W                   0
   7 6J=2    #UNS BCMILLEN   
   4 6J=#    (   
   4 6J=L    NOJB
   9 6J=Y          BC       2,ASTOPBC
   8 6J#S          BRN      XCHN 
   5 6J*6    NOJBPL  
  10 6J*D          TESTCOR   1,STILPCA   
  14 6J*Q          BRN      XFCA             [J IF NO PCA
   5 6JB4    STILPCA 
   8 6JBB          FPCAJO    2,1 
   9 6JBN          BC       2,ASTOPBC
   8 6JC2          BRN      XFCA 
   5 6JC#    NOSTOP  
   9 6JCL          BC       2,ASTOPBC
  19 6JCY          LDN   4  1                   [SET ^NOT SUCCESSFUL^ MARKER 
  14 6JD=          FREECORE 3                   [FREE BBC
   8 6JDJ          BRN      NOWT 
   4 6JDW    )   
   7 6JFG    NOJOB FREECORE 3
   8 6J_6          CALL  3  XERR1
  10 6KDQ                   +FWHSTAT,-1  
   7 6KYB                   1
   7 6LD2    XNBC  FREECORE 3
   8 6LMS          LDX   2  FX2  
   9 6LXL          LDX   0  AWORK2(2)
  18 6MC=          BNZ   0  XNBC1               [J IF NUMBER LIST CASE   
   7 6MWW          LDN   0  2
   9 6NBG          STO   0  AWORK2(2)
   8 6NW6          CALL  3  XERR1
  10 6P*Q                   +HMOPJNO1,-1 
   7 6PTB                   0
   8 6Q*2    XNBC1 CALL  3  XERR1
  10 6QSL                   +HMOPJNO,-1  
   7 6R#=                   0
   4 6RRW    #   
   7 6S?G    XERR1 NGNC  6  1
   7 6SR6    XERR  LDN   6  0
   7 6T=Q    #SKI  TESTING   
   8 6TQB          RGTRACE  12   
   8 6W=2          LDX   2  FX2  
   9 6WPL          LDX   0  AWORK1(2)
  16 6X9=          BNZ   0  MAC                 [J IF FROM MACRO 
   8 6XNW          BZE   6  XER  
   8 6Y8G          SBX   3  FX1  
   9 6YN6          SPARANOX AWORK2(2)
   8 6_7Q          ADX   3  FX1  
   9 6_MB    XER   ERRORX   0(3),1(3)
  18 7272          BZE   6  XFIN                [ENDCOM IF NOT NUM LIST  
   9 72LL          MFREE    CPB,CUNI 
   8 736=          BRN      NOWT 
   8 73KW    MAC   LDX   7  2(3) 
   8 745G          SETREP   NOMOP
   8 74K6          BZE   7  UPP  
   8 754Q          SETREP   NOJOB
   4 757W    UPP 
   5 75YW          UP
   4 7642    #   
  10 76HL          MENDAREA 20,K99BCAST  
   4 773=    #END
   8 ____ ...17011466000100000000
