  17 22BG ...        SEG      SETPB,,CENT(COMMAND PROCESSOR),,G400   
   4 22R2 ...[   
  15 237G ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23J2 ...[   
   4 23YG    #   
  20 24D6    #           THIS SEGMENT IS ENTERED FROM THE SEGMENT SETPARAM. IT   
  19 24XQ    #           COMPLETES IMPLEMENTATION OF THE SETPARAM COMMAND AND
  15 25CB    #           RETURNS TO THE COMMAND PROCESSOR .  
   4 25X2    #   
  11 26BL          SEGENTRY K1SETPB,SENTRY1  
   6 26HS ...#UNS G400   
  16 26P2 ...#      THIS CHAPTER IS MODIFIED FOR G3PLUS-IH MK 2  
   9 26W=    MINUS1         #77777777
   7 27*W    TWENTYFIVE  +25 
   8 27TG    SPACE          #20  
  15 28*6    XATCHR         #40                 [@ CHARACTER 
   7 28SQ    TEN         +10 
   7 29#B    PAREN       #30 
   7 29S2    XRPAR       #31 
   6 29_8 ...#UNS G400   
   9 2=6B ...XDSK           4H*JBV   
   4 2=?L    #   
   5 2=R=    SENTRY1 
   9 2?=W          STOZ     AWORK2(2)
   9 2?QG          STOZ     AWORK4(2)
   7 2#=6          LDN   0  0
   7 2#PQ          PARAPASS 0
   6 2#QT ...#UNS G400   
   4 2#RY ...(   
   9 2#T3 ...      HUNT 3,CPB,CUNI   
   9 2#W6 ...      LDX   0  ANUM(3)  
   8 2#X9 ...      BNG   0  START
   8 2#Y# ...      ANDN  0  #7777
   7 2#_C ...      SBN   0  4
   8 2*2G ...      BNG   0  START
   9 2*3K ...      LDX   0  APARA(3) 
   9 2*4N ...      TXU   0  XDSK(1)  
   8 2*5R ...      BCS      START
   9 2*6W ...      ACROSS   GLOBALS,2
   4 2*7_ ...)   
   5 2*9B    START   
   8 2*P2          LDX   1  FX1  
   9 2B8L          STOZ     AWORK3(2)
   7 2BN=          LDN   4  1
   9 2C7W          ADS   4  AWORK2(2)
  10 2CMG          HUNT     3,CPB,CUNI   
   9 2D76          LDX   6  ANUM(3)  
  18 2DLQ          BZE   6  XERRN               [1ST PARAMETER MISSING   
  17 2F6B          ANDN  6  #7777               [GET CHARACTER COUNT 
   7 2FL2          SBN   6  1
  19 2G5L          BNZ   6  STR1                [BRN IF NOT SINGLE CHARACTER 
   4 2GK=    SPB1
  16 2H4W          LDCH  6  APARA(3)            [TAKE CHARACTER  
  16 2HJG          SBN   6  #40                 [CONVERT TO NO   
  10 2J46          TXL   6  TWENTYFIVE(1)
  20 2JHQ          BCC      PN4                 [J IF >24TH PARAMETER SPECIFIED  
  15 2K3B          BNG   6  PN4                 [ERROR IF NOT
  15 2KH2          BZE   6  PN4                 [ALPHABETIC  
  16 2L2L          CALL  7  SEARCH              [GET ALAS BLOCK  
  19 2LG=          LDN   5  APARAFIR            [POINTER TO FIRST PARAMETER  
   8 2L_W          BRN      NCHA 
   4 2MFG    #   
  20 2M_6    #           SUBROUTINE TO OBTAIN IN X1 THE DATUM OF THE ALAS BLOCK  
  12 2NDQ    #           1 LEVEL ABOVE SETPARAM  
   4 2NYB    #   
   5 2PD2    SEARCH  
   8 2PXL          LDX   2  FX2  
   9 2QC=          LDX   4  AWORK2(2)
  10 2QWW          MHUNTW   1,CPB,CALAS  
   9 2RBG    P1    LDX   1  FPTR(1)  
   9 2RW6          LDX   0  ATYPE(1) 
   8 2S*Q          TXL   0  CACT 
   9 2STB          BCC      XERROR   
   8 2T*2          SRL   0  12   
   9 2TSL          ERN   0  CPB+CALAS
   8 2W#=          BNZ   0  P1   
  17 2WRW          LDX   0  EXT+10(2)           [TOP BREAKIN LEVEL   
   8 2X?G          ANDN  0  #7777
  10 2XR6          STO   0  ACOMMUNE1(2) 
  10 2Y=Q          LDEX  0  CPBCPLEV(1)  
  10 2YQB          TXL   0  ACOMMUNE1(2) 
   9 2_=2          BCS      XERROR   
   8 2_PL          BCT   4  P1   
   7 329=          EXIT  7  0
   4 32NW    #   
  21 338G    #           FOR ALPHABETIC CHARACTER 'X' SPECIFIED,FIND THE (X-#40)TH   
  13 33N6    #           PARAMETER IN THE ALAS BLOCK 
   4 347Q    #   
   8 34MB    NPARA ANDN  4  #7777
  18 3572          ADN   4  7                   [ADD 1 AND ROUND DIVISION
   7 35LL          SRL   4  2
  19 366=          ADX   5  4                   [POINTER TO NEXT PARAMETER   
   7 36KW    NCHA  SMO      5
  15 375G          LDX   4  0(1)                [PT/N WORD   
   8 37K6          SMO      FX1  
   9 384Q          TXU   4  MINUS1   
  20 38JB          BCS      NT                  [BRN IF END OF BLOCK NOT REACHED 
   4 3942    #   
  21 39HL    #           NO MORE PARAMETERS: ADDITIONAL NULL PAR.S MUST BE CREATED   
   4 3=3=    #   
   9 3=GW          LDX   4  AWORK3(2)
  18 3?2G          BNZ   4  XERR6               [ERROR IF CALL BY NAME   
  16 3?G6          LDX   4  ALOGLEN(1)          [LOGICAL LENGTH  
  17 3?_Q          ADX   4  6                   [NEW LOGICAL LENGTH  
  21 3#FB          ADS   6  APARANUM(1)         [ADJUST NO. OF PARAMETERS IN BLOCK   
   7 3#_2          LDX   7  1
  18 3*DL          ALTLEN   7,4                 [INCREASE BLOCK LENGTH   
   9 3*Y=          CALL  7  SEARCH   
   8 3BCW          LDX   0  GSIGN
   8 3BXG          BRN      ZER  
   7 3CC6    ZEQ   ADN   5  1
   7 3CWQ    ZER   SMO      5
  19 3DBB          STO   0  0(1)                [SET NULL,ACCESSED PARAMETER 
  19 3DW2          BCT   6  ZEQ                 [BRN IF COUNT NOT EXHAUSTED  
   7 3F*L          NGN   6  1
   7 3FT=          SMO      5
  16 3G#W          STO   6  1(1)                [SET END OF BLOCK
   8 3GSG          BRN      NTHP 
  21 3H#6    NT                                 [X5 IS NOW POINTER TO LAST NULL PARAM
   8 3HRQ          BCT   6  NPARA
   8 3J?B          LDX   2  FX2  
   9 3JR2          LDX   0  AWORK3(2)
  15 3K=L          BZE   0  NTHP                [NORMAL CASE 
   9 3KQ=          LDX   0  AWORK4(2)
   8 3L9W          ANDN  4  #7777
   8 3LPG          ADN   4  11   
   7 3M96          SRL   4  2
  19 3MNQ          BNZ   0  WRK4                [J IF CPB/CUNI BLOCK PRESENT 
  11 3N8B          SETUPCORE    4,3,CPB,CUNI 
   7 3NN2          LDN   6  1
  10 3P7L          STO   6  JPARNUM(3)   
   9 3PM=          STO   4  AWORK4(2)
   8 3Q6W          BRN      NOMOV
  10 3QLG    WRK4  HUNT     3,CPB,CUNI   
  10 3R66          LDX   0  ALOGLEN(3)   
   7 3RKQ          TXL   0  4
   8 3S5B          BCC      NOMOV
   7 3SK2          PHOTO    6
  10 3T4L          ALTLEN   3,4,CPB,CUNI 
   9 3TJ=          TESTMOVE 6,NOMOV  
  10 3W3W          HUNT     3,CPB,CUNI   
   9 3WHG    NOMOV CALL  7  SEARCH   
   7 3X36          SMO      5
   8 3XGQ          LDXC  4  0(1) 
   7 3Y2B          BCC      /
   9 3YG2          STO   4  ANUM(3)  
   8 3Y_L          LDX   0  GSIGN
   7 3_F=          SMO      5
  16 3_YW          ORS   0  0(1)                [MARK ACCESSED   
   8 42DG          BZE   4  START
   8 42Y6          ANDN  4  #7777
   7 43CQ          SMO      5
   8 43XB          LDN   6  1(1) 
   9 44C2          LDN   7  APARA(3) 
   7 44WL          SMO      4
  21 45B=          MVCH  6  0                   [MOVE FROM CALAS BLOCK TO CUNI BLOCK 
   8 45TW          BRN      START
   4 46*G    #   
  19 46T6    #           FIND THE FIRST PARAMETER IN THE ALAS BLOCK BEGINNING
  13 47#Q    #           WITH THE SPECIFIED STRING   
   4 47SB    #   
   9 48#2    STR1  LDX   4  ANUM(3)  
  16 48RL          SRL   4  12                  [PARAMETER TYPE  
   7 49?=          SBN   4  2
   7 49QW          SBN   6  1
  17 4==G          BNZ   6  STR2                [MORE THAN 2 CHARS   
  16 4=Q6          BZE   4  STR2                [J IF COMPOSITE  
   9 4?9Q          LDCH  6  APARA(3) 
   9 4?PB          TXU   6  XATCHR(1)
   8 4#92          BCS      PN4  
   7 4#NL          BCHX  3  /
  16 4*8=          STO   6  AWORK3(2)           [SET CALL BY NAME
   8 4*MW          BRN      SPB1 
   9 4B7G    STR2  LDX   6  AWORK4(2)
   8 4BM6          BNZ   6  XHIGH
   7 4C6Q          LDN   6  0
  18 4CLB          PARALYSE ,6                  [PARALYSE 1ST PARAMETER  
  21 4D62          TESTERR  UNPAIR,UP           [REPORT ERROR + END IF UNPAIRED BRACK
  10 4D*S ...      TESTREP  TOOMANY,XERR9
  16 4DKL          BZE   4  NONUM               [BRN IF COMPOSITE
   7 4F5=          SBN   4  1
  15 4FJW          BNZ   4  PN4                 [FORMAT ERROR
   7 4G4G          ADN   4  1
  19 4GJ6          PARAPASS 4                   [QUALIFIED; GET PRECEDING NO 
  10 4H3Q          HUNT     3,CPB,CUNI   
  16 4HHB          LDX   0  ANUM(3)             [CHARACTER COUNT 
   8 4J32          ANDN  0  #7777
   7 4JGL          LDN   5  0
   7 4K2=          LDN   6  0
   9 4KFW          LDCH  4  APARA(3) 
   9 4K_G          TXU   4  XATCHR(1)
   8 4LF6          BCS      XCDB 
  16 4LYQ          STO   4  AWORK3(2)           [SET CALL BY NAME
   8 4MDB          BRN      XCDB1
   4 4MY2    #   
  17 4NCL    #           CONVERT PRECEDING NO. TO BINARY IN X5 AND X6
   4 4NX=    #   
   9 4PBW    XCDB  CDB   5  APARA(3) 
  21 4PWG          BCS      XERR0               [NON NUMERIC CHARACTER IS ERRONEOUS  
   5 4QB6    XCDB1   
   7 4QTQ          BCHX  3  /
  21 4R*B          BCT   0  XCDB                [BRN IF CHARACTER COUNT NOT EXHAUSTED
  20 4RT2          BNZ   5  PN6                 [ERROR IF LARGE NUMBER SPECIFIED 
  10 4S#L          TXL   6  TWENTYFIVE(1)
   8 4SS=          BCC      PN6  
   8 4T?W          BNG   6  PN6  
   8 4TRG          BNZ   6  NO   
   7 4W?6          LDN   6  1
   4 4WQQ    NO  
   7 4X=B          LDN   7  2
   7 4XQ2          LDN   4  1
   8 4Y9L          PARALYSE ,4,7 
  10 4YFD ...      TESTREP  TOOMANY,XERR9
  19 4YP=          PARAPASS 7                   [GET RID OF SPACES IN STRING 
   8 4_8W          BRN      SRCH 
  20 4_NG    NONUM LDN   6  1                   [SUPPLY 1 FOR NO NO. SPECIFIED   
  19 5286          PARAPASS 6                   [GET RID OF SPACES IN STRING 
   9 52MQ    SRCH  CALL  7  SEARCH   
   9 537B          LDN   5  APARAFIR 
  10 53M2          HUNT     3,CPB,CUNI   
   9 546L          LDX   4  ANUM(3)  
  20 54L=          BZE   4  NCHA                [BRN IF NULL PARAMETER SPECIFIED 
   8 555W          LDN   4  #7777
   9 55KG          ANDS  4  ANUM(3)  
   8 5656          BRN      NUMCH
   4 56JQ    #   
  17 574B    #     QUALIFIED OR COMPOSITE PARAMETER AT A HIGH LEVEL  
   4 57J2    #   
   9 583L    XHIGH HUNT  3,CPB,CUNI  
  10 58H=          LDX   4  ALOGLEN(3)   
  19 592W          SETUPCORE 4,3,CPB,CUNI       [SET UP DUMMY FOR ERROR REPT 
  10 59GG          HUNT2OPT 2,CPB,CUNI,3 
   7 5=26          LDX   5  2
   7 5=FQ          CHAIN 3,2 
   7 5=_B          LDX   2  5
   8 5?F2          ADN   2  A1   
   8 5?YL          ADN   3  A1   
   7 5#D=          SMO      4
   7 5#XW          MOVE  2  0
   8 5*CG          SBN   3  A1   
   7 5*X6          LDN   5  0
   7 5BBQ          LDN   6  0
   8 5BWB          SMO      FX2  
   9 5CB2          STO   3  AWORK1   
   7 5CTL          LDX   2  3
   9 5D*=          LDX   0  ANUM(3)  
   8 5DSW          ANDN  0  #7777
   9 5F#G          LDCH  4  APARA(3) 
   9 5FS6          TXU   4  XATCHR(1)
   8 5G?Q          BCS      NOTAT
   8 5GRB          SMO      FX2  
   9 5H?2          STO   4  AWORK3   
   8 5HQL          BRN      XBCT 
   7 5J==    XBCH  BCHX  3  /
   5 5JPW    NOTAT   
   9 5K9G          LDCH  4  APARA(3) 
   9 5KP6          TXU   4  SPACE(1) 
   7 5L8Q          BCC   XBCT
   9 5LNB          CDB   5  APARA(3) 
   8 5M82          BCS      NOTEN
   8 5MML    XBCT  BCT   0  XBCH 
  18 5N7=    NOTEN BNZ   5  XERR8               [J IF NUMBER TOO LARGE   
  10 5NLW          TXL   6  TWENTYFIVE(1)
   8 5P6G          BCC      XERR8
   8 5PL6          BNG   6  XERR8
   8 5Q5Q          BNZ   6  NOT  
   7 5QKB          LDN   6  1
  16 5R52    NOT   BNZ   0  XCHARS              [J IF MORE CHARS 
   4 5RJL    NOT1
   9 5S4=          CALL  7  SEARCH   
   9 5SHW          LDN   5  APARAFIR 
   8 5T3G          BRN      NCHA 
   5 5TH6    XCHARS  
  21 5W2Q          TXU   4  PAREN(1)            [IS THE NEXT CHAR LEFT PARENTHESIS   
   8 5WGB          BCS      XERR8
   7 5X22          LDN   4  0
   8 5XFL          BRN      XBX  
  15 5X_=    XLP   LDCH  7  APARA(3)            [STORE THE   
  16 5YDW          TXU   7  SPACE(1)            [ENCLOSED STRING 
  17 5YYG          BCC      XBX                 [IN THE CUNI BLOCK   
   9 5_D6          TXU   7  XRPAR(1) 
   8 5_XQ          BCC      XEND 
   9 62CB          DCH  7  APARA(2)  
   7 62X2          ADN   4  1
   7 63BL          BCHX  2  /
   7 63W=    XBX   BCHX  3  /
   8 64*W          BCT   0  XLP  
  16 64TG          BRN      XERR8               [NO R PARENTHESIS
   8 65*6    XEND  BCT   0  XERR8
   8 65SQ          SMO      FX2  
   8 66#B          LDX  3  AWORK1
   9 66S2          FREECORE BPTR(3)  
   9 67?L          STO  4  ANUM(3)   
   8 67R=          BZE   4  NOT1 
   8 68=W          ADN   4  11   
   7 68QG          SRL   4  2
  10 69=6          ALTLEN   3,4,CPB,CUNI 
  10 69PQ          HUNT     3,CPB,CUNI   
  13 6=9B          PTYPE    CPB,CUNI,3,APARA,ANUM(3) 
   8 6=P2          BRN      SRCH 
   4 6?8L    #   
  21 6?N=    #           IDENTIFY PARAMETER REQUESTED BY COMPARISON WITH STRING GIVEN
  21 6#7W    NCP1  ADX   4  ANUM(3)             [NO OF CHARS IN ALAS PARAMETER IN X4 
   8 6#MG          BRN      NCP3 
   8 6*76    NCP2  SMO      FX2  
   9 6*LQ          LDX   1  AWORK1   
   7 6B6B          SMO      5
  21 6BL2          LDX   4  0(1)                [LET PT/N WORD OF NEXT ALAS PARAMETER
  17 6C5L          ANDN  4  #7777               [ISOLATE NO OF CHARS 
  18 6CK=    NCP3  ADN   4  7                   [ADD 1 AND ROUND DIVISION
   7 6D4W          SRL   4  2
  21 6DJG          ADX   5  4                   [POINTER TO PT/N WORD OF NEXT PARAMET
   7 6F46    NUMCH SMO      5
   8 6FHQ          LDX   4  0(1) 
   8 6G3B          SMO      FX1  
   9 6GH2          TXU   4  MINUS1   
  20 6H2L          BCS      OK3                 [BRN UNLESS END OF BLOCK REACHED 
   8 6HG=          BRN      XERR6
  20 6H_W    OK3   ANDN  4  #7777               [ISOLATE NO OF CHARS IN PARAMETER
  19 6JFG          SBX   4  ANUM(3)             [SBN NO OF CHARS IN STRING   
  20 6J_6          BNG   4  NCP1                [INCORRECT IF STRING>PARAMETER   
   8 6KDQ          SMO      FX2  
   9 6KYB          STO   4  ACOMMUNE1
  18 6LD2          LDX   7  ANUM(3)             [NO OF CHARS IN STRING   
  15 6LXL          LDX   2  3                   [CONSERVE 3  
   8 6MC=          SMO      FX2  
   9 6MWW          STO   1  AWORK1   
   7 6NBG    XCOMP SMO      5
  20 6NW6          LDCH  4  1(1)                [GET A CHAR FROM ALAS PARAMETER  
   8 6P*Q          SMO      FX1  
   8 6PTB          TXU   4  SPACE
  16 6Q*2          BCS      OK4                 [OK IF NOT SPACE 
   7 6QSL          BCHX  1  /
   7 6R#=          LDN   0  1
   8 6RRW          SMO      FX2  
   9 6S?G          SBS   0  ACOMMUNE1
   8 6SR6          BRN      XCOMP
   4 6T=Q    OK4 
  18 6TQB          LDCH  0  APARA(2)            [GET A CHAR FROM STRING  
   7 6W=2          ERX   0  4
  17 6WPL          BNZ   0  NCP2                [BRN IF DISSIMILAR   
   7 6X9=          BCHX  1  /
   7 6XNW          BCHX  2  /
  20 6Y8G          BCT   7  XCOMP               [BRN TO COMPARE ANOTHER CHARACTER
   8 6YN6          SMO      FX2  
   9 6_7Q          LDX   4  ACOMMUNE1
  20 6_MB          BNG   4  NCP2                [BRN IF TRUE COMPARISON NOT MADE 
  19 7272          BCT   6  NCP2                [REJECT IF NOT N'TH PARAMETER
   8 72LL          LDX   2  FX2  
   9 736=          LDX   1  AWORK1(2)
   9 73KW          LDX   4  AWORK3(2)
   8 745G          BZE   4  NTHP 
  10 74K6          HUNT     3,CPB,CUNI   
   9 754Q          LDX   4  ANUM(3)  
  21 75JB          ANDN  4  #7777               [NUMBER OF CHARS BEING SEARCHED FOR  
   7 7642          SMO      5
   8 76HL          LDX   6  0(1) 
   8 773=          ANDN  6  #7777
  18 77GW          SBX   6  4                   [NO OF CHARS REMAINING   
   7 782G          LDX   7  6
   9 78G6          STO   4  AWORK1(2)
   8 78_Q          ADN   6  11   
   7 79FB          SRL   6  2
   9 79_2          LDX   4  AWORK4(2)
   8 7=DL          BZE   4  SETUP
  10 7=Y=          TXU   6  ALOGLEN(3)   
   9 7?CW          BCC      XNOMOV   
   7 7?XG          PHOTO    4
  10 7#C6          ALTLEN   3,6,CPB,CUNI 
   9 7#WQ          TESTMOVE 4,XNOMOV 
  10 7*BB          HUNT     3,CPB,CUNI   
   9 7*W2          BRN      XNOMOV   
   5 7B*L    SETUP   
  11 7BT=          SETUPCORE    6,3,CPB,CUNI 
   7 7C#W          LDN   4  1
  10 7CSG          STO   4  JPARNUM(3)   
   9 7D#6          STO   4  AWORK4(2)
   5 7DRQ    XNOMOV  
   9 7F?B          STO   7  ANUM(3)  
   9 7FR2          CALL  7  SEARCH   
   9 7G=L          LDX   7  ANUM(3)  
   8 7GQ=          LDX   0  GSIGN
   7 7H9W          SMO      5
   8 7HPG          ORS   0  0(1) 
   8 7J96          BZE   7  START
   9 7JNQ          LDX   4  AWORK1(2)
   7 7K8B    XA    BCHX  1  /
   8 7KN2          BCT   4  XA   
   9 7L7L          STO   3  AWORK1(2)
   7 7LM=    XB    SMO      5
   8 7M6W          LDCH  6  1(1) 
   9 7MLG          DCH   6  APARA(3) 
   7 7N66          BCHX  1  /
   7 7NKQ          BCHX  3  /
   8 7P5B          BCT   7  XB   
   9 7PK2          LDX   3  AWORK1(2)
  13 7Q4L          PTYPE    CPB,CUNI,3,APARA,ANUM(3) 
   8 7QJ=          BRN      START
   4 7R3W    #   
  18 7RHG    #           COMPARE LENGTHS OF 'OLD' AND 'NEW' PARAMETERS   
   4 7S36    #   
   7 7SGQ    NTHP  SMO      5
  19 7T2B          LDX   4  0(1)                [PT/N WORD OF 'OLD' PARAMETER
  19 7TG2          HUNT     3,COMDAT,CSETP      [GET 'NEW' PARAMETER BLOCK   
   9 7T_L          LDX   0  ANUM(3)  
  18 7WF=          ANDN  4  #7777               ['OLD' LENGTH IN CHARS   
  18 7WYW          ANDN  0  #7777               ['NEW' LENGTH IN CHARS   
   7 7XDG          ADN   4  3
   7 7XY6          ADN   0  3
  18 7YCQ          SRL   4  2                   ['OLD' LENGTH IN WORDS   
  18 7YXB          SRL   0  2                   ['NEW' LENGTH IN WORDS   
  16 7_C2          SBX   0  4                   [X0='NEW'-'OLD'  
  21 7_WL          BZE   0  OKCPY               [BRN IF NO ADJUSTMENT OF LENGTH REQUI
   4 82B=    #   
  21 82TW    #           IF THE POINTERS IN APTBEPAR AND ADNOPARAL POINT TO A PARAMET
  19 83*G    #           AFTER THE ONE TO BE ALTERED, THEY MUST BE RESET .   
   4 83T6    #   
  21 84#Q          LDX   6  APTBEPAR(1)         [POINTER TO NEXT PARAMETER TO BE PASS
   8 84SB          SRL   67 12   
   7 85#2          TXL   5  6
  20 85RL          BCC      OK1                 [BRN IF LESS THAN POINTER IN X5  
  16 86?=          ADX   6  0                   [ADJUST POINTER  
   8 86QW          SLL   67 12   
  10 87=G          STO   6  APTBEPAR(1)  
  21 87Q6    OK1   LDX   6  ADNOPARAL(1)        [POINTER TO NEXT PARAMETER TO BE ANAL
   8 889Q          SRL   67 12   
   7 88PB          TXL   5  6
   8 8992          BCC      OK2  
  16 89NL          ADX   6  0                   [ADJUST POINTER  
   8 8=8=          SLL   67 12   
  10 8=MW          STO   6  ADNOPARAL(1) 
   4 8?7G    OK2 
   4 8?M6    #   
  17 8#6Q          LDX   6  ALOGLEN(1)          [ADDRESS OF LAST WORD
  18 8#LB          ADN   6  A1(1)               [IN OLD BLOCK +1 IN X6   
   7 8*62          SMO      5
  21 8*KL          LDN   7  1(1)                [START ADDRESS OF ^OLD^ PARAMETER +1 
  20 8B5=          ADX   7  4                   [END ADDRESS OF 'OLD' PARAMETER  
  19 8BJW          SBX   6  7                   [NO OF WORDS TO BE MOVED IN 6
  16 8C4G          NGS   0  AWORK1(2)           [SAVE DIFFERENCE 
  19 8CJ6          BPZ   0  XEXT                [NEED INCREASED BLOCK LENGTH 
  19 8D3Q          ADX   0  7                   [DESTINATION OF FIRST WORD   
   8 8DHB    #     BRIDGE THE GAP
  17 8F32    TRUNC BZE   6  NCATE               [J IF 512 MULTIPLE   
   8 8FGL          SBNC  6  512  
  18 8G2=          BCS      NLAST               [J IF < 512 WORDS LEFT   
   8 8GFW          MOVE  7  512  
   8 8G_G          ADN   7  512  
   8 8HF6          ADN   0  512  
  19 8HYQ          BRN      TRUNC               [MOVE IN BLOCKS OF 512 WDS   
   7 8JDB    NLAST SMO      6
  16 8JY2          MOVE  7  0                   [REMAINDE R IN   
  10 8KCL    NCATE LDX   6  ALOGLEN(1)   
   7 8KX=          LDX   7  1
   9 8LBW          SBX   6  AWORK1(2)
  15 8LWG          ALTLEN   7,6                 [CHOP END OFF
  14 8MB6          CALL  7  SEARCH              [RESET X1
   8 8MTQ          BRN      OKCPY
   4 8N*B    #   
  18 8NT2    #           EXTEND BLOCK AND MOVE DOWN SUBSEQUENT PARAMETERS
   4 8P#L    #   
  21 8PS=    XEXT  STO   5  3                   [STO POINTER TO START OF ^OLD^ PARAME
  10 8Q?W          LDX   5  ALOGLEN(1)   
  16 8QRG          ADX   5  0                   [NEW LOG LENGTH  
   7 8R?6          LDX   7  1
  15 8RQQ          ALTLEN   7,5                 [EXTEND BLOCK
  17 8S=B          CALL  7  SEARCH              [RELOCATE ALAS BLOCK 
  21 8SQ2          ADN   5  A1-1(1)             [ADDRESS OF LAST WORD IN EXTENDED BLO
   9 8T9L          LDX   4  AWORK1(2)
  21 8TP=          ADX   4  5                   [ADDRESS OF LAST WORD IN ORIGINAL BLO
  15 8W8W    MOVIT MOVE  4  1                   [COPY 1 WORD 
   7 8WNG          SBN   4  1
   7 8X86          SBN   5  1
   8 8XMQ          BCT   6  MOVIT
  16 8Y7B          LDX   5  3                   [RELOAD POINTER  
   4 8YM2    #   
  12 8_6L    #           COPY IN 'NEW' PARAMETER 
   4 8_L=    #   
   5 925W    OKCPY   
  11 92KG          MHUNT    3,COMDAT,CSETP   
   7 9356          SMO   5   
  20 93JQ          LDN   7  0(1)                [START ADDRESS OF 'OLD' PARAMETER
  19 944B          LDX   0  A1+1(3)             [PT/N WORD OF NEW PARAMETER  
   8 94J2          ANDN  0  #7777
   7 953L          ADN   0  7
   7 95H=          SRL   0  2
  19 962W          LDN   6  ANUM(3)             [PT/N WORD OF 'NEW' PARAMETER
   7 96GG          SMO      0
  21 9726          MOVE  6  0                   [INSERT 'NEW' PARAMETER IN PLACE OF '
   8 97FQ          LDCT  4  #400 
   7 97_B          SMO   5   
   8 98F2          ORS   4  0(1) 
   6 98YL    UP    ENDCOM
   4 99D=    #   
   9 99XW    PCHNUMERR      +CHNUMERR
   9 9=CG    PJSETP1        +JSETP1  
   9 9=X6    PAPFERR        +APFERR  
   9 9?BQ    PJPARMIS       +JPARMIS 
   9 9?WB    PJSETP8        +JSETP8  
   9 9#B2    PJSETP9        +JSETP9  
   9 9#TL    PADOCER        +ADOCER  
   9 9**=    PJSETP10       +JSETP10 
   9 9*SW    PJSETP11       +JSETP11 
   9 9B4N ...PJMAXPAR       +JMAXPAR 
   4 9B#G    #   
   4 9BS6    #   
  10 9C?Q    XERR0 LDX   7  PCHNUMERR(1) 
   8 9CRB          BRN      NGN  
  17 9D?2    XERR  LDX   7  PJSETP1(1)          [EXPRESSION INVALID  
   8 9DQL          BRN      NGN  
  18 9F==    XERR2 LDX   7  PAPFERR(1)          [PARAMETER FORMAT ERROR  
   8 9FPW          BRN      NGN  
  18 9G9G    XERR5 LDX   7  PJPARMIS(1)         [1ST PARAMETER MISSING   
  10 9GP6          LDX   6  PJSETP8(1)   
   8 9H8Q          BRN      XCOM 
  20 9HNB    XERR6 SMO      FX1                 [PARAMETER SPECIFIED NONEXISTENT 
   9 9J82          LDX   7  PJSETP9  
   7 9JML    NGN   NGN   6  1
   8 9K7=    XCOM  COMERRX  7,6  
  13 9KLW    #              CALL BY NAME FORMAT ERROR
   5 9L6G    XERR8   
  10 9LL6          LDX   7  PJSETP10(1)  
   7 9M5Q          BRN   NGN 
   5 9M9M ...XERR9   
  10 9M*J ...      LDX   7  PJMAXPAR(1)  
   8 9MFF ...      BRN      NGN  
  18 9MKB    #              NOT ENOUGH COMMAND LEVELS FOR CALL BY NAME   
   5 9N52    XERROR  
   8 9NJL          SMO      FX1  
   9 9P4=          LDX   7  PJSETP11 
   8 9PHW          BRN      NGN  
   9 9Q3G    XERRN LDX   4  AWORK4(2)
   8 9QH6          BZE   4  XERR5
   8 9R2Q          BRN   XERR8   
   9 9RGB    PN4   LDX   4  AWORK4(2)
   8 9S22          BZE   4  XERR2
   8 9SFL          BRN      XERR8
   9 9S_=    PN6   LDX   4  AWORK4(2)
   8 9TDW          BZE   4  XERR 
   8 9TYG          BRN      XERR8
   4 9WD6    #   
  10 9WXQ          MENDAREA 20,GAPSETPB  
   4 9XCB    #END
   8 ____ ...60262653000100000000
