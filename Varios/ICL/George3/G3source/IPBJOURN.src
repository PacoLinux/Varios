  15 22FL    #SEG  IPBJOURN                     [COMM - IPB  
  16 22_=    [ (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982   
   8 23DW    #OPT  K0IPBJOURN=1  
  14 23YG    #LIS  K0IPBJOURN>K0IPB>K0ALLGEO>K0GREATGEO  
   7 24D6          8HIPBJOURN
   4 24XQ    [   
  13 25CB          SEGENTRY  K1IPBJOURN,X1IPBJOURN   
  13 25X2          SEGENTRY  K2IPBJOURN,X2IPBJOURN   
   4 26BL    [   
  21 26W=    [     THIS SEGMENT IS ENTERED AT K1 TO ANALYSIS THE PARAMEPERS TO THE   
  15 27*W    [     COMMAND IPBJOURN . THE ACTIVITY IS A CPAT 
   4 27TG    [   
  21 28*6    [     K2 ENTRY POINT DEALS WITH THE MOVING OF JOURNAL INFORMATION FROM  
  21 28SQ    [     AMXOR AJOURN BLOCKS TO THE AMXOR AMISCJOURN BLOCK IN THE MISCELLAN
  21 29#B    [     CHAIN.  THE ACTIVITY IS AN AMOP AJOURN CREATED BY IPBSTART DURING 
  21 29S2    [     EMS.  THE ACTIVITY WILL HAVE BEEN STARTED BY EITHER THE AMOP IPBIN
  21 2=?L    [     OR THE AMOP IPBOUT ACTIVITIES WHEN THEY HAVE HANDLED DATA FROM OR 
  20 2=R=    [     TO THE DCP.  DEPENDING ON THE SETTING OF THE FIXED CORE WORD  
   6 2?=W     [- IPBJRN  
  20 2?QG    [     THE AMXOR AMISCJOURN MAY BE WRITTEN OUT TO THE SYSTEM JOURNAL.
   4 2#=6    [   
   9 2#PQ    PAPFERR        +APFERR  
  14 2*9B    ONMSK          #575620             [ON SPACE
   9 2*P2    PJSETP8        +JSETP8  
   9 2B8L    PJPARMIS       +JPARMIS 
  10 2BN=    PJTOOMANY       +JTOOMANY   
   4 2C7W    [   
   8 2CMG    [     SUBROUTINES   
   4 2D76    [   
   4 2DLQ    [   
   8 2F6B    [     K1  IPBJOURN  
   4 2FL2    [   
   6 2G5L    X1IPBJOURN  
  18 2GK=          PARANUMB 5                   [GET COUNT OF PARAMETERS 
   7 2H4W          SBN   5  2
  18 2HJG          BPZ   5  XJTOOMANY           [MORE THAN ONE PARAMETER 
   7 2J46          SPARAPAS  
  10 2JHQ          MHUNT    3,CPB,CUNI   
   9 2K3B          LDX   4  ANUM(3)  
  18 2KH2          BNG   4  T1                   [J. PARAMETER MISSING   
   8 2L2L          BZE   4  T1   
   8 2LG=          ANDN  4  #7777
   7 2L_W          SBN   4  4
  18 2MFG          BPZ   4  PFERR                [>3 CHARS : FORMAT ERROR
   9 2M_6          LDX   4  APARA(3) 
   7 2NDQ          SRL   4  6
  16 2NYB          ERX   4  ONMSK(1)            [COMPARE WITH ON 
   8 2PD2          BZE   4  ON1  
  17 2PXL          ERN   4  #1066                [COMPARE WITH OFF   
   8 2QC=          BZE   4  OFF1 
   5 2QWW    PFERR   
  10 2RBG          LDX   6  PAPFERR(1)   
   4 2RW6    XERR
   7 2S*Q          COMERRX  6
  14 2STB    T1    LDX   7  PJSETP8(1)          [FIRST   
  17 2T*2          LDX   6  PJPARMIS(1)          [PARAMETER MISSING  
   8 2TSL           COMERRX  6,7 
   6 2W#=    XJTOOMANY   
  10 2WRW          LDX   6  PJTOOMANY(1) 
   8 2X?G          BRN      XERR 
   8 2XR6    ON1   LDX   0  GSIGN
  19 2Y=Q          STO   0  IPBJRN              [SET FIXED CORE WORD NEGATIVE
   4 2YQB    XEND
   6 2_=2          ENDCOM
   4 2_PL    OFF1
  18 329=          STOZ     IPBJRN               [CLEAR FIXED CORE WORD  
   8 32NW          BRN      XEND 
   4 338G    [   
   8 33N6    [     K2  IPBJOURN  
   4 347Q    [   
   6 34MB    X2IPBJOURN  
  16 3572          HUNTMISB 2,AMXOR,AMISCJOURN  [DOES BLOCK EXIST
  16 35LL          BPZ   2  XHJOURN              [J BLOCK EXISTS 
  19 366=          SETNCORE 4095,2,AMXOR,AMISCJOURN [CREATE AMISCJOYRN BLOCK 
  10 36KW          STOZ     IPBCOUNT(2)  
   8 375G          LDX   3  BMISC
   8 37K6          CHAIN    2,3  
   5 384Q    XHJOURN 
  16 38JB          HUNTW   3,AMXOR,AJOURN       [HUNT DATA BLOCK 
  17 3942          BNG   3  XFIN                [J. NO DATA BLOCKS   
  14 39HL          LDX   0  GSIGN               [B0 = 1  
  21 3=3=          STO   0  IPBSW(2)            [SET B0 =1 OF IPBSW TO INDICATE ACT. 
  12 3=GW          HUNTMISB 2,AMXOR,AMISCJOURN   
  19 3?2G          LDX   0  ALOGLEN(2)          [LOGICAL LENGTH OF AMISCJOURN
  21 3?G6          SBX   0  IPBCOUNT(2)         [SUBTRACT AMOUNT OF AMISCJOURN USED  
  19 3?_Q          SBX   0  ALOGLEN(3)          [SUBTRACT AMXOR AJOURN LENGHT
  16 3#FB          BZE   0  XOUT                [NOT ENOUGH ROOM 
  15 3#_2          BNG   0  XOUT                [IN THE BLOCK
  21 3*DL          LDN   4  A1(3)               [LD. ADDR. OF DATA IN AJOURN BLOCK   
  16 3*Y=          LDN   5  IPBDATA(2)          [LD. ADDR OF DATA
  20 3BCW          ADX   5  IPBCOUNT(2)         [ADD ON DISPLACEMENT DOWN BLOCK  
  10 3BXG          SMO      ALOGLEN(3)   
  19 3CC6          MOVE  4  0                   [MOVE DATA INTO MISC BLOCK   
  10 3CWQ          LDX   4  ALOGLEN(3)   
  21 3DBB          ADS   4  IPBCOUNT(2)         [INCREASE COUNT BY AMOUNT IN AJOURN  
  14 3DW2                                       [BLOCK   
  20 3F*L          FREECORE 3                    [FREE OFF THE AMXOR AJOURN BLOCK
   9 3FT=          BRN      XHJOURN  
   4 3G#W    XOUT
  20 3GSG          LDX   0  IPBJRN              [LD. FIXED CORE WD TO INDICATE   
  20 3H#6          BZE   0  XCLEAR              [IF BLOCK SHOULD BE WRITTEN AWAY 
   8 3HRQ          OUTBLOCN  4096
  12 3J?B          SETNCORE  4095,2,AMXOR,AMISC  
  12 3JR2          HUNTMISB   3,AMXOR,AMISCJOURN 
   8 3K=L          LDN   4  A1(3)
   8 3KQ=          LDN   5  A1(2)
   8 3L9W          LDN   6  4095 
   8 3LPG          MOVLONG  4,5,6
   8 3M96          LDN   6  4095 
   7 3MNQ          SLL   6  2
  11 3N8B          OUTEXES  6,A1,AMXOR,AMISC 
   8 3NN2          MONOUT  IPBLOG
  10 3P7L          MFREEW    AMXOR,AMISC 
   8 3PM=          COOR3   #62   
  13 3Q6W                           [OUTPUT THE BLOCK
   5 3QLG    XCLEAR  
  12 3R66          HUNTMISB 2,AMXOR,AMISCJOURN   
  19 3RKQ          STOZ     IPBCOUNT(2)         [CLEAR DOWN PTR. DOWN BLOCK  
  19 3S5B          BRN      XHJOURN             [JUMP BACK TO DEAL WITH BLOCK
   4 3SK2    XFIN
   9 3T4L          LDX   0  FINISH   
  15 3TJ=          BNZ   0  XFINA               [J. IF FINISH
  21 3W3W          STOZ     IPBSW(2)            [CLEAR IPBSW TO INDICATE FINISH WITH 
  17 3WHG          COOR1                        [AWAIT TO BE STARTED 
  10 3X36          BRN      X2IPBJOURN   
   5 3XGQ    XFINA   
  18 3Y2B          SUICIDE                      [CLEAR AWAY THE ACTIVITY 
   4 3YG2    #END
   6 ____ ...416675630001
