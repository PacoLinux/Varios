  13 22FL          SEG   INCOM,3,MIKE PUTNAM,INCDUMP 
   4 22_=    #   
  11 23DW          SEGENTRY K1INCOM,X1INCOM  
  13 23NN ...      FSHENTRY K2INCOM,X2INCOM,,X2INCOM 
   4 23YG    #   
  21 24D6    #  THIS SEGMENT ANALYSES THE ^INCDUMP^ COMMAND & GIVES COMMAND ERRORS IF
  18 24XQ    #              1) NOT ISSUED FROM JOB RUNNING UNDER =DUMPER 
  15 25CB    #              2) 1ST PARAMETER MISSING OR NULL 
  15 25X2    #              3) 1ST PARAMETER NOT RECOGNISED  
  21 26BL    #   THE SEGMENT THEN TAKES ACTION ACCORDING TO THE 1ST PARAMETER AS FOLL
  17 26W=    #     DUMP     SEGMENT GOES ACROSS TO INCWAIT WITH X7=0 
  15 27*W    #     NOW      AS FOR DUMP, EXCEPT B0 OF X7 SET 
  20 27TG    #     RETURN   MAY HAVE A 2ND PARAMETER. TAKE ACTION AS FOLLOWS:-   
  21 28*6    #              A) IF ABSENT OR NULL, ASSUME ALL OBSOLETE TAPES TO BE RET
  19 28SQ    #                                              (X6 SET -IVE).   
  21 29#B    #              B) IF A +IVE NUMBER, MAX. NO. OF OBSOLETE TAPES TO RETURN
  18 29S2    #                                   TO THE POOL (SET IN X6).
  21 2=?L    #              C)IF MT ENTRANT DESCRIPTION, TAPE TO BE RETURNED IF OBSOL
  21 2=R=    #              IF 2ND PARAM. IS MT ENTRANT, 3RD AND SUBSEQUENT PARAMETER
  18 2?=W    #              MAY BE SPECIFIED BUT THEY MUST BE MT ENTRANTS
  21 2?QG    #                                            (TSNS PUT IN FINTER1 BLOCK)
  18 2#=6    #              IF 2ND & SUBSEQUENT PARAMETERS ARE INCORRECT,
  21 2#PQ    #              ERROR MESSAGES ARE GENERATED AND THEN THE FOLLOWING PARAM
  14 2*9B    #              ARE EXAMINED & DEALT WITH.   
  15 2*P2    #              GOES ACROSS TO INCOBS WITH X7=0. 
  21 2B8L    #     REUSE    SAME AS RETURN EXCEPT TAPES TO-BE-REUSED (INDICATED BY X7
  12 2BN=    #              INSTEAD OF RETURNED. 
  21 2C7W    #     QUEST    GOES ACROSS TO DTQUEST TO ASK OPERATORS ABOUT INCREMENTS 
  13 2CMG    #              WITH ALL TAPES WRONGED.  
  19 2D76    #     COMPRESS GOES ACROSS TO COMPRESS(ENTRY 2,FOR ODDREASONS)  
  18 2DLQ    #     RETRIEVE COUNTS ALL FREQ BLOCKS IN THE OFFLINE CHAUN. 
  20 2F6B    #              IT THEN OUTPUTS THE MESSAGE %A FILES BEING RETRIEVED 
   4 2FL2    #   
  20 2G5L    #  GIVES COMERR IF 1ST PARAM. IS DUMP OR QUEST AND THE JOB IS NOT   
   8 2GK=    #  SYSTEM-ISSUED.   
   4 2H4W    #   
   6 2HJG    #  CONSTANTS
   4 2J46    #   
   5 2J=B ...START   
   7 2JDL ...      FSHSKIP  B
   4 2JLW ...(   
   9 2JT6 ...               8HDUMP   
  21 2K3B                   +XINCWT             [LABEL TO BRANCH TO IF PARAM. IS DUMP
   8 2KH2                   8HNOW
   8 2L2L                   +XNOW
   4 2L=D ...)   
   9 2LG=                   8HRETURN 
   8 2L_W                   +XRET
   9 2MFG                   8HREUSE  
   8 2M_6                   +XREU
   7 2N6# ...      FSHSKIP  B
   4 2N?G ...(   
   9 2NDQ                   8HQUEST  
   8 2NYB                   +XQ  
   4 2P88 ...)   
  10 2PD2                   8HCOMPRESS   
  10 2PXL                   +XCOMPRESS   
  10 2QC=                   8HRETRIEVE   
   8 2QWW                   +XRV 
  21 2RBG                   +0,0,0              [SPARE FOR MENDING IN EXTRA PARAMETER
  20 2RW6                   +0,0,0              [1ST ZERO INDICATES END OF LIST  
   8 2S*Q                   +0   
   4 2STB    #   
   9 2T*2    ZDUMP          12HDUMPER
  17 2TSL    ZPAR           +JPARMIS            [PARAMETER MISSING   
   7 2T_S ...      FSHSKIP  B
   4 2W72 ...(   
  21 2W#=    ZNEG           +JNEGNO             [NEGATIVE NUMBER NOT ALLOWED IN %C   
  21 2WRW    ZUSE           +ERINUSE            [COMMAND BEING USED BY ANOTHER JOB   
   4 2X3N ...)   
  20 2X?G    ZINC           +INCDUMP            [THIS COMMAND NOT FOR GENERAL USE
  18 2XR6    ZERM           +ERMCH              [PARAMETER NOT RECOGNISED
   4 2Y=Q    #   
   7 2YQB    #  SUBROUTINE   
   4 2_=2    #   
  21 2_PL    #  CHECKS IF CURRENT JOB IS SYSTEM-ISSUED. GIVES A COMERR IF IT ISN'T.  
  15 329=    #  ON ENTRY,X2=FX2.            X4 IS S/R LINK.  
  11 32NW    #  ON EXIT, X2=FX2.    USES X0. 
   5 338G    XCHSYSIS
  18 33N6          BCONTEXT (4),2,SYSISS        [EXIT IF SYSTEM ISSUED   
   5 347Q    NOSYS   
  19 34MB          LDX   0  ZINC(1)             [COMMAND NOT FOR GENERAL USE 
   8 3572          BRN      XCOM 
   4 35LL    #   
   4 366=    #   
   5 36KW    X1INCOM 
  18 375G          FJOCA 3,2                    [X3-> CURRENT JOB BLOCK  
  21 37K6          TESTNAMX 3,JUSER(3),ZDUMP(1),NOSYS,2  [J IF JOB NOT RUN UNDER =DUM
  16 384Q          SPARAPAS                     [1ST PARAMETER   
   9 389Y ...      FSHCODE  A,X2INCOM
  19 38C6 ...X2INCOM                            [REENTRY POINT IN A MACHINE  
   9 38JB          MHUNT 2,CPB,CUNI  
   9 3942          LDX   3  ANUM(2)  
  18 39HL          BNG   3  NONE                [J IF PARAMETER MISSING  
  17 3=3=          BZE   3  NONE                [J IF PARAMETER NULL 
  20 3=GW          ANDN  3  #7777               [X3= NO. OF CHARS. IN PARAMETER  
   7 3?2G          SBN   3  9
  16 3?G6          BPZ   3  XER                 [J IF >8 CHARS.  
   8 3?_Q          ADN   3  12   
  20 3#FB          SRL   3  2                   [X3= NO. OF WORDS IN PARAMETER   
  18 3#_2          LDX   4  3                   [PRESERVE NO. OF WORDS   
   7 3*DL          SBN   1  3
   4 3*Y=    TEST
  20 3BCW          LDX   3  4                   [X3= NO. OF WORDS IN PARAMETER   
  19 3BXG          ADN   1  3                   [X1-> NTH PARAMETER IN LIST  
  21 3CC6          LDX   0  START(1)            [X0= 1ST WORD OF PARAMETER IN LIST   
  19 3CWQ          BZE   0  XER                 [J IF PARAMETER NOT IN LIST  
   4 3DBB    NEXT
   7 3DW2          SMO      3
  18 3F*L          LDX   0  -1+APARA(2)         [X0= NEXT WORD OF PARAM. 
   7 3FT=          SMO      3
  18 3G#W          BXU   0  -1+START(1),TEST    [J IF NOT THIS PARAMETER 
  18 3GSG          BCT   3  NEXT                [J IF MORE WORDS TO TEST 
  10 3GTN ...      FSHCODE  B,XFSHBTRAN  
   4 3GWW ...(   
  15 3GY4 ...#  INCDUMP CODE MUST ONLY BE OBEYED ON MACHINE A
  19 3G_= ...#    PARAMETERS 'DUMP','NOW' AND 'QUEST' FORBIDDEN ON MACHINE B 
  15 3H2D ...      TRANSBEG FSHINCDUMP,INCOM,2,CLB,,ACOMMUNE1
  16 3H3L ...      BRN      XFSHBEND            ['ENDCOM' RETURN 
  17 3H4S ...      BRN      XABANDCOM           ['ABANDCOM' RETURN   
  17 3H62 ...      BRN      XCOMBRKIN           ['COMBRKIN' RETURN   
   6 3H78 ...XFSHBTRAN   
   4 3H8B ...)   
   7 3H9J ...      FSHSKIP  B
   4 3H=Q ...(   
  20 3H#6          LDX   3  START+2(1)          [X3-> LABEL IN LIST TO BRANCH TO 
  17 3HRQ          FREECORE 2                   [FREE CPB/CUNI BLOCK 
   8 3J?B          ADX   3  FX1  
  17 3JR2          LDN   7  0                   [CLEAR MARKER WORD   
  21 3K=L          EXIT  3  0                   [BRANCH TO APPROPRIATE CODE TO DEAL W
  18 3KQ=                                       [   PARTICULAR PARAMETER 
   4 3L9W    #   
   4 3LPG    #   
   4 3M96    XNOW
  21 3MNQ          LDCT  7  #400                [SET ^GO TO DUMP IMMEDIATELY^ MARKER 
   5 3N8B    XINCWT  
  18 3NN2          CALL  4  XCHSYSIS            [CHECK SYSTEM-ISSUED JOB 
  18 3P7L          JBSS  XINUSE,,BIDCOM         [SET 'DUMP OPERATING' BIT
  19 3PM=                                       [UNLESS ALREDY SET WHEN ERROR
  21 3Q6W                                       [ENSURE NEVER 2 DUMPERS SIMULTANEOUSL
   6 3Q#4 ...#UNS FNSAD2 
  21 3QF= ...      MBC       ,BATCHSTART,BATCHFIN           [ NEITHER STARTEDNOR FINI
  21 3QLG          ACROSS   INCWAIT,1           [GO TO WAIT (IF NECESSARY) AND DUMP  
   4 3R66    #   
   4 3R*Y ...)   
   4 3RKQ    XREU
   7 3RQY ...      FSHSKIP  B
   4 3RY6 ...(   
  17 3S5B          LDN   7  1                   [SET ^REUSE^ MARKER  
   4 3S*8 ...)   
   4 3SK2    XRET
   7 3SQ8 ...      FSHSKIP  B
   4 3SXB ...(   
  21 3T4L          SPARAPAS                     [GET 2ND (OR SUBSEQUENT) PARAMETER   
   9 3TJ=          MHUNT 3,CPB,CUNI  
  10 3W3W          LDX   0  JPARNUM(3)   
   7 3WHG          SBN   0  3
  17 3X36          BPZ   0  PNOTNUM             [J IF >2ND PARAMETER 
  19 3XGQ          NGN   6  1                   [SET ^ALL^ MARKER OPTIONALLY 
   9 3Y2B          NGX   0  ANUM(3)  
  21 3YG2          BPZ   0  XALL                [J IF 2ND PARAMETER MISSING OR NULL  
  21 3Y_L                                       [   THEN ALL OBSOLETE TAPES TO BE REL
  19 3_F=          SETNCORE 0,2,FILE,FINTER1    [BLOCK TO PUT TSNS (IF ANY)  
  10 3_JB ...      MHUNT    3,CPB,CUNI   
   9 3_MG ...      LDCH  0  APARA(3) 
   8 3_QL ...      SBN   0  #30  
   9 3_TQ ...      BZE   0  NOTNUM   
  21 3_YW          CHNUMCON 1                   [SEE IF 2ND PARAM IS A NUMBER (NO ERR
   9 42DG          MHUNT 3,CPB,CUNI  
  19 42Y6          TESTREP2 CHNUMERR,NOTNUM     [J IF NOT VALID NUMBER FORMAT
  11 43CQ          MHUNTW   1,CPAR,GNUMCON   
  19 43XB          LDX   6  A1+1(1)             [X6= NO. OF TAPES TO RELEASE 
   7 44C2          FREECORE 1
  10 44WL          MFREEW   FILE,FINTER1 
  18 45B=          BNG   6  NEG                 [J IF NO. OF TAPES -IVE  
   4 45TW    XALL
  16 46*G          FREECORE 3                   [FREE CPB/CUNI   
  21 46T6          ACROSS   INCOBS,1            [GO & RETURN OR MARK AS NOT USED NO.O
  21 47#Q                                       [   MTS SPECIFIED (OR LESS) OR LIST O
  16 47SB                                       [   MTS SPECIFIED
   4 4848 ...)   
   6 48#2    XCOMPRESS   
   7 48F8 ...      FSHSKIP  B
   4 48LB ...(   
  20 48RL          ACROSS   COMPRESS,2           [ENTRY 2 FOR HISTORICAL REASONS 
   5 49?=    PNOTNUM 
   9 49QW          LDX   0  ANUM(3)  
  21 4==G          BZE   0  MFREE               [J IF 3RD (OR SUBSEQUENT) PARAMETER N
  17 4=Q6          BNG   0  XALL                [J IF NO MORE PARAMS.
   5 4?9Q    NOTNUM  
  16 4?PB          NAMETOP  3,FILE,FNAME        [RENAME UNI BLOCK
  21 4#92          FNORM                        [SEE IF PARAM. IS VALID ENTRANT DESCR
   9 4#NL          MHUNT 3,FILE,FNAME
  19 4*8=          NAMETOP  3,CPB,CUNI          [RENAME PARAM BLOCK TO UNI   
  21 4*MW          TESTREP2 NAMEFORM,MFREE      [J IF NOT CORRECT ENTRANT DESCRIPTION
  10 4B7G          MHUNT 2,FILE,FABSNB   
   9 4BM6          LDX   3  ATYPE(2) 
   8 4C6Q          ANDN  3  #2000
   9 4CLB          SMO      HDREC(2) 
  19 4D62          LDX   4  A1+1(2)             [X4= TSN SPECIFIED (IF ANY)  
  15 4DKL          FREECORE 2                   [FREE FABSNB 
  17 4F5=          BZE   3  XFINT               [J IF NOT MT ENTRANT 
  11 4FJW          MHUNTW   3,FILE,FINTER1   
  10 4G4G          LDX   5  ALOGLEN(3)   
  18 4GJ6          BZE   5  NOTSN               [J IF NO PREVIOUS TSNS   
   5 4H3Q    SLOOK   
   7 4HHB          SMO      5
  18 4J32          BXE   4  A1-1(3),MFREE       [J IF TSN ALREADY KNOWN  
  18 4JGL          BCT   5  SLOOK               [J IF MORE TSNS TO SEARCH
  10 4K2=          LDX   5  ALOGLEN(3)   
   5 4KFW    NOTSN   
   7 4K_G          ADN   5  1
  20 4LF6          ALTLEN   3,5,FILE,FINTER1    [LENGTHEN FINTER1 BLOCK BY 1 WORD
  11 4LYQ          MHUNTW   3,FILE,FINTER1   
   7 4MDB          SMO      5
  16 4MY2          STO   4  A1-1(3)             [INSERT NEW TSN  
   5 4NCL    MFREE   
   8 4NX=          MFREE CPB,CUNI
   8 4PBW          BRN      XRET 
   5 4PWG    XFINT   
  21 4QB6          ERROR AMISS,ERNOP            [OUTPUT ERROR MESSAGE ^SERIAL NUMBER 
   8 4QTQ          BRN      MFREE
   4 4R*B    #   
  21 4RT2    XQ                                 [SOME INCRS. HAVE ALL OF THEIR TAPES 
  21 4S#L                                       [WRONGED.  NEED TO ASK OPERATORS IF T
  18 4SS=                                       [WANT TO TAPERIGHT ANY.  
  19 4T?W          ACROSS   DTQUEST,1           [ENTER QUESTIONING SEGMENT   
   4 4TRG    #   
   4 4W3# ...)   
   4 4W?6    XRV 
   7 4WD# ...      FSHSKIP  B
   4 4WKG ...(   
  17 4WQQ          LDX   3  BOLC                [X3->IST BLOCK ON OLC
  16 4X=B          LDN   7  0                   [COUNT OF FREQS  
   5 4XQ2    RVLOOP  
  16 4Y9L          BXE   3  CXOL,RVEND          [J IF END OF OLC 
  16 4YP=          LDX   0  ATYPE(3)            [TYPE OF BLOCK   
   8 4_8W          SRL   0  12   
   9 4_NG          SBN   0  FILE+FREQ
  16 5286          BNZ   0  RVNEXT              [J IF NOT A FREQ 
  15 52MQ          ADN   7  1                   [INCR. COUNT 
   5 537B    RVNEXT  
  16 53M2          LDX   3  FPTR(3)             [LINK DOWN CHAIN 
   9 546L          BRN      RVLOOP   
   5 54L=    RVEND   
   9 555W          OUTPACK  7,1,NUMA 
  18 55KG          MONOUT   INCDUMPRV           [%A FILES BEING RETRIEVED
  10 55LR ...      FSHCODE  A,XFSHAEND   
   4 55N4 ...(   
   9 55P* ...      TRANSFIN ,,ALIEN  
   5 55QL ...XFSHAEND
   4 55RX ...)   
   4 55T8 ...)   
  10 55WF ...      FSHCODE  B,XFSHBEND   
   4 55XQ ...(   
   9 55_3 ...      BRN      (GEOERR) 
   5 562# ...XFSHBEND
   4 563K ...)   
   6 5656          ENDCOM
   4 56JQ    #   
   4 574B    #   
   7 579J ...      FSHSKIP  B
   4 57BQ ...(   
   5 57J2    XINUSE  
  21 583L          LDX   0  ZUSE(1)             [COMMAND BEING USED BY ANOTHER JOB   
   8 58H=          BRN      XCOM 
   4 58R4 ...)   
   8 592W    XER   SMO      FX1  
  18 59GG          LDX   0  ZERM                [PARAMETER NOT RECOGNISED
   8 5=26          BRN      XCOM 
   7 5=7# ...      FSHSKIP  B
   4 5=#G ...(   
  16 5=FQ    NEG   LDX   0  ZNEG(1)             [PARAMETER -IVE  
   8 5=_B          BRN      XCOM 
   4 5?98 ...)   
   4 5?F2    NONE
  17 5?YL          LDX   0  ZPAR(1)             [PARAMETER MISSING   
   4 5#D=    XCOM
  20 5#XW          COMERRX  0                   [GIVE APPROPRIATE COMMAND ERROR  
   4 5*CG    #   
  10 5*G_ ...      FSHCODE  B,XFSHBCOM   
   4 5*LD ...(   
   6 5*PX ...XABANDCOM   
   7 5*TB ...      ABANDCOM  
   6 5*YT ...XCOMBRKIN   
   7 5B4# ...      COMBRKIN  
   5 5B7R ...XFSHBCOM
   4 5B?= ...)   
   4 5BBQ    #   
   4 5BWB    #END
   8 ____ ...32650060000200000000
