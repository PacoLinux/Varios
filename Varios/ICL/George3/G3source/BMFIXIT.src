  15 22FL          SEG      BMFIXIT,84,EDWARD MOON,FILESTORE 
  21 22_=                                       [ THIS SEGMENT IMPLEMENTS RECOVERY   
  19 23DW                                       [ PROCEDURES FOR FILEBENT AND
  18 23YG                                       [ BLKTWICE CORRUPTIONS   
  12 245? ...      SEGENTRY K1BMFIXIT,Z1BMFIXIT  
  12 24=4 ...      SEGENTRY K2BMFIXIT,Z2BMFIXIT  
  12 24BT ...      SEGENTRY K3BMFIXIT,Z3BMFIXIT  
  12 24HL ...      SEGENTRY K4BMFIXIT,Z4BMFIXIT  
  18 25X2    NSIZRONG       +BMSIZEWRONG        [ SIZE INCONSISTENCY ETC.
  12 26BL    MDUBALBLK1     +BM2ALBLK1          [
  20 26W=    MDUBALBLK2     +BM2ALBLK2          [ HEADER MESSAGES FOR BLOCKTWICES
  12 27*W    M2ALHEAD       +BM2ALHEADS         [
  19 27TG    MFILERONG      +BMTWICEFILE        [ DETAILS OF FILE AND BLOCK  
   6 28*6    TYPEFTWICE  
  18 28SQ    #HAL +FI+FTWICE,0                  [ FI/FTWICE TYPE/SUBTYPE 
  19 29#B    SUB1                               [ SUBROUTINE TO HUNT FI/FONCE
  10 29S2          HUNTMISB 2,FI,FONCE   
   7 2=?L          EXIT  1  0
  20 2=CH ...SUB2                               [ SUBROUTINE TO HUNT FI/FTWICE   
  10 2=HD ...      HUNTMISB 2,FI,FTWICE  
   7 2=M* ...      EXIT  1  0
  19 2=R=    MONOUT                             [ SUBROUTINE TO DO MONOUTX   
   8 2?=W          SBX   6  FX1  
   7 2?QG          MONOUTX  7
   8 2#=6          ADX   6  FX1  
   7 2#PQ          EXIT  6  0
  19 2*9B    OUTPACK                            [ SUBROUTINE TO DO OUTPACK   
   8 2*P2          SBX   7  FX1  
   8 2B8L          OUTPACKX 4,5,6
   8 2BN=          ADX   7  FX1  
   7 2C7W          EXIT  7  0
  20 2CMG    XHUNTFTWICE                        [ SUBROUTINE TO HUNT FI/FTWICE   
  10 2D76          HUNTMISB 3,FI,FTWICE  
   7 2DLQ          EXIT  7  0
   6 2F6B    Z1BMFIXIT   
  21 2FL2                                       [ ENTRY POINT FOR BLOCKTWICE RECOVERY
  21 2G5L                                       [ ON FIRST PASS BLOCK NO.PUT IN FONCE
  21 2GK=                                       [ ON SECOND PUT IN FTWICE WITH NAME  
  20 2GP7 ...      LDX   5  ACOMMUNE5(2)        [COUNT OF BLOCKS NOT YET MAPPED. 
  20 2GT4 ...      LDX   4  ACOMMUNE1(2)        [PRESERVE ACOMMUNE1 FOR BLOCKMAP 
  20 2GY_ ...                                   [IN CASE STEPAGAIN COORDINATES.  
   7 2H4W          STEPAGAIN 
  10 2H#N ...      STO   4  ACOMMUNE1(2) 
  16 2HJG          LDX   6  ERESN(3)            [ RESIDENCE NO.  
   9 2HS# ...      BZE   6  MAPFCB   
  18 2JHQ ...      LDX   1  ACOMMUNE1(2)        [ NO. OF BLOCKS IN FILE  
  19 2K3B          ADN   1  ESERN(3)            [ ADDRESS OF FIRST BLOCK NO. 
  21 2KH2          SBX   1  5                   [ GET POINTER TO BLOCK BEING MAPPED  
  15 2L2L          LDX   4  0(1)                [ BLOCK NO.  
  13 2LG= ...      BSON     BMBLKTWICE,PUTINFTWICE   
  18 2L_W                                       [ J. IF ON SECOND PASS   
  15 2MFG          CALL  1  SUB1                [ HUNT FONCE 
  17 2M_6 ...      NGN   7  2                   [ INITIALISE COUNTER 
   5 2NDQ    NXTBLK  
   7 2NYB ...      ADN   7  2
  18 2PD2          BXGE  7  A1(2),PUTINFONCE    [ J. IF END OF USED SPACE
   7 2PXL          SMO      7
  18 2QC= ...      LDX   0  A1+1(2)             [ COMPARE RESIDENCE NOS. 
   7 2QWW          SBX   0  6
  16 2RBG          BNZ   0  NXTBLK              [ J. IF DIFFERENT
   7 2RW6          SMO      7
  17 2S*Q ...      LDX   0  A1+2(2)             [ COMPARE BLOCK NOS. 
   7 2STB          SBX   0  4
  20 2T*2          BZE   0  NALREADY            [ J. IF SAME NO. ALREADY THERE   
  16 2W#=          BRN      NXTBLK              [ J. IF DIFFERENT
  17 2WRW    PUTINFONCE                         [ IF ON FIRST PASS   
   8 2X?G          LDX   0  A1(2)
   7 2XR6          ADN   0  2
   8 2Y2Y ...      STO   0  A1(2)
  18 2Y=Q          BXL   0  ALOGLEN(2),NOLEN    [ J. IF BLOCK NOT FULL   
  10 2YQB          LDX   0  ALOGLEN(2)   
   8 2_=2          ADN   0  10   
  18 2_PL          ALTLENG  2,0,SUB1            [ OTHERWISE LENGTHEN IT  
   8 2__D ...      CALL  1  SUB1 
   5 329=    NOLEN   
   7 32NW          SMO      7
  16 338G ...      STO   4  A1+2(2)             [ STORE BLOCK NO.
   7 347Q          SMO      7
  17 34MB ...      STO   6  A1+1(2)             [ AND RESIDENCE NO.  
   5 3572    NALREADY
  19 35LL          UP                           [ TO CARRY ON WITH MAPPING   
  17 366=    PUTINFTWICE                        [ IF ON SECOND PASS  
   9 36KW ...      LDX   2  BSACHAPTR
  20 375G          POP      PUTINFTWICE,2,FTABWAIT [ LOCK OUT OTHER ACTIVITIES   
   7 37K6          BACKSPACE 
  16 384Q          STEPAGAIN                    [ TO NAME RECORD 
  18 38#J ...      STO   3  AWORK3(2)           [ SAVE PTR. TO NAME REC. 
  20 38JB ...      STO   6  AWORK4(2)           [ SAVE RESIDENCE NO. FOR LATER   
   5 38S8 ...REHUNT  
  12 3942          LDN   1  BMISC               [
  19 39HL    NEXT                               [ SEARCH MISCELLANEOUS CHAIN 
  19 3=3=          LDX   1  BPTR(1)             [ BACKWARDS FOR FI/FTWICES   
  20 3=GW          BXE   1  CXMI,SETUPFTWICE    [ END OF SEARCH AT BASE OF CHAIN 
   9 3?2G          LDX   0  ATYPE(1) 
   8 3?=# ...      SMO      FX1  
  17 3?G6          BXU   0  TYPEFTWICE,NEXT     [ J.IF NOT A FTWICE  
   7 3?WL ...      TOPFCB2  3
   9 3#?6 ...      LDX   5  FME1(3)  
  18 3#ML ...      LDX   6  FME2(3)           [ COMPARE USERNAME OF FILE 
   9 3*46 ...      LDX   7  FME3(3)  
   9 3*DL          TXU   5  FTUSE(1) 
  17 3*Y=          TXU   6  FTUSE+1(1)          [ WITH THAT IN FTWICE
  10 3BCW          TXU   7  FTUSE+2(1)   
  21 3BXG          BCS      NEXT                [J.IF DIFFERENT TO FIND NEXT FTWICE  
   9 3C7# ...      LDX   3  AWORK3(2)
   9 3CC6          LDX   5  ELOC1N(3)
  19 3CWQ          LDX   6  ELOC2N(3)           [ COMPARE LOCAL NAME OF FILE 
   9 3DBB          LDX   7  ELOC3N(3)
   9 3DW2          TXU   5  FTLOC(1) 
  17 3F*L          TXU   6  FTLOC+1(1)          [ WITH THAT IN FTWICE
  10 3FT=          TXU   7  FTLOC+2(1)   
  16 3G#W          BCS      NEXT                [J. IF DIFFERENT 
   9 3GSG          LDX   5  FTGEN(1) 
  20 3H#6          LDX   6  FTLAN(1)            [ COMPARE GENERATION AND LANGUAGE
   9 3HRQ          TXU   5  EGENN(3) 
  17 3J?B          TXU   6  ELANN(3)            [ WITH THOSE OF FILE 
  19 3JR2          BCS      NEXT                [ J.IF DIFFERENT ELSE THIS IS
  16 3K=L          LDX   0  ALOGLEN(1)          [THE ONE WE WANT 
  18 3KQ=          SBX   0  A1(1)               [CHECK IF LONG ENOUGH FOR
  15 3L9W          LDN   7  2                   [ EXTRA ENTRY
  15 3LD6 ...      BXL   7  0,NOSET2            [ J.IF IT IS 
   9 3LLB ...      CHAIN    1,BMISC+1
   9 3LSL ...      LDX   1  BMISC+1  
  10 3M2W ...      LDX   0  ALOGLEN(1)   
   7 3M96 ...      ADN   0  2
   9 3MCB ...      ALTLENG  1,0,SUB2 
   8 3MKL ...      CALL  1  SUB2 
   7 3MRW ...      LDX   1  2
   9 3N26 ...      BRN      NOSET2   
   6 3N8B    SETUPFTWICE 
  20 3NN2 ...      SETNCORE 15,1,FI,FTWICE      [ SET UP FTWICE IF ONE NOT FOUND 
   9 3P7L          LDN   0  FTRES-A1 
  18 3PM=          STO   0  A1(1)               [ INITIAL RECORD HEADER  
   9 3PN9 ...      CHAIN    1,BMISC+1
   7 3PP8 ...      TOPFCB2  1
   9 3PQ7 ...      LDN   5  FME1(1)  
   9 3PR6 ...      LDX   1  BMISC+1  
   9 3PS5 ...      LDN   6  FTUSE(1) 
  17 3PT4 ...      MOVE  5  3                  [ USERNAME INTO FTWICE
   7 3PW3 ...      STEPAGAIN 
   9 3PX2 ...      LDX   1  BMISC+1  
   9 3PX_ ...      LDN   5  ELOC1N(3)
   7 3PYY ...      ADN   6  3
  18 3P_X ...      MOVE  5  3                  [ LOCALNAME INTO FTWICE   
   9 3Q2W ...      LDX   0  EGENN(3) 
  16 3Q3T ...      STO   0  FTGEN(1)           [ AND GENERATION  
   9 3Q4S ...      LDX   0  ELANN(3) 
  17 3Q5R ...      STO   0  FTLAN(1)           [ AND LANGUAGE CODE   
   5 3Q6W    NOSET2  
   8 3QF? ...      LDX   2  FX2  
  18 3QRN ...      LDX   6  AWORK4(2)           [ RECOVER RESIDENCE NO.  
   7 3R66          LDN   0  1
   8 3RKQ          SMO      A1(1)
  17 3S5B ...      STO   6  A1(1)               [ STORE RESIDENCE NO.
  18 3SK2          ADS   0  A1(1)               [ UPDATE RECORD HEADER   
   8 3T4L          SMO      A1(1)
  16 3TJ= ...      STO   4  A1(1)               [ STORE BLOCK NO.
  18 3W3W          ADS   0  A1(1)               [ UPDATE RECORD HEADER   
   9 3W?N ...      LDX   2  BSACHAPTR
  15 3WHG          VOP      2,FTABWAIT          [ END LOCKOUT
  18 3X36          STEP                         [ BACK TO BLOCKS RECORD  
   5 3X=Y ...MAPFCB  
  17 3XGQ          UP                           [ TO CARRY ON MAPPING
  12 3Y2B                                       [
  19 3YG2    Z2BMFIXIT                          [ ENTRY POINT FOR ECOPS NOT =
  16 3Y_L                                       [ BLOCKS RECORD  
  21 3_9D ...                                   [ X5 = NO. OF BLOCKS FROM REC. HDR.  
  10 3_F= ...      LDX   0  ACOMMUNE2(2) 
   9 3_P4 ...      STO   0  AWORK4(2)
   7 3_YW          BACKSPACE 
  16 42DG          STEPAGAIN                    [ ON NAME RECORD 
  16 42Y6          JBS      NOGEOERR,3,BNWORK   [ J. IF WORKFILE 
   7 43=H ...      FREELINK  
  10 43JY ...      ACROSS   BMAPONE,12   
   5 43XB    NOGEOERR
   8 443? ...      SLL   5  15   
   7 4478 ...      ORN   5  1
   9 44?5 ...      STO   5  ECOPSN(3)
  13 44C2          MBS      3,BNBMUWB,BNTEMP,BNERASE 
  20 44WL          STEPREWRITE                  [ MARK TO BE ERASED IN BMAPTWO   
   9 45B=          LDN   4  ELOC1N(3)
   7 45TW          LDN   5  3
   7 46*G          LDN   6  4
  20 46T6          CALL  7  OUTPACK             [ OUTPACK LOCAL NAME OF ! FILE   
  10 47#Q ...      LDX   7  NSIZRONG(1)  
  17 47SB          CALL  6  MONOUT              [ MONOUT SIZE MESSAGE
   7 47XG ...      FINDLINK 2
  10 482L ...      BS       2,AW2PUTINFL 
  18 48RL          STEP                         [ TO BLOCKS RECORD AGAIN 
   9 48YS ...      LDX   0  AWORK4(2)
  10 4962 ...      STO   0  ACOMMUNE2(2) 
  17 49?=          UP                           [ TO MAP NEXT ENTRY  
  12 49QW                                       [
  21 4==G                                       [ ENTRY POINT TO CORRECT BLOCKTWICES 
  12 4=Q6                                       [
   6 4?9Q    Z3BMFIXIT   
  17 4?PB ...      SETNCORE 10,3,FILE,FABSNB    [ SET UP FABSNB FOR  
  17 4#92          LDN   0  10                  [ OPENING DIRECTORIES
   8 4#NL          STO   0  A1(3)
  16 4*8= ...      SETNCORE 3,3,BSTB,EMPTYB     [ SET UP EMPTYB  
  16 4*MW          LDN   0  3                   [ FOR GETBAXES   
   8 4B7G          STO   0  A1(3)
  10 4BM6 ...      LDX   7  MDUBALBLK1(1)
   9 4C6Q          CALL  6  MONOUT   
  10 4CLB ...      LDX   7  MDUBALBLK2(1)
   9 4D62          CALL  6  MONOUT   
  10 4DKL ...      LDX   7  M2ALHEAD(1)  
  16 4F5=          CALL  6  MONOUT              [ OUTPUT HEADINGS
  10 4FJW          CALL  7  XHUNTFTWICE  
   6 4G4G    NEXTFTWICE  
  10 4GJ6          MHUNT    2,FILE,FABSNB
   8 4GP# ...      LDN   0  10   
   8 4GWG ...      STO   0  A1(2)
   9 4H3Q          LDN   6  A1+1(3)  
   9 4HHB          LDN   7  A1+1(2)  
  18 4J32          MOVE  6  6                   [ FILENAME INTO FABSNB   
   7 4J88 ...      ADN   6  6
   7 4J*B ...      ADN   7  6
   7 4JGL          LDN   0  1
   7 4K2=          SMO      7
  15 4KFW          STO   0  0                   [ REEL NO.   
  16 4K_G          ADS   0  7                   [ UPDATE POINTER 
  14 4LF6          MOVE  6  2                   [ DETAILS
  16 4LYQ          OPENDIR  XBR,GENERAL         [ OPEN DIRECTORY 
  19 4NJR ...      READAGAIN                    [ READ BLOCKS RECORD OF FILE 
  10 4NX=          MHUNT    3,FILE,FRB   
  17 4PBW          NAMETOP  3,FILE,FWB          [ RENAME FRB TO FWB  
   9 4PWG ...      LDX   4  ERES(3)  
  10 4QB6          MHUNT    2,BSTB,EMPTYB
  17 4QTQ ...      STO   4  A1+1(2)             [ STORE RESIDENCE NO.
   8 4RT2          SMO      FX2  
  20 4S#L ...      STOZ     AWORK1              [ POINTER TO BLOCK NOS. IN FTWICE
   5 4SS=    NEXTSWAP
  20 4T?W          GETBAX                       [ GET A NEW BLOCK TO SWAP FOR OLD
  10 4TRG          CALL  7  XHUNTFTWICE  
   9 4W?6          SMO      AWORK1(2)
  19 4WQQ          LDX   6  FTBLK(3)            [ OLD BLOCK NO. FOR BACKREAD 
  12 4X=B ...      BACKREAD ,BSBS,RFAIL,EX4,EX6  
  10 4XQ2          MHUNT    3,BSTB,BREAD 
   9 4XR5 ...      BPZ   3  READOK   
   5 4XRL ...RFAIL   
   9 4XS8 ...      LDX   6  BA3(2)   
  11 4XT? ...      SETNCORE 512,3,BSTB,BREAD 
   8 4XWB ...      LDN   0  32   
   8 4XXF ...      STO   0  A1(3)
   8 4XYJ ...      LDN   0  #41  
   9 4X_M ...      STO   0  A1+1(3)  
   9 4Y2Q ...      LDN   0  A1+2(3)  
   9 4Y3T ...      LDN   0  A1+3(3)  
   9 4Y4Y ...      STOZ     A1+2(3)  
   8 4Y63 ...      MOVE  0  35   
   9 4Y76 ...      STO   6  BACK2(3) 
   5 4Y89 ...READOK  
  18 4Y9L          LDX   6  BACK2(3)            [ RECOVER OLD BLOCK NO.  
  10 4YP=          MHUNT    2,BSTB,FULLB 
  20 4_8W          LDX   5  A1+2(2)             [FIND NEW BLOCK NO. FROM GETBAX  
  19 4_NG          STO   5  BACK2(3)            [ SWAP NOS. IN BREAD BLOCK   
  18 5286          NAMETOP  2,BSTB,EMPTYB       [ RENAME FULLB FOR REUSE 
  10 52MQ          MHUNT    2,FILE,FWB   
   8 537B          ADN   2  ERES 
   5 53M2    NEXTNO  
  20 546L          ADN   2  1                   [ GET POINTER TO NEXT BLOCK NO.  
  20 54L=          BXU   6  0(2),NEXTNO         [ J. IF NOT DOUBLY ALL. BLOCK NO.
  19 555W          STO   5  0(2)                [ ELSE REPLACE WITH NEW NO.  
  19 55KG ...      BACKWRITE FREE,BSBS,,EX4,BACK2(3)    [ REWRITE NEW BLOCK  
   7 5656          LDN   0  2
  20 56JQ          ADS   0  AWORK1(2)           [ UPDATE POINTER TO BLOCK NOS.   
  10 574B          CALL  7  XHUNTFTWICE  
  19 57J2          LDN   0  FTBLK(3)            [ POINTER TO FIRST BLOCK NO. 
   8 583L          ADX   3  A1(3)
  18 58H= ...      ADN   3  A1+1                [POINTER TO END OF BLOCK 
  19 592W ...      SBX   3  AWORK1(2)           [ LESS NO.OF BLOCKS SWAPPED  
  19 59GG ...      BXL   0  3,NEXTSWAP          [ J. IF NOT ALL SWAPPED YET  
  16 5=26          REPLACE                      [ BLOCKS RECORD  
   9 5=9Y ...      MFREEW   FILE,FWB 
   7 5=FQ          BACKSPACE 
  16 5=QM ...      MHUNTW   3,FILE,ENT          [ TO NAME RECORD 
  10 5?3J ...      NAME     3,FILE,FWB   
  10 5?#F ...      BS       3,BEBLOCK2   
   7 5?KB ...      REWRITE   
  10 5?W? ...      MHUNTW   3,FILE,FWB   
  21 5#78 ...      JMBS     XCLOSE,3,BEONLINE,BEALLINCS,BEDUMP,BEINDEX   [WANT TO OUT
  21 5#D=                                                 [ALWAYSDUMP,KEEPONLINE SET 
  19 5#XW ...      LDX   0  EINC(3)                       [ OR IF NOT DUMPED 
   9 5*CG          BZE   0  XCLOSE   
   7 5BBQ          STOZ     0
  19 5BHY ...      DLA   0  ECOPS(3)                      [ ELSE PUT OFFLINE 
  10 5BP6 ...      BC       3,BEBLOCK2   
   7 5BW# ...      REWRITE   
   9 5C3G ...      MFREE    FILE,FWB 
   6 5C8N ...      READ  
  10 5C*W ...      MHUNTW   1,FILE,FRB   
  10 5CH4 ...      NAME     1,BSTB,FULLB 
   8 5CN= ...      FULLBPAS 1,2  
  19 5CTL          DELETE                        [ AND DELETE BLOCKS RECORD  
   5 5G?Q    XCLOSE  
  10 5GRB          CALL  7  XHUNTFTWICE  
   9 5H?2          LDN   4  FTUSE(3) 
   7 5HQL          LDN   5  3
   7 5J==          LDN   6  3
  15 5JPW          CALL  7  OUTPACK             [ USERNAME   
  10 5K9G          CALL  7  XHUNTFTWICE  
   9 5KP6          LDN   4  FTLOC(3) 
   7 5L8Q          LDN   5  3
   7 5LNB          LDN   6  4
  15 5M82          CALL  7  OUTPACK             [ LOCALNAME  
  10 5MML          CALL  7  XHUNTFTWICE  
   9 5N7=          LDN   4  FTGEN(3) 
   7 5NLW          LDN   5  1
   7 5P6G          LDN   6  5
  15 5PL6          CALL  7  OUTPACK             [ GEN. NO.   
  10 5Q5Q          CALL  7  XHUNTFTWICE  
   9 5QKB          LDN   4  FTLAN(3) 
   7 5R52          LDN   5  1
   7 5RJL          LDN   6  6
  15 5S4=          CALL  7  OUTPACK             [ LANGUAGE   
  10 5SHW          CALL  7  XHUNTFTWICE  
   9 5T3G          LDN   4  FTRES(3) 
   7 5TH6          LDN   5  1
   8 5W2Q          LDN   6  33   
  15 5WGB          CALL  7  OUTPACK             [ RES. NO.   
  10 5X22          CALL  7  XHUNTFTWICE  
   9 5XFL          LDN   4  FTBLK(3) 
   7 5X_=          LDN   5  1
   8 5YDW          LDN   6  42   
  15 5YYG          CALL  7  OUTPACK             [ BLOCK NO.  
  10 5_D6 ...      LDX   7  MFILERONG(1) 
  20 5_XQ          CALL  6  MONOUT              [ COMPLETED FILENAME + BLOCK NO. 
  10 62CB          CALL  7  XHUNTFTWICE  
  18 62X2          FREECORE 3                   [ FREE THIS FILES FTWICE 
  15 63W=          CLOSETOP                     [ DIRECTORY  
  10 64*W          CALL  7  XHUNTFTWICE  
  10 64TG          BPZ   3  NEXTFTWICE   
   8 6BL2          CALL  1  SUB1 
  14 6C5L          FREECORE 2                   [ FONCE  
  10 6CK= ...      MFREE    BSTB,EMPTYB  
  14 6D4W          MFREE    FILE,FABSNB         [ TIDY UP
   5 6DJG          UP
   6 6F46 ...Z4BMFIXIT   
   5 6GH2 ...NEXTWICE
  10 6H2L ...      CALL  7  XHUNTFTWICE  
  10 6HG= ...      BNG   3  TWICESGONE   
  16 6H_W ...      FREECORE 3                   [ FREE ALL TWICES
   9 6JFG ...      BRN      NEXTWICE 
   6 6J_6 ...TWICESGONE  
   5 6K=3 ...NEXTFWB 
  10 6KGY ...      HUNTMISB 3,FILE,FWB   
   9 6KRT ...      BNG   3  NOMORFWB 
  20 6L4Q ...      FREECORE 3                   [ FREE ALL FWB'S IN MISC. CHAIN  
   9 6L*M ...      BRN      NEXTFWB  
   5 6LLJ ...NOMORFWB
  10 6LXL ...      HUNTMISB 3,FI,FUSER   
   8 6MC= ...      LDX   0  A1(3)
  17 6MWW ...      ADN   0  A1(3)           [ PTR. TO END OF BLOCK   
   8 6NBG ...      LDN   2  A1(3)
   5 6NW6 ...NEXTONE 
  16 6P*Q ...      ADN   2  5              [  NEXT WORD TO TEST  
  15 6PTB ...      BXL   0  2,NOMOR        [  END OF BLOCK   
   8 6R#= ...      STOZ     0(2) 
   9 6T=Q ...      BRN      NEXTONE  
   5 6TQB ...NOMOR   
  10 6W=2 ...      HUNTW    3,FILE,FTAB  
   9 6WPL ...      BNG   3  NOWFTAPE 
   7 6X9= ...      FREECORE 3
  16 6XNW ...      BRN      NOMOR             [  FREE ALL FTABS  
   5 6Y8G ...NOWFTAPE
  10 6YN6 ...      HUNTMISB 3,FI,FTAPE   
   8 6_7Q ...      LDX   0  A1(3)
   8 6_MB ...      ADN   0  A1(3)
   9 7272 ...      LDN   2  A1+1(3)  
   8 72LL ...      LDCT  6  #400 
   5 736= ...NEXTAPE 
   9 73KW ...      BXL   0  2,NONLEFT
   8 745G ...      ORS   6  0(2) 
   8 74K6 ...      ERS   6  0(2) 
  18 754Q ...      ADN   2  4                  [   CLEAR ALL FOUND BITS  
   9 75JB ...      BRN      NEXTAPE  
   5 7642 ...NONLEFT 
  10 76HL ...      HUNT      3,BSTB,FBSIB
   9 773= ...      BNG   3  NOWSETMUP
  16 77GW ...      FREECORE 3                  [ FREE ALL FBSIBS 
   9 782G ...      BRN      NONLEFT  
   6 78G6 ...NOWSETMUP   
  17 78_Q ...      DOWN     BACKROOM,1          [  SET THEM UP AGAIN 
  10 79FB ...      HUNTMISB 3,FI,FLIST   
   8 79_2 ...      STOZ     A1(3)
   6 7=DL ...      REWIND
   8 7=Y= ...      REWIND     1  
  18 7?CW ...      REWIND     2               [ RESET FILES TO BEGINNING 
  18 7?XG ...      BSOFF    BMBLKTWICE,STARTAGAIN  [ I.E. J. IF PARTRES  
  10 7#C6 ...      HUNTMISB 3,FI,FONCE   
   8 7#WQ ...      LDX   1  FX1  
   8 7*BB ...      LDX   2  FX2  
   8 7*W2 ...      LDX   6  A1(3)
  17 7B*L ...      ADN   6  A1(3)               [ PTR.TO END OF BLOCK
   8 7BT= ...      LDN   3  A1(3)
   7 7C#W ...      LDN   4  1
   5 7CSG ...NEXTENT 
  20 7D#6 ...      LDN   5  3                   [ SET PSEUDO PTRS. FOR BLOCKMAP  
  18 7DRQ ...      LDX   0  1(3)               [ CHECK FOR RES.3 BLK.0   
   7 7F?B ...      SBN   0  3
  16 7FR2 ...      BNZ   0  MAPIT              [ J. IF NOT RES.3 
   8 7G=L ...      LDX   0  2(3) 
  20 7GQ= ...      ADN   3  1                  [ UPDATE PTR. FOR NOT MAPPING CASE
  15 7H9W ...      BZE   0  NOMAPIT            [ J. IF BLK.0 
  19 7HPG ...      SBN   3  1                  [ DECR. PTR. IF WE ARE MAPPING
   5 7J96 ...MAPIT   
   9 7JNQ ...      BLOCKMAP XBR,XBR  
   5 7K8B ...NOMAPIT 
   7 7KN2 ...      ADN   3  1
   7 7L7L ...      LDX   7  3
   7 7LM= ...      ADN   7  1
  16 7M6W ...      BXL   7  6,NEXTENT       [ J. IF NOT FINISHED 
   6 7MLG ...STARTAGAIN  
  10 7N66 ...      ACROSS     BMAPONE,1  
   4 8GFW ...XBR 
   9 8G_G ...      GEOERR   1,BMFIXIT
   4 8HF6 ...[   
   4 8HYQ ...#END
   6 ____ ...562066150002
