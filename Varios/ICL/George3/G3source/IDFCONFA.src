  15 228= ...#SEG  IDFCONFA8                     [A C PUTMAN 
  13 22?B ...# COPYRIGHT INTERNATIONAL COMPUTERS 1982
   4 22BG ...#   
  21 22JQ ...#           THIS SEGMENT CREATED NOV 81 BY SPLITTING OFF FROM IDFCONF   
   4 22R2 ...#   
   8 22_=    #OPT  K0IDFCONF=0   
  16 23DW    #LIS  K0IDFCONF>K0ALLGEO>K0GREATGEO>K0COMMAND>K0IDF 
   7 23YG ...      8HIDFCONFA
  12 24D6 ...      SEGENTRY K1IDFCONFA,X1IDFCONFA
  12 24XQ ...      SEGENTRY K2IDFCONFA,X2IDFCONFA
   4 2F6B    #   
  16 2FL2    #     SUBROUTINE TO SEARCH FOR UNIT/CI CHANNEL CELL 
   4 2G5L    #   
   4 2GK=    #   
   5 2H4W    ZMSEARCH
   8 2HJG          SBX   7  FX1  
  17 2J46          LDN   3  BIPB                [BASE OF IPB CHAIN   
   8 2JHQ          MSEARCH  2,3,4
   8 2K3B          SMO      FX2  
   9 2KH2          LDX   0  ACOMMUNE1
  20 2L2L          BZE   0  XERR                [J TO GEOERR IF UNIT NOT FOUND   
   9 2LG=          LDX   0  BRECNO(2)
   8 2L_W          SMO      FX2  
  19 2MFG          STO   0  AWORK3              [RECORD NO OF UNIT/CI CHANNEL
   8 2M_6          SMO      FX2  
  18 2NDQ          STO   3  AWORK4              [CELL PTR WITHIN RECORD  
   7 2NYB          SMO      2
  18 2PD2          LDX   5  BLINE(3)            [PTR TO IDENTIFIER CHAIN 
   8 2PXL          ADX   7  FX1  
   7 2QC=          EXIT  7  0
   4 2QWW    #   
  19 2QXK ...[  SUBROUTINE TO FIND REC + CELL PTRS FOR CELL DEFINED IN AW3   
   5 2QY# ...UNITNEW 
   8 2Q_3 ...      SMO      FX2  
   9 2Q_Q ...      LDX   3  AWORK3   
   4 2R2F ...[   
  10 2R38 ...[ FIND CELL DEFINED IN X3   
   4 2R3X ...[   
   7 2R4L ...NXTEC LDXC  0  3
   7 2R5* ...      SRL   0  9
   8 2R64 ...      ANDN  3  #777 
   9 2R6R ...      TXU   0  BRECNO(2)
   8 2R7G ...      BCC      (7)  
   8 2R89 ...      BRN      YNEW 
   4 2R8Y ...[   
   4 2R9M ...[   
  20 2R=B ...[                                    USE NXNEW IF X2 NOT IDF REC.   
   4 2R?5 ...[   
   7 2R?S ...NXNEW LDXC  0  3
   7 2R#H ...      SRL   0  9
   8 2R*= ...      ANDN  3  #777 
   4 2RBG    #   
  20 2RW6    #     SUBROUTINE TO SET X2 POINTING TO A1 OF FRRB CONTAINING RECORD 
  19 2S*Q    #     KNOWN TO BE IN CORE.ON ENTRY X0 CONTAINS RECORD NO REQD   
   4 2STB    #   
   4 2T*2    #   
   4 2TSL    YNEW
   8 2W#=          SMO      FX2  
   9 2WRW          STO   0  ACOMMUNE3
   7 2X?G          NEW      2
   7 2XR6          EXIT  7  0
   4 2Y96 ...[   
   4 2Y=Q    #   
   4 2YQB    #   
  19 2_=2    #     SUBROUTINE TO FIND CELL WHOSE IDF PTR IS IN X3 ON ENTRY.  
  19 2_PL    #     ON EXIT X2->A1 OF FRRB CONTAINING RECORD AND X3 CONTAINS  
  11 329=    #     CELL PTR WITHIN THE RECORD
   4 32NW    #   
   4 338G    #   
   5 33N6    NEXCELL 
  19 347Q          BPZ   3  YSAME               [J IF CELL IN CURRENT RECORD 
   8 34MB          SBX   7  FX1  
  15 3572          LDXC  0  3                   [REMOVE B0   
   7 35LL          SRL   0  9
   8 366=          LDX   2  FX2  
  19 36KW          STO   0  ACOMMUNE3(2)        [STORE NO OF NEXT RECORD REQD
  16 375G          MXB      2,NOFX12            [FIND NEXT RECORD
   8 37K6          ADX   7  FX1  
   5 384Q    YSAME   
  17 38JB          ANDN  3  #777                [RELATIVE CELL PTR   
   7 3942          EXIT  7  0
   4 39HL    #   
   4 3BCW    #   
   4 3BXG    #   
  11 3CC6    #     SUBROUTINE TO FREE A CELL 
   4 3CWQ    #   
   4 3DBB    #   
   5 3DW2    YFREE   
   8 3F*L          SBX   5  FX1  
  20 3FT=          MFREECELL 2,3                [PTR GIVEN TO NEXT CELL ON CHAIN 
   8 3G#W          ADX   5  FX1  
   7 3GSG          EXIT  5  0
   4 3H#6    #   
   4 3HRQ    #   
  21 3J?B ...#     SUBROUTINE TO FREE ALL CELLS ON A CHAIN.ON ENTRY/EXIT X2 IS PTR   
  19 3JR2    #     TO RECORD,AND X3 IS WD PTR WITHIN REC,OF THE CHAIN BASE   
  16 3K=L    #     X4 IS SET -VE IF AT LEAST ONE CELL IS FREED   
   4 3KQ=    #   
   4 3L9W    #   
   5 3LPG    XFREE   
   8 3M96          SBX   7  FX1  
   5 3MNQ    XFREEA  
   7 3N8B          SMO      2
   8 3NN2          LDX   0  0(3) 
  17 3P7L          BZE   0  XFREED              [J IF END OF CHAIN   
  15 3PM=          CALL  5  YFREE               [FREE A CELL 
  15 3Q6W          NGN   4  1                   [SET MARKER  
   9 3QLG          BRN      XFREEA   
   5 3R66    XFREED  
   8 3RKQ          ADX   7  FX1  
   7 3S5B          EXIT  7  0
   4 3SK2    #   
   4 3T4L    #   
  19 3TJ=    #     SUBROUTINE TO FREE ASSOCIATE AND ATTRIBUTE CELLS FOR AN   
  20 3W3W    #     IDENTIFIER(OR UNIT).ON ENTRY X3 CONTAINS IDF PTR TO IDENTIFIER
  18 3WHG    #     (OR UNIT) CELL.ON EXIT X2 IS PTR TO RECORD CONTAINING 
  18 3X36    #     IDENTIFIER OR UNIT AND X3 IS CELL PTR WITHIN RECORD   
   4 3XGQ    #   
   4 3Y2B    #   
   8 3YG2 ...ZATA  SBX   6  FX1  
   8 3Y_L ...      ADN   3  BASS 
  18 42DG          CALL  7  XFREE               [FREE ASSOCIATE CELL(S)  
   9 42Y6          ADN   3  BATT-BASS
  18 43CQ          CALL  7  XFREE               [FREE ATTRIBUTE CELL(S)  
   8 43XB          SBN   3  BATT 
   8 44C2          ADX   6  FX1  
   7 44WL          EXIT  6  0
   4 45TW    #   
  20 46*G    #     SUBROUTINE TO CHECK IF THE CELL,WHOSE IDF POINTER IS IN X3,   
  19 46T6    #     CORRESPONDS TO A DEVICE ON THE CONFIGURATION OF THE UNIT  
  17 47#Q    #     DEFINED BY AWORK1.EXIT 0 IF IT DOES,EXIT 1 IF NOT 
   4 47SB    #   
   4 48#2    #   
   6 48RL    RIGHTCONF   
   8 49?=          SBX   6  FX1  
  16 49QW          CALL  7  NEXCELL             [FIND DEVICE CELL
   8 4==G          ADX   6  FX1  
   7 4=Q6          ADX   3  2
  19 4?9Q          LDX   0  BNUMB(3)            [INDICATOR OF TYPE OF CELL   
   7 4?PB          ADXC  0  0
  18 4#92          BCC      NOTONCONF           [J IF 7020 INTERFACE CELL
  18 4#NL          LDEX  0  BTYPE(3)            [GEOG NO OF 7900 OR ZERO 
   8 4*8=          SMO      FX2  
  18 4*MW          LDXC  1  AWORK1              [MARKER OF TYPE OF ENTRY 
  18 4B7G          BCC      YIPBCONF            [J IF 7900 CONFIG CHANGE 
  20 4BM6          BNZ   0  NOTONCONF           [J IF CI CONFIG,DEVICE ON 7900   
  19 4C6Q          LDX   0  IPROPNO(3)          [PROG PROP NO IN IDENT CELL  
   8 4CLB          BRN      YTEST
   5 4D62    YIPBCONF
  20 4DKL          BZE   0  NOTONCONF           [J IF 7900 CONFIG,DEVICE ON CI   
  14 4F5=          LDEX  1  1                   [GEOG NO 
   5 4FJW    YTEST   
  18 4G4G          ERX   1  0                   [COMPARE GEOG OR PROP NOS
  15 4GJ6          ANDX  1  BITS22LS            [CLEAR B0,B1 
  16 4H3Q          BNZ   1  NOTONCONF           [J IF NO MATCH   
  10 4H4T ...      HUNTW    1,IDF,IDELETE
  15 4H5Y ...      BNG   1  ZVND         [ALL IDENTS CHANGED 
  18 4H73 ...      ORX   1  GSIGN             [SET MARKER FOR RIGHTCONF  
   9 4H86 ...      LDX   0  BNO(3)   
   5 4H99 ...ZDELP   
   7 4H=# ...      ADN   1  1
   8 4H?C ...      LDXC  7  A1(1)
  14 4H#G ...      BCS      /                  [UNSET B0 
   7 4H*K ...      SBX   7  0
   8 4HBN ...      BZE   7  ZVND 
  19 4HCR ...      BNG   7  ZDELP              [J TO CHECK NEXT IDELETE ENTRY
  16 4HDW ...      EXIT  6  1                 [IDENT NOT FOUND   
  21 4HF_ ...ZVND  BPZ   1  (6)                  [EXIT UNLESS CALLED FROM RIGHTCONF  
   8 4HHB          LDX   0  GSIGN
   8 4J32          SMO      FX2  
  19 4JGL          ORS   0  AWORK2              [SET CLUSTER CHANGE MARKER   
   7 4K2=          EXIT  6  0
  20 4KFW    NOTONCONF                          [DEVICE NOT ON RELEVANT CONFIG   
   7 4K_G          EXIT  6  1
   4 4LF6    #   
   4 4LYQ    #   
  20 4MDB    #     SUBROUTINE TO RE-FIND CELL CONTAINING 'CURRENT' ATTRIBUTION   
  20 4MY2    #     PTR,TO CLEAR THIS POINTER IF NECESSARY,AND TO UPDATE MARKER   
  19 4NCL    #     WORDS.LINK X1.BRANCHES TO XMOVEUP WHEN ALL ATTRIBUTIONS   
   9 4NX=    #     HAVE BEEN EXAMINED
   4 4PBW    #   
   4 4PWG    #   
   5 4QB6    XMISC   
   8 4QTQ          SMO      FX2  
   9 4R*B          LDX   3  AWORK4   
   7 4RT2          LDXC  0  3
   7 4S#L          SRL   0  9
  21 4SS=          CALL  7  YNEW                [FIND CELL WITH CURRENT ATTRIB PTR   
   8 4T?W          ANDN  3  #777 
  17 4TRG          ADX   3  2                   [ABSOLUTE CELL PTR   
  19 4W?6          BNZ   6  NOATCHANGE          [J IF ATTRIBUTION TO REMAIN  
   7 4WQQ          SMO      4
  17 4X=B          STOZ     0(3)                [CLEAR ATTRIBUTION   
   6 4XQ2    NOATCHANGE  
   7 4Y9L          SBN   5  1
  18 4YP=          BZE   5  XMOVEUP             [J IF ALL ATTRIBS CHECKED
   7 4_8W          ADN   4  1
  18 4_NG          ANDN  4  #7                  [UPDATE PTR WITHIN CELL  
   7 5286          EXIT  1  0
   4 529C ...#   
   4 52P3 ...#   
   4 #T9W ...#   
  17 #T=8 ...#     NEXT WE CLEAR OUT NOMINATIONS AND ATTRIBUTIONS FOR
  18 #T=* ...#     DEVICES ON THE OLD CONFIGURATION FROM CONSOLE PROPERTY
  18 #T=G ...#     CELLS AND CONSOLE PROPERTY ATTRIBUTION CELLS,MOVING UP
  10 #T=M ...#     POINTERS IF NECESSARY 
   4 #T=S ...#   
   4 #T=_ ...#   
   6 #T?3 ...X1IDFCONFA  
   6 #T?6 ...SNOMINIDF   
   7 #T?C ...      LDN   0  0
  16 #T?D ...      CALL  7  YNEW                [FIND RECORD ZERO
  19 #T?K ...      LDN   3  BEXOT(2)            [BASE OF CONSOLE PROP CHAIN  
   5 #T?Q ...NEXTPROP
  17 #T?X ...      LDX   3  0(3)                [NEXT CONS PROP PTR  
  19 #T#4 ...      BNZ   3  PROPFOUND           [J IF FURTHER CONSOLE PROP   
   7 #T#9 ...      LDN   0  0
  17 #T#B ...      CALL  7  YNEW                [RE-FIND RECORD ZERO 
   8 #T#H ...      SMO      FX2  
  18 #T#N ...      LDX   4  AWORK1              [PROG PROP NO OR GEOG NO 
  20 #T#T ...      BRN      YJOIN               [JUMP TO DEAL WITH IDENT CHAIN   
   6 #T*2 ...PROPFOUND   
   8 #T*7 ...      SMO      FX2  
  20 #T*# ...      STO   3  AWORK3              [KEEP IDF PTR TO CONS PROP CELL  
   8 #T*F ...      SMO      FX2  
  20 #T*L ...      STO   3  AWORK4              [PTR TO CELL WITH 1ST ATTRIB PTR 
  17 #T*R ...      CALL  7  NEXCELL             [FIND CONS PROP CELL 
   7 #T*Y ...      ADX   3  2
  20 #TB5 ...      LDX   5  BTYPE(3)            [PTR TO NOMINATED CONSOLE CELL   
  17 #TB= ...      BZE   5  NOCHANGE            [J IF CENTRAL CONSOLE
   7 #TBC ...      LDX   3  5
  21 #TBJ ...      CALL  6  RIGHTCONF           [CHECK IF NOM CONSOLE ON OLD CONFIG  
  17 #TBP ...      LDN   6  0                   [SET MARKER IF IT IS 
   8 #TBW ...      SMO      FX2  
  20 #TC3 ...      LDX   3  AWORK3              [IDF POINTER TO CONS PROP CELL   
   7 #TC8 ...      LDXC  0  3
  19 #TC* ...      SRL   0  9                   [RECORD NO OF CONS PROP CELL 
  19 #TCG ...      CALL  7  YNEW                [FIND CONS PROP RECORD AGAIN 
  18 #TCM ...      ANDN  3  #777                [CELL PTR WITHIN RECORD  
  20 #TCS ...      ADX   3  2                   [ABSOLUTE PTR TO CONS PROP CELL  
  19 #TC_ ...      BNZ   6  NOCHANGE            [NOM CONS TO STAY THE SAME   
  19 #TD6 ...      STOZ     BTYPE(3)            [CHANGE NOM CONS TO CENTRAL  
   5 #TD? ...NOCHANGE
  19 #TDD ...      LDX   5  BNUMB(3)            [NO OF CONS PROP ATTRIBUTIONS
  19 #TDK ...      BZE   5  NEXTPROP            [J FOR NEXT CONSOLE PROPERTY 
  20 #TDQ ...      LDN   4  5                   [PTR IN CELL TO NEXT ATTRIB PTR  
   5 #TDX ...NEXTATT 
   7 #TF4 ...      SMO      4
  16 #TF9 ...      LDX   3  0(3)                [NEXT ATTRIB PTR 
  17 #TFB ...      BZE   3  ZERR                [J IF ZERO TO GEOERR 
  20 #TFH ...      CALL  6  RIGHTCONF           [CHECK IF DEVICE ON OLD CONFIG   
  17 #TFN ...      LDN   6  0                   [SET MARKER IF IT IS 
  20 #TFT ...      CALL  1  XMISC               [CLEAR ATTRIB IF NEC,CHANGE PTRS 
  19 #TG2 ...      BNZ   4  NEXTATT             [J IF NEXT PTR IN SAME CELL  
  20 #TG7 ...      LDX   3  BLINE(3)            [PTR TO 1ST CONS PROP ATT CELL   
   6 #TG# ...NEXTATCELL  
   8 #TGF ...      BZE   3  ZERR 
   8 #TGL ...      SMO      FX2  
  20 #TGR ...      STO   3  AWORK4              [PTR TO NEXT CONS PROP ATT CELL  
  16 #TGY ...      CALL  7  NEXCELL             [FIND THIS CELL  
  15 #TH5 ...      ADX   3  2                   [ABS CELL PTR
  20 #TH= ...      LDN   4  1                   [PTR IN CELL TO NEXT ATTRIB PTR  
   6 #THC ...NEXTATTRIB  
   7 #THJ ...      SMO      4
  16 #THP ...      LDX   3  0(3)                [NEXT ATTRIB PTR 
   8 #THW ...      BZE   3  ZERR 
  20 #TJ3 ...      CALL  6  RIGHTCONF           [CHECK IF DEVICE ON OLD CONFIG   
   7 #TJ8 ...      LDN   6  0
  20 #TJ* ...      CALL  1  XMISC               [CLEAR ATTRIB IF NEC,CHANGE PTRS 
  19 #TJG ...      BNZ   4  NEXTATTRIB          [J IF NEXT PTR IN SAME CELL  
  20 #TJM ...      LDX   3  0(3)                [PTR TO NEXT CONS PROP ATT CELL  
  10 #TJS ...      BRN      NEXTATCELL   
   5 #TJ_ ...XMOVEUP 
   8 #TK6 ...      LDX   2  FX2  
  21 #TK? ...      LDN   3  BLINE               [REL PTR TO 1ST CONS PROP ATT CELL   
  20 #TKD ...      STO   3  ACOMMUNE1(2)        [FOR INITIAL CONS ATTRIB CELLS   
  19 #TKK ...      STO   3  ACOMMUNE2(2)        [FOR NEW CONS ATTRIB CELLS   
  18 #TKQ ...      STOZ     AWORK4(2)           [TO HOLD NO OF ATTS LEFT 
   9 #TKX ...      LDX   3  AWORK3(2)
   7 #TL4 ...      LDXC  0  3
   7 #TL9 ...      SRL   0  9
  18 #TLB ...      CALL  7  YNEW                [RE-FIND CONS PROP RECORD
   8 #TLH ...      ANDN  3  #777 
  19 #TLN ...      ADX   3  2                   [ABS PTR TO CONS PROP CELL   
  17 #TLT ...      LDX   6  BNUMB(3)            [NO OF ATTS INITIALLY
  17 #TM2 ...      BZE   6  NEXTPROP            [J FOR NEXT CONS PROP
  19 #TM7 ...      LDX   1  3                   [FOR INITIAL CONS PROP CELL  
  19 #TM# ...      LDN   4  5                   [PTR IN CELL TO NEXT ATT PTR 
   7 #TMF ...      LDN   5  5
   5 #TML ...XNEXT   
   7 #TMR ...      SMO      4
  17 #TMY ...      LDX   7  0(1)                [NEXT ATTRIBUTION PTR
  19 #TN5 ...      SBN   6  1                   [SUBTRACT FROM ATT WD COUNT  
  17 #TN= ...      BZE   7  TCLEARED            [J IF ATTRIB CLEARED 
   7 #TNC ...      SMO      4
  17 #TNJ ...      STOZ     0(1)                [CLEAR FORMER POINTER
   8 #TNP ...      ORX   7  GSIGN
   7 #TNW ...      LDXC  0  7
   7 #TP3 ...      SRL   0  9
  11 #TP8 ...      BXU   0  BRECNO(2),NOTREC 
  20 #TP* ...      ERX   7  GSIGN               [UNSET B0 IF PTR TO CURRENT REC  
   5 #TPG ...NOTREC  
   7 #TPM ...      SMO      5
  18 #TPS ...      STO   7  0(3)                [SET UP NEW ATTRIB PTR   
   7 #TP_ ...      LDN   0  1
   8 #TQ6 ...      SMO      FX2  
  18 #TQ? ...      ADS   0  AWORK4              [UPDATE NEW ATTRIB COUNT 
  19 #TQD ...      BZE   6  PROPEND             [J WHEN ALL ATTS DEALT WITH  
   7 #TQK ...      ADN   5  1
   8 #TQQ ...      ANDN  5  #7   
  18 #TQX ...      BNZ   5  TCLEARED            [J IF NO NEW CELL REQD   
   8 #TR4 ...      LDX   2  FX2  
  10 #TR9 ...      SMO      ACOMMUNE2(2) 
  19 #TRB ...      LDX   3  0(3)                [NEXT CONS PROP ATT CELL PTR 
  18 #TRH ...      STOZ     ACOMMUNE2(2)        [FURTHER PTR IN WORD 0   
   7 #TRN ...      LDXC  0  3
   7 #TRT ...      SRL   0  9
   8 #TS2 ...      CALL  7  YNEW 
   8 #TS7 ...      ANDN  3  #777 
  19 #TS# ...      ADX   3  2                   [ABS PTR TO NEXT NEW ATT CELL
  18 #TSF ...      LDN   5  1                   [NEXT ATT PTR IN WORD 1  
   5 #TSL ...TCLEARED
  19 #TSR ...      BZE   6  PROPEND             [J WHEN ALL ATTS DEALT WITH  
   7 #TSY ...      ADN   4  1
   8 #TT5 ...      ANDN  4  #7   
  18 #TT= ...      BNZ   4  XNEXT               [J IF NO NEW CELL REQD   
  18 #TTC ...      LDX   4  2                   [PRESERVE RECORD POINTER 
  18 #TTJ ...      LDX   2  FX2                 [OF NEW CONS ATTRIB CELL 
  10 #TTP ...      SMO      ACOMMUNE1(2) 
  19 #TTW ...      LDX   1  0(1)                [NEXT CONS PROP ATT CELL PTR 
  18 #TW3 ...      STOZ     ACOMMUNE1(2)        [FURTHER PTR IN WORD 0   
   7 #TW8 ...      LDXC  0  1
   7 #TW* ...      SRL   0  9
   8 #TWG ...      CALL  7  YNEW 
   8 #TWM ...      ANDN  1  #777 
  19 #TWS ...      ADX   1  2                   [ABS PTR TO NEXT OLD ATT CELL
  19 #TW_ ...      LDX   2  4                   [RESET X2->REC OF NEW CELL   
  18 #TX6 ...      LDN   4  1                   [NEXT ATT PTR IN WORD 1  
   8 #TX? ...      BRN      XNEXT
   5 #TXD ...PROPEND 
  18 #TXK ...      SBX   3  2                   [GET REL CELL PTR IN REC 
   8 #TXQ ...      SMO      FX2  
  19 #TXX ...      ADX   3  ACOMMUNE2           [ADD REL PTR TO NEXT ATT CELL
  20 #TY4 ...      CALL  7  XFREE               [FREE ATT CELLS NO LONGER REQD   
   8 #TY9 ...      LDX   2  FX2  
   9 #TYB ...      LDX   3  AWORK3(2)
  18 #TYH ...      LDX   6  AWORK4(2)           [NEW NO OF ATTRIBUTIONS  
   7 #TYN ...      LDXC  0  3
   7 #TYT ...      SRL   0  9
   8 #T_2 ...      CALL  7  YNEW 
   8 #T_7 ...      ANDN  3  #777 
  19 #T_# ...      ADX   3  2                   [ABS PTR TO CONS PROP CELL   
  16 #T_F ...      STO   6  BNUMB(3)            [NEW ATTRIB COUNT
   9 #T_L ...      BRN      NEXTPROP 
   4 #T_R ...#   
   4 #T_Y ...#   
  17 #W25 ...#     NEXT WE THROW AWAY ASSOCIATE AND ATTRIBUTE CELLS  
  19 #W2= ...#     AND THE OLD IDENTIFIER CELLS,SETTING UP A NEW IDENTIFIER  
  13 #W2C ...#     CHAIN FROM THE IDF/IREPORT BLOCK  
   4 #W2J ...#   
   4 #W2P ...#   
   5 #W2W ...YJOIN   
  19 #W33 ...      CALL  7  ZMSEARCH            [SEARCH FOR UNIT/CI CHANNEL  
   8 #W38 ...      LDX   1  FX2  
   9 #W3* ...      LDX   6  AWORK3(1)
   7 #W3G ...      SLL   6  9
   9 #W3M ...      ADX   6  AWORK4(1)
  19 #W3S ...      STO   6  AWORK3(1)            [SAVE IDF PTR TO UNIT CELL  
   8 #W3_ ...      ADN   3  BLINE
  20 #W46 ...      LDN   4  0                    [MARK NO ATTR OR ASSOC CHANGE   
   9 #W4? ...TONXI LDX   0  BRECNO(2)
   7 #W4D ...      SLL   0  9
   7 #W4K ...      ORX   0  3
   8 #W4Q ...      SMO      FX2  
  19 #W4X ...      STO   0  AWORK4               [SAVE PTR TO PREV IDENT CELL
   7 #W54 ...      SMO      2
   8 #W59 ...      LDX   3  0(3) 
  18 #W5B ...      BZE   3  ZENI                [J IF END OF IDENT CHAIN 
   9 #W5H ...TENID CALL  7  NEXCELL  
  18 #W5N ...      HUNTW    1,IDF,IDELETE      [REFIND IDELETE AFTER COOR
  19 #W5T ...      BNG   1  VREID               [J IF ALL IDENTS BEING FREED 
   7 #W62 ...      SMO      3
   9 #W67 ...      LDX   0  BNO(2)   
  17 #W6# ...      CALL  6  ZDELP              [SEE IF IDENT CHANGED 
  14 #W6F ...      BRN      ZINDL              [J IF SO  
  18 #W6L ...      BRN      TONXI              [ELSE GO ON TO NEXT IDENT 
   8 #W6R ...ZINDL LDXC  7  A1(1)
  17 #W6Y ...      BCC      VREID             [J IF CELL TO BE FREED 
   8 #W75 ...      CALL  6  ZATA 
  21 #W7= ...      BRN      TONXI            [ELSE FREE ASSOC+ATT AND GO TO NXT ID   
  15 #W7C ...VREID CALL  6  ZATA           [FREE ASSOC + ATT 
   8 #W7J ...      SMO      FX2  
   9 #W7P ...      LDX   3  AWORK4   
  15 #W7W ...      CALL  7  NXTEC            [FIND PREV CELL 
  16 #W83 ...      CALL  5  YFREE           [FREE CURRENT CELL   
   7 #W88 ...      LDX   5  2
  17 #W8* ...      ADX   5  3               [SAVE ADDR OF PREV CELL  
  15 #W8G ...      CALL  7  UNITNEW          [FIND UNIT CELL 
   7 #W8M ...      LDN   0  1
   7 #W8S ...      ADX   2  3
   9 #W8_ ...      SBS   0  BNUMB(2) 
  16 #W96 ...[                          UPDATE COUNT OF IDENTS   
   7 #W9? ...      SMO      5
  17 #W9D ...      LDX   3  0                [GET ADDR OF NEXT CELL  
  16 #W9K ...      BZE   3  VREDV              [J IF END OF CHAIN
  21 #W9Q ...      ORX   3  GSIGN            [NEXCELL MUST NOT ASSUME X2 IS IDF REC  
   8 #W9X ...      BRN      TENID
   7 #W=4 ...VREDV SMO      5
   8 #W=9 ...      LDX   0  BNO  
   8 #W=B ...      STO   0  5(2) 
  18 #W=H ...[                                   UPDATE HIGEST IDENT NO. 
   8 #W=N ...ZENI  LDX   2  FX2  
   8 #W=T ...      ANDX  4  GSIGN
  19 #W?2 ...      ORS   4  AWORK2(2)       [SET CLUS CHANGED MARK IF ASSOC  
  15 #W?7 ...[                               OR ATTS FREED   
   8 #W?# ...      LDCT  0  #200 
   9 #W?F ...      ANDX  0  AWORK2(2)
   9 #W?L ...      BZE   0  XFINISH  
  19 #W?R ...[                                    J IF NOT NEED TO FREE CELL 
   9 #W?Y ...      CALL  7  UNITNEW  
   5 #W#5 ...XDELETE 
   9 #W#= ...      LDX   6  BRECNO(2)
   7 #W#C ...      SLL   6  9
  21 #W#J ...      ADX   6  3                   [IDF PTR TO CI UNIT CELL (B0 CLEAR)  
   7 #W#P ...      LDN   0  0
  16 #W#W ...      CALL  7  YNEW                [FIND RECORD ZERO
  18 #W*3 ...      LDN   3  BIPB                [PTR TO BASE OF IPB CHAIN
   5 #W*8 ...YNEXUN  
   7 #W** ...      SMO      3
  16 #W*G ...      LDX   4  0(2)                [PTR TO NEXT UNIT
  18 #W*M ...      BZE   4  XERR                [J IF NOT FOUND TO GEOERR
  18 #W*S ...      LDXC  0  4                   [UNSET B0 IF NECESSARY   
   7 #W*_ ...      BCS      /
  19 #WB6 ...      BXE   0  6,UFOUND            [J IF PTR TO UNIT CELL FOUND 
  20 #WB? ...      BPZ   4  UNEXT               [J IF NEXT UNIT IN CURRENT RECORD
   7 #WBD ...      SRL   0  9
  21 #WBK ...      CALL  7  YNEW                [FIND REC CONTAINING NEXT UNIT CELL  
   5 #WBQ ...UNEXT   
  18 #WBX ...      LDEX  3  4                   [CELL PTR WITHIN RECORD  
   9 #WC4 ...      BRN      YNEXUN   
   5 #WC9 ...UFOUND  
   8 #WCB ...      CALL  5  YFREE
   4 #WCN ...#   
   6 #WX# ...X2IDFCONFA  
   5 *6D= ...ZENCN   
   5 **QL    XFINISH 
  10 *B2D ...      VFREEW   IDF,IDELETE  
   8 *B==          LDX   2  FX2  
   9 *BPW          LDX   6  AWORK1(2)
   9 *C9G ...      LDX   7  AWORK2(2)
   7 *D8Q          LDX   5  7
   9 *DNB          ANDX  5  BITS22LS 
  18 *F82          BNZ   5  XFINB               [J IF IDF TO BE LEFT OPEN
   5 *FML    XFINA   
   7 *G7=          IDFWRITE  
   6 *GLW          CLOSE 
   5 *H6G    XFINB   
  15 *HL6          BPZ   6  XFINI               [J IF 7900   
  20 *J5Q          BNG   7  XOVER               [J IF CLUSTER CHANGE MARKER SET  
  18 *JKB          UPPLUS   2                   [NO CLUSTERING CHANGES   
   5 *JP? ...XFINI   
   5 *K52    XOVER   
  17 *KJL          UPPLUS   1                   [CLUSTERING CHANGES  
  10 *L4= ...XERR  GEOERR   1,UNITILL1   
  10 *MH6    ZERR  GEOERR   1,IDFERROR   
   4 *MJN ...[   
   4 *NGB    #END
   6 ____ ...674342070001
