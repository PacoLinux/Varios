  17 22FL ...      SEG      CONTINUE,,CENT(COMMAND PROCESSOR),,G400  
   4 22_= ...[   
  15 23DW ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23YG ...[   
   8 24D6    [ THE ENTRY POINTS  
  19 24XQ    [     THESE ENTRY POINTS MUST BE KEPT FIXED WITHIN THE SEGMENT  
   6 254Y ...#UNS G400   
  16 25=6 ...#      THIS CHAPTER IS MODIFIED FOR G3PLUS-IH MK2   
  12 25CB          SEGENTRY K1CONTINUE,QENTRY1   
   9 25X2          BRN      (GEOERR) 
  19 26BL          SEGENTRY K3CONTINUE,QENTRY3                [ENTRY FROM DC 
   4 26W=    [   
  20 27*W          SEGENTRY K4CONTINUE,QENTRY4               [ENTRY FROM COMPROB 
  20 27TG          SEGENTRY K5CONTINUE,QENTRY5               [ENTRY FROM COMPROB 
  20 28*6          SEGENTRY K21CONTINUE,QENTRY21             [RE-ENTRY FROM HLS  
  20 28SQ          SEGENTRY K22CONTINUE,QENTRY22             [RE-ENTRY FROM HLS  
   4 29#B    [   
  21 29S2    #     THIS SEGMENT CONTAINS THE ENTIRE CODING FOR THE CONTINUE COMMAND. 
  21 2=?L    #     IT DELETES ALL COMMAND PROCESSOR LEVELS UP TO AND INCLUDING THE   
  19 2=R=    #     TOP BREAK IN LEVEL,CLOSING DOWN ANY MACRO DEFINITION FILES
  19 2?=W    #     ASSOCIATED WITH THESE LEVELS.RETURN IS THEN MADE TO THE   
  20 2?QG    #     COMMAND PROCESSOR SO THAT THE JOB MAY BE RESUMED AT THE POINT 
  12 2#=6    #     AT WHICH IT WAS BROKEN IN ON  
   4 2#PQ    [   
   5 2*9B    QENTRY3 
  10 2*P2    [     ENTRY FROM DISCONNECT 
  17 2B8L          STOZ     AWORK1(2)            [INDICATE NO PARAM  
   8 2BN=          BRN      XK71 
   4 2C7W    [   
   5 2CMG    QENTRY1 
  10 2D76          SEGENTRY K70CONTINUE  
  20 2DLQ          BRN      XK71                [MENDED TO NULL IF REPORTNEST ON 
  18 2F6B          STOZ     AWORK1(2)           [INDICATE NO CA PARAMETER
  16 2FL2          PARAPASS                     [OBTAIN PARAMETER
  10 2G5L          MHUNT    3,CPB,CUNI   
   9 2GK=          LDX   6  ANUM(3)  
  15 2H4W          BNG   6  XK71                [NO PARAMETER
   8 2HJG          BZE   6  XK71 
   9 2J46          LDX   6  APARA(3) 
   8 2JHQ          ORN   6  #7777
   8 2K3B          ERN   6  #7777
   9 2KH2          TXU   6  XCA(1)   
  19 2L2L          BCS      XERROR              [PARA NOT ^CA^ - FORMAT ERROR
  21 2LG=          STO   6  AWORK1(2)           [SET MARKER NON-ZERO - REPORT MASK   
  21 2L_W                                       [TO BE RETAINED FROM TOP B-I LEVEL   
   4 2MFG    XK71
  10 2M_6          SEGENTRY K71CONTINUE  
  19 2NDQ          LDX   5  EXT+10(2)           [LOAD BREAK IN TYPE AND LEVEL
   8 2NYB          ANDN  5  #7777
   8 2PD2          CLEANUP  5,3  
  21 2PXL          LDX   3  EXT+3(2)            [COUNT OF INT ISS LEVELS CLEARED &UT 
   8 2QC=          BZE   3  NOII 
   5 2QWW    NAGAIN  
  20 2RBG          FREELINK                     [FREE LINK FOR INT ISS. COMMAND  
   9 2RW6          BCT   3  NAGAIN   
   4 2S*Q    NOII
  10 2STB          MHUNTW   2,CPB,CALAS  
   9 2T*2          LDX   3  FPTR(2)  
  19 2TSL          LDX   6  CPBFSD(2)           [LOAD ORIGINAL BREAK IN TYPE 
   8 2W#=          LDX   1  FX2  
   9 2WRW          DCH   6  EXT+10(1)
   4 2X?G    [   
  10 2XR6          SEGENTRY K72CONTINUE  
  20 2Y=Q          BRN      XK73                [MENDED TO NULL IF REPORTNEST ON 
   9 2YQB          LDX   6  AWORK1(1)
  17 2_=2          BZE   6  XK73                [NEST REPORTING LEVEL
  21 2_PL          LDX   6  CMXMSK(2)           [RETAIN REPORTING MASK FROM TOP B-I  
  14 329=          STO   6  AMXMSK(1)           [LEVEL   
   4 32NW    XK73
  10 338G          SEGENTRY K73CONTINUE  
   7 33N6          FREECORE 2
   7 347Q          FREECORE 3
   9 34MB          LDX   6  MASKC(1) 
  10 3572          ERS   6  CONTEXT(2)   
   7 35LL          SBN   5  1
  16 366=          STO   5  CPLEV(2)            [SET NEW CPLEVEL 
  10 36KW          MHUNTW   3,CPB,CALAS  
   4 375G    [   
  10 37K6          SEGENTRY K74CONTINUE  
  20 384Q          BRN      XK75                [MENDED TO NULL IF REPORTNEST ON 
   9 38JB          LDX   6  AWORK1(2)
   8 3942          BZE   6  NOCA 
  19 39HL          LDX   6  AMXMSK(2)           [RETAIN REPORT MASK FROM TOP 
  15 3=3=          STO   6  CMXMSK(3)           [B-I LEVEL   
   8 3=GW          BRN      XK75 
   4 3?2G    NOCA
  21 3?G6          LDX   6  CMXMSK(3)           [NEST REPORTING LEVEL I.E. REVERT TO 
  18 3?_Q          STO   6  AMXMSK(2)           [MASK FROM NEXT LEVEL UP 
   4 3#FB    XK75
  10 3#_2          SEGENTRY K75CONTINUE  
   6 3*68 ...#UNS G400   
   7 3*?B ...      RVHOOK   3
  17 3*DL          LDX   5  CMFMSK(3)           [ COPY TRACING LEVEL 
  15 3*Y=          STO   5  AMFMSK(2)           [ INTO CPAT  
   6 3BCW    #SKI  CALLS 
   4 3BXG    (   
   7 3CC6          FJOCA    3
   8 3CWQ          CLEARPROG    3
   4 3DBB    )   
   8 3DW2          BRN      TCONT
   5 3F*L    QENTRY21
  11 3FT=          HLSINFRT SJBRKIN,STOPPED  
   5 3G#W    SJBRKIN 
   9 3GSG          LDCH  4  EXT+10(2)
   7 3H#6          LDN   0  2
   9 3HRQ          DCH   0  EXT+10(2)
   9 3J?B          BRN      STOPJB   
   5 3JR2    QENTRY4 
  17 3K=L          HLSINFORM SAVEABLE,STOPPED,JOBNO(2),,,,XSTLINK(1) 
  20 3KQ=    QENTRY5                            [ FROM CONTINUE - END OF BREAKIN 
   5 3L9W    TCONT   
   9 3LPG          LDCH  4  EXT+10(2)
  18 3M96          LDCT  0  #020                [ CLEAR BREAKIN MARKER   
   9 3MNQ          STO   0  EXT+10(2)
   5 3N8B    STOPJB  
   9 3NN2          TXU   4  PHIVE(1) 
   8 3P7L          BCS      PACK 
  10 3PM=          LDXC  0  JOBEVENTS(2) 
   9 3Q6W          BCS      PBRKIN   
  19 3QLG          TICKLER  JOBNO(2)            [LIFT ANY MP SUSPENSIONS...  
  20 3R66    [                                  [(RELIED ON BY OP'S MOP BREAK IN)
   8 3RKQ          LDN   6  #4000
  10 3S5B          ANDX  6  CONTEXT(2)   
  17 3SK2          BNZ   6  RUN                 [J IF FULLY STARTED  
  17 3T4L          HLSINFORM SAVEABLE,FSTART,JOBNO(2),,,,XFSLINK(1)  
   8 3TJ=          BRN      RUN  
   4 3W3W    )   
   5 3WHG    PBRKIN  
   7 3X36          PROGBRKIN 
   5 3XGQ    QENTRY22
  10 3Y2B          HLSINFRT RUN,FSTART   
   4 3YG2    RUN 
  12 3Y_L          FREEBUFF    [ FREEBUFF CPAT   
  13 3_F=          HLSINFORM RUNBK,RESUME,JOBNO(2)   
   5 3_YW    RUNBK   
  16 42DG          RUNPROG                      [RE ENTER PROGRAM
   4 42Y6    PACK
   9 43CQ          TXU   4  SIX(1)   
   8 43XB          BCS      PAK  
   6 44C2          ENDCOM
   9 44WL    PAK   TXU   4  THREE(1) 
   8 45B=          BCS      READA
  10 45TW          LDXC  0  JOBEVENTS(2) 
   8 46*G          BCS      READA
   8 46T6          MHUNTW   2,CLB
  10 47#Q          LDX   4  ANUMCHA(2)   
  10 47SB          STO   4  APARANUM(2)  
   9 48#2          ACROSS   ALASB,1  
   5 48RL    READA   
   9 49?=          ACROSS   COMPRO,10
   4 49QW    [   
   4 4==G    [   
   5 4=Q6    XSTLINK 
  10 4?9Q          LINK    CONTINUE,21   
   5 4?PB    XFSLINK 
  10 4#92          LINK     CONTINUE,22  
   4 4#NL    [   
   9 4*8=    MASKC          #60000   
   4 4*MW    [   
   8 4B7G    THREE          +3   
   8 4BM6    PHIVE          +5   
   8 4C6Q    SIX            +6   
   4 4CLB    [   
  20 4D62    PSERR          +APFERR             [ PARAMETER FORMAT ERROR MESSAGE 
   5 4DKL    XERROR  
   9 4F5=          LDX   5  PSERR(1) 
   7 4FJW          NGN   6  1
   8 4G4G          COMERRX   5,6 
   4 4GJ6    #   
  13 4H3Q    #     DATA CONSTANT FOR NESTED REPORT   
   9 4HHB    XCA            4HCA00   
   4 4J32    #   
  11 4JGL          MENDAREA 20,K99CONTINUE   
   4 4K2=    #END
   6 ____ ...054242530002
