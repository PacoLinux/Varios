  15 22FL    #LIS K0WHATPERB>K0ALLGEO>K0GREATGEO>K0COMMAND   
   9 22KH ... SEG WHATPERB,,,,G203   
   4 22PD ...[   
  15 22T* ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  13 22_= ...[     THIS EXCLUDES CODE UNDER #SKI G203
   4 2357 ...[   
   8 2394    #OPT K0WHATPERB=0   
   7 23BX ...#OPT G203 = 0   
   6 23JQ ...#SKI G203&1 
  17 23QK ...# WITH UGUG EDIT M203 (ALLOW WHATPER FOR REMOTE DEVICES)
  13 23YG          SEGENTRY K1WHATPERB,QK1WHATPERB   
  13 24D6          SEGENTRY K2WHATPERB,QK2WHATPERB   
   4 24DB    [   
   4 24DG    STUC
   8 24DL    #HAL  +EWDAS+ASTUC,0
   4 24DQ    XOF 
   8 24DW    #HAL  +EWDAS+EXOF,0 
   9 24F2    XFUSSY         +FUSSY   
   9 24F6    XFSWO          +JUNITSWO
   9 24F=    XWPMOP         +AWPMOP  
   9 24FB                   +AWPMOPG 
   9 24FG    XFRESUNEN      +FRESUNEN
   9 24FL    XFRONG         +FWRONG  
   9 24FQ    XFIDLE         +FIDLE   
   9 24FW    XFONLINE       +FONLINE 
   9 24G2    XFOLJOB        +FOLJOB  
   8 24G6    XFFAIL      +FFAIL  
  14 24G=    #     S/R   TO CLOSE UP USER AND LOCAL NAMES
   9 24GB    #     X3   -> USER NAME 
   7 24GG    #     X4    LINK
   5 24GL    UPCLOSE 
   8 24GQ          SBX   4  FX1  
  10 24GW          LDN   7  ACOMMUNE1(2) 
   8 24H2          FLENME1  0,3,5
  10 24H6          SBN   7  ACOMMUNE1(2) 
   7 24H=          SLC   7  2
   8 24HB          LDN   0  24   
   7 24HG          SBX   0  7
  16 24HL          BPZ   0  NOT25               [J IF < 25 CHARS 
  11 24HQ          HUNTW    3,ADATA,CSTORE   
   9 24HW          BPZ   3  PHOUND1  
  11 24J2          SETNCORE 7,3,ADATA,CSTORE 
   5 24J6    PHOUND1 
  17 24J=          ADN   2  ACOMMUNE1           [THIS NECESSARY AS   
   8 24JB          ADN   3  A1   
  18 24JG          MVCH  2  26                  [OUTPARAM USES ACOMMUNE7 
  11 24JL          OUTPARAM 7,A1,ADATA,CSTORE
   8 24JQ          BRN      XIT  
   5 24JW    NOT25   
  10 24K2          OUTPARAM 7,ACOMMUNE1  
   4 24K6    XIT 
   8 24K=          ADX   4  FX1  
   7 24KB          EXIT  4  0
   4 24KG    #   
   5 24KL    XHTAWP  
  10 24KQ          MHUNTW   3,ADATA,AWPER
   7 24KW          EXIT  7  0
   4 24L2    #   
   4 24L6    [   
   6 24L=    #UNS  ADMELI
   4 24LB    (   
   4 24LG    [   
  18 24LL    [                        MESSAGES FOR STATE OF DME MX LINES 
   4 24LQ    [   
   5 24LW    ZCND  +9
   8 24M2          9HCONNECTED   
   6 24M6    ZDCD  +12   
   9 24M=          12HDISCONNECTED   
   6 24MB    ZBECN +15   
   9 24MG          15HBEING CONNECTED
   6 24ML    ZBEDC +18   
  10 24MQ          18HBEING DISCONNECTED 
   6 24MW    ZTOCN +15   
   9 24N2          15HTO BE CONNECTED
   6 24N6    ZTODC +18   
  10 24N=          18HTO BE DISCONNECTED 
   5 24NB    WIRE4 +6
   7 24NG          6H4-WIRE  
   5 24NL    WIRE2 +6
   7 24NQ          6H2-WIRE  
   5 24NW    ZFAST +4
   6 24P2          4HFAST
   5 24P6    ZSLOW +4
   6 24P=          4HSLOW
   5 24PB    NORM  +6
   7 24PG          6HNORMAL  
   5 24PL    STAND +7
   7 24PQ          7HSTANDBY 
   5 24PW    ZOP   +8
   7 24Q2          8HOPERABLE
   6 24Q6    ZINOP +10   
   8 24Q=          10HINOPERABLE 
   8 24Q?    ZSWOUT         +14  
  11 24Q#                   14H,SWITCHED OUT)
   8 24Q*    ZWTBSW         +28  
  15 24QB                   28H,WAITING TO BE SWITCHED OUT)  
   8 24QC    ZNULL          +1   
   8 24QD                   1H)  
   4 24QF    [   
   4 24QG    [   
  16 24QL    [                    BITS IN STATE OF DME MX LINE   
  18 24QQ          BITDEFS  AWORK1,0,YFOUR,YSLOW,YNORMAL,YBEING,YCN,YOP  
  12 24QW          BITDEFS  AWORK1,19,TRANSBIT   
  15 24R4          BITDEFS  AWORK2,0,PRCONT,NOCSC,YSWO,YWTBSW
  11 24R=          FIELDDEF PORT,6,AWORK1,17 
  12 24RB          FIELDDEF SCOUPLER,6,AWORK1,11 
  11 24RG          FIELDDEF PROT,6,AWORK1,23 
   4 24RL    [   
  20 24RQ    ZOUTPAR                    [SUB TO OUTPUT PARAMS FOR MX LINE STATE  
   8 24RW          SBX   7  FX1  
  16 24S2          LDN   5  1(3)                [ADDR OF PARAM   
  10 24S6          LDN   6  ACOMMUNE1(2) 
   8 24S=          SMO      0(3) 
  19 24SB          MVCH  5  0                   [MOVE PARAM INTO ACOMM WDS.  
  11 24SG          OUTPARAM 0(3),ACOMMUNE1   
   8 24SL          ADX   7  FX1  
   7 24SQ          EXIT  7  0
   4 24SW    )   
   6 24XQ    QK1WHATPERB 
   4 24XS    [   
  16 24XW          STO   5  AWORK2(2)           [SAVE PER TYPE   
   9 24XY          CALL  7  XHTAWP   
  14 24Y2          LDX   2  3                   [SAVE X3 
   8 24Y4          LDN   4  A1+5 
  16 24Y6          STO   4  A1(3)               [INTERNAL POINTER
  19 24Y8          ADN   3  A1+5                [INT PTR TO FIRST REC HDDR   
   8 24Y=          BRN      ST1  
   8 24Y#    UPD   LDX   2  FX2  
  16 24YB          LDX   5  AWORK2(2)           [RESTORE PER TYPE
   9 24YD          CALL  7  XHTAWP   
  14 24YG          LDX   2  3                   [SAVE X3 
   9 24YJ          LDX   4  A1+1(3)  
   7 24YL          SBN   4  1
  18 24YN          BZE   4  NOUGH               [J IF NO MORE RECS IN BLK
  17 24YQ          STO   4  A1+1(3)             [UPDATE COUNT OF RECS
  18 24YS          LDN   4  K54-K50+14          [LENGTH OF THIS RECORD   
  19 24YW          ADS   4  A1(3)               [POINTER SET TO NEXT RECORD  
  20 24YY          ADX   3  A1(3)               [ABSOLUTE POINTER TO NEXT RECORD 
   4 24_2    ST1 
  17 24_4          JSWOUT   3,SWITOUT           [J IF SWITCHED OUT   
  15 24_6          LGEOG    3,0                 [GET GEOG NO 
   8 24_8          SMO      FX2  
   9 24_=          STO   0  AWORK3   
   7 24_#          LDX   0  5
   7 24_B          SBN   0  6
  18 24_D          BZE   0  UDAS                [J IF TYPE 6 (ED) TO UDAS
   7 24_G          SBN   0  1
  19 24_J          BZE   0  UDAS                [J IF TYPE 7 (MCF) TO UDAS   
   7 24_L          SBN   0  6
  19 24_N          BZE   0  UDAS                [J IF TYPE 13 (FD) TO UDAS   
   8 24_Q          SBN   0  13   
  19 24_S          BZE   0  UDAS                [J IF TYPE 26 (EDS30) TO UDAS
  15 24_W          JWRONG   3,WRONGED           [J IF WRONGED
  15 24_Y          JONL     3,ONLINE            [J IF ONLINE 
   7 2522          LDX   0  5
   8 2524          SBN   0  10   
  14 2526          BZE   0  MULT                [J IF UX 
   7 2528          SBN   0  1
  13 252=          BZE   0  MULT                [MX  
   8 252#          SBN   0  13   
  16 252B          BZE   0  MULTB               [OR IPB (TYPE 24)
   9 252D          JALLOC   3,OWNED  
   8 252G          BRN      XIDLE
   4 252J    MULT
   8 252Q          MOPSTATE 3,6  
   8 252Y          BRN      MXMS 
   5 2532    MULTB   
   9 2534          MOPSTATE 3,6,AMOP 
   4 2536    MXMS
   7 2538          STOZ     4
   8 253=          BNG   6  OFF  
   8 253#          BZE   6  ON   
   7 253*          SBN   6  5
   8 253B          BZE   6  OFF  
   7 253C          LDN   4  1
   7 253D          ADN   6  4
   8 253G          BZE   6  ON   
   7 253J          SBN   6  1
   8 253L          BZE   6  OFF  
   9 253S          CALL  7  OUTNUM   
   9 253W          CALL  7  OUTMESS  
   7 253Y          +JONOFF   
   8 2542          BRN      MXMS1
   4 2544    ON  
   9 2546          CALL  7  OUTNUM   
  18 2548          CALL  7  OUTMESS             [IF ALLOCATED THEN MOP ON
   6 254=          +JON  
   8 254#          BRN      MXMS1
   9 254B    OFF   CALL  7  OUTNUM   
   9 254D          CALL  7  OUTMESS  
   6 254G          +JOFF 
   5 254J    MXMS1   
   7 254L          SMO      4
   9 254N          LDX   7  XWPMOP(1)
   9 254Q          BRN      MONOUTX  
   4 254S    UDAS
  10 254W          LDX   6  K54-K50+8(3) 
   9 254Y          JTWIN    3,NOTWIN 
   7 2552          NGN   6  1
   5 2554    NOTWIN  
  10 2556          LDX   4  K54-K50+8(3) 
   8 2558          ANDN  4  #20  
  18 255=          BNZ   4  USUDA               [J IF IN USE BY SYSTEM   
  15 255#          JENG     3,SET               [J IF ENGAGED
   9 255B          CALL  7  OUTNUM   
   9 255D          CALL  4  OUTWIN   
   9 255G          LDX   7  XFIDLE(1)
   9 255J          BRN      MONOUTX  
   9 255L    USUDA CALL  7  OUTNUM   
   9 255N          CALL  4  OUTWIN   
   9 255Q          CALL  7  MONOUT   
   6 255S          +FUSSY
   8 255W          BRN      UDOUT
   9 255Y    SET   JHOLD    3,SETH   
   9 2562          CALL  7  OUTNUM   
   9 2564          CALL  4  OUTWIN   
   8 2566          BRN      UDENG
   9 2568    SETH  CALL  7  OUTNUM   
   9 256=          CALL  7  OUTMESS  
   6 256#          +FHSET
   9 256B          CALL  4  OUTWIN   
   9 256D    UDENG CALL  7  MONOUT   
   6 256G          +FENG 
   5 256J    UDOUT   
   9 256L          CALL  7  XHTAWP   
   8 256N          ADX   3  A1(3)
   8 256Q          LGEOG    3,3  
  21 256S          LDN 2 BCCB                   [SEARCH CCB CHAIN FOR ASTUC BLOCKS   
   9 256W    ULOOP LDX   2  FPTR(2)  
   8 256Y          TXU   2  CXCC 
  14 2572          BCC      ZEND                [J IF END
   9 2574          LDX   0  ATYPE(2) 
   9 2576          TXU   0  STUC(1)  
  17 2578          BCS      ZEND                [J IF NOT RIGHT TYPE 
   9 257=          TXU   3  GEOGU(2) 
  17 257#          BCS      ULOOP               [J IF NOT RIGHT UNIT 
   9 257B          LDXC  6  BACK1(2) 
  17 257D          CALL  7  OUTOCT              [O/P SERIAL NUMBER   
   9 257G          CALL  7  MONOUT   
   6 257J          +FSUSN
  19 257L          LDN   4  1                   [SET HEADING NOT O/P MARKER  
   9 257N          STO   4  AWORK4(2)
   8 257Q          LDN   2  BCCB 
  17 257S    #     THIS ROUTINE OUTPUTS NAMES OF FILES OPEN ON UNIT  
   5 257W    ULOOP1  
   9 257Y          LDX   2  BPTR(2)  
   9 2582          LDX   0  ATYPE(2) 
   9 2584          TXU   0  XOF(1)   
   8 2586          BCS      REST 
  17 2588          TXU   6  ASUSN(2)            [CHECK IF /EXOF BLOCK
  17 258=          BCC      XOUT                [IS FOR CORRECT S.U. 
  10 258#          LDX   5  ALOGLEN(2)   
  10 258B          SBN   5  FDCELLS-A1   
   9 258D          BZE   5  ULOOP1   
   7 258G    XCELL SBN   5  6
   7 258J          SMO      5
  19 258L          TXU   6  FDCELLS(2)          [LOOK AT NEXT CELL IN BLOCK  
   8 258N          BCC      XOUT 
   8 258Q          BNZ   5  XCELL
   9 258S          BRN      ULOOP1   
  10 258W    XOUT  MHUNTW   1,ADATA,AWPER
   9 258Y          LDN   5  A1+2(1)  
  15 2592          LDN   4  FIP(2)              [FILENAME IS 
  18 2594          MOVE  4  3                   [MOVED TO ACTIVITY BLOCK 
  18 2596          LDX   5  BACK2(2)            [X5 CONTAINS EXOFILE NO  
   9 2598          LDX   4  FIP+4(2) 
  17 259=          SRL   4  12                  [X4 CONTAINS F.G.N.  
   8 259#          SMO      FX2  
   9 259B          LDX   0  AWORK4   
   8 259D          BZE   0  XOUT1
   9 259G          CALL  7  MONOUT   
   7 259J          +FFLOP1   
   9 259L          CALL  7  MONOUT   
   7 259N          +FFLOP2   
  19 259Q          STOZ     AWORK4(2)           [UNSET HEADING NOT O/P MARKER
  10 259S    XOUT1 MHUNTW   1,ADATA,AWPER
   9 259W          LDX   0  A1+2(1)  
  18 259Y          BNZ   0  XOUT2               [J IF NOT A SCRATCH FILE 
   9 25=2          CALL  7  MONOUT   
   6 25=4          +FSCR 
   8 25=6          BRN      XOF1 
   5 25=8    XOUT2   
   7 25==          OUTBLOCN 6
  12 25=#          OUTPARN  12,A1+2,ADATA,AWPER  
  14 25=B          OUTNUM   4,0,1               [AND FGN 
   9 25=D          CALL  7  MONOUT   
   7 25=G    #SKI  G3MARK>7-7
   6 25=J          +FUDAS
   7 25=L    #SKI  G3MARK<8$8
   6 25=N          +JTIMD
  20 25=Q    XOF1  FINDEXOF 2,5,REST            [FIND LAST RELEVANT /EXOF BLOCK  
   9 25=S          BRN      ULOOP1   
  18 25=W    REST  FINDSU   2,6,ZEND            [FIND LAST RELEVANT BLOCK
   8 25=Y          BRN      ULOOP
   8 25?2    ZEND  BRN      UPD  
   5 25?4    SWITOUT 
   9 25?6          CALL  7  OUTNUM   
   9 25?8          LDX   7  XFSWO(1) 
   9 25?=          BRN      MONOUTX  
   5 25?#    WRONGED 
  17 25?B          CALL  7  OUTNUM              [OUTPUT OPERATORS NO.
   9 25?D          LDX   7  XFRONG(1)
   9 25?G          BRN      MONOUTX  
   5 25?J    XIDLE   
  17 25?L          CALL  7  OUTNUM              [OUTPUT OPERATORS NO.
   9 25?N          LDX   7  XFIDLE(1)
   9 25?Q          BRN      MONOUTX  
   5 25?S    OWNED   
  20 25?W          JENG     3,USSY              [J IF NOT ^RESERVED & UNENGAGED^ 
  17 25?Y          CALL  7  OUTNUM              [OUTPUT OPERATORS NO.
  10 25#2          LDX   7  XFRESUNEN(1) 
   9 25#4          BRN      MONOUTX  
  13 25#6    #     PERIPHERAL BEING USED BY SYSTEM   
   4 25#8    USSY
  15 25#=          CALL  7  OUTNUM              [O/P GEOG NO 
   9 25##          LDX   7  XFUSSY(1)
   9 25#B          BRN      MONOUTX  
   4 25#D    #   
  10 25#G    #     DEVICE ONLINE TO A JOB
   4 25#J    #   
   5 25#L    ONLINE  
   9 25#N          CALL  7  OUTNUM   
  10 25#Q          LDX   7  XFONLINE(1)  
   7 25#S          MONOUTX  7
   9 25#W          CALL  7  XHTAWP   
   8 25#Y          ADX   3  A1(3)
   8 25*2          LJOBNO   3,4  
   7 25*4          LDX   0  4
  16 25*6          CALL  7  OUTNUM2             [OUTPUT JOB NO.  
   9 25*8          CALL  7  XHTAWP   
   8 25*=          SMO      A1(3)
  10 25*#          LDN   3  K54-K50+1(3) 
   9 25*B          CALL  4  UPCLOSE  
   9 25*D          CALL  7  XHTAWP   
  10 25*G          LDX   7  XFOLJOB(1)   
  18 25*J          ADX   3  A1(3)               [X3 IS ABSOLUTE POINTER  
   9 25*L          JFAIL    3,PHAIL  
   9 25*N          BRN      MONOUTX  
   5 25*Q    PHAIL   
   7 25*S          MONOUTX  7
   9 25*W          LDX   7  XFFAIL(1)
   5 25*Y    MONOUTX 
   7 25B2          MONOUTX  7
   8 25B4          BRN      UPD  
   8 25B6    NOUGH LDX   2  FX2  
   5 25B8    RONG1   
  17 25BL          OFF      AWHATBIT            [GIVE THE ALL CLEAR  
   6 25BP ...#SKI G203&1 
   9 25BS ...      ACROSS   WHATPER,7
   7 25BX ...#SKI G203&1$1   
   8 25C2 ...      FON      #114 
   5 25C8    TOENDCOM
   6 25C=          ENDCOM
   6 25CB    QK2WHATPERB 
   4 25CF    [   
  10 25CJ          MHUNTW   3,CPAR,JSCE  
   9 25CM          LDX   4  JSCEB(3) 
   7 25CQ          SRL   4  9
  16 25CT          ANDN  4  #77                 [ISOLATE LINE NUM
   7 25CY          LDN   5  0
   9 25D3          LDX   3  JSCEP(3) 
  19 25D6          LINECTRL 4,3                 [ASK EXEC FOR STATE OF LINE  
  15 25D9          STO   4  AWORK1(2)           [SAVE REPLY  
   9 25D#          STO   5  AWORK2(2)
   8 25DC          OUTBLOCN 21   
  10 25DG          MHUNT    3,CPB,CUNI   
   9 25DK          LDX   6  ANUM(3)  
  19 25DN          ANDN  6  #7777               [NUM OF CHARS IN PERI DESC   
  18 25DR          OUTPARAM 6,APARA,CPB,CUNI    [OUTPUT PERI DESC PARAM  
  18 25DW          BPZ   5  ZNPRC               [J IF NOT UNDER PROG CTRL
   9 25DX          LDN   3  ZOP(1)   
  10 25DY          JBS      ZPOP,2,YOP   
   9 25D_          LDN   3  ZINOP(1) 
   4 25F2    ZPOP
   9 25F3          CALL  7  ZOUTPAR  
   4 25F4    [   
   9 25F5          LDN   3  ZSWOUT(1)
  10 25F6          JBS      ZOUTSW,2,YSWO
   9 25F7          LDN   3  ZWTBSW(1)
  11 25F8          JBS      ZOUTSW,2,YWTBSW  
   9 25F9          LDN   3  ZNULL(1) 
   5 25F=    ZOUTSW  
   9 25F?          CALL  7  ZOUTPAR  
   9 25F#          MONOUT   ALIPRSTAT
   8 25F*          BRN      ZCOK 
   5 25FB    ZNPRC   
   4 25FC    [   
  20 25FD    [                                   NOW OUTPUT PARAMS SHOWING STATE 
  15 25FF          LDN   3  ZCND(1)             [CONNECTED   
  20 25FH          JBS      ZBEING,2,YBEING     [J IF CHANGE OF STATE IN PROGRESS
  10 25FL          JBS      ZTOBE,2,NOCSC
  16 25FP          JBS      ZOUTCN,2,YCN        [J IF CONNECTED  
  15 25FS          LDN   3  ZDCD(1)             [DISCONNECTED
   9 25FX          BRN      ZOUTCN   
   9 25G2    ZTOBE LDN   3  ZTOCN(1) 
  17 25G5          JBS      ZOUTCN,2,YCN        [J IF TO BE CONNECTED
   9 25G8          LDN   3  ZTODC(1) 
  18 25G?          BRN      ZOUTCN              [ELSE TO BE DISCONNECTED 
   5 25GB    ZBEING  
   9 25GF          LDN   3  ZBECN(1) 
  17 25GJ          JBS   ZOUTCN,2,YCN           [J IF BEING CONNECTED
  17 25GM          LDN   3  ZBEDC(1)            [BEING DISCONNECTED  
   5 25GQ    ZOUTCN  
   9 25GT          CALL  7  ZOUTPAR  
  19 25GY    [                                   NOW OUTPUT NUMBER OF WIRES  
   9 25H3          LDN   3  WIRE4(1) 
  11 25H6          JBS      ZOUTWIRE,2,YFOUR 
   9 25H9          LDN   3  WIRE2(1) 
   5 25H#    ZOUTWIRE
   9 25HC          CALL  7  ZOUTPAR  
  16 25HG    [                                   NOW OUTPUT SPEED
   9 25HK          LDN   3  ZFAST(1) 
  11 25HN          JBC      ZOUTSP,2,YSLOW   
   9 25HR          LDN   3  ZSLOW(1) 
   5 25HW    ZOUTSP  
   9 25H_          CALL  7  ZOUTPAR  
  20 25J4    [                                   NOW STATUS (NORMAL OR STANDBY)  
   9 25J7          LDN   3  NORM(1)  
  12 25J=          JBS      ZOUTSTAT,2,YNORMAL   
   9 25J*          LDN   3  STAND(1) 
   5 25JD    ZOUTSTAT
   9 25JH          CALL  7  ZOUTPAR  
  16 25JL    [                                   NOW OPERABILITY 
   9 25JP          LDN   3  ZOP(1)   
  10 25JS          JBS      ZOUTOP,2,YOP 
   9 25JX          LDN   3  ZINOP(1) 
   5 25K2    ZOUTOP  
   9 25K5          CALL  7  ZOUTPAR  
   4 25K8    [   
  17 25K?          MONOUT   AMXLSTAT            [OUTPUT STATE MESSAGE
  17 25KB          VFREE    CPB,CUNI            [FINISHED WITH CUNI  
   9 25KF          LF       2,PORT,6 
  18 25KJ          OUTNUM   6,0                       [OUTPUT PORT PARAM 
  10 25KM          LF       2,SCOUPLER,6 
  17 25KQ          OUTNUM   6,0                       [AND COUPLER   
  11 25KT          JBC      NTRAN,2,TRANSBIT 
  21 25KY          OUTNULL                            [LET END OF MSG OUT IF TRANS   
   9 25L3    NTRAN MONOUT   ALIPCTR  
  10 25L6          JBC      ZCOK,2,NOCSC 
   9 25L9          MONOUT   CSCNRUN  
   4 25L#    ZCOK
   6 25L* ...#SKI G203&1 
   9 25LB ...      ACROSS   WHATPER,7
   7 25LC ...#SKI G203&1$1   
   8 25LG          FON      #114 
   6 25LK          ENDCOM
   4 25LN    [   
  17 25LR    #     SUBROUTINES TO OUTPUT MESS IN LOCN FOLLOWING CALL 
   5 25LW    MONOUT  
   7 25L_          LDX   1  7
   8 25M4          SBX   7  FX1  
   8 25M7          MONOUTX  0(1) 
   8 25M=          ADX   7  FX1  
   7 25M*          EXIT  7  1
   5 25MD    OUTWIN  
   8 25MF ...      SBX   4  FX1  
   8 25MH          BNG   6  NTWIN
   8 25MP          SLL   6  16   
  17 25MS          BNG   6  SHARED              [B16 SET IS SHARED   
   7 25MX          SLL   6  1
  17 25N2          BNG   6  NDED                [B17 SET IS DEDICATED
   9 25N5          CALL  7  OUTMESS  
  16 25N8          +ENTIN32                     [ELSE UNDEDICATED
   9 25N?          BRN      NTWIN1   
   5 25NB    SHARED  
   9 25NF          CALL  7  OUTMESS  
   7 25NJ          +ENTIN33  
   9 25NM          BRN      NTWIN1   
   4 25NQ    NDED
   9 25NT          CALL  7  OUTMESS  
   7 25NY          +ENTIN31  
   9 25N_ ...      BRN      NTWIN1   
   5 25P2 ...NTWIN   
  10 25P3 ...      JENVNOT  NTWIN1,CME   
   9 25P4 ...      JNCMESH  3,NTWIN1 
   9 25P5 ...      CALL  7  OUTMESS  
   7 25P6 ...      +CMESHWP  
   5 25P7 ...NTWIN1  
   8 25P8 ...      ADX   4  FX1  
   7 25P#          EXIT  4  0
   5 25PC    OUTMESS 
   7 25PG          LDX   1  7
   8 25PK          SBX   7  FX1  
   8 25PN          OUTMESSX 0(1) 
   8 25PR          ADX   7  FX1  
   7 25PW          EXIT  7  1
  16 25P_    #           SUBROUTINE TO OUTPUT DECIMAL NO IN X0   
   5 25Q4    OUTNUM  
  15 25Q7          LGEOG    3,0                 [GET OPS NO  
   5 25Q=    OUTNUM2 
   8 25Q*          SBX   7  FX1  
   8 25QD          OUTNUM   0,0  
   8 25QH          ADX   7  FX1  
   7 25QL          EXIT  7  0
   4 25QP    #   
  16 25QS    #           SUBROUTINE TO OUTPUT AN OCTAL NO. IN X6 
   4 25QX    #   
   5 25R2    OUTOCT  
   8 25R5          SBX   7  FX1  
  18 25R8          CONSERNO 6                   [CONVERT SERIAL NUMBER   
  10 25R?          MHUNTW   2,FLIB,FTSN  
  12 25RB          OUTPARAM A1(2),A1+1,FLIB,FTSN 
   8 25RF          ADX   7  FX1  
   7 25RJ          EXIT  7  0
   5 25RM    MISTAKE 
   8 25RQ          COMERRX  0(1) 
   4 25X2    #END
   6 ____ ...470464210001
