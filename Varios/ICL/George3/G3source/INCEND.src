  13 22FL ...      SEG   INCEND,864,FILESTORE,INCDUMP
   4 22_=    #   
  11 23DW          SEGENTRY K1INCEND,X1INCEND
  11 23SK ...      SEGENTRY K2INCEND,(GEOERR)
  11 248# ...      SEGENTRY K3INCEND,X3INCEND
   4 24D6    #   
   6 24XQ    #  CONSTANTS
   4 25CB    #   
  19 26BL    MLANCOMP       4HC1                [LANGUAGE FOR COMPOSITE S/F  
  16 26W=    NAMEINCR       12HGEORGE3DUMP      [INCREMENT NAME  
  18 27*W    SCLEANDAY      864000/K9*100       [NO. OF CLEANCTS IN DAY  
  19 27TG    PFIN  PAIR  DTSTART,7              [END OF DUMP - DTA LINK PAIR 
   4 28*6 ...#   
  19 28SQ ...#  AWORK1 SWITCH WORD - BIT DEFINITIONS (ALSO USED BY INCFIN)   
   4 29#B ...#   
  11 29S2 ... BITDEFS AWORK1,0,MBFINAL,MBEMS 
   6 2=?L ...#UNS AMTGR  
  17 2=R= ... BITDEFS AWORK1,2,MBPGENRES  [ POTENTIALLY GENRESABLE   
   4 2?=W    #   
   7 2?QG    #  SUBROUTINES  
   4 2#=6    #   
  16 2#PQ    #  THIS SUBR. FINS THE FLIB/FLINC BLOCK FOR ALTLENG.
   6 2*9B    SFINDFLINC  
  10 2*P2          MHUNTW   2,FLIB,FLINC 
   7 2B8L          EXIT  1  0
   4 2BN=    #   
  15 2C7W    #  THIS DOES THE SAME WITH A FILE/FINTER BLOCK. 
   6 2CMG    SFINDFINT   
  10 2D76          MHUNTW   2,FILE,FINTER
   7 2DLQ          EXIT  1  0
   4 2F6B    #   
   4 2FL2    #   
  21 2G5L    #  THIS SUBROUTINE SIMPLY DOES A STEPWRITE - WHOSE EXPANSION IS LENGTHY.
  18 2GK=    #  ON ENTRY,                               X7 IS S/R LINK.  
  21 2H4W    #  ON EXIT, X1=FX1, X2=FX2, X3-> CURRENT RECORD.   USES X0. MAY COORDINA
   4 2HJG    #   
   5 2J46    SWRITE  
   8 2JHQ          SBX   7  FX1  
  20 2K3B          STEPWRITE                    [ENSURE INFORMATION WRITTEN TO BS
   8 2KH2          ADX   7  FX1  
   7 2L2L          EXIT  7  0
   4 2LG=    #   
  21 2L_W    #  OPENS INCINDEX IN GENERAL MODE, DELETES ANY RECORDS NECESSARY IF CURR
  21 2MFG    #  INCREMENT IS END-OF-BATCH AND ADJUSTS STATE WORD OF CURRENT INCR..   
   8 2M_6    #  CLOSES INCINDEX  
  19 2N6# ...#  ALSO USED BY EMS ENTRY X2, WHICH ENTERS IT AT LABEL NOAPROC  
  14 2N?G ...#  AND REQUIRES THAT INCINDEX BE LEFT OPEN  
  21 2NDQ    #  ON ENTRY, X5= CURRENT INCR. NO., X6= REPLY WORD.        X4 IS S/R LIN
  21 2NYB    #  ON EXIT, X1= FX1, X2= FX2, X5= CURRENT INCR. NO., X6= REPLY WORD. USE
  20 2PD2    #                                          ALSO USES X7 & AWORK4.   
   4 2PXL    #   
   6 2QC=    UPDATEINDEX 
   8 2QWW          SBX   4  FX1  
  19 2RBG          OPENINC  ,GENERAL            [OPEN INCINDEX (GENERAL MODE)
  20 2RW6          STOZ  AWORK4(2)              [CLEAR ^1ST RELIABLE INCR.^ WORD 
  21 2STB                                       [MTS FOR WHICH BITNOWPR HAS TO BE SET
   5 2T*2    QSTEP   
   6 2TSL          STEP  
   7 2W#=    #SKI  K6INCEND  
   4 2WRW    (   
   9 2X?G          BNZ   3  NOENDINC 
  21 2XR6          GEOERR   BRIEFPM,INCINDEX    [CANT FIND LAST INCR. REC. IN INCINDE
   5 2Y=Q    NOENDINC
   4 2YQB    )   
  16 2_=2          BXE   5  AINCNOR(3),YLAST    [J IF LAST INCR. 
   9 2_PL          LDX   0  AWORK4(2)
  21 329=          BNZ   0  NORV                [J IF PREVIOUS RELIABLE INCR EXISTS  
  21 32NW          JBS   NORV,3,BINSUITRVR      [J IF INCR NOT SUITABLE FOR RETRIEVIN
  10 338G          LDX   0  AINCNOR(3)   
  20 33N6          STO   0  AWORK4(2)           [AWORK4 = 1ST RELIABLE INCR. NO. 
   4 347Q    NORV
   8 34MB          LDCT  0  #200 
   7 3572          ANDX  0  6
  21 35LL          BZE   0  QSTEP               [J IF CURRENT INCR. NOT END OF BATCH 
  21 366=                                       [ONLY REMOVE 'BEING REDUMPED' INCRS. 
  16 36KW                                       [  END OF BATCH  
  11 36TN ...      JBC      QSTEP,,BATCHSTART
   8 375G          LDCT  0  #100 
   7 37K6          ANDX  0  6
  21 384Q          BZE   0  NOTOBS              [J IF NOT GENERAL RESTOREABLE INCR.  
  10 38JB          HUNTW 1,FILE,FINCPROC 
   9 3942          BNG   1  NOTOBS   
  18 39HL          LDX   0  AINCNOR(3)          [X0= NO. OF THIS INCR.   
  20 3=3=          LDX   2  FINCNOBS(1)         [X2= NO. OF STILL OBSOLETE INCRS.
  19 3=GW          BZE   2  NONEOBS             [J IF NO INCREMENTS OBSOLETE 
   5 3?2G    NFOUND  
   7 3?G6          SMO      2
  19 3?_Q ...      BXE   0  INCLISTS-1(1),OBS     [J IF THIS INCR. OBSOLETE  
  20 3#FB          BCT   2  NFOUND              [J IF MORE INCR. NOS. TO SEARCH  
   5 3#_2    NONEOBS 
   8 3*DL          LDX   2  FX2  
   9 3*Y=          BRN      NOTOBS   
   4 3BCW    OBS 
  15 3BXG          CALL  7  SWRITE              [STEPWRITE   
  16 3CC6          BS    3,BIOBSR               [MARK AS OBSOLETE
  21 3CWQ          BC    3,BIPRNOBSR            [REMOVE PROC BUT NOT OBS BIT IF SET  
   7 3DBB          READAGAIN 
  10 3DW2          MHUNTW   1,FILE,FRB   
  10 3F*L          HUNTW    3,FILE,APROC 
  20 3FT=          BNG   3  MAPROC              [J IF 1ST NEWLY OBSOLETE INCR.   
   9 3G#W          LDX   7  AMTS(1)  
  21 3GSG          BXU   7  AMTS(3),MAPROC      [J IF NO. OF TAPES DIFFERENT TO LAST 
  18 3H#6                                       [   NEWLY OBSOLETE INCR. 
   4 3HRQ    MNEW
  19 3J?B          LDX   2  AMAG(1)             [X2 = NEXT TSN IN NEW RECORD 
  20 3JR2          LDX   0  AMTS(3)             [X0= NO. OF TAPES IN OLD RECORD  
   4 3K=L    MOLD
  16 3KQ=          BXE   2  AMAG(3),MFOU        [J IF TAPE FOUND 
  19 3L9W          ADN   3  INCMAGLEN           [UPDATE OLD RECORD POINTER   
  19 3LPG          BCT   0  MOLD                [J IF MORE TAPES TO SEARCH   
  14 3M96          MHUNTW   1,FILE,FRB          [TAPES IN
  21 3MNQ                                       [RECORDS DIFFERENT SO SET UP NEW FLIN
  15 3N8B                                       [   & APROC  
   5 3NN2    MAPROC  
  15 3P7L          NAME     1,FILE,APROC        [NEW APROC   
  19 3PM=          LDX   7  AINCNO(1)           [NOTE INCR. NO. OF NEW RECORD
  20 3Q6W          SETNCORE 2,3,FLIB,FLINC      [SET UP & INITIALISE NEW FLINC   
   7 3QLG          LDN   0  1
   8 3R66          STO   0  A1(3)
   9 3RKQ          BRN      MEXTEND  
   4 3S5B    MFOU
  10 3SK2          MHUNTW   3,FILE,APROC 
  19 3T4L          ADN   1  INCMAGLEN           [UPDATE NEW RECORD POINTER   
  20 3TJ=          BCT   7  MNEW                [J IF MORE TAPES IN NEW RECORD   
  12 3W3W                                       [
  21 3WHG                                       [RECORDS ARE SAME SO INSERT INC. NO. 
  16 3X36                                       [EXISTING FLINC  
  10 3XGQ          MHUNTW   1,FILE,FRB   
  18 3Y2B          LDX   7  AINCNO(1)           [X7= INCR. NO. TO INSERT 
   7 3YG2          FREECORE 1
  10 3Y_L          MHUNTW   3,FLIB,FLINC 
   5 3_F=    MEXTEND 
  10 3_YW          LDX   0  ALOGLEN(3)   
   7 42DG          ADN   0  1
  10 42Y6          STO   0  ACOMMUNE1(2) 
  21 43CQ          ALTLENG  3,ACOMMUNE1(2),SFINDFLINC [LENGTHEN FLIB/FLINC BY 1 WORD 
  10 43XB          MHUNTW   3,FLIB,FLINC 
   7 44C2          LDN   0  1
   8 44WL          ADS   0  A1(3)
  10 45B=          SMO      ALOGLEN(3)   
  17 45TW          STO   7  A1-1(3)             [INSERT NEW INCR. NO.
   7 46*G          STEPAGAIN 
   5 46T6    NOTOBS  
  21 47#Q          JBC   QSTEP,3,BINSUITRVR     [J IF INCR. SUITABLE FOR RETRIEVING  
  21 47SB                                       [(ASSUMES THAT ^NOT SUITABLE FOR RETR
  21 48#2                                       [   BIT IS ONLY SET BY DUMPER WHEN IN
  18 48RL                                       [   SETS UP THE RECORD)  
  20 493D ...      JBS      QSTEP,3,BIUNFINR   [DONT DELETE INCS BEING PRODUCED  
   8 49?=    #SKI  K6INCEND>99-99
  21 49QW          TRACE AINCNOR(3),INC DEL     [NO. OF INCOMPLETE INCR. DELETED FROM
  19 4==G          DELETE                       [DELETE INCOMPLETE INCR. REC.
   8 4=Q6          BRN      QSTEP
   4 4?9Q    #   
  21 4?PB    #  IF THERE ARE NEWLY OBSOLETE INCRS.(I.E. FILE/APROC & FLIB/FLINC BLOCK
  21 4#92    #  ARE PRESENT), RE-CHAIN BLOCKS IN ASCENDING INCR. NO. ORDER & THEN PUT
  18 4#NL    #  OUT ^INCREMENT(S) N1,N2 OBSOLETE ON MT(S) T1 T2^ MESSAGES
   4 4*8=    #   
   5 4*MW    YLAST   
  10 4B7G          HUNTW    3,FILE,APROC 
  20 4BM6          BNG   3  NOAPROC             [J IF NO NEWLY OBSOLETE INCRS.   
   5 4C6Q    NXTPROC 
  11 4CLB          MHUNTW   2,FILE,FINCPROC  
  19 4D62          LDX   7  AINCNO(3)           [NOTE INCR. NO. IN THIS APROC
  20 4DKL          CHAIN 3,2                    [CHAIN FILE/APROC & FLIB/FLINC TO
  21 4F5=          MHUNTW   1,FLIB,FLINC        [   FILE/FINCPROC BLOCK SO THAT REVER
  20 4FJW          CHAIN 1,3                    [   ORDER OF FLINCS (OR APROCS)  
  10 4G4G          MHUNTW   3,FILE,APROC 
  21 4GJ6          BXU   7  AINCNO(3),NXTPROC   [J IF FOUND DIFFERENT APROC TO LAST T
  19 4H3Q                                       [& SO MORE BLOCKS TO RE-CHAIN
  17 4HHB    MOUT                               [NOW OUTPUT MESSAGES 
  20 4J32          OUTINCS                      [OUTPUT LIST OF INCREMENT NOS.   
  10 4JGL          MFREEW   FLIB,FLINC   
  17 4K2=          OUTTSNS                      [OUTPUT LIST OF TSNS 
  10 4KFW          MFREEW   FILE,APROC   
  21 4K_G          MONOUT   INCOBSMT2           [^INCREMENT%A %B OBSOLETE ON MT%C %D 
  10 4LF6          HUNTW    3,FILE,APROC 
  19 4LYQ          BPZ   3  MOUT                [J IF MORE MESSAGES TO OUTPUT
   4 4M4M ...#   
  13 4M8J ...#  ENTRY 2 USES THIS SR ENTERING HERE   
   4 4M#F ...#   
   5 4MDB    NOAPROC 
  20 4MJ? ...      SETNCORE 0,3,FILE,FINTER     [BLOCK TO HOLD LIST OF TSNS FOR  
  21 4MN8 ...                                   [WHICH BIT BMNCURR NEEDS SETTING IN  
  19 4MS5 ...                                   [MAGSTATE WORDS IN INCINDEX  
   9 4MY2          LDX   0  AWORK4(2)
  19 4NCL          BNZ   0  NOTONLY             [J IF >1 RECORDS IN INCINDEX 
  21 4NX=          STO   5  AWORK4(2)           [AWORK4 = 1ST (ONLY) RELIABLE INCR. N
   5 4PBW    NOTONLY 
  15 4PWG          CALL  7  SWRITE              [STEPWRITE   
  21 4Q8X ...      MBC   3,BIREDUMPR,BINSUITRVR,BIUNFINR[CLEAR REDUMP,NOT RETRIEVABLE
  17 4QH# ...                                   [AND UNFINISHED BITS 
   8 4QTQ          LDCT  0  #240 
  20 4R*B          ANDX  0  6                   [IF B1 OR B3 SET IN REPLY WORD,  
  21 4RT2          ORS   0  AINCSTATER(3)       [                SET THEM IN AINCSTAT
   8 4S#L          LDCT  1  #300 
   8 4SS=          LDCT  0  #300 
  21 4T?W          ANDX  0  6                   [BOTH B1 & B2 OF REPLY WORD SET IF RE
  21 4TRG          SBX   0  1                   [                                   I
  19 4W?6          BNZ   0  STEPREWR            [J IF NOT RESTOREABLE INCR.  
   9 4WQQ          BS    3,BIGENRESR 
  18 4X=B                                       [MARK AS SUIT FOR GEN RES
   5 4XQ2    STEPREWR
   6 4XQ3 ...#UNS AMTGR  
   4 4XQ4 ...(   
  21 4XQ5 ...      JBS      SGENRES,3,BIGENRESR    [ IF NOT A RESTORABLE INCREMENT   
  20 4XQ6 ...      BC       3,BIRESTBATR           [ IT CAN'T BE LAST IN A BATCH 
   5 4XQ7 ...SGENRES 
   4 4XQ8 ...)   
   7 4XQ9 ...#UNS  FPARTRES  
   4 4XQD ...(   
  20 4XQM ...      BSXD     0,BINCWRFAIL        [IF INCR WRITE FAILED MARK SO IN 
  14 4XQW ...      ANDX  0  6                   [INCINDEX
   9 4XR5 ...      BZE   0  NOTWRFAIL
  10 4XR# ...      BS       3,BIWFBREAKR 
   6 4XRH ...NOTWRFAIL   
   4 4XRQ ...)   
   6 4XS9 ...#UNS FNSAD2 
   4 4XWD ...(   
  12 4XXH ...      JBC      NOENDBAT,,BATCHSTART 
  12 4XYM ...      JBC      NOENDBAT,3,BIENDBATR 
  20 4Y2W ...      BS        ,BATCHFIN          [ SET END OF BATCH BIT IN INCWORD
   5 4Y55 ...NOENDBAT
   4 4Y7# ...)   
   9 4Y9L ...      HUNTW 1,FILE,FGET 
   9 4YP=          BNG   1  NOFINCK  
  10 4_8W          LDX   0  INCSIZE(1)   
  19 4_NG          STO   0  AINCSIZER(3)        [SIZE OF DUMP IN BS BLOCKS   
  10 5286          LDX   7  INCFILNUM(1) 
   8 52MQ          LDCT   0 #100 
   7 537B          ANDX   0 6
  18 53M2          BZE   0  NGENRST              [J IF NOT GEN RSTABLE   
  17 546L          ADN   7  1                    [ADD 1 FOR MASTER   
  10 54L=          STO   7  INCFILNUM(1) 
   5 555W    NGENRST 
  17 55KG          STO   7  AINCFILESR(3)        [NO OF FILES IN INCR
  10 5656          LDX   0  INCBEGDUMP(1)
  20 56JQ          STO   0  AINCSTAT2R(3)       [TIME TO DUMP INCR. (TOTAL TIME) 
  17 574B          STOZ     AINCSTAT4R(3)       [CLEAR WORDS NOT USED
  10 57J2          STOZ     AINCSTAT1R(3)
   5 583L    NOFINCK 
  16 58H=          LDX   5  AMTSR(3)            [X5= NO. OF MTS  
  18 592W          LDN   1  0                   [SET COU_ER DOWN MT WORDS
  20 59GG          LDN   2  0                   [CLEAR ^EXTRA MT STATE BITS^ WORD
  21 5=26          SETBIT   1,2                 [^LAST RELIABLE INCR. ON THIS MT^ BIT
  15 5=FQ                                       [BIT BMLASTRV
  21 5=_B          BBUS  3,6,NOEOT              [J IF NOT WRITE FAIL OR END OF TAPE  
  19 5?F2          SETBIT   5,2                 [BIT BMNCURR - DUMPER WONT   
  20 5?YL                                       [BE DUMPING ON THIS TAPE AGAIN   
   5 5#D=    NOEOT   
   7 5#XW          SMO      1
  21 5*CG          ORS   2  AMAGSTATER(3)       [SET ANY BITS NECESSARY IN MT STATE W
  19 5*X6          ADN   1  INCMAGLEN           [UPDATE COUNTER DOWN MT WORD 
  20 5BBQ          BCT   5  NOEOT               [J IF MORE MT WORDS TO LOOK AT   
   4 5BWB    #   
  21 5CB2    #  IF HIT END-OF-TAPE OR WRITE FAIL, INSERT TSNS OF MTS INTO FINTER BLOC
   4 5CTL    #   
  21 5D*=          BBUS  3,6,NOEOT1             [J IF NOT HIT END OF TAPE OR WRITE FA
  19 5DSW          LDX   5  AMTSR(3)            [X5=NO. OF MTS IN THIS INCR. 
  10 5F#G          MHUNTW   3,FILE,FINTER
  21 5FS6          ALTLENG  3,5,SFINDFINT       [LENGTHEN BLOCK FOR TSNS FOR WHICH   
  20 5G?Q                                       [      ^BMNCURR^  NEEDS SETTING  
  16 5GRB          STEPAGAIN                    [X3-> LAST RECORD
  10 5H?2          MHUNTW   2,FILE,FINTER
   8 5HQL          ADN   2  A1   
  21 5J==          LDN   1  AMAGR(3)            [X1-> START OF MT WORDS IN LAST RECOR
   5 5JPW    NXFINT  
   8 5K9G          LDX   0  0(1) 
  19 5KP6          STO   0  0(2)                [INSERT TS IN FINTER BLOCK   
   8 5L8Q    #SKI  K6INCEND>99-99
  20 5LNB          TRACE 0,INCNOWPR             [TSN OF MT NOT NOW NEEDING WPR   
   7 5M82          ADN   2  1
  16 5MML          ADN   1  INCMAGLEN           [UPDATE MODIFIERS
  18 5N7=          BCT   5  NXFINT              [J IF MORE TSNS TO INSERT
   5 5NLW    NOEOT1  
   8 5P6G          LDX   2  FX2  
   9 5PL6          LDX   0  AWORK4(2)
  20 5Q5Q          BXE   0  AINCNOR(3),YFIRST   [J IF NO RELIABLE PREVIOUS INCRS.
   4 5QKB    #   
  19 5R52 ...#  NOW LOOK AT PREVIOUS (RELIABLE OR 'IN PROGRESS') RECORD AND  
  20 5RJL    #    1) REMOVE BIT BMLASTRV FOR THOSE TAPES WHICH ARE IN LAST RECORD
  21 5S4=    #    2) SET IN FINTER BLOCK TSNS OF THOSE TAPES NOT PRESENT IN LAST RECO
   4 5SHW    #   
  18 5T3G          READAGAIN                    [READ LAST RECORD AGAIN  
   5 5TH6    RBACK   
  18 5W2Q          READBACK                     [READ LAST BUT ONE RECORD
  10 5WGB          MHUNTW   3,FILE,FRB   
  19 5WMJ ...      JBS      NRFREE,3,BIUNFIN  [IGNORE NSUITRV BIT IF INCR IN 
  14 5WSQ ...                                   [PROGRESS
  19 5X22          JBS   RFREE,3,BINSUITRV      [J IF PREV.INC.NOT SUITABLE  
   5 5X9S ...NRFREE  
  21 5XFL          NAME  3,FILE,FWB             [X3-> PREVIOUS LAST RELIABLE RECORD  
  21 5X_=          LDX   5  AMTS(3)             [X5= NO. OF MTS IN LAST BUT ONE RECOR
  15 5YDW          BZE   5  NOCOMP              [J IF NOT MTS
   5 5YYG    NXTMT   
  16 5_D6          MHUNTW   2,FILE,FRB          [X2-> LAST RECORD
  21 5_XQ          LDX   0  AMAG(3)             [X0= NEXT TSN IN LAST BUT ONE RECORD 
  20 62CB          LDX   1  AMTS(2)             [X1= NO. OF MTS IN LAST RECORD   
   5 62X2    MORETSNS
  20 63BL          BXE   0  AMAG(2),SAME         [J IF FOUND TSN IN LAST RECORD  
  19 63W=          ADN   2  INCMAGLEN           [UPDATE MOD. IN LAST RECORD  
  19 64*W          BCT   1  MORETSNS            [J IF MORE TSNS TO LOOK AT   
   8 64TG          LDX   2  FX2  
  14 65*6          STO   0  ACOMMUNE1(2)        [KEEP TSN
  10 65SQ          MHUNTW   1,FILE,FWB   
  21 66#B          SBX   3  1                   [X3= MOD. DOWN LAST BUT ONE RECORD   
  15 66S2          STO   3  ACOMMUNE2(2)        [KEEP MOD.   
  21 67?L          STO   5  ACOMMUNE3(2)        [KEEP NO. OF MTS MOD. IN LAST BUT ONE
  10 67R=          MHUNTW   3,FILE,FINTER
  10 68=W          LDX   5  ALOGLEN(3)   
   7 68QG          ADN   5  1
  20 69=6          ALTLENG  3,5,SFINDFINT       [LENGTHEN FINTER BLOCK BY 1 WORD 
  10 69PQ          MHUNTW   3,FILE,FINTER
  10 6=9B          LDX   0  ACOMMUNE1(2) 
   7 6=P2          SMO      5
  19 6?8L          STO   0  A1-1(3)             [INSERT TSN AT END OF LIST   
   8 6?N=    #SKI  K6INCEND>99-99
  21 6#7W          TRACE 0,INCNOWPR             [TSN OF TAPE NO LONGER NEEDING WPR   
  15 6#MG          LDX   5  ACOMMUNE3(2)        [RESTORE X5  
  10 6*76          MHUNTW   3,FILE,FWB   
  20 6*LQ          ADX   3  ACOMMUNE2(2)        [AMAG(3) IS LAST TSN DEALT WITH  
  18 6B6B                                       [IN LAST BUT ONE RECORD  
   5 6BL2    UPDINC  
  21 6C5L          ADN   3  INCMAGLEN           [UPDATE MOD IN LAST BUT ONE RECORD   
  16 6CK=          BCT   5  NXTMT               [J IF MORE MTS   
  19 6D4W          REWRITE                      [REWRITE LAST BUT ONE RECORD 
   9 6DJG          BRN      NOCOMP   
   4 6F46    SAME
  21 6FHQ          BC   3,BMLASTRV              [REMOVE LAST RVABLE INCR FROM MT STAT
  20 6G3B                                       [   WORD IN LAST BUT ONE RECORD  
   9 6GH2          BRN      UPDINC   
   5 6H2L    RFREE   
   7 6HG=          FREECORE 3
   8 6H_W          BRN      RBACK
   4 6JFG    #   
  21 6J_6    #  NOW (STARTING AT THE LAST BUT ONE RECORD IN INCINDEX), READ BACKWARDS
  21 6KDQ    #  THROUGH INCINDEX SETTING BMNCURR (B5) IN THE AMAGSTATE WORDS OF THOSE
  13 6KYB    #  MTS WITH THEIR TSN IN FINTER BLOCK.  
   9 6LD2    #  FINISH WHEN EITHER   
  12 6LXL    #    1) COME TO BEGINNING OF FILE   
  20 6MC= ...# OR 2) NO FINTER ENTRY AT ALL FOUND IN A COMPLETE INCREMENT RECORD.
   4 6MWW    #   
   5 6NBG    NOCOMP  
   9 6NW6          MFREEW   FILE,FRB 
  18 6P*Q          MHUNTW   3,FILE,FWB          [X3-> LAST BUT ONE RECORD
   5 6PTB    MARKWPR 
  10 6Q*2          MHUNTW   1,FILE,FINTER
  10 6QSL          LDX   2  ALOGLEN(1)   
  21 6R#=          BZE   2  ZFINT               [J IF NO TSNS TO MARK WITH BMNCURR   
   8 6RRW          SMO      FX2  
  21 6S?G          STOZ     ACOMMUNE1           [CLEAR ^NON-CURRENT TSNS FOUND^ MARKE
   4 6SR6    TSN 
   7 6T=Q          SMO      2
  19 6TQB          LDX   5  A1-1(1)             [X5= NEXT TSN IN FINTER BLOCK
  18 6W=2          LDX   0  AMTS(3)             [X0= NO. OF MTS IN FWB   
  15 6WPL          BZE   0  ZFINT               [J IF NO MTS 
   5 6X9=    TNXTSN  
  16 6XNW          BXE   5  AMAG(3),TSNFOUND    [J IF FOUND TSN  
  17 6Y8G          ADN   3  INCMAGLEN           [UPDATE MOD IN FWB   
  16 6YN6          BCT   0  TNXTSN              [J IF MORE TSNS  
   5 6_7Q    TESTFINT
  10 6_MB          MHUNTW   3,FILE,FWB   
  20 7272          BCT   2  TSN                 [J IF MORE TSNS FROM FINTER BLOCK
  12 72BS ...      JBS      NZFINT,3,BINSUITRV   
   8 72LL          SMO      FX2  
   9 736=          LDX   0  ACOMMUNE1
  21 73KW          BZE   0  ZFINT               [J IF NO TSNS IN FINTER BLOCK IN THIS
  21 745G                                       [ (ASSUME WE HAVE NOW DEALT WITH ALL 
  18 74K6                                       [   RECORDS NECESSARY)   
   5 74SY ...NZFINT  
  19 754Q          LDX   5  AINCNO(3)           [X5= INCR. NO. OF THIS RECORD
  19 75JB          REWRITE                      [ENSURE BMNCURRS IN RECORD   
   9 7642          MFREEW   FILE,FWB 
  21 76HL          BXE   5  AWORK4(2),YFIRST    [J IF NOW BACK TO 1ST INCR. IN INCIND
  17 773=          READBACK                     [READ PREVIOUS RECORD
  10 77GW          MHUNTW   3,FILE,FRB   
  10 782G          NAME     3,FILE,FWB   
  21 78G6          BRN      MARKWPR             [GO & CHECK IF BMNCURRS TO BE INSERTE
  17 78_Q                                       [   IN THIS RECORD   
   5 79FB    TSNFOUND
   8 79_2          SMO      FX2  
  21 7=DL          STO   5  ACOMMUNE1           [SET NON-ZERO MARKER (MT PRESENT IN W
  20 7=Y=                                       [           BMNCURR   IS NOW SET)
  20 7?CW          BS    3,BMNCURR              [SET WILL NOT BE DUMPED ON BIT   
   9 7?XG          BRN      TESTFINT 
   5 7#C6    ZFINT   
  14 7#WQ          FREECORE 3                   [FREE FWB
   5 7*BB    YFIRST  
  10 7*W2          MFREEW   FILE,FINTER  
   9 7*Y9 ...      BBUS     2,6,NGRST
   6 7B2D ...      WIND  
  19 7B4M ...      CALL  7  SWRITE                      [IF RESTABLE INCR SET
  19 7B6W ...      BS      3,BIFINALR                  [FINAL BIT IN LAST REC
   5 7B95 ...NGRST   
  19 7B?# ...      JBS      NCLOSE,2,MBEMS     [IF EMS ENTRY DONT CLOSE II   
  16 7B*L          CLOSETOP                     [CLOSE INCINDEX  
   5 7BKD ...NCLOSE  
   8 7BT=          ADX   4  FX1  
   7 7C#W          EXIT  4  0
   4 7CB7 ...#   
   4 7CCD ...#   
  19 7CDP ...#  OUTPUTS A BLANK LINE TO THE MONFILE, CALLED ON X7, USES X4.  
   7 7CG2 ...#  COORDINATES. 
   4 7CH? ...#   
   5 7CJJ ...OUTBLANK
   8 7CKT ...      SBX   7  FX1  
   8 7CM6 ...      LDN   4  #40  
  10 7CNC ...      OUTMOND  0,ACOMMUNE1,4
   8 7CPN ...      ADX   7  FX1  
   7 7CQ_ ...      EXIT  7  0
   4 7CSG    #   
   4 7D#6    #   
  21 7DRQ    #  SETS UP A PARAMETER BLOCK READY FOR 'MONOUT' WITH INCR. NO. AS 1ST   
  21 7F?B    #  PARAMETER AND SERIAL NOS. (ON WHICH INCR. IS TO BE DUMPED) AS 2ND.   
  19 7FR2 ...#  ON ENTRY, RELEVANT FINC BLOCK IS CALLED FGET. X4 IS S/R LINK.
  18 7G=L    #  ON EXIT, X1=FX1, X2=FX2.        USES X0, X3, X5, X6 & X7.
   6 7GQ=    SETMONOUT   
   8 7H9W          SBX   4  FX1  
  16 7HPG ...      MHUNTW   3,FILE,FGET         [X3-> FGET BLOCK 
  19 7J96          LDX   5  INCCOPIES(3)        [X5= NO. OF MTS TO DUMP ON   
  19 7JNQ          LDX   3  INCNO(3)            [X3= NO. OF THIS INCREMENT   
  21 7K8B          OUTBLOCN 11                  [GET PARAMETER BLOCK -SHOULD AMPLY CO
  16 7KN2                                       [   MOST CASES   
  19 7L7L          OUTPACK  3,1,INCNUM          [INC.NO. TO PARAMETER BLOCK  
   7 7LM=          SBN   5  1
  15 7M6W          BNZ   5  SPARAM              [J IF >1 TAPE
  16 7MLG          OUTNULL                      [NULL PARAMETER  
   9 7N66          BRN      PARASENT 
   5 7NKQ    SPARAM  
   8 7P5B          LDCT  0  #630 
  10 7PK2          STO   0  ACOMMUNE1(2) 
  16 7Q4L          OUTPARN  1,ACOMMUNE1         [MAKE MT PLURAL  
   5 7QJ=    PARASENT
  16 7R3W          MHUNTW   3,FILE,FGET         [X3-> FGET BLOCK 
  21 7RHG          MHUNTW   2,FILE,FQUAL        [X2-> FQUAL BLOCK (USED SIMPLY AS A B
  18 7S36                                       [                   HERE)
  17 7SGQ          STOZ     1                   [ZEROISE CHAR. COUNT 
  18 7T2B ...      LDX   5  INCCOPIES(3)        [NO OF MTS IN FINC(FGET) 
   5 7TG2    NXTSN   
  18 7WF=          LDX   7  FGETSER(3)          [X7 = NEXT TSN IN FGET   
   7 7WYW          LDN   0  8
   5 7XDG    NXOCT   
   7 7XY6          LDN   6  0
   7 7YCQ          SLL   67 3
  17 7YXB          BNZ   6  NZERO               [J IF DIGIT NON-ZERO 
  20 7_C2          BPZ   4  NONSIG              [J IF DIGIT NON-SIGNIFICANT ZERO 
   5 7_WL    NZERO   
  21 82B=          ORX   4  GSIGN               [ENSURE 'SIGNIFICANT DIGITS' MARKER S
  19 82TW          DCH   6  A1(2)               [PUT CHAR. INTO FQUAL BLOCK  
   7 83*G          BCHX  2  /
  21 83T6          ADN   1  1                   [UPDATE COUNT OF CHARS. IN PARAMETER 
   5 84#Q    NONSIG  
  19 84SB          BCT   0  NXOCT               [J IF MORE CHARS. TO EXTRACT 
   8 85#2          ERX   4  GSIGN
   8 85RL          LDN   6  #20  
  17 86?=          DCH   6  A1(2)               [INSER3 SPACE CHA$R. 
  17 86QW          ADN   1  1                   [UPDATE CHAR. COUNT  
   7 87=G          BCHX  2  /
  17 87Q6 ...      ADN   3  FGETLEN             [UPDATE MOD DOWN FGET
  18 889Q ...      BCT   5  NXTSN               [J IF MORE MTS IN FGET   
  21 88PB          OUTPARAM 1,A1,FILE,FQUAL     [PUT TSNS IN OUTPUT PARAMETER BLOCK  
   8 8992          ADX   4  FX1  
   7 89NL          EXIT  4  0
   4 8=8=    #   
   4 8=MW    #   
   4 8=RR ...#   
  19 8??C ...X1INCEND                           [ENTRY FROM INCSTART SEGMENT 
  18 8?R3 ...#  THE DUMPING CODE HAS FINISHED. ONE OR BOTH OF THE INCRS  
  19 =BHQ ...#  BEING PRODUCED HAS BEEN COMPLETED FOR ONE REASON OR ANOTHER  
  18 =C3B ...#  WE LOOK AT EACH FINC IN TURN AND WIND UP THOSE WHICH HAVE
   8 =CH2 ...#  BIT BINCFIN SET. 
   4 =D2L ...#   
  10 =DG= ...      MFREE    ADATA,CREADL 
  10 =D_W ...      MFREE    FILE,FLOCNB  
  19 =FFG ...      BC       2,MBFINAL           [SET 'FINAL INC NOT PRODUCED'
  17 =F_6 ...      JBC      NOFERUS2,,BFERUS    [J IFNO FERUS SET UP 
  10 =GDQ ...      MHUNTW   3,FILE,FINC  
  12 =GYB ...      JBC      WAITFER,3,BINCGENRES 
  21 =HD2 ...      JBC      NOFERUS2,3,BINCFIN [ONLY FREE FERUS IF NOT STILL PRODUCIN
  18 =HXL ...                                   [A RESTORABLE INCRMENT   
   5 =JC= ...WAITFER 
  20 =JWW ...      POP   WAITFER,,FERUSALT      [WAIT TILL CAN ALTER THE FERUS   
  18 =KBG ...      BC    ,BFERUS                [CLEAR FERUS EXISTS BIT  
  15 =KW6 ...      HUNTMISB 3,FI,FERUS          [X3->FERUS   
  14 =L*Q ...      FREECORE 3                   [FREE IT 
  20 =LTB ...      VOP   ,FERUSALT              [RELEASE FERUS ALTERATION SEMA   
   5 =M*2 ...NOFERUS2
  21 =MSL ...      LONGON   ADUMPWT             [RELEASE ACTS. 'WAITING FOR DUMPER TO
  21 =N#= ...                                   [                   FINISH INCREMENT'
  10 =NRW ...      VFREEW   FI,FINCCOPY  
  20 =P?G ...      MHUNTW   3,FILE,FABSNB       [RENAME FABSNB FOR SAFTEYS SAKE  
  18 =PR6 ...      NAME     3,FI,FDUMPFAB       [IN CASE SOMEONE FREES IT
  17 =Q=Q ...      MHUNTW   3,FILE,FINC         [X3->1ST FINC BLOCK  
  20 =QQB ...      JBS      Y1ST,3,BINCFIN      [J IF THIS INCR NEEDS WINDING UP 
  19 =R=2 ...      HUNT2    3,FILE,FINC,3       [OTHERWISE IT MUST BE THE 2ND
  12 =RPL ...      JBC      (GEOERR),3,BINCFIN   
  18 =S9= ...Y1ST  NAME     3,FILE,FGET         [RENAME IT TO IDENTIFY IT
   4 =SNW ...#   
  18 =T8G ...#  WE HAVE LOCATED AN INCR WHICH IS TO BE FINISHED OFF. THE 
  20 =TN6 ...#  FINC HAS BEEN RENAMED A FGET BLOCK FOR EASE OF IDENTIFIACTION.   
  17 =W7Q ...#  WE UPDATE THE RECORD IN INCINDEX FOR THIS INCREMENT  
   4 =WMB ...#   
   9 =X72 ...      LDX   0  CLEANCT  
  10 =XLL ...      SBX   0  INCBEGDUMP(3)
  20 =Y6= ...      BPZ   0  NMIDNIGHT           [J IF NOT DUMPED OVER MIDNIGHT   
   8 =YKW ...      SMO      FX1  
  20 =_5G ...      ADX   0  SCLEANDAY           [ADD ON DAY'S WORTH OF CLEANCTS  
   6 =_K6 ...NMIDNIGHT   
  17 ?24Q ...      STO   0  INCBEGDUMP(3)       [TIME TAKEN TO DUMP  
  18 ?2JB ...      LDX   5  INCNO(3)            [X5= CURRENT INCR. NO.   
  20 ?342 ...      LDX   6  INCREPLY(3)         [X6= REPLY WORD FOR THIS INCR.   
  16 ?3HL ...      CALL  4  UPDATEINDEX         [UPDATE INCINDEX 
   4 ?43= ...#   
  19 ?4GW ...#  AND OUTPUT THE END OF INCREMENT MESSAGE TO ALL OPS CONSOLES  
   4 ?52G ...#   
  18 ?5G6 ...      CALL  7  OUTBLANK            [BLANK LINE TO MONFILE   
  20 ?5_Q ...      CALL  4  SETMONOUT           [SET PARAMETER BLOCK FOR MONOUT  
  19 ?6FB ...      INFORM   3,INCFIN,1          [OUTPUT END OF INCR MESSAGE  
   4 ?6_2 ...#   
  12 ?D5_ ...      MHUNTW 3,FILE,FGET           [
   4 ?D9W ...#   
  20 ?DPG ...#  IF THE INCREMENT WAS RESTORABLE WE NOW OUTPUT A MESSAGE SAYING SO
   4 ?F96 ...#   
   8 ?FNQ ...      LDCT  4  #300 
   8 ?G8B ...      LDCT  2  #300 
  10 ?GN2 ...      ANDX  4  INCREPLY(3)  
   7 ?H7L ...      SBX   4  2
  21 ?HM= ...      BNZ   4  NOTEOB              [J IF NOT GENERAL RESTOREABLE INCR. (
  21 ?J6W ...                                   [  RESTOREABLE IF B1 & B2 OF REPLY WO
  17 ?JLG ...                                   [           BOTH SET)
   8 ?K66 ...      LDX   2  FX2  
  20 ?KKQ ...      BS       2,MBFINAL           [REMEMBER A FINAL INCREMENT HAS  
  16 ?L5B ...                                   [BEEN PRODUCED   
  18 ?L*8 ...      BC       ,BTOTD               [CLEAR TOTAL DUMP BIT   
  20 ?LK2 ...      CALL  4  SETMONOUT           [SET PARAMETER BLOCK FOR MONOUT  
  21 ?M4L ...      MONOUT   INCSUIT             [PUT OUT 'SUITABLE FOR GENERAL RESTOR
  21 ?MJ= ...      LONGON   AFINDUMP            [RELEASE ACTS. WAITING FOR GENERAL-  
   5 ?MS4 ...NOTEOB  
  21 ?N3W ...      FON   BSUJWT                 [RELEASE UNJAMMER (IF WAITING FOR DUM
   6 ?P36 ...#UNS  JSKI28
   4 ?PGQ ...(   
  17 ?Q2B ...#     **************************************************
  11 ?QG2 ...#     IP REDUMP  CSS SPECIAL MOD
   9 ?Q_L ...      SEGENTRY K98INCEND
   9 ?RF= ...      BRN      NOCSSMOD 
   9 ?RYW ...XY             12HREDUMP
   9 ?SDG ...ZY    GEOERR   1,REDUMP 
   9 ?SY6 ...      SEGENTRY K97INCEND
  10 ?TCQ ...      LDX   6  INSTPARAM+1  
   7 ?TXB ...      ADN   6  1
   8 ?WC2 ...      SMO      FX1  
   8 ?WWL ...      LDN   5  XY   
  11 ?XB= ...      INSTPARA ZY,5,6,NOCSSMOD  
   5 ?XTW ...NOCSSMOD
   9 ?Y*G ...      SEGENTRY K96INCEND
  18 ?YT6 ...#      __________________________________________________   
   4 ?_#Q ...)   
   4 ?_SB ...#   
  18 #2#2 ...#  SEGMENT INCFIN FINISHES OFF THE TIDYING UP OF INDIVIDUAL 
   7 #2RL ...#  INCREMENTS   
   4 #3?= ...#   
  18 #3QW ...      DOWN     INCFIN,1            [FINISH TIDYING UP INCR. 
  18 #4=G ...      CALL  7  OUTBLANK            [BLANK LINE TO MONFILE   
   5 #4CN ...#UNS IDC
   4 #4JW ...#SKI
  19 #4Q6 ...      MFREEW   FILE,FGET           [FAREWELL TRUSTY FINC(FGET)  
  10 #59Q ...      HUNTW    3,FILE,FINC  
  20 #5PB ...      BNG   3  NOFINCS             [J IF NO MORE INCS THAT MAY WANT 
  15 #692 ...                                    [TIDYING UP 
  18 #6NL ...      JBS      Y1ST,3,BINCFIN      [J IF MORE TIDYING NEEDED
   5 #78= ...NOFINCS 
  21 #7MW ...#     ******************************************************************
   4 #87G ...#   
  18 #8M6 ...#     WE GO ACROSS TO INCFIN TO FINISH TIDYING UP INCREMENT 
   4 #96Q ...#   
   9 #9LB ...      ACROSS   INCFIN,2 
   4 #NQQ ...#   
   4 #R8W ...#   
  19 #RH? ...#   THE SEGMENT OVERFLOWED AND THE CODE FOR THE K2 ENTRY POINT  
  11 #RTN ...#  HAS BEEN REMOVED TO INCCHECK 
  21 #S86 ...#  ENTRY 2. THE ONE PIECE OF THIS SEGMENT WHICH IS STILL NEEDED IS AN   
  20 #SMQ ...#  OCCASIONAL ENTRY TO SR UPDATEINDEX AT LABEL NOAPROC. THIS IS DONE
  10 #T7B ...#  VIA ENTRY TO K3INCEND.   
   4 #WL= ...#   
   4 #X5W ...#   
   4 #XKG ...#   
   5 #Y56 ...X3INCEND
  16 #YJQ ...      BS       2,MBEMS             [SHOW EMS ENTRY  
  18 #_4B ...      LDN   4  UP                  [RELATIVISED RETUNR LINK 
   9 #_J2 ...      BRN      NOAPROC  
   5 #_RS ...UP    UP
   4 *23L ...#   
   4 *2H= ...#   
  10 *32W ...      MENDAREA 40,K99INCEND 
   4 *3GG ...#   
   4 *426 ...#END
   8 ____ ...04015457000300000000
