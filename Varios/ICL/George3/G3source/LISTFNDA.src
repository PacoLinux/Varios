   8 22FL ...#OPT  K0LISTFNDA=0  
  18 22_= ...#LIS  K0LISTFNDA>K0LISTFILE>K0ALLGEO>K0GREATGEO>K0COMMAND   
  15 23DW ...#SEG  LISTFNDA                     [ROB RUSHTON 
  10 23YG ...               8HLISTFNDA   
  11 24D6 ...      SEGENTRY K1LISTFNDA,POINT1
  12 24XQ ...      SEGENTRY  K2LISTFNDA,POINT2   
  12 25CB ...      SEGENTRY  K3LISTFNDA,POINT3   
  19 25X2 ...[K1LISTFNDA    THIS ENTRY DELETES A REQUEST FROM :SYSTEM.OUTPUT 
  18 2CMG    [              AND FORMS THE FIRST STAGE OF AN LFDEL MACRO  
   4 2D76    [   
  19 2DLQ ...[K2LISTFNDA    IS ENTERED BY A CPAT WHICH WAS BROKEN-IN ON WHILE
  20 2F6B    [              WAITING FOR AN OUTPUT DEVICE FOR AN ONLINE REQUEST   
  18 2FL2    [              IT DELETES THE REQUEST FROM :SYSTEM.OUTPUT   
   4 2G5L    [   
  19 2GK= ...[K3LISTFNDA     THIS ENTRY POINT IS USED BY THE SCHEDULER FROM  
  17 2H4W    [               THE LFCHANGE MACRO ISSUED BY CHANGELIST 
   4 2HJG    [   
   4 2LG=    [   
  21 2L_W    [***********************************************************************
   6 2MFG    [DATA AREA  
   4 2M_6    [   
  15 2NDQ    [     THERE FOLLOWS A SELECTION OF IDENTIFIERS  
   7 2NKY ...#UNS G3SIGNET   
  19 2NR6 ...XSIGPROP       #40000              [SIGNET PROPERTY IDENTIFIER. 
  12 2NYB    [     FOR BIT POSITIONS IN GMODE.   
  19 2PD2    XIMPBIT        #21400000           [IMPLEMENTABLE REQUEST MASK  
  20 2PXL    PROPBIT        #4000000            [ THERE IS A PROPERTY PARAMETER  
  17 2QC=    SIMPBIT        #20000000           [BEING IMPLEMENTED   
  18 2QWW    XPARTIMP       #2000000            [PARTIALLY IMPLEMENTED   
  16 2RBG    XFILOFFBS      #1400000            [FILES OFFLINE   
  18 2RW6    SETUPBIT       #10000000           [THERE IS A SETUP FILE   
   4 2S*Q    [   
   4 2YQB    [   
   4 2_=2    [   
  21 2_PL    [***********************************************************************
   7 329=    [SUBROUTINE AREA
   4 3CWQ    [   
   5 3DBB    SKIPENT 
  19 3DW2    [THIS S/R ADVANCES THE READ POINTERS TO THE NEXT ENTRY IN SYSOUT
  19 3F*L    [X6 CONTAINS A COPY OF GMODE(WHICH MAY HAVE BEEN DOCTORED IF WE 
  18 3FT=    [ARE NO LONGER POSITIONED ON THE FIRST RECORD OF THE ENTRY).
   8 3G#W          SBX   7  FX1  
   8 3GSG          LFRECNUM 1,6  
   8 3H#6          SKIP     ,0(1)
   8 3HRQ          ADX   7  FX1  
   7 3J?B          EXIT  7  0
   4 3JR2    [   
   4 3K=L    STEP
   8 3KQ=          SBX   4  FX1  
   6 3L9W          STEP  
   8 3LPG          ADX   4  FX1  
   7 3M96          EXIT  4  0
   4 4MY2    [   
   5 4NCL    READREC 
  14 4NX=    [THIS S/R READS THE NEXT RECORD FROM SYSOUT 
   8 4PBW          SBX   6  FX1  
   6 4PWG          READ  
  10 4QB6          MHUNTW   1,FILE,FRB   
   8 4QTQ          ADX   6  FX1  
   7 4R*B          EXIT  6  0
   5 4RT2    SCRUBENT
  13 4S#L    [THIS S/R DELETES A REQUEST FROM SYSOUT.
  16 4SS=    [IT EXPECTS ACOMMUNE1 AND ACOMMUNE2 TO CONTAIN KEYS 
  16 4T?W    [IT WILL PICK UP ANY FABSNBS AND ALSO THE CONSOLE   
  13 4TRG    [PROPERTY ASSOCIATED WITH THE REQUEST.  
  18 4W?6    [IF WE ARE WITHIN LFCHANGE MACRO WE NEED TO PRESERVE COPY   
  12 4WQQ    [OF THE APROPNOS BLOCK IN GOUT/INFO4
   8 4X=B          SBX   7  FX1  
  14 4XQ2          GETSOUT  ,ACOMMUNE1(2),ACOMMUNE2(2)   
   9 4Y9L          TESTREP2 OK,SCOK  
   9 4YP=          GEOERR   1,INDXREP
   7 4_8W    SCOK  READAGAIN 
  10 4_NG          MHUNTW   1,FILE,FRB   
  10 5286          NAME     1,GOUT,INFO  
   9 52MQ          LDX   5  GMODE(1) 
   8 537B          LDX   1  FX1  
  10 53M2          LDX   0  PROPBIT(1)   
   7 546L          ANDX  0  5
  16 54L=          BZE   0  SCNP                [J IF NO PROPERTY
   9 555W          LDX   0  AWORK1(2)
   7 55KG          SBN   0  4
  17 5656          BNZ   0  NOTCHANGE           [J IF NOT LFCHANGE   
  14 56JQ    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   
  13 574B    [CODE TO CATER FOR CH WITH UR PARAMETER 
  20 57J2          CALL  6  READREC             [ELSE PRESERVE PROPERTY RECORD   
  10 583L          NAME     1,GOUT,INFO4 
   8 58H=          BRN      SCNP 
   6 592W    NOTCHANGE   
  14 59GG    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!   
   8 5=26          CALL  4  STEP 
  21 5=FQ    [IT IS POSSIBLE THAT THIS CODE WILL BE OBEYED DURING E.M.S. BY BACKMAP  
   9 5=_B          LDCH  0  ATYPE(2) 
   9 5?F2          SBN   0  ACTLF/64 
  16 5?YL          BNZ   0  SCNP                [J IF NOT ACTLF  
  19 5#D=          LDX   0  CPROPNO-A1(3)       [TRANSFER CONSOLE PROPERTY   
  14 5#XW          STO   0  GSWORK3(2)          [NUMBER  
  15 5*CG    SCNP  BNG   5  SCOL                [J IF ONLINE 
   9 5*X6          CALL  6  READREC  
  10 5BBQ          NAME     1,GOUT,INFO2 
   7 5BWB          LDX   3  1
   8 5CB2          LDX   1  FX1  
  17 5CTL          CALL  6  WFLTEST             [CHECK FOR WORKFILE  
  10 5D*=    SCOL  LDX   0  SETUPBIT(1)  
   7 5DSW          ANDX  0  5
  17 5F#G          BZE   0  SCNS                [J IF NO SETUP FILE  
   9 5FS6          CALL  6  READREC  
  10 5G?Q          NAME     1,GOUT,INFO3 
   7 5GRB          LDX   3  1
   8 5H?2          LDX   1  FX1  
   9 5HQL          CALL  6  WFLTEST  
   8 5J==    SCNS  LFRECNUM 4,5  
  19 5JPW    [WE HAVE TO DELETE THE RECORDS IN REVERSE ORDER AS THE FILESTORE
  18 5K9G    [GETS CONFUSED IF WE DELETE THE LAST KEY-RECORD FROM A BLOCK
  10 5KP6    [WITHOUT EMPTYING THE BLOCK.
   9 5L8Q    SCDEL CALL  6  SDELETE  
   8 5LNB          BCT   4  SCDEL
   8 5M82          CALL  4  STEP 
   8 5MML          ADX   7  FX1  
   7 5N7=          EXIT  7  0
   4 5NLW    [   
   5 5P6G    SDELETE 
   8 5PL6          SBX   6  FX1  
   6 5Q5Q          DELETE
   8 5QKB          ADX   6  FX1  
   7 5R52          EXIT  6  0
   4 5RJL    [   
  21 5S4=    [THI S/R CHECKS WHETHER THE FABSNB POINTED AT BY X3 IS FOR A WORKFILE.  
  10 5SHW    [IF SO, B16 OF ATYPE IS SET 
   4 5T3G    [   
  10 5TH6    WFLAB          12HWORKFILE  
   5 5W2Q    WFLTEST 
  13 5WGB          TESTNAMX 3,WFLAB(1),A1+1(3),XT,2  
   8 5X22          LDN   0  #200 
   9 5XFL          ORS   0  ATYPE(3) 
   8 5X_=    XT    LDX   2  FX2  
   7 5YDW          EXIT  6  0
   4 #Y56    [   
   4 #YJQ    [   
  21 #_4B    [***********************************************************************
   5 #_J2 ...POINT1  
  15 *23L    [THIS ENTRY POINT IMPLEMENTS THE LFDEL MACRO.   
  10 *2H=          LDX   0  ACOMMUNE3(2) 
  16 *32W          STO   0  AWORK1(2)           [PRESERVE SWITCH 
   7 *3GG          PICKREP  0
  16 *426          STO   0  AWORK2(2)           [PRESERVE REPLY  
  15 *4FQ          CALL  7  SCRUBENT            [DELETE ENTRY
  10 *4_B          ACROSS   LISTNEXT,3   
  10 *5F2    [              ************ 
   4 *5YL    [   
  21 *6D=    [***********************************************************************
   5 *6XW ...POINT2  
  19 *7CG    [THIS IS THE ENTRY POINT FOR AN ONLINE CPA WHICH HAS BEEN BROKEN
  19 *7X6    [IN ON,AND HAS REASON TO BELIEVE THE REQUEST IS NOT CURRENTLY   
   8 *8BQ    [BEING IMPLEMENTED  
  10 *8WB    NOB   OPENSOUT NOB,GENERAL  
  15 *9B2          LDX   4  AWORK2(2)           [PICK UP KEY 
   7 *9TL          LDN   5  0
  19 *=*=          GETSOUT  ,4,5                [LOCATE REQUESTS FOR THIS JOB
   7 *=SW          STEPAGAIN 
   9 *?#G    TRT   BZE   3  NFOUND   
  11 *?S6          TXU   4  GOUTKEY1-A1(3)   
   9 *#?Q          BCS      NFOUND   
  10 *#RB          LDX   6  GMODE-A1(3)  
  17 **?2          BNG   6  THISO               [J IF REQUEST FOUND  
   9 **QL          CALL  7  SKIPENT  
   8 *B==          BRN      TRT  
  10 *BPW    THISO LDX   7  SETUPBIT(1)  
  16 *C9G          LDX   0  GOUTKEY2-A1(3)      [REMEMBER SUB KEY
   9 *CP6          STO   0  AWORK3(2)
  17 *D8Q          ANDX  7  6                   [FIND OUT ABOUT SETUP
   8 *DNB          LFRECNUM 5,6  
  10 *F82          ANDX  6  PROPBIT(1)   
  16 *FML          BZE   6  NOLP                [J NO PROPERTIES 
   8 *G7=          CALL  4  STEP 
   8 *GLW    NOLP  BZE   7  NOLS 
   9 *H6G          CALL  6  READREC  
  10 *HL6          NAME     1,FILE,FABSNB
   7 *J5Q          LDX   3  1
   8 *JKB          LDX   1  FX1  
  17 *K52          CALL  6  WFLTEST             [CHECK FOR WORKFILE  
   6 *KJL          LFTHAW
   9 *L4=    NOLS  CALL  6  SDELETE  
   8 *LHW          BCT   5  NOLS 
  16 *M3G          LFHUNT   7,AWORK2(2),AWORK3(2),GOUT,GUPDATE   
   9 *MH6          BNG   3  NFOUND   
  10 *N2Q          LDX   0  XFILOFFBS(1) 
   9 *NGB          ANDX  0  GMODE(3) 
  18 *P22          BZE   0  NOFFLON             [J IF NO OFFLINE FILES   
   9 *PFL          LDX   1  FPTR(3)  
  16 *P_=          FREECORE 1                   [ELSE FREE FABSNB
   5 *QDW    NOFFLON 
  15 *QYG          FREECORE 3                   [FREE GUPDATE
   5 *RD6    NFOUND  
  17 *RXQ          CLOSEOUT                     [CLOSE SYSTEM.OUTPUT 
   9 *R_3 ...      FSHCODE  A,XENDA1 
   4 *S2# ...(   
   4 *S3K ...#   
  21 *S4W ...#     SHARED FILESTORE: WE MUST PASS BREAK-IN BACK TO THE 'B' MACHINE   
  10 *S67 ...#     IF THIS IS APPROPIATE 
   4 *S7D ...#   
  19 *S8P ...      NOTALIEN XENDA1              [JIF NOT AN ALIEN ACTIVITY   
  20 *S=2 ...      TRANSFIN                    [RETURN TO 'B' (WITH ZERO OFFSET) 
   4 *S?? ...#   
   5 *S#J ...XENDA1  
   4 *S*T ...)   
   7 *SCB          COMBRKIN  
   4 *SX2    [   
  21 *TBL    [***********************************************************************
  14 *TW=    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!7!!!!!!   
   5 *W*W ...POINT3  
  15 *WTG    [THIS IS THE ENTRY POINT FROM THE LFCHANGE MACRO
  17 *X*6    [USED BY CH TO CHANGE URGENCY OF A :SYSTEM.OUTPUT ENTRY 
  12 *XSQ    [ACOMMUNE1 HOLDS INDEX KEY OF ENTRY 
  12 *Y#B    [ACOMMUNE2 HOLDS INDEX SUBKEY   ^   
  11 *YS2    [ACOMMUNE3 HOLDS THE NEW URGENCY
   7 *_?L          LDN   0  4
   9 *_R=          STO   0  AWORK1(2)
  20 B2=W          LDN   3  ACOMMUNE1(2)        [IDENTIFY INTERFACE PARAMETERS   
   9 B2QG          LDN   4  AWORK2(2)
   7 B3=6          MOVE  3  3
  15 B3PQ          CALL  7  SCRUBENT            [DELETE ENTRY
  10 B49B          MHUNT    1,GOUT,INFO  
  15 B4P2          LDX   0  AWORK4(2)           [NEW URGENCY 
  14 B58L          DCH   0  GOUTKEY1(1)         [CREATE  
  14 B5N=          STOZ     GOUTKEY2(1)         [NEW KEYS
  20 B67W ...      STOZ     GSWORK1(2)          [GWAKECT=0.ACTS AS FLAG IN L/END.
  14 B776          STOZ     GSWORK2(2)          [GMULTID 
  16 B7LQ          DOWN     LISTEND,3           [INSERT REQUEST  
  16 B86B          LDN   0  1                   [UPDATE SUBKEY   
   9 B8L2          ADS   0  AWORK3(2)
  19 B95L          GETSOUT  ,AWORK2(2),AWORK3(2) [REPOSITION READ POINTERS   
   5 B9K=          UP
  14 B=4W    [!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!7!!!!!!   
   4 B=JG    #END
   8 ____ ...25770540000100000000
