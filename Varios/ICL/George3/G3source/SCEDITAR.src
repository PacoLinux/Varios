  13 22FL          SEG      SCEDITAR,,KEITH H MARTIN 
  13 22_=    #LIS      K0SCEDITAR>K0ALLGEO>K0GREATGEO
   4 23DW    #   
   4 23YG    #   
   4 24D6    #   
   4 24XQ    #   
   8 25CB    #              IN   
   8 25X2    #              **   
   4 26BL    #   
  18 26W=          SEGENTRY K1SCEDITAR,SDATA        [SCREENFULL OF DATA  
  17 27*W          SEGENTRY K2SCEDITAR,SCONTROL     [CONTROL +6,+7   
  14 27TG          SEGENTRY K3SCEDITAR,SCOMMANDT    [T   
  14 28*6          SEGENTRY K4SCEDITAR,SCOMMANDP    [P   
  14 28SQ         SEGENTRY K5SCEDITAR,SCOMMANDE     [E   
  14 29#B          SEGENTRY K6SCEDITAR,SCOMMANDF    [F   
  14 29S2          SEGENTRY K7SCEDITAR,SCOMMANDQ    [Q   
   4 2=?L    #   
   4 2=R=    #   
   8 2?=W    #              OUT  
   8 2?QG    #              ***  
  13 2#=6    #     K1SCEDITOR   [OUTPUT POINTER READY
  13 2#PQ    #     K2SCEDITOR   [OUTPUT ASCREEN BLOCK
  14 2*9B    #     K3SCEDITOR   [REOUTPUT ASCREEN BLOCK  
  10 2*P2    #     K4SCEDITOR   [BREAK IN
   4 2B8L    #   
   4 2BN=    #   
   4 2C7W    #   
   4 2CMG    #   
  21 2D76    # ================================================================= #   
   4 2DLQ    #   
   4 2F6B    #   
   4 2FL2    #   
   4 2G5L    #   
  11 2GK=    SSKATC         CATMASK COMMANDS 
   9 2H4W    SSCOMMA        4H000,   
   8 2HJG    SSSPACE        4H000
   8 2J46    SSSPACES       4H   
   9 2JHQ    SSHASH         4H000#   
   9 2K3B    SSMINUS        4H000-   
   9 2KH2    SST            4H000T   
   9 2L2L    SSP            4H000P   
   9 2LG=    SSR            4H000R   
   9 2L_W    SSQ            4H000Q   
   9 2MFG    SSE            4H000E   
   9 2M_6    SSF            4H000F   
   8 2NDQ    SSTEN          +10  
   9 2NYB    SSBITS22       #17777777
  11 2PD2    SSDELIM        16H:;<=>?!^/%&'+/
   9 2PXL    SSMAGICNO      +7036875 
  13 2QC=    SSKATZ         CATMASK FORCETYP,BROADCAS
  11 2QWW    SSSYN          13HSYNTAX ERROR: 
   4 2RBG    #   
   8 2RW6    SS6            +6   
   8 2S*Q    SS7            +7   
   8 2STB    SS8            +8   
  19 2T58 ...XISBITNG        #00010000         [FLAG FOR NON GRAPHIC FILES   
   4 2T*2    #   
   4 2TSL    #   
   4 2W#=    #   
   4 2WRW    #   
   4 2X?G    #   
  21 2XR6    # ================================================================= #   
   4 2Y=Q    #   
   4 2YQB    #   
   4 2_=2    #   
   4 2_PL    #   
  15 329=    #              MONITORING FILE MESSAGE ADDRESSES
  10 32NW    SE2MBAFR       +ISE2MBAFR   
  10 338G    SYROTBOTF      +ISYROTBOTF  
   9 33N6    SYRYBSL        +ISYRYBSL
  10 347Q    SROTKINA       +ISROTKINA   
   9 34MB    SBTNA          +ISBTNA  
  10 3572    SYROTEOTF      +ISYROTEOTF  
  10 35LL    SFMNBUITC      +ISFMNBUITC  
   9 366=    SYFYNF         +ISYFYNF 
   8 36KW    SIT            +ISIT
   9 375G    SNTL           +ISNTL   
   8 37K6    SEA            +ISEA
   9 384Q    SEOE           +ISEOE   
   4 38JB    #   
   4 3942    #   
   4 39HL    #   
   4 3=3=    #   
  21 3=GW    # ================================================================= #   
   4 3?2G    #   
   4 3?G6    #   
   4 3?_Q    #   
   4 3#FB    #   
   4 3#_2    #   
  11 3*DL    #              MATCH SUBROUTINE 
   4 3*Y=    #   
  15 3BCW    #              DOES THE USER GIVEN STRING MATCH 
  12 3BXG    #              WITH THE CURRENT LINE
   4 3CC6    #   
   8 3CWQ    #              GIVEN
  18 3DBB    #              X1=PTR TO STRING(ISPARAMA+2 OR ISPARAMB+2)   
  11 3DW2    #              X2=PTR TO LINE   
   4 3F*L    #   
   9 3FT=    #              LINK X7  
   8 3G#W    #              USES 
  12 3GSG    #              X3 CHAR FROM STRING  
  11 3H#6    #              X4 CHAR FROM LINE
  12 3HRQ    #              X5 LENGTH OF STRING  
  11 3J?B    #              X6 LENGTH OF LINE
  11 3JR2    #              EXIT CALL+1 MATCH
  12 3K=L    #                    CALL+2 N0 MATCH
   5 3KQ=    MATCH   
  11 3L9W    #              CALCULATE LENGTHS
  16 3LPG          LDX   5  0(1)                [STRING LENGTH   
   8 3M96          SMO      FX1  
  16 3MNQ          ANDX  5  SSBITS22            [IGNORE DIRECTION
   8 3N8B          LDCT  6  #600 
   9 3NN2          ANDX  6  FRH+1(2) 
   9 3P7L          BNZ   6  MATCHUP  
   7 3PM=          ADN   6  1
   5 3Q6W    MATCHUP 
   9 3QLG          ADX   6  FRH(2)   
   7 3R66          SLC   6  2
   8 3RKQ          SBN   6  12   
  17 3S5B          BZE   6  MATCHNO           [NO MATCH IF ZERO LINE 
  15 3SK2                                       [LINE LENGTH 
  13 3T4L    #              STEP OVER RED TAPE ETC   
   7 3TJ=          ADN   1  1
   8 3W3W          ADN   2  FRH+2
  10 3WHG    #              TEST MATCH   
   5 3X36    MATCHA  
   8 3XGQ          LDCH  3  0(1) 
   8 3Y2B          LDCH  4  0(2) 
   9 3YG2          BXU   3  4,MATCHNO
  10 3Y_L    #              STEP POINTERS
   7 3_F=          BCHX  1  /
   7 3_YW          BCHX  2  /
  10 42DG    #              TEST COUNTS  
   9 42Y6          BCT   5  MATCHB   
   5 43CQ    MATCHYES
   7 43XB          EXIT  7  0
   5 44C2    MATCHB  
   9 44WL          BCT   6  MATCHA   
   5 45B=    MATCHNO 
   7 45TW          EXIT  7  1
   4 46*G    #   
   4 46T6    #   
   4 47#Q    #   
   4 47SB    #   
  21 48#2    # ================================================================= #   
   4 48RL    #   
   4 49?=    #   
   4 49QW    #   
   4 4==G    #   
   4 4=Q6    #   
  16 4?9Q    #              STEPBACKOLD,STEPBACKNEW SUBROUTINES  
  19 4?PB    #              STEPS BACK TO LINE (X0) IN NEW OR OLD FILE AND   
  13 4#92    #              RESETS CURRENT LINE NO   
   4 4#NL    #   
   9 4*8=    #              LINK X7  
   4 4*MW    #   
  15 4B7G    #              X3 MUST BE PTR TO AMXOR:ASCREEN  
   4 4BM6    #   
   4 4C6Q    #   
   4 4CLB    #   
   4 4D62    #   
   6 4DKL    STEPBACKNEW 
   8 4DTD ...      SBX   7  FX1  
  16 4F5=          LDX   6  ISNCURRENT(3)       [CURREN LINE NO  
  15 4FJW          SBX   6  0                   [DIFFERENCE  
  17 4G4G          TRACE   6,ISNEWBAK          [<<<<<<<<<<>>>>>>>>>> 
   9 4GJ6          BZE   6  STEPBNEX 
  14 4H3Q          STO   0  ISNCURRENT(3)       [RESET   
   5 4HHB    STEPBNA 
   8 4J32          BACKSPACE    0
   9 4JGL          BCT   6  STEPBNA  
   7 4K2=          LDN   0  0
  17 4KFW          CURTAIL      0               [SET APPEND=READ PTR 
   5 4K_G    STEPBNEX
   8 4L9# ...      ADX   7  FX1  
   7 4LF6          EXIT  7  0
   4 4LYQ    #   
   4 4MDB    #   
   4 4MY2    #   
   4 4NCL    #   
   6 4NX=    STEPBACKOLD 
   8 4P74 ...      SBX   7  FX1  
  10 4PBW          LDX   6  ISOCURRENT(3)
   7 4PWG          SBX   6  0
  17 4QB6          TRACE   6,ISOLDBAK           [<<<<<<<<<>>>>>>>>>> 
   9 4QTQ          BZE   6  STEPBOEX 
  10 4R*B          STO   0  ISOCURRENT(3)
  16 4RK8 ...      BNG   6  STEPBOB           [J IF MOVE FORWARDS
   5 4RT2    STEPBOA 
   8 4S#L          BACKSPACE    1
   9 4SS=          BCT   6  STEPBOA  
   5 4T?W    STEPBOEX
   8 4THN ...      ADX   7  FX1  
   7 4TRG          EXIT  7  0
   5 4TTD ...STEPBOB 
  15 4TXB ...      NGX   6  6               [MAKE +VE COUNT  
   5 4T_# ...STEPBOC 
   7 4W3= ...      STEP     1
  14 4W58 ...      BCT   6  STEPBOC         [TO LOOP ON  
   8 4W76 ...      ADX   7  FX1  
   7 4W94 ...      EXIT  7  0
   4 4W?6    #   
   4 4WQQ    #   
   4 4X=B    #   
   4 4XQ2    #   
  21 4Y9L    # ================================================================= #   
   4 4YP=    #   
   4 4_8W    #   
   4 4_NG    #   
   4 5286    #   
   4 52MQ    #   
  11 537B    #              SETLOP SUBROUTINE
   4 53M2    #   
  17 546L    #              MUST BE CALLED BEFORE OVERWRITING ISMFORM
  17 54L=    #              WHEN OUTPUTTING TEXT RDY OR PTR RDY BUT  
  13 555W    #              NOT FOR ERROR MESSAGES   
   4 55KG    #   
  10 5656    #              CALL ON X7   
   4 56JQ    #   
  18 574B    #              N.B  THE LAST OUTPUT IS THE ONE PRIOR TO THE 
  18 57J2    #              ---  ONE ON THE SCREEN NOW OR THE ONE JUST   
  13 583L    #                   ABOUT TO BE OUTPUT  
  15 58H=    #                CF LAST OUTPUT - CURRENT OUTPUT
   5 592W    SETLOP  
   9 59GG          LDN   5  ISBITLOP 
  18 5=26          ORS   5  ISBITS(3)           [SET LAST OUTPUT READY   
  16 5=FQ          LDCT  0  #002                [B7=TEXT READY   
  10 5=_B          ANDX  0  ISMFORM(3)   
  16 5?F2          BZE   0  SETLOPX               [J IF PTR RDY  
  19 5?YL          ERS   5  ISBITS(3)           [CLEAR LAST OUTPUT PTR READY 
   5 5#D=    SETLOPX 
   7 5#XW          EXIT  7  0
   4 5*CG    #   
   4 5*X6    #   
   4 5BBQ    #   
   4 5BWB    #   
  21 5CB2    # ================================================================= #   
   4 5CTL    #   
   4 5D*=    #   
   4 5DSW    #   
   4 5F#G    #   
  12 5FS6    #              SCOMMENT SUBROUTINE  
   4 5G?Q    #   
   4 5GRB    #   
  17 5H?2    #              OUTPUT FIXED MESSAGE ON NEXT FREE LINE   
   4 5HQL    #   
  10 5J==    #              CALL ON X7   
  11 5JPW    #              EXIT TO CALL+1   
   4 5K9G    #   
  11 5KP6    #              X6 PTR TO MESSAGE
   4 5L8Q    #   
   5 5LNB    SCOMMENT
   8 5LY8 ...      SBX   7  FX1  
  11 5M82          MHUNTW   3,AMXOR,ASCREEN  
   8 5MML          LDN  0  ISBITR
   9 5N7=          ORS   0  ISBITS(3)
  14 5NLW          ERS   0  ISBITS(3)           [CLEAR R 
   9 5P6G          LDN   0  ISBITLOP 
  16 5PL6          ANDX  0  ISBITS(3)           [LAT OP PTR RDY  
  14 5Q5Q          BNZ   0  SCOM1               [J IF SO 
   9 5QKB          LDX   0  ISMERR(3)
   8 5R52          SRL   0  18   
   7 5RJL          SBN   0  3
  18 5S4=          BNZ   0  SCOM1               [J IF ONE ON SCREEN NOW  
   8 5SHW          LDCT  0  #004 
  16 5T3G          ORS   0  ISMERR(3)           [SET CLEAR SCREEN
   5 5TH6    SCOM1   
  15 5W2Q          LDX   5  ISMFORM(3)          [SAVE THIS   
   9 5WGB          LDX   0  ISMERR(3)
  10 5X22          STO   0  ISMFORM(3)   
   7 5XFL          MONOUTX  6
  11 5X_=          MHUNTW   3,AMXOR,ASCREEN  
  10 5YDW          STO   5  ISMFORM(3)   
   8 5YYG          LDCT  0  #010 
  14 5_D6          ADS   0  ISMERR(3)          [LINES+1  
   8 5_XQ          LDCT  0  #004 
   9 62CB          ORS   0  ISMERR(3)
  17 62X2          ERS   0  ISMERR(3)           [CLEAR CLEAR SCREEN  
   8 636S ...      ADX   7  FX1  
   7 63BL          EXIT  7  0
   4 63W=    #   
   4 64*W    #   
   4 64TG    #   
   4 65*6    #   
  21 65SQ    # ================================================================= #   
   4 66#B    #   
   4 66S2    #   
   4 67?L    #   
   4 67R=    #   
  12 68=W    #              WRITETIDY SUBROUNTINE
   4 68QG    #   
  10 69=6    #              TIDY UP WHEN 
  11 69PQ    #              WRITING COMPLETED
   4 6=9B    #   
  17 6=P2    #              SET READ POINTERS EQUAL TO END OF FILE   
   4 6?8L    #   
  13 6?N=    #              UPDATE ISNCURRENT BY 1   
   4 6#7W    #   
   9 6#MG    #              LINK X7  
   6 6*76    WRITETIDY   
   8 6*BY ...      SBX   7  FX1  
   7 6*LQ          WIND     0
  11 6B6B          MHUNTW   3,AMXOR,ASCREEN  
   7 6BL2          LDN   0  1
  10 6C5L          ADS   0  ISNCURRENT(3)
   8 6C*D ...      ADX   7  FX1  
   7 6CK=          EXIT  7  0
   4 6D4W    #   
   4 6DJG    #   
   4 6F46    #   
   4 6FHQ    #   
   4 6G3B    #   
  21 6GH2    # ================================================================= #   
   4 6H2L    #   
   4 6HG=    #   
   4 6H_W    #   
   4 6JFG    #   
  12 6J_6    #              SDIRECTION SUBROUTINE
  17 6KDQ    #              %A OR %B OF T OR B BACKWARDS OR FORWARDS 
   4 6KYB    #   
  10 6LD2    #              CALL ON X7   
  10 6LXL    #                X6=0 NUMBER
  10 6MC=    #                X6=1 STRING
  14 6MWW    #                X2=PTR TO ANALYSED PARAM   
  14 6NBG    #                   (ISPARAMA OR ISPARAMB)  
  13 6NW6    #                X3=PTR TO AMXOR/ASCREEN
   4 6P*Q    #   
  12 6PTB    #              EXIT CALL+1 BACKWARDS
  12 6Q*2    #                   CALL+2 FORWARDS 
   4 6QSL    #   
   4 6R#=    #   
   6 6RRW    SDIRECTION  
  15 6S?G          BZE   6  SDIRA               [J IF NUMBER 
   8 6SR6          LDX   6  2(2) 
   9 6T=Q          BNG   6  SDIRX0   
   5 6TQB    SDIRX1  
  14 6W=2          EXIT  7  1                   [FORWARDS
   5 6WPL    SDIRX0  
  15 6X9=          EXIT  7  0                   [BACKWARDS   
   5 6XNW    SDIRA   
   8 6Y8G          LDX   6  0(2) 
  14 6YN6          BNG   6  SDIRX1              [J IF E  
   7 6_7Q          SLL   6  1
  14 6_MB          BNG   6  SDIRX0              [J IF B  
   8 7272          LDX   6  0(2) 
   8 72LL          SMO      FX1  
  13 736=          ANDX  6  SSBITS22         [LINE NO
  16 73KW          BXL   6  ISOCURRENT(3),SDIRX0    [J IF BACK   
  14 745G          EXIT  7  1                   [FORWARDS
   4 74K6    #   
   4 754Q    #   
   4 75JB    #   
   4 7642    #   
  21 76HL    # ================================================================= #   
   4 773=    #   
   4 77GW    #   
   4 782G    #   
   4 78G6    #   
  11 78_Q    #              REFIND SUBROUTINE
   4 79FB    #   
  11 79_2    #              USED BY ALTLEN   
   4 7=DL    #   
   5 7=Y=    REFIND  
  11 7?CW          MHUNTW   2,AMXOR,ASCREEN  
   7 7?XG          EXIT  1  0
   4 7#C6    #   
   4 7#WQ    #   
   4 7*BB    #   
   4 7*W2    #   
  21 7B*L    # ================================================================= #   
   4 7BT=    #   
   4 7C#W    #   
   4 7CSG    #   
   4 7D#6    #   
   4 7DRQ    #   
  11 7F?B    #              SABS SUBROUTINE  
   4 7FR2    #   
  16 7G=L    #              CONVERT RELATIVE ENDPOINT TO ABSOLUTE
   4 7GQ=    #   
   9 7H9W    #              LINK X7  
   4 7HPG    #   
   9 7J96    #              REQUIRES 
  13 7JNQ    #              X3=PTR TO ASCREEN BLOCK  
  12 7K8B    #              X2=PTR TO ENDPOINT   
   4 7KN2    #   
   4 7L7L    SABS
   8 7LM=          LDX   0  0(2) 
  14 7M6W          BNG   0  SABSX               [J IF E  
   8 7MLG          LDX   0  1(2) 
  18 7N66          BNG   0  SABSX               [J IF ALREADY ABSOLUTE   
   8 7NKQ          LDX   6  1(2) 
   8 7P5B          SMO      FX1  
  15 7PK2          ANDX  6  SSBITS22         [GET RELATIVE NO
   7 7Q4L          SLL   0  1
  15 7QJ=          BPZ   0  SABSF               [J IF FORWARD
  16 7R3W          NGX   6  6                   [SET BACKWARDS   
   5 7RHG    SABSF   
  16 7S36          ADX   6  ISOCURRENT(3)       [MAKE ABSOLUTE   
  16 7SGQ          BPZ   6  SABSB               [J IF NOT OFF TOP
  14 7T2B          LDCT  6  #200                [SET B   
   5 7TG2    SABSB   
   8 7T_L          STO   6  0(2) 
   5 7WF=    SABSX   
   7 7WYW          EXIT  7  0
   4 7XDG    #   
   4 7XY6    #   
   4 7YCQ    #   
   4 7YXB    #   
  20 7_C2    # K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 #  
   8 7_WL    #              SDATA
   4 82B=    #   
  15 82TW    #              COME HERE WITH BLOCKFULL OF TEXT 
  15 83*G    #              TO BE WRITTEN AWAY TO NEW FILE   
   4 83T6    #   
   8 84#Q    #              USES 
  18 84SB    #              AWORK1 RELATIVE PTR TO NEXT LINE WITHIN BLOCK
  14 85#2    #              AWORK3 LENGTH OF TEXT LEFT   
   4 85RL    #   
   4 86?=    #   
  16 86QW    #              CODE FROM 'SDATEL' TO END IS COMMON  
  14 87=G    #              FOR WRITING TO NEWFILE FROM  
  13 87Q6    #              FILE.FRB        AWORK4=0 
  13 889Q    #              AMXOR.ASCREEN   AWORK4#0 
   4 88PB    #   
   4 8992    #   
   5 89NL    SDATA   
  10 8=8=          MHUNTW   1,FILE,FRB   
  17 8=MW          TRACE   1,ISDATA          [<<<<<<<<<<>>>>>>>>>>   
  14 8?7G          LDN   0  A1+2                [DATA PTR
   9 8?M6          STO   0  AWORK1(2)
   9 8#6Q          STOZ     AWORK4(2)
  12 8#LB    #     CALCULATE TOTAL CHAR LENGTH   
   8 8*62          LDCT  0  #600 
   9 8*KL          ANDX  0  A1+1(1)  
   9 8B5=          BNZ   0  SDATAB   
   7 8BJW          ADN   0  1
   5 8C4G    SDATAB  
   8 8CJ6          ADX   0  A1(1)
   7 8D3Q          SLC   0  2
  16 8DHB          SBN   0  12                  [CH COUNT OF DATA
  16 8F32          STO   0  AWORK3(2)        [CHS LEFT IN BLOCK  
   9 8FGL    #     ANY MORE DATA LEFT
   5 8G2=    SDATEL  
   8 8GFW          LDX   2  FX2  
   9 8G_G          LDX   0  AWORK3(2)
  16 8HF6          BZE   0  SDATEND             [J IF ALL COPIED 
  10 8HYQ    #     USE 80 CH LINES TO END
  14 8JDB          LDN   4  80                  [SET 80  
  17 8JY2          BXGE  0  4,SDATX             [J IF MORE THAN 80   
  20 8KCL          LDX   4  0                   [ELSE SET REMAINDER (LAST LINE)  
   5 8KX=    SDATX   
  17 8LBW          SBS   4  AWORK3(2)        [DECREASE LENGTH LEFT   
   3 8LCC ...
  11 8LCY ...#     REMOVE ALL TRAILING SPACES
   9 8LDF ...      LDX   1  AWORK4(2)
  17 8LF2 ...      BNZ   1  SDLINA              [J IF USING ACREEN   
  10 8LFH ...      MHUNTW   1,FILE,FRB   
   9 8LG4 ...      BRN      SDLINB   
   5 8LGK ...SDLINA  
  11 8LH6 ...      MHUNTW   1,AMXOR,ASCREEN  
   5 8LHM ...SDLINB  
   9 8LJ8 ...      ADX   1  AWORK1(2)
   7 8LJP ...      SLC   1  2
   7 8LK= ...      SBN   1  1
  16 8LKR ...      SRC   1  2                   [CH BEFORE FIRST 
   7 8LL# ...      LDX   5  1
   7 8LLT ...      LDX   3  1
   5 8LMB ...SDLINY  
   7 8LMX ...      BCHX  3  /
   8 8LND ...      LDCH  0  0(3) 
   8 8LN_ ...      SBN   0  #20  
  15 8LPG ...      BZE   0  SDLINX              [J IF SPACE  
  17 8LQ3 ...      STO   3  5                   [LAST KNOWN NON SPACE
   5 8LQJ ...SDLINX  
   9 8LR5 ...      BCT   4  SDLINY   
  16 8LRL ...      SLC   1  2                   [PTR BEFORE LINE 
  18 8LS7 ...      SLC   5  2                   [PTR TO LAST NON SPACE   
  16 8LSN ...      SBX   5  1                   [NEW CHAR COUNT  
   7 8LT9 ...      LDX   4  5
  12 8LWG    #     CALCULATE LENGTH OF NEW RECORD
  14 8MB6          LDX   1  4                   [CH COUNT
   7 8MTQ          SRC   1  2
   8 8N*B          LDCT  5  #600 
   7 8NT2          ANDX  5  1
   8 8P#L          BZE   5  SDATE
   7 8PS=          ADN   1  1
   5 8Q?W    SDATE   
  16 8QRG          ADN   1  2                   [RECORD LENGTH   
   8 8R?6          ANDN  1  #7777
  13 8RQQ          STO   1  5                   [COPY
  11 8S=B          STEP     0,0(1),SBREAKIN  
  14 8SQ2          TESTREP  FILEFULL,SDFF,REFUSED,SDREF  
   8 8WNG          BRN      SDIT 
   7 8X86    #     FILE FULL 
   4 8XMQ    SDFF
   9 8Y7B          LDN   6  SYFYNF   
   9 8YM2          BRN      SDREF1   
   7 8_6L    #     BS LIMIT  
   5 8_L=    SDREF   
   9 925W          LDN   6  SYRYBSL  
   5 92KG    SDREF1  
   8 9356          ADX   6  FX1  
   7 93JQ          SMO      6
   7 944B          LDX   6  0
  15 94J2          CALL  7  SCOMMENT            [OUT PUT MESS
  16 953L          BRN      SEDITEND            [J TO TERMINATE  
   9 95H=    #     CONSTRUCT RECORD  
   4 962W    SDIT
   4 96GG    #   
   9 9726          LDX   1  AWORK4(2)
  17 97FQ          BNZ   1  SDIT2              [J IF USING ASCREEN   
  10 97_B          MHUNTW   1,FILE,FRB   
   8 98F2          BRN      SDIT3
   5 98YL    SDIT2   
  11 99D=          MHUNTW   1,AMXOR,ASCREEN  
   5 99XW    SDIT3   
   8 9=CG    #     RED TAPE WD 0 
   9 9=X6          STO   5  FRH(3)   
  10 9?BQ    #     SPACEFILL LAST WORD   
   8 9?LJ ...      BZE   4  SDIT4
   8 9?WB          SMO      FX1  
   9 9#B2          LDX   0  SSSPACES 
  16 9#TL          LDX   7  5                   [RECORD LENGTH   
   7 9**=          SBN   7  1
   7 9*SW          SMO      7
   8 9B#G          STO   0  0(3) 
  11 9BS6    #     UPDATE PTR TO NEXT TEXT   
  10 9C?Q    #     GET PTR TO THIS TEXT  
   5 9CHJ ...SDIT4   
  15 9CRB          LDX   7  AWORK1(2)        [REL PTR TO TEXT
   8 9D?2          LDN   0  20   
  13 9DQL          ADS   0  AWORK1(2)        [UPDATE 
   7 9F==          LDX   2  1
  16 9FPW          ADX   2  7                   [ABS PTR TO TEXT 
   8 9G9G    #     RED TAPE WD 1 
  15 9GP6          LDX   5  4                   [CH LENGTH   
   7 9H8Q          SRC   5  2
   8 9HNB          LDCT  6  #600 
   7 9J82          ANDX  6  5
  11 9J9Y ...      MHUNTW   1,AMXOR,ASCREEN  
   8 9J?W ...      SMO      FX1  
   9 9J*S ...      LDX   0  XISBITNG 
  18 9JCQ ...      ANDX  0  ISBITS(1)            [IS NON GRAPHIC FLAG SET
  15 9JFN ...      BZE   0  SDIT6                [JUMP IF NOT
  16 9JHL ...      ADN   6  #7400                [SET ALPHA SHIFT
   5 9JKJ ...SDIT6   
   8 9JML          ADN   6  #41  
   9 9K7=          STO   6  FRH+1(3) 
   7 9KLW    #     COPY TEXT 
   8 9KWN ...      BZE   4  SDIT5
   8 9L6G          ADN   3  FRH+2
   7 9LL6          SMO      4
   7 9M5Q          MVCH  2  0
   5 9M*J ...SDIT5   
   9 9MKB ...      CALL  7  WRITETIDY
  17 9N52          BRN      SDATEL              [DEAL WITH REMAINDER 
   9 9NJL    #     ALL WRITTEN AWAY  
   5 9P4=    SDATEND 
  11 9PHW          MHUNTW   3,AMXOR,ASCREEN  
  17 9Q3G    #     IF COPYING THEN J TO CONSTRUCT ANOTHER SCREENFULL 
   9 9QH6          LDN   0  ISBITC   
   9 9R2Q          ANDX  0  ISBITS(3)
  15 9RGB          BNZ   0  NEXTSCREEN          [J IF COPYING
  17 9S22    #     IF NOT COPYING AND NOT REPEATING OUTPUT PTR RDY   
   9 9SFL          LDN   0  ISBITR   
   9 9S_=          ANDX  0  ISBITS(3)
  17 9TDW          BZE   0  POINTER             [J IF NOT REPEATING  
  13 9TYG    #     IF REPEATING EXECUTE COMMAND AGAIN
   9 9WD6          LDN   0  ISBITT   
   9 9WXQ          ANDX  0  ISBITS(3)
  14 9XCB          BNZ   0  TREPEAT             [J IF T  
  14 9XX2          BRN      PREPEAT             [J IF P  
   4 9YBL    #   
   4 9YW=    #   
   4 9_*W    #   
   4 9_TG    #   
   4 =2*6    #   
  20 =2SQ    # K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 #  
   4 =3#B    #   
   4 =3S2    #   
   4 =4?L    #   
   4 =4R=    #   
   4 =5=W    #   
   4 =5QG    #   
   4 =6=6    #   
   4 =6PQ    #   
  14 =79B    #              DEAL WITH ACTION KEYS 6 AND 7
   4 =7P2    #   
   5 =88L    SCONTROL
  11 =8N=          MHUNTW   3,AMXOR,ASCREEN  
  10 =97W          MHUNTW   2,FILE,FRB   
  17 =9MG          TRACE  3,ISCONTRL          [<<<<<<<<<<>>>>>>>>>>  
   8 ==76    #     GET NO OF AK  
   8 ==LQ          LDX  0  A1(2) 
   8 =?6B          ANDN  0  #7777
   7 =?L2          SBN   0  1
   8 =#5L          ADN   2  A1+2 
   7 =#K=          SRC   0  2
   7 =*4W          ADX   2  0
   8 =*JG          LDCH  0  0(2) 
  16 =*S# ...      ANDN  0  #17                 [LOOSE ANY SHIFT 
  10 =B46          BXE   0  SS6(1),SAK6  
   8 =BHQ    #     MUST BE  AK 7 
   4 =C3B    SAK7
   9 =CH2          LDN   0  ISBITLOP 
  16 =D2L          ANDX  0  ISBITS(3)           [LAST OP PTR RDY?
  14 =DG=          BNZ   0  REPEAT             [J IF SO  
   4 =D_W    #   
   8 =FFG          LDCT  0  #002 
  16 =F_6          ANDX  0  ISMFORM(3)          [THIS OP PTR RDY 
  14 =GDQ          BNZ   0  SAK7Q               [J IF NOT
  12 =GYB    #     STEP BACK - THIS OP PTR RDY   
  10 =JWW          LDX   0  ISOPAGE(3)   
  16 =KBG          BNG  0  REPEAT               [J IF NOT ALLOWED
   9 =KGC ...      LDN  1  ISBITCE   
   9 =KL# ...      ORS   1  ISBITS(3)
  18 =KQ9 ...      ERS   1  ISBITS(3)           [CLEAR COMMAND EXPECTED  
  10 =KW6          CALL  7  STEPBACKOLD  
  11 =L*Q          MHUNTW   3,AMXOR,ASCREEN  
  10 =LTB          LDX   0  ISNPAGE(3)   
  10 =M*2          CALL  7  STEPBACKNEW  
   9 =MSL          BRN      SMATCH   
   9 =N#=    #     THIS OP TEXT RDY  
   5 =NRW    SAK7Q   
  10 =P?G          LDX   0  ISOPAGE(3)   
  16 =PR6          BNG   0  REPEAT             [J IF NOT ALLOWED 
  11 =Q=Q          LDX   0  ISOPREVPAGE(3)   
  16 =QQB          BNG   0  REPEAT             [J IF NOT ALLOWED 
  11 =R=2    #     STEP BACK TO PREVIOUS PAGE
  10 =RPL          CALL  7  STEPBACKOLD  
  11 =S9=          MHUNTW   3,AMXOR,ASCREEN  
  11 =SNW          LDX   0  ISNPREVPAGE(3)   
  10 =T8G          CALL  7  STEPBACKNEW  
   8 =TN6          LDCT  0  #400 
  16 =W7Q          STO   0  ISOPREVPAGE(3)      [SET NOT ALLOWED 
  13 =WMB          STO  0  ISOPAGE(3)           [SAME
  13 =X72          BRN      SMATCH          [CARRY ON
   8 =XLL    #     ACTION KEY 6  
   4 =Y6=    SAK6
   8 =YKW          LDCT  0  #002 
  16 =_5G          ANDX  0  ISMFORM(3)          [THIS OP PTR RDY 
  14 =_K6          BZE   0  REPEAT             [J IF SO  
  12 ?24Q    #     NOW WRITE AWAY DATA IN BLOCK  
  11 ?2JB          MHUNTW   1,AMXOR,ASCREEN  
  19 ?2WR ...      LDCT  0  #200                [CHAR 1,FOR PRECEEDING PACE  
  17 ?398 ...      ORN   0  ISTEXT              [PTR TO DATA IN BLOCK
   8 ?3HL          LDX  2  FX2   
   9 ?43=          STO   0  AWORK1(2)
  17 ?4GW ...      STO   0  AWORK4(2)        [SET NON ZERO FOR C+6   
  10 ?52G    #     SET TOTAL CHAR LENGTH 
  10 ?5G6          LDX   0  ISTEXTCT(1)  
  17 ?5PY ...      SBN   0  1                   [FOR PRECEEDONG SPACE
  17 ?5_Q          STO   0  AWORK3(2)        [=CHARS LEFT IN BLOCK   
   9 ?6FB          BRN      SDATEL   
   4 ?6_2    #   
   4 ?7DL    #   
   4 ?7Y=    #   
   4 ?8CW    #   
  20 ?8XG    # K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 #  
   4 ?9C6    #   
  20 ?9WQ    # K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 #  
   4 ?=BB    #   
   4 ?=W2    #   
   4 ??*L    #   
   4 ??T=    #   
  10 ?##W    #              TTT     PPP  
  10 ?#SG    #               T      P P  
  10 ?*#6    #               T      PPP  
   9 ?*RQ    #               T      P
   9 ?B?B    #               T      P
   4 ?BR2    #   
   6 ?C=L    SCOMMANDP   
   6 ?CQ=    SCOMMANDT   
  11 ?D9W          MHUNTW   3,AMXOR,ASCREEN  
  11 ?DPG    #              REPEATS COME HERE
  14 ?F96    #              ENSURE X3 CORRECT BEFORE JUMP
   5 ?FNQ    TREPEAT 
   5 ?G8B    PREPEAT 
  17 ?GN2          TRACE  3,ISPPPTTT          [<<<<<<<<<<>>>>>>>>>>  
   8 ?H7L         LDN   6  ISBITA
  10 ?HM=          ANDX   6  ISBITS(3)   
  14 ?J6W          BNZ   6  REP1               [J IF STR 
  10 ?JLG          LDN   2  ISPARAMA(3)  
  19 ?K66          CALL  7  SABS               [ENSURE ENDPOINT1 IS ABSOLUTE 
   4 ?KKQ    REP1
  17 ?L5B          LDN   6  ISBITA              [WHAT IS ENDPOINT1   
   9 ?LK2          ANDX  6  ISBITS(3)
  10 ?M4L          LDN   2  ISPARAMA(3)  
  10 ?MJ=          CALL  7  SDIRECTION   
  10 ?N3W          BRN      SBACKWARDS   
   9 ?NHG          BRN      SFORWARDS
   4 ?P36    #   
   4 ?PGQ    #   
   4 ?Q2B    #   
   4 ?QG2    #   
  21 ?Q_L    # ================================================================= #   
   4 ?RF=    #   
   4 ?RYW    #   
   4 ?SDG    #   
   4 ?SY6    #   
   4 ?TCQ    #   
   4 ?TXB    #   
  14 ?WC2    #              MOVE BACKWARDS TO ENDPOINT1  
   6 ?WWL    SBACKWARDS  
  16 ?XB=          TRACE  3,ISBACK          [<<<<<<<<<<>>>>>>>>>>
   9 ?XTW          LDN   0  ISBITT   
   9 ?Y*G          ANDX  0  ISBITS(3)
  18 ?YT6          BNZ   0  SSBTNA              [ERROR IF T & BACKWARDS  
  10 ?_#Q          LDX   0  ISOCURRENT(3)
  16 ?_SB          BZE   0  SSYROTBOTF          [OFF BEGINNING   
   5 #2#2    SBACK3  
  15 #2RL          BACKSPACE   1                [BACK 1 LINE 
  16 #3?=          STEPAGAIN   1                [AND LOOK AT IT  
  11 #3QW          MHUNTW   2,AMXOR,ASCREEN  
   7 #4=G          LDN   0  1
  15 #4Q6          SBS   0  ISOCURRENT(2)       [AND COUNT IT
   7 #59Q          STO  2  7 
  16 #5PB          TESTBRKIN  SBREAKIN            [J IF BRK IN   
   7 #692          LDX   2  7
   4 #6NL    #   
  11 #78=    #              IS THIS THE LINE?
   9 #7MW          LDN   0  ISBITA   
   9 #87G          ANDX  0  ISBITS(2)
  14 #8M6          BNZ   0  SBACK              [J IF STR 
   4 #96Q    #   
  12 #9LB    #              LOOKING FOR NUMBER   
  10 #=62          LDX   0  ISPARAMA(2)  
   7 #=KL          SLL   0  1
  17 #?5=          BNG   0  SBACK2              [J IF MUST RUN OF TOP
  10 #?JW          LDX   0  ISPARAMA(2)  
  17 ##4G          BXE   0  ISOCURRENT(2),SMATCH     [J IF THIS IS IT
  16 ##J6          BRN      SBACK2              [LOOP UNTIL FOUND
   4 #*3Q    #   
  12 #*HB    #              LOOKING FOR STRING   
   5 #B32    SBACK   
  16 #BGL          LDX   0  2                   [SAVE ASCREEN PTR
  15 #C2=          LDN   1  ISPARAMA+2(2)      [STRING PTR   
  15 #CFW          LDX   2  3                   [PTR TO LINE 
  18 #C_G          CALL  7  MATCH               [IS THERE A STRING MATCH?
  13 #DF6          BRN      SMATCH          [... YES 
  17 #DYQ          LDX   2  0                   [RESTORE ASCREEN PTR 
   4 #FDB    #   
  12 #FY2    #              THIS IS NOT THE LINE 
   5 #GCL    SBACK2  
  15 #GX=          LDX   2  ISOCURRENT(2)       [IF LINE ZERO
  15 #HBW          BZE   2  SSYROTBOTF          [THEN ERROR  
  16 #HWG          BRN      SBACK3              [ELSE TRY NEXT   
   4 #JB6    #   
   4 #JTQ    #   
   4 #K*B    #   
   4 #KT2    #   
  21 #L#L    # ================================================================= #   
   4 #LS=    #   
   4 #M?W    #   
   4 #MRG    #   
   4 #N?6    #   
   4 #NQQ    #   
  14 #P=B    #              MOVE FORWARDS TO ENDPOINT1   
  11 #PQ2    #              COPY OVER IF T   
  11 #Q9L    #              THROW AWAY IF P  
   6 #QP=    SFORWARDS   
  17 #R8W          TRACE  3,ISFORW            [<<<<<<<<<<>>>>>>>>>>  
   5 #RNG    SFOR7   
  15 #S86          STEPAGAIN    1               [LOOK AT LINE
   5 #SMQ    SFOR71  
  11 #T7B          MHUNTW   2,AMXOR,ASCREEN  
   5 #TM2    SFORA   
   4 #W6L    #   
  11 #WL=    #              IS THIS THE LINE?
   9 #X5W          LDN   0  ISBITA   
   9 #XKG          ANDX  0  ISBITS(2)
  14 #Y56          BNZ   0  SFOR1              [J IF STR 
   4 #YJQ    #   
  12 #_4B    #              LOOKING FOR NUMBER   
  10 #_J2          LDX   0  ISPARAMA(2)  
  14 *23L          BNG   0  SFOR2               [J IF E  
  17 *2H=          BXE   0  ISOCURRENT(2),SMATCH     [J IF THIS IS IT
   8 *32W          BRN      SFOR2
   4 *3GG    #   
  12 *426    #              LOOKING FOR STRING   
   5 *4FQ    SFOR1   
  15 *4_B          BZE   3  SFOROFF             [J IF OFF END
  16 *5F2          LDX   0  2                   [SAVE ASCREEN PTR
  15 *5YL          LDN   1  ISPARAMA+2(2)      [STRING PTR   
  14 *6D=          LDX   2  3                   [LINE PTR
  17 *6XW          CALL  7  MATCH               [ARE THEY THE SAME?  
  13 *7CG          BRN      SMATCH             [YES  
  16 *7X6          LDX   2  0                   [PTR TO ASCREEN  
   8 *8BQ         BRN      SFOR2X
   5 *8WB    SFOR2   
   4 *9B2    #   
  14 *9TL    #              WHAT TO DO WITH THIS LINE?   
  15 *=*=          BZE   3  SFOROFF             [J IF OFF END
   5 *=SW    SFOR2X  
   9 *?#G          LDN   0  ISBITT   
   9 *?S6          ANDX  0  ISBITS(2)
  16 *#?Q          BZE   0  SFOR3               [J IF P TO IGNORE
   4 *#RB    #   
  12 **?2    #              CURRENT COMMAND IS T 
  13 **QL    #              SO WRITE THIS LINE AWAY  
   5 *B==    SFORCOOR
  15 *BPW          STEPAGAIN    1               [PTR TO LINE 
  11 *C9G          TESTREP  COORED,SFORCOOR  
  13 *CP6          STO   3  4                   [SAVE
  15 *D8Q          LDX   2  FRH(3)             [REC LENGTH   
  14 *DNB          STEP     0,0(2),SBREAKIN     [APPEND  
  17 *F82          STO   3  5                   [SET READY FOR MOVE  
  18 *FML          TESTREP  FILEFULL,SDFF,REFUSED,SDREF,COORED,SFORCOOR  
   7 *G7=          SMO      4
  14 *GLW          LDX   2  FRH                [LENGTH   
  15 *H6G          MOVE  4  0(2)                [COPY OVER   
   9 *HL6          CALL  7  WRITETIDY
   4 *J5Q    #   
  13 *JKB    #              COMMON T & P PATH AGAIN  
  13 *K52    #              READ NEXT LINE - IF ANY  
   5 *KJL    SFOR3   
  15 *L4=          STEP     1                   [READ AGAIN  
  11 *LHW          MHUNTW   2,AMXOR,ASCREEN  
   7 *M3G          LDN   0  1
  17 *MH6          ADS   0  ISOCURRENT(2)      [OLD FILE LINE NO + 1 
  14 *N2Q          BZE   3  SFOROFF             [J IF EOF
   7 *NGB          STO   2  7
  16 *P22          TESTBRKIN  SBREAKIN            [J IF BRK IN   
   7 *PFL          LDX   2  7
   8 *P_=          BRN      SFORA
   4 *QDW    #   
  13 *QYG    #              RUN OFF END OF OLD FILE  
   5 *RD6    SFOROFF 
  11 *RXQ          MHUNTW   3,AMXOR,ASCREEN  
   9 *SCB          LDN   0  ISBITE   
  10 *SX2          ANDX   0  ISBITS(3)   
  16 *TBL          BNZ   0  SEDITEND           [J TO WIND UP IF E
   9 *TW=          LDN   0  ISBITA   
   9 *W*W          ANDX  0  ISBITS(3)
  15 *WTG          BNZ   0  SSYROTEOTF         [ERR IF STR   
  10 *X*6          LDX   0  ISPARAMA(3)  
  15 *XSQ          BPZ   0  SSYROTEOTF         [ERR IF NOT E 
   4 *Y#B    #   
   4 *YS2    #   
   5 *_?L    SMATCH  
  11 *_R=          MHUNTW   3,AMXOR,ASCREEN  
   9 B2=W          LDN   6  ISBITB   
   9 B2QG          ANDX  6  ISBITS(3)
  15 B3=6          BNZ   6  SMATCHX            [ MJ IF STR   
  10 B3PQ          LDN   2  ISPARAMB(3)  
  19 B49B          CALL  7  SABS               [ENSURE ENDPOINT 2 IS ABSOLUTE
   5 B4P2    SMATCHX 
   4 B58L    #   
   4 B5N=    #   
   4 B67W    #   
   4 B6MG    #   
  21 B776    # ================================================================= #   
   4 B7LQ    #   
   4 B86B    #   
   4 B8L2    #   
   4 B95L    #   
   4 B9K=    #   
  13 B=4W    #              CONSTRUCT NEXT SCREENFULL
   4 B=JG    #   
   4 B?46    #   
   4 B?HQ    #   
   6 B#3B    NEXTSCREEN  
  11 B#H2          MHUNTW   1,AMXOR,ASCREEN  
  17 B*2L          TRACE  1,ISNEXTSC          [<<<<<<<<<<>>>>>>>>>>  
   9 B*G=          LDN   0  ISBITC   
  16 B*_W          ORS   0  ISBITS(1)             [SET COPYING   
   7 BBFG          LDX   3  1
  20 BB_6          CALL  7  SETLOP              [SET OR CLEAR LAST OUTPUT READY  
  14 BCDQ          LDCT  0  #022                [LINE 2  
  14 BCYB          ADN   0  79                  [COL 79  
  10 BDD2          STO   0  ISMFORM(1)   
   8 BDXL          LDX  2  FX2   
  16 BFC=          STOZ     AWORK2(2)        [COUNT OF LINES=0   
   8 BFWW          LDN   0  #20  
  16 BGBG          LDN   3  ISTEXT(1)           [DUMP LINES HERE 
  16 BGW6          DCH   0  0(3)                [AFTER 1 SPACE   
   7 BH*Q          BCHX  3  /
  14 BHTB          SBX   3  1                   [MAKE REL
  16 BJ*2          STO   3  ISTEXTCT(1)         [SAVE PTR HERE   
   9 BJSL          LDN   6  ISBITB   
   9 BK#=          ANDX  6  ISBITS(1)
  17 BKRW          BNZ   6  NEX9               [J IF ENDPOINT2 TEXT  
  10 BL?G          LDX   0  ISPARAMB+1(1)
  14 BLR6          BZE   0  NEXTX              [ J IF ,0 
   4 BM=Q    NEX9
  15 BMQB          LDX   3  1                   [ASCREEN PTR 
  16 BN=2          LDN   2  ISPARAMB(3)         [PTR TO ENDPOINT2
  10 BNPL          CALL  7  SDIRECTION   
  16 BP9=          BRN      NEXTBAK          [CANT GO BACKWARDS  
   4 BPNW    NEX1
  11 BQ8G          MHUNTW   1,AMXOR,ASCREEN  
  13 BQN6 ...      ALTLENGD 1,ASCREENLEN+401,REFIND  
   8 BR7Q          STEPAGAIN    1
  14 BRMB          BZE   3  NEXTEOF         [J IF OFF END
   4 BS72    #   
   4 BSLL    #   
  10 BT6=    #              NOW MAIN LOOP
   5 BTKW    NEXTLOOP
  11 BW5G          MHUNTW   1,AMXOR,ASCREEN  
   9 BWK6          LDN   0  ISBITB   
   9 BX4Q          ANDX  0  ISBITS(1)
  14 BXJB          BNZ   0  NEXT2              [J IF STR 
  16 BY42          LDX   0  AWORK2(2)          [LINES THIS TIME  
  16 BYHL          ADX   0  ISOCURRENT(1)       [+LINES LAST TIME
  15 B_3=          BXE   0  ISPARAMB(1),NEXTX   [J IF MATCH  
  18 B_GW          BRN      NEXT3               [J IF E OR NOT THIS LINE 
   5 C22G    NEXT2   
  14 C2G6          STO   1  AWORK3(2)        [ASCREEN PTR
  14 C2_Q          STO   3  AWORK4(2)        [LINE PTR   
  16 C3FB          LDN   1  ISPARAMB+2(1)      [PTR TO END PT 2  
  14 C3_2          LDX   2  3                   [LINE PTR
   8 C4DL          CALL  7  MATCH
  15 C4Y=          BRN      NEXTXY              [J IF MATCH  
   8 C5CW          LDX   2  FX2  
  14 C5XG          LDX   1  AWORK3(2)        [ASCREEN PTR
  14 C6C6          LDX   3  AWORK4(2)        [LINE PTR   
   5 C6WQ    NEXT3   
   8 C7BB    #     SCREEN FULL?  
   4 C7MQ ...#   
   9 C7_6 ...      LDX   7  AWORK2(2)
   8 C8=G ...      SBN   7  20   
  15 C8HW ...      BPZ   7  NEXTQ               [J IF FULL   
  13 C8T=    #     NOT THIS LINE - SO MUST DISPLAY IT
  11 C9#W    #     BUT ONLY 80 CHS PER LINE  
   8 C9SG          LDCT  6  #600 
   9 C=#6          ANDX  6  FRH+1(3) 
   9 C=RQ          BNZ   6  NEXTEXT  
   7 C??B          ADN   6  1
   5 C?R2    NEXTEXT 
   9 C#=L          ADX   6  FRH(3)   
   7 C#Q=          SLC   6  2
   8 C*9W          SBN   6  12   
   8 C*PG          LDN   7  80   
  16 CB96          BXL   6  7,NEXTA             [J IF LENGTH OK  
  15 CBNQ          LDN   6  80                  [RESET TO MAX
   5 CC8B    NEXTA   
  15 CCN2          SBX   7  6                   [REMAINDER   
   9 CD7L    #     MOVE CONSTANT PART
  16 CDM=          LDN   3  FRH+2(3)                [PTR TO TEXT 
  19 CF6W          LDX   4  ISTEXTCT(1)         [PTR TO NEXT FREE LOCATION   
  16 CFLG          ADX   4  1                   [MAKE ABSOLUTE   
   9 CG66          BZE   6  NEXTA1   
   7 CGKQ          SMO      6
  16 CH5B          MVCH  3  0                   [COPY TO BLOCK   
   5 CHK2    NEXTA1  
  10 CJ4L    #     SPACE FILL TO 80 CHARS
  17 CJJ=          LDX   3  4                [PTR TO NEXT FREE LOCN  
  15 CK3W          BZE   7  NEXTA2              [J IF FULL   
   8 CKHG          LDN   0  #20  
   5 CL36    NEXTA3  
   8 CLGQ          DCH   0  0(3) 
   7 CM2B          BCHX  3  /
   9 CMG2          BCT   7  NEXTA3   
   5 CM_L    NEXTA2  
  14 CNF=          SBX   3  1                   [MAKE REL
  10 CNYW          STO   3  ISTEXTCT(1)  
   8 CPDG    #     ROOM FOR MORE 
   8 CPY6          LDX   2  FX2  
  15 CQCQ          LDX   6  AWORK2(2)        [COUNT OF LINES 
   7 CQXB          ADN   6  1
  13 CRC2          STO   6  AWORK2(2)        [RESET  
  15 CRWL          STEP     1                   [READ NEXT   
  15 CSB=          BZE   3  NEXTEOF             [J IF OFF END
   9 CSTW          BRN      NEXTLOOP 
   5 CT*G    NEXTEOF 
  10 CTT6    #     RUN OFF END OF FILE   
  16 CW#Q    #     REMEMBER TO OUTPUT ASCREEN BLOCK AFTER MESSAGE
  11 CWSB          MHUNTW   1,AMXOR,ASCREEN  
   9 CX#2          LDN   0  ISBITB   
   9 CXRL          ANDX  0  ISBITS(1)
  16 CY?=          BNZ   0  NEXTEOFA           [J TO ERR IF STR  
  10 CYQW          LDX   0  ISPARAMB(1)  
  14 C_=G          BNG   0  NEXTX               [OK IF TE
   3 C_*L ...
  18 C_DQ ...      LDX   0  AWORK2(2)           [NO OF LINES FOR SCREEN  
  16 C_HW ...      ADX   0  ISOCURRENT(1)       [+ START LINE NO 
  16 C_M2 ...      BXE   0  ISPARAMB(1),NEXTX  [JIF RIGHT LINE   
   5 C_Q6    NEXTEOFA
   3 C_T= ...
  18 C_YB ...      LDX   0  AWORK2(2)           [NO OF LINES FOR SCREEN  
   8 D23G ...      SBN   0  20   
  18 D26L ...      BZE   0  NEXTQ               [J IF 20 LINES TO DISPLAY
   8 D29Q          SMO      FX1  
  14 D2PB          LDX   6  SYROTEOTF          [OFF END  
   9 D392          CALL  7  SCOMMENT 
  16 D3NL          LDX   1  3                   [PTR TO ASCREEN  
   8 D48=          LDCT  0  #010 
  15 D4MW          ADS   0  ISMFORM(1)         [SET LINES+1  
   8 D57G          BRN      NEXTX
   4 D5M6    #   
  12 D66Q    #     TIDY UP BEFORE OUTPUTING BLOCK
   5 D6LB    NEXTXY  
  11 D762          MHUNTW   1,AMXOR,ASCREEN  
   5 D7KL    NEXTX   
   8 D85=    #     CLEAR COPYING 
   9 D8JW          LDN   0  ISBITC   
   9 D94G          ORS   0  ISBITS(1)
   9 D9J6          ERS   0  ISBITS(1)
   8 D=3Q          BRN      NEXTZ
   5 D=HB    NEXTQ   
  11 D?32          MHUNTW   1,AMXOR,ASCREEN  
   5 D?GL    NEXTZ   
  12 D*F6    #     UPDATE ALL FILE LINE NUMBERS  
  10 D*YQ          LDX   0  ISOPAGE(1)   
  11 DBDB          STO   0  ISOPREVPAGE(1)   
   4 DBY2    #   
  10 DCCL          LDX   0  ISNPAGE(1)   
  11 DCX=          STO   0  ISNPREVPAGE(1)   
   4 DDBW    #   
  10 DDWG          LDX   0  ISOCURRENT(1)
  10 DFB6          STO   0  ISOPAGE(1)   
   4 DFTQ    #   
  10 DG*B          LDX   0  ISNCURRENT(1)
  10 DGT2          STO   0  ISNPAGE(1)   
   4 DH#L    #   
   8 DHS=          SMO      FX2  
   9 DJ?W          LDX   0  AWORK2   
  18 DJRG          ADS   0  ISOCURRENT(1)       [+LINES OUTPUT THIS TIME 
  10 DJWL ...#     DONT OUTPUT IF B-IN   
   7 DJ_Q ...      STO   1  7
   9 DK4W ...      TESTBRKIN SBREAKIN
   7 DK82 ...      LDX   1  7
  16 DK?6    #     NOW CALCULATE LENGTH OF TEXT AND SHRINK BLOCK 
  10 DKQQ          LDX   5  ISTEXTCT(1)  
   9 DL=B          LDN   6  ISTEXT   
   7 DLQ2          SLC   5  2
   7 DM9L          SLC   6  2
  15 DMP=          SBX   5  6                   [CHAR LENGTH 
  10 DN8W          STO   5  ISTEXTCT(1)  
  15 DN=K ...      LDX   4  5                  [CHAR LENGTH  
   7 DN## ...      SBN   4  1
  15 DNB3 ...      BNZ   4  NEXTNZ             [J IF NOT NULL
   9 DNCQ ...      LDX   4  ISMERR(1)
   8 DNFF ...      SRL   4  18   
   7 DNH8 ...      SBN   4  3
  18 DNJX ...      BNZ   4  POINTER             [J IF ERR MESS ON SCREEN 
   5 DNLL ...NEXTNZ  
  11 DNNG          ADN   5  4*ASCREENLEN+3   
  17 DP86          SRL   5  2                   [WD LENGTH OF BLOCK  
  10 DPMQ          ALTLENG  1,5,REFIND   
   9 DQ7B          BRN      OUTPUT   
   4 DQM2    #   
  14 DR6L    #              CANT GO BACKWARDS - TERMINATE
   5 DRL=    NEXTBAK 
   8 DS5W          SMO      FX1  
   8 DSKG          LDX   6  SIT  
  16 DT56          CALL  7  SCOMMENT           [PART 1 OF MESS   
  15 DTJQ          BRN      SSE2MBAFR          [ AND PART 2  
   4 DYGG    #   
   4 D_26    #   
   4 D_FQ    #   
   4 D__B    #   
  20 F2F2    # K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 #  
   4 F2YL    #   
   8 F3D=    #              EEE  
   7 F3XW    #              E
   8 F4CG    #              EEE  
   7 F4X6    #              E
   8 F5BQ    #              EEE  
   4 F5WB    #   
   4 F6B2    #   
  13 F6TL    #              TWO PARTS TO E COMMAND   
   4 F7*=    #   
   4 F7SW    #   
  18 F8#G    #              PART 1 : COPY REST OF OLD FILE TO NEW FILE   
  12 F8S6    #                       SET  TE,0   
   6 F9?Q    SCOMMANDE   
  11 F9RB          MHUNTW   3,AMXOR,ASCREEN  
  17 F=?2          TRACE  3,ISEEEEEE          [<<<<<<<<<<>>>>>>>>>>  
  10 F=QL          LDN   0  ISBITT+ISBITE
  14 F?==          ORS   0  ISBITS(3)           [SET T+E 
  10 F?PW          LDN   0  ISBITA+ISBITB
   9 F#9G          ORS   0  ISBITS(3)
  19 F#P6          ERS   0  ISBITS(3)           [BOTH ENDPOINTS ARE NUMBERS  
   8 F*8Q          LDCT  0  #400 
  14 F*NB          STO   0  ISPARAMA(3)         [SET E   
  16 FB82          STO   0  ISPARAMA+1(3)       [..AND ASOLUTE   
  16 FBML          STOZ     ISPARAMB(3)         [SET RELATIVE 0  
  10 FC7=          STOZ     ISPARAMB+1(3)
   9 FCLW          BRN      SCOMMANDT
   4 FD6G    #   
   4 FDL6    #   
   4 FF5Q    #   
  15 FFKB    #              PART 2 : ALL COPYING DONE-TIDY UP
   5 FG52    SEDITEND
  17 FGJL          TRACE  0,ISENDEND          [<<<<<<<<<<>>>>>>>>>>  
   6 FH4= ...      CLOSE 
  15 FHHW          LDX   6  SEOE(1)             [END OF EDIT 
  13 FJ3G    #              E AND Q JOIN FORCES HERE 
   5 FJH6    SEANDQ  
  15 FK2Q          CALL  7  SCOMMENT            [OUT MESSAGE 
  18 FKGB ...      ACROSS   SCEDIT,2       [WRAP UP CODE NOW IN SCEDIT   
   4 FPCB    #   
   4 FPX2    #   
   4 FQBL    #   
  20 FQW=    # K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 K6 #  
   4 FR*W    #   
   4 FRTG    #   
   8 FS*6    #              FFF  
   7 FSSQ    #              F
   8 FT#B    #              FFF  
   7 FTS2    #              F
   7 FW?L    #              F
   4 FWR=    #   
   6 FX=W    SCOMMANDF   
  11 FXQG          MHUNTW   3,AMXOR,ASCREEN  
  17 FY=6          TRACE  3,ISFFFFFF          [<<<<<<<<<<>>>>>>>>>>  
   9 FYPQ          LDN   0  ISBITF   
  13 F_9B          ORS   0  ISBITS(3)          [SET F
   9 F_P2          LDN   0  ISBITLC  
   9 G28L          ANDX  0  ISBITS(3)
  16 G2N=          BZE   0  SSFMNBUITC          [J IF F NOT VALID
  10 G37W          LDX   0  ISOPREVCOM(3)
  10 G3MG          CALL  7  STEPBACKOLD  
  11 G476          MHUNTW   3,AMXOR,ASCREEN  
  10 G4LQ          LDX   0  ISNPREVCOM(3)
  10 G56B          CALL  7  STEPBACKNEW  
   9 G5L2          BRN      POINTER  
   4 G65L    #   
   4 G6K=    #   
   4 G74W    #   
   4 G7JG    #   
  20 G846    # K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 K7 #  
   4 G8HQ    #   
   4 G93B    #   
   4 G9H2    #   
   4 G=2L    #   
   8 G=G=    #               QQ  
   8 G=_W    #              Q  Q 
   8 G?FG    #              Q  Q 
   8 G?_6    #              Q  Q 
   8 G#DQ    #               QQQQ
   4 G#YB    #   
   6 G*D2    SCOMMANDQ   
  17 G*XL          TRACE  0,ISQQQQQQ          [<<<<<<<<<<>>>>>>>>>>  
  16 GBC= ...      CLOSEABANDON                 [FORGET NEW FILE 
  16 GBWW          LDX   6  SEA(1)              [EDIT ABANDONED  
   9 GCBG          BRN      SEANDQ   
   4 GCW6    #   
   4 GD*Q    #   
  21 GDTB    # ================================================================= #   
   4 GF*2    #   
   4 GFSL    #   
   4 GG#=    #   
   4 GGRW    #   
   4 GH?G    #   
  15 GHR6    #              OUTPUT MESSAGE THEN POINTER READY
  17 GJ=Q    SSYROTBOTF     [YOUVE RUN OFF THE BEGINNING OF THE FILE 
   9 GJQB          LDN   3  SYROTBOTF
   9 GK=2          BRN      SSOUTCOM 
  17 GKPL    SSYRYBSL       [YOU'VE REACHED YOUR BACKING STORE LIMIT 
   9 GL9=          LDN   3  SYRYBSL  
   9 GLNW          BRN      SSOUTCOM 
  17 GM8G    SSE2MBAFR      [ENDPOINT2 MUST BE A FORWARD REFERENCE   
   9 GMN6          LDN   3  SE2MBAFR 
   9 GN7Q          BRN      SSOUTCOM 
  17 GNMB    SSROTKINA      [REPETITION OF THIS KIND IS NOT ALLOWED  
   9 GP72          LDN   3  SROTKINA 
   9 GPLL          BRN      SSOUTCOM 
  16 GQ6=    SSBTNA        [BACKWARDS TRANSCRIPTION NOT ALLOWED  
   8 GQKW          LDN   3  SBTNA
   9 GR5G          BRN      SSOUTCOM 
  16 GRK6    SSYROTEOTF     [YOU'VE RUN OFF THE END OF THE FILE  
   9 GS4Q          LDN   3  SYROTEOTF
   9 GSJB          BRN      SSOUTCOM 
  16 GT42    SSFMNBUITC     [IF MAY NOT BE USED IN THIS CONTEXT  
   9 GTHL          LDN   3  SFMNBUITC
   9 GW3=          BRN      SSOUTCOM 
  14 GWGW    SSYFYNF        [YOU'VE FILLED YOUR NEW FILE 
   9 GX2G          LDN   3  SYFYNF   
   9 GXG6          BRN      SSOUTCOM 
  13 GX_Q    SSIT           [INSTRUCTION TERMINATED  
   8 GYFB          LDN   3  SIT  
   9 GY_2          BRN      SSOUTCOM 
  11 G_DL    SSNTL          [NUMBER TOO LARGE
   8 G_Y=          LDN   3  SNTL 
   9 H2CW          BRN      SSOUTCOM 
  11 H2XG    SSEA           [EDIT ABANDONED  
   8 H3C6          LDN   3  SEA  
   9 H3WQ          BRN      SSOUTCOM 
  10 H4BB    SSEOE          [END OE EDIT 
   8 H4W2          LDN   3  SEOE 
   5 H5*L    SSOUTCOM
   8 H5T=         ADX   3  FX1   
  16 H6#W          LDX   6  0(3)                [PTR TO MESSAGE  
   9 H6SG          CALL  7  SCOMMENT 
   9 H7#6          BRN      POINTER  
   4 H7RQ    #   
   4 H8?B    #   
   4 H8R2    #   
   4 H9=L    #   
  21 H9Q=    # ================================================================= #   
   4 H=9W    #   
   4 H=PG    #   
   4 H?96    #   
   4 H?NQ    #   
  13 H#8B    #               SWITCH TO OTHER CHAPTERS
   5 H#N2    POINTER 
   9 H*7L          ACROSS  SCEDITOR,1
   5 H*M=    OUTPUT  
   9 HB6W          ACROSS  SCEDITOR,2
   5 HBLG    REPEAT  
   9 HC66          ACROSS  SCEDITOR,3
   5 HCKQ    SBREAKIN
  10 HD5B          ACROSS   SCEDITOR,4   
   4 HDK2    #   
   4 HF4L    #   
   4 HFJ=    #   
   4 HG3W    #   
  21 HGHG    # ================================================================= #   
   4 HH36    #   
   4 HHGQ    #   
   4 HJ2B    #   
   4 HJG2    #   
   4 HJ_L    #   
   4 HKF=    #   
   4 HKYW    #   
   4 HLDG    #   
   4 HLY6    #   
   4 HMCQ    #END
   6 ____ ...352240340010
