  17 22FL ...      SEG      DOMONOP,,CENT(INSTALLATION HOOKS),,G400  
   4 22_=    [   
  15 23DW    [ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23YG    [   
   4 24D6    #   
  16 24XQ    #               THIS CHAPTER IS FOR G3PLUS-IH MK2   
   4 25CB    #   
  16 25X2    #               IT IMPLEMENTS THE SPECIAL HOOKS FOR 
  16 26BL    #               1)  OPEN MODE PERI'S (*MT AND *DA)  
  17 26W=    #               2)  DEWELLMENT OF A JOB (MOP AND BACK)  
  17 27*W    #               3)  THE END OF A JOB (FOR ANY REASON)   
   4 27TG    #   
   8 28*6    #     ENTRY POINTS  
  15 28SQ          SEGENTRY K1DOMONOP,START     [FROM OPCA   
  15 29#B          SEGENTRY K2DOMONOP,RETURN    [FROM RETURN 
  12 29S2          SEGENTRY K3DOMONOP,PASSWORD   
  10 2=?L          SEGENTRY K4DOMONOP,XLT
  17 2=R=          SEGENTRY K5DOMONOP,XVC       [ CLEAR VDU SCREEN   
   4 2?=W    #   
   4 2?QG    #   
   9 2#=6    XOB            4HOB :   
  16 2#PQ    #              ENTRY POINTS TO SET UP MACRO NAMES   
   9 2*9B          SEGENTRY G400HKD3 
   9 2*P2    XHKD3          12HHOOKS 
   8 2B8L                   4H.  
   9 2BN=          SEGENTRY G400OPNMT
   9 2C7W    XOPNMT         12HOPENMT
   9 2CMG          SEGENTRY G400OPNDA
   9 2D76    XOPNDA         12HOPENDA
  10 2DLQ          SEGENTRY G400DEWELL   
   9 2F6B    XDEWELL        12HDEWELL
  10 2FL2          SEGENTRY G400JOBEND   
   9 2G5L    XJOBEND        12HJOBEND
   4 2GK=    #   
  11 2H4W    #     RESET USER NAME IN CPREFIX
   4 2HJG    #   
   5 2J46    XUSER   
   8 2JHQ          LDX   2  FX2  
   8 2K3B          FJOCA    3,2  
   9 2KH2          LDN   4  JUSER(3) 
  10 2L2L          LDN   5  CPREFIX(2)   
   7 2LG=          MOVE  4  3
   7 2L_W          EXIT  7  0
   4 2MFG    #   
   4 2M_6    #   
  14 2NDQ    #     SET UP FILE/FABSNB BLOCK WITH JDF NAME
   4 2NYB    #   
   5 2PD2    SFABSNB 
  18 2PXL          HUNT     3,FILE,FABSNB       [ USE EXISTING FABSNB... 
   8 2QC=          SBX   7  FX1  
   8 2QWW          BPZ   3  STUF 
  11 2RBG          SETNCORE 10,3,FILE,FABSNB 
   4 2RW6    STUF
   8 2S*Q          LDN   0  10   
   8 2STB          STO   0  A1(3)
  10 2T*2          MHUNTW   2,JWELL,AJDF 
  16 2TSL          LDX   0  JDFGEN(2)           [ GENERATION NO. 
   9 2W#=          STO   0  A1+8(3)  
  16 2WRW          LDX   0  JDFLANG(2)          [ LANGUAGE CODE  
   9 2X?G          STO   0  A1+9(3)  
  15 2XR6          LDN   4  JDFDIR(2)           [ DIRECTORY  
   9 2Y=Q          LDN   5  A1+1(3)  
   7 2YQB          MOVE  4  3
  15 2_=2          LDN   4  JDFNAME(2)          [ FILENAME   
   9 2_PL          LDN   5  A1+4(3)  
   7 329=          MOVE  4  3
   7 32NW          LDN   0  1
   9 338G          STO   0  A1+7(3)  
   8 33N6          ADX   7  FX1  
   7 347Q          EXIT  7  0
   4 34MB    #   
  14 3572    #     LOCATE STATUS WORD IN JWELL/COPYSYS   
   4 35LL    #   
   5 366=    WELL3   
   8 36KW          STO   0  GEN0 
  11 375G          MHUNTW   3,JWELL,COPYSYS  
   7 37K6          JLADJUST 3
   9 384Q          BRN      (GEN0)   
   4 38JB    #   
  20 3942    #     ############################################################# 
   4 39HL    #   
  11 3=3=    #           K    K            1 
  11 3=GW    #           K   K            11 
  11 3?2G    #           K  K              1 
  11 3?G6    #           K K               1 
  11 3?_Q    #           KKK               1 
  11 3#FB    #           K  K              1 
  11 3#_2    #           K   K             1 
  11 3*DL    #           K    K            1 
  11 3*Y=    #           K     K           1 
   4 3BCW    #   
  20 3BXG    #     ############################################################# 
   4 3CC6    #   
   5 3CWQ    START   
   4 3DBB    #   
  12 3DW2    #              OPEN MODE PERI HOOK  
   4 3F*L    #   
   7 3FT=          SETHKPCA  
  15 3G#W          SETNCORE 2,3,IUSER,G400BLK   [MARKER BLK  
   7 3GSG          LDN   0  5
  15 3H#6          STO   0  A1(3)               [KIND OF BLK 
   7 3HRQ          LDX   0  4
  13 3J?B          SRL   0  15                  [TYPE
  15 3JR2          STO   0  A1+1(3)             [SAVE TYPE   
   7 3K=L          FCAPCA   2
   8 3KQ=          CHAIN    3,2  
   7 3L9W          LDX   6  4
   9 3LPG          LDX   7  EVENT2(2)
   9 3M96          LDXC  5  AWORK1(2)
   8 3MNQ          BCC      NUP  
   6 3N8B          UNPLUG
   4 3NN2    NUP 
  16 3P7L          REPMON                       [GENERATE MESSAGE
   4 3PM=    #   
  20 3Q6W    #     ############################################################# 
   4 3QLG    #   
  11 3R66    #           K    K          222 
  11 3RKQ    #           K   K          2   2
  11 3S5B    #           K  K               2
  11 3SK2    #           K K                2
  11 3T4L    #           KKK               2 
  11 3TJ=    #           K  K             2  
  11 3W3W    #           K   K           2   
  10 3WHG    #           K    K         2
  12 3X36    #           K     K       2222222   
   4 3XGQ    #   
  20 3Y2B    #     ############################################################# 
   4 3YG2    #   
   5 3Y_L    RETURN  
   4 3_F=    #   
  12 3_YW    #              ENTRY FROM MONRESUM  
   4 42DG    #   
  17 42Y6          FJOCA    3,2                 [RETURN FROM REPMON  
   9 43CQ          TESTRCTP 3,NOTR   
   8 43XB          BRN      SHOOK
   4 44C2    NOTR
   7 44WL          FPCAJO   3
   9 45B=          TESTTP   3,SHOOK  
   7 45TW          RINGTP   3
  19 46*G    SHOOK                              [SET UP BLOCK FOR HOOK MACRO 
  14 46T6          SETNCORE CPDATA-A1+8,3,ADATA,CREADL   
  17 47#Q          LDN   0  29                  [LENGTH OF COMMAND   
   8 47SB          STO   0  A1(3)
   9 48#2          LDN   4  XOB(1)   
   9 48RL          LDN   5  CPDATA(3)
  18 49?=          MVCH  4  17                  [ SET UP 'OB :USERNAME.' 
  11 49QW          MHUNT    2,IUSER,G400BLK  
  15 4==G          LDX   4  A1+1(2)             [TYPE OF PERI
   7 4=Q6          SBN   4  5
  15 4?9Q          BZE   4  XMT                 [ J IF *MT   
   9 4?PB          LDN   4  XOPNDA(1)
   8 4#92          BRN      XS   
   4 4#NL    #   
   4 4*8=    XMT 
   9 4*MW          LDN   4  XOPNMT(1)
   4 4B7G    XS  
  18 4BM6          MVCH  4  12                  [ SET UP HOOK MACRONAME  
  17 4C6Q          ISSUCOMB XERR,XBRK           [ ISSUE THE COMMAND  
  10 4CLB          VFREE    APED,ANEVENT 
  18 4D62          MHUNT    3,IUSER,G400BLK     [IF OK,TEST IF ^MR^ EXIT 
   9 4DKL          LDX   0  A1+1(3)  
  14 4F5=          BPZ   0  XEND                [NOT MR  
  16 4FJW          FJOCA    2                   [FIND JOB BLOCK  
  16 4G4G          TESTCOR  2,XS1               [J IF CORE IMAGE 
  19 4GJ6          BRN      XEND                [ELSE TREAT AS EX(NO PARAM)  
   4 4H3Q    XS1 
   8 4HHB          FPCACA   2,FX2
  17 4J32          CHAIN    3,2                 [CHAIN BLK AFTER PCA 
   7 4JGL          STEPBACK  
  16 4K2=          RUNPROG                      [RESTART PROGRAM 
   4 4KFW    XEND
  10 4K_G          MFREE    IUSER,G400BLK
  19 4LF6          ENDCOM                       [CONTINUE AT COMMAND LEVEL   
   4 4LYQ    XBRK
  10 4MDB          VFREE    APED,ANEVENT 
   7 4MY2          STEPBACK  
   7 4NCL          PROGBRKIN 
   4 4NX=    XERR
  10 4PBW          VFREE    APED,ANEVENT 
  11 4PWG          MHUNT    3,IUSER,G400BLK  
  14 4QB6          LDX   0  GSIGN               [SET B0=1
  17 4QTQ          ERS   0  EXT+8(2)            [SET COMERR STATUS   
  10 4R*B          LDX   0  CONTEXT(2)   
   8 4RT2          SLL   0  15   
  17 4S#L          BNG   0  XEND                [J IF NO CORE IMAGE  
   8 4SS=          BRN      XS1  
   4 4T?W    #   
  20 4TRG    #     ############################################################# 
   4 4W?6    #   
  11 4WQQ    #           K    K         3333 
  11 4X=B    #           K   K         3    3
  11 4XQ2    #           K  K               3
  11 4Y9L    #           K K                3
  11 4YP=    #           KKK              33 
  11 4_8W    #           K  K               3
  11 4_NG    #           K   K              3
  11 5286    #           K    K        3    3
  11 52MQ    #           K     K        3333 
   4 537B    #   
  20 53M2    #     ############################################################# 
   4 546L    #   
   5 54L=    PASSWORD
   4 555W    #   
  13 55KG    #               HOOK ON JOB DEWELLMENT  
   4 5656    #   
  14 56JQ          SETNCORE CPDATA-A1+6,3,ADATA,CREADL   
   9 574B          LDN   4  XOB(1)   
   9 57J2          LDN   5  CPDATA(3)
  18 583L          MVCH  4  17                  [ SET UP 'OB :USERNAME.' 
  10 58H=          LDN   4  XDEWELL(1)   
  17 592W          MVCH  4  12                  [ SET UP MACRONAME   
   8 59GG          LDN   4  29   
   8 5=26          STO   4  A1(3)
   8 5=FQ          LDX   2  FX2  
  16 5=_B          SETHOOK                      [ SET HOOK STATUS
   8 5?F2          CALL  0  WELL3
  10 5?YL          JBS      PA2,3,JLBMOP 
   9 5#D=          ISSUCOMB XUPC,XUPA
   8 5#XW          BRN      PA3  
   4 5*CG    PA2 
   8 5*X6          INHIBITBRK SER
   8 5BBQ          ISSUCOM  XUPC 
   7 5BWB          PERMITBRK 
   4 5CB2    PA3 
  15 5CTL          HUNT     2,IUSER,G400BLK     [CHECK EXIT  
   8 5D*=          BNG   2  XUPB 
  15 5DSW          LDX   0  A1(2)               [MARKER NO   
   7 5F#G          SBN   0  6
  17 5FS6          BNZ   0  XUPB                [CONTINUE IF NOT AB  
  16 5G?Q          FREECORE 2                   [FREE MARKER BLK 
   4 5GRB    XUPC
   7 5H?2          PERMITBRK 
   8 5HQL          CALL  0  WELL3
  10 5J==          JBS      SER,3,JLBMOP 
   9 5JPW          CALL  7  SFABSNB  
  15 5K9G          CLOSETOP                     [CLOSE JDF   
  16 5KP6          CALL  0  WELL3               [X3->JWEL/COPYSYS
  17 5L8Q          JBS      SER,3,JLBRJ         [J IF TEMP ABOR RJ   
   6 5LNB          ERASE 
   4 5M82    SER 
  15 5MML          UPPLUS   2                   [ABORT EXIT  
   4 5N7=    XUPA
   8 5NLW          CALL  0  WELL3
  11 5P6G          JBS      XUPA1,3,JLBMOP   
   9 5PL6          CALL  7  SFABSNB  
   7 5Q5Q          CLOSETOP  
   8 5QKB          CALL  0  WELL3
  10 5R52          JBS      XUPA1,3,JLBRJ
   8 5RJL          LDX   3  FX2  
  11 5S4=          JBS      XUPA1,3,EXABTEMP 
   6 5SHW          ERASE 
   5 5T3G    XUPA1   
  16 5TH6          UP                           [ BREAKIN EXIT   
   4 5W2Q    XUPB
   7 5WGB          PERMITBRK 
   7 5X22          UPPLUS   1
   4 5XFL    #   
  20 5X_=    #     ############################################################# 
   4 5YDW    #   
  11 5YYG    #           K    K            4 
  11 5_D6    #           K   K            44 
  11 5_XQ    #           K  K            4 4 
  11 62CB    #           K K            4  4 
  11 62X2    #           KKK           4   4 
  11 63BL    #           K  K         4444444
  11 63W=    #           K   K             4 
  11 64*W    #           K    K            4 
  11 64TG    #           K     K           4 
   4 65*6    #   
  20 65SQ    #     ############################################################# 
   4 66#B    #   
   4 66S2    XLT 
   4 67?L    #   
  17 67R=    #              HOOK ON THE END OF A JOB FOR ANY REASON  
   4 68=W    #   
   8 68QG          FJOCA    3,FX2
  19 69=6          SPRIVON  JSSTART,3           [SET SYSTEM STARTED STATUS   
   8 69PQ          LDN   0  62   
  18 6=9B          STO   0  ACOREP(3)           [GIVE JOB A HIGH CORE AND
  16 6=P2          LDX   0  ISISSMP             [MILL PRIORITY   
   9 6?8L          STO   0  AMILLP(3)
  14 6?N=          SETNCORE CPDATA-A1+8,3,ADATA,CREADL   
  17 6#7W          LDN   0  29                  [LENGTH OF COMMAND   
   8 6#MG          STO   0  A1(3)
   9 6*76          LDN   4  XOB(1)   
   9 6*LQ          LDN   5  CPDATA(3)
  18 6B6B          MVCH  4  17                  [SET UP 'OB :HOOKS.---'  
  10 6BL2          LDN   4  XJOBEND(1)   
  16 6C5L          MVCH  4  12                  [SET UP MACRONAME
  17 6CK=          SETHOOK                      [GIVE JOB HOOK STATUS
   8 6D4W          ISSUCOM XUP   
   4 6DJG    XUP 
   5 6F46          UP
   5 6FHQ    XUPLUS  
   7 6G3B          UPPLUS   1
   4 6GH2    #   
   4 6H2L    #   
  20 6HG=    #     ############################################################# 
   4 6H_W    #   
  11 6JFG    #           K    K       5555555
  10 6J_6    #           K   K        5  
  10 6KDQ    #           K  K         5  
  10 6KYB    #           K K          5  
  11 6LD2    #           KKK          555555 
  11 6LXL    #           K  K               5
  11 6MC=    #           K   K              5
  11 6MWW    #           K    K       5     5
  11 6NBG    #           K     K       55555 
   4 6NW6    #   
  20 6P*Q    #     ############################################################# 
   4 6PTB    #   
   4 6Q*2    #   
   9 6QSL          SEGENTRY G400VC   
   4 6R#=    XVC 
  14 6RRW          TESTHKN  XVC3       [ J IF NOT IN HOOK
   7 6S?G          CLEARVDU  
   9 6SR6          SEGENTRY G400VC2  
   4 6T=Q    XVC2
   6 6TQB          ENDCOM
   9 6W=2          SEGENTRY G400VC3  
   4 6WPL    XVC3
   9 6X9=          COMERR   ANOTKNOWN
   4 6XNW    #   
   4 6Y8G    #END
   6 ____ ...260374040001
