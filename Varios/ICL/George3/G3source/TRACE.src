  11 22FL    #LIS  K0TRACE>K0COMMAND>K0ALLGEO
  14 22R2 ...      SEG      TRACE,867,SECTION CENT,,G400 
   4 234B ...[   
  15 23*Q ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23M6 ...[   
  21 23YG    #              THIS CHAPTER IMPLEMENTS THE TRACE AND FULLTRACE COMMANDS.
  19 24D6    #              IF A TRACE COMMAND IS GIVEN,PROVIDING NO PREVIOUS
  21 24XQ    #              FULLTRACE COMMAND HAS BEEN GIVEN,IT ADJUSTS THE MASK OF  
  21 25CB    #              THE CATEGORY BITS CORRESPONDING TO THE PARAMETERS OF THE 
  21 25X2    #              COMMAND TO INCLUDE RELEVANT BITS SET IN THE INSTALLATION 
  13 26BL    #              PARAMETER MINTRACE AND   
  21 26W=    #              EXCLUDE RELEVANT BITS UNSET IN WORD CMFMSK IN THE SECOND 
  21 27*W    #              CPB/CALAS BLOCK,IF THIS BLOCK EXISTS.IT THEN STORES THE  
  21 27TG    #              NEW MASK IN WORD AMFMSK IN THE ACTIVITY BLOCK AND IN WORD
  16 28*6    #              CMFMSK IN THE FIRST CPB/CALAS BLOCK. 
  20 28SQ    #              IF A FULLTRACE COMMAND IS GIVEN,A BIT IS SET IN WORD 
  21 29#B    #              CLONG1 IN THE ACTIVITY BLOCK AND ALL RELEVANT CATEGORY   
  19 29S2    #              BITS ARE SET IN WORD AMFMSK IN THE SAME BLOCK.   
  21 2=?L    #              ONCE A FULLTRACE COMMAND HAS BEEN GIVEN,ANY SUBSEQUENT   
  13 2=R=    #              TRACE COMMAND IS IGNORED.
   6 2=T8 ...#UNS G400   
   4 2=X6 ...(   
  13 2=_4 ...#     THIS CHAPTER IS FOR G3PLUS-IH MK2 
   4 2?32 ...#   
  15 2?4Y ...#              IN HOOKED CONTEXT WITH NO PARAMS 
  20 2?6W ...#              THIS COMMAND SWITCHES THE PRIVILAGE MONITORING STATUS
   4 2?8S ...)   
  11 2?=W          SEGENTRY K1TRACE,SENTRY1  
  11 2?QG          SEGENTRY K2TRACE,SENTRY2  
   7 2#=6    #SKI  JNL1<1$1  
   9 2#PQ    QMASK          #02121777
   6 2*9B    #SKI  JNL1  
   9 2*P2    QMASK          #02023777
  19 2B8L    SENTRY2                            [ENTRY FROM FULLTRACE COMMAND
   7 2BN=          LDN   7  1
   7 2C7W          SRC   7  4
   7 2CMG          LDX   6  7
  20 2D76          ANDX  6  CLONG1(2)       [TEST IF PREVIOUS FULLTRACE COMMAND  
  19 2DLQ          BNZ   6  N1              [ISSUED.IF SO,BRANCH TO ENDCOM.  
  20 2F6B          ORS   7  CLONG1(2)       [SET BIT 3 IN CLONG1 IN THE ACTIVITY 
  17 2FL2                                   [BLOCK TO FLAG FULLTRACE 
   9 2G5L          LDX   7  QMASK(1) 
  21 2GK=          ORS   7  AMFMSK(2)       [SET ALL OUTPUT CATEGORY BITS IN AMFMSK  
  17 2H4W                                   [IN THE ACTIVITY BLOCK   
   7 2HJG          LDX   3  2
  20 2J46    N3    HUNT2  1,CPB,CALAS,3     [SET POINTER TO NEXT CPB/CALAS BLOCK 
  21 2JHQ          ORS   7  CMFMSK(1)       [SET ALL OUTPUT CATEGORY BITS IN CMFMSK  
  17 2K3B                                   [IN THIS CPB/CALAS BLOCK 
   9 2KH2    #SKI  K6TRACE>999-999   
   4 2L2L    (   
  12 2LG=             TRACE    CMFMSK(1),FULLTRAC
  10 2L_W             TRACE    1,FTLEVEL 
   4 2MFG    )   
  10 2M_6          LDEX  4  CPBCPLEV(1)  
   7 2NDQ          LDX   3  1
  20 2NYB          BNZ   4  N3              [TEST IF NO FURTHER CPB/CALAS BLOCKS 
   9 2PD2    #SKI  K6TRACE>999-999   
   4 2PXL    (   
  12 2QC=          TRACE    CLONG1(2),FLTRACE1   
  12 2QWW          TRACE    AMFMSK(2),FLTRACE2   
   4 2RBG    )   
   8 2RW6          BRN      N1   
   5 2S*Q    SENTRY1 
   6 2SBK ...#UNS G400   
   4 2SCD ...(   
  16 2SD? ...      TESTHKN  XDSK1           [J IF NOT IN A HOOK  
   7 2SF6 ...      LDN   4  0
   8 2SF_ ...      PARANUMB 5,4  
  16 2SGS ...      BNZ   5  XDSK1           [J IF PARAMS PRESENT 
  17 2SHM ...      DOTPMON  XDSK3           [J IF PRIV MONITORING SET
  18 2SJG ...      DOSPMON                  [SET PRIV MONITORING STATUS  
   8 2SK* ...      BRN      XDSK2
  19 2SL8 ...XDSK3 DOUPMON                  [ UNSET PRIV MONITORING STATUS   
  10 2SM3 ...XDSK2 MHUNTW   3,CPB,CALAS  
  19 2SMW ...      CPHOOK   3               [ SAVE HOOK BITS AT THIS CP LEVEL
   8 2SNP ...      BRN      N1   
  19 2SPJ ...XDSK4 COMERR   JCONTINC,JDIR   [NOT ALLOWED IN NO-USER CONTEXT  
   5 2SQC ...XDSK1   
  20 2SR= ...      BRUSEN   XDSK4           [ ERROR IN NO-USER NOT HOOKED CONTEXT
   4 2SS5 ...)   
   9 2STB          LDX   7  CLONG1(2)
   7 2T*2          SLL   7  3
  20 2TSL          BPZ   7  N4              [TEST IF FULLTRACE COMMAND HAS BEEN  
  21 2W#=                                   [ISSUED EARLIER.IF NOT,ANALYSE PARAMETERS
  20 2WRW                                   [OF COMMAND USING TANAMONST:OTHERWISE
  21 2X?G                                   [MARK PAMAMETERS ACCESSED AND BRANCH TO  
  13 2XR6                                   [ENDCOM  
   7 2Y=Q          MARKAXES  
   8 2YQB          BRN      N1   
  21 2_=2    N4    TANAMONST N1             [ANALYSE PARAMETERS.IF ERRONEOUS,WRITE   
  21 2_PL                                   [APPROPRIATE ERROR MESSAGE AND BRANCH TO 
  20 329=                                   [ENDCOM.IF VALID,PUT MASK OF OUTPUT  
  20 32NW                                   [CATEGORY BITS IN ACOMMUNE1 IN THE   
  15 338G                                   [ACTIVITY BLOCK  
  20 33N6          LDX   7  IMINTRACE       [LOAD INSTALLATION PARAMETER MINTRACE
   9 347Q    #SKI  K6TRACE>999-999   
   4 34MB    (   
  12 3572          TRACE    ACOMMUNE1(2),TRUEMASK
  10 35LL          TRACE    7,MINTRACE   
   4 366=    )   
  21 36KW          ORS   7  ACOMMUNE1(2)    [SET IN MASK CORRESPONDING BITS SET IN   
  20 375G                                   [MINTRACE AT PRESENT UNSET IN MASK   
  20 37K6          MHUNTW 3,CPB,CALAS       [SET POINTER TO FIRST CPB/CALAS BLOCK
  16 384Q    #     TEST IF TOP COMMAND PROCESSOR LEVEL > EXT+10. 
  18 38JB    #     THIS WORD CONTAINS EITHER ZERO OR TOP BREAKIN LEVEL   
   9 3942          LDEX  7  EXT+10(2)
   9 39HL          TXL   7  CPLEV(2) 
   8 3=3=          BCC      N2   
  21 3=GW          HUNT2  1,CPB,CALAS,3     [SET POINTER TO SECOND CPB/CALAS BLOCK   
  21 3?2G          LDX   7  CMFMSK(1)       [LOAD WORD CMFMSK OF 2ND CPB/CALAS BLOCK 
   9 3?G6    #SKI  K6TRACE>999-999   
   9 3?_Q          TRACE    7,TRMASK3
  21 3#FB          ANDS  7  ACOMMUNE1(2)    [UNSET IN MASK CORRESPONDING BITS UNSET  
  19 3#_2                                   [IN CMFMSK AT PRESENT SET IN MASK
  17 3*DL    N2    LDX   7  ACOMMUNE1(2)    [LOAD THE ADJUSTED MASK  
  21 3*Y=          STO   7  AMFMSK(2)       [STORE IT IN WORD AMFMSK OF THE ACTIVITY 
  13 3BCW                                   [BLOCK   
  20 3BXG          STO   7  CMFMSK(3)       [STORE IT IN WORD CMFMSK OF THE FIRST
  15 3CC6                                   [CPB/CALAS BLOCK 
   9 3CWQ    #SKI  K6TRACE>999-999   
   4 3DBB    (   
  11 3DW2          TRACE    AMFMSK(2),TRMASK1
  11 3F*L          TRACE    CMFMSK(3),TRMASK2
   4 3FT=    )   
   6 3G#W    N1    ENDCOM
  10 3GSG          MENDAREA 40,GAPTRACE  
   4 3H#6    #END
   8 ____ ...06137311000200000000
