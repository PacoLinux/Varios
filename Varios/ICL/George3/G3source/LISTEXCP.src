   8 22FL    #OPT  K0LISTEXCP=0  
  10 22_= ...#OPT           ATPOLTRP2=0  
   8 23DW    #OPT  ATPPHASET=0   
   9 23YG ...#OPT           ATPOLTR=0
  18 24D6    #LIS  K0LISTEXCP>K0LISTFILE>K0ALLGEO>K0GREATGEO>K0COMMAND   
   7 24XQ    #SEG  LISTEXCP  
  10 25CB                   8HLISTEXCP   
  15 25X2    [THIS SEGMENT CONTAINS CERTAIN EXCEPTION PATHS  
  10 26BL          SEGENTRY K50LISTEXCP  
  21 26W=    [***********************************************************************
   6 27*W    [DATA AREA  
  21 27TG    [THE CONTENTS OF THIS WORD WILL BE OVERWRITTEN BY THE CONTENTS OF %A OF 
  18 28*6    [THE LFPRINT MACRO,IF THE LATTER IS INCLUDED AT RESTORE TIME
   8 28SQ    SM             +0   
  11 29#B          SETENTRY LISTEXCP,POINT,5 
   4 2=?L    [   
   5 2=R=    TABLE48 
  19 2?=W    [POSSIBLE INVALID CHARACTERS FOR 48 CHARACTER REPERTOIRE PRINTER
  13 2?QG    TCOLON         #12                 [:   
  13 2#=6    TSEMICOLON     #13                 [;   
  13 2#PQ    TLESS          #14                 [<   
  13 2*9B    TGREATER       #16                 [>   
  13 2*P2    TQUERY         #17                 [?   
  13 2B8L    TSHRIEK        #21                 [!   
  13 2BN=    TDOUBLEDASH    #22                 [^   
  13 2C7W    THASH          #23                 [#   
  13 2CMG    TPERCENT       #25                 [%   
  13 2D76    TAT            #40                 [@   
  13 2DLQ    TLHSQBR        #73                 [[   
  13 2F6B    TDOLLAR        #74                 [$   
  13 2FL2    TRHSQBR        #75                 []   
  13 2G5L    TUPARROW       #76                 [_   
  13 2GK=    TLPTARROW      #77                 [_   
  17 2H4W    XCONT          #74202035           [CONTINUATION LINE   
   8 2HJG    OCT64          #64  
   5 2HML    PLPLINK 
  10 2HQQ          PAIR     LISTINIT,5   
   6 2HTW    PTPCPLINK   
  10 2H_2          PAIR     LISTINTC,5   
  21 2J46    [***********************************************************************
   7 2JHQ    [SUBROUTINE AREA
   6 2K3B    TESTCHAR48  
  20 2KH2    [THIS SUBROUTINE TESTS A CHARACTR IN X6 AGAINST TABLE48 OF POSSIBLE 
  15 2L2L    [INVALID CHARACTERS FOR 48 CHARACTER REPERTOIRE.
  21 2LG=    [IF THE CHARACTER IS INVALID,EXIT IS TO THE INSTRUCTION FOLLOWING CALL  
  19 2L_W    [IF THE CHARACTER IS VALID,EXIT IS TO ONE INSTRUCTION FURTHER.  
  17 2MFG          LDN   1  TABLE48             [X1 POINTS TO TABLE  
  14 2M_6          LDN   5  15                  [COUNTER 
   8 2NDQ    NEXT  SMO      FX1  
  19 2NYB          LDX   0  0(1)                [GET NEXT CHARACTER IN TABLE 
  17 2PD2          TXU   0  6                   [DO CHARACTERS MATCH?
  15 2PXL          BCS      NOMATCH             [J IF NO BUT 
  15 2QC=          EXIT  4  0                   [IF YES,EXIT 
   5 2QWW    NOMATCH 
  18 2RBG          ADN   1  1                   [UPDATE TABLE MODIFIER   
  20 2RW6          BCT   5  NEXT                [AND GET NEXT CHARACTER UNLESS   
  20 2S*Q          EXIT  4  1                   [TABLE FULLY SCANNED,WHEN EXIT   
  21 2STB    [***********************************************************************
   9 2T*2    [MAIN BODY OF SEGMENT   
  21 2TSL    [***********************************************************************
   5 2W#=    POINT1  
  21 2WRW    [THIS ENTRY POINT IS FROM LISTINIT IF THE LFPRINT MACRO HAS BEEN ISSUED 
  21 2X?G    [AND THE SPECIAL DEBATCHING FACILITY HAS BEEN INCORPORATED.WE EXPECT TO 
  21 2XR6    [COLON USERNAME POINT JOBNAME IN WORDS GZDATA+5 ET SEQ OF THE ACTLF BUFF
  18 2Y=Q          LDX   7  SM(1)               [GET LFPRINT PARAMETER   
   7 2YQB          LDN   5  0
  19 2_=2          LDN   3  0                   [X3 WILL BE ADDRESS MODIFIER 
  14 2_PL          LDN   4  #20                 [SPACE   
  19 329=          DCH   4  GZDATA+5(2)         [OVERWRITE COLON WITH SPACE  
  16 32NW    Y1    ADN   3  1                   [UPDATA MODIFIER 
   7 338G          SRC   3  2
   8 33N6    Y2    SMO      FX2  
  16 347Q          LDCH  5  GZDATA+5(3)         [GET CHARACTER   
   8 34MB          SBN   5  #36  
  15 3572          BZE   5  Y4                  [J IF POINT  
  19 35LL          BNG   7  Y3                  [J IF CHARACTER MUST BE SAVED
  16 366=          SMO      FX2                 [ELSE SPACEFILL  
  10 36KW          DCH   4  GZDATA+5(3)  
  20 375G    Y3    SLL   7  1                   [PREPARE TEST FOR NEXT CHARACTER 
   7 37K6          SLC   3  2
   8 384Q          BRN      Y1   
  18 38JB    Y4    LDX   7  SM(1)               [NOW DEAL WITH USERNAME  
  16 3942          SLL   7  12                  [GET USERNAME KEY
   8 39HL          SMO      FX2  
  16 3=3=          DCH   4  GZDATA+5(3)         [SPACEFILL POINT 
   7 3=GW    Y5    SLC   3  2
   7 3?2G          ADN   3  1
   7 3?G6          SRC   3  2
   8 3?_Q    Y6    SMO      FX2  
  17 3#FB          LDCH  5  GZDATA+5(3)         [GET NEXT CHARACTER  
  16 3#_2          SBN   5  #20                 [IS IT A SPACE?  
  14 3*DL          BZE   5  Y8                  [IF YES J
  21 3*Y=          BNG   7  Y7                  [NOT A SPACE;J IF CHARACTER TO STAY  
  13 3BCW          SMO      FX2                 [ELSE
  15 3BXG          DCH   4  GZDATA+5(3)         [SPACEFILL   
  20 3CC6    Y7    SLL   7  1                   [PREPARE TO TEST NEXT CHARACTER  
   8 3CWQ          BRN      Y5   
   5 3DBB    Y8    UP
   4 3DW2    [   
  21 3F*L    [***********************************************************************
   5 3FT=    POINT2  
  14 3G7M    [THIS ENTRY POINT IS FROM LISTPROC AT EMS   
  14 3GG4    [ SET ACTLFS FOR ALL LOCAL OUTPUT DEVICES   
  15 3GSG    [DEBATCHING CHARACTERS FOR ALL CENTRAL PRINTERS 
  14 3H6X          LDN   7  1                   [TP TYPE 
   5 3HF#    X2FIND  
  15 3HRQ          FINDPEREC 3,APFOTY,7,ZRENT    [GET NEXT LP
  19 3HXS          BNG   3  X2TYPE              [ J IF NOMORE OF THIS TYPE   
  16 3HYS ...      JWRONG  3,ZRENT              [J. DEVICE WRONG 
  19 3H_T ...      JALLOC 3,ZRENT               [INCASE ENGAGE BEAT US TO IT 
   7 3J3W          SBN   7  2
  15 3J7Y          BNZ   7  X2NLP               [ J IF NOT LP
  20 3J#5          LPROP    3,4                 [GET PROP. BITS FROM DEVICE LIST 
   8 3J#S          LDN   6  160  
   7 3J*H          SRC   4  5
  17 3JB=          BNG   4  STOW                [J IF 160 PP PRINTER 
   8 3JB_          LDN   6  132  
   7 3JCN          SLC   4  3
  15 3JDC          BNG   4  STOW                [J IF 132 PP 
   8 3JF6          LDN   6  120  
   7 3JFT          SRC   4  2
  15 3JGJ          BNG   4  STOW                [J IF 120 PP 
  16 3JH?          LDN   6  96                   [OTHERWISE 96 PP
   4 3JJ2    STOW
   8 3JK8          LADDP    3,1  
  21 3JLB          STO   6  AREPERT(1)          [INITIALIZE TO H/W WIDTH OF PRINTER  
   8 3JMK          LDN   0  65   
   8 3JN#          SLL   0  12   
  19 3JP3          ADS   0  AREPERT(1)          [INITIALIZE PAGELENGTH TO 65 
   8 3JPQ          LDX   1  FX1  
   9 3JR2          LDX   6  K52+4(3) 
   7 3K=L          SLL   6  6
   8 3KQ=          ADN   6  #40  
   7 3L9W          SRC   6  6
  19 3LPG          STO   6  K52+4(3)              [WILL BE INCREMENTED TO A  
  17 3MQ5          LPUNSET  3                   [ PAPER NOT ALIGNED  
   5 3MRD    X2NLP   
  17 3MSR          SETALLOC 3                   [ DEVICE ALLOCATED   
  11 3MW6          GETACT   ACTLF,CENTRAL,1  
  17 3MXF          LDX   2  BPTR(2)             [ -> ACTLF/CENTRAL   
  16 3MYS          STO   3  CPPTR(2)            [ DEVICE LIST PTR
   8 3N27          LGEOG    3,0  
  17 3N3G          DEX   0  JSOURCE1(2)         [ GEOGRAPHICAL NO.   
  10 3N4T          LDN   1  PLPLINK(1)   
   9 3N68          BZE   7  X2NLPL   
  11 3N7H          ADN   1  PTPCPLINK-PLPLINK
   5 3N8W    X2NLPL  
   7 3N=9          ADN   7  2
  15 3N?J          DCH   7  JSOURCE1(2)         [ DEVICE TYPE
  18 3N#X          LINKSET  2,0(1)              [ LINK TO LISTING CODE   
   6 3NB=          FPUT  
  15 3NCK          BRN      ZRENT               [ NEXT DEVICE
   5 3NDY    X2TYPE  
  15 3NG?          SLL   7  1                   [ NEXT TYPE  
   7 3NHL          ANDN  7  7
  16 3NJ_          BNZ   7  X2FIND              [ J IF VALID TYPE
   5 3NL#          UP
  21 3NN2    [********************************************************************** 
   5 3P7L    POINT3  
   4 3PM=    [   
  19 3Q6W    [THIS ENTRY POINT IS FROM LISTLOOP AFTER AN ERROR CONDITION HAS 
  14 3QLG    [OCCURRED ON A 48 CHARACTER REPERTOIRE ECCP.
  21 3R66    [THE CODE VALIDATES THE CURRENT RECORD OF THE FILE,AND IF NECCESSATY THE
  20 3RKQ    [PREVIOUS RECORD,TO DISCOVER IF THE ERROR CONDITION AROSE FROM AN   
  13 3S5B    [ATTEMPT TO PRINT AN INVALID CHARACTER. 
  18 3SK2    [IF AN INVALID CHARACTER IS FOUND,ACOMMUNE8 IS SET NEGATIVE.
  19 3T4L    [IF AN INVALID CHARACTER IS NOT FOUND,ACOMMUNE8 IS SET TO ZERO  
  15 3TJ=          STOZ     AWORK1(2)           [INITIALISE  
  10 3W3W          STOZ     ACOMMUNE8(2) 
  14 3WHG          STOZ     ACOMMUNE7(2)        [EOF KEY 
   5 3X36    PREVREC 
  20 3XGQ          STEPAGAIN                    [GET POINTER TO CURRENT RECORD   
  20 3Y2B          LDEX  7  FRH(3)              [GET NUMBER OF WORDS IN RECORD   
  11 3YG2 ...      MHUNTW   2,GOUT,INFOLIST  
   9 3Y_L          LDX   0  GQUAL(2) 
   7 3_F=          ANDN  0  1
  17 3_YW          BNZ   0  SPECIAL             [J IF SPECIAL LISTING
   9 42DG          LDX   0  GQUAL(2) 
   8 42Y6          ANDN  0  #740 
  20 43CQ          BZE   0  SPECIAL             [J IF LISTING OF AMORPHOUS FILE  
   7 43XB          ADN   3  1
   7 44C2          SBN   7  1
   5 44WL    SPECIAL 
  18 45B=          ADN   3  1                   [POINTER TO START OF DATA
  17 45TW          SBN   7  1                   [COUNT OF DATA WORDS 
  18 46*G          SLL   7  2                   [COUNT OF DATA CHARACTERS
  21 46T6    [NOTE:CHARACTERS IN THE LAST WORD OF THE RECORD WHICH ARE NOT ACTUALLY  
  21 47#Q    [PART OF THE RECORD REQUIRE NO SPECIAL ACTION BECAUSE THEY ARE SPACES   
  17 47SB          LDN   2  0                   [CHARACTER MODIFIER  
   5 48#2    TRYNEXT 
   7 48RL          SMO      3
  17 49?=          LDCH  6  0(2)                [GET NEXT CHARACTER  
  15 49QW          CALL  4  TESTCHAR48          [IS IT VALID?
  15 4==G          BRN      NOTVALID            [IF NOT,JUMP 
  19 4=Q6          BCHX  2  /                   [ELSE IF NOT END OF RECORD   
  19 4?9Q          BCT   7  TRYNEXT             [RETURN FOR NEXT CHARACTER   
  16 4?PB          SMO      FX2                 [HAVE WE ALREADY 
  18 4#92          LDX   0  AWORK1              [BACKSPACED ONE RECORD?  
  15 4#NL          BNZ   0  OKFIN               [IF YES,JUMP 
  17 4*8= ...      MHUNTW   2,GOUT,INFOLIST     [TEST IF ON FIRST REC
   8 4*MW          SMO      FX2  
   9 4B7G          LDX   0  AWORK4   
  20 4BM6          BPZ   0  NOTSETUP            [J IF NOT CURRENTLY LISTING SETUP
   9 4C6Q          LDX   0  GNUM(2)  
  19 4CLB          BZE   0  OKALLFIN            [J IF ON 1ST RECORD OF SETUP 
   9 4D62          BRN      TOBACK   
   5 4DKL    NOTSETUP
  18 4F5=          LDX   5  GNUM(2)             [FIRST RECORD OF LISTING?
  11 4FJW ...      MHUNTW   2,GOUT,INFOLIST  
   9 4G4G          LDX   4  GFLI(2)  
   7 4GJ6          TXU   4  5
  15 4H3Q          BCC      OKALLFIN            [IF YES,JUMP 
   7 4HHB    #SKI  ATPOLTR   
   9 4J32          TRACEVER 4,INV48  
   5 4JGL    TOBACK  
  16 4K2=          BACKSPACE                    [ELSE WE MUST NOW
  18 4KFW          LDN   0  1                   [CHECK PRECEDING RECORD  
   8 4K_G          LDX   2  FX2  
   9 4LF6          STO   0  AWORK1(2)
   9 4LYQ          BRN      PREVREC  
   5 4MDB    NOTVALID
   7 4MY2    #SKI  ATPOLTR   
   9 4NCL          TRACEVER 0,INV48CH
   9 4NX=          TRACE 6,LFEXCPIC  
   8 4PBW          LDX   2  FX2  
  16 4PWG          LDX   7  AWORK1(2)           [BACKSPACE MARKER
  18 4QB6          ORX   7  GSIGN               [INVALID CHARACTER MARKER
   5 4QTQ    OKFIN   
   6 4R*B          STEP  
  10 4RT2          STO   7  ACOMMUNE8(2) 
  10 4S#L          STOZ     ACOMMUNE7(2) 
  15 4SS=          BNZ   3  OKALLFIN            [J IF NOT EOF
  13 4T?W          LDX   0  GSIGN               [ELSE
  10 4TRG          STO   0  ACOMMUNE7(2) 
   5 4W?6    OKALLFIN
   5 4WQQ          UP
  21 4X=B    [*******************-*************************************************  
   5 4XQ2    POINT4  
  19 4Y9L    [THIS ENTRY POINT COPES WITH MODE 4 LINEPRINTER TRANSFERS WHEN  
  19 4YP=    [THE RECORD SIZE IN CHARACTERS EXCEEDS THE CURRENT PRINT WIDTH  
  17 4_8W          STOZ     AWORK1(2)           [INITIALISE SHIFT KEY
  20 4_NG          STOZ     AWORK2(2)           [INITIALISE COUNT OF PRINT CHARS 
   6 4_R5 ...#UNS  ANIP  
  10 4_TN ...      LDEX  4  JSOURCE3(2)  
   6 4_Y? ...#UNS  ANIP  
   4 522W ...#SKI
   4 525F ...(   
  16 5286          LADDP    ,3                  [GET APIA POINTER
  17 52MQ          LDEX  4  AREPERT(3)          [PRINT WIDTH INTO X4 
   4 537B ...)   
   7 54L=    #SKI  ATPPHASET 
   8 555W          LDN   4  40   
  18 55KG          SBX   4  GZDUMP(2)           [NUMBER AND CONT LINES!  
  21 5656          LDN   5  0                   [INITIALISE ACTLF BUFFER CHARACTER CO
   7 56JQ          STEPAGAIN 
  16 574B          LDX   1  GZPOINT(2)          [POINT TO NEXT   
  17 57J2          SRC   1  2                   [CHARACTER OF RECORD 
   9 57MX ...      BNZ   7  NGRAPH   
  17 57RS ...      LDN   7  #74                 [STORE GRAPHIC SHIFT 
   5 57XP ...NGRAPH  
  16 583L          STO   7  ACOMMUNE3(2)        [INITIALISE SHIFT
   9 58H=          LDX   0  GZDUMP(2)
   7 592W          SRL   0  2
   7 59GG          SMO      0
  18 5=26          DCH   7  GZDATA+1(2)         [PUT IN SHIFT CHARACTER  
   5 5=FQ    NEXTCHAR
   7 5=_B          SMO      3
  19 5?F2          LDCH  6  0(1)                [GET NEXT RECORD CHARACTER   
   8 5?YL          SMO      FX1  
   9 5#D=          TXL   6  TDOLLAR  
  17 5#XW          BCS      NOTSHIFT            [J IF NOT SHIFT OR _ 
   8 5*CG          SMO      FX1  
   9 5*X6          TXU   6  TLPTARROW
  15 5BBQ          BCS      TOSHIFT             [J IF SHIFT  
   5 5BWB    NOTSHIFT
  19 5CB2          STOZ     AWORK1(2)           [CURRENT CHARACTER NOT SHIFT 
   7 5CTL          LDN   0  1
  20 5D*=          ADS   0  AWORK2(2)           [UPDATE COUNT OF PRINT CHARACTERS
   6 5DSW    TOACTLFUPD  
  21 5F#G          ADN   5  1                   [UPDATE COUNT OF ACTLF BUFFER CHARACT
   6 5FS6    TORECUPDATE 
  20 5G?Q          SLC   1  2                   [UPDATE POINTER TO RECORD BUFFER 
   7 5GRB          ADN   1  1
   7 5H?2          SRC   1  2
  14 5HQL          LDX   2  5                   [INSERT  
   8 5J==          SMO      FX2  
   9 5JPW          ADX   2  GZDUMP   
  17 5K9G          SRC   2  2                   [CURRENT CHARACTER   
  13 5KP6          SMO      FX2                 [INTO
  14 5L8Q          DCH   6  GZDATA+1(2)         [ACTLF   
  14 5LNB          LDX   2  FX2                 [BUFFER  
   9 5M82          LDX   0  AWORK2(2)
   7 5MML          ADN   0  4
  21 5N7=          TXU   0  4                   [IS PRINT COUNT 4 LESS THAN MAXIMUM? 
  15 5NLW          BCS      NOTEQUAL            [IF NOT JUMP 
  21 5P6G          STO   1  AWORK4(2)           [ELSE REMEMBER 1POINTERS TO RECORD   
  17 5PL6          STO   5  AWORK3(2)           [AND ACTLF BUFFERS   
  19 5Q5Q          LDX   0  ACOMMUNE3(2)        [AND PRESERVE CURRENT SHIF   
  16 5QKB          STO   0  ACOMMUNE2(2)        [CURRENT SHIFT   
   6 5R52    TOANYMORE   
  14 5RJL          LDX   0  1                   [ANY MORE
  15 5S4=          SLC   0  2                   [CHARACTERS  
  13 5SHW          SBX   0  GZPOINT(2)          [IN  
  14 5T3G          SBX   0  GZLENGTH(2)         [RECORD? 
  20 5TH6          BNZ   0  NEXTCHAR            [IF YES RETURN FOR NEXT CHARACTER
   9 5W2Q          ADX   5  GZDUMP(2)
  17 5WGB          LDX   0  GZLENGTH(2)         [ELSE UPDATE COUNT OF
   4 5X22    TOUP
  19 5XFL          STO   0  GZTRANS(2)          [RECORD CHARACTERS DEALT WITH
  21 5X_=          ADN   5  1                   [COUNT SHIFT CHARACTER AT START OF   
   6 5Y4B ...#UNS  ANIP  
   9 5Y7G ...     STO   5  GZDUMP(2) 
   6 5Y=L ...#UNS  ANIP  
   4 5Y*Q ...#SKI
  21 5YDW          DCA      ,STO,5,COUNT        [BUFFER AND INSERT COUNT IN CONTROL A
   7 5YYG    #SKI  ATPOLTRP2 
   9 5_D6          TRACEVER 5,EXCPCT 
   5 5_XQ          UP
   5 62CB    NOTEQUAL
  20 62X2          SBN   0  5                   [IS COUNT OF CHARACTERS FOR PRINT
  19 63BL          TXU   0  4                   [LINE ONE MORE THAN MAXIMUM? 
  21 63W=          BCS      TOANYMORE           [IF NOT GO BACK FOR NEXT CHARACTER   
  19 64*W          LDX   3  AWORK3(2)           [ELSE GET POINTER TO ACTLF   
  21 64TG          ADN   3  1                   [BUFFER WHEN 4 CHARACTERS OF PRINT   
  13 65*6          LDN   6  4                   [SHOT
   7 65SQ          SRC   3  2
   7 66#B          LDN   2  0
  13 68=W          SMO      FX2                 [4   
   9 68QG          LDX   5  GZDUMP   
   7 69=6          SRC   5  2
   8 69PQ          ADX   5  FX2  
   5 69TM ...TOCONT  
   8 69_J ...      SMO      FX1  
  15 6=5F ...      LDCH  0  XCONT(2)            [AND MOVE IN 
   7 6=9B          SMO      5
  18 6=P2          DCH   0  GZDATA+1(3)         [CONTINUATION CHARACTERS 
   8 6?8L ...      BCHX   2  /   
   8 6?N= ...      BCHX   3  /   
   9 6B6B          BCT   6  TOCONT   
   8 6BL2          LDX   2  FX2  
  17 6C5L          LDX   5  AWORK3(2)           [ACTLF BUFFER COUNT  
  19 6CK=          ADN   5  4                   [ALLOW FOR CONTINUATION LINE 
  19 6D4W          ADX   5  GZDUMP(2)           [AND SPACES AND NUMBER.....  
  16 6DJG          LDX   0  AWORK4(2)           [COUNT OF RECORD 
  16 6F46          SLC   0  2                   [CHARS DEALT WITH
  10 6FHQ          SBX   0  GZPOINT(2)   
   8 6G3B          BRN      TOUP 
   5 6GH2    TOSHIFT 
   8 6GS5 ...      SMO      FX1  
  17 6H58 ...      BXE   6  TUPARROW,XDELSFT    [J. IF DELTA SHIFT   
  16 6HB? ...      STO   6  ACOMMUNE3(2)        [JREMEMBER SHIFT 
   5 6HMB ...XDELSFT 
   9 6HYF ...      LDX   0  AWORK1(2)
  20 6J9J ...      BNG   0  TORECUPDATE         [J. IF PREVIOUS CHARACTER SHIFT  
  14 6JGM ...      LDX   0  GSIGN               [ELSE SET
   9 6JRQ ...      ORS   0  AWORK1(2)
  15 6K4T ...                                   [SHIFT KEY   
  20 6K*Y ...      BNZ   5  TOACTLFUPD          [AND RETURN TO DEPOSIT CHARACTER 
  20 6KM3 ...      BRN      TORECUPDATE         [1ST SHIFT CHAR ADD, IN AT END   
  21 6KYB    [*****************************-*****************************************
   5 6LD2    POINT5  
  19 6LXL    [THIS ENTRY POINT TESTS FOR THE PRESENCE OF AN INVALID CHARACTER
  21 6MC=    [AFTER AN ERROR CONDITION WHILE IMPLEMENTING A MODE 4 PERI-IF AN INVALID
  21 6MWW    [CHARACTER IS FOUND IN THE CURRENT OR PRECEDING RECORD ACOMMUNE8 IS SET 
  21 6NBG    [NEGATIVE-OTHERWISE ACOMMUNE8 IS SET TO ZERO-NOTE THE FILE WILL BE A SHI
  15 6NW6    [SHIFT FILE AND THE LISTING WILL NOT BE SPECIAL 
  20 6P*Q          STOZ     ACOMMUNE8(2)        [INITIALISE INVALID CHARACTER KEY
  17 6PTB          STOZ     AWORK1(2)           [AND BACKSPACE KEY   
  16 6Q*2          STOZ     ACOMMUNE7(2)        [END OF FILE KEY 
   6 6QSL    PREV96REC   
   7 6R#=          STEPAGAIN 
   9 6RRW          LDX   0  FRH+1(3) 
   9 6S?G          LDEX  7  FRH(3)   
  20 6SR6          SLC   0  2                   [GET NUMBER OF UNUSED CHARACTERS 
  18 6T=Q          ANDN  0  3                   [IN LAST WORD OF RECORD  
  19 6TQB          BZE   0  TOFULLWORD          [J IF LAST WORD FULLY USED   
  18 6W=2          SBN   7  1                   [ELSE ADJUST WORD COUNT  
   6 6WPL    TOFULLWORD  
  18 6X9=          ADN   3  2                   [POINT TO START OF DATA  
  17 6XNW          SBN   7  2                   [NUMBER OF DATA WORDS
  20 6Y8G          SLL   7  2                   [CONVERT TO CHARACTERS AND ADJUST
  19 6YN6          ADX   7  0                   [FOR LAST WORD IF NECESSARY  
   6 6_7Q    NEXT96CHAR  
   9 6_MB          LDCH  6  FRH(3)   
  21 7272          SBN   7  1                   [NUMBER OF REMAINING CHARS TO BE CHQ 
  17 72LL          TXU   6  TUPARROW(1)         [IS IT DELTA SHIFT?  
  15 736=          BCC      YESDELTA            [IF YES JUMP 
  17 73KW          TXU   6  TLPTARROW(1)        [IS IT ILLEGAL #77?  
  15 745G          BCC      TOERROR             [IF YES JUMP 
  19 74K6          BRN      TOKVALID            [ELSE MUST BE VALID CHARACTER
   5 754Q    YESDELTA
  20 75JB          BZE   7  TEND96REC           [J IF ON LAST CHARACTER OF RECORD
  16 7642          SLC   3  2                   [UPDATE POINTER  
  13 76HL          ADN   3  1                   [TO  
  16 773=          SRC   3  2                   [RECORD BUFFER   
  21 77GW          LDCH  6  FRH(3)              [GET NEXT CHARACTER AND UPDATE COUNT 
  18 782G          SBN   7  1                   [OF REMAINING CHARACTERS 
  17 78G6          TXU   6  TUPARROW(1)         [IS IT DELTA SHIFT?  
  19 78_Q          BCC      YESDELTA            [IF YES GET NEXT CHARACTER   
   9 79FB          TXL   6  OCT64(1) 
  19 79_2          BCS      TOERROR             [J IF ILLEGAL:LESS THAN #64  
  10 7=DL          TXL   6  TLPTARROW(1) 
  16 7=Y=          BCC      TOERROR             [J IF ILLEGAL:#77
  10 7?CW          TXU   6  TLHSQBR(1)   
  16 7?XG          BCC      TOERROR             [J IF ILLEGAL:#73
   5 7#C6    TOKVALID
  18 7#WQ          BZE   7  TEND96REC           [J IF ON END OF RECORD   
  17 7*BB          SLC   3  2                   [ELSE UPDATE RECORD  
  18 7*W2          ADN   3  1                   [CHARACTER POINTER AND   
   7 7B*L          SRC   3  2
  19 7BT=          BRN      NEXT96CHAR          [RETURN FOR NEXT CHARACTER   
   6 7C#W    TEND96REC   
  20 7CSG          LDX   0  AWORK1(2)           [ALREADY BACKSPACED ONE RECORD?  
  15 7D#6          BNZ   0  TO96FIN             [IF YES JUMP 
  11 7DRQ ...      MHUNTW   2,GOUT,INFOLIST  
   8 7F?B          SMO      FX2  
   9 7FR2          LDX   0  AWORK4   
  20 7G=L          BPZ   0  NOT96SETUP          [J IF NOT 3URRENTLY LISTING SETUP
   9 7GQ=          LDX   0  GNUM(2)  
  20 7H9W          BZE   0  OKALL96FIN          [J IF ON FIRST RECORD OF SETUP   
  19 7HPG          BRN      TO96BACK            [ELSE CHECK PRECEDING RECORD 
   6 7J96    NOT96SETUP  
  14 7JNQ          LDX   5  GNUM(2)             [ARE WE  
  15 7K8B ...      MHUNTW   2,GOUT,INFO         [POSITIONED  
  17 7KN2          LDX   4  GFLI(2)             [ON FIRST RECORD OF  
  14 7L7L          TXU   4  5                   [LISTING 
  15 7LM=          BCC      OKALL96FIN          [IF YES JUMP 
   5 7M6W    TO96BACK
  20 7MLG          BACKSPACE                    [ELSE POSITION ON PREVIOUS RECORD
   7 7N66          LDN   0  1
  17 7NKQ          STO   0  AWORK1(2)           [SET BACKSPACED KEY  
   7 7P5B    #SKI  ATPOLTRP2 
   9 7PK2          TRACEVER 5,BACKSP 
   9 7Q4L          BRN      PREV96REC
   5 7QJ=    TOERROR 
   7 7R3W    #SKI  ATPOLTRP2 
   9 7RHG          TRACEVER 6,INV96  
   9 7S36          TRACE 6,LFEXCPIC  
  17 7SGQ          LDX   7  AWORK1(2)           [BACKSPACED MARKER   
  18 7T2B          ORX   7  GSIGN               [INVALID CHARACTER MARKER
   5 7TG2    TO96FIN 
   6 7T_L          STEP  
  10 7WF=          STO   7  ACOMMUNE8(2) 
  10 7WYW          STOZ     ACOMMUNE7(2) 
  15 7XDG          BNZ   3  OKALL96FIN          [J IF NOT EOF
  13 7XY6          LDX   0  GSIGN               [ELSE
   8 7YCQ          SMO      FX2  
  14 7YXB          STO   0  ACOMMUNE7           [SET KEY 
   6 7_C2    OKALL96FIN  
   5 7_WL          UP
   4 82B=    #END
   8 ____ ...36506370000200000000
