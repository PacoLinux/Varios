  10 22FL    #LIS  K0EDITEX>K0ALLGEO>0   
  16 22_=    #SEG  EDITEX7                      [DICK RUSBRIDGE  
   7 23DW          8HEDITEX  
   4 23YG    #   
  11 24D6    #              ENTRY POINT TABLE
   4 24XQ    #   
  10 25CB     SEGENTRY K1EDITEX,N1EDITEX 
  10 25X2     SEGENTRY K2EDITEX,N2EDITEX 
   9 26BL     SEGENTRY K3EDITEX,OH   
  10 26W=     SEGENTRY K4EDITEX,N4EDITEX 
  10 27*W     SEGENTRY K5EDITEX,N5EDITEX 
  10 27TG     SEGENTRY K7EDITEX,N7EDITEX 
   9 28*6    OH    GEOERR   1,EDITEX 
   4 28SQ    #   
  14 29#B    #     QFIT IS THE CHARACTER COMPARE ROUTINE 
  19 29S2    #     ENTERED WITH X1,X3 POINTERS TO THE START OF THE STRINGS   
  12 2=?L    #     X4 NO OF CHARACTERS TO COMPARE
  13 2=R=    #     STRINGS FIT IF X4 IS ZERO ON EXIT 
   4 2?=W    #   
  16 2?QG    QFIT  BZE   4  QFT3                [J IF NULL STRING
   8 2#=6    QFT1  LDCH  7  0(1) 
  16 2#PQ          LDCH  6  0(3)                [LOAD CHARACTERS 
  17 2*9B          BXE   7  IEDS(2),QFT2        [J IF DUMMY SYMBOL   
  15 2*P2          BXU   7  6,QFT3              [J IF MISS   
  17 2B8L    QFT2  BCHX  1  /                   [INCREMENT POINTERS  
   7 2BN=          BCHX  3  /
  13 2C7W          BCT   4  QFT1                [LOOP
  13 2CMG    QFT3  EXIT  0  0                   [EXIT
   4 2D76    #   
   4 2DLQ    #   
  18 2F6B    RLOF  LDX   5  IEOM(2)             [RELOCATE OLD FILE BLOCK 
   9 2FB8          LDX   3  IEOA(2)  
   9 2FL2          TESTMOVE 5,RLO1   
  10 2G5L          MHUNTW   3,EDIT,FRB   
   7 2GK=          PHOTO    5
   9 2H4W          STO   3  IEOA(2)  
   9 2HJG          STO   5  IEOM(2)  
   7 2J46    RLO1  EXIT  6  0
   4 2JHQ    #   
   4 2K3B    #   
   8 2KH2    SPACE          #20  
   4 2L2L    #   
  18 2LG=    RLNF  LDX   5  IENM(2)             [RELOCATE NEW FILE BLOCK 
   9 2LQ4          LDX   3  IENA(2)  
   9 2L_W          TESTMOVE 5,RLN1   
  10 2MFG          MHUNTW   3,FILE,FAPB  
   7 2M_6          PHOTO    5
   9 2NDQ          STO   3  IENA(2)  
   9 2NYB          STO   5  IENM(2)  
   7 2PD2    RLN1  EXIT  6  0
   4 2PXL    #   
   4 2QC=    #   
  19 2QWW    RLIF  LDX   5  IEIM(2)             [RELOCATE INSTRUCTION BLOCK  
   9 2R6N          LDX   3  IEIA(2)  
   9 2RBG          TESTMOVE 5,RLI1   
  10 2RW6          MHUNTW   3,FILE,FRB   
   7 2S*Q          PHOTO    5
   9 2STB          STO   3  IEIA(2)  
   9 2T*2          STO   5  IEIM(2)  
   7 2TSL    RLI1  EXIT  6  0
   4 2W#=    #   
   4 2WRW    #   
  10 2X?G    #     CHARACTER MOVE ROUTINE
   4 2XR6    #   
   8 2Y=Q    MVCH  BZE   3  MVC1 
   8 2YQB          MVCH  6  0(3) 
   7 2_=2          SBN   3  1
   7 2_PL          SRL   3  9
   7 329=          SLL   3  9
   8 32NW          BRN      MVCH 
   7 338G    MVC1  EXIT  0  0
   4 33N6    #   
  19 347Q    #     SIZE CHECKS ON THE SIZE OF THE FAPB (AS ROUTINE IN EDITPT)
   4 34MB    #   
   8 3572    SIZE  SBX   0  FX1  
  15 35LL          STO   0  AWORK1(2)           [SAVE LINK   
   8 366=          CALL  6  RLNF 
  16 36KW          LDX   3  IENA(2)             [ADDRESS OF FAPB 
  16 375G          LDX   5  ALOGLEN(3)          [LENGTH OF BLOCK 
   7 37K6          LDX   6  4
   8 384Q          ADN   6  20   
  16 38JB          SRL   6  2                   [LENGTH REQUIRED 
  18 3942          BXGE  5  6,SIZE1             [J IF BLOCK BIG ENOUGH   
  15 39HL    SIZE0 ALTLEN   3,6                 [ADJUST SIZE 
  15 3=3=          MHUNTW   3,FILE,FAPB          [FIND BLOCK 
  16 3=GW          STO   3  IENA(2)             [STORE ADDRESS   
  16 3?2G          PHOTO    5                   [GET NEW PHOTO   
  15 3?G6          STO   5  IENM(2)             [AND STORE   
   8 3?PY          CALL  6  RLIF 
   8 3?_Q          CALL  6  RLOF 
  14 3#FB    SIZEX LDX   0  AWORK1(2)           [GET LINK
   8 3#_2          ADX   0  FX1  
   7 3*DL          EXIT  0  0
   7 3*Y=    SIZE1 SLL   6  1
  18 3BCW          BXGE  6  5,SIZEX             [J IF FAPB NOT MASSIVE   
   7 3BXG          SRL   6  1
   8 3CC6          SBN   5  33   
  17 3CWQ          BNG   5  SIZEX               [NOT WORTH ALTERING  
   7 3DBB          LDX   0  6
  18 3DW2          SBN   0  32                  [ELSE ADJUST LENGTH TO   
  18 3F*L          BPZ   0  SIZE0               [THE GREATER OF 32 AND   
  17 3FT=          LDN   6  32                  [THE REQUIRED LENGTH 
   8 3G#W          BRN      SIZE0
   4 3GSG    #   
  19 3H#6    #     SFST IS THE ROUTINE WHICH SEARCHES FORWARD FOR A CHARACTER
  13 3HRQ    #     STRING. IT SHOULD BE CALLED WITH  
  10 3J?B    #     X4 = LENGTH OF STRING 
  15 3JR2    #     X3 = CHARACTER NO WHERE SEARCH IS TO START
  13 3K=L    #     X1 = PTR TO STRING IN EDITING FILE
   4 3KQ=    #   
  15 3L9W    #     EXIT     CALL+2  FOUND :- CHAR NO IN X7   
  15 3LPG    #     EXIT     CALL+1  NOT FOUND :- X7 = IEOH   
   4 3M96    #   
   9 3MNQ    SFST  STO   6  ACOM6(2) 
   9 3N8B          LDX   5  ACOM7(2) 
   7 3NN2          SBX   5  3
   7 3P7L          SBX   5  4
  18 3PM=          BNG   5  SFT3                [J IF SEARCH IMPOSSIBLE  
  15 3Q6W          ADN   5  1                   [NO OF PERMS 
  18 3QLG          STO   4  AWORK2(2)           [NO OF CHARS IN STRING   
   7 3R66          SRC   3  2
   9 3RKQ          ADX   1  IEIA(2)  
  17 3S5B          ADX   3  IEOA(2)             [WORK OUT MODIFIERS  
   9 3T4L          ADX   3  IEOG(2)  
   9 3WHG          STO   1  AWORK3(2)
   9 3X36          STO   3  AWORK4(2)
   6 3X3S ...#UNS  EDCHAR
   4 3X4G ...(   
   9 3X58 ...      LDX   4  AWORK1(2)
  16 3X5W ...      BNG   4  SFT2                [J IF FOR A,B,R  
   8 3X6J ...      LDCT  4  #040 
   9 3X7= ...      SMO      AWORK1(2)
  15 3X7Y ...      ANDX  4  4(2)                [TEST FOR .S 
  14 3X8L ...      BZE   4  SFT2                [J IF NOT
   4 3X9# ...SFT5
  17 3X=2 ...      LDX   3  AWORK4(2)           [CURRENT POSN IN LINE
   8 3X=N ...      LDX   1  FX1  
   4 3X?B ...SFT6
   8 3X#4 ...      LDCH  6  0(3) 
  15 3X#Q ...      BXU   6  SPACE(1),SFT4       [G IF .S CHAR
   7 3X*D ...      BCHX  3  /
   9 3XB6 ...      STO   3  AWORK4(2)
  17 3XBS ...      BCT   5  SFT6                [LOOP ON LINE LENGTH 
  15 3XCG ...      BRN      SFT3                [NO CAN FIND 
   4 3XD8 ...)   
   6 3XDW ...#UNS  EDCHAR
   4 3XFJ ...#SKI
   8 3XGQ          BRN      SFT2 
   9 3Y2B    SFT1  LDX   3  AWORK4(2)
   9 3YG2          LDX   1  AWORK3(2)
   7 3Y_L          BCHX  3  /
   9 3_F=          STO   3  AWORK4(2)
   9 3_YW    SFT2  LDX   4  AWORK2(2)
   8 42DG          CALL  0  QFIT 
  14 42Y6          BZE   4  SFT4                [J IF FIT
  15 43CQ          BCT   5  SFT1                [TRY AGAIN   
  19 43XB    SFT3  LDX   7  ACOM7(2)            [SET X7 TO END OF SEARCH AREA
   9 44C2          LDX   6  ACOM6(2) 
  16 44WL          EXIT  6  0                   [EXIT NOT FOUND  
   9 45B=    SFT4  LDX   7  AWORK4(2)
  17 45TW          SBX   7  IEOA(2)             [SET X7 TO POINT TO  
  17 46T6          SBX   7  IEOG(2)             [FIRST CHARACTER IN  
  15 48#2          SLC   7  2                   [THE STRING  
   9 48RL          LDX   6  ACOM6(2) 
  15 49?=          EXIT  6  1                   [EXIT FOUND  
   4 49QW    #   
  17 4==G    #     TSFS IS THE ROUTINE WHICH SEARCHES BACKWARDS FOR A
   9 4=Q6    #     CHARACTER STRING  
  10 4?9Q    #     PARAMETERS AS FOR SFST
   4 4?PB    #   
   9 4#92    TSFS  STO   6  ACOM6(2) 
   7 4#NL          LDX   5  3
   7 4*8=          SBX   5  4
  18 4*MW          BNG   5  TSF3                [J IF SEARCH IMPOSSIBLE  
  16 4B7G          ADN   5  1                    [NO. OF PERMS   
  18 4BM6          STO   4  AWORK2(2)           [NO OF CHARS IN STRING   
   7 4C6Q          SBX   3  4
   7 4CLB          SRC   3  2
  17 4D62          ADX   1  IEIA(2)             [WORK OUT MODIFIERS  
   9 4DKL          ADX   3  IEOA(2)  
   9 4FJW          ADX   3  IEOG(2)  
   9 4H3Q          STO   1  AWORK3(2)
   9 4HHB          STO   3  AWORK4(2)
   6 4HJ3 ...#UNS  EDCHAR
   4 4HJN ...(   
   8 4HK* ...      LDCT  4  #040 
   9 4HL2 ...      SMO      AWORK1(2)
  15 4HLM ...      ANDX  4  4(2)                [TEST FOR .S 
  14 4HM# ...      BZE   4  TSF2                [J IF NOT
   4 4HM_ ...TSF5
  21 4HNL ...      STOZ     AWORK2(2)           [SAVES STEPPING X3 WHEN CHAR FOUND   
  17 4HP? ...      LDX   3  AWORK4(2)           [CURRENT POSN IN LINE
   8 4HPY ...      LDX   1  FX1  
   4 4HQK ...TSF6
   8 4HR= ...      LDCH  6  0(3) 
  15 4HRX ...      BXU   6  SPACE(1),TSF4       [G IF .S CHAR
   7 4HSJ ...      SLC   3  2
   7 4HT9 ...      SBN   3  1
  15 4HTW ...      SRC   3  2                   [BACK  BCHX  
   9 4HWH ...      STO   3  AWORK4(2)
  17 4HX8 ...      BCT   5  TSF6                [LOOP ON LINE LENGTH 
  15 4HXT ...      BRN      TSF3                [NO CAN FIND 
   4 4HYG ...)   
   6 4H_7 ...#UNS  EDCHAR
   4 4H_S ...#SKI
   8 4J32          BRN      TSF2 
   9 4JGL    TSF1  LDX   3  AWORK4(2)
   7 4K2=          SLC   3  2
   7 4KFW          SBN   3  1
  16 4K_G          SRC   3  2                   [BACKWARDS BCHX  
   9 4LF6          LDX   1  AWORK3(2)
   9 4LYQ          STO   3  AWORK4(2)
   9 4MDB    TSF2  LDX   4  AWORK2(2)
  14 4MY2          CALL  0  QFIT                [CHECK   
  14 4NCL          BZE   4  TSF4                [J IF FIT
  15 4NX=          BCT   5  TSF1                [TRY AGAIN   
  19 4PBW    TSF3  LDN   7  0                   [SET X7 TO START OF RECORD   
   9 4PWG          LDX   6  ACOM6(2) 
  16 4QB6          EXIT  6  0                   [EXIT NOT FOUND  
   9 4R*B    TSF4  SBX   3  IEOG(2)  
  17 4SS=          SBX   3  IEOA(2)             [SET X7 TO POINT TO  
  19 4T?W          SLC   3  2                   [FIRST CHARACTER OF STRING   
   9 4TRG          SBX   3  AWORK2(2)
   7 4W?6          LDX   7  3
   9 4WQQ          LDX   6  ACOM6(2) 
  15 4X=B          EXIT  6  1                   [EXIT FOUND  
   4 4XQ2    #   
  17 4Y9L    #     K1EDITEX IS THE ROUTINE WHICH CHECKS THE CURRENT  
   9 4YP=    #     RECORD BY CONTEXT 
   4 4_8W    #   
   5 4_NG    N1EDITEX
   9 5286          STO   6  AWORK3(2)
  16 52MQ          CALL  6  RLIF                [RELOCATE BLOCKS 
   8 537B          CALL  6  RLOF 
  18 53M2 ...      LDX   7  IEOC(2)             [SET START EQUAL TO IEOC 
  19 546L ...      LDX   6  IEOH(2)                   [SET STOP EQUAL TO IEOH
   9 54L= ...      STO   6  AWORK2(2)
  17 555W ...      STO   7  AWORK1(2)            [SEE IF START = 0   
  14 5#D=          BZE   7  SBC12               [J IF SO 
  16 5#XW    SBC11 UP                           [ELSE NOT FOUND  
   9 5*CG    SBC12 SMO      AWORK3(2)
  16 5*X6          LDX   7  1(2)                [GET INDICATOR   
   7 5BBQ          LDX   5  7
  16 5BWB          ANDX  5  BSB18               [GET CHAR COUNT  
   7 5CB2          LDX   4  5
   7 5CTL          NGX   5  5
  18 5D*=          ADX   5  AWORK2(2)           [CHECK IF ROOM FOR SEARCH
   8 5DHX ...      LDCT  6  #100 
   7 5DL6 ...      ANDX  6  7
  17 5DN* ...      BNZ   6  NAYG                [J IF 'G' INSTRUCTION
  14 5DSW          BNG   5  SBC11               [J IF NOT
  18 5F#G ...NAYG  ADN   5  1                   [NO OF PERMS FOR CONTAINS
   7 5FS6          SLC   7  1
  19 5G?Q          BNG   7  SBC30               [J TO IGNORE LEADING SPACES  
   7 5GRB          SRC   7  1
  16 5H?2          BNG   7  SBC20               [J FOR CONTAINS  
   9 5HQL          SMO      AWORK3(2)
   8 5J==          LDX   1  2(2) 
  16 5JPW          LDX   3  IEOA(2)             [SET UP POINTERS 
   9 5K9G          ADX   1  IEIA(2)  
   9 5MML          ADX   3  IEOG(2)  
   7 5N7=          SLC   7  2
   8 5NLW          BPZ   7  NOTG 
   9 5P6G          LDN   3  IESD(2)  
   4 5PL6    NOTG
  14 5QKB          CALL  0  QFIT                [DO CHECK
  15 5R52          BNZ   4  SBC11               [NOT FOUND   
  14 5RJL    SBC13 UPPLUS 1                     [FOUND   
   9 5S4=    SBC20 SMO      AWORK3(2)
   8 5SHW          LDX   1  2(2) 
  15 5T3G          LDX   3  IEOA(2)             [GET POINTERS
   9 5TH6          ADX   1  IEIA(2)  
   9 5WGB          ADX   3  IEOG(2)  
  17 5X_=          STO   1  ACOM7(2)            [STORE TEMPORARILY   
   9 5YDW          STO   3  ACOM8(2) 
   9 5YYG          STO   4  AWORK4(2)
  15 5_D6          BRN      SBC22               [J TO COMPARE
   9 5_XQ    SBC21 LDX   3  ACOM8(2) 
   9 62CB          LDX   1  ACOM7(2) 
  18 62X2          BCHX  3  /                   [STEP PTR FOR NEXT SEARCH
   9 63BL          STO   3  ACOM8(2) 
   9 63W=    SBC22 LDX   4  AWORK4(2)
  14 64*W          CALL  0  QFIT                [COMPARE 
  15 64TG          BZE   4  SBC13               [J IF FOUND  
  15 65*6          BCT   5  SBC21               [TRY AGAIN   
  15 65SQ          BRN      SBC11               [NOT FOUND   
  16 66#B    SBC30 BZE   4  SBC13               [J IF NULL STRING
   9 66S2          SMO      AWORK3(2)
   8 67?L          LDX   1  2(2) 
   9 67R=          LDX   3  IEOA(2)  
   9 68=W          ADX   1  IEIA(2)  
   9 68QG          LDX   5  AWORK2(2)
   9 69PQ          ADX   3  IEOG(2)  
   7 6=9B ...      STOZ     6
  15 6?8L    SBC31 LDCH  7  0(3)                [GET O/F CHAR
   8 6?N=          SMO      FX1  
  16 6#7W          BXU   7  SPACE,SBC32         [J IF NOT A SPACE
  15 6#MG          BCHX  3  /                   [UPDATE PTR  
  20 6#X# ...      ADN   6  1                   [INCREMENT NO. OF LEADING SPACES 
  17 6*76          BCT   5  SBC31               [REDUCE NO OF PERMS  
  19 6*LQ          BRN      SBC11               [NOT FOUND IF O/F ALL SPACES 
   9 6*Q9 ...SBC32 SMO      AWORK3(2)
   8 6*TN ...      LDX   5  1(2) 
  19 6*_7 ...      ANDX  5  BSB18               [GET NO. OF CHARS. IN TERM.  
   7 6B4L ...      NGX   5  5
  18 6B85 ...      ADX  5  AWORK2(2)           [CHECK IF ROOM FOR SEARCH 
  18 6B?J ...      SBX   5  6                   [IGNORING LEADING SPACES 
  14 6BC3 ...      BNG   5  SBC11               [J IF NOT
  19 6BGG ...      STO   1  ACOM7(2)            [POINTS TO <STRING> IN TERM. 
   9 6BL2          STO   3  ACOM8(2) 
   9 6C5L          STO   4  AWORK4(2)
   7 6CK=          LDN   5  1
   8 6D4W          BRN      SBC22
   4 6DJG    #   
  20 6F46    #     K2EDITEX IS THE ROUTINE WHICH EVALUATES CHARACTER BY CONTEXT  
   4 6FHQ    #   
   5 6G3B    N2EDITEX
  16 6GH2          STO   6  AWORK1(2)           [SAVE DATA PTR   
   8 6H2L          CALL  6  RLOF 
   8 6HG=          CALL  6  RLIF 
   9 6H_W          SMO      AWORK1(2)
  16 6JFG          LDX   4  4(2)                [GET INDICATOR   
   6 6JJL ...#UNS  EDCHAR
   8 6JMQ ...      LDCT  6  #100 
   6 6JQW ...#UNS  EDCHAR
   4 6JW2 ...#SKI
   8 6J_6          LDCT  6  #177 
   7 6KDQ          ANDX  6  4
  14 6KYB          BZE   6  PT1                 [J NOT .E
   9 6LD2          LDX   7  IEOH(2)  
   8 6LXL          BRN      PT4  
   9 6MC=    PT1   LDX   3  IEOC(2)  
   9 6MWW    PT3   SMO      AWORK1(2)
  17 6NBG          LDX   1  4(2)                [GET PTR TO STRING   
   9 6NW6          SMO      AWORK1(2)
   8 6P*Q          LDX   4  3(2) 
  16 6PTB          BNG   4  PT5                 [J IF BACKWARDS  
   9 6Q*2          LDX   7  IEOH(2)  
   9 6QSL          STO   7  ACOM7(2) 
   8 6R#=          CALL  6  SFST 
  16 6RRW          BRN      PT6                 [J IF NOT FOUND  
   9 6S?G    PT4   SMO      AWORK1(2)
  16 6SR6          STO   7  3(2)                [STORE CHAR NO IN
  18 6T=Q          SMO      AWORK1(2)           [APPROPRIATE DATA AREA   
   8 6TQB          STOZ     4(2) 
   5 6W=2    PTUP  UP
   7 6WPL    PT5   NGX   4  4
   8 6X9=          CALL  6  TSFS 
  16 6XNW          BRN      PT6                 [J IF NOT FOUND  
  15 6Y8G          BRN      PT4                 [J IF FOUND  
  16 6YN6    PT6   LDN   5  IEWT                [J IF CONDITIONAL
  16 6_7Q          BXU   5  AWORK1(2),PT4       [REPETITION CHECK
  17 6_MB          LDXC  5  IESY(2)             [J IF A TERMINATOR   
  17 7272          BCS      PT4                 [STOPS ON THIS RECORD
  18 72LL          LDX   6  7                   [SAVE DESTINATION CHAR   
  18 736=          EDCOM    CHNF                [^CHARACTER NOT FOUND^   
   7 73KW          LDX   7  6
   8 745G          BRN      PT4  
   4 74K6    #   
   4 754Q    #   
   8 75JB    ZABR  SBX   0  FX1  
   9 7642          STO   0  ACOM6(2) 
   7 76HL          LDX   3  7
  19 773=          SBX   3  IEOC(2)             [NO OF CHARS TO TRANSCRIBE   
  15 77GW          BNG   3  ZABEX               [J IF NONE   
   9 782G          LDX   5  IENH(2)  
   9 78G6          SBX   5  IENC(2)  
  16 78_Q          STO   5  AWORK4(2)           [ROOM LEFT IN N/F
   8 79FB          BZE   5  ZAB0 
  11 79_2          BXL   3  AWORK4(2),ZABR1  
  16 7=DL          BXE   3  AWORK4(2),ZABR1     [J IF ENOUGH ROOM
  17 7=Y=    ZAB0  LDN   0  #10                  [ELSE NOTE WE HAVE  
  17 7?CW          ORS   0  IESW(2)             [TRUNCATED THE RECORD
  17 7?XG          LDX   3  AWORK4(2)           [AND ADJUST THE SIZE 
  16 7#C6    ZABR1 SBS   3  AWORK4(2)           [UPDATE ROOM LEFT
   9 7#WQ          STO   3  AWORK2(2)
  17 7*BB          LDX   4  IENC(2)             [WORK OUT WHAT IENC  
  15 7*W2          ADX   4  3                   [WOULD BECOME
   9 7B*L          ADX   4  IEWT+4(2)
  17 7BT=          CALL  0  SIZE                [CHECK SIZE OF FAPB  
   9 7C#W          LDX   6  IEOC(2)  
   9 7CSG          LDX   3  AWORK2(2)
   9 7D#6          LDX   7  IENC(2)  
  19 7DRQ          ADS   3  IEOC(2)             [UPDATE CHARACTER POINTERS   
   9 7F?B          ADS   3  IENC(2)  
   7 7FR2          SRC   6  2
   7 7G=L          SRC   7  2
  15 7H9W          BNZ   7  ZABR2               [NOT REQUIRED
   9 7NGX ...      LDX   0  IEOG(2)  
   8 7NXN ...      SBN   0  A1   
  19 7P#F ...      SLL   0  2                   [NO. OF CHAR IN RECORD HDDR  
   3 7PP= ...
  17 7Q63 ...      LDX   4  IEOA(2)             [OLD FILE BLOCK ADDR 
  17 7QGS ...      LDX   5  IENA(2)             [NEW FILE BLOCK ADDR 
  16 7QXK ...      ADN   4  A1                  [SRT OF RED TAPE 
  16 7R#B ...      ADN   5  A1                  [SRT OF RED TAPE 
  17 7RP7 ...      SMO      0                   [LENGTH OF RED TAPE  
  16 7S5Y ...      MVCH  4  0                   [AND COPY OVER   
   9 7SGQ    ZABR2 ADX   6  IEOG(2)  
   9 7T2B          ADX   7  IENG(2)  
  18 7T_L          ADX   6  IEOA(2)             [GET ABSOLUTE ADDRESSES  
   9 7WF=          ADX   7  IENA(2)  
  16 7WYW          CALL  0  MVCH                [MOVE THE DATA   
  17 7XDG          LDX   3  IEWT+4(2)           [LENGTH OF 2ND STRING
  16 7XY6          LDX   6  IEWT+3(2)           [POINTER TO IT   
  16 7YCQ          BXL   3  AWORK4(2),ZABR3     [J IF SUFFICIENT 
  13 7YXB          BXE   3  AWORK4(2),ZABR3     [ROOM
  17 7_C2          LDN   0  #10                  [ELSE NOTE WE HAVE  
  17 7_WL          ORS   0  IESW(2)             [TRUNCATED THE RECORD
  15 82B=          LDX   3  AWORK4(2)           [AND ADJUST  
   9 82TW    ZABR3 ADX   6  IEIA(2)  
  17 83*G          ADS   3  IENC(2)             [UPDATE N/F POINTER  
  17 83T6          CALL  0  MVCH                [MOVE IN 2ND STRING  
   9 84#Q    ZABEX LDX   0  ACOM6(2) 
   8 84SB          ADX   0  FX1  
   7 85#2          EXIT  0  0
   4 85RL    #   
   4 86?=    #   
  15 86QW    SETUP STO   0  ACOM6(2)            [SAVE LINK   
  16 87=G          CALL  6  RLOF                [RELOCATE BLOCKS 
   8 87Q6          CALL  6  RLIF 
   9 889Q          LDX   7  IEOH(2)  
   9 88PB          LDXC  6  IESY(2)  
  17 8992          BCC      SET1                [J IF NO REPETITION  
   9 89NL          LDX   7  IECC(2)  
  18 8=8=    SET1  STO   7  ACOM7(2)            [SET ENDPOINT FOR SEARCH 
  18 8=MW          LDX   3  IEOC(2)             [START POINT FOR SEARCH  
  17 8?7G          LDX   1  IEWT+1(2)             [PTR TO KEY STRING 
  16 8?M6          LDX   4  IEWT+2(2)           [LENGTH OF STRING
   6 8?PP ...#UNS  EDCHAR
   4 8?S# ...(   
   8 8?WX ...      LDCT  0  #400 
  17 8?_G ...      STO   0  AWORK1(2)           [SET -VE FOR A,B,R   
   4 8#45 ...)   
  14 8#6Q          LDX   0  ACOM6(2)            [GET LINK
   7 8#LB          EXIT  0  0
   4 8*62    #   
   4 8*KL    #   
  16 8B5=    N4EDITEX                           [A INSTRUCTION   
  18 8BJW          CALL  0  SETUP               [SET PARAMS FOR SEARCH   
  17 8C4G          CALL  6  SFST                [SEARCH FOR STRING   
  16 8CJ6          BRN      NFD                 [J IF NOT FOUND  
  18 8D3Q          ADX   7  IEWT+2(2)           [INCLUDE LENGTH OF STRING
  14 8DHB          CALL  0  ZABR                [DO WORK 
   8 8F32    ZA    LDN   0  #10  
  18 8FGL          ANDX  0  IESW(2)             [SEE IF RECORD TRUNCATED 
  14 8G2=          BZE   0  ZAEX                [J IF NOT
  15 8GFW          ERS   0  IESW(2)             [CLEAR SWITCH
   8 8G_G          EDCOM    YRTL 
   9 8HF6          ACROSS   EDITOR,12
   9 8HYQ    ZAEX  ACROSS   EDITOR,3 
   4 8JDB    #   
   4 8JY2    #   
  16 8KCL    N5EDITEX                           [B INSTRUCTION   
  18 8KX=          CALL  0  SETUP               [SET PARAMS FOR SEARCH   
  17 8LBW          CALL  6  SFST                [SEARCH FOR STRING   
  16 8LWG          BRN      NFD                 [J IF NOT FOUND  
  15 8MB6          CALL  0  ZABR                [DO THE WORK 
   8 8MTQ          BRN      ZA   
   4 8N*B    #   
   4 8NT2    #   
  16 8P#L    N7EDITEX                           [R INSTRUCTION   
  18 8PS=          CALL  0  SETUP               [SET PARAMS FOR SEARCH   
  17 8Q?W          CALL  6  SFST                [SEARCH FOR STRING   
  16 8QRG          BRN      NFD                 [J IF NOT FOUND  
  15 8R?6          CALL  0  ZABR                [DO THE WORK 
  17 8RQQ          LDX   7  IEWT+2(2)           [SKIP PAST UNWANTED  
  14 8S=B          ADS   7  IEOC(2)             [STRING  
   8 8SQ2          BRN      ZA   
   4 8T9L    #   
   4 8TP=    #   
  17 8W8W    NFD   STOZ     IEWT+4(2)           [NULLIFY 2ND STRING  
  17 8WNG          CALL  0  ZABR                [COPY REQUIRED CHARS 
  16 8X86          LDXC  6  IESP(2)             [ANY REPETITION  
  14 8XMQ          BCC      NFD2                [J IF NOT
  19 8Y7B          LDXC  7  IESY(2)             [DOES TERMINATOR STOP ON THIS
  16 8YM2          BCC      NFD2                [RECORD, J IF NOT
  17 8_6L          BZE   7  NFD1                [J IF STACK NOW CLEAR
   8 8_L=          LDCT  6  #400 
  17 925W          ORX   7  6                   [KEEP B0 OF IESP SET 
  16 92KG    NFD1  STO   7  IESP(2)             [RESET STACK TO  
  16 9356          SMO   7                      [PULLED UP VALUE 
  10 93JQ          LDX   6  IESTACK+1(2) 
  17 944B          STO   6  IEIP(2)             [SKIP PAST TERMINATOR
   9 94J2          STOZ     IESY(2)  
   8 953L          BRN      ZA   
  18 95H=    NFD2  EDCOM    CHNF                [^CHARACTER NOT FOUND^   
   8 962W          BRN      ZA   
   9 96GG     MENDAREA 100,K100EDITEX
   4 9726    #END
   8 ____ ...30054047001500000000
