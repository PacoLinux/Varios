   4 22FL    [   
   9 22_=    [THIS VERSION INCLUDES: 
   7 23DW    [1.   OPENSYS   
   9 23YG    [2.   BREAKIN ON OPEN   
  10 24D6    [3.   LESS DUMPER FREEZING  
   8 24QH ...[4.   TWIN SYSTEMS  
  17 24W2 ...      SEG      NEWDIR,2,EDWARD  MOON ,FILESTORE,G3UGSI  
   4 24_F ...[   
  16 254Y ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LIMITED 1982
  14 258C ...[     THIS EXCLUDES CODE UNDER #SKI G3UGSI  
   4 25?W ...[   
  11 25CB          SEGENTRY K1NEWDIR,Z1NEWDIR
  11 25X2          SEGENTRY K2NEWDIR,Z2NEWDIR
  11 26BL          SEGENTRY K3NEWDIR,Z3NEWDIR
  11 26W=          SEGENTRY K4NEWDIR,Z4NEWDIR
  11 27*W          SEGENTRY K5NEWDIR,Z5NEWDIR
   4 27TG    [   
  14 282N ...      FSHENTRY K7NEWDIR,Q7NEWDIR,,Q7NEWDIR  
   4 287W ...[   
   7 288L ...#OPT  G3UGSI=0  
   6 289B ...#SKI  G3UGSI
   4 28=6 ...(   
  16 28=W ...# MACRO TO PREVENT M551 AND SECURITY INTERFACE CLASH
   7 28?L ...      SINM551   
   4 28#B ...)   
  21 28*6    # THIS ROUTINE HAS ENTRIES FOR THE MAKEDIR COMMAND AND THE TEMPDIR MACRO
  21 28SQ    #     FOR MAKEDIR IT SETS UP AND APPENDS A SUITABLE DICTIONARY ENTRY AND
  15 29#B    #     IF NECESSARY MAKES UP A PSEUDO USERNAME.  
  21 29S2    #     FOR TEMPDTR THE TEMPORARY DIRECTORY IS SET UP WITH A PSEUDO USERNA
  10 2=?L    XERNOTO        +ERNOTOWNER  
   9 2=R=    XERNAME        +ERNAME  
   9 2?=W    XERMAKAL       +ERMAKAL 
   9 2?QG    XERMAKPR       +ERMAKPR 
   9 2#=6    XERMAKPS       +ERMAKPS 
   9 2#PQ    XERMAK         +ERMAK   
   8 2*9B    SLAN           4HC1 
   8 2*P2    NOSA           #41  
   9 2B8L    NEWUSER     8HNEWUSER   
   4 2BN=    [   
   5 2C7W    Z1NEWDIR
  10 2CMG    # ENTRY FROM NEWDIR MACRO   
   7 2D76          LDN   4  4
   9 2DLQ          BRN      NOTPSEUDO
   4 2F6B    [   
   5 2FL2    Z2NEWDIR
  15 2G5L    # ENTRY WITH ONE PARAMETER FROM MAKEDIR,PSEUDO  
   7 2GK=          LDN   4  1
   8 2H4W          BRN      PSEUD
   4 2HJG    [   
   5 2J46    Z3NEWDIR
  16 2JHQ    # ENTRY WITH TWO PARAMETERS FROM MAKEDIR,NOT PSEUDO 
   7 2K3B          LDN   4  2
   6 2KH2    NOTPSEUDO   
   9 2L2L ...      LDN   5  FLOG-1   
   8 2LG=          BRN      NPSEU
   4 2L_W    [   
   5 2MFG    Z4NEWDIR
  16 2M_6    # ENTRY WITH THREE PARAMETERS FROM MAKEDIR,PSEUDO   
   7 2NDQ          LDN   4  3
   8 2NYB          BRN      PSNAM
   4 2PD2    [   
   5 2PXL    Z5NEWDIR
  10 2QC=    # ENTRY FROM  TEMPDIRA MACRO
   7 2QWW          LDN   4  0
  11 2RBG          SETNCORE 10,1,FILE,FABSNB 
   8 2RW6          LDN   0  10   
  16 2S*Q          STO   0  A1(1)               [RECORD HEADER   
  18 2STB          FJOCA    1,2                 [GET USERNAME & JOBNAME  
  17 2T*2          ADN   1  JUSER               [MOVE THEM INTO ABSNB
  18 2TSL          MHUNTW   2,FILE,FABSNB       [JUST SET UP SO W IS OK  
   8 2W#=          ADN   2  A1+1 
   7 2WRW          MOVE  1  3
  10 2X?G          SBN   1  JUSER-JNAME  
   7 2XR6          ADN   2  3
   7 2Y=Q          MOVE  1  3
  21 2YQB          LDN   0  1                   [SET UP REEL AND GENERATION NUMBERS  
   8 2_=2          STO   0  3(2) 
   8 2_PL          STO   0  4(2) 
  17 329=          SMO      FX1                 [SET  UP  LANG  WORD 
   8 32NW          LDX   0  SLAN 
   8 338G          STO   0  5(2) 
  21 33N6    PSEUD CALL  5  NXUZA               [GET A MADE UP USERNAME IN A CREATE B
  21 347Q    PSNAM LDN   5  FPSEU               [LENGTH OF PSEUDO DICTIONARY ENTRY   
   5 34MB    NPSEU   
  11 3572          SETNCORE 3,3,FILE,ADICT   
  19 35LL          BZE   4  SETFL               [NO DICT ENTRY FOR TEMPDIRA  
  21 366=          SETUPCOR 5,3,FILE,FWB        [INSERT BLOCK FOR DICTIONARY ENTRY   
  16 36KW          STO   5  A1(3)               [RECORD HEADER   
   9 375G          STOZ     A1+1(3)  
   9 37K6          LDN   1  A1+1(3)  
   9 384Q          LDN   2  A1+2(3)  
   8 38JB          SMO      A1(3)
  16 3942          MOVE  1  510                 [ZEROISE BLOCK   
   7 39HL          LDN   0  1
  18 3=3=          STO   0  CNEWZ(3)            [SET UP NEW FORMAT WORD  
   8 3=GW          ADN   3  CUSER
  10 3?2G          MHUNTW   2,FILE,CREATE
  21 3?G6          ADN   2  CEUSE1              [LOCATE USERNAME IN THE CREATE BLOCK 
  17 3?_Q          MOVE  2  3                   [MOVE IT INTO CUSER  
  10 3#FB          HUNT     2,FILE,FABSNB
   8 3#_2          SMO      A1(2)
  21 3*DL          ADN   2  A1-6                [LOCATE LOCALNAME IN THE FABSNB AND  
  17 3*Y=          ADN   3  CLOCN-CUSER         [MOVE IT INTO CLOCN  
   7 3BCW          MOVE  2  3
  14 3BXG          LDX   0  GSIGN               [NEGATIVE
  19 3CC6          ORS   0  CPSEU-CLOCN(3)      [MARKER FOR PSEUDO DIRECTORY 
   7 3CWQ          LDEX  0  4
   7 3DBB          ANDN  0  1
  15 3DW2          BNZ   0  SETFL               [J IF PSEUDO 
  18 3F*L          STOZ     CPSEU-CLOCN(3)      [ZEROISE PSEUDO MARKER   
   8 3FT=          LDX   0  ACES 
  10 3G#W          ADN   3  CPASS-CLOCN  
  16 3GSG          STO   0  0(3)                [PUT SPACES IN   
  15 3H#6          STO   0  1(3)                [THE PASSWORD
   8 3HRQ          STO   0  2(3) 
   5 3J?B    SETFL   
  11 3JR2          SETNCORE 6,3,FILE,FLOCNB  
  10 3K=L          MHUNT    2,FILE,FABSNB
   8 3KQ=          ADN   3  A1   
   8 3L9W          SMO      A1(2)
  21 3LPG          ADN   2  A1-6                [LOCATE LAST NAME IN THE FABSNB AND  
  18 3M96          MOVE  2  6                   [MOVE IT INTO THE FLOCNB 
  16 3MNQ          MHUNT    2,FILE,FABSNB       [RECOVER POINTER 
   7 3N8B          LDN   0  6
  21 3NN2          SBS   0  A1(2)               [REDUCE LENGTH FOR OPENING THE DIRECT
   5 3P7L    WTLOOP  
  21 3PM=          OPEN     TBR,GENERAL,QUERY,LEAVE     [OPEN NEW DIRECTORY'S DIRECTO
  19 3Q6W          TESTRPN2 OK,NAME             [ERROR IN HIGHER DIRECTORY   
  16 3Q?# ...      BZE   4  NOCHEKA             [TEMPDIRA MACRO  
   6 3QCQ ...#UNS FTS1   
   4 3QJ8 ...(   
   7 3QNL ...      TOPFCB   2
   9 3QT4 ...      JFREZFCB 2,RFROZEN
   4 3Q_G ...)   
   7 3R66          LDX   0  4
   7 3RKQ          SBN   0  4
  21 3S5B          BZE   0  NOCHEK              [DONT CHECK OWNERSHIP IF NEWDIR MACRO
   7 3SK2          OWNERCHE  
   6 3SN6 ...#UNS FTS1   
   4 3SR= ...(   
  15 3SWB ...      TESTREP2 NOINFACC,RFROZEN,NOALLACC,RFROZEN
   4 3S_G ...)   
   9 3T4L          TESTREP2 OK,NOCHEK
  13 3TJ=          CLOSE                        [DIR 
  10 3W3W          LDX   3  XERNOTO(1)   
  20 3WHG          BRN      NOTBRK              [CLEAR UP BLOCKS & REPORT ERROR  
   5 3X36    NOCHEKA 
  21 3XGQ          GETDIR   3                   [WE WANT TO WAIT FOR ERASING IF TEMPD
   9 3Y2B          BRN      NOCHEKX  
   5 3YG2    NOCHEK  
  20 3Y_L          GETDIR   1                   [TEST TO SEE IF NAME ALREADY USED
   5 3_F=    NOCHEKX 
  10 3_YW          MHUNTW   3,FILE,ENT   
  10 42DG          TESTRPN2 NOFILE,USED1 
  20 42Y6          JBC      NTMPD,3,BETEMP      [J UNLESS SUPERIOR IS TEMPORARY  
  10 43CQ          MHUNTW   1,FILE,CREATE
  21 43XB          BS       1,CETEMP            [IF SO MAKE THIS ONE TEMPORARY TOO   
   9 44C2          MFREEW   FILE,FWB 
   8 44WL          ORX   4  GSIGN
   5 45B=    NTMPD   
   9 45TW          LDN   2  EUSE1(3) 
  10 46*G          MHUNTW   3,FILE,ADICT 
   8 46T6          ADN   3  A1   
  17 47#Q          MOVE  2  3                   [MOVE IN SUPUSER NAME
  17 47SB          BZE   4  TEMPS               [J IF TEMPDIRA MACRO 
  16 48#2          BNG   4  TEMPS               [J. IF TEMPDIR   
  10 48RL          MHUNTW   3,FILE,FWB   
   9 49?=          ADN   3  CSUPUSER 
  16 49QW          MOVE  2  3                   [MOVE IN CSUPUSER
   8 4==G          SBN   2  EUSE1
   7 4=Q6          FREECORE 2
  13 4?9Q          OPENSYS  TBREAK,DICTIONARY,GENERAL
   7 4?PB          LDX   0  4
   7 4#92          SBN   0  2
  17 4#NL          BNZ   0  PSDUS               [J IF PSEUDO OR MACRO
  21 4*8=          READDICT                     [GET THE DICT ENTRY OF THE SUPERIOR U
  11 4*MW          MHUNTW   3,FILE,ADICTENT  
   9 4B7G          LDX   0  CPSEU(3) 
  21 4BM6          BNG   0  PSDIR               [J IF SUPUSER IS PSEUDO FOR NONPSEUDO
  21 4C6Q ...      CHEKPRIV 0,NEWUSER,NOPRIV    [J TO NOPRIV UNLESS USER HAS PRIVILEG
  11 4CLB          MHUNTW   2,FILE,ADICTENT  
   7 4D62          LDX   3  2
   8 4DKL          ADN   2  CUSER
   8 4F5=          ADN   3  A1   
  17 4FJW          MOVE  2  3                   [MOVE UP USER NAME   
   8 4G4G          SBN   3  A1   
   8 4GJ6          ALTLEND  3,3  
  11 4H3Q          MHUNTW   2,FILE,ADICTENT  
  10 4HHB          NAME     2,FILE,ADICT 
   8 4J32          BRN      PSDUS
   5 4JGL    TEMPS   
   8 4K2=          SBN   2  EUSE1
   7 4KFW          FREECORE 2
   5 4K_G    PSDUS   
  19 4LF6          CALL  5  SETADICT            [GET ADICT WITH NEW USERNAME 
  12 4LYQ          OPENSYS  ZBREAK,LEXICON,WRITE 
   4 4MDB    [   
  21 4MY2    [     CODE TO PREVENT MAKING NON-TEMP DIR DURING DUMP WHEN ONE OF SAME  
   7 4NCL    [NAME WAS ERASED
   4 4NX=    [   
  16 4PBW          JBC      NOTDUMPING,,BFERUS  [J IF NOT DUMPING
  16 4PWG          BNG   4  NOTDUMPING          [OR IF TEMPORARY 
  15 4QB6          BZE   4  NOTDUMPING          [OR TEMPDIRA 
   8 4QTQ          LDN   2  BMISC
  21 4R*B          HUNT2B   2,FI,FERUS          [FIND PROHIBITED NAME BLOCK IN MISC C
  10 4RT2          HUNTW    1,FILE,ADICT 
  17 4S#L          LDX   0  FERUSNUM(2)         [NO. OF NAMES IN LIST
  10 4SS=          BZE   0  NOTDUMPING   
  15 4T?W          LDX   5  A1(1)               [LOAD NAME   
  14 4TRG          LDX   6  A1+1(1)             [ BEING  
  15 4W?6          LDX   7  A1+2(1)             [  CREATED   
   5 4WQQ    NEXTUSER
  13 4X=B          TXU   5  FERUSER(2)          [JUMP
  14 4XQ2          TXU   6  FERUSER+1(2)        [ IF NAME
  15 4Y9L          BXE   7  FERUSER+2(2),RASE   [  PROHIBITED
  17 4YP=          ADN   2  FERUSLEN            [GET PTR TO NEXT NAME
  18 4_8W          BCT   0  NEXTUSER            [& TEST UNTIL NONE LEFT  
  17 4_NG          BRN      NOTDUMPING          [NAME OK SO PROCEED  
  17 5286    RASE                               [NAME IS PROHIBITED  
  14 52MQ          FREECORE 1                   [ADICT   
   7 537B          LDN   0  1
   7 53M2          SBX   0  4
  21 546L          BZE   0  USEDX               [GET ANEW NAME IF IT WAS A MADE-UP NA
  20 54L=          LONGSET  ADUMPWT,(GEOERR)    [PREPARE TO WAIT FOR END OF DUMP 
   7 555W          LDN   7  3
   6 55KG    NEXTCLOSE   
  18 5656          CLOSETOP                     [LEXICON,DICTIONARY & DIR
   9 56JQ          BCT   7  NEXTCLOSE
  20 574B          LONGSTOP TBR,,INCFREZ        [OUTPUT WAITING MESSAGE & WAIT   
   5 575Y ...WAITLAB 
  15 57J2          BRN      WTLOOP              [TRY AGAIN   
   4 583L    [   
   6 58H=    NOTDUMPING  
  21 592W          READLEX                      [THE 1ST ADICT CONTAINS THE NEW USERN
  10 59GG          TESTRPN2 NOUSER,USED  
  21 5=26          READLEX                      [THE 1ST ADICT BLOCK IS NOW SUPUSER'S
  11 5=FQ          MHUNTW   2,FILE,ASELFLEX  
   7 5=_B          LDX   0  4
   7 5?F2          SBN   0  2
  16 5?YL          BZE   0  REAL                [J IF NON PSEUDO 
   7 5#D=          SBN   0  2
  17 5#XW          BZE   0  REAL                [J IF NEWDIR MACRO   
   8 5*CG          LDCT  1  #600 
  15 5*X6          BNG   4  PSONLY              [J IF TD MACS
  16 5BBQ          BZE   4  PSONLY              [J IF TEMPDIRA   
   8 5BWB          LDCT  1  #400 
   5 5CB2    PSONLY  
  19 5CTL          ORS   1  GSUPUSER(2)         [SET PSEUDO &/OR LEMP BITS   
   4 5D*=    REAL
  10 5DSW          NAME  2,FILE,ASUPLEX  
  10 5F#G          MHUNTW   1,FILE,CREATE
   9 5FS6          ADN   2  GLUSER   
  17 5G?Q          ADN   1  CEUSE1              [MOVE IN NEW USERNAME
   7 5GRB          MOVE  1  3
  18 5H?2          NEWUSER                      [INSERT NEW LEXICON ENTRY
  14 5HQL          CLOSETOP                     [LEXICON 
  16 5J==          BZE   4  TDMAC               [J IF TEMPDIRA   
  20 5JPW          BNG   4  TDMAC               [J IF TEMP BECAUSE SUPERIOR IS   
  17 5JQR ...[     THIS SECTION IS ONLY COMPILED IF THE G3 USER GROUP
  16 5JRN ...[     SECURITY INTERFACE ENHANCEMENT IS SWITCHED ON 
  18 5JSK ...#SKI G3UGSI                        [G3UG SECURITY INTERFACE 
   4 5JTG ...(   
  10 5JWC ...      MHUNTW   3,FILE,FWB   
   9 5JX# ...      LDX   5  CPSEU(3) 
   8 5JY9 ...      BNG   5  REAL2
  11 5J_6 ...      NAME     3,FILE,ADICTENT  
  10 5K23 ...      SINMAC1  CPASS(3),5,2 
  11 5K2Y ...      MHUNTW   3,FILE,ADICTENT  
  10 5K3T ...      NAME     3,FILE,FWB   
   7 5K4Q ...      LDN   1  5
   9 5K5M ...      LDN   2  CPASS(3) 
   7 5K6J ...      MOVE  1  3
   5 5K7F ...REAL2   
   4 5K8B ...)   
   9 5K9G          CALL  5  SETADICT 
  21 5KP6          READDICT                     [GET DICTIONARY POSITIONED TO INSERT 
   6 5L8Q          INSERT
  17 5LNB          CLOSETOP                     [CLOSE THE DICTIONARY
   9 5M82          MFREEW   FILE,FWB 
   5 5MML    TDMAC   
   7 5N7=          LDX   0  4
   7 5NLW          SBN   0  2
  18 5P6G          BZE   0  TRAP                [SET TRAP FOR REAL USER  
   7 5PL6          SBN   0  2
  15 5Q5Q          BNZ   0  NOTRAP              [J IF PSEUDO 
   4 5QKB    TRAP
  18 5R52          SETNCORE 5,2,FILE,FTRAP      [FOR TRAPADD BY MAKEFILE 
  10 5RJL          MHUNTW   1,FILE,CREATE
   7 5S4=          LDN   0  5
  10 5SHW          STO   0  ETRAPRH(2)   
  15 5T3G          LDN   0  2                   [READ TRAP   
  10 5TH6          STO   0  ETRAPMODES(2)
   7 5TN# ...#UNS  ILOGACC   
  19 5TTG ...      BS       2,EDEFTRAP         [INDICATE THIS IS DEFAULT TRAP
   9 5W2Q          ADN   1  CEUSE1   
   9 5WGB          ADN   2  ETRUSER1 
   7 5X22          MOVE  1  3
   5 5XFL    NOTRAP  
  10 5X_=          MHUNTW   1,FILE,CREATE
   6 5Y4B ...#UNS FNSAD  
  15 5Y7G ...      MBS      1,CEONLINE,CECOOR,CEINDEXF,CECARE
   6 5Y=L ...#UNS FNSAD  
   4 5Y*Q ...#SKI
  18 5YDW          MBS      1,CEONLINE,CECOOR,CEALLINCS,CEINDEXF,CECARE  
   9 5YYG          BNZ   4  NOTEMPBIT
   9 5_D6          BS       1,CETEMP 
   6 5_XQ    NOTEMPBIT   
   7 62CB          LDEX  7  4
   7 62X2          SBN   7  1
  15 63BL          BZE   7  PSEUDOBIT           [J IF PSEUDO 
   7 63W=          SBN   7  2
  16 64*W          BNZ   7  NOPSEUDOBIT         [J UNLESS PSEUDO 
   6 64TG    PSEUDOBIT   
  16 65*6          BS       1,CEPSEUDO          [PSEUDO-DIRECTORY
   6 65SQ    NOPSEUDOBIT 
   9 66#B          LDN   0  INDEXDIR 
  10 66S2          STO   0  CEINDEX(1)   
   6 66TP ...#UNS FNSAD  
   4 66XD ...(   
  19 66_7 ...      BZE   4  NOSET              [NO 'DIRBELOW' BIT IF TEMPDIR 
   7 672W ...      TOPFCB   3
  12 674K ...      JBS      NOSET,3,BFDIRBELOW   
  13 676# ...      MBS      3,BFDIRBELOW,BFDIRUPDATE 
   5 6783 ...NOSET   
   4 679Q ...)   
  13 67CJ ...      OPENREL  TBR,GENERAL,CREATE,QUERY 
  11 67KB ...      TESTREP   ALREADY,NOTBRK  
  10 67R=          MFREEW   FILE,FLOCNB  
  10 68=W          MFREE    FILE,FABSNB  
   5 68?_ ...RETFROMA
  17 68QG          BNZ   4  MADE                [J UNLESS TEMPDIRA   
  16 69=6          FJOCA    1,2                 [GET X1->JOBBLOCK
  18 69PQ          BS       1,JBTEMP            [MARK 'TEMPDIR EXISTS'   
  14 6=9B          CLOSE                        [TEMP DIR
   5 6=BJ ...NCLOSINB
   8 6=P2          SETREP2  OK   
   7 6?8L          UPPLUS   2
   4 6?N=    MADE
   6 6#7W          CLOSE 
   6 6#*4 ...NCLOSINB2   
   8 6#MG          SETREP2  OK   
   7 6*76          UPPLUS   1
   4 6*LQ    NAME
  16 6B6B          BZE   4  (GEOERR)            [J IF TEMPDIRA   
  10 6BL2          LDX   3  XERNAME(1)   
   5 6C5L    NOTBRK  
  19 6CK=          LDN   7  0                   [REMEMBER NOT BREAKIN CASE   
  10 6D4W          BRN      REPALREADY   
   5 6DJG    USED1   
   7 6F46          LDN   7  0
  18 6FHQ          JBC      NOWAIT,3,BEERASE    [J UNLESS 'TO BE ERASED' 
   9 6G3B          LDX   7  ELOC1(3) 
  21 6GH2          LONGSET  #12,(GEOERR),7      [PREPARE TO WAIT FOR ERASURE TO FINIS
   5 6H2L    NOWAIT  
   7 6HG=          FREECORE 3
  15 6H_W          CLOSETOP                     [SUPERIOR DIR
  17 6JFG          BNZ   7  WAIT                [J IF LONGWAIT REQD  
  10 6J_6          LDX   3  XERMAKAL(1)  
   6 6KDQ    REPALREADY  
   9 6KYB          KEEPREP  5,ALREADY
  21 6LD2    WTBRK                              [FREE BLOCKS BEFORE UP IF BROKEN IN  
  10 6LXL          MFREEW   FILE,ADICT   
   5 6MC=    NOPRIVY 
  10 6MWW          MFREEW   FILE,CREATE  
  10 6NBG          MFREE    FILE,FABSNB  
  10 6NW6          MFREEW   FILE,FLOCNB  
   6 6NXN ...RETFORERR   
  15 6P*Q          BNZ   7  TBR                 [J IF BREAKIN
  18 6PTB          BZE   4  SETREP              [SET REPLY IF TEMPDIRA   
   9 6Q*2          VFREEW   FILE,FWB 
   7 6QSL          LDEX  4  4
   7 6R#=          SBN   4  4
  18 6RRW          BNZ   4  XERR                [REPORT ERROR IF COMMAND 
   5 6S?G    SETREP  
   7 6SR6          PUTREP   5
   7 6T=Q          UPPLUS   1
   4 6TQB    WAIT
   8 6W=2          LONGSTOP WTBRK
  15 6WPL          BRN      WTLOOP              [TRY AGAIN   
   4 6X9=    XERR
   7 6XNW          ERRORX   3
   7 6Y8G          UPPLUS   1
   5 6YN6    NOPRIV  
  10 6_7Q          LDX   3  XERMAKPR(1)  
   6 6_#Y ...#UNS FTS1   
  14 6_G6 ...      CLOSETOP                    [ DICT.   
  19 6_MB          CLOSE                        [CLOSE SUPERIOR DIR ONLY ONLY
   5 7272    NOPRIVZ 
   7 72LL          LDN   7  0
  20 736=          BRN      NOPRIVY             [CLEAR UP BLOCKS & REPORT ERROR  
   5 73KW    PSDIR   
  10 745G          LDX   3  XERMAKPS(1)  
   7 74K6          CLOSETOP  
   7 754Q          CLOSETOP  
  10 75JB          MFREEW   FILE,ADICTENT
   9 7642          BRN      NOPRIVZ  
   4 76HL    USED
  10 773=          MFREEW   FILE,ASELFLEX
   5 77GW    USEDX   
   7 782G          CLOSETOP  
  16 78G6          BZE   4  NONEG               [J IF TEMPDIRA   
  21 78_Q          BNG   4  NONEG               [J IF TO BE TEMP. SINCE SUPERIOR IS  
  13 79FB          CLOSE                        [DICT
   9 79_2          MFREEW   FILE,FWB 
   5 7=DL    NONEG   
  14 7=Y=          CLOSE                        [SUP CIR 
   7 7?CW          LDEX  0  4
   7 7?XG          SBN   0  2
  21 7#C6          BNG   0  PSTH                [J IF TEMPDIR OR MAKEDIR WITH ONE PAR
   7 7#WQ          SBN   0  2
   9 7*BB          BZE   0  NOTBRK   
  19 7*W2          MFREE    CPB,CUNI            [FREE FIRST PARAMETER BLOCK  
  10 7B*L          MHUNT    2,FILE,FNAME 
  10 7BT=          NAMETOP  2,CPB,CUNI   
   9 7C#W          LDX   3  XERMAK(1)
   9 7CSG          BRN      NOTBRK   
   4 7D#6    PSTH
  10 7DRQ          MFREEW   FILE,CREATE  
  10 7F?B          MFREEW   FILE,FLOCNB  
  10 7FR2          MFREEW   FILE,ADICT   
  10 7G=L          HUNT     1,FILE,FABSNB
   7 7GQ=          LDN   0  6
  20 7H9W          ADS   0  A1(1)               [RESTORE LENGTH BEFORE GOING BACK
  20 7HPG          BRN      PSEUD               [MAKE UP ANOTHER PSEUDO USERNAME 
  21 7J96    NXUZA                              [THIS ROUTINE PUTS THE NEXT USERNAME 
  21 7JNQ          SBX   5  FX1                 [UP BY THE SYSTEM INTO A FILE/CREATE 
   7 7K8B          CREATEB   
  21 7KN2          LDCT  1  #600                [IT UPDATES THIS NAME IN FIXED CORE  
  16 7L7L          ADN   1  2                   [SET UP MODIFIER 
  17 7LM=    VAGAN LDCH  0  AUSE(1)             [PICK UP A  CHARACTER
  16 7M6W          SBN   0  1                   [REDUCE IT BY 1  
   8 7MLG          SMO      FX1  
  18 7N66          TXL   0  NOSA                [TEST NOT LESS THAN 'A'  
  19 7NKQ          BCS      VUPD                [JUMP IF CHAR0LESS THAN 'A'  
  21 7P5B          DCH   0  AUSE(1)             [OTHERWISE REPLACE UPDATED CHARACTER 
  10 7PK2          MHUNTW   2,FILE,CREATE
  19 7Q4L          ADN   2  CEUSE1              [INTO THE FILE/CREATE BLOCK  
   8 7QJ=          LDN   1  AUSE 
   7 7R3W          MOVE  1  3
   8 7RHG          ADX   5  FX1  
   7 7S36          EXIT  5  0
   5 7SGQ    SETADICT
   8 7T2B          SBX   5  FX1  
  11 7TG2          SETNCORE 3,2,FILE,ADICT   
  10 7T_L          MHUNTW   1,FILE,CREATE
   9 7WF=          ADN   1  CEUSE1   
   8 7WYW          ADN   2  A1   
   7 7XDG          MOVE  1  3
   8 7XY6          ADX   5  FX1  
   7 7YCQ          EXIT  5  0
  19 7YXB    VUPD  LDN   0  #72                 [REPLACE CHARACTER WITH A 'Z'
  21 7_C2          DCH   0  AUSE(1)             [AND UPDATE THE CHARACTER MODIFIER   
  19 7_WL          LDX   0  1                   [TEST WHETHER NECESSARY TO   
  17 82B=          SRL   0  2                   [CHANGE WORD MODIFIER
  17 82TW          BZE   0  VDOWN               [JUMP IF NECESSARY   
  20 83*G          LDCT  0  #200                [OTHERWISE DECREMENT CHARACTER   
  20 83T6          SBX   1  0                   [MODIFIER AND LOOK AT NEXT MOST  
  18 84#Q          BRN      VAGAN               [SIGNIFICANT CHARACTER   
  20 84SB    VDOWN SBN   1  1                   [DECREMENT WORD MODIFIER AND SET 
  20 85#2          LDCT  0  #600                [CHARACTER MODIFIER TO 3THEN LOOK
  21 85RL          ADX   1  0                   [AT NEXT MOST SIGNIFICANT CHARACTER  
   8 86?=          BRN      VAGAN
   5 86QW    ZBREAK  
  15 87=G          CLOSETOP                     [DICTIONARY  
   5 87Q6    TBREAK  
  15 889Q          CLOSETOP                     [DIRECTORY   
   4 88PB    TBR 
   5 8992          UP
   6 89?9 ...#UNS FTS1   
   4 89*D ...(   
   5 89CM ...RFROZEN 
   7 89FW ...      REPALLER  
   7 89J5 ...      UPPLUS   1
   4 89L# ...)   
   4 89NL    [   
  10 8=8=          MENDAREA 20,K99NEWDIR 
   4 8=MW    #END
   8 ____ ...04256470000200000000
