  13 22FL          SEG      SCEDITOR,,KEITH H MARTIN 
  13 22_=    #LIS      K0SCEDITOR>K0ALLGEO>K0GREATGEO
   4 23DW    #   
   4 23YG    #   
   4 24D6    #   
   4 24XQ    #   
   8 25CB    #              IN   
   8 25X2    #              **   
  17 26BL          SEGENTRY K1SCEDITOR,POINTER  [OUTPUT POINTER READY
  19 26W=          SEGENTRY K2SCEDITOR,OUTPUT   [OUTPUT AMXOR:ASCREEN BLOCK  
  19 27*W          SEGENTRY K3SCEDITOR,REPEAT   [REOUTPUT AMXOR:ASCREEN BLOCK
  15 27TG         SEGENTRY K4SCEDITOR,SBREAKIN    [BREAK IN  
  11 28*6          SEGENTRY  K5SCEDITOR,START
   4 28SQ    #   
   4 29#B    #   
   8 29S2    #              OUT  
   8 2=?L    #              ***  
   4 2=R=    #   
   9 2?=W    #     K1SCEDITAR   [DATA
  11 2?QG    #     K2SCEDITAR   [CONTROL+6,+7
   9 2#=6    #     K3SCEDITAR   [T   
   9 2#PQ    #     K4SCEDITAR   [P   
   9 2*9B    #     K5SCEDITAR   [E   
   9 2*P2    #     K6SCEDITAR   [F   
   9 2B8L    #     K6SCEDITAR   [Q   
   4 2BN=    #   
   4 2C7W    #   
   4 2CMG    #   
   4 2D76    #   
  21 2DLQ    # ================================================================= #   
   4 2F6B    #   
   4 2FL2    #   
   4 2G5L    #   
   4 2GK=    #   
  11 2H4W    SSKATC         CATMASK COMMANDS 
   9 2HJG    SSCOMMA        4H000,   
   8 2J46    SSSPACE        4H000
   9 2JHQ    SSHASH         4H000#   
   9 2K3B    SSMINUS        4H000-   
   9 2KH2    SST            4H000T   
   9 2L2L    SSP            4H000P   
   9 2LG=    SSR            4H000R   
   9 2L_W    SSQ            4H000Q   
   9 2MFG    SSE            4H000E   
   9 2M_6    SSF            4H000F   
   9 2N8Y ...SSI            4H000I   
   8 2NDQ    SSTEN          +10  
   9 2NYB    SSBITS22       #17777777
  11 2PD2    SSDELIM        16H:;<=>?!^/%&'+/
   9 2PXL    SSMAGICNO      +7036875 
  13 2QC=    SSKATZ         CATMASK FORCETYP,BROADCAS
  11 2QWW    SSSYN          13HSYNTAX ERROR: 
   4 2RBG    #   
   8 2RW6    SS6            +6   
   8 2S*Q    SS7            +7   
   8 2STB    SS8            +8   
   4 2T*2    #   
   4 2TSL    #   
   4 2W#=    #   
   4 2WRW    #   
   4 2X?G    #   
  21 2XR6    # ================================================================= #   
   4 2Y=Q    #   
   4 2YQB    #   
   4 2_=2    #   
   4 2_PL    #   
  15 329=    #              MONITORING FILE MESSAGE ADDRESSES
  10 32NW    SE2MBAFR       +ISE2MBAFR   
  10 338G    SYROTBOTF      +ISYROTBOTF  
   9 33N6    SYRYBSL        +ISYRYBSL
  10 347Q    SROTKINA       +ISROTKINA   
   9 34MB    SBTNA          +ISBTNA  
  10 3572    SYROTEOTF      +ISYROTEOTF  
  10 35LL    SFMNBUITC      +ISFMNBUITC  
   9 366=    SYFYNF         +ISYFYNF 
   8 36KW    SIT            +ISIT
   9 375G    SNTL           +ISNTL   
   8 37K6    SEA            +ISEA
   9 384Q    SEOE           +ISEOE   
   9 38JB    SIFE           +ISIFE   
   4 3942    #   
   4 39HL    #   
   4 3=3=    #   
   4 3=GW    #   
  21 3?2G    # ================================================================= #   
   4 3?G6    #   
   4 3?_Q    #   
   4 3#FB    #   
   4 3#_2    #   
   4 3*DL    #   
  16 3*Y=    #              STEPBACKOLD,STEPBACKNEW SUBROUTINES  
  19 3BCW    #              STEPS BACK TO LINE (X0) IN NEW OR OLD FILE AND   
  13 3BXG    #              RESETS CURRENT LINE NO   
   4 3CC6    #   
   9 3CWQ    #              LINK X7  
   4 3DBB    #   
  15 3DW2    #              X3 MUST BE PTR TO AMXOR:ASCREEN  
   4 3F*L    #   
   4 3FT=    #   
   4 3G#W    #   
   4 3GSG    #   
   6 3H#6    STEPBACKNEW 
   8 3HHY ...      SBX   7  FX1  
  16 3HRQ          LDX   6  ISNCURRENT(3)       [CURREN LINE NO  
  15 3J?B          SBX   6  0                   [DIFFERENCE  
  17 3JR2          TRACE   6,ISNEWBAK          [<<<<<<<<<<>>>>>>>>>> 
   9 3K=L          BZE   6  STEPBNEX 
  14 3KQ=          STO   0  ISNCURRENT(3)       [RESET   
   5 3L9W    STEPBNA 
   8 3LPG          BACKSPACE    0
   9 3M96          BCT   6  STEPBNA  
   7 3MNQ          LDN   0  0
  17 3N8B          CURTAIL      0               [SET APPEND=READ PTR 
   5 3NN2    STEPBNEX
   8 3NXS ...      ADX   7  FX1  
   7 3P7L          EXIT  7  0
   4 3PM=    #   
   4 3Q6W    #   
   4 3QLG    #   
   4 3R66    #   
   6 3RKQ    STEPBACKOLD 
   8 3RTJ ...      SBX   7  FX1  
  10 3S5B          LDX   6  ISOCURRENT(3)
   7 3SK2          SBX   6  0
  17 3T4L          TRACE   6,ISOLDBAK           [<<<<<<<<<>>>>>>>>>> 
   9 3TJ=          BZE   6  STEPBOEX 
  10 3W3W          STO   0  ISOCURRENT(3)
  16 3W?N ...      BNG   6  STEPBOB           [J IF MOVE FORWARDS
   5 3WHG    STEPBOA 
   8 3X36          BACKSPACE    1
   9 3XGQ          BCT   6  STEPBOA  
   5 3Y2B    STEPBOEX
   8 3Y=8 ...      ADX   7  FX1  
   7 3YG2          EXIT  7  0
   5 3YHY ...STEPBOB 
  15 3YKW ...      NGX   6  6               [MAKE +VE COUNT  
   5 3YMS ...STEPBOC 
   7 3YPQ ...      STEP     1
  14 3YRN ...      BCT   6  STEPBOC         [TO LOOP ON  
   8 3YTL ...      ADX   7  FX1  
   7 3YXJ ...      EXIT  7  0
   4 3Y_L    #   
   4 3_F=    #   
   4 3_YW    #   
   4 42DG    #   
  21 42Y6    # ================================================================= #   
   4 43CQ    #   
   4 43XB    #   
   4 44C2    #   
   4 44WL    #   
   4 45B=    #   
  11 45TW    #              SETLOP SUBROUTINE
   4 46*G    #   
  17 46T6    #              MUST BE CALLED BEFORE OVERWRITING ISMFORM
  17 47#Q    #              WHEN OUTPUTTING TEXT RDY OR PTR RDY BUT  
  13 47SB    #              NOT FOR ERROR MESSAGES   
   4 48#2    #   
  10 48RL    #              CALL ON X7   
   4 49?=    #   
  18 49QW    #              N.B  THE LAST OUTPUT IS THE ONE PRIOR TO THE 
  18 4==G    #              ---  ONE ON THE SCREEN NOW OR THE ONE JUST   
  13 4=Q6    #                   ABOUT TO BE OUTPUT  
  15 4?9Q    #                CF LAST OUTPUT - CURRENT OUTPUT
   5 4?PB    SETLOP  
   9 4#92          LDN   5  ISBITLOP 
  18 4#NL          ORS   5  ISBITS(3)           [SET LAST OUTPUT READY   
  16 4*8=          LDCT  0  #002                [B7=TEXT READY   
  10 4*MW          ANDX  0  ISMFORM(3)   
  16 4B7G          BZE   0  SETLOPX               [J IF PTR RDY  
  19 4BM6          ERS   5  ISBITS(3)           [CLEAR LAST OUTPUT PTR READY 
   5 4C6Q    SETLOPX 
   7 4CLB          EXIT  7  0
   4 4D62    #   
   4 4DKL    #   
   4 4F5=    #   
   4 4FJW    #   
  21 4G4G    # ================================================================= #   
   4 4GJ6    #   
   4 4H3Q    #   
   4 4HHB    #   
   4 4J32    #   
  12 4JGL    #              SCOMMENT SUBROUTINE  
   4 4K2=    #   
   4 4KFW    #   
  17 4K_G    #              OUTPUT FIXED MESSAGE ON NEXT FREE LINE   
   4 4LF6    #   
  10 4LYQ    #              CALL ON X7   
  11 4MDB    #              EXIT TO CALL+1   
   4 4MY2    #   
  11 4NCL    #              X6 PTR TO MESSAGE
   4 4NX=    #   
   5 4PBW    SCOMMENT
   8 4PLN ...      SBX   7  FX1  
  11 4PWG          MHUNTW   3,AMXOR,ASCREEN  
   8 4QB6          LDN  0  ISBITR
   9 4QTQ          ORS   0  ISBITS(3)
  14 4R*B          ERS   0  ISBITS(3)           [CLEAR R 
   9 4RT2          LDN   0  ISBITLOP 
  16 4S#L          ANDX  0  ISBITS(3)           [LAT OP PTR RDY  
  14 4SS=          BNZ   0  SCOM1               [J IF SO 
   9 4T?W          LDX   0  ISMERR(3)
   8 4TRG          SRL   0  18   
   7 4W?6          SBN   0  3
  18 4WQQ          BNZ   0  SCOM1               [J IF ONE ON SCREEN NOW  
   8 4X=B          LDCT  0  #004 
  16 4XQ2          ORS   0  ISMERR(3)           [SET CLEAR SCREEN
   5 4Y9L    SCOM1   
  15 4YP=          LDX   5  ISMFORM(3)          [SAVE THIS   
   9 4_8W          LDX   0  ISMERR(3)
  10 4_NG          STO   0  ISMFORM(3)   
   7 5286          MONOUTX  6
  11 52MQ          MHUNTW   3,AMXOR,ASCREEN  
  10 537B          STO   5  ISMFORM(3)   
   8 53M2          LDCT  0  #010 
  14 546L          ADS   0  ISMERR(3)          [LINES+1  
   8 54L=          LDCT  0  #004 
   9 555W          ORS   0  ISMERR(3)
  17 55KG          ERS   0  ISMERR(3)           [CLEAR CLEAR SCREEN  
   8 55T# ...      ADX   7  FX1  
   7 5656          EXIT  7  0
   4 56JQ    #   
   4 574B    #   
   4 57J2    #   
   4 583L    #   
  21 58H=    # ================================================================= #   
   4 592W    #   
   4 59GG    #   
   4 5=26    #   
   4 5=FQ    #   
  12 5=_B    #              NONSPACE SUBROUTINE  
  15 5?F2    #              PUT NEXT NONSPACE CHAR INTO X0   
   4 5?YL    #   
  11 5#D=    #              X2 CCM TO INPUT  
  15 5#XW    #              X4 COUNT OF CHS LEFT(MAY BE XERO)
   4 5*CG    #   
  10 5*X6    #              CALL ON X6   
  17 5BBQ    #              EXIT CALL+1  END OF LINE-NO MORE CHARS   
  12 5BWB    #                   CALL+2  CH IN X0
   4 5CB2    #   
   4 5CTL    #   
   5 5D*=    NONSPACE
  15 5DSW          BCHX  2  /                   [STEP INPUT  
  16 5F#G          BZE   4  NONSPACEX           [J IF END OF LINE
   7 5FS6          SBN   4  1
   8 5G?Q          LDCH  0  0(2) 
  16 5GRB          BXE   0  SSSPACE(1),NONSPACE [LOOP ON SPACES  
   7 5H?2          EXIT  6  1
   6 5HQL    NONSPACEX   
   7 5J==          EXIT  6  0
   4 5JPW    #   
   4 5K9G    #   
   4 5KP6    #   
   4 5L8Q    #   
  21 5LNB    # ================================================================= #   
   4 5M82    #   
   4 5MML    #   
   4 5N7=    #   
   4 5NLW    #   
  11 5P6G    #              NEXT SUBROUTINE  
  12 5PL6    #              PUT NEXT CHAR INTO X0
   4 5Q5Q    #   
  11 5QKB    #              X2 CCM TO INPUT  
  15 5R52    #              X4 COUNT OF CHS LEFT(MAY BE ZERO)
   4 5RJL    #   
  10 5S4=    #              CALL ON X6   
  16 5SHW    #              EXIT CALL+1 END OF LINE-NO MORE CHARS
  12 5T3G    #                   CALL+2 CH IN X0 
   4 5TH6    #   
   4 5W2Q    #   
   4 5WGB    NEXT
   7 5X22          BCHX  2  /
   8 5XFL          BZE   4  NEXTX
   7 5X_=          SBN   4  1
   8 5YDW          LDCH  0  0(2) 
   7 5YYG          EXIT  6  1
   5 5_D6    NEXTX   
   7 5_XQ          EXIT  6  0
   4 62CB    #   
   4 62X2    #   
   4 63BL    #   
   4 63W=    #   
  21 64*W    # ================================================================= #   
   4 64TG    #   
   4 65*6    #   
   4 65SQ    #   
   4 66#B    #   
  12 66S2    #              PARAMETER SUBROUTINE 
  14 67?L    #              ANALYSE PARAMETER FOR T OR P 
   4 67R=    #   
  16 68=W    #              ON ENTRY  X2 PTR TO FIRST NON SPACE  
  15 68QG    #                           CHAR FOR ANALYSIS   
  15 69=6    #              ON EXIT   X2 PTR TO FIRST CHAR   
  13 69PQ    #                           NOT ANALYSED
  15 6=9B    #                           X2=0 IF END OF LIME 
   4 6=P2    #   
  15 6?8L    #              X4 RUNNING COUNT OF CHARS LEFT   
  13 6?N=    #              X0 CHAR POINTED TO BY X2 
  14 6#7W    #              X3 PTR TO AMXOR/ASCREEN BLOCK
   4 6#MG    #   
  10 6*76    #              CALL ON X7   
  16 6*LQ    #              EXIT CALL+1 PARAM IN NUMBER FORMAT   
  16 6B6B    #                   CALL+2 PARAM IN STRING FORMAT   
   4 6BL2    #   
  16 6C5L    #              RESULT IN TO ISPARAMB(3) AS FOLLOWS  
  14 6CK=    #              NUMBERS ISPARAMB  B0=1  IF E 
  20 6D4W    #                                B1=1  IF BAKCWARDS MOVEMENT CAUSES 
  20 6DJG    #                                      RUNNING OFF START OF FILE (B)
  20 6F46    #                                B2-23 LINE NUMBER-ALWAYS ABSOLUTE  
  10 6FHQ    #               ISPARAMB+1  
  16 6G3B    #              STRING  ISPARAMB+2 B0=1 IF BACKWARDS 
  19 6GH2    #                                 B1-23 COUNT OF CHARS IN STRING
  12 6H2L    #                      ISPARAMB+3)  
  16 6HG=    #                      ISPARAMB+4) TEXT OF STRING   
  12 6H_W    #                      ISPARAMB+5)  
   4 6JFG    #   
  18 6J_6    #              DEFAULT VALUE SHOULD BE SET UP BEFORE ENTRY  
   4 6KDQ    #   
   6 6KYB    PARAMETER   
  17 6LD2          TRACE    2,ISPARAM          [<<<<<<<<<<>>>>>>>>>> 
  15 6LXL          BXE   0  SSCOMMA(1),PAR4     [J IF NULL   
  15 6MC=          BXU   0  SSE(1),PARX         [J IF NOT E  
   8 6MWW          LDCT  6  #400 
  14 6NBG          STO   6  ISPARAMB(3)         [SET E   
  12 6NW6          CALL  6  NEXT                [
  14 6P*Q          STOZ     2                   [SET EOL 
  15 6PTB          BRN      PAR84                [MORE CHARS 
   4 6Q*2    PARX
  17 6QSL          BXU   0  SSHASH(1),PARB      [J IF NOT ABSOLUTE   
   8 6R#=          LDCT  6  #400 
  14 6RRW          ORX   7  6                   [SET #   
   8 6S?G          CALL  6  NEXT 
   9 6SR6          BRN      SYNERR   
   4 6T=Q    PARB
  17 6TQB          BXU   0  SSMINUS(1),PART     [J IF NOT BACKWARDS  
  16 6W=2          BNG   7  SYNERR              [ERROR IF - AND #
   8 6WPL          LDCT  6  #200 
  15 6X9=          ORX   7  6                   [- NOW SET   
   8 6XNW          CALL  6  NEXT 
  14 6Y8G          BRN      SYNERR              [- WHAT? 
   4 6YN6    PART
  16 6_7Q          BXGE  0  SSTEN(1),PARQ       [J IF NOT NUMBER 
   4 6_MB    #   
   4 7272    #   
   4 72LL    #   
  13 736=    #              THIS PARAM IS A NUMBER   
   7 73KW          LDN   5  0
   7 745G          LDN   6  0
   8 74K6          SMO      FX2  
  12 754Q          STO   7  AWORK3          [LINK
   4 75JB    PAR2
  15 7642          CDB   5  0(2)                [INTO 5 AND 6
  16 76HL          BCS      PAR3                [J IF END OF NO  
  15 773=          BVSR     SSNTL             [J IF TOO BIG  
   7 77GW          STO   6  7
   8 782G          CALL  6  NEXT 
  14 78G6          BRN      PAR4Y                [DONE   
   7 78_Q          LDX   6  7
   8 79FB          BRN      PAR2 
   4 79_2    PAR3
   7 7=DL          STO   6  7
   8 7=Y=          BRN      PARP 
   5 7?CW    PAR4Y   
   7 7?XG          STOZ     2
   4 7#C6    #   
  14 7#WQ    #              MAKE ABSOLUTE IF - OR NOT #  
   4 7*BB    PARP
   8 7*W2          BNZ   5  SSNTL
   7 7B*L          LDX   6  7
   8 7BT=          SMO      FX2  
   9 7C#W          LDX   7  AWORK3   
  14 7CSG          BNG   7  PAR8                [J IF #  
   8 7D#6          LDCT  5  #200 
   7 7DRQ          ANDX  5  7
   5 7F?B    PAR81   
  15 7FR2          ORX   6  5                  [SET - OR NOT 
  10 7G=L          STO   6  ISPARAMB+1(3)
  17 7GQ=         TRACE   6,ISNUMREL           [<<<<<<<<<<>>>>>>>>>> 
  13 7H9W          STOZ     ISPARAMB(3)        [CLEAR
   8 7HPG          BRN      PAR4 
   4 7J96    #   
  10 7JNQ    #     SAVE NUMBER AND FINISH
   4 7K8B    PAR8
  17 7KN2          TRACE    6,ISNUMABS         [<<<<<<<<<<>>>>>>>>>> 
  10 7L7L          STO   6  ISPARAMB(3)  
   5 7LM=    PAR84   
   8 7M6W          LDCT  6  #400 
  15 7MLG          STO   6  ISPARAMB+1(3)      [SET ABSULOTE 
   4 7N66    PAR4
  16 7NKQ          ANDX  7  SSBITS22(1)         [CLEAR - AND #   
  10 7P5B          EXIT  7  0  [NUMBER   
   4 7PK2    #   
   4 7Q4L    #   
   4 7QJ=    #   
  14 7R3W    #              MUST BE STRING IF NOT NUMBER 
   4 7RHG    PARQ
  15 7S36          BNG   7  SYNERR              [ERROR IF #  
  17 7SGQ          LDN   1  SSDELIM(1)          [MUST BE DELIMITOR   
  15 7T2B          LDCT  6  16                  [CHOICE OF 16
   5 7TG2    PARQ6   
  15 7T_L          LDCH  5  0(1)                [NEXT DELIM  
  15 7WF=          BXE   0  5,PARQ5             [J IF FOUND  
   7 7WYW          BCHX  1  /
  14 7XDG          BCT   6  PARQ6               [TRY NEXT
   8 7XY6          LDX   1  FX1  
  17 7YCQ          BRN      SYNERR              [WHERES THE DELIM?   
   4 7YXB    #   
  11 7_C2    #              DELIMITOR FOUND  
   5 7_WL    PARQ5   
   8 82B=          SMO      FX2  
  15 82TW          STO   7  AWORK1              [SAVE LINK   
  16 83*G          LDN   1  ISPARAMB+3(3)       [SAVE CHARS HERE 
  15 83T6          LDN   7  13                  [BUT ONLY 12 
   5 84#Q    PAR72   
   8 84SB          CALL  6  NEXT 
  17 85#2          BRN      SYNERR              [NO TERMINATING DELIM
  15 85RL          BXE   0  5,PAR99             [J IF DELIM  
   8 86?=          BCT   7  PAR83
  14 86QW          BRN      SYNERR              [TOO LONG
   5 87=G    PAR83   
  14 87Q6          DCH   0  0(1)                [SAVE CH 
   8 889Q          BCHX  1  PAR72
   4 88PB    #   
  10 8992    #     NOW CALCULATE LENGTH  
   5 89NL    PAR99   
   8 8=8=          LDN   5  13   
  14 8=MW          SBX   5  7                   [=LENGTH 
   9 8?7G          BZE   5  SYNERR   
   8 8?M6          SMO      FX2  
   9 8#6Q          LDX   7  AWORK1   
   8 8#LB          LDCT  6  #200 
   7 8*62          ANDX  6  7
  16 8*KL          BZE   6  PAR20               [J IF FORWARDS   
   8 8B5=          LDCT  6  #400 
  14 8BJW          ORX  5  6                   [SET MINUS
   5 8C4G    PAR20   
  17 8CJ6          TRACE    5,ISTEXT           [<<<<<<<<<<>>>>>>>>>> 
  10 8D3Q          STO   5  ISPARAMB+2(3)
   4 8DHB    #   
   7 8F32    #     TIDY UP   
   8 8FGL          CALL  6  NEXT 
  14 8G2=          STOZ     2                   [SET EOL 
   8 8GFW          LDX   1  FX1  
  10 8G_G          ANDX  7  SSBITS22(1)  
  10 8HF6          EXIT  7  1  [STRING   
   4 8HYQ    #   
   4 8JDB    #   
   4 8JY2    #   
   4 8KCL    #   
  21 8KX=    # ================================================================= #   
   4 8LBW    #   
   4 8LWG    #   
   4 8MB6    #   
   4 8MTQ    #   
  11 8N*B    #              REFIND SUBROUTINE
   4 8NT2    #   
  11 8P#L    #              USED BY ALTLEN   
   4 8PS=    #   
   5 8Q?W    REFIND  
  11 8QRG          MHUNTW   2,AMXOR,ASCREEN  
   7 8R?6          EXIT  1  0
   4 8RQQ    #   
   4 8S=B    #   
   4 8SQ2    #   
   4 8T9L    #   
  21 8TP=    # ================================================================= #   
   4 8W8W    #   
   4 8WNG    #   
   4 8X86    #   
   4 8XMQ    #   
  10 8Y7B    #              SYNTAX ERROR 
   4 8YM2    #   
   4 8_6L    #   
   5 8_L=    SYNERR  
  16 925W          MHUNTW   3,FILE,FRB          [FIND BAD COMMAND
  17 92KG          TRACE  3,ISYNERR            [<<<<<<<<<<>>>>>>>>>> 
   8 9356          LDCT  6  #600 
   9 93JQ          ANDX  6  A1+1(3)  
   9 944B          BNZ   6  SYNERRB  
   7 94J2          ADN   6  1
   5 953L    SYNERRB 
   8 95H=          ADX   6  A1(3)
   7 962W          SLC   6  2
   8 96GG          SBN   6  12   
   4 96J9 ...#   
  11 96KY ...      MHUNTW   2,AMXOR,ASCREEN  
  17 96MM ...      LDN   0  80                  [LENGTH OF TIP LINE  
  17 96PB ...      SBX   0  ISTEXTCT(2)         [ - LENGTH OF LINE NO
  14 96R5 ...      SBN   0  3                   [ - ^<- ^
  17 96SS ...      BXL   6  0,SYNERRLEN         [J IF WITHIN LIMITS  
  19 96WH ...      LDX   6  0                   [ELSE RESET TO MAX POSSIBLE  
   6 96Y= ...SYNERRLEN   
  17 9726          LDN   7  13                  [LENGTH OF FIXED TEXT
  18 97FQ          ADX   7  6                   [TOTAL LENGTH OF MESSAGE 
   7 97_B          LDX   5  7
   7 98F2          ADN   5  3
  16 98YL          SRA   5  2                   [ROUNDED TO WORDS
  11 99D=          SETUPCORE    5,5,CPB,CUNI 
  16 99XW          ADN   5  A1                  [PUT OUTPUT HERE 
   9 9=CG          LDN   4  SSSYN(1) 
  15 9=X6          MVCH  4  13                  [FIXED PART  
  10 9?BQ          MHUNTW   3,FILE,FRB   
  17 9?WB          LDN   4  A1+2(3)             [PTR TO BAD COMMAND  
   7 9#B2          SMO      6
   7 9#TL          MVCH  4  0
  11 9**=          MHUNTW   3,AMXOR,ASCREEN  
  17 9*SW          LDN   0  ISBITR+ISBITT+ISBITP+ISBITQ+ISBITE+ISBITF
   9 9B#G          ORS   0  ISBITS(3)
   9 9BS6          ERS   0  ISBITS(3)
  10 9C?Q          LDX   6  ISMFORM(3)   
   8 9CRB          LDCT  4  #030 
  10 9D?2          STO   4  ISMFORM(3)   
  16 9DQL          LDN   4  #40                 [MESSAGE CATEGORY
  11 9F==          OUTMON   7,A1,4,CPB,CUNI  
   9 9FPW          VFREE    CPB,CUNI 
  11 9G9G          MHUNTW    3,AMXOR,ASCREEN 
  10 9GP6          STO   6  ISMFORM(3)   
   9 9H8Q          BRN      POINTER  
   4 9HNB    #   
   4 9J82    #   
   4 9JML    #   
  20 9K7=    # K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 K5 #  
   4 9KLW    #   
   4 9L6G    #   
   5 9LL6    START   
  11 9M5Q          MHUNTW   3,AMXOR,ASCREEN  
  17 9MKB          LDCT  0  #034                [LINE 3,CLEAR SCREEN 
   9 9N52          STO   0  ISMERR(3)
   8 9NJL          LDCT  0  #400 
  15 9P4=          STO   0  ISOPAGE(3)          [SET C+7 ..  
  16 9PHW          STO   0  ISOPREVPAGE(3)      [.. NOT ALLOWED  
   8 9Q3G          STEP        1 
  17 9QH6          TESTBRKIN  SCOMMANDQ         [Q ACTION IF BRK IN  
  16 9R2Q          BNZ   3  POINTER            [J IF NOT EMPTY   
   9 9RGB          LDX   6  SIFE(1)  
   9 9S22          CALL  7  SCOMMENT 
   9 9SFL          BRN      SCOMMANDQ
   4 9S_=    #   
   4 9TDW    #   
   4 9TYG    #   
   4 9WD6    #   
  20 9WXQ    # K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 K1 #  
   4 9XCB    #   
   4 9XX2    #   
   4 9YBL    #   
   4 9YW=    #   
  11 9_*W    #              SET POINTER READY
   4 9_TG    #   
  16 =2*6    #              COME HERE WHEN READY FOR NEXT COMMAND
   4 =2SQ    #   
  16 =3#B    #              ALL BITS CLEARED/SET AS APPROPRIATE  
   4 =3S2    #   
   4 =4?L    #   
   5 =4R=    POINTER 
  11 =5=W          MHUNTW   3,AMXOR,ASCREEN  
  13 =5QG          ALTLENGD   3,ASCREENLEN+2,REFIND  
  11 =6=6          MHUNTW   3,AMXOR,ASCREEN  
  17 =6PQ          TRACE    3,ISPOINT          [<<<<<<<<<<>>>>>>>>>> 
   9 =79B          LDN   0  ISBITCE  
  18 =7P2          ORS   0  ISBITS(3)              [SET COMMAND EXPECTED 
   9 =88L          LDN   0  ISBITF   
  14 =8N=          ANDX  0  ISBITS(3)          [F SET?   
   9 =97W          LDN   4  ISBITLC  
  15 =9MG          ORS   4  ISBITS(3)          [SET F ALLOWED
  15 ==76          BZE   0  PTRX               [J IF RIGHT   
  17 ==LQ          ERS   4  ISBITS(3)          [ELSE SET FI NVALID   
   4 =?6B    PTRX
  17 =?L2          LDN   0  ISBITR+ISBITT+ISBITP+ISBITE+ISBITF+ISBITQ
   9 =#5L          ORS   0  ISBITS(3)
  17 =#K=          ERS   0  ISBITS(3)           [CLEAR LAST COMMAND  
   9 =*4W          CALL  7  SETLOP   
  17 =*JG          LDCT  0  #010                [SET LINE 1,PTR READY
  10 =B46          STO   0  ISMFORM(3)   
  16 =BHQ          LDN   2  ISTEXT(3)           [PUT LINE NO HERE
  17 =C3B          LDX   5  ISOCURRENT(3)       [CURRENT OLD FILE NO 
  10 =CH2          MPY   5  SSMAGICNO(1) 
  18 =D2L          LDN   0  0                   [TESTS FOR LEADING ZEROES
  15 =DG=          LDN   4  7                   [ONLY 7 CHS  
   7 =D_W          LDN   7  0
   4 =FFG    PTRA
   7 =F_6          CBD   5  7
   8 =GDQ          DCH   7  0(2) 
  16 =GYB          ADX   0  7                   [LEADING ZERO?   
  15 =HD2          BNZ   0  PTRB                [J IF NO MORE
   8 =HXL          BCT   4  PTRA 
   8 =JC=          BCHX  2  PTRC 
   4 =JWW    PTRB
   7 =KBG          BCHX  2  /
   8 =KW6          BCT   4  PTRA 
   4 =L*Q    PTRC
   7 =LTB          SLC   2  2
   9 =M*2          LDN   0  ISTEXT(3)
   7 =MSL          SLC   0  2
   7 =N#=          SBX   2  0
  14 =NRW          STO   2  ISTEXTCT(3)         [AND DONE
   4 =P?G    #   
   4 =PR6    #   
   4 =Q=Q    #   
   4 =QQB    #   
   4 =R=2    #   
  20 =RPL    # K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 K2 #  
   4 =S9=    #   
   4 =SNW    #   
   4 =T8G    #   
   4 =TN6    #   
  14 =W7Q    #              OUTPUT AMXOR/ASCREEN BLOCK   
   4 =WMB    #   
  18 =X72    #              LINE NO,COL NO,READX BIT MUST BE SET/CLEARED 
  15 =XLL    #                  BEFORE ENTRY TO THIS ROUTINE 
   4 =Y6=    #   
  18 =YKW    #              LAST MESSAGE OUTPUT BIT WILL BE SET/CLEARED  
  12 =_5G    #                  AS APPROPRIATE   
   4 =_K6    #   
  17 ?24Q    #              CLEAR SCREEN WILL BE SET IF LAST OUTPUT  
  12 ?2JB    #                  WAS A TEXT READY 
   4 ?342    #   
  20 ?3HL    #              BROADCASTS WILL BE DEALT WITH BY THIS SECTION ONLY   
   5 ?43=    OUTPUT  
   9 ?4GW          VFREEW    FILE,FRB
  11 ?52G          MHUNTW   3,AMXOR,ASCREEN  
  17 ?5G6          TRACE    3,ISOUTPUT         [<<<<<<<<<<>>>>>>>>>> 
  17 ?5_Q          LDCT  0  #002                [B7=1,SUPPRESS READY 
  10 ?6FB          ANDX  0  ISMFORM(3)   
  17 ?6_2          BNZ   0  OUTSB2               [J IF NOT PTR READY 
   4 ?7DL    #   
  11 ?7Y=    #     OUTPUT MUST BE PTR READY  
  10 ?8CW    #     OUTPUT FIRST BC IF ANY
  19 ?8XG          HUNT2J   1,AMXOR,BBC,FX2,OUTSB   [J IF NONE,ELSE FIND IT  
  17 ?9C6          TRACE    1,ISOUTBBC         [<<<<<<<<<<>>>>>>>>>> 
  19 ?9WQ          LDX   6  ISMFORM(3)          [SAVE OVER OUTPUTING OF BC   
  18 ?=BB          LDCT  5  #002                [LINE 0,COL 0,TEXT RDY   
   9 ?=W2          LDX   0  ISMERR(3)
   8 ??*L          SRL   0  18   
   7 ??T=          SBN   0  3
  19 ?##W          BNZ   0  OUTSD               [J IF ERROR ON SCREEN ALREADY
   9 ?#SG          LDX   0  ISBITS(3)
  17 ?*#6          ANDN  0  ISBITLOP            [LAST 0P PTR READY   
  14 ?*RQ          BNZ   0  OUTSD               [J IF SO 
  21 ?B?B          LDCT  5  #006                [LINE 0,COL 0,TEXT RDY,CLEAR SCREEN  
   5 ?BR2    OUTSD   
  10 ?C=L          STO   5  ISMFORM(3)   
   8 ?CQ=          SMO      FX1  
  15 ?D9W          LDX   5  SSKATZ              [CATEGORIES  
  16 ?DPG          OUTMON   A1(1),A1+2,5,AMXOR,BBC  [OUTPUT BC   
   9 ?F96          MFREEW   AMXOR,BBC
  11 ?FNQ          MHUNTW   3,AMXOR,ASCREEN  
  17 ?G8B          STO   6  ISMFORM(3)          [RESET TO ORIGINAL   
  16 ?GN2          BRN      REPEAT              [J TO OUTPUT DATA
   4 ?H7L    #   
   4 ?HM=    #   
  13 ?J6W    #     NOW OUTPUT TEXT IN ASCREEN BLOCK  
   5 ?JLG    OUTSB   
  11 ?K66          MHUNTW   3,AMXOR,ASCREEN  
   5 ?KKQ    OUTSB2  
  17 ?L5B          TRACE    3,ISOUTMES         [<<<<<<<<<<>>>>>>>>>> 
   9 ?LK2          LDX   0  ISBITS(3)
  17 ?M4L          ANDN  0  ISBITLOP            [LAST OP PTR READY   
  14 ?MJ=          BNZ   0  OUTSD3               [J IF SO
   9 ?N3W          LDX   0  ISMERR(3)
   8 ?NHG          SRL   0  18   
   7 ?P36          SBN   0  3
  19 ?PGQ          BNZ   0  OUTSD3              [J IF ERROR ON SCREEN ALREADY
  16 ?Q2B          LDCT  0  #004                [B6=CLEAR SCREEN 
  14 ?QG2          ORS   0  ISMFORM(3)          [AND SET 
   8 ?Q_L          BRN     OUTSD3
   4 ?RF=    #   
   4 ?RYW    #   
  20 ?SDG    # K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 K3 #  
   4 ?SY6    #   
   5 ?TCQ    REPEAT  
   9 ?TMJ ...      VFREEW   FILE,FRB 
  11 ?TXB          MHUNTW   3,AMXOR,ASCREEN  
  17 ?WC2          TRACE    3,ISREPEAT          [<<<<<<<<<<<<<<<<<<<<
   5 ?WWL    OUTSD3  
  16 ?XB=          LDCT  0  #030               [LINE 3,TEXT RDY  
  19 ?XTW          STO   0  ISMERR(3)           [LINE 1 FOR ERRORS & COMMENTS
  18 ?Y8? ...      TRACE    ISTEXTCT(3),ISOUTLEN   [<<<<<<<<<<>>>>>>>>>> 
  16 ?YGN ...      OUTEXES     ISTEXTCT(3),ISTEXT,AMXOR,ASCREEN  
  10 ?YT6          READY        SBREAKIN 
   4 ?_#Q    #   
   4 ?_SB    #   
   4 #2#2    #   
   4 #2RL    #   
  21 #3?=    # ================================================================= #   
   4 #3QW    #   
   4 #4=G    #   
   4 #4Q6    #   
   4 #59Q    #   
   4 #5PB    #   
  14 #692    #              DECIDE WHAT TO DO WITH INPUT 
   4 #6NL    #   
   4 #78=    #   
  10 #7MW          MHUNTW   3,FILE,FRB   
   8 #87G          LDX   0  A1(3)
  16 #8M6          BNG   0  SDR1                [J IF CONTROL CH 
   8 #96Q    #     INPUT IS TEXT 
  11 #9LB          MHUNTW   3,AMXOR,ASCREEN  
   9 #=62          LDN   0  ISBITCE  
  17 #=KL          ANDX  0  ISBITS(3)           [COMMAND EXPECTED?   
  14 #?5=          BNZ   0  SCOMMAND            [J IF SO 
  15 #?JW          BRN      SDATA               [MUST BE DATA
   8 ##4G    #     ACTION KEYS   
   4 ##J6    SDR1
   8 #FY2          LDX  0  A1(3) 
   8 #GCL          ANDN  0  #7777
   7 #GX=          SBN   0  1
   8 #HBW          ADN   3  A1+2 
   7 #HWG          SRC   0  2
   7 #JB6          ADX   3  0
   8 #JTQ          LDCH  0  0(3) 
  16 #K5J ...      ANDN  0  #17                 [LOOSE ANY SHIFT 
  17 #K*B          TRACE    0,ISACTION          [<<<<<<<<<<>>>>>>>>>>
  14 #KT2          BXL   0  SS6(1),REPEAT       [J IF 0-5
  14 #L#L          BXL   0  SS8(1),SCONTROL     [J IF 6-7
  14 #LS=          BRN      REPEAT              [     7-9
   4 #M?W    #   
   4 #MRG    #   
   4 #N?6    #   
   4 #NQQ    #   
   4 #P=B    #   
  21 #PQ2    # ================================================================= #   
   4 #Q9L    #   
   4 #QP=    #   
   4 #R8W    #   
   4 #RNG    #   
  11 #S86    #              COMMAND ANALYSIS 
   4 #SMQ    #   
  12 #T7B    #              NOW WE HAVE A COMMAND
   9 #TM2    #              OUTMON IT
  16 #W6L    #              SYNTAX CHECK IT (EXCEPT FOR DIRECTION
  17 #WL=    #                               OF %A AND %B OF T OR P) 
  15 #X5W    #              BRANCH TO SCOMMAND<COMMAND NAME> 
   4 #XKG    #   
   4 #Y56    #   
   5 #YJQ    SCOMMAND
  11 #_4B          MHUNTW   3,AMXOR,ASCREEN  
   9 #_J2          LDN  0  ISBITCE   
  14 *23L          ERS   0  ISBITS(3)           [CLEAR CE
  15 *2H=          HUNTW    2,FILE,FRB          [FIND COMMAND
  17 *32W          TRACE   2,ISCOMAND          [<<<<<<<<<<>>>>>>>>>> 
   8 *3GG          LDCT  4  #600 
   9 *426          ANDX  4  A1+1(2)  
   9 *4FQ          BNZ   4  SCOMMUP  
   7 *4_B          ADN   4  1
   5 *5F2    SCOMMUP 
   8 *5YL          ADX   4  A1(2)
   7 *6D=          SLC   4  2
   8 *6XW          SBN   4  12   
  14 *7CG          BZE   4  SETFPTR          [J IF NULL  
  17 *7FP ...      LDN  0  80                   [LENGTH OF TOP LONE  
  17 *7HY ...      SBX   0  ISTEXTCT(3)         [ - LENGTH OF LINE NO
  14 *7L7 ...      SBN   0  3                   [ - ^<- ^
  17 *7NB ...      BXL   4  0,SCOMMLENOK        [J IF WITHIN LIMITS  
  19 *7QK ...      LDX   4  0                   [ELSE RESET TO MAX POSSIBLE  
   6 *7SS ...SCOMMLENOK  
  13 *7X6          STO   4  7            [SAVE COUNT 
   9 *8BQ          ADN   2  A1+2-1   
   8 *8WB          LDCT  6  #600 
  16 *9B2          ORX   2  6              [PTR TO LAST CHAR - 1 
  16 *9TL          CALL  6  NONSPACE        [TEST FOR NULL INPUT 
  13 *=*=          BRN      SETFPTR        [IT WAS   
  15 *=SW          LDX   4  7               [RESTORE COUNT   
   9 *?#G          LDX   5  SSKATC(1)
  11 *?S6          OUTMON   4,A1+2,5,FILE,FRB
  10 *#?Q          MHUNTW   2,FILE,FRB   
  11 *#RB          MHUNTW   3,AMXOR,ASCREEN  
   9 **?2          ADN   2  A1+2-1   
   8 **QL          LDCT  6  #600 
  18 *B==          ORX   2  6                   [PTR TO CHAR BEFORE FIRST
   9 *BPW          CALL  6  NONSPACE 
  13 *C9G          NULL              [CANT BE NULL   
   4 *CP6    #   
  11 *D8Q          BXU   0  SSF(1),SCOMNOTF  
   9 *DNB          CALL  6  NONSPACE 
  19 *F82          BRN      SCOMMANDF          [J IF F               **** F  
  10 *FML          LDX   5  ISOCURRENT(3)
  10 *G7=          STO   5  ISOPREVCOM(3)
  10 *GLW          LDX   5  ISNCURRENT(3)
  10 *H6G          STO   5  ISNPREVCOM(3)
   9 *HL6          BRN      SYNERR   
   5 *J5Q    SCOMNOTF
  10 *JKB          LDX   5  ISOCURRENT(3)
  10 *K52          STO   5  ISOPREVCOM(3)
  10 *KJL          LDX   5  ISNCURRENT(3)
  10 *L4=          STO   5  ISNPREVCOM(3)
  14 *LHW          STOZ     5                   [R MARKER
  15 *M3G          BXU   0  SSR(1),SCOMA1       [J IF NOT R  
   9 *MH6          LDN   5  ISBITR   
  14 *N2Q          ORS   5  ISBITS(3)           [SET R   
   9 *NGB          CALL  6  NONSPACE 
  14 *P22          BRN      SYNERR              [R WHAT? 
   5 *PFL    SCOMA1  
   8 *PKH ...      SMO      FX2  
  17 *PPD ...      STO   0  AWORK4             [SAVE COMMAND LETTER  
  15 *PT* ...      BXE   0  SSI(1),SCOMAT      [I GOES TO T  
  14 *P_=          BXE   0  SST(1),SCOMAT       [J IF T  
  14 *QDW          BXE   0  SSP(1),SCOMAP       [J IF P  
  12 *QYG    #     MUST BE F E F OR T BY DEFAULT 
  10 *RD6          BXE   0  SSQ(1),SCOMZ1
  10 *RXQ          BXE   0  SSE(1),SCOMZ1
  10 *SCB          BXE   0  SSF(1),SCOMZ1
   7 *SX2    #     ASSUME T  
  14 *TBL          ORX   0  GSIGN        [AND REMEMBER   
   9 *TW=          BRN      SCOMAT   
   5 *W*W    SCOMZ1  
  14 *WTG          BNZ   5  SSROTKINA        [ERR IF R   
  15 *X*6          STO   0  7                [COMMAND LETTER 
   9 *XSQ          CALL  6  NONSPACE 
  16 *Y#B          BRN      SCOMZ2            [OK NO MORE CHARS  
  15 *YS2          BRN      SYNERR            [TOO MANY CHARS
   5 *_?L    SCOMZ2  
  18 *_R=          BXE   7  SSQ(1),SCOMMANDQ     [               **** Q  
  18 B2=W          BXU   7  SSF(1),SCOMMANDE    [               **** E   
  18 B2QG          BRN      SCOMMANDF           [                **** F  
   5 B3=6    SCOMAT  
   9 B3PQ          LDN   6  ISBITT   
   9 B49B          BRN      SCOMATP  
   5 B4P2    SCOMAP  
   9 B58L          LDN   6  ISBITP   
   5 B5N=    SCOMATP 
  15 B67W          ORS   6  ISBITS(3)           [SET T OR P  
  10 B6MG          LDN   6  ISBITA+ISBITB
   9 B776          ORS   6  ISBITS(3)
  18 B7LQ          ERS   6  ISBITS(3)           [SET %A & %B AS NUMBERS  
  18 B86B          STOZ     ISPARAMB+1(3)         [DEFAULT OF 0 FOR %A   
  10 B8L2          STOZ     ISPARAMB(3)  
  15 B95L          BNG   0  SCOMDEFT     [JI F T BY DEFAULT  
   9 B9K=          CALL  6  NONSPACE 
  14 B=4W          STOZ     2                   [SET EOL 
   9 B=JG          BZE   2  SCOMAB9  
   5 B?46    SCOMDEFT
  17 B?HQ          ANDX  0  SSBITS22(1)        [CLEAR DEFAULT IF ANY 
  15 B#3B          CALL  7  PARAMETER           [ANALYSE %A  
  15 B#H2          BRN      SCOMAB9             [J IF NUMBER 
   9 B*2L          LDN   6  ISBITA   
  15 B*G=          ORS   6  ISBITS(3)           [SET STRING  
   5 B*_W    SCOMAB9 
  10 BBFG          LDN   5  ISPARAMB(3)  
  10 BB_6          LDN   6  ISPARAMA(3)  
  18 BCDQ          MOVE  5  ISPARAMB-ISPARAMA   [MOVE %A TO CORRECT POSN 
   5 BCYB    SCOMAB2 
  16 BD3G ...      STOZ     ISPARAMB+1(3)       [SET 0 IN CASE T 
   8 BD6L ...      SMO      FX2  
  14 BD9Q ...      LDX   5  AWORK4             [I T OR P 
  14 BD#W ...      BXE   5  SSI(1),SCOMAB22    [J IF I   
   8 BDD2          LDN   5  20   
  17 BDXL          STO   5  ISPARAMB+1(3)         [20 DEFAULT FOR %B 
   5 BF7D ...SCOMAB22
  10 BFC=          STOZ     ISPARAMB(3)  
  16 BFWW          BZE   2  SCOMAB4             [J IF END OF LINE
  15 BGBG          BXU   0  SSSPACE(1),SCOMAB27 [J IF CHAR   
   9 BGW6          CALL  6  NONSPACE 
  14 BH*Q          BRN      SCOMAB4             [E O L   
   5 BHTB    SCOMAB27
   8 BH_? ...      SMO      FX2  
  14 BJ58 ...      LDX   6  AWORK4             [ I T OR P
  18 BJ95 ...      BXE   6  SSI(1),SYNERR      [EP2 NOT ALLOWED WITH I   
  16 BJ*2          BXU   0  SSCOMMA(1),SYNERR    [SHOULD BE COMMA
   9 BJSL          CALL  6  NONSPACE 
  14 BK#=          BRN      SCOMAB4             [NO %B   
  15 BKRW          CALL  7  PARAMETER           [ANALYSE %B  
  15 BL?G          BRN      SCOMAB33            [J IF NUMBER 
   9 BLR6          LDN   6  ISBITB   
  15 BM=Q          ORS   6  ISBITS(3)           [SET STRING  
   5 BMQB    SCOMAB33
  15 BN=2          BZE   2  SCOMAB4              [J IF EOL   
  20 BNPL          BXU   0  SSSPACE(1),SYNERR     [ERR IF ODD CH AT END OF LINE  
   9 BP9=          CALL  6  NONSPACE 
  14 BPNW          BRN      SCOMAB4            [EOL OK   
  17 BQ8G          BRN      SYNERR              [WOTS THESE ODD CHARS
   5 BQN6    SCOMAB4 
   9 BR7Q          LDN   6  ISBITT   
   9 BRMB          ANDX  6  ISBITS(3)
  18 BS72          BNZ   6  SCOMMANDT           [               **** T   
  18 BSLL          BRN      SCOMMANDP           [               **** P   
   5 BT6=    SETFPTR 
  10 BTKW          LDX   0  ISOCURRENT(3)
  10 BW5G          STO   0  ISOPREVCOM(3)
  10 BWK6          LDX   0  ISNCURRENT(3)
  10 BX4Q          STO   0  ISNPREVCOM(3)
   9 BXJB          BRN      POINTER  
   4 BY42    #   
   4 BYHL    #   
   4 B_3=    #   
   4 B_GW    #   
   4 C22G    #   
  20 C2G6    # K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 K4 #  
   4 C2_Q    #   
   4 C3FB    #   
   4 C3_2    #   
   4 C4DL    #   
  15 C4Y=    #              SORTOUT BREAKIN WHEN ONE DETECTED
   4 C5CW    #   
  18 C5XG    #              TAKE AUTOMATIC E ACTION IF NOT MOP BREAK IN  
   4 C6C6    #   
  14 C6WQ    #              IF PTR RDY REOUTPUT POINTER  
  15 C7BB    #              IF TEXT RDY TERMINATE INSTRUCTION
   5 C7W2    SBREAKIN
  11 C8*L          MHUNTW   3,AMXOR,ASCREEN  
   9 C8T=          LDN   0  ISBITR   
   9 C9#W          ORS   0  ISBITS(3)
  14 C9SG          ERS   0  ISBITS(3)           [CLEAR R 
  15 C=#6          BRKINOFF SERIOUS           [J IF SERIOUS  
  11 C=RQ    #              ONLY MOP BREAK IN
  11 C??B          MHUNTW   3,AMXOR,ASCREEN  
  17 C?R2          TRACE   3,ISMOPBRK           [<<<<<<<<<<>>>>>>>>>>
   8 C#=L          LDCT  0  #002 
  10 C#Q=          ANDX  0  ISMFORM(3)   
  16 C*9W          BZE   0  POINTER             [J IF PTR READY  
  14 C*PG    #              MUST BE DURING AN INSTRUCTION
   9 CB96          LDN  0  ISBITLOP  
   9 CBNQ          ORS   0  ISBITS(3)
  17 CC8B          ERS   0  ISBITS(3)          [FORCE CLEAR SCREEN   
   8 CCN2          LDCT  0  #030 
  14 CD7L          STO   0  ISMERR(3)           [      ; 
  17 CDM=          LDX   0  ISOPAGE(3)          [STEP BACK TO HERE   
  16 CF6W          BNG  0  SSIT                 [J IF NOT VALID  
  16 CFLG          CALL  7  STEPBACKOLD         [... IN OLD FILE 
  11 CG66          MHUNTW   3,AMXOR,ASCREEN  
  17 CGKQ          LDX   0  ISNPAGE(3)          [STEP BACK TO HERE IN
  15 CH5B          CALL  7  STEPBACKNEW         [... NEW FILE
  15 CHK2          BRN      SSIT                [INTR TERM   
   4 CJ4L    #   
   5 CJJ=    SERIOUS 
  18 CK3W          TESTBRKIN  SER2            [TO CLEAR BRK IN BIT ONLY  
   4 CKHG    SER2
  11 CL36          MHUNTW   3,AMXOR,ASCREEN  
  17 CLGQ          TRACE   3,ISABANDN           [<<<<<<<<<<>>>>>>>>>>
   4 CLLX ...#   
   8 CLR4 ...      LDCT  0  #002 
  10 CLX9 ...      ANDX  0  ISMFORM(3)   
  15 CM3B ...      BZE   0  SER1             [J IF ON PTR RDY
   9 CM7H ...#     FORCE CLEAR SCREEN
   8 CM?N ...      LDCT  0  #034 
   9 CMCT ...      STO   0  ISMERR(3)
  12 CMJ2 ...#     MUST BE DURING AN INSTRUCTION 
  12 CMN7 ...#     POSSIBY SOME DATA DISPLAYED   
   8 CMS# ...#     DONT LOOSE IT!
  16 CMYF ...      LDX   0  ISOPAGE(3)           [GO BACK TO HERE
  16 CN4L ...      BNG   0  SER1                [BUT J IF INVALID
  16 CN8R ...      CALL  7  STEPBACKOLD          [OK SO STEP BACK
  11 CN#Y ...      MHUNTW   3,AMXOR,ASCREEN  
   4 CNF=    #   
   4 CRWL    SER1
  13 CSB=          LDX   5  ISMFORM(3)          [SAVE
  17 CSTW          LDX   0  ISMERR(3)           [ERR MESS PLACEMENT  
  10 CT*G          STO   0  ISMFORM(3)   
  17 CTT6          LDX   6  ISOCURRENT(3)      [CURRENT OLD FILE PTR 
  17 CW#Q          LDX   7  ISNCURRENT(3)      [CURRENT NEW FILE PTR 
   8 CWSB          OUTBLOCKN   6 
   8 CX#2          OUTNUM   6,0  
   8 CXRL          OUTNUM   7,0  
   9 CY?=          MONOUT   FILEUP   
  11 CYQW          MHUNTW   3,AMXOR,ASCREEN  
  14 C_=G          STO   5  ISMFORM(3)          [RESET   
   8 C_Q6          LDCT  0  #020 
  19 D29Q          ADS   0  ISMERR(3)           [EXTRA LINE FOR BIG MESSAGE  
   8 D2PB          LDCT  0  #004 
   9 D392          ORS   0  ISMERR(3)
  17 D3NL          ERS   0  ISMERR(3)           [CLEAR CLEAR SCREEN  
  17 D48=          BRN      SCOMMANDE          [AUTOMATIC E ACTION   
   4 D4MW    #   
   4 D57G    #   
  21 D5M6    # ================================================================= #   
   4 D66Q    #   
   4 D6LB    #   
   4 D762    #   
   4 D7KL    #   
   4 D85=    #   
  15 D8JW    #              OUTPUT MESSAGE THEN POINTER READY
  17 D94G    SSYROTBOTF     [YOUVE RUN OFF THE BEGINNING OF THE FILE 
   9 D9J6          LDN   3  SYROTBOTF
   9 D=3Q          BRN      SSOUTCOM 
  17 D=HB    SSYRYBSL       [YOU'VE REACHED YOUR BACKING STORE LIMIT 
   9 D?32          LDN   3  SYRYBSL  
   9 D?GL          BRN      SSOUTCOM 
  17 D#2=    SSE2MBAFR      [ENDPOINT2 MUST BE A FORWARD REFERENCE   
   9 D#FW          LDN   3  SE2MBAFR 
   9 D#_G          BRN      SSOUTCOM 
  17 D*F6    SSROTKINA      [REPETITION OF THIS KIND IS NOT ALLOWED  
   9 D*YQ          LDN   3  SROTKINA 
   9 DBDB          BRN      SSOUTCOM 
  16 DBY2    SSBTNA        [BACKWARDS TRANSCRIPTION NOT ALLOWED  
   8 DCCL          LDN   3  SBTNA
   9 DCX=          BRN      SSOUTCOM 
  16 DDBW    SSYROTEOTF     [YOU'VE RUN OFF THE END OF THE FILE  
   9 DDWG          LDN   3  SYROTEOTF
   9 DFB6          BRN      SSOUTCOM 
  16 DFTQ    SSFMNBUITC     [IF MAY NOT BE USED IN THIS CONTEXT  
   9 DG*B          LDN   3  SFMNBUITC
   9 DGT2          BRN      SSOUTCOM 
  14 DH#L    SSYFYNF        [YOU'VE FILLED YOUR NEW FILE 
   9 DHS=          LDN   3  SYFYNF   
   9 DJ?W          BRN      SSOUTCOM 
  13 DJRG    SSIT           [INSTRUCTION TERMINATED  
   8 DK?6          LDN   3  SIT  
   9 DKQQ          BRN      SSOUTCOM 
  11 DL=B    SSNTL          [NUMBER TOO LARGE
   8 DLQ2          LDN   3  SNTL 
   9 DM9L          BRN      SSOUTCOM 
  11 DMP=    SSEA           [EDIT ABANDONED  
   8 DN8W          LDN   3  SEA  
   9 DNNG          BRN      SSOUTCOM 
  10 DP86    SSEOE          [END OE EDIT 
   8 DPMQ          LDN   3  SEOE 
   5 DQ7B    SSOUTCOM
  15 DQM2          ADX   3  FX1               [ABS PTR TO EG 
  16 DR6L          LDX   6  0(3)                [PTR TO MESSAGE  
   9 DRL=          CALL  7  SCOMMENT 
   9 DS5W          BRN      POINTER  
   4 DSKG    #   
   4 DT56    #   
   4 DTJQ    #   
   4 DW4B    #   
  21 DWJ2    # ================================================================= #   
   4 DX3L    #   
   4 DXH=    #   
   4 DY2W    #   
   4 DYGG    #   
   4 D_26    #   
   4 D_FQ    #   
   4 D__B    #   
   4 F2F2    #   
  21 F2YL    # ================================================================= #   
   4 F3D=    #   
   4 F3XW    #   
   4 F4CG    #   
   4 F4X6    #   
   4 F5BQ    #   
   4 F5WB    #   
   4 F6B2    #   
  13 F6TL    #              SWITCH TO OTHER CHAPTERS 
   5 F7*=    SDATA   
  10 F7SW          ACROSS   SCEDITAR,1   
   5 F8#G    SCONTROL
  10 F8S6          ACROSS   SCEDITAR,2   
   6 F9?Q    SCOMMANDT   
  10 F9RB          ACROSS   SCEDITAR,3   
   6 F=?2    SCOMMANDP   
  10 F=QL          ACROSS   SCEDITAR,4   
   6 F?==    SCOMMANDE   
  10 F?PW          ACROSS   SCEDITAR,5   
   6 F#9G    SCOMMANDF   
  10 F#P6          ACROSS   SCEDITAR,6   
   6 F*8Q    SCOMMANDQ   
  10 F*NB          ACROSS   SCEDITAR,7   
   4 FB82    #   
   4 FBML    #   
   4 FC7=    #   
   4 FCLW    #END
   6 ____ ...235266220005
