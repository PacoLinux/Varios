   7 22FL    #OPT  K0GIVE=0  
  14 22_=    #LIS  K0GIVE>K0OBJPROG>K0ALLGEO>K0GREATGEO  
   7 23DW ...#SEG  GIVE865   
   4 23JR ...[   
  15 23NN ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   4 23SK ...[   
   6 23YG          8HGIVE
   4 24D6    #   
   4 24XQ    #   
  18 25CB    [           THIS SEGMENT IS ENTERED FROM OPCA TO PROCESS THE
  20 25X2 ...[     165 (GIVE) ORDER WITH N(M) = 4 OR 12. SEGMENT GIVEA DEALS WITH
  20 26BL ...[     ALL OTHER VALUES OF N(M). (GIVE USED TO DEAL WITH THEM ALL BUT
   9 26W= ...[     BECAME TOO BIG.)  
   4 27TG    #   
   8 28*6    [     ENTRY POINT   
  10 28SQ          SEGENTRY K1GIVE,ZEP1  
   4 29#B    #   
   6 2?QG    XK9   +K9   
   6 2#=6    XK    +1024 
   6 2#PQ    XMINK -1024 
   4 2DLQ    #   
  18 2F6B    ZEP1                               [ENTRY POINT FROM OPCA   
  19 2FL2          LDX   7  EVENT5(2)           [ISOLATE X-FIELD OF EXTRACODE
   7 2G5L          SLC   7  3
   7 2GK=          ANDN  7  7
   7 2H4W          DATUMA   3
   7 2HJG          MACCS  ,3 
   7 2J46          LDX   3  0
   7 2JHQ          ADX   3  7
  17 2K3B          LDXC  4  EVENT2(2)           [(MODIFIED) OPERAND  
  16 2KF3 ...      BCS      (GEOERR)            [MUST BE 4 OR 12 
   7 2KWN ...      SBN   4  4
  16 2L#* ...      BZE   4  XG4                 [J IF N(M) = 4   
   7 2LQ2 ...      SBN   4  8
   8 2M7M ...      BZE   4  XG12 
  21 2MK# ...      BRN      (GEOERR)            [IF N(M) NOT 4 OR 12 AS FOUND BY OPCA
   4 2N2_ ...#   
  19 2NDQ    STSLR STO   4  0(3)                [STORE SINGLE-LENGTH REPLY   
   8 2NYB    #SKI  K6GIVE>99-99  
   9 2PD2          TRACE    4,GIVESLR
   7 2PXL    TC    EVENTFIN  
   4 2QC=    #   
   4 36KW    XG12
  12 375G    [     GIVE(12) NEW ACTIVE CORE SIZE 
  20 37K6    [ CORE GIVEN MESSAGE INCLUDED BY G12MESS MACRO - HENCE THE LOCATING 
  16 384Q    [     UNIVERSALS K2GIVE,K3GIVE DEFINED BY SEGENTRIES
   8 3#FB          CALL  6  SIZEA
   9 3#_2          TXL   4  BCORSZ(1)
  19 3*DL          BCS      XG12B               [BRN IF REQUEST<CURRENT TOTAL
  19 3*Y=          LDX   4  BCORSZ(1)           [ELSE SET REPLY=CURRENT TOTAL
   5 3BCW    XG12B   
  17 3BXG          TXU   4  ACORSZ(1)           [EXIT IF REQUEST =   
  17 3CC6          BCC      STSLR                [PRESENT ACTIVE CORE
   6 3CWQ          UNPLUG
   8 3DBB          CALL  6  TPTST
   7 3DW2          DATUMB   6
   8 3F*L          ADDRESS  6,7,3
  20 3FT=    #SKI  ELLS                         [ ACTIVATE CORE SCHEDULER TO USE 
  17 3G#W          KICKASWINT                   [   NEWLY FREED CORE 
   8 3GSG          STO   4  0(3) 
   8 3H#6    #SKI  K6GIVE>99-99  
  10 3HRQ          TRACE    4,GIV12REP   
   6 3J?B          CAPCA 
   7 3JR2          SWAPOUT   
  18 3K2S ...      CALL  3  XINVFPB             [INVALIDATE ANY VALID FPB
   8 3K=L          FPCACA   1,2  
   9 3KQ=          STO   4  ALIMIT(1)
  10 3L9W          HUNT2    1,BSTB,BSCB  
   7 3LPG          LDX   6  4
  18 3M96          SBN   6  1                   [ENSURE A REMAINDER IN   
  18 3MNQ          DVS   5  BSSS                 [THE RANGE 1 - [BSSS],  
  17 3N8B          ADN   5  1                    [NOT 0 - ([BSSS]-1) 
   7 3NN2          ADN   6  1
  15 3P7L          STO   4  ACORSZ(1)           [UPDATE SCB  
  10 3PM=          STO   5  AWRDNUM(1)   
   9 3Q6W          STO   6  ASHEET(1)
   7 3QLG    [ K2GIVE ****   
   9 3R66          SEGENTRY K2GIVE   
   8 3RKQ          BRN      XG4Z1
   4 3S5B    #   
  21 3SK2    SIZEA NGN   0  128                 [ACTIVE LIMIT-ROUND TO MULT OF 128   
   8 3T4L          BRN      SZD  
  20 3TJ=    SIZE  NGN   0  64                  [TOTAL LIMIT -ROUND TO MULT OF 64
   8 3W3W    SZD   LDX   4  0(3) 
  15 3WHG          BNG   4  SZ                  [ERROR IF -VE
  16 3X36          BZE   4  SZ                  [ERROR IF ZERO   
   7 3XGQ          SBN   4  1
  17 3Y2B          SBX   4  0                   [V IS CLEARED BY CALL
  16 3YG2          BVSR     SZ                  [ERROR IF TOO BIG
   7 3Y_L          ANDX  4  0
   7 3_F=          LDN   5  1
   9 3_YW          LDXC  0  ASU2(2)  
   7 42DG          SBN   5  0
   9 42Y6          LDXC  0  ASU3(2)  
   7 43CQ          SBN   5  0
   9 43XB          LDXC  0  ASU4(2)  
   7 44C2          SBN   5  0
  19 44WL          BNG   5  SZB                 [J IF > 1 MEMBER NON-EXISTENT
   8 45B=          LDN   5  128  
  19 45TW          BXL   4  5,SZ                [ ELSE ERROR IF REQUEST < 128
   7 46*G    SZB   FJOPCA   2
   9 46T6          TESTRT   XG12A,2  
   8 47#Q          LDX   2  FX2  
   9 47SB          MHUNTW   1,ASCBT  
   7 48#2          EXIT  6  0
   6 48#N ...#UNS  ISTDP 
   4 48*B ...(   
  20 48B4 ...[     SUBROUTINE TO INVALIDATE A VALID FPB FOR PROGRAM IF ONE EXISTS
  17 48BQ ...[     X3 - LINK, ON EXIT X2=FX2. ENTRY MUST BE IN CPA   
   5 48CD ...XINVFPB 
   8 48D6 ...      FJOCA 2,FX2   
  19 48DS ...      JBCC  XG4Z2,2,JBWASIN        [J IF VALID FPB DOESN'T EXIST
  19 48FG ...      LDX   0  JOBNUM(2)           [USE JOB NO. TO LOCATE FPB   
  17 48G8 ...      LDN   2  BOBJUNUSE           [BASE OF FPB CHAIN   
   5 48GW ...XG4FPB  
  16 48HJ ...      LDX   2  FPTR(2)             [LOAD NEXT FPB   
  10 48J= ...      BXU   2  CXOBJUN,XG4Z3
   9 48JY ...      GEOERR 1,FPBMISNG 
  16 48KL ...XG4Z3 BXU   0  JOBNOWAS(2),XG4FPB  [J IF NOT OUR FPB
  10 48L# ...      STOZ     JOBNOWAS(2)  
   9 48M2 ...      LDX   0  ALOGL(2) 
  19 48MN ...      ADS   0  CINVFPB             [ADD INTO INVALID FPB TOTAL  
   5 48NB ...XG4Z2   
   8 48P4 ...      LDX   2  FX2  
   7 48PQ ...      EXIT  3  0
   4 48QD ...)   
   6 49?=    SZ    UNPLUG
   4 49QW    SZ1 
   9 4==G          ILLEGAL  ILLINS   
   4 4=Q6 ...#   
   5 5286    XG12A   
   8 52MQ          LDX   2  FX2  
   4 537B    XG3 
   4 53M2    #   
  15 546L    [     GIVE(3) CORE STORE ALLOCATED TO PROGRAM   
   4 54L=    #   
  14 5=26          LDX   4  ALIMIT(2)           [GET SIZE
  20 5=FQ          BRN      STSLR               [J TO STORE SINGLE LENGTH REPLY  
   4 5=_B    #   
  20 5#D=    SCBC  NGN   5  1                   [ENTRY FOR MERELY UPDATING SCB   
   8 5#XW          LDX   2  FX2  
   8 5*CG          BRN      SCB1 
  21 5*X6    SCBA  LDN   5  1                   [ENTRY FOR UNPLUG ETC,GETTING LOGICAL
  20 5BBQ          BRN      UNP                 [ FILE NO,AND AVOIDING SWAPOUT   
  21 5BWB    SCZ   NGNC  5  1                   [ENTRY FOR EVERYTHING,AVOIDING S/O   
  15 5CB2    SCB   NGN   5  0                   [NORMAL ENTRY
   8 5CTL    UNP   SBX   3  FX1  
   6 5D*=          UNPLUG
   8 5DSW          ADDRESS  2,7,1
   8 5F#G          STO   4  0(1) 
   8 5FS6    #SKI  K6GIVE>99-99  
  10 5G?Q          TRACE    4,GIVE4REP   
   8 5GRB          CALL  6  TPTST
   8 5H?2          LDX   1  FX2  
   8 5HQL          BNG   5  SCY  
   6 5J==          CAPCA 
   8 5JPW          BNZ   5  SCB1 
   7 5K9G          SWAPOUT   
   8 5KP6    SCB1  FPCACA   1,2  
   8 5L8Q          BNG   5  SCB3 
   8 5LNB    SCY   ADX   3  FX1  
   8 5M82    SCB3  STO   1  GEN6 
  10 5MML          HUNT2    1,BSTB,BSCB  
   9 5N7=          LDX   0  AOBJLF(1)
   9 5NLW          STO   0  AWORK3(2)
   7 5P6G          NGX   0  5
   7 5PL6          LDX   6  4
   7 5Q5Q          SBN   6  1
   8 5QKB          DVS   5  BSSS 
   7 5R52          ADN   6  1
   8 5RJL          BNG   0  SCB2 
   7 5S4=          ADN   5  1
   8 5SHW          SMO      GEN6 
   9 5T3G          STO   4  ALIMIT   
  10 5TH6          LDX   0  BWRDNUM(1)   
   9 5W2Q          STO   0  AWORK1(2)
   9 5WGB          LDX   0  BCORSZ(1)
   9 5X22          STO   0  AWORK2(2)
   9 5XFL          LDX   7  ACORSZ(1)
   9 5X_=          STO   4  ACORSZ(1)
   9 5YDW          STO   4  BCORSZ(1)
  10 5YYG          STO   5  AWRDNUM(1)   
  10 5_D6          STO   5  BWRDNUM(1)   
   9 5_XQ          STO   5  AWORK4(2)
   9 62CB          LDX   5  BSHEET(1)
   9 62X2          STO   6  ASHEET(1)
   9 63BL          STO   6  BSHEET(1)
   7 63W=          EXIT  3  0
   9 64*W    SCB2  LDX   5  BSHEET(1)
   7 64TG          EXIT  3  0
   4 65*6    #   
   8 65SQ    TPTST TESTTP   ,SCTP
   7 66#B          EXIT  6  0
   8 66S2    SCTP  STO   6  GEN6 
  16 67?L          FINDPUC  ,1                  [TRUSTED PROGRAM 
  17 67R=          LDX   6  ALIMIT(1)           [CALC NEW PUC LIMIT  
   9 68=W          SBX   6  ALIMIT(2)
   7 68QG          ADX   6  4
  18 69=6          BNG   6  SZ1                 [ILLEGAL IF PUC LIMIT -VE
  14 69PQ          BZE   6  SZ1                 [ OR ZERO
   7 6=9B          LDN   2  1
   9 6=P2          LDXC  0  ASU2(1)  
   7 6?8L          SBN   2  0
   9 6?N=          LDXC  0  ASU3(1)  
   7 6#7W          SBN   2  0
   9 6#MG          LDXC  0  ASU4(1)  
   7 6*76          SBN   2  0
  21 6*LQ          BNG   2  PUCN                [J IF > 1 MEMBER OF PUC NON-EXISTENT 
   8 6B6B          LDN   0  128  
  20 6BL2          BXL   6  0,SZ1               [ ELSE ERROR IF PUC LIMIT < 128  
   8 6C5L    PUCN  LDX   2  FX2  
  17 6CK=          STO   6  ALIMIT(1)           [SET NEW PUC LIMIT   
   9 6D4W          BRN      (GEN6)   
   4 6DJG    #   
  21 6F46    NUSHT          [INSERTS EXTRA SHEET NUMBERS IN SCB.IF ACOM7=0 WE TAKE   
  21 6FHQ                   [[X6] ENTRIES FROM A FULLB BLK;IF NOT THEN IT IS SHEET NO
   8 6G3B          STO   7  GEN4 
  10 6GH2          LDX   7  ACOMMUNE7(2) 
   8 6H2L          BPZ   7  NUSH1
  10 6HG=          MHUNTW   1,BSTB,FULLB 
   7 6H_W    NUSH1 FPCACA   2
  10 6JFG          HUNT2    2,BSTB,BSCB  
   8 6J_6          BPZ   7  NUSH2
   8 6KDQ          STO   1  GEN5 
   8 6KYB          ADN   1  A1+2 
   7 6LD2          ADX   2  5
   9 6LXL          ADN   2  AOBJST   
   7 6MC=          SMO      6
   7 6MWW          MOVE  1  0
   8 6NBG          FREECORE GEN5 
   9 6NW6          BRN      (GEN4)   
   7 6P*Q    NUSH2 SMO      5
   9 6PTB          STO   7  AOBJST(2)
   9 6Q*2          BRN      (GEN4)   
   4 6QSL    #   
  21 6R#=    XGET1          [GETS A SINGLE BACKING STORE SHEET AFTER TRANSFER FAILURE
   8 6RRW          SBX   7  FX1  
   8 6S?G          LDX   2  FX2  
  11 6SR6          GETBACK  AWORK3(2),PRIV   
   8 6T=Q          ADX   7  FX1  
   7 6TQB          EXIT  7  0
   4 6W=2    #   
  19 6WPL    XGET2          [GETS A SINGLE BACKING STORE SHEET FOR NORMAL USE
   8 6X9=          SBX   7  FX1  
   8 6XNW          LDX   2  FX2  
   9 6Y8G          GETBACK  AWORK3(2)
   8 6YN6          ADX   7  FX1  
   7 6_7Q          EXIT  7  0
   4 6_MB    #   
   8 7272    ZERO  STOZ     0(1) 
   7 72LL          SBN   3  2
   8 736=          STOZ     1(1) 
   8 73KW          LDN   2  2(1) 
   8 745G          SBNC  3  512  
   8 74K6          BCS      ZEZ  
   7 754Q    ZEY   MOVE  1  0
  17 75JB          ADN   2  512                 [NO NEED TO UPDATE X1
   8 7642          SBNC  3  512  
   8 76HL          BCC      ZEY  
   8 773=    ZEZ   MOVE  1  0(3) 
   8 77GW          LDX   2  FX2  
   7 782G    ZEX   EXIT  0  0
   4 78G6    #   
   8 78_Q    STOZ  SBX   7  FX1  
   9 79FB          LDX   3  AWORK2(2)
   8 79_2          ADN   3  1023 
   8 7=DL          SMO      FX1  
   8 7=Y=          ANDX  3  XMINK
   9 7?CW          SBX   3  AWORK2(2)
   8 7?XG          BZE   3  STA  
   7 7#C6          TXL   4  3
   8 7#WQ          BCC      STB  
   7 7*BB    STC   LDX   3  4
   7 7*W2    STB   SBX   4  3
  12 7B*L          GETWORD  AWORK2(2),1,WRITE,3,3
   9 7BT=          ADS   3  AWORK2(2)
   8 7C#W          CALL  0  ZERO 
   8 7CSG          BZE   4  STX  
   8 7D#6    STA   SMO      FX1  
   8 7DRQ          TXL   4  XK   
  16 7F?B          BCS      STC                 [BRN IF <1K LEFT 
   8 7FR2          LDN   3  1024 
  18 7G=L          BRN      STB                 [ELSE LOOP TO ZEROISE 1K 
   7 7GQ=    STX   WORDFIN   
   8 7H9W          ADX   7  FX1  
   7 7HPG          EXIT  7  0
   4 7JNQ    #   
   4 7K8B    #   
  15 7KN2    [     GIVE(4) NEW CORE ALLOCATION IF POSSIBLE   
   4 7L7L    #   
   4 7MLG    XG4 
   8 7N66          CALL  6  SIZE 
   9 7NKQ          LDX   6  BCORSZ(1)
   7 7P5B          TXL   6  4
  19 7PK2          BCS      XG4A                [BRN IF REQUEST>CURRENT TOTAL
   7 7Q4L          TXL   4  6
  19 7QJ=          BCS      YG4A                [BRN IF REQUEST<CURRENT TOTAL
   9 7R3W    XG4V  TXU   6  ACORSZ(1)
   8 7RHG          BCC      STSLR
  18 7S36    XG4B                                [ACTIVE=CURRENT TOTAL   
   8 7SGQ          CALL  3  SCB  
  18 7SQJ ...      CALL  3  XINVFPB             [INVALIDATE ANY VALID FPB
   4 7T2B    YG4B
   7 7TG2          SBX   5  6
   8 7T_L          BZE   5  XG4Z 
   7 7WF=          SBN   5  1
   8 7WYW          BNZ   5  XG4AA
  19 7XDG          SMO      6                   [ONLY ONE SHEET TO BE FREED  
  15 7XY6          LDX   7  AOBJST(1)           [SHET NUMBER 
   7 7YCQ          LDN   6  1
  18 7YXB          SBS   6  ALOGLEN(1)          [SHORTEN SCB BY ONE WORD 
  16 7_C2          FREEBACK AOBJLF(1),7         [FREE THE SHEET  
   8 7_WL          BRN      XG4Z 
   7 82B=    XG4AA ADN   5  3
  11 82TW          SETUPCORE 5,3,BSTB,FULLB  
   8 83*G          FPCACA   1,2  
  10 83T6          HUNT2    1,BSTB,BSCB  
   8 84#Q          STO   5  A1(3)
   7 84SB          SBN   5  2
   9 85#2          LDX   0  AOBJLF(1)
   9 85RL          STO   0  A1+1(3)  
   9 86?=          ADN   6  AOBJST(1)
   9 86QW          LDN   7  A1+2(3)  
   7 87=G          SMO      5
   7 87Q6          MOVE  6  0
  10 889Q          LDX   6  ALOGLEN(1)   
   7 88PB          SBX   6  5
   7 8992          LDX   7  1
   8 89NL          ALTLEN   7,6  
   7 8=8=          FREEBAX   
  10 8=MW          MFREEW   BSTB,EMPTYB  
   8 8?7G          BRN      XG4Z 
   4 8?M6    YG4A
   7 8#6Q          NGNC  0  1
   9 8#LB          TXL   4  ACORSZ(1)
  13 8*62          BCC      XG4B                [BXG 
   8 8*KL          CALL  3  SCZ  
   7 8B5=          TXU   4  7
   8 8BJW          BCC      YG4Y 
  10 8C4G    [     J IF REQ =CURR ACTIVE 
  10 8CJ6          HUNT2    1,AOBJPROG,0 
   7 8D3Q          SBX   7  4
  19 8DHB          SBS   7  COBJUSE             [DECREMENT CORE USED FOR O/PS
   7 8F32          LDX   7  1
   6 8F4Y ...#UNS CA1D   
   4 8F6W ...(   
   8 8F8S ...      ADX   4  CA1D 
   8 8F=Q ...      SBN   4  A1   
   4 8F#N ...)   
   6 8FBL ...#UNS CA1D   
   4 8FDJ ...#SKI
   9 8FGL          ADN   4  A1D-A1   
  18 8G2=          ALTLEN   7,4                 [SHORTEN PROGRAM BLOCK   
  20 8GFW    #SKI  ELLS                         [ ACTIVATE CORE SCHEDULER TO USE 
  17 8G_G          KICKASWINT                   [   NEWLY FREED CORE 
   6 8H3D ...#UNS CA1D   
   4 8H5B ...(   
   8 8H7# ...      SBX   4  CA1D 
   8 8H9= ...      ADN   4  A1   
   4 8H?8 ...)   
   6 8H*6 ...#UNS CA1D   
   4 8HC4 ...#SKI
   9 8HF6          SBN   4  A1D-A1   
   6 8HYQ    YG4Y  CAPCA 
   8 8JDB          FPCACA    1,2 
  10 8JY2          HUNT2     1,BSTB,BSCB 
   7 8KCL          BRN   YG4B
   4 8KX=    #   
   4 8LBW    XG4A
   7 8LWG          FJOPCA   2
  19 8Q*L ...      LDX   5  JSIZE(2)            [SAVE [JSIZE] OVER HLSINFORM 
   9 8QB4 ...      LDX   0  AOBJCORES
  19 8QBG ...      BXGE  0  4,XSIZEOK           [J IF REQUEST <OR= COREOBJECT
  21 8QBY ...      BXL   5  4,XSIZEOK           [J IF REQUEST > JSIZE(MAXSIZE ETC.)  
   7 8QCB ...      LDX   3  4
  21 8QCS ...      ADN   3  1023                [LOAD SIZE AND ROUND UP TO NEXT 'K'  
  16 8QD= ...      SRL   3  10                  [ FOR HLSINFORM  
   4 8QDN ...[   
  21 8QF6 ...[     AS WE ARE IN PCA AND NEED TO BE IN CPA FOR HLSINFORM,WE NEED TO   
  21 8QFJ ...[     UNPLUG PROGRAM AND RETURN TO CPA.HOWEVER,AFTER HLSINFORM,WE NEED  
  21 8QG2 ...[     TO RETURN TO PCA AS CODE LATER(IN SUBR. SCBA) DOES A CAPCA TO GET 
   4 8QGD ...[   
   7 8QGW ...[     TO THE CPA
   4 8QH# ...[   
   6 8QHQ ...     UNPLUG 
   6 8QJ8 ...     CAPCA  
  19 8QJL ...[     INFORM HLS THAT SIZE REQUIRED IS > COREOBJECT & < MAXSIZE 
   4 8QK4 ...[   
  15 8QKG ...      HLSINFORM XBRKIN,MAXSIZE,JOBNO(2),3,,,XOK 
  16 8QKY ...      LDX   5  AOBJCORES           [REQUEST DENIED  
  16 8QLB ...XOK                                [REQUEST GRANTED 
   8 8QLS ...      PCAPLUG XBRKIN
   8 8QM= ...      BRN      XG4BB
   5 8QMN ...XBRKIN  
  21 8QN6 ...      STEPBACK                     [IN CASE OF BREAK IN WE STEP BACK ONR
  17 8QNJ ...      PROGBRKIN                    [ AND REPORT BREAKIN 
   5 8QP2 ...XSIZEOK 
   9 8QPD ...      TXL   5  AOBJCORES
  20 8QPW ...      BCS      XG4BB               [IF [JSIXE] > COREOBJECT,WE USE  
  15 8QQ# ...      LDX   5  AOBJCORES           [ COREOBJECT 
   5 8QRG    XG4BB   
   8 8R?6          LDX   0  AFREE
   7 8RQQ          TXL   0  4
  20 8S=B          BCS      XG4C                [BRN UNLESS ENOUGH FREE CORE LEFT
   7 8SQ2          TXL   5  4
  20 8T9L          BCC      XG4T                [BRN IF REQUEST .LE. COREOBJECT  
  17 8TP=    XG4C  TXL   6  5                   [SET X5 = MAX(X5,X6) 
   8 8W8W          BCS      XG4D 
   7 8WNG          LDX   5  6
  17 8X86    XG4D  LDX   4  6                   [SET X4 = MAX(X6,X0) 
   7 8XMQ          TXL   4  0
   8 8Y7B          BCC      XG4E 
   7 8YM2          LDX   4  0
  17 8_6L    XG4E  TXL   5  4                   [SET X4 = MIN(X4,X5) 
   8 8_L=          BCC      XG4U 
   7 925W          LDX   4  5
   9 92KG    XG4U  ANDX  4  CMIN64   
   7 9356          TXL   6  4
  21 93JQ          BCC      XG4V                [BRN IF AMENDED REQUEST=CURRENT TOTAL
   4 944B    XG4T
   8 94J2          CALL  3  SCBA 
   9 953L          LDX   7  BCORSZ(1)
   9 95H=          SBX   7  ACORSZ(1)
   7 962W          SBX   6  5
  19 96GG          BZE   6  XG4P                [J IF NO EXTRA SHEETS NEEDED 
  18 9726          LDX   3  1                   [ OTHERWISE EXTEND SCB   
  10 97FQ          LDX   0  ALOGLEN(3)   
   7 97_B          ADX   0  6
   9 98F2          STO   0  AWORK1(2)
  10 98YL          ALTLEN   3,AWORK1(2)  
  20 99D=    XG4P  BZE   7  NOSO                [J IF ACTIVE=TOTAL TO AVOID S/O  
   7 99XW          SWAPOUT   
   4 9=CG    NOSO
  20 9=X6          BZE   6  QS0                 [J IF NO EXTRA SHEETS REQUIRED   
  19 9?BQ          BCT   6  SHGT1               [J IF >1 EXTRA SHEET REQUIRED
  20 9?WB          CALL  7  XGET2               [ OTHERWISE,IF JUST ONE,GET IT   
   8 9#B2          BRN      QS0  
   7 9#TL    SHGT1 ADN   6  3
  19 9**=          SETUPCORE 6,3,BSTB,EMPTYB    [GET SHEETS IF >1 REQUIRED   
   8 9*SW          STO   6  A1(3)
   9 9B#G          LDX   0  AWORK3(2)
   9 9BS6          STO   0  A1+1(3)  
   6 9C?Q          GETBAX
  21 9CRB          NGS   6  ACOMMUNE7(2)        [SET -VE IF SHEET INFO IN BSTB/FULLB 
  14 9D?2                                       [ BLOCK  
  21 9DQL    QS0   EXTRACOR 4,YES               [TRY TO EXTEND O/P BLK-J TO YES IF OK
  17 9F==          CALL  3  SCBC                [OTHERWISE UPDATE SCB
  18 9FG4 ...      CALL  3  XINVFPB             [INVALIDATE ANY VALID FPB
   7 9FPW          SBX   6  5
   8 9G9G          BZE   6  XG4SU
  20 9GP6          CALL  7  NUSHT               [ AND INSERT NEW SHEET NUMBERS   
  13 9H8Q          SETNCORE 1024,7,AOBJPROG,BHWRITE  
  19 9HNB          LOCK     7                   [SET UP LOCKED BLOCK OF ZEROS
   8 9J82          ADN   7  A1   
   7 9JML          LDX   1  7
   8 9K7=          LDN   3  1024 
   8 9KLW          CALL  0  ZERO 
   7 9L6G          LDX   3  7
  21 9LL6    XG4H  SBN   6  1                   [DECREMENT NO OF SHEETS NEEDING 0-ING
  20 9M5Q    XG4J  NGN   7  1                   [X7<0 FOR 1ST ATTEMPT AT A WRITE 
  21 9MKB    XG4J1 LDX   4  BSSS                [IF WE ARE ZEROISING THE LAST SHEET, 
  21 9N52          BNZ   6  XG4K                [ FORM COUNT OF NO OF 1K BLOCKS TO   
  15 9NJL          LDX   4  AWORK4(2)           [ BE ZEROISED
   8 9P4=          ADN   4  1023 
   8 9PHW    XG4K  SRL   4  10   
   8 9Q3G          FPCACA   1,2  
  10 9QH6          HUNT2    1,BSTB,BSCB  
  21 9R2Q          BPZ   7  XG4K1               [IF WRITE HAS NOT PREVIOUSLY FAILED, 
   7 9RGB          SMO      5
  17 9S22          LDX   7  AOBJST(1)           [ GET SHEET NUMBER   
   8 9SFL          BRN      XG4K2
  21 9S_=    XG4K1 SMO      5                   [IF WRITE PREVIOUSLY FAILED,REPLACE- 
  17 9TDW          STO   7  AOBJST(1)           [ MENT SHEET NO IN X7
   8 9TYG    XG4K2 LDX   1  FX1  
  16 9WD6    XG4L  WRITEAUTO BSET+ASWAP,XK(1),,AWORK3(2),EX7,3   
  18 9WXQ          ADN   7  8                   [NO OF BLOCKLETS IN 1K   
  20 9XCB          BCT   4  XG4L                [J IF MORE 1K BLOCKS TO ZEROISE  
   7 9XX2    XG4M  ADN   4  1
   8 9YBL          BACKWAIT XG4M 
  18 9YW=          BCT   4  XG4N                [J IF FAILURE HAS OCCURED
  21 9_*W          ADN   5  1                   [OTHERWISE INCREMENT PNTR TO NEXT SHT
  21 9_TG          BNZ   6  XG4H                [ IN SCB,AND J IF MORE SHEETS TO OISE
   8 =2*6          SBN   3  A1   
  19 =2SQ          FREECORE 3                   [FREE LOCKED 1K ZERO BLOCK   
   8 =3#B          LDX   3  FX2  
   9 =3S2          LDX   4  FPTR(3)  
  16 =4?L    XG4SS LDX   3  4                   [FREE ANY FQBLKS 
   9 =4R=          LDX   4  FPTR(3)  
   9 =5=W          LDX   0  ATYPE(3) 
   8 =5QG          TXL   0  CACT 
   8 =6=6          BCC      XG4ST
   8 =8N=          BRN      XG4SS
   4 =97W    #   
  21 =9MG    XG4N  CALL  7  XGET1               [IF A BS FAIL OCCURS,GET A NEW SHEET 
  10 ==76          LDX   7  ACOMMUNE7(2) 
  16 ==LQ          BRN      XG4J1               [ AND TRY AGAIN  
   4 =?6B    #   
  21 =?L2    YES   CALL  3  SCBC                [IF O/P WAS SUCCESSFULLY EXTENDED,   
  15 =*JG          SBX   6  5                   [ UPDATE SCB 
   8 =B46          BZE   6  XG4Z 
   8 =BHQ          CALL  7  NUSHT
   8 =C3B          BRN      XG4Z 
   9 =CH2    XG4SU SBX   4  AWORK2(2)
   8 =D2L          BRN      XG4SV
  21 =DG=    XG4ST LDX   4  BSSS                [ZEROISE NEW AREAS OF OLD ULTIMATE   
  14 =D_W          SBX   4  AWORK1(2)           [ SHEET  
   8 =FFG    XG4SV BZE   4  XG4G 
   8 =F_6          CALL  7  STOZ 
   4 =GDQ    XG4G
   4 =GYB    XG4S
   8 =HD2    XG4Z  FPCACA   3,FX2
   9 =HXL          LDX   4  ALIMIT(3)
   9 =KBG    XG4ZZ LDX   7  ALMT(3)  
   9 =KW6          LDX   0  ADTM(3)  
  19 =L*Q          ADX   0  ALIMIT(3)           [CALCULATE NEW LIMIT FOR EXEC
  18 =LTB          DCH   7  0                   [ AND DUMP MODES BACK IN 
   9 =M*2          STO   0  ALMT(3)  
  18 =MSL          ANDX  7  CACT                [ FLOATING POINT OVERFLOW
  16 =N#=          ORS   7  ALMT(3)             [   AND UNDERFLOW
  19 =NRW          TESTTP   3,XG4NR             [UPDATE PUCS PDA WDS IF ITS  
   8 =P?G          BRN      XG4NS
  14 =PR6    XG4NR FINDPUC  3,3                 [ AN RCTP
   8 =Q=Q          BRN      XG4ZZ
   5 =QQB    XG4NS   
   7 =RPL    [ K3GIVE ****   
   9 =S9=          SEGENTRY K3GIVE   
   7 =SNW          OUTBLOCN 6
  10 =T8G          OUTPACKC 4,1,PROGCORE 
  11 =TN6          OUTPARC  JOBMILL,TIMENOW  
   7 =W7Q          OUTPMILL  
  21 =WMB          MONOUT   COREG4              [PUT CORE GIVEN MESSAGE IN MON. FILE 
  21 =X72          PERCOUNT                     [OUTPUT CNT OF PERIPH TRANSFERS (S/J)
   5 =XLL    XG4Z1   
   8 =Y6=          FJOCA    3,2  
   9 =YKW          LDX   5  JCSIZE(3)
  17 =_5G          ANDX  5  BITS22LS            [REMEMBER OLD SIZE   
   8 =_K6          LDCT  0  #600 
   9 ?24Q          ANDX  0  JCSIZE(3)
   7 ?2JB          ORX   0  4
   9 ?342          STO   0  JCSIZE(3)
   9 ?3HL          LDX   0  COBJQUOTA
  20 ?43=          BXGE  0  4,XG4X              [J IF NEW SIZE < OR = OBJECTQUOTA
  19 ?4GW          BXL   0  5,XG4X              [J IF OLD SIZE > OBJECTQUOTA 
  19 ?52G          MONOUT   BIGPROG             [ ELSE ISSUE BIGPROG MESSAGE 
   4 ?5G6    XG4X
   7 ?5_Q          RUNPROG   
   4 CH5B    #   
   8 CHK2          MENDAREA 25   
   4 CJ4L    #END
   8 ____ ...63372534000200000000
