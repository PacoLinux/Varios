   7 22FL    #SEG  IPBLISTE  
  14 22PD ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS 1982
  11 22_=    #LIS  K0IPBLISTE>K0IPB>K0ALLGEO 
  10 23DW                   8HIPBLISTE   
   4 23YG    [   
  13 24D6          SEGENTRY K1IPBLISTE,SK1IPBLISTE   
  13 25CB          SEGENTRY K3IPBLISTE,SK3IPBLISTE   
  13 25X2          SEGENTRY K4IPBLISTE,SK4IPBLISTE   
   4 26BL    [   
   8 26W=    #DEF  SAFETMARGIN=5 
   8 27*W    SDEL           #76  
   8 27TG    SHIFT          #74  
  14 28*6    PRINT          #76327632,#76207620,#76207620
   9 28SQ    STESC          #76130075
  19 29#B    RECHE                              [SEE IF READ ERROR ON STEP   
  17 29S2          LFBBUS   M,2,(7)             [J IF NO READ ERROR  
  10 2=?L          STO   4  IWORK15(2)   
   7 2=R=          LDN   3  0
  16 2?=W          BRN      WOBL                [J TO CLEAR IOUT 
   4 2?QG    [   
  15 2#=6    [     RE-FIND BLOCK IF IT MOVES DURING ALTLENG  
   4 2#PQ    [   
  10 2*9B    RFND  MHUNTW   2,ADATA,ACONV
   7 2*P2          EXIT  1  0
   4 2B8L    [   
  20 2BN=    [     GET AMXOR/IOUT BLOCK FOR OUTPUT ; WORD COUNT IN X3 ON ENTRY   
   4 2C7W    [   
   9 2CMG    SAMIO LDXC  3  AWORK1(2)
   8 2D76          SRL   3  12   
   7 2DLQ          LDX   6  3
  10 2F6B          SBN   6  SAFETMARGIN  
   7 2FL2          SRL   3  2
   8 2G5L    SETU  SBX   7  FX1  
  14 2GK=          SETNCORE IDATA-A1(3),2,AMXOR,IOUT,1   
   8 2H4W          SMO      FX2  
  19 2HJG          STO   2  ACOMMUNE2           [PRESERVE AMXOR/IOUT ADDRESS 
   7 2J46          LDN   0  1
   8 2JHQ          SMO      FX2  
  19 2K3B          ADS   0  AWORK1              [COUNT OF BLOCKS FOR OUTPUT  
  20 2KH2          STOZ     IMOPTY(2)           [MADE NON-ZERO IF OUTPUT ALONE   
   8 2L2L          LDN   0  IDATA
   7 2LG=          SLL   3  2
  19 2L_W          STO   0  IBSW(2)             [START ADDRESS FOR DEPOSITING
  10 2MFG          SBN   3  SAFETMARGIN  
   8 2M_6          SMO      FX2  
  17 2NDQ          LDX   0  JSOURCE1            [IDENTIFIER NUMBER   
   8 2NYB          ORX   3  GSIGN
   8 2PD2          ANDN  0  #7777
  17 2PXL          STO   0  IPBIDE(2)           [IDENTIFIER NUMBER   
   8 2QC=          ADX   7  FX1  
  18 2QWW          STO   3  IPBIDE+1(2)         [INITIAL ROOM IN BLOCK   
   7 2RBG          EXIT  7  0
   4 2RW6    [   
  14 2S*Q    [              TEST FOR MOP OFF OR FINISH   
   4 2STB    [   
  10 2T*2    TMF   MHUNTW   3,AMXOR,ADCB 
  10 2TSL          LDX   1  A1+FPTR(3)   
  10 2W#=          LDX   0  FOURTHWD(1)  
   9 2WRW          ORX   0  FINISH   
   7 2X?G          SLL   0  2
   8 2XR6          BPZ   0  (7)  
  10 2Y=Q          ACROSS   IPBLISTC,5   
   4 2YQB    [   
  21 2_=2    [     IF SEND 2000 CHARACTER RECORD TO TAPE , DON'T ADD NEWLINE/RUNOUT  
   4 2_PL    [   
   9 329=    TWOTH LDEX  6  FRH(3)   
   8 32NW          SBN   6  502  
  17 338G          BNZ   6  TEXB                [NOT 1997-2000 CHARS 
   8 33N6          LDX   6  CACT 
  21 347Q          ANDX  6  FRH+1(3)            [ISOLATE CHAR POS OF 1ST UNUSED CHAR 
  16 34MB          BNZ   6  TEXB                [NOT 2000 CHARS  
  21 3572          LDCT  5  #300                [SET 'CONCATENATE NEXT RECORD' MARKER
  19 35LL          ORS   5  IWORK11(2)          [AND ENFORCE WHOLE RESTART   
  19 366=          BRN      START               [AVOID RESTART SECTION UPDATE
   4 36KW    TEXB
   8 375G          LDCT  5  #100 
  10 37K6          ORS   5  IWORK11(2)   
  21 384Q          ERS   5  IWORK11(2)          [UNSET 'CONCATENATE' MARKER , IF SET 
   7 38JB          EXIT  7  0
   6 3942    SK4IPBLISTE 
  18 39HL          CALL  7  TMF                 [CHECK FOR MOP OFF ETC.  
  15 3=3=          CALL  7  SAMIO               [GET NEW IOUT
  16 3=GW          STEPAGAIN                    [REFIND RECORD   
  10 3?2G          LDX   4  IWORK15(2)   
  18 3?G6          BZE   4  REOK                [J TO CONTINUE LISTING   
  21 3?_Q          LDX   5  IWORK1(2)           [RESET X5 IF NOT REACHED START REC.  
  17 3#FB          BRN      STEPOK              [AND GO TO FIND IT   
   4 3#_2    [   
  21 3*DL    [     ENTRY FROM IPBLISTA TO LISTFILE TO A 7020 BULK DEVICE VIA THE IPB 
  19 3*Y=    [     STEP RECORDS TO START RECORD NUMBER , OR TO NEXT RECORD   
  20 3BCW    [     AWORK2 IS UNTOUCHED AS IT CONTAINS GMODE FOR ENTERING LISTEND 
   4 3BXG    [   
   4 3CC6    [   
  12 3CWQ    [     K1 ENTRY FOR LISTING MAINFILE 
   4 3DBB    [   
   6 3DW2    SK1IPBLISTE 
   8 3F*L          CALL  7  SAMIO
   7 3FT=          STEPAGAIN 
   5 3G#W    STEPOK  
   9 3GSG          LDX   0  IWORK9(2)
  17 3H#6          CALL  7  TWOTH               [2000 CHARS TO TAPE? 
   8 3HRQ          BRN      START
   4 3J?B    [   
   4 3JR2    NEWS
   8 3K=L          CALL  7  TMF  
  17 3KQ=          CALL  7  SAMIO               [GET NEW IOUT BLOCK  
   7 3L9W    NXREC LDN   4  0
   6 3LPG          STEP  
   8 3M96          CALL  7  RECHE
  19 3MNQ    REOK                               [RETURN LABEL FROM READ ERROR
  17 3N8B          BZE   3  WOBL                [END OF FILE REACHED 
  20 3NN2          LDX   0  IWORK9(2)           [RESTART SECTION=2 RECS FOR TP   
  17 3P7L          CALL  7  TWOTH               [2000 CHARS TO TAPE? 
  20 3PM=          BPZ   0  VERT                [THIS IS SECOND RECORD OF SECTION
   8 3Q6W          LDCT  4  #10  
  20 3QLG          ADX   0  4                   [ADD 1 TO RESTART SECTION NUMBER 
  20 3R66          BPZ   0  START               [OVERFLOW HAS ALREADY INVERTED B0
   8 3RKQ    VERT  ERX   0  GSIGN
   4 3S5B    [   
  10 3SK2    START LDX   7  IWORK13(2)   
  19 3T4L          STO   3  IWORK15(2)          [SAVE INITIAL RECORD POINTER 
   7 3TJ=          ANDN  7  4
  15 3W3W          BZE   7  NOMON               [NOT MONFILE 
  18 3WHG          LDX   7  1(3)                [MONFILE CATEGORY WORD   
  17 3X36          ANDX  7  IWORK14(2)          [MONFILE DISCARD MASK
  19 3XGQ          BNZ   7  NOMON               [DON'T DISCARD THIS RECORD   
   7 3Y2B          LDN   7  1
  17 3YG2          ADS   7  IWORK10(2)          [UPDATE RECORD NUMBER
   8 3Y_L          BRN      NXREC
   4 3_F=    [   
   9 3_YW    NOMON STO   0  IWORK9(2)
   9 42DG          LDCH  5  IWORK9(2)
  10 42Y6          MHUNTW   2,AMXOR,IOUT 
   8 43CQ          LDX   1  FX2  
   7 43XB          LDCT  6  4
   9 44C2          ANDX  6  IWORK9(1)
  21 44WL          ERS   6  IWORK9(1)           [UNSET FIRST REC OF SECTION BIT IFSET
  18 45B=          BZE   6  NSOS                [NOT FIRST REC OF SECTION
   8 45TW          ANDN  5  #37  
   7 46*G          BCHX  2  /
  18 46T6          DCH   5  IPBIDE+1(2)         [RESTART NO. IN CHAR 1   
   9 47#Q          ANDX  2  BITS22LS 
  10 47SB    NSOS  LDX   6  IPBIDE+1(2)  
  19 48#2          STO   2  ACOMMUNE2(1)        [PRESERVE AMXOR/IOUT ADDRESS 
   8 48RL          ANDN  6  #7777
  21 49?=          ADX   2  IBSW(2)             [POINTER TO DEPOSIT NEXT CHARACTER   
   8 49QW          LDCT  4  #400 
  18 4==G          ANDX  4  IWORK13(1)          [B0 SET IF PERI-TYPE FILE
   7 4=Q6          LDX   5  4
   8 4?9Q          BZE   4  SPON 
   8 4?PB          LDCT  4  #600 
  19 4#92          ANDX  4  1(3)                [POSITION OF LAST CHARACTER  
  20 4#NL          BNZ   4  SPLAS               [LAST WORD OF RECORD SPACE-FILLED
   7 4*8=    SPON  ADN   4  1
  17 4*MW    SPLAS ADX   4  0(3)                [WORD COUNT OF RECORD
   7 4B7G          SLC   4  2
  19 4BM6          SBN   4  12                  [RECORD HEADER AND LAST WORD 
   8 4C6Q    PERY  BPZ   4  PER1 
   7 4CLB          LDN   4  0
  18 4D62    PER1  STO   4  ACOMMUNE5(1)        [NO OF CHARS IN RECORD   
   4 4DKL    [   
  21 4F5=    [     OUTPUT TO REMOTE TAPE PUNCH : GRAPHIC FILE TREATED AS CARD OR LINE
  21 4FJW    [     PRINTER FILE ; DIFFERENT ROUTINE FOR NORMAL OR ALLCHAR TAPE FILE  
   4 4G4G    [   
  21 4GJ6    TOUT  BZE   4  TANL                [NULL RECORD (MAYBE END OF CARD FILE)
  19 4H3Q          LDX   0  5                   [NON-ZERO IF PERI TYPE FILE  
   7 4HHB          ADN   3  1
  18 4J32          LDX   5  4                   [RECORD CHARACTER COUNT  
  20 4JGL          BZE   0  TSTO                [TREAT NON-PERI FILE AS GRAPHIC  
   8 4K2=          ORX   3  GSIGN
  16 4KFW          LDCH  0  0(3)                [MODE INDICATOR  
   9 4K_G          ANDX  3  BITS22LS 
   8 4LF6          LDX   1  FX1  
   7 4LYQ          ADN   3  1
   9 4MDB          TXL   0  SHIFT(1) 
  18 4MY2          BCC      TSTO                [SHIFT : NORMAL MK6 FILE 
   7 4NCL          LDN   4  4
   7 4NX=          ANDX  4  0
  21 4PBW          BNZ   4  SHIN                [B21 SET : PREVIOUS SHIFT MK5 FILE   
   8 4PWG          SMO      FX2  
   9 4QB6          LDX   4  IWORK5   
  19 4QTQ          BNZ   4  TNTP                [ZERO IF TAPE PUNCH TYPE FILE
  18 4R*B          ERN   0  #10                 [TP GRAPHIC MODE IS #10  
  15 4RT2    TNTP  BZE   0  TSTO                [GRAPHIC FILE
   8 4S#L          SBN   0  #12  
  18 4SS=          BZE   0  TSTO                [TR GRAPHIC MODE IS #12  
   8 4T?W          SBN   0  #16  
  17 4TRG          BZE   0  TRUN                [TP MODE #20 : RUNOUT
  21 4W?6          LDN   0  #74                 [MODE 0(TP) ,2 OR 22 : ALPHA MK5 FILE
   8 4WQQ    TSTO  LDX   1  FX2  
  16 4X=B          STO   0  AWORK3(1)           [INITIAL SHIFT   
  17 4XQ2    TJUM  BZE   0  TGRAP               [GRAPHIC TAPE FILE   
   4 4Y9L    [   
  21 4YP=    [     FILE IN 3-SHIFT MODE ; TRANSMIT CONTROL CHARACTERS IN ESCAPE MODE 
   4 4_8W    [   
   8 4_NG          LDX   1  FX1  
   8 5286    SHIN  LDN   4  #75  
   8 52MQ          SMO      FX2  
   9 537B          TXU   4  AWORK3   
  17 53M2          BCS      SLOOP               [INITIAL SHIFT ALPHA 
  21 546L          DCH   4  0(2)                [DEFAULT SHIFT IS ALPHA : STORE BETA 
   7 54L=          BCHX  2  /
   7 555W          SBN   6  1
   8 55KG    SLOOP LDCH  0  0(3) 
   7 5656          SBN   5  1
  13 56JQ          TXL   0  SHIFT(1)            [#74 
  19 574B          BCS      SLOP                [DEPOSIT ORDINARY CHARACTER  
  13 57J2          TXL   0  SDEL(1)             [#76 
  19 583L          BCS      SREM                [REMEMBER ALPHA OR BETA SHIFT
  19 58H=          BZE   5  TANL                [END OF RECORD : ADD NEWLINE 
   7 592W          ADN   5  1
   7 59GG          LDX   0  6
  10 5=26          SBN   0  SAFETMARGIN  
  20 5=FQ          BNG   0  NPUND               [J IF NOT ROOM FOR EXPANDED CHAR 
   7 5=_B          SBN   5  1
   7 5?F2          BCHX  3  /
   8 5?YL          LDCH  0  0(3) 
   7 5#D=          SBN   5  1
   8 5#XW          SBN   0  #14  
  17 5*CG          BNG   0  SESC                [ESCAPE #7600 - #7613
   7 5*X6          SBN   0  5
  20 5BBQ          BNG   0  SNORM               [TRANSFER #7614 - #7620 UNCHANGED
   7 5BWB          SBN   0  6
  17 5CB2          BNG   0  SESC                [ESCAPE #7621 - #7626
   7 5CTL          SBN   0  3
  15 5D*=          BNZ   0  SNORM               [NOT NEWLINE 
   8 5DSW          LDX   1  FX2  
  19 5F#G          SMO      ACOMMUNE2(1)        [START ADDRESS OF ANXOR-IOUT 
  21 5FS6          DSA   6  IPBIDE+1            [IN CASE END OF NPU BEFORE NEXT NL   
   8 5G?Q          LDX   1  FX1  
   8 5GRB    SNORM LDN   0  #76  
  19 5H?2          DCH   0  0(2)                [STORE DELTA PAIR UNCHANGED  
   7 5HQL          BCHX  2  /
   7 5J==          SBN   6  1
   8 5JPW          LDCH  0  0(3) 
   8 5K9G          BRN      SLOP 
   4 5KP6    [   
   8 5L8Q    SESC  LDCH  4  0(3) 
  21 5LNB          ERN   4  #60                 [IN ALPHA SHIFT #7603 BECOMES #761363
  20 5M82          SLL   4  6                   [BUT IN BETA SHIFT #7476136375   
  15 5MML          ADX   4  STESC(1)            [#76130075   
   7 5N7=          LDN   1  4
  18 5NLW          LDN   7  2                   [NO. OF CHARS TO MOVE -1 
   8 5P6G          LDN   0  #74  
   8 5PL6          SMO      FX2  
   9 5Q5Q          TXU   0  AWORK3   
  17 5QKB          BCC      SALP                [ALREADY ALPHA SHIFT 
  21 5R52          DCH   0  0(2)                [ESCAPE CHARS MUST BE SENT IN ALPHA  
   7 5RJL          BCHX  2  /
   7 5S4=          SBN   6  1
  18 5SHW          BPZ   6  SALP                [ONLY ROOM FOR 3 CHARS   
  21 5T3G          LDN   7  3                   [MOVE ALSO #75 TO RESET BETA SHIFT   
   7 5TH6    SALP  SMO      7
  18 5W2Q          MVCH  1  1                   [INSERT ESCAPE CHARACTERS
   7 5WGB          SBX   6  7
   8 5X22          LDX   1  FX1  
   8 5XFL          BRN      SUP3 
   4 5X_=    [   
   8 5YDW    SREM  SMO      FX2  
  16 5YYG          STO   0  AWORK3              [REMEMBER SHIFT  
   8 5_D6    SLOP  DCH   0  0(2) 
   7 5_XQ          BCHX  2  /
   7 62CB    SUP3  BCHX  3  /
   7 62X2          SBN   6  1
  16 63BL          BZE   5  TANL                [END OF RECORD   
   8 63W=          BNG   6  NPUND
  16 64*W          BZE   6  NPUND               [AMXOR/IOUT FULL 
   8 64TG          BRN      SLOOP
   4 65*6    [   
   9 65SQ    TRUN  ADN   1  PRINT+1  
  19 66#B          SLL   5  1                   [N RUNOUTS = 2N LINE CHARS   
  15 66S2          MVCH  1  4                   [#76207620   
   7 67?L          SBX   6  5
   7 67R=          LDX   1  2
   7 68=W          LDX   0  6
  19 68QG          BPZ   6  NUFRM               [ENOUGH ROOM IN IOUT BLOCK   
  20 69=6          ADN   0  SAFETMARGIN         [CAN RUN INTO SAFETY MARGIN HERE 
  15 69PQ          BPZ   0  NUFRM               [ENOUGH NOW  
  19 6=9B          ADX   5  0                   [STORE AS MUCH AS POSSIBLE   
   7 6=P2    NUFRM SBN   1  1
   7 6?8L          SBN   5  5
  19 6?N=          BNG   5  PEND                [NO MORE CHARACTERS NEEDED   
   7 6#7W          SMO      5
  17 6#MG          MVCH  1  1                   [RUNOUT CHARACTERS   
  16 6*76          BPZ   0  PEND                [END OF RECORD   
   8 6*LQ          LDX   2  FX2  
  19 6B6B          NGX   5  0                   [NO. OF CHARS STILL TO STORE 
   9 6BL2          LDXC  6  AWORK1(2)
   8 6C5L          SRL   6  12   
  10 6CK=          MHUNTW   1,AMXOR,IOUT 
  20 6D4W          DSA   6  IPBIDE+1(1)         [OUTPUT CHAR. CT. : BLOCK FULL   
  18 6DJG          CALL  7  SAMIO               [GET NEW AMXOR/IOUT BLOCK
   7 6F46          SRL   5  1
   8 6FHQ          ADN   2  IDATA
   8 6G3B          BRN      TRUN 
   4 6GH2    [   
  21 6H2L    [     FILE IN GRAPHIC MODE ; CONVERT INTO LINE CODE & ADD NEWLINE AT END
   4 6HG=    [   
   8 6H_W    TGRAP LDX   1  FX1  
   8 6JFG    TLOOP LDCH  0  0(3) 
   7 6J_6          SBN   5  1
  13 6KDQ          TXL   0  SHIFT(1)            [#74 
   8 6KYB          BCS      TLOP 
  18 6LD2          LDN   4  #76                 [CONVERT INTO LINE CODE  
  18 6LXL          DCH   4  0(2)                [E.G. #74 BECOMES #7664  
   7 6MC=          BCHX  2  /
   8 6MWW          SBN   0  #10  
   7 6NBG          SBN   6  1
  18 6NW6          DCH   0  0(3)                [IN CASE JUMP TO NPUND   
  16 6P*Q          BNZ   6  TLOP                [NOT END OF NPU  
  21 6PTB          ADN   5  1                   [HAVEN'T DCH CONVERTED RECORD CHAR   
   8 6Q*2          BRN      NPUND
   8 6QSL    TLOP  DCH   0  0(2) 
   7 6R#=          BCHX  2  /
   7 6RRW          BCHX  3  /
   7 6S?G          SBN   6  1
   8 6SR6          BNZ   5  TNOT 
  20 6T=Q    TANL  SMO      FX2                 [ENTRY FROM TOUT FOR NULL RECORD 
  21 6TQB          LDX   0  IWORK5              [ZERO IF PAPER TAPE PUNCH TYPE FILE  
  19 6W=2          BZE   0  PEND                [NEWLINES ALREADY PROVIDED   
   8 6WPL          LDX   1  FX1  
   8 6X9=          SMO      FX2  
  21 6XNW          LDX   0  IWORK11             [B2 SET IF THIS WAS 2000-CHAR RECORD 
   7 6Y8G          SLL   0  2
  18 6YN6          BNG   0  PEND                [CONCATENATE NEXT RECORD 
   7 6_7Q          LDX   0  6
  10 6_MB          SBN   0  SAFETMARGIN  
   8 7272          ADN   1  PRINT
  20 72LL          BNG   0  TULL                [LESS THAN 8 CHARS  SPACE IN IOUT
  21 736=          LDN   0  0                   [ROOM FOR FULL SEPARATOR : NL+3 ROS  
   7 73KW    TULL  ADN   0  8
  16 745G          ORX   1  GSIGN               [POINT TO CHAR 2 
  16 74K6          ANDN  0  #76                 [MAKE COUNT EVEN 
  20 754Q          SBX   6  0                   [UPDATE ROOM LEFT IN AMXOR/IOUT  
   7 75JB          SMO      0
  17 7642          MVCH  1  0                   [NEWLINE + RUNOUTS   
   8 76HL          BRN      PEND 
   8 773=    TNOT  BNZ   6  TLOOP
   4 77GW    [   
  21 782G    [     AMXOR/IOUT BLOCK FULL : END OF NPU ; SEND WHOLE BLOCK IF IT DOES  
  20 78G6    [     NOT CONTAIN NEWLINE ; IF IT DOES , COPY CHARACTERS SINCE LAST 
  17 78_Q    [     NEWLINE INTO NEW AMXOR/IOUT AND SHORTEN OLD ONE   
   4 79FB    [   
   8 79_2    NPUND LDX   1  FX2  
  19 7=DL          LDX   0  IWORK18(1)          [SAVE OLD VALUE OF IWORK18   
  19 7=Y=          SBX   0  IWORK15(1)          [RELATIVE TO START OF BLOCK  
  15 7?CW          STO   0  ACOMMUNE6(1)        [IN ACOMMUNE6
  18 7?XG          STO   3  IWORK18(1)          [PRESERVE RECORD POINTER 
  19 7#C6          LDX   2  ACOMMUNE2(1)        [START ADDRESS OF AMXOR/IOUT 
  18 7#WQ          LDX   0  IWORK15(1)          [INITIAL RECORD POINTER  
  10 7*BB          SBS   0  IWORK18(1)   
   7 7*W2          PHOTO    0
  10 7B*L          STO   0  ACOMMUNE7(1) 
  20 7BT=          LDX   0  6                   [NO PREV. NL SO TAKE FULL BLOCK  
  10 7C#W          STO   7  ACOMMUNE4(1) 
  18 7CSG          STO   5  ACOMMUNE5(1)        [CURRENT REMAINDER COUNT 
  20 7D#6          LDX   6  IPBIDE+1(2)         [CHAR. REM. CT. AT LAST NEWLINE  
   9 7DRQ          LDXC  5  AWORK1(1)
  17 7F?B          SRL   5  12                  [INITIAL CHAR COUNT  
   8 7FR2          ORX   6  CACT 
  10 7G=L          STOC  6  IPBIDE+1(2)  
   8 7GQ=          ANDN  6  #7777
  18 7H9W          LDX   3  5                   [PRESERVE FOR SETUPCORE  
  10 7HPG          SBN   5  SAFETMARGIN  
   7 7J96          SRL   3  2
  18 7JNQ          SBX   5  6                   [EQUAL IF NO NL IN BLOCK 
  19 7K8B          SBX   6  0                   [NO OF CHAR SINCE NL OR START
   8 7KN2          BNZ   5  WHOB 
  21 7L7L          LDX   5  6                   [NO OF CHARS DEPOSITED SINCE START   
   7 7LM=          LDN   6  0
  18 7M6W    WHOB  DSA   5  IPBIDE+1(2)         [CHAR. COUNT FOR OUTPUT  
  17 7MLG          CALL  7  SETU                [GET NEW AMXOR/IOUT  
   7 7N66          LDX   3  6
   9 7NKQ          LDN   7  IDATA(2) 
   7 7P5B          LDX   1  2
  18 7PK2          HUNT2    1,AMXOR,IOUT        [FIND OVERFLOWED BLOCK   
   8 7Q4L          BZE   6  OWFUL
   7 7QJ=          LDX   6  5
   7 7R3W          SRC   6  2
  20 7RHG          ADN   6  IDATA(1)            [POINT TO FIRST CHAR TO REPLACE  
  21 7S36          MVCH  6  0(3)                [MOVE CHARS SINCE NL INTO NEW BLOCK  
  10 7SGQ    OWFUL ADN   5  IDATA-A1*4+3 
   7 7T2B          SBX   7  2
   7 7TG2          SRL   5  2
  21 7T_L          STOZ     IBSW(1)             [SET BLOCK STATUS WORD FOR DATA SEG  
  17 7WF=          ALTLEN   1,5                 [SHORTEN OLD BLOCK   
   8 7WYW          LDX   1  FX2  
   9 7XDG          LDXC  6  AWORK1(1)
   8 7XY6          SRL   6  12   
  17 7YCQ          SBN   6  SAFETMARGIN(3)      [ROOM LEFT IN BLOCK  
  18 7YXB          LDX   5  ACOMMUNE5(1)        [RECORD REMAINDER COUNT  
  10 7_C2          LDX   0  ACOMMUNE7(1) 
   9 7_WL          TESTMOVE 0,NSTL   
  10 82B=          HUNTW    3,ADATA,ACONV
   8 82TW          ADN   3  A1   
   8 83*G          BPZ   3  NST  
  10 83T6          LDX   0  ACOMMUNE4(1) 
  10 84#Q          STO   0  IWORK15(1)   
   7 84SB          STEPAGAIN 
   7 85#2          LDX   1  2
  10 85RL          LDX   0  IWORK15(1)   
  10 86?=          STO   0  ACOMMUNE4(1) 
   8 86QW          BRN      NST  
  10 87=G    NSTL  LDX   3  IWORK15(1)   
  10 87Q6    NST   STO   3  IWORK15(1)   
  10 889Q          ADX   3  IWORK18(1)   
  10 88PB          MHUNTW   2,AMXOR,IOUT 
  10 8992          STO   3  IWORK18(1)   
  10 89NL          STO   2  ACOMMUNE2(1) 
   7 8=8=          ADX   2  7
  21 8=MW          LDX   0  AWORK3(1)           [PREV SHIFT IF 3-SHIFT ; 0 IF GRAP   
   8 8?7G          BRN      TJUM 
   4 8?M6    [   
  21 8#6Q    [     END OF RECORD : OUTPUT IF END OF RESTART SECTION ; GET NEXT RECORD
   4 8#LB    [   
   8 8*62    PEND  LDX   1  FX2  
   7 8*KL          LDX   4  2
  19 8B5=          LDX   2  ACOMMUNE2(1)        [START ADDRESS OF AMXOR/IOUT 
  21 8BJW          LDX   0  IWORK11(1)          [B2 SET IF THIS WAS 2000-CHAR RECORD 
   7 8C4G          SLL   0  2
  21 8CJ6          BNG   0  NOWTH               [QUEUE THIS 2000-CHAR TP RECORD AND  
  18 8D3Q                                       [CONCATENATE NEXT RECORD 
   7 8DHB          LDN   0  1
  17 8F32          ADS   0  IWORK10(1)          [UPATE LINE NUMBER   
  10 8FGL          LDX   3  IWORK12(1)   
  15 8G2=          BNG   3  RESEC               [ALL RECORDS 
   7 8GFW          SBN   3  1
  10 8G_G          STO   3  IWORK12(1)   
  16 8HF6          BZE   3  NOTF                [END OF LISTING  
   9 8HYQ    RESEC LDX   5  IWORK9(1)
  18 8JDB          BNG   5  NOTF                [END OF RESTART SECTION  
   7 8JY2    PNOW  LDX   0  6
   8 8KCL          SBN   0  12   
  20 8KX=          BNG   0  NOWTH               [NOT WORTH STARTING RECORD HERE  
  17 8LBW          DSA   6  IPBIDE+1(2)         [ROOM LEFT IN BLOCK  
   7 8LWG          SBX   4  2
  20 8MB6          STO   4  IBSW(2)             [POINTER TO DEPOSIT NEXT CHAR.   
  17 8MTQ          BRN      NXREC               [GET SECOND RECORD   
   4 8N*B    [   
   8 8NT2    WOBL  LDX   1  FX2  
  10 8P#L          MHUNTW   2,AMXOR,IOUT 
  20 8PS=          LDX   6  IPBIDE+1(2)         [ROOM LEFT IN BLOCK AFTER FIRST  
  16 8Q?W          ANDN  6  #7777               [RECORD INSERTED 
  20 8QRG    NOTF  LDCT  0  4                   [INDICATE END OF RESTART SECTION 
  10 8R?6          ORS   0  IPBIDE+1(2)  
  20 8RQQ          ORS   0  IWORK9(1)           [SET ^FIRST REC OF SECTION^ BIT  
  19 8S=B    NOWTH LDXC  5  AWORK1(1)           [NPU SIZE IN CHARS IN B1-11  
   8 8SQ2          SRL   5  12   
  18 8T9L          SBN   5  SAFETMARGIN         [REPLACE SAFETY MARGIN   
   9 8TP=          STOZ     IBSW(2)  
  19 8W8W          SBX   5  6                   [X5 = CT. OF CHARS DEPOSITED 
  10 8WNG          DSA   5  IPBIDE+1(2)  
  10 8X86          ADN   5  IDATA-A1*4+3 
  20 8XMQ          SRL   5  2                   [REQUIRED AMXOR/IOUT BLOCK LENGTH
  10 8Y7B          TXU   5  ALOGLEN(2)   
   8 8YM2          BCC      PREN 
   8 8_6L          ALTLEN   2,5  
  16 8_L=    PREN  BZE   3  OUDEL               [END OF LISTING  
   6 925W    SK3IPBLISTE 
  18 92KG          CALL  7  TMF                 [TEST MOP OFF OR FINISH  
  10 9356          LDXC  5  ISTATUS(3)   
  18 93JQ          BCS      TINOP               [IDENTIFIER INOPERABLE   
  10 944B          LDCH  0  ISTATUS(3)   
   7 94J2          ANDN  0 2 
  16 953L          BNZ   0  TINOP               [FAIL STILL SET  
   8 95H=          LDCT  0  #400 
   7 962W          ADXC  5  5
   8 96GG          BCC      NOWAR
   8 9726          ADN   0  #2000
   9 97FQ          ORS   0  IWAIT(3) 
   6 97_B          COOR1 
  15 98F2          BRN      SK3IPBLISTE         [TRY AGAIN   
   4 98YL    [   
   9 99D=    NOWAR ORS   0  IWAIT(3) 
  19 99XW          LDX   3  IADIN(3)            [ADDRESS OF IPBIN ACTIVITY   
  10 9=CG          LDX   3  IPBRING(3)   
   9 9=X6          SBN   3  IPBRING  
  10 9?BQ          LDX   2  ACTRING(3)   
  19 9?WB          SBN   2  ACTRING             [FIND ACTIVITY AFTER IPBOUT  
   9 9#B2          LDX   4  BPTR(2)  
   8 9#TL          LDX   2  FX2  
   9 9**=          LDEX  5  AWORK1(2)
  10 9*SW          SEGENTRY K53IPBLISTE  
  18 9B#G          ADS   5  ALFTRANS(2)         [UPDATE TRANSFER COUNT   
  19 9BS6          MHUNTW   1,AMXOR,IOUT        [THIS WILL BE THE LAST OUTPUT
   7 9C?Q          LDN   0  1
  17 9CRB          ERS   0  IMOPTY(1)           [WAKE ME AFTER OUTPUT
   8 9D?2          BRN      NOWCH
  10 9DQL    NEBO  MHUNTW   1,AMXOR,IOUT 
  10 9F==    NOWCH LDX   0  ALOGLEN(1)   
   9 9FPW          SBN   0  IDATA-A1 
  19 9G9G          ADS   0  ALFWORDS(2)         [COUNT OF WORDS TRANSFERRED  
   8 9GP6          CHAIN    1,4  
   8 9H8Q          BCT   5  NEBO 
  19 9HNB          DEX   5  AWORK1(2)           [CLEAR COUNT OF OUTPUT BLOCKS
   4 9J82    [   
   8 9JML          LDCT  0  #100 
  18 9K7=          ANDX  0  IPBSW(3)            [B2 SET IF IPBOUT ASLEEP 
   8 9KLW          BZE   0  WAKE 
  16 9L6G          ERS   0  IPBSW(3)            [INDICATE ON LIST
   7 9LL6          LDX   2  3
   6 9M5Q          FPUT  
   6 9MKB    WAKE  COOR1 
   8 9N52          BRN      NEWS 
   4 9NJL    [   
  20 9P4=    TINOP LDN   7  0                   [INDICATE LISTING NOT FINISHED   
  20 9PHW          LDCH  0  ISTATUS(3)          [B4 SET IF UNAV RATHER THAN HELD 
   8 9Q3G          LDX   2  FX2  
   7 9QH6          ANDN  0  2
  21 9R2Q          DEX   0  AWORK4(2)           [0 IF HOLD BUTTON ; 2 IF STOP BUTTON 
   8 9RGB          LDCT  0  #030 
  10 9S22          ORS   0  ISTATUS(3)   
  10 9SFL          ERS   0  ISTATUS(3)   
  10 9S_=          ACROSS   IPBLISTC,2   
   4 9TDW    [   
   8 9TYG    OUDEL LDX   2  FX2  
  10 9XX2          MHUNT    2,AMXOR,IOUT 
   8 9YBL          LDCT  0  #40  
  17 9YW=          ORS   0  IPBIDE+1(2)         [REPORT OUTPUT SENT  
   8 =2*6          LDX   2  FX2  
  16 =2SQ          LFBBS    M,2,SAYCOR          [J IF READ ERROR 
  10 =3S2          ACROSS   IPBLISTC,1   
   4 =4?L    [   
   5 =4R=    SAYCOR  
   8 =5=W          LFBITCLR M,2  
  10 =5QG          ACROSS   IPBLISTC,3   
   4 =6=6    #END
   2 =79B
   6 ____ ...633207150001
