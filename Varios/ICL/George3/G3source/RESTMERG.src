  15 22FL ...      SEG   RESTMERG,864,PETER CAMPBELL,RESTORE 
   4 22_=    #   
  12 23DW          SEGENTRY K1RESTMERG,X1RESTMERG
  12 23YG          SEGENTRY K2RESTMERG,X2RESTMERG
   4 24D6    #   
  21 24XQ    #   THIS SEGMENT IS USED BY THE RESTORING ROUTINES RESTOREG & RESTLIMB  
  20 25CB    #   TO MERGE A DIRECTORY READ FROM AN INCREMENT DUMP TAPE WITH THE  
  19 25X2    #   'SKELETON' DIRECTORY FOR ITS FILES PREVIOUSLY READ FROM TAPE
  21 26BL    #     ENTRY    A MAGNETIC TAPE IS POSITIONED AFTER THE START SENTINEL FO
  13 26W=    #              DIRECTORY TO BE MERGED.  
  21 27*W    #              THE TOP FILE OPEN CONTAINS THE SKELETON ENTRIES TO BE MER
  21 27TG    #     METHOD   EACH ENTRY IN THE BLOCKS ON TAPE IS EXAMINED (READ INTO F
  21 28*6    #              BLOCK). FOR THOSE FILES DUMPED IN THE CURRENT INCREMENT  
  20 28SQ    #              CORRESPONDING SKELETON ENTRY IS FOUND IN THE TOP FILE
  21 29#B    #              OPEN.THE ECOPS WORD IN THE ENTRY IS REPLACED BY THAT IN  
  20 29S2    #              THE SKELETON ENTRY WHICH IS DELETED AND THE ENTRY IS 
  19 2=?L    #              APPENDED, THE NEW BLOCKS RECORD IS APPENDED AND  
  18 2=R=    #              THEN A TEST IS MADE TO SEE IF AN INDEX RECORD
  19 2?=W    #              NEEDS APPENDING.  FINALLY THE OLD USER TRAPS ARE 
  21 2?QG    #              APPENDED AND PROCESS REPEATED FOR EACH DIRECTORY ENTRY.  
  20 2#=6    #              MAGNETIC TAPE ENTRIES AND ENTRIES WHICH DO NOT HAVE A
  20 2#PQ    #              CORRESPONDING SKELETON ENTRY ARE MARKED AS HAVING NO 
   9 2*9B    #              COPIES.  
  14 2*P2    #     EXIT     THE DIRECTORY HAS BEEN MERGED
  18 2B8L    #              THE TAPE IS POSITIONED AFTER EFS OF DIRECTORY
  12 2BN=    #              UP      NORMAL EXIT  
  18 2C7W    #              ANY ANAMOLY CAUSES ENTRY INTO RESTERR SEGMENT
   4 2CMG    #   
   4 2D76    #   
   4 2DLQ    #   
  11 2F6B    #  USE OF ACCS. AND AWORK WORDS 
  11 2FL2    #  ---------------------------- 
   4 2G5L    #   
  14 2GK=          BITDEFS  4,0,XBINDEX,XBBLOCKS,XBDIR   
  11 2H4W          FIELDDEF NUMTRAPS,9,4,23  
  18 2HJG    #     X4    USED AS A BIT & FIELD WORD. DEFINED AS FOLLOWS: 
  21 2J46    #           XBINDEX  CURRENT DIR. ENTRY IS INDEXED (IN SKELETON ENTRY)  
  21 2JHQ    #           XBBLOCKS CURRENT DIR. ENTRY HAS A BLOCKS RECORD (I.E. WAS DU
  15 2K3B    #                                  THIS INCR.)  
  21 2KH2    #           XBDIR    DIR. ENTRY CURRENTLY BEING DEALT WITH IS FOR A DIRE
  21 2L2L    #           NUMTRAPS NO. OF TRAP RECORDS IN TAPE DIR. ENTRY (ASSUMED TO 
  21 2LG=    #     X3    USED VARIOUSLY AS POINTER TO FILE/FRESTOR BLOCK, POINTER TO 
  20 2L_W    #           RECORD WITHIN THAT BLOCK, STEP & STEP-APPEND POINTERS   
  21 2MFG    #     X5    MODIFIER (-A1) DOWN FILE/FRESTOR FOR CURRENT RECORD AND ALSO
  12 2M_6    #     X6    SHORT TIME WORKING WORD 
  14 2NDQ    #     X7    LINK ACCUMULATOR AND WORK WORD  
  21 2NYB    #    AWORK1 NO. OF RECORDS TO SKIP IN TAPE DIR. (BLOCKS AND/OR INDEX REC
   9 2PD2    #           OR NEITHER) 
   4 2PXL    #   
   4 2QC=    #   
   7 2QWW    #  SUBROUTINES  
   4 2RBG    #   
   4 2RW6    #   
  21 2S*Q    #  READS A BLOCK FROM MT EXAMINING THE REPLY WORD FOR UNEXPECTED BITS.  
  17 2STB    #  GOES TO RESTERR IF B0, B1 OR B3 OF MT REPLY WORD SET.
  21 2T*2    #  ON ENTRY, X0= NO. OF WORDS TO READ, X3->BUFFER TO READ INTO.  X5 IS S
  18 2TSL    #  ON EXIT, X1=FX1, X2=FX2, X3-> BUFFER BLOCK.     USES X0. 
   4 2W#=    #   
   5 2WRW    SREADMT 
   8 2X?G          SBX   5  FX1  
   8 2XR6          LDX   2  FX2  
  18 2Y=Q          DCA   ,STO,0,COUNT           [INSERT COUNT OF WORDS   
  15 2YQB          DCA   ,STOZ,,MODE            [ READ MODE  
   8 2_=2          LDN   0  A1(3)
  19 2_PL          DCA   ,STO,0,CTSA            [SET START OF BUFFER ADDRESS 
  16 329=          LOCK  3                      [LOCK DOWN BUFFER
   7 32NW          LOCK  2   
   7 338G          GPERI ,2  
   6 33N6          COOR1 
   7 347Q          UNLOCK   2
   7 34MB          UNLOCK   3
   8 3572          LDCT  0  #640 
   9 35LL          ANDX  0  CPRW1(2) 
  21 366=          BNZ   0  XER19               [ERROR IF B0, B1 OR B3 SET IN REPLY W
   8 36KW          ADX   5  FX1  
   7 375G          EXIT  5  0
   4 37K6    #   
  21 384Q    #  GETS A POINTER TO THE NEXT RECORD IN THE DIRECTORY ON TAPE (UPDATING 
  17 38JB    #  X5). JUMPS OUT IF THE END OF DIRECTORY IS REACHED.   
  21 3942    #  ON ENTRY, X5=MOD. DOWN FRECTOR BLOCK OF CURRENT RECORD (-A1).  X7 IS 
  21 39HL    #  ON EXIT, X3->FILE/FRESTOR BLOCK, X5= MOD(-A1) DOWN BLOCK OF NEW RECOR
   4 3=3=    #   
   5 3=GW    SGETREC 
  11 3?2G          MHUNTW   3,FILE,FRESTOR   
   7 3?G6          SMO      5
  19 3?_Q          LDEX  0  FRH+A1(3)           [X0= LENGTH OF CURRENT RECORD
   7 3#FB          ADX   5  0
   7 3#_2          SMO      5
  18 3*DL          LDEX  0  FRH+A1(3)           [X0= LENGTH OF NEW RECORD
  20 3*Y=          BNZ   0  SNLAST              [J IF NOT LAST RECORD IN BLOCK   
   5 3BCW    SGETBL  
   8 3BXG          SBX   7  FX1  
   8 3CC6          LDN   0  GSBS 
  19 3CWQ          CALL  5  SREADMT             [READ NEXT BLOCK FROM TAPE   
   8 3DBB          ADX   7  FX1  
   8 3DW2          LDCT  0  #020 
   9 3F*L          ANDX  0  CPRW1(2) 
  16 3FT=          BNZ   0  ZTM                 [J IF TM DETECTED
   9 3G#W          LDEX  0  FRH+A1(3)
  21 3GSG          BZE   0  SGETBL              [GET NEXT BLOCK IF THIS ONE IS EMPTY 
  21 3H#6                                       [SHOULDN'T HAPPEN BUT MIGHT POSSIBLY 
  20 3HRQ          LDN   5  0                   [INITIALISE POINTER DOWN BLOCK   
   5 3J?B    SNLAST  
   7 3JR2          EXIT  7  0
   4 3K=L    #   
  15 3KQ=    #  DOES A STEP-APPEND ON THE FILE AT DEPTH 0.   
  18 3L9W    #  ON ENTRY, X2->RECORD WANT TO APPEND.      X7 IS S/R LINK 
  20 3LPG    #  ON EXIT, X1=FX1, X2=FX2, X3->WHERE TO APPEND RECORD.     USES X0.
   4 3M96    #   
   5 3MNQ    SAPPEND 
   8 3N8B          SBX   7  FX1  
  17 3NN2          LDEX  1  FRH(2)              [X1= LENGTH OF RECORD
  15 3P7L          STEP  ,0(1)                  [STEP-APPEND 
   8 3PM=          ADX   7  FX1  
   7 3Q6W          EXIT  7  0
   4 3QLG    #   
  16 3R66    #  RESETS UP POINTER TO CURRENT RECORD IN TAPE DIR. 
  20 3RKQ    #  ON ENTRY, X5= MOD. DOWN CURRENT TAPE BLOCK.      X7 IS S/R LINK. 
  15 3S5B    #  ON EXIT, X2->RECORD TO APPEND.     USES X0.  
   4 3SK2    #   
   5 3T4L    SFRESTOR
  10 3TJ=          MHUNTW  2,FILE,FRESTOR
   7 3W3W          ADX   2  5
   8 3WHG          ADN   2  A1   
   7 3X36          EXIT  7  0
   4 3XGQ    #   
  18 3Y2B    #  MOVES A RECORD FROM TAPE OR DUMMY DIR. TO MAKE REAL DIR. 
  21 3YG2    #  ON ENTRY, X2->RECORD TO MOVE, X3->WHERE TO MOVE IT.  X7 IS S/R LINK. 
  17 3Y_L    #  ON EXIT, X1= LENGTH OF RECORD, X2 & X3 AS ON ENTRY.  
   4 3_F=    #   
   5 3_YW    SMOVE   
   9 42DG          LDEX  1  FRH(2)   
   8 42Y6          MOVE  2  0(1) 
   7 43CQ          EXIT  7  0
   4 43XB    #   
   4 44C2    #   
   4 44WL    #   
  12 45B=    #  ENTERED FROM RESTOREG & RESTLIMB.
  11 45TW    #  INITIALISE BUFFER & POINTERS.
   6 46*G    X1RESTMERG  
  11 46T6          MHUNTW   3,FILE,FRESTOR   
  20 47#Q          STOZ     A1(3)               [INITIALISE 1ST WORD OF BUFFER   
  19 47SB          LDN   5  0                   [  AND MODIFIER DOWN BLOCK   
   4 48#2    #   
  21 48RL    #  READS A NAME RECORD FROM TAPE (GEOERRS IF IT ISN'T AND NOTES WHAT REC
  17 49?=    #  ARE PRESENT ON TAPE.  CLEARS THE INDEXED BIT & FIELD.
   5 49QW    XNAMEREC
  17 4==G          LDN   4  0                   [CLEAR MARKER WORD   
  21 4=Q6          CALL  7  SGETREC             [READ (STEP) NAME RECORD FROM TAPE   
  19 4?9Q          ADX   3  5                   [X3-> (RECORD FROM TAPE)-A1  
   9 4?PB          LDX   0  ERES(3)  
  19 4#92          BNZ   0  XGEO                [GEOERR IF NOT NAME RECORD   
   9 4#NL          LDX   2  ENUSE(3) 
  18 4*8=          STF   ,NUMTRAPS,2            [REMEMBER NO. OF TRAPS   
   8 4*MW          LDX   2  FX2  
  20 4B7G          STOZ     AWORK1(2)           [CLEAR ^NO. OF TAPE RECS TO SKIP^
  15 4BM6          LDN   7  0                   [  AND AGAIN 
   9 4C6Q          LDX   0  ESER(3)  
  16 4CLB          BNZ   0  XNCOP               [J IF MT ENTRY   
  17 4D62          JBCC  XNINDEX,3,BEINDEX      [CLEAR ^INDEXED^ BIT 
  21 4DKL          ADN   7  1                   [UPDATE NO. OF TAPE RECORDS TO SKIP  
   8 4F5=          NGN   0  #100 
  21 4FJW          ANDS  0  EINDEX(3)           [CLEAR INDEX FIELD (B18-23 OF EINDEX)
  21 4G4G                                       [PROBABLY NO NEED FOR THIS, BUT NOT S
   5 4GJ6    XNINDEX 
   9 4H3Q          LDEX  0  ECOPS(3) 
  19 4HHB          BZE   0  XNBLOCKS            [J IF NO BLOCKS REC ON TAPE  
  17 4J32          ADN   7  1                   [UPDATE SKIP COUNT   
   5 4JGL    XNBLOCKS
  21 4K2=          STO   7  AWORK1(2)           [REMEMBER NO. OF RECORDS TO SKIP IN T
   4 4KFW    #   
  21 4K_G    #  BRANCHES IF ENTRANT IS A MT OR IS NOT DUMPED IN THE CURRENT INCREMENT
  21 4LF6    #  OTHERWISE LOCATES THE DUMMY NAME RECORD - FOR A TERMINAL FILE IT MUST
  21 4LYQ    #  1ST TERMINAL FILE RECORD BUT FOR DIRS. THE ORDER OF DIR. ENTRIES IS N
  21 4MDB    #  NECESSARILY THE ORDER OF DIRS. DUMPED (IN MK. 8.20+, DUMMY RECORDS FO
  21 4MY2    #  PRECEDE THOSE FOR TERMINALS FILES WHICH AGAIN PRECEDE ALL REAL ENTRIE
  21 4NCL    #  GEOERRS IF DOESN'T FIND THE ENTRY.  REMEMBER THAT SEVERAL DIRS. WITH 
  21 4NX=    #  LOCAL NAME (BUT DIFFERENT USER NAMES) AND ONE TERMINAL FILE WITH THAT
  21 4PBW    #  MAY BE DUMPED IN THE SAME INCR..  SETS INDEXED BIT & INDEX NO. FIELD 
  11 4PWG    #  APPROPRIATE AND ALSO ECOPS.  
   7 4PX* ...#UNS FTS2REVERT 
   4 4PY8 ...(   
  19 4P_3 ...      JFNZ     XNCOP,3,FEFSNO      [ IF ALIEN MUST BE OFFLINE   
  21 4P_W ...      LDEX  0  ECOPS(3)            [ MAY HAVE MESSED UP BY FILE HANDLER 
  12 4Q2P ...      BZE   0  XNCOP               [
   4 4Q3J ...)   
   6 4Q44 ...#UNS AMTGR  
  11 4Q4J ...      JBS  NOTPARTRES,,BGENRES  
   6 4Q54 ...#UNS AMTGR  
   4 4Q5J ...#SKI
  12 4Q65 ...      BWNZ     GINCTSN,NOTPARTRES   
  11 4Q6Y ...      HUNTMISB 1,FI,FRESTUSER   
  17 4Q7R ...      SMO      FRNXT(1)             [ POINTER TO USER   
   9 4Q8L ...      LDX   0  FRINC(1) 
  10 4Q9F ...      BXU   0  EINC(3),XNCOP
  16 4Q=# ...#     JUMP IF THIS FILE NOT DUMPED IN THIS INCREMENT
   8 4Q?7 ...      BRN      PRES 
   6 4Q#2 ...NOTPARTRES  
   9 4QB6          LDX   0  EINC(3)  
   9 4QTQ          SBX   0  JINCLAST 
  20 4R*B          BNZ   0  XNCOP               [J IF NOT DUMPED IN THIS INCR.   
   4 4RK8 ...PRES
  19 4RT2          BS    ,XBBLOCKS              [SET ^BLOCKS REC PRESENT^ BIT
   9 4S#L          LDX   0  EUSE1(3) 
  16 4SS=          BZE   0  XNOTDIR             [J IF NOT A DIR. 
  21 4T?W          BS    ,XBDIR                 [SET ^CURRENT ENTRY IS A DIR.^ BIT   
   5 4TRG    XNOTDIR 
   6 4W?6          REWIND
   5 4WQQ    XSTEP   
   6 4X=B          STEP  
   7 4X*G ...#UNS FTS2REVERT 
  15 4XDL ...      BZE   3  XNCOP               [ FH ????????
   7 4XHQ ...#UNS FTS2REVERT 
   4 4XLW ...#SKI
  18 4XQ2          BZE   3  XGEO                [GEOERR IF NO DUMMY ENRTY
   9 4Y9L          LDX   0  ERESN(3) 
  17 4YP=          BNZ   0  XSTEP               [J IF NOT NAME REC.  
  11 4_8W          MHUNTW   2,FILE,FRESTOR   
  19 4_NG          ADX   2  5                   [X2 -> (RECORD FROM TAPE)-A1 
  18 5286          JBS   XDIR,,XBDIR            [J IF LOOKING FOR A DIR. 
   9 52MQ          LDX   0  EUSE1N(3)
  20 537B          BNZ   0  XSTEP               [J IF NOT FOUND TERMINAL FILE YET
   5 53M2    XTESTDUM
   9 546L          LDX   0  EINCN(3) 
   7 549Q ...#UNS FTS2REVERT 
  14 54#W ...      BPZ   0  XNCOP               [ FH ?   
   7 54D2 ...#UNS FTS2REVERT 
   4 54H6 ...#SKI
  19 54L=          BPZ   0  XGEO                [GEOERR IF NOT DUMMY ENTRY   
   7 54PB ...#UNS FTS2REVERT 
  21 54SG ...      TESTNAMX 5,ELOC1(2),ELOC1N(3),XSTEP,6  [   FH   ????   IF WRONG DU
   7 54XL ...#UNS FTS2REVERT 
   4 552Q ...#SKI
  21 555W          TESTNAMX 5,ELOC1(2),ELOC1N(3),XGEO,6  [GEOERR IF WRONG DUMMY ENTRY
   9 55KG          LDX   6  ECOPSN(3)
  21 5656          STO   6  ECOPS(2)            [TRANSFER BLOCKS WORD TO REAL ENTRY  
  16 56JQ          JBC   XXNINDEX,3,BNINDEX     [J IF NOT INDEXED
  20 574B          BS    2,BEINDEX              [SET INDEX BIT IN REAL DIR. ENTRY
  20 57J2          BS    ,XBINDEX               [REMEMBER INDEX RECORD PRESENT   
  10 583L          LDX   0  EINDEXN(3)   
   8 58H=          ANDN  0  #77  
  19 592W          ORS   0  EINDEX(2)           [MOVE INDEX NO. TO DIR. ENTRY
   4 59GG    #   
  21 5=26    #  APPENDS NAME RECORD IN TAPE DIR. TO DIR. & DELETES DUMMY NAME RECORD 
   7 5=FQ    #  IF PRESENT   
   5 5=_B    XXNINDEX
  18 5?F2          ADN   2  A1                  [X2-> RECORD TO APPEND   
  19 5?YL          CALL  7  SAPPEND             [X3-> STEP-APPEND POSITION   
  16 5#D=          CALL  7  SFRESTOR            [SET UP X2 AGAIN 
  21 5#XW          CALL  7  SMOVE               [MOVE RECORD FROM TAPE DIR. TO DIR.  
  17 5*CG          BZE   6  XNODUM              [J IF NO DUMMY ENTRY 
  19 5*X6          DELETE                       [DELETE SKELETON NAME RECORD 
   4 5BBQ    #   
  21 5BWB    #  APPENDS AND DELETES DUMMY BLOCKS & INDEX RECORDS IF PRESENT.  COPES I
  17 5CB2    #  COORDINATION OCCURS DURING STEP AND/OR STEP-APPEND.  
   5 5CTL    XNODUM  
  18 5D*=          JBC   YNOCOP,,XBBLOCKS       [J IF NO BLOCKS RECORD   
   5 5DSW    YSTEP   
   6 5F#G          STEP  
   5 5FS6    YSTEP1  
  18 5G?Q          LDX   2  3                   [X2-> RECORD TO APPEND   
  18 5GRB          LDX   6  3                   [RETAIN POINTER TO RECORD
  19 5H?2          CALL  7  SAPPEND             [X3-> STEP-APPEND POSITION   
  10 5HQL          TESTREP2 COORED,YCOOR 
  18 5J==          LDX   2  6                   [RELOAD POINTER TO RECORD
  16 5JPW          CALL  7  SMOVE               [MOVE REC TO DIR.
  21 5K9G          DELETE                       [DELETE DUMMY BLOCKS OR INDEX RECORD 
   5 5KP6    YNOCOP  
  21 5L8Q          JBCC  YNOINDEX,,XBINDEX      [J IF NO INDEX RECORD (CLEAR INDEX MA
  21 5LNB                                       [                             THERE I
   8 5M82          BRN      YSTEP
   5 5MML    YCOOR   
  21 5N7=          STEPAGAIN                    [X3-> BLOCKS OR INDEX RECORD AGAIN   
   9 5NLW          BRN      YSTEP1   
   4 5P6G    #   
  21 5PL6    #  SKIPS BLOCKS AND INDEX RECORDS ON TAPE AND TRANSFERS TRAP RECORDS FRO
   9 5Q5Q    #  TAPE DIR. TO DIR..   
   5 5QKB    YNOINDEX
   8 5R52          SMO      FX2  
  21 5RJL          LDX   6  AWORK1              [X6= NO. OF RECORDS TO SKIP ON TAPE  
   9 5S4=          BZE   6  YNOSKIP  
   5 5SHW    YSKIP   
  19 5T3G          CALL  7  SGETREC             [SKIP BLOCKS OR INDEX RECORD 
   8 5TH6          BCT   6  YSKIP
   5 5W2Q    YNOSKIP 
  16 5WGB          LF    ,NUMTRAPS,6            [X6= NO. OF TRAPS
  16 5X22          BZE   6  XNAMEREC            [J IF NO TRAPS   
   5 5XFL    YTRAP   
  17 5X_=          CALL  7  SGETREC             [GET NEXT TRAP RECORD
   7 5YDW          SMO      5
  19 5YYG          LDN   2  A1(3)               [X2-> TRAPS RECORD ON TAPE   
  19 5_D6          CALL  7  SAPPEND             [X3-> STEP-APPEND POSITION   
  14 5_XQ          CALL  7  SFRESTOR            [RESET X2
  21 62CB          CALL  7  SMOVE               [MOVE TRAPS RECORD FROM TAPE DIR. TO 
   8 62X2          BCT   6  YTRAP
   9 63BL          BRN      XNAMEREC 
   4 63W=    #   
   4 64*W    XDIR
  21 64TG          TESTNAMX 3,EUSE1(2),EUSE1N(3),XSTEP,6   [USERS MAY BE IN DIFFERENT
  21 65*6                                       [DUE TO ERASURE OF DIRS. SEARCH BY US
  21 65SQ                                       [SINCE DIR. WITH SAME LOCAL NAME BUT 
  21 66#B                                       [DIFFERENT USER NAME MAY HAVE BEEN CR
  17 66S2                                       [(AND BOTH DUMPED)   
   9 67?L          BRN      XTESTDUM 
   4 67R=    #   
  18 68=W    #  MT ENTRANT OR FILE NOT IN THIS INCR..  SHOW FILE OFFLINE.
   5 68QG    XNCOP   
   7 68QK ...#UNS FTS2REVERT 
  19 68QN ...      JFNZ  NOTPARALLEL,3,FEFSNO   [ IF ALIEN MUST BE OFFLINE   
   6 68QP ...#UNS AMTGR  
  11 68QQ ...      JBC NOTPARALLEL,,BGENRES  
   6 68QR ...#UNS AMTGR  
   4 68QS ...#SKI
  19 68QW ...      BWZ      GINCTSN,NOTPARALLEL   [ JUMP IF PARTIAL RESTORES 
   6 68R4 ...#UNS  FPD   
   4 68RL ...(   
  17 68S8 ...#     THE FILE WAS NOT DUMPED IN THE RESTORED INCREMENT 
  21 68SQ ...      LDEX  0  ECOPS(3)            [ IF THE FILE WAS OFFLINE AT THE TIME
  16 68T# ...      BZE   0  NOTPARALLEL            [ IGNORE IT   
  11 68TW ...      MHUNTW   1,FILE,FINTER7   
   9 68WD ...      LDX   0  EINC(3)  
  13 68X2 ...      BXL   0  GRPINCNUM(1),NOTPARALLEL 
  21 68XJ ...#     IF THE FILE WAS DUMPED IN AN INCREMENT LOWER THAN GRPINCNUM IT CAN
  15 68Y6 ...#     HAVE BEEN DUMPED IN THE PARALLEL INCREMENT
  13 68YN ...      BXE   0  GRPINCNUM(1),PARALLELINC 
  21 68_= ...#     JUMP IF THE FILE APPEARS TO HAVE BEEN DUMPED IN THE PARALLEL INC  
   4 68_S ...#   
  19 692B ...#     THE FILE MAY HAVE BEEN DUMPED IN A NEW PARALLEL INCREMENT 
  10 692Y ...#     RESET THE FILE FINTER7
  18 693G ...      STO   0  GRPINCNUM(1)        [ NEW INCREMENT NUMBER   
  21 6944 ...      STOZ     GRPINCFILES(1)      [ CLEAR NO AND SIZE OF FILES IN INCRE
  12 694L ...      STOZ     GRPINCSIZE(1)       [
   6 6958 ...PARALLELINC 
   7 695Q ...      LDN   0  1
  21 696# ...      ADS   0  GRPINCFILES(1)      [ UPDATE NUMBER AND SIZE OF INCREMENT
   9 696W ...      LDX   0  ECOPS(3) 
  19 697D ...      SRL   0  15                        [ PICK OUT SIZE OF FILE
  20 6982 ...      ADS   0  GRPINCSIZE(1)       [ UPDATE NO AND SIZE OF INCREMENT
   6 698J ...NOTPARALLEL 
   4 6996 ...)   
  19 69=6          LDX   2  3                   [X2->NAME RECORD IN TAPE DIR.
   7 69PQ          LDN   6  0
  18 6=9B          DLA   6  ECOPS(2)            [CLEAR FILE ONLINE FIELD 
   9 6=P2          BRN      XXNINDEX 
   4 6?8L    #   
  21 6?N=    #  END OF DIR. REACHED (TAPE-MARK READ).  READ QUALIFIER BLOCK - CHECK  
  10 6#7W    #  IF IT IS END SENTINEL.   
   4 6#MG    ZTM 
  21 6*76          BNZ   4  XGEO                [GEOERR IF NOT TRYING TO READ NAME RE
  21 6*LQ                                       [NON-STANDARD BUT EFFECTIVE USE (X4 I
  21 6B6B                                       [                              BIT WO
   8 6BL2          LDN   0  20   
  17 6C5L          CALL  5  SREADMT             [READ QUALIFIER BLOCK
   8 6CK=          LDCT  0  #020 
   9 6D4W          ANDX  0  CPRW1(2) 
  18 6DJG          BNZ   0  XER21               [ERROR MESSAGE IF TM READ
   8 6F46          LDCT  0  #400 
  21 6FHQ          BXU   0  ESENTYP(3),XER4     [ERROR MESSAGE IF NOT READ END SENTIN
   4 6G3B    #   
  21 6GH2    #  FOR MARK 8.20+ INCREMENTS, SPARE DUMMY ENTRIES FOR DIRS. MAY BE PRESE
  15 6H2L    #  (BUT NOT FOR TERMINAL FILES).  DELETE THESE. 
   6 6HG=    X2RESTMERG  
   6 6H_W          REWIND
   5 6JFG    ZSTEP   
   6 6J_6          STEP  
   5 6KDQ    ZSTEP1  
  19 6KYB          BZE   3  ZEND                [J IF NO MORE ENTRIES AT ALL 
   9 6LD2          LDX   0  EINCN(3) 
  19 6LXL          BPZ   0  ZEND                [J IF NO MORE DUMMY ENTRIES  
   9 6MC=          LDX   0  EUSE1N(3)
   9 6MWW ...      BZE   0  XSKELETON
   7 6NBG          LDN   7  3
   4 6NW6    ZDEL
  21 6P*Q          DELETE                       [DELETE NAME, BLOCKS & INDEX RECORDS 
   6 6PTB          STEP  
   8 6Q*2          BCT   7  ZDEL 
   9 6QSL          BRN      ZSTEP1   
   4 6R#=    ZEND
   5 6RRW          UP
   6 6RXR ...XSKELETON   
  21 6S3N ...      LDN   7  2                     [ ERASE DUMMY SKELETON ENTRYAND BLO
   8 6S7K ...      BRN      ZDEL 
   4 6S?G    XGEO
  18 6SR6          GEOERR   BRIEFPM,DUMPORD     [ ONE OF THE FOLLOWING : 
  21 6T=Q                                       [1. NAME REC EXPECTED IN TAPE DIR BUT
  21 6TQB                                       [2. ENTRY NOT FOUND WHEN LOOKING FOR 
  21 6W=2                                       [3. REAL ENTRY FOUND WHEN LOOKING FOR
  17 6WPL                                       [       DUMMY ENTRY  
  19 6X9=                                       [4. WRONG DUMMY ENTRY FOUND  
  21 6XNW                                       [5. TM DETECTED BUT NOT TRYING TO REA
  17 6Y8G                                       [         NAME RECORD
  21 6YN6                                       [6. SUPERFLUOUS DUMMY ENTRY NOT A DIR
   4 6_7Q    #   
  21 6_MB    XER4  ACROSS   RESTERR,4           [END SENTINEL EXPECTED BUT NOT FOUND 
  21 7272    XER19 ACROSS   RESTERR,19          [B0, B1 OR B3 SET IN MT REPLY WORD   
  20 72LL    XER21 ACROSS   RESTERR,21          [SUPERFLUOUS TAPE MARK DETECTED  
   4 736=    #   
  11 73KW          MENDAREA 30,K99RESTMERG   
   4 745G    #   
   4 74K6    #END
   8 ____ ...24131731000100000000
