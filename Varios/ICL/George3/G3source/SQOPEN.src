  16 22FL ...#SEG  SQOPEN70                     [FRANK BLUNDELL  
   4 22KH ...#   
  16 22PD ...#     COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982  
   4 22T* ...#   
   7 22_=    #LIS  K0SQOPEN  
   7 23DW          8HSQOPEN  
   4 23YG    #   
  18 24D6    #     THIS  ROUTINE DEALS WITH MODES #1600 AND #1700 OPEN   
  17 24XQ    #     THE MODES ARE ONLY AVAILABLE TO TRUSTED PROGRAMS  
  14 25CB    #     ON ENTRY X4  TYPE B0-B9 CHANNEL B14-23
  16 25X2    #              X5  COPY OF WORD 0 OF CONTROL AREA   
  13 26BL    #              XCA CONTROL AREA ADDRESS 
  14 26W=    #     THE CONTROL AREA IS FIVE WORDS LONG   
   4 27*W    #   
   4 27TG    #   
  11 28*6          SEGENTRY K1SQOPEN,QK1SQ   
   4 28SQ    #   
   6 29#B    STEN  +10   
   9 29S2    XLINK PAIR     PERION,1 
  10 2=?L    XPER           +JYPERFAIL   
   9 2=R=    XNOC           +JYNOCA  
   9 2?=W    XINV           +JYINVAL 
   9 2?QG    XMOD           +JYMODE  
   4 2#=6    #   
   7 2#PQ    #DEF  XCA=EVENT2
   4 2*9B    #   
   4 2*P2    #   
   4 2B8L    #   
   5 2BN=    QK1SQ   
  16 2C7W    #SKI  K6SQOPEN>199-199             [CHECK TYPE/MODE 
   4 2CMG    (   
   7 2D76          LDX   0  5
   8 2DLQ          SRL   0  15   
   8 2F6B          ANDN  0  #377 
   7 2FL2          SBN   0  6
   8 2G5L          BZE   0  XCH1 
   7 2GK=          SBN   0  1
   8 2H4W          BZE   0  XCH1 
   7 2HJG          SBN   0  6
   8 2J46          BZE   0  XCH1 
   8 2JHQ          SBN   0  13   
   8 2K3B          BNZ   0  XCH3 
   7 2KH2    XCH1  LDX   0  5
   9 2L2L          ANDX  0  XCHM(1)  
   8 2LG=          SBN   0  #1600
   8 2L_W          BZE   0  XCH2 
   8 2MFG          SBN   0  #100 
   8 2M_6          BZE   0  XCH2 
   9 2NDQ    XCH3  GEOERR   1,SQOPEN 
   9 2NYB    XCHM           #71777   
   4 2PD2    XCH2
   4 2PXL    )   
   7 2QC=          LDX   6  4
  18 2QWW          LDX   4  XNOC(1)             [C.A OUT OF RESERVATIONS 
   7 2RBG          LDN   7  5
  19 2RW6          CHECKEXT XCA(2),7,XVIOL,,W,XA,HLOCK1     [CHECK 5 WORDS   
  18 2S*Q    XA    SETNCORE GEXDLEN,3,EWDAS,GEXD            [SET UP GEXD 
  18 2STB          STOZ     A1(3)                           [AND ZEROIZE 
   8 2T*2          ADN   3  A1   
   8 2TSL          LDN   4  1(3) 
   9 2W#=          MOVE  3  GEXDLEN-1
   8 2WRW          SBN   3  A1   
   8 32NW          ANDN  6  #1777
  16 338G          STO   6  EXCHNL(3)               [CHANNEL NO. 
   7 347Q          LDX   7  5
  14 34MB          ANDN  5  #1777               [X5= MODE
  16 3572          STO   5  EOPMODE(3)          [STORE IN GEXD   
   9 35LL          LDX   0  XCA(2)   
  16 366=          STO   0  CNR(3)              [SAVE CA ADDRESS 
   8 36KW          SRL   7  10   
  18 375G          ANDN  7  3                   [X7 HOLDS ADDITIVE BITS  
   7 37K6          LDX   0  7
   7 384Q          ANDN  0  1
   9 38JB          LDX   4  XINV(1)  
  20 3942          BZE   0  XVIOL               [ERROR NO SERIAL NO. SPECIFIED   
   9 39HL          LDX   4  XMOD(1)  
  17 3=3=          TRUSTED  2,XC,S              [CHECK IF S-TRUSTED  
  16 3=GW          LDCT  6  #400                [S-TRUSTED MARK  
   8 3?2G          BRN      XCC  
  20 3?G6    XC    TRUSTED  2,XVIOL,Q           [CHECK IF Q-TRUSTED-ERROR IF NOT 
  16 3?_Q          LDN   6  0                   [Q-TRUSTED MARK  
   8 3#FB    XCC   SBN   5  #1700
  18 3#_2          BZE   5  XCB                 [JUMP IF OPEN MODE  1700 
  20 3*DL          BZE   6  XVIOL               [ERROR IF NOT S-TRUSTED AND OPEN 
  15 3*Y=    XCB   LDX   1  CNR(3)              [MODE 1600   
   7 3BCW          ADN   1  1
  18 3BXG          FADDRESS 2,1,HLOCK1          [GET OUT SERIAL NUMBER   
   8 3CC6          LDX   0  0(1) 
   8 3CWQ          SMO      FX1  
   8 3DBB          LDX   4  XINV 
   8 3DW2          BZE   0  XVIOL
   9 3F*L          STO   0  ESERL(3) 
   8 3FT=          SRL   0  18   
  18 3G#W          BNZ   0  XVIOL               [ERROR IF NOT 6 DIGITS   
  17 3GSG          CAPCA    EWDAS,GEXD          [TO CPA TAKING GEXD  
  10 3G_M ...      MHUNTW   3,EWDAS,GEXD 
  13 3H3B ...      LDCT  4  6                   [*DA 
  15 3H55 ...      ADX   4  EXCHNL(3)           [TYPE/UNIT NO
  20 3H6S ...      LDN   7  0                   [BRKIN MARKER FOR BRKIN ROUTINE  
  20 3H8H ...      HLSXONCH YD,4,YC             [SEE IF OK WITH HLS TO CARRY ON  
  10 3H#6          MHUNTW   3,EWDAS,GEXD 
  16 3HDS ...      BZE   5  XOF1                [J IF MODE #1700 
  19 3HGB ...      LDN   1  CWRITE              [MODE #1600 NEEDS WRITE TRAPS
   8 3HHY ...      BRN      XOF2 
   4 3HKG ...XOF1
  19 3HM4 ...      LDN   1  CREAD               [MODE #1700 NEEDS READ TRAPS 
   4 3HNL ...XOF2
  12 3HX_ ...      EXCHOFE  DIR,ESERL(3),1,XOF3  
  10 3H_B ...      MHUNTW   3,EWDAS,GEXD 
  12 3H_Y ...      TESTREP2 OK,XOF5,NOFILE,XOF4  
  19 3J48 ...      OUTPACK  ESERL(3),1,CSN      [REPLY OF NOTRAPS SO ILLEGAL 
   9 3J5K ...      MONOUT   EWSTUC   
   8 3J72 ...      BRN      YC   
   4 3J7B ...XOF4
   8 3J7G ...      LDX   0  EDSQ 
  20 3J7L ...      BPZ   0  XOF3                [J IF FULL SECURITY NOT IN USE   
  11 3J7Q ...      OUTPACK  ESERL(3),1,CSN   
  20 3J7W ...      MONOUT   EWRARED             [DIRECTORY FOR SU Z DOESNT EXIST 
   8 3J82 ...      BRN      YC   
   4 3J83 ...XOF5
   8 3J84 ...      BZE   5  XOF6 
  21 3J85 ...      EXCHOFE  DIR,ESERL(3),,XOF3,AW [D&T WRITTEN+ACCESSED FOR *DA DIR  
   8 3J86 ...      BRN      XOF7 
   4 3J87 ...XOF6
  21 3J88 ...      EXCHOFE  DIR,ESERL(3),,XOF3,A  [D&T ACCESSED FOR :DANNNNNN DIRECTO
   4 3J89 ...XOF7
  10 3J8= ...      MHUNTW   3,EWDAS,GEXD 
   4 3J8C ...XOF3
  17 3J?B          SUSEST   ESERL(3),XDA        [CHECK IF UNIT ONLINE
  10 3K=L    YE    MHUNTW   3,EWDAS,GEXD 
  10 3MNQ    XD    MFINDSU  1,ESERL(3)   
  10 3N8B          LADDP    HUNIT(1),1   
  16 3NN2          JDED     1,NA                [OK IF DEDICATED 
  15 3P7L          BZE   6  NA                  [OR Q TRUSTED
  15 3PM=          BRN      NB                  [ELSE ERROR  
  15 3Q6W    NA    PLOCKSU  ESERL(3),NC,ND      [GET LOCK OUT
  18 3R66          SETNCORE FDCELLS-A1+6,2,EWDAS,EXOF   [GETCORE FOR EXOF
  10 3RKQ          MHUNTW   3,EWDAS,GEXD 
  10 3S5B          MFINDSU  1,ESERL(3)   
   9 3SK2          STOZ     EXMARK(2)
  17 3T4L          LDN   0  1                   [SET UP EXOF BLOCK   
  16 3TJ=          STO   0  COPEN(2)            [COUNT OF OPENS=1
  14 3W3W          NGS   0  HOME(2)             [HOME =-1
   9 3WHG          LDX   0  ESERL(3) 
  16 3X36          STO   0  ASUSN(2)            [SERIAL NUMBER   
   7 3XGQ          LDN   0  0
  15 3Y2B          STO   0  FIP(2)              [ZEROISE FIP 
   9 3YG2          LDN   4  FIP(2)   
   9 3Y_L          LDN   5  FIP+1(2) 
   8 3_F=          MOVE  4  14   
   7 3_YW          LDN   0  1
   9 42DG          STO   0  FBUK(2)  
  16 42Y6          STO   0  FIP+5(2)            [NO.OF AREAS=1   
  16 43CQ          STO   0  FIP+8(2)            [BUCKET SIZE=1   
  10 43XB          LDX   0  EOPMODE(3)   
   8 44C2          SBN   0  #1600
  18 44WL          BNZ   0  XE                  [JUMP IF NOT S-TRUSTED   
   8 45B=          LDCT  0  #404 
  21 45TW          STO   0  EXMARK(2)           [OPEN TO S-TRUSTED FOR WRITING BITS  
  19 46*G    XE    LDX   0  ASUSN(2)            [SET UP FILE DESCRIPTION CELL
  16 46T6          STO   0  FDCELLS(2)          [SERIAL NUMBER   
   9 47#Q          LDX   0  HBLOCK(1)
  17 47SB          STO   0  FDCELLS+4(2)        [SEGMENTS/SEEK AREA  
   9 48#2          LDX   0  HCYL(1)  
  15 48RL          STO   0  FDCELLS+3(2)        [CYLINDERS   
  17 49?=          STOZ     FDCELLS+2(2)        [STARTING CYLINDER=0 
  17 49QW          STOZ     FDCELLS+1(2)        [STARTING SEGMENT=0  
  10 4==G          MPY   0  FDCELLS+4(2) 
   7 4=Q6          ADN   1  1
  17 4?9Q          STO   1  FDCELLS+5(2)        [NUMBER OF BUCKETS   
  18 4?PB          LDN   7  1                   [UNIT IN USE BY S-TRUSTED
  21 4#92          LDN   1  BCCB                [CHECK IF UNIT IN USE BY S-TRUSTED   
   9 4#NL    XG    LDX   1  BPTR(1)  
   8 4*8=          TXU   1  CXCC 
  17 4*MW          BCC      XF                  [JUMP IF END OF CHAIN
   9 4B7G          LDX   0  BACK2(1) 
  16 4BM6          BZE   0  XF                  [OR NO MORE EXOFS
   7 4C6Q          LDCT  0  4
   9 4CLB          ANDX  0  EXMARK(1)
  16 4D62          BZE   0  XG                  [OR NOT S-TRUSTED
   9 4DKL          LDX   0  ASUSN(2) 
  19 4F5=          TXU   0  ASUSN(1)            [CHECK IF FIP UNIT THE SAME  
   8 4FJW          BCC      XH   
  15 4G4G          LDEX  4  FIP+5(1)            [NO OF AREAS 
   7 4GJ6          NGN   5  6
   7 4H3Q    XK    ADN   5  6
   7 4HHB          SBN   4  1
  18 4J32          BNG   4  XG                  [JUMP IF NO MORE AREAS   
   7 4JGL    XJ    SMO      5
  10 4K2=          TXU   0  FDCELLS(1)   
  17 4KFW          BCS      XK                  [JUMP IF DIFFERENT   
  16 4K_G                                       [OTHERWISE ERROR 
   9 4LF6    XH    LDX   4  ASUSN(2) 
   8 4LYQ          SMO      FX2  
  19 4MDB          LDN   1  ACOMMUNE1           [CONVERT SERIAL NO. TO CHARS 
   7 4MY2          LDN   0  6
   7 4NCL          SLL   4  3
   7 4NX=    XHA   SLC   4  6
   8 4PBW          DCH   4  0(1) 
   7 4PWG          BCHX  1  /
   7 4QB6          SRL   4  3
   8 4QTQ          BCT   0  XHA  
   7 4R*B          LDN   4  6
  10 4RT2          OUTPARAM 4,ACOMMUNE1  
  16 4S#L          MONOUT   EWLOCKSU            [S-TRUSTED BLOCK 
  10 4SS=          MHUNTW   2,EWDAS,EXOF 
  15 4T?W          FREECORE 2                   [FREE EXOF   
   4 4TRG    #   
  10 4W?6          MHUNTW   3,EWDAS,GEXD 
  16 4WQQ          LDX   5  CNR(3)              [SAVE CA ADDRESS 
  16 4X=B          LDX   4  ESERL(3)            [SAVE SERIAL NO. 
   8 4XQ2          LDCT  0  #400 
   7 4Y9L          ORX   0  7
   8 4YP=          STO   0  A1(3)
   7 4_8W          LDN   6  1
  17 4_NG          BRN      YB                  [JUMP TO SET REPLIS  
   4 5286    #   
  17 52MQ    XF    LDX   0  EOPMODE(3)          [PASSED FIRST CHECK  
   8 537B          SBN   0  #1700
  18 53M2          BZE   0  XL                  [JUMP IF OPEN MODE=1700  
  21 546L          LDN   7  7                   [UNIT CANNOT BE OPENED SY S-TRUSTED  
   8 54L=          LDN   1  BCCB 
  19 555W    XN    LDX   1  BPTR(1)             [CHECK OUR UNIT NOT IN USE BY
  15 55KG          TXU   1  CXCC                [ANYBODY ELSE
   8 5656          BCC      XM   
   9 56JQ          LDX   0  BACK2(1) 
   8 574B          BZE   0  XM   
   9 57J2          LDX   0  ASUSN(2) 
   9 583L          TXU   0  ASUSN(1) 
  18 58H=          BCC      XH                  [JUMP IF FIP HOMES SAME  
  17 592W          LDEX  4  FIP+5(1)            [NO. OF FILE AREAS   
  15 59GG          BZE   4  XN                  [JUMP IF NONE
   7 5=26          LDN   5  0
   7 5=FQ    XP    SMO      5
  12 5=_B          TXU   0  FDCELLS(1)          [
  17 5?F2          BCC      XH                  [JUMP IF UNIT IN USE 
   7 5?YL          ADN   5  6
   8 5#D=          BCT   4  XP   
   8 5#XW          BRN      XN   
  21 5*CG    XL    LDN   1  BCCB                [MODE #1700 -CHECK IF ALREADY OPEN   
   9 5*X6    XQ    LDX   1  BPTR(1)  
   8 5BBQ          TXU   1  CXCC 
  16 5BWB          BCC      XM                  [J.END OF CHAIN  
   9 5CB2          LDX   0  BACK2(1) 
  16 5CTL          BZE   0  XM                  [OR NO MORE EXOFS
   9 5D*=          LDX   0  ASUSN(2) 
  17 5DSW          TXU   0  ASUSN(1)            [CHECK SERIAL NUMBER 
  16 5F#G          BCS      XQ                  [J. IF DIFFERENT 
   9 5FS6          LDX   0  HOME(1)  
   9 5G?Q          TXU   0  HOME(2)  
  17 5GRB          BCS      XQ                  [J.IF DIFFERENT HOMES
   9 5H?2    #SKI  K6SQOPEN>199-199  
   4 5HQL    (   
   8 5J==          LDCT  0  #404 
   9 5JPW          ANDX  0  EXMARK(1)
   8 5K9G          BZE   0  XOK  
  10 5KP6          GEOERR   1,MODE1700   
   4 5L8Q    XOK 
   4 5LNB    )   
  17 5M82          LDN   0  1                   [UNIT ALREADY OPEN   
  18 5MML          ADS   0  COPEN(1)            [ADD 1 TO COUNT OF OPENS 
  15 5N7=          LDX   6  1                   [SAVE POINTER
  16 5NLW          FREECORE 2                   [FREE OUR EXOF   
   8 5P6G          BRN      ZA   
  15 5PL6    XM    LDX   6  2                   [SAVE POINTER
  16 5Q5Q          EXOFILE  6                   [CHAIN IN EXOF   
  16 5QKB    ZA    LDX   2  6                   [RESTORE POINTER 
   9 5R52          LDX   0  BACK2(2) 
  17 5RJL          STO   0  CEFN(3)             [SAVE EXOFILE NUMBER.
   4 5S4=    #   
  11 5SHW    #     CONNECT FILE TO PROGRAM   
   4 5T3G    #   
   7 5T4K ...#UNS  CAFSINT   
   4 5T5N ...(   
  20 5T6R ...#     FOR CAFS, THIS A POINT WHERE A REPORT ON AN EVENT OF INTEREST 
  20 5T7W ...#     TO THE CAFS SUBSYSTEM IS REQUIRED.    A FILE IS BEING OPENED  
  12 5T8_ ...#     AND IT MAY BE OPENED UNCLEANLY
   4 5T=4 ...#   
  21 5T?7 ...      LDX   0  EOPMODE(3)          [GET THE MODE OF OPENING OF THE FILE 
   8 5T#= ...      SBN   0  #1700
  19 5T** ...      BNZ   0  NOTOFW              [JIF NOT OPENING FOR WRITING 
  18 5TBD ...      CAFSINFO OPENFILE,CEFN(3)    [NOTIFY CAFS SUBSYSTEM   
   4 5TCH ...#   
   5 5TDL ...NOTOFW  
   4 5TFP ...)   
  13 5TH6          LDCT  4  AOLPT/8             [TYPE
  14 5W2Q          LDN   5  EXAOLPT             [LENGTH  
   8 5WGB          GETACTX  4,5,1
  15 5X22          LDX   1  BPTR(2)             [SET UP AOLPT
  10 5XFL          MHUNTW   3,EWDAS,GEXD 
  19 5XPC ...      LDX   0  EXOTRAP(3)          [TRANSFER EXOFILE ENTRY TRAPS
  17 5XS2 ...      STO   0  EXTRAPS(1)          [FROM GEXD TO AOLPT  
  15 5X_=          LDX   0  JOBNO(2)            [JOB NUMBER  
   9 5YDW          STO   0  JOBNO(1) 
   8 5_XQ          LDX   0  GSIGN
  10 62CB          STO   0  FRECHADD(1)  
  14 62X2          STOZ     CPRW1(1)            [CLEAR   
  14 63BL          STOZ     CPRW2(1)            [REPLY   
  14 63W=          STOZ     CPRSW(1)            [WORDS   
  10 64*W          MFINDSU  2,ESERL(3)   
   9 64TG ...      LDX   4  HUNIT(2) 
  15 65*6 ...      STO   4  CPPTR(1)            [STO DL PTR  
   9 66#B          LDX   0  EXCHNL(3)
  14 66S2          STO   0  EWCHAN(1)           [CHANNEL 
   7 67?L          LDN   0  1
  18 67R=          STO   0  ATMARK(1)           [MARKER FOR AOLPT TYPE   
   8 68=W          LDCT  0  #400 
  18 68QG          ORS   0  GIVE(1)           [ GIVE MARKER OPEN ILLEGAL 
   9 69=6          LDX   0  CEFN(3)  
  16 69PQ          STO   0  EXNUM(1)            [EXOFILE NUMBER  
   8 6=9B          LDX   2  FX1  
  16 6=P2          LINKSET  1,XLINK(2)          [LINK TO K1PERION
  10 6?8L          LDX   0  EOPMODE(3)   
   9 6?N=          STO   0  EXMODE(1)
   8 6#7W          SBN   0  #1700
  15 6#MG          BZE   0  ZB                  [JUMP IF 1700
   8 6*76          LDCT  0  #200 
  10 6*LQ          ORS   0  EXMAYBE(1)   
   8 6B6B    ZB    LDX   2  FX2  
  14 6BL2          FPCACA   2                   [FIND PCA
  21 6CMT ...      VFREE    ADATA,ASUP          [ENSURE BLOCK DOESNT ALREADY EXIST   
  13 6D36 ...      LDX   5  ESERL(3)            [SUSN
   9 6DBC ...      LDX   6  EXCHNL(3)
  15 6DPN ...      LGEOG    4,4                 [GEOG UNIT NO
  17 6F4_ ...      SETNCORE 3,3,ADATA,ASUP      [NEW ADATA/ASUP BLOCK
   7 6FD= ...      LDN   0  6
  13 6FRH ...      STO   0  A1(3)               [*DA 
  15 6G6S ...      STO   4  A1+1(3)             [GEOG UNIT NO
  13 6GG5 ...      STO   5  A1+2(3)             [SUSN
   7 6GTB ...      LDCT  0  6
  15 6H8M ...      ORX   0  6                   [TYPE/UNIT NO
  15 6KYB          LDCT  6  #400                [OFFLINE BIT 
  16 6LD2          PUTPERENT 0,6                [CONNECT CHANNEL 
  16 6MC=          ROLPA                        [CHAIN IN OLPA   
  10 6NBG          MHUNTW   3,EWDAS,GEXD 
  16 6NW6    ZC    LDX   5  CNR(3)              [SAVE CA ADDRESS 
   9 6P*Q          LDX   4  ESERL(3) 
   8 6PTB          STO   4  A1(3)
   9 6Q*2          STOZ     A1+1(3)  
   9 6QSL          STOZ     A1+2(3)  
   9 6R#=          STOZ     A1+3(3)  
  15 6RRW          MFINDSU  1,A1(3)             [FIND ASTUC  
  10 6S?G          LDX   0  EOPMODE(3)   
   8 6SR6          SBN   0  #1700
  19 6T=Q          BZE   0  NOTS                [DON'T SET 'SET' BIT IF #1700
   8 6TQB          LDCT  0  #100 
  15 6W=2          ORS   0  ASMARK(1)           [UNIT SET BIT
   4 6WPL    NOTS
  16 72LL          LADDP    HUNIT(1),2          [X2 PTR TO AIPA  
  10 736=          LDX   0  BSUNIT1(2)   
   7 745G          SRL   0  6
  16 74K6          ANDN  0  #77                 [COUNT OF SU'S   
   7 754Q          SBN   0  1
  16 75JB          BZE   0  ZD                  [J. IF ONLY ONE  
   8 7642          LDCT  0  #400 
  16 76HL          ORX   0  ASUBASE(1)          [REPLY IF SPLIT  
   9 773=          STO   0  A1+3(3)  
  18 77GW    ZD    LDN   6  4                   [COUNT OF WORDS TO MOVE  
  15 782G    YB    UNLOCKSU 4                   [UNLOCK UNIT 
   7 78G6          RELST    4
   4 78_Q ...YBA 
   7 79FB          ADN   5  1
  15 79K5 ...      LDN   4  A1                  [ADDR IN GEXD
   5 79NS ...ZADDR   
  19 79SH ...      GETWORD  5,5,WRITE,7,6       [GEORGE ADDR OF OBJ PROG CA  
  17 79Y= ...      TESTREPNOT OK,RV1            [GEOERR FOR FAILURE  
  10 7=3_ ...      MHUNTW   3,EWDAS,GEXD 
  16 7=7N ...      ADX   4  3                   [X4=A1 OF GEXD   
   7 7=?C ...      TXL   7  6
  20 7=C6 ...      BCC      ZMOV                [J IF MOVE CAN BE DONE IN ONE GO 
   7 7=GT ...      SMO      7
  18 7=LJ ...      MOVE  4  0                   [MOVE AS MANY AS ALLOWED 
   9 7=Q? ...      LDX   5  CNR(3)   
   7 7=W2 ...      ADN   5  1
  19 7=_P ...      ADX   5  7                   [UPDATE AREA TO BE ACCESSSED 
  20 7?5D ...      SBX   6  7                   [DECREMENT NO OF WORDS REQUIRED  
   7 7?97 ...      SMO      7
  18 7?#W ...      LDN   4  A1                  [UPDATE POINTER IN GEXD  
   8 7?DK ...      BRN      ZADDR
   4 7?J# ...ZMOV
   7 7?N3 ...      SMO      6
  16 7?RQ ...      MOVE  4  0                   [MOVE IN REPLIES 
  15 7?XG          FREECORE 3                   [FREE GEXD   
   7 7#7# ...      WORDFIN   
  17 7#C6          RUNPROG                      [AND RESTART PROGRAM.
   4 7#WQ    #   
  16 7*BB ...#     UNIT NOT ONLINE-CHECK FOR ADDITIVE MODE #4000 
  15 7*W2    #     OUTPUT REQUEST TO OPERATORS IF NECESSARY  
   4 7B*L    #   
  10 7BT=    XDA   MHUNTW   3,EWDAS,GEXD 
   7 7C#W          ANDN  7  2
  19 7CSG          BZE   7  YA                  [JUMP IF MODE #4000 NOT USED 
  16 7D#6          LDX   5  CNR(3)              [SAVE CA ADDRESS 
   8 7DRQ          LDCT  0  #400 
  18 7F?B          ORN   0  6                   [REPLY= UNIT NOT ONLINE  
   8 7FR2          STO   0  A1(3)
  17 7G=L          LDN   6  1                   [1 WORD TO MOVE IN   
  17 7GQ=          BRN      YBA                 [JUMP TO SET REPLIES 
  16 7H9W    YA    SMUSEST  ESERL(3),YC,YD      [GET UNIT ONLINE 
   8 7HPG          BRN      YE   
   4 7J96    #   
   4 7JGG ...#   
   7 7JKL ...#     IPB FAIL  
   4 7JNQ ...#   
   7 7JRW ...ND    NGNC  7  1
   4 7JX2 ...#   
   7 7K26 ...#     BREAKIN   
   4 7K5= ...#   
  21 7K8B ...NC    LDN   7  0                   [X7=1 IF IPB FAIL;X7=0 FOR BREAKIN   
  10 7K?G ...      MHUNTW  3,EWDAS,GEXD  
   9 7L7L          RELST    ESERL(3) 
  14 7M6W    YD    MHUNTW   3,EWDAS,GEXD        [BREAKIN 
  13 7M=7 ...      LDX   6  ESERL(3)            [SUSN
   7 7M?= ...      FREECORE 3
  16 7M#* ...      STEPBACK                     [PUT ONR BACK ONE
  15 7M*D ...      BZE   7  NIPB                [J IF BREAKIN
  15 7MBH ...      TWAIT    6,NIPB              [WAIT FOR IPB
  16 7MCL ...      RUNPROG                      [..AND TRY AGAIN 
  16 7MDP ...NIPB  PROGBRKIN                    [REPORT BREAKIN  
   4 7P5B    #   
  18 7QJ=    NB    OUTPACK  ESERL(3),1,CSN      [CONVERT SERIAL NUMBER   
   9 7R3W          MONOUT   ENTIN35  
  10 7RHG          MHUNTW   3,EWDAS,GEXD 
   9 7RPQ ...      RELST    ESERL(3) 
  14 7T2B    YC    MHUNTW   3,EWDAS,GEXD        [CANTDO  
  20 7TG2          LDX   5  EXCHNL(3)           [CONVERT CHANNEL NO. TO DECIMAL  
   7 7T_L ...      LDCT  4  6
  18 7WF= ...      ADX   4  5                   [DEVICE TYPE AND CHANNEL 
  16 82B=          LDX   5  CNR(3)              [SAVE CA ADDRESS 
   7 82TW          FREECORE 3
  11 83*G ...XPAR  OUTPACK 4,1,PERINAME,XPAR 
   7 84#Q          ADN   5  1
   9 84SB ...      GETWORD  5,5,WRITE
   9 85#2 ...      TESTREPNOT OK,RV1 
   8 85RL ...      LDCT  4  #440 
   7 86?=          SMO      5
   7 86KM ...      STO   4  0
   7 86Y4 ...      WORDFIN   
  17 87=G          LDX   4  XPER(1)             [ILLEGAL IDENTIFIER  
   7 87Q6    XVIOL ILLEGALX 4
   4 889Q    #   
   4 88PB    #   
   9 8992    RV    GEOERR   1,RESVIOL
   4 89B8 ...RV1 
   9 89HB ...      GEOERR   1,SDERV  
   4 89NL    #   
  10 8=8=          MENDAREA 30,K99SQOPEN 
   4 8=MW    #   
   4 8?7G    #END
   8 ____ ...70231154000100000000
