  16 22C4 ...#SEG  RENGAGE8                     [G CONSTANTINIDES
  14 22S6 ...#LIS  K0RENGAGE>K0PCT>K0ALLGEO>K0GREATGEO   
  10 2398 ...               8HRENGAGE8   
   7 23L= ...# ENTRY POINTS  
  12 243# ...      SEGENTRY K1RENGAGE,Z1RENGAGE  
  12 24DB ...      SEGENTRY K2RENGAGE,Z2RENGAGE  
  12 24TD ...      SEGENTRY K3RENGAGE,Z3RENGAGE  
   6 24Y3 ...#UNS  JSKI33
  12 252L ...      SEGENTRY K4RENGAGE,Z4RENGAGE  
   6 2559 ...#UNS  JSKI33
  12 257S ...      SEGENTRY K5RENGAGE,Z5RENGAGE  
  12 25=G ...      MENDAREA APCTGAP,K100RENGAGE  
   4 25MJ ...[   
  21 264L ...[ THIS SEGMENT IS ENTERED BY A CPA OR ACTLF WHEN IT IS WAITING FOR AN   
  21 26FN ...[ ENGAGE ON A REMOTE 7020 CLUSTER. IT OUTPUTS A REGULAR 'PLEASE ENGAGE' 
   6 26WQ ...[ MESSAGE.  
   4 27?S ...[   
   4 27NW ...[   
   4 285Y ...[   
  19 28H2 ...[ K1 IS THE NORMAL ENTRY POINT. (DOWN FROM THE ^SLEEP^ MACRO).  
   4 28Y4 ...[   
  20 29*6 ...[ K2 IS ENTERED BY A DOWN FROM THE ^SLEEP2^ MACRO AND IS USED DURING
  20 29Q8 ...[ LISTINGS ONLY. IT RESETS THE ACTLF FOR ANY ERROR RESTARTS, TAKING 
  16 2=7= ...[ ^CAN'T DO^ OR ^TERMINATE^ ACTION IF NECCESSARY.   
   4 2=J# ...[   
   4 2=_B ...[   
  20 2?BD ...[ K3 IS ENTERED BY A DOWN FROM RMTLF AFTER LISTING THE SETUP FILE.  
  21 2?RG ...[ IT OUTPUTS THE 'PLEASE LINE UP' MESSAGE AND TAKESAPPROPRIATE ACTION.  
   4 2#8J ...[   
   6 2#=P ...#UNS  JSKI33
   4 2##W ...(   
   9 2#C3 ...ZZCLET         #77763777
   9 2#F8 ...ZJCORRUPT      +JCORRUPT
   4 2#H* ...)   
   8 2#KL ...ZJPAGES     +JPAGES 
   8 2*2N ...ZJKEN          +JKEN
  18 2*CQ ...ZJMARY         +JMARY              [PLEASE ENGAGE UNIT IXXN.
  21 2*SS ...ZJRESET        +JRESET             [.......DITTO.......FOR ERROR RESTART
  16 2B9W ...# ZJRESET MUST OCCUR AFTER ZJMARY IN THIS SEGMENT.  
   4 2BLY ...#   
  10 2C42 ...# FREE BLOCKS LEFT BY DEMAND
   4 2CF4 ...#   
  10 2CW6 ...RIDEM VFREEW   ADATA,ADEMAND
  21 2D?8 ...      VFREEW   GMON,AOUT           [ABSENT IF DEMANDY DELAY NOT ENDED   
   7 2DN= ...      EXIT  5  0
   4 2F5# ...[   
  12 2FGB ...# SET UP IWORK WORDS FOR SETUP FILE 
   4 2FXD ...#   
   6 2G#G ...SETUPWORK   
   7 2GPJ ...      LDN   0  1
  20 2H6L ...      STO   0  IWORK1(2)           [START SETUP LISTING AT BEGINING 
   8 2HHN ...      LDCT  0  #200 
  20 2HYQ ...      STO   0  IWORK2(2)           [SET 'ALL' RESTART FOR SETUP FILE
  18 2J*S ...      NGS   1  IWORK3(2)           [LIST ALL OF SETUP FILE  
   7 2NTG ...      EXIT  4  0
   4 2P=J ...#   
   6 2PML ...XLOCACONV   
  16 2Q4N ...#     SUBROUTINE HUNTS FOR ADATA,ACONV BLOCK ON X2  
   7 2QFQ ...#     LINK IN X1
  10 2QWS ...      MHUNTW   2,ADATA,ACONV
   8 2R?W ...      BRN      (1)  
   4 2RNY ...#   
  19 2RPD ...TNENG LDCH  0  ATYPE(2)            [SUB TO CHECK IF LF DEVICE...
  21 2RPY ...[                                   ...ENGED WHEN THEY SAID THEY WOULDNT
   9 2RQD ...      SBN   0  ACTLF/64 
  15 2RQY ...      BNZ   0  (5)                 [J IF NOT LF 
  15 2RRD ...      HUNT7020 3,2                 [FIND BUFFER 
   7 2RRY ...      LDCT  0  3
   9 2RSD ...      ANDX  0  EWAIT(3) 
  17 2RSY ...      BNZ   0  (5)                 [J IF MOP OFF OR INOP
   8 2RTD ...      LDCT  0  #40  
   9 2RTY ...      ANDX  0  EWAIT(3) 
  15 2RWD ...      BZE   0  XENGAGE             [J IF ENGAGED
   7 2RWY ...      EXIT  5  0
   4 2RXD ...[   
   4 2RXY ...[   
   4 2RYD ...[   
  16 2RYY ...TENCD                              [CANTDO ENGAGE   
  18 2R_D ...      CALL  5  TNENG               [DOESNT EXIT IF ENGAGED  
   8 2R_Y ...      BRN      XCDY 
   4 2S2D ...[   
   4 2S2Y ...[   
   4 2S3D ...[   
  15 2S3Y ...TENTE                              [TERMINATE   
  18 2S4D ...      CALL  5  TNENG               [DOESNT EXIT IF ENGAGED  
   8 2S4Y ...      BRN      XTE  
   4 2S62 ...#   
   4 2SH4 ...#   
   6 2SH9 ...#UNS  JSKI33
   4 2SHB ...(   
   4 2SHH ...[   
   4 2SHN ...[   
   4 2SHT ...[   
  16 2SJ2 ...[ ENTRY PT. DOWN FROM RMTLF TO DEALLOCATE LP OR TP  
   4 2SJ7 ...[   
   4 2SJ# ...[   
  19 2SJF ...[                         ENTRY FROM PCTLISTA FOR CORRUPT FILE  
   6 2SJL ...Z5RENGAGE   
  18 2SJR ...      LDN   7  0                [MARK ENTRY FROM PCTLISTA   
   8 2SJY ...      BRN      ZTDNG
   4 2SK5 ...[   
  20 2SK= ...[                               ENTRY FROM RMTLF FOR CORRUPT FILE   
   4 2SKC ...[   
   6 2SKJ ...Z4RENGAGE   
  17 2SKP ...      LDCT  7  #400              [MARK ENTRY FROM RMTLF 
   5 2SKW ...ZTDNG   
   8 2SL3 ...      LFBITCLR M,2  
  10 2SL8 ...      MHUNTW   3,A7020,ALIST
   8 2SL* ...      LDCT  0  #100 
   9 2SLG ...      ANDX  0  EWAIT(3) 
   8 2SLM ...      BNZ   0  TWAKL
   6 2SLS ...      FLIP  
   5 2SL_ ...TWAKL   
   8 2SM6 ...      WAKEUP   LF   
  10 2SM? ...SLERE MHUNTW   3,A7020,ALIST
   8 2SMD ...      LDX   0  BIT11
   8 2SMK ...      ORN   0  #4000
   9 2SMQ ...      ANDX  0  EWAIT(3) 
   8 2SMX ...      BZE   0  TWUP2
   8 2SN4 ...      LDCT  0  #43  
   9 2SN9 ...      ANDX  0  EWAIT(3) 
   9 2SNB ...      BNZ   0  ZENGREQ  
   9 2SNH ...      LDX   0  EWAIT(3) 
   8 2SNN ...      ANDN  0  #4000
  18 2SNT ...      BZE   0  TWCO                [J IF DC4 NOT SENT YET   
   7 2SP2 ...      LDN   0  4
   9 2SP7 ...      ANDX  0  EWAIT(3) 
  15 2SP# ...      BNZ   0  SAYCO              [J IF SPOOLING
  18 2SPF ...      LONGSET  #36,XFAIL           [REMOTE ENG. BUTTON WAIT 
   7 2SPL ...      LDN   6  2
  19 2SPR ...WTDIN COOR4    #62                [GIVE PCTCON TIME TO SEE INOP 
   8 2SPY ...      BCT   6  WTDIN
   7 2SQ5 ...      LONGOFF   
  10 2SQ= ...      MHUNTW   3,A7020,ALIST
   8 2SQC ...      LDCT  0  #43  
   9 2SQJ ...      ANDX  0  EWAIT(3) 
  18 2SQP ...      BNZ   0  ZENGREQ             [J UNLESS ALREADY ENGAGED
   8 2SQW ...SAYCO LDX   1  FX1  
   9 2SR3 ...      LDX   0  ZZCLET(1)
   9 2SR8 ...      ANDS  0  EWAIT(3) 
   6 2SR* ...      OUTPER
  10 2SRG ...      INFORM   0,JCORRUPT,1 
   8 2SRM ...      BRN      TWUP2
   8 2SRS ...TWCO  LDCT  0  #400 
   9 2SR_ ...      ORS   0  EWAIT(3) 
   6 2SS6 ...      COOR1 
   8 2SS? ...      BRN      SLERE
   4 2SSD ...[   
   4 2SSK ...[   
   5 2SSQ ...ZENGREQ 
   8 2SSX ...      LDCT  0  #100 
   9 2ST4 ...      ANDX  0  EWAIT(3) 
  18 2ST9 ...      BZE   0  NDEVR                 [J IF NO DEVICE ERROR  
   8 2STB ...      SMO      FX1  
   9 2STH ...      LDX   0  ZZCLET   
   9 2STN ...      ANDS  0  EWAIT(3) 
   5 2STT ...NDEVR   
   8 2SW2 ...      BZE   7  NOPAR
   4 2SW7 ...[   
   4 2SW# ...[   
   4 2SWF ...)   
   6 2SY6 ...Z2RENGAGE   
  17 2T*8 ...      LDX   7  GSIGN               [SET K2 ENTRY MARKER.
  18 2TQ= ...      MHUNTW   1,A7020,ALIST       [FIND THE BUFFER BLOCK.  
   8 2TWK ...      LDX   0  BIT9 
   9 2W2Y ...      ORS   0  EWAIT(1) 
  20 2W7# ...      LDCT  0  3                   [J. DOWN THE ^CAN'T DO^ PATH IF  
  19 2WJB ...      ANDX  0  EWAIT(1)            [INOPERABLE OR MOPPED OFF.   
   8 2W_D ...      BNZ   0  YCD  
  21 2XBG ...      LDCT  0  #100                [J. OUT IF NO ERROR: ORDINARY RESTART
   9 2XRJ ...      ANDX  0  EWAIT(1) 
   8 2Y8L ...      BZE   0  NOPAR
   9 2YKN ...RCDEN LDX   0  IWORK2(2)
  17 2_2Q ...SPLRC BNG   0  PAGE                [J. IF PAGE RESTART  
  19 2_CS ...      BNZ   0  NOPAR               [NOWT TO DO IF ^ALL^ RESTART.
  21 2_SW ...PAGE  LDX   4  BITS22LS            [GET RESTART RECORD NO. FOR FAILED   
  20 329Y ...      ANDX  4  INTLK4(1)           [BUFFER (TOP BITS MAY BE SET).   
   8 32#H ...      BNZ   4  ZKLK4
  16 32C6 ...[                                   J IF INTLK4 OK  
  20 32FP ...      LDX   4  IWORK14(2)           [ELSE USE INITIAL LINE NUMBER   
   5 32J# ...ZKLK4   
  18 32M2 ...      BPZ   0  RESET               [J. UNLESS PAGE RESTART. 
  21 3344 ...# GET NUMBER OF PAGES IT IS POSSIBLE TO RETREAT. IF > 1 ASK FOR PAGES.  
  19 33F6 ...      MHUNTW   3,ADATA,IPTEMP      [GET THE PAGE RESTART BLOCK. 
   8 33HP ...      LDX   0  A1(3)
   8 33L# ...      BPZ   0  ZAPOS
   8 33NX ...      STOZ     A1(3)
   5 33RG ...ZAPOS   
  21 33W8 ...      LDX   0  ALOGLEN(3)          [X0 CONTAINS VALUE OF IPAGE AT START 
   8 34?= ...      LDCT  6  #100 
   7 34N# ...      ANDX  6  7
   8 355B ...      BNZ   6  NTVI 
   7 35GD ...      LDN   6  4
   9 35XG ...      ANDX  6  EWAIT(1) 
   8 364P ...      BZE   6  NTVI 
   7 369Y ...      LDN   5  1
  10 36C7 ...      STO   5  ACOMMUNE6(2) 
   8 36JB ...      BRN      NMVD 
   4 36PL ...NTVI
  21 376N ...      SBN   0  2                   [OF LISTING: MAX. NO. OF PAGES WE CAN
  21 37HQ ...      LDN   5  0                   [SET COUNT.                  [GO BACK
  21 37YS ...      BXL   0  A1(3),XALL          [J. IF ALL ENTRIES IN BLOCK VALID.   
  21 38*W ...      LDX   0  A1(3)               [OTHERWISE USE COUNT OF PAGES THROWN.
  18 3982 ...XALL  ADN   0  1                   [COUNT OF VALID ENTRIES. 
  21 3994 ...[                                   LOOK FOR ENTRY FOR BEGINNING OF LAST
   7 39=6 ...      LDX   6  0
   7 39?8 ...NVSR  SMO      6
   8 39#= ...      TXU   4  A1(3)
  16 39*# ...      BCC      NXT1                [J IF FOUND ENTRY
   8 39BB ...      BCT   6  NVSR 
  15 39CD ...[                                  J TO TRY NEXT
   8 39DG ...      STOZ     A1(3)
  15 39FJ ...[                                   IF NOT FOUND
  20 39GL ...      STO   4  A1+1(3)             [JUST RESTART FROM LAST OUTPUT   
   8 39HN ...      BRN      RESET
  20 39K4 ...NXT1  SMO      0                   [ADD 1 INTO PAGES COUNT (X5) FOR 
  21 3=26 ...      TXL   4  A1(3)               [EVERY RESTART RECORD < FAILED RECORD
  21 3=C8 ...      BCS      NXT2                [EVENTUALLY GETTING COUNT OF PAGES   
  21 3=S= ...      ADN   5  1                   [WE CAN GO BACK FROM FAILURE POINT.  
   8 3?9# ...NXT2  BCT   0  NXT1 
  20 3?LB ...      STO   5  ACOMMUNE6(2)        [STORE THIS COUNT FOR THE ^PAGES^
  21 3#3D ...      SBN   5  1                   [DON'T ASK FOR PAGES IF      [COMMAND
  21 3#DG ...      BZE   5  ONLY1               [WE CAN ONLY GO BACK 1 PAGE ANYHOW   
   8 3#TJ ...      LDCT  0  #100 
   9 3*=L ...      ANDX  0  FINISH   
   8 3*MN ...      BNZ   0  YCD  
  21 3B4Q ...      LONGSET  #104,XFAIL          [WAITING STYLE CANNOT BE ^TERMINATE^D
   7 3BFS ...      PHOTO    6
  21 3BWW ...      OUTPER                       [SET UP PERIPHERAL IDENTIFIER PARAMET
  10 3C?Y ...      LDX   5  ZJPAGES(1)   
  10 3CP2 ...      MHUNTW   1,A7020,ALIST
   7 3D64 ...      LDN   0  2
   9 3DH6 ...      ANDX  0  EWAIT(1) 
  11 3DY8 ...      DEMANDX  0,5,PCD,NOTE,,2  
  17 3F*= ...      CALL  5  RIDEM               [FREE DEMAND BLOCKS  
  20 3FQ# ...PRCD  TESTMOVE 6,NMVD              [RESET THE POINTER, IF NECESSARY.
  11 3G7B ...      MHUNTW   3,ADATA,IPTEMP   
   5 3G=Y ...ONLY1   
  10 3GBG ...NMVD  LDX   5  ALOGLEN(3)   
   8 3GKL ...      LDX   1  A1(3)
   7 3GSQ ...      SBN   5  1
   7 3H3W ...      DVS   0  5
   7 3H?2 ...      LDX   6  1
   7 3HP= ...      LDN   1  1
   7 3HYB ...NXT3  SMO      1
   8 3J7G ...      TXU   4  A1(3)
   8 3JBL ...      BCC      RFND 
   8 3JKQ ...      BUX   1  NXT3 
  10 3JSW ...RFND  SBX   1  ACOMMUNE6(2) 
   8 3JYM ...      BPZ   1  POS  
  21 3K4D ...                                       [X6 = NO. OF TIMES IPTEMP HAS BEE
   7 3K89 ...      SBN   6  1
   8 3K#2 ...      BNG   6  SMBLK
  21 3KCR ...                                      [J. STILL FILLING IPTEMP FOR 1ST T
  21 3KHJ ...      ADX   1  5                       [PICK UP ENTRY FROM END OF BLOCK 
   8 3KM* ...      BRN      POS  
   5 3KR6 ...SMBLK   
  21 3KWX ...      LDN   1  0                        [NUMBER > LIST START FROM BEGINI
  18 3L2N ...      LDN   6  0                       [SET X6 TO BEGINING  
   4 3L6F ...POS 
   7 3L== ...      SMO      1
  20 3LB3 ...      LDX   4  A1+1(3)                  [LD. RECORD FOR RESTART PAGE
   7 3LFS ...      LDX   0  6
  21 3LKK ...      MPA   0  5                       [MULT X0 BY X5 ADD X1 TO RESULT  
   8 3LPD ...      STO   1  A1(3)
   8 3LT2 ...      LDX   1  FX1  
  19 3P5D ...RESET STO   4  IWORK14(2)          [INSERT THE RESTART LINE NO. 
  21 3PGG ...      SBX   4  IWORK1(2)           [DIFFERENCE TWIXT RESTART AND CURRENT
  19 3PXJ ...      LDXC  0  IWORK3(2)           [J. IF LISTING ALL OF FILE.  
   8 3Q#L ...      BCS      NOPAR
  21 3QPN ...      SBS   4  IWORK3(2)           [RESET NO. OF LINES STILL TO BE LISTE
   8 3R6Q ...      BRN      NOPAR
   4 3RHS ...#   
  21 3RYW ...PCD   LDN   0  1                   [SET DEFAULT OF 1 PAGE IF REPLY CAN'T
  10 3S*Y ...      STO   0  ACOMMUNE6(2) 
   8 3SR2 ...      BRN      PRCD 
   4 3T84 ...#   
   4 3TK6 ...#   
   4 3W28 ...#   
  21 3WC= ...Z1RENGAGE                          [ENTERED WHEN THE REMOTE PERIPHERAL  
  20 3WS# ...                                   [ASSOCIATED WITH THE ACTIVITY IS 
  18 3X9B ...                                   [REQUIRED TO BE ENGAGED. 
   4 3XLD ...#   
  18 3Y3G ...      LDN   7  0                   [SET MARKER FOR K1ENTRY. 
   8 3YDJ ...NOPAR LDCT  0  #100 
   7 3YTL ...      LDX   3  0
   7 3_=N ...      ANDX  3  7
   7 3_MQ ...      ERS   3  7
   9 424S ...      ANDX  0  FINISH   
   8 42FW ...      BNZ   0  XCD  
   8 42WY ...      BNZ   3  XCDY1
  16 43#2 ...      HUNT7020 3,2                 [FIND 7020 BUFFER
   8 43P4 ...      LDCT  0  #040 
   9 4466 ...      ANDX  0  EWAIT(3) 
  16 44H8 ...      BZE   0  ZENGA                [J. IF ENGAGED  
   4 44HR ...[   
   4 44JB ...[   
  17 44J_ ...[     TAKE TE ACTION IF DISENGAGED DURING EB ASTERISKS  
   4 44KJ ...[   
   4 44L7 ...[   
  18 44LQ ...      BNZ   7  ZNLFE               [J IF NOT FROM K1 OR K5  
   4 44M* ...[   
   4 44MY ...[   
   8 44NH ...      LDX   2  FX2  
   9 44P6 ...      LDCH  0  ATYPE(2) 
   9 44PP ...      SBN   0  ACTLF/64 
  16 44Q# ...      BNZ   0  ZNLFE               [J IF NOT ACTLF  
  10 44QX ...      LDX   0  IWORK10(2)   
   7 44RG ...      ANDN  0  1
  18 44S5 ...      BNZ   0  XCDZ                [J TO SIMULATE TE IF EB  
   4 44SN ...[   
   4 44T? ...[   
   5 44TW ...ZNLFE   
   4 44WF ...[   
   4 44X4 ...[   
   4 44XM ...[   
   9 44Y= ...      LONGSET  #36,XFAIL
   7 45*# ...      PHOTO    5
   6 45QB ...      OUTPER
   8 467D ...      TESTMOVE 5,NMV
   8 46JG ...      HUNT7020 3,2  
  17 46_J ...NMV   LDCT  2  #100                [J. IF NOT ERRORED.  
   9 47BL ...      ANDX  2  EWAIT(3) 
   8 47RN ...      BZE   2  PLES 
  21 488Q ...      FLIP                         [FLIP SO THAT PCTCON NOTIFIES OPERABI
  18 48KS ...      LDN   2  ZJRESET-ZJMARY      [SET UP AND OUTPUT THE   
  18 492W ...PLES  SMO      FX1                 [APPROPRIATE REQUEST FOR 
  20 49CY ...      LDX   5  ZJMARY(2)           [THE DEVICE TO BE RE-ALLOCATED.  
   6 49DY ...#UNS  JSKI33
   4 49FY ...(   
   8 49GY ...      LDN   0  #4000
   9 49HY ...      ANDX  0  EWAIT(3) 
   8 49JY ...      BZE   0  TREQ 
   8 49KY ...      SMO      FX1  
   9 49LY ...      LDX   0  ZZCLET   
   9 49MY ...      ANDS  0  EWAIT(3) 
   8 49NY ...      SMO      FX1  
   9 49PY ...      LDX   5  ZJCORRUPT
   4 49QY ...TREQ
   4 49RY ...)   
   7 49T2 ...      LDN   0  2
   9 4==4 ...      ANDX  0  EWAIT(3) 
  12 4=M6 ...      DEMANDX  0,5,TENCD,TENTE,RE,1 
   8 4?48 ...      LDCT  0  #100 
   9 4?F= ...      ANDX  0  FINISH   
   8 4?W# ...      BNZ   0  XCD  
  18 4#?B ...      HUNT7020 3,2                 [FIND THE 7020 BUFFER.   
  15 4#ND ...      LDCT  0  #003                [HAS MOPOFF  
  16 4*5G ...      ANDX  0  EWAIT(3)            [OR INOPERABILITY
  15 4*GJ ...      BNZ   0  XCD                 [HAPPENED?   
  14 4*XL ...      LDCT  0  #040                [NO - IS 
  17 4B#N ...      ANDX  0  EWAIT(3)            [THE PERIPHERAL NOW  
  14 4BPQ ...      BZE   0  XENGAGE             [ENGAGED?
  13 4C6S ...      LONGSET  #36,XFAIL           [NO  
  20 4CHW ...      BRN      RE                  [RE-ENTER THE DEMAND ROUTINES.   
   4 4CYY ...#   
   8 4D3H ...ZENGA LDN   0  #4000
   9 4D66 ...      ANDX  0  EWAIT(3) 
  19 4D8P ...      ERS   0  EWAIT(3)                 [UNSET B12 IF ENGAGED   
   8 4D?# ...      BRN      XENG 
   5 4DB2 ...XENGAGE 
  17 4DR4 ...      CALL  5  RIDEM               [FREE DEMAND BLOCKS  
   7 4DY? ...XENG  NGN   6  1
   8 4F5G ...      ERX   6  BIT9 
  19 4F=P ...      ANDS  6  EWAIT(3)              [ENSURE B9 IN EWAIT UNSET  
  16 4FCY ...      LDCT  6  #100                [ERROR BIT SET?  
   9 4FK8 ...      ANDX  6  EWAIT(3) 
  20 4G2= ...      BZE   6  UPLS2               [NO - RETURN TO CONTINUE LISTING.
  19 4GC# ...      LDCH  0  ATYPE(2)            [FINISHED IF INPUT DEVICE.   
  17 4GSB ...      ERS   6  EWAIT(3)            [UNSET THE ERROR BIT.
   9 4H9D ...      SBN   0  ACTLF/64 
   8 4HLG ...      BNZ   0  UPLS2
   7 4HMR ...      LDN   5  4
   9 4HP4 ...      ANDX  5  EWAIT(3) 
   8 4HQ* ...      BZE   5  XNSP 
   8 4HRL ...      BPZ   7  XCD  
  11 4HSX ...      MHUNTW   1,ADATA,IPTEMP   
   8 4HW8 ...      LDX   0  A1(1)
   8 4HXF ...      BNG   0  XCD  
   8 4HYQ ...      LDX   1  FX1  
   4 4J23 ...XNSP
  21 4J3J ...      LDX   1  3                   [UNSET THE FLIP MARKER IF ITS SET.   
   7 4JDL ...      PCTHUNTB 1
   9 4JTN ...      LDEX  0  APCTID(3)
   7 4K=Q ...      ADX   1  0
   7 4KMS ...      BCHX  3  /
   9 4L4W ...      LDCH  0  APCTID(3)
   9 4LFY ...      ORS   0  APCTD(1) 
   9 4LX2 ...      ORS   0  APCTX(1) 
   9 4M#4 ...      ERS   0  APCTD(1) 
   9 4MP6 ...      ERS   0  APCTX(1) 
  19 4N68 ...      BZE   7  UPLS2               [J. UNLESS K2 ENTRY POINT.   
   4 4NH= ...#   
  15 4NY# ...      LDXC  0  AWORK1(2)           [J. IF TP.   
   8 4P*B ...      BCS      TPW17
  20 4PQD ...      STOZ     IWORK17(2)          [CLEAR CONTINUATION LINE MARKER. 
   8 4Q7G ...      BRN      XFREC
   7 4QJJ ...TPW17 LDN   7  4
   9 4Q_L ...      ANDX  7  EWAIT(3) 
   8 4RBN ...      BZE   7  XFREC
   9 4RRQ ...      LDX   7  BITS22LS 
  10 4S8S ...      ANDS  7  IWORK17(2)   
  20 4SKW ...XFREC LDN   7  ALFBUFFNUM          [ERROR RESTART DURING LISTING:   
  20 4T2Y ...      STOZ     INTLK1(3)           [RE-INITIALISE THE BUFFER BLOCK. 
   9 4TD2 ...      LDX   6  BITS22LS 
   9 4TT4 ...      STOZ     INTLK3(3)
   7 4W=6 ...YBUFF SMO      7
  10 4WM8 ...      ANDS  6  INTLK-1(3)   
   7 4X4= ...      SMO      7
  10 4XF# ...      STOZ     INTLK2-1(3)  
   8 4XWB ...      BCT   7  YBUFF
  19 4Y?D ...      LDX   5  IWORK14(2)          [SET THE START LINE NUMBER.  
  18 4YNG ...      LDX   0  IWORK2(2)           [J. IF NOT ^LINE^ RESTART
   9 4_5J ...      STO   5  IWORK1(2)
   8 4_GL ...      BNZ   0  REWIN
  21 4_XN ...      STO   5  IWORK19(2)          [PENULTIMATE BUFFER'S RESTART NUMBER.
  21 52#Q ...      BZE   4  TERM                [J. OUT IF ALREADY ON RESTART RECORD.
  15 52PS ...      NGX   4  4                   [X4 WAS -VE. 
  21 536W ...NBACK BACKSPACE                    [GO BACK AS NECESSARY FOR LINE RESTAR
   8 53HY ...      BCT   4  NBACK
   9 53_2 ...      LDXC  0  IWORK5(2)
  17 54B4 ...      BCC      YGRAPHIC            [BR IF NOT BASIC FILE
   9 54R6 ...      LDEX  4  FRH(3)   
   7 5588 ...      ADN   4  3
  18 55K= ...      CALL  1  XLOCACONV           [LOCATE ADATA,ACONV BLOCK
  10 562# ...      LDX   0  ALOGLEN(2)   
   7 56CB ...      TXL   0  4
  18 56SD ...      BCC      XBIGNUFF            [BR IF BLOCK BIG ENOUGH  
   7 579G ...      PHOTO    7
  15 57LJ ...      ALTLENG  2,4,XLOCACONV       [EXTEND BLOCK
  18 583L ...      TESTMOVE 7,STILL             [BR IF NO BLOCKS MOVED   
   7 58DN ...      STEPAGAIN 
   5 58TQ ...STILL   
   9 59=S ...      CALL  1  XLOCACONV
   5 59MW ...XBIGNUFF
   8 5=4Y ...      LDN   4  A1(2)
   9 5=G2 ...      SMO      FRH(3)   
  18 5=X4 ...      MOVE  3  0                   [COPY RECORD INTO ACONV  
  15 5?#6 ...      LDN   1  6                   [ 'NORMAL'   
   8 5?P8 ...      SMO      FX2  
   9 5#6= ...      LDXC  0  IWORK5   
   7 5#H# ...      ADXC  0  0
   9 5#YB ...      BCS      NORMAL   
  15 5**D ...      ADN   1  #20                 [ 'ALLCHARS' 
   5 5*QG ...NORMAL  
   8 5B7J ...      LDN   2  #10  
  18 5BJL ...      TRANSFOR 1,2                 [PERFORM TRANSFORMATION  
   5 5B_N ...YGRAPHIC
  21 5CBQ ...TERM  UP                           [FINISHED. (ALSO USED FOR ^TERMINATE^
   4 5CRS ...#   
   7 5C_3 ...REWIN LDN   0  4
   9 5D6= ...      ANDX  0  EWAIT(3) 
   9 5D?F ...      BNZ   0  NOTSET   
   8 5DDN ...      LDCT  0  #540 
  10 5DKY ...      ANDX  0  IWORK20(2)   
   9 5F32 ...      BNG   0  NOTSET   
   9 5FD4 ...      BZE   0  NOTSET   
   7 5FT6 ...      SLL   0  3
   8 5G=8 ...      BPZ   0  XRAX 
   8 5GM= ...      LDCT  0  #40  
  10 5H4# ...      ERS   0  IWORK20(2)   
   9 5HFB ...      BRN      NOTSET   
   4 5HWD ...XRAX
  17 5J?G ...      LDX   5  IWORK10(2)          [SETUP FILE'S GQUAL  
   9 5JNJ ...      LDN   1  IWORK6(2)
   9 5K5L ...      LDN   0  IWORK1(2)
   7 5KGN ...      MOVE  0  5
   9 5KXQ ...      STO   5  IWORK5(2)
   8 5L#S ...      LDX   0  GSIGN
  21 5LPW ...      ORS   0  IWORK20(2)          [SET THE 'LISTING SETUP FILE' MARKER 
  21 5M6Y ...      CALL  4  SETUPWORK           [SET UP IWORK WORDS FOR SETUP FILE   
   6 5MJ2 ...      REWIND
   8 5M_4 ...      FILEMOVE 1,0  
  21 5NB6 ...      ORX   5  IWORK10(2)          [J. IF NOT TRANSFORMING EITHER FILE. 
   8 5NR8 ...      BPZ   5  UPLS1
  21 5P8= ...      LDXC  0  IWORK10(2)          [J. IF TRANSFORMING SETUP FILE ONLY. 
   8 5PK# ...      BCC      YTRA 
  20 5Q2B ...      LDXC  0  IWORK5(2)           [J. IF TRANSFORMING BOTH FILES.  
   8 5QCD ...      BCS      UPLS1
  21 5QSG ...      MFREEW   ADATA,ACONV         [FREE THE TRANSFORM BLOCK WHILE SETUP
  19 5R9J ...      BRN      UPLS1               [FILE IS BEING LISTED AGAIN. 
  21 5RLL ...YTRA  SETNCORE 40,3,ADATA,ACONV    [SET UP A BLOCK FOR SETUP FILE LISTIN
  17 5S3N ...UPLS1 UPPLUS   1                   ['ALL' RESTART EXIT  
   4 5SDQ ...[   
   5 5STS ...NOTSET  
   6 5T=W ...      REWIND
  18 5TMY ...      LDXC  0  AWORK1(2)           [J. IF NOT LINE PRINTER. 
   8 5W52 ...      BCS      UPLS1
  20 5WG4 ...      STOZ     IWORK16(2)          [ENSURE PAGE THROW FOR RESTART.  
   8 5WX6 ...      BRN      UPLS1
   4 5X#8 ...#   
  17 5XP= ...XCDX  LONGOFF                      [CLEAR WAITING STYLE.
  16 5Y6# ...XCDY  LDCH  0  ATYPE(2)            [J. UNLESS LP.   
   9 5YHB ...      SBN   0  ACTLF/64 
   8 5YYD ...      BNZ   0  XCD  
  10 5_*G ...      MHUNTW   1,A7020,ALIST
   7 5_FT ...      LDXC  0  7
   8 5_L8 ...      BCC      XCNRP
   7 5_QJ ...      LDN   0  4
   9 627L ...      ANDX  0  EWAIT(1) 
   8 62JN ...      BNZ   0  SPLA 
   8 62_Q ...      LDCT  0  #100 
   9 63BS ...      ANDX  0  EWAIT(1) 
   8 63RW ...      BNZ   0  XCNRP
   8 648Y ...      LDCT  0  #100 
   7 64L2 ...      ORX   7  0
   8 6534 ...      BRN      RCDEN
  10 65?Q ...SPLA  MHUNTW 3,ADATA,IPTEMP 
   9 65JD ...      LDX   0  IWORK2(2)
   7 65T8 ...      SRC   0  1
   9 66== ...      ORX   0  BITS22LS 
   9 66M# ...      ANDX  0  IWORK2(2)
   8 66RM ...      LDCT  4  #100 
   7 66Y2 ...      ORX   7  4
   9 66YP ...      ANDX  4  EWAIT(1) 
   8 66_D ...      BZE   4  SPLRC
   7 672C ...      LDN   4  1
   8 673B ...      ADS   4  A1(3)
   8 674B ...      BRN      SPLRC
   7 67FD ...XCNRP LDX   3  1
   8 67WG ...      BRN      XCDER
  10 68?J ...XCDY1 MHUNTW   3,A7020,ALIST
  21 68NL ...XCDER LDCT  0  4                   [SET THE ^WAITING TO BE WRONGED^ BIT.
   9 695N ...      ORS   0  EWAIT(3) 
   8 69GQ ...      BUX   7  XCDZ 
   4 69XS ...#   
  18 6=#W ...XCD   ADN   7  1                   [MARKER FOR ^CAN'T DO^.  
   8 6=PY ...XTE   HUNT7020 3,2  
   8 6?72 ...XCDZ  LDCT  0  #40  
   9 6?J4 ...      ORS   0  EWAIT(3) 
   9 6?_6 ...      ERS   0  EWAIT(3) 
  20 6#B8 ...      LDCT  0  #002                [SET INOP BIT IN CANTDO ACTION   
  17 6#R= ...      ORS   0  EWAIT(3)            [IN CASE OF 'INPUT'  
  15 6*8# ...      LDX   5  APCTID(3)           [ISOLATE THE 
  18 6*KB ...      ANDX  5  HALFTOP             [INTERFACE BIT POSITION  
  15 6B2D ...      LDX   6  APCTID(3)           [ISOLATE THE 
  17 6BCG ...      ANDN  6  #7777               [RELATIVE PCT POINTER
  20 6BSJ ...      PCTHUNTB 3                   [FIND THE PCT INFORMATION BLOCK  
  20 6C9L ...      ADX   3  6                   [POINT TO THE RELAVANT PCT RECORD
  14 6CLN ...      LDX   1  3                   [FIND THE
  14 6D3Q ...NXT4  LDX   0  APCTDEV(1)          [RELEVANT
  14 6DDS ...      ANDX  0  5                   [DEVISE  
  14 6DTW ...      BNZ   0  XFND                [RECORD  
  18 6F=Y ...      BDX   1  NXT4                [IN THE PCT DESCRIPTION. 
   4 6FN2 ...#   
  14 6G54 ...XFND  SRL   0  12                  [REMOVE  
  16 6GG6 ...      ORS   0  APCTX(3)            [THE SECONDARY   
  15 6GX8 ...      ORS   0  APCTD(3)            [OR PRIMARY  
  14 6H#= ...      ERS   0  APCTX(3)            [FLIP BIT
  15 6HP# ...      ERS   0  APCTD(3)            [IF IT'S SET 
  14 6J6B ...      LDX   0  APCTDEV+1(1)        [ENGAGE  
  14 6JHD ...      ANDN  0  #0100               [OCCURED?
  10 6JYG ...      ERS   0  APCTDEV+1(1) 
   7 6K*J ...      LDXC  7  7
   8 6KQL ...      BCS      WASKX
   8 6L7N ...      BNZ   7  UPLS1
   5 6LJQ ...      UP
   7 6LR? ...#SKI  JSKI33<1$1
   7 6L_S ...UPLS2 UPPLUS   2
   6 6M35 ...#SKI  JSKI33
   4 6M4B ...(   
   8 6M5M ...UPLS2 LDX   2  FX2  
   9 6M6Y ...      LDCH  0  ATYPE(2) 
   9 6M89 ...      SBN   0  ACTLF/64 
   8 6M9G ...      BZE   0  SLERE
   5 6M=R ...TWUP2   
   7 6M#4 ...      UPPLUS   2
   4 6M** ...)   
   4 6MBW ...#   
  21 6MRY ...YCD   LDX   4  BITS22LS            [GET THE RESTART NO. OF THE FAILED   
  14 6N92 ...      ANDX  4  INTLK4(1)           [BLOCK.  
  17 6NL4 ...      LDXC  0  IWORK3(2)           [J. IF ^ALL^ LISTING.
   8 6P36 ...      BCS      WASK2
  21 6PD8 ...      SBX   4  IWORK1(2)           [UPDATE NO. OF LINES LEFT TO BE LISTE
   9 6PT= ...      SBS   4  IWORK3(2)
   4 6Q=# ...#   
  17 6QMB ...WASK2 ORN   7  1                   [SET CAN'T DO SWITCH 
  18 6R4D ...WASKX FREELINK                     [GET RID OF LINK TO RMTLF
   8 6RFG ...      LDCT  0  #100 
  19 6RWJ ...      LDX   4  IWORK20(2)          [RESET AWORK4 SO LISTEND WILL
  18 6S?L ...      ANDX  0  4                   [(& AWORK2 - SETUP BIT)  
  15 6SNN ...      STO   4  AWORK4(2)           [UNDERSTAND  
   9 6T5Q ...      STO   0  AWORK2(2)
  19 6TGS ...      LDXC  0  IWORK5(2)           [J. IF WASN'T TRANSFORMING.  
   9 6TXW ...      BCC      XTRANSF1 
  10 6W#Y ...      MFREEW   ADATA,ACONV  
   5 6WQ2 ...XTRANSF1
  19 6X74 ...      LDXC  0  IWORK2(2)           [J. UNLESS ^PAGE^ RESTART.   
   8 6XJ6 ...      BCC      XFR  
  19 6X_8 ...      MFREEW   ADATA,IPTEMP        [FREE THE PAGE RESTART BLOCK.
  18 6YB= ...XFR   BZE   7  YTE                 [J. IF ^TERMINATE^ING.   
  21 6YR# ...      BNG   4  WSET                [J. IF LISTING SETUP FILE : SET A5 -V
  15 6_8B ...      LDX   0  IWORK2(2)           [RESTART TYPE
   7 6_KD ...      SLL   0  1
   8 722G ...      BNG   0  XAXA 
  17 72CJ ...      LDX   4  IWORK14(2)          [LINE NO FOR RESTART 
   7 72L5 ...      SBN   4  1
   8 72SL ...      BRN      XAXB 
   7 739N ...XAXA  LDN   4  0
   4 73LQ ...XAXB
  18 743S ...      LFEND    CD,4                [CANTDO EXIT FOR RESTARTS
   4 74DW ...WSET
  17 74TY ...      LFEND    CD                  [CANTDF - NO RESTARTS
   4 75?2 ...#   
   4 75N4 ...YTE 
   8 75P6 ...      LDX   2  FX2  
  10 75Q8 ...      LDCH  0  IWORK20(2)   
   8 75R= ...      ANDN  0  #54  
   8 75S# ...      ERN   0  #50  
  20 75TB ...      BNZ   0  NSETE               [J IF NOT SETUP FILE AT TOP LEVEL
  19 75WD ...      FILEMOVE 1,0                 [PUT MAINFILE TO TOP LEVEL   
  20 75XG ...[                                   NOTE MB AND CENTRAL LFS GO ON TO
  20 75YJ ...[                                   MAINFILE IF SETUP TERMINATED.   
  21 75_L ...[                                   BUT GOING TO MF BEFORE END OF SF... 
  19 762N ...[                                   MEANS START MF NOT LINED UP.
   5 763Q ...NSETE   
  16 7656 ...      LFEND    TE                  [TERMINATE EXIT  
   4 76G8 ...#   
   4 76X= ...#   
  20 77## ...NOTE  GEOERR   1,TERNOTAL          [CAN'T TERMINATE PAGES REQUEST   
  17 77PB ...XFAIL GEOERR   1,WAITUNST          [LONGWAIT FIELDS SET?
   4 786D ...#   
   6 78HG ...Z3RENGAGE   
  17 78YJ ...      LDX   7  GSIGN               [K8 LISTEND SWITCH   
  17 79*L ...      MHUNTW   1,A7020,ALIST       [FIND BUFFER BLOCK   
   8 79QN ...      LDCT  0  #100 
   9 7=7Q ...      ANDX  0  FINISH   
  21 7=JS ...      BNZ   0  YCD                 [J. IF FINISH NOW TO SIMULATE CANTDO 
   7 7=_W ...      LDX   3  1
   9 7?56 ...      LDX   1  EWAIT(3) 
   7 7?8B ...      ANDN  1  4
   8 7??L ...      BNZ   1  TWILP
  19 7?BY ...      LONGSET  #36,XFAIL           [REMOTE ENGAGE BUTTON STYLE  
   5 7?KF ...TWILP   
  21 7?S2 ...      FLIP                         [MARK TO BE NOTIFIED OF RE-ENGAGEMENT
   7 7#94 ...      PHOTO    4
   7 7#L6 ...      LDN   6  2
  21 7*38 ...XCHAS COOR4    #62                 [WAIT SO PCTCON CAN SET PLEASE ENGAGE
  20 7*D= ...      BCT   6  XCHAS               [BIT IN EWAIT AFTER SENDING DC4  
   9 7*T# ...      TESTMOVE 4,NMVX   
  10 7B=B ...      MHUNTW   3,A7020,ALIST
   7 7BMD ...      PHOTO    4
   8 7C4G ...NMVX  LDCT  0  #40  
   9 7CFJ ...      ANDX  0  EWAIT(3) 
  18 7CWL ...      BNZ   0  NMVZ                [J. IF NOT YET RE-ENGAGED
   7 7C_W ...      LDN   0  4
   9 7D56 ...      ANDX  0  EWAIT(3) 
   8 7D8B ...      BNZ   0  NMVZ 
  21 7D?N ...      LONGOFF                      [MAY HAVE BEEN RE-ENGAGED AT ONCE AND
  20 7DNQ ...      BRN      XOKX                [THEREFORE WAIT FIELD NOT CLEARED
   6 7F5S ...NMVZ  OUTPER
   8 7FGW ...      LDCT  6  #200 
  10 7FXY ...      ANDX  6  IWORK20(2)   
  21 7G*2 ...      ERS   6  IWORK20(2)          [B1 SET IF FIRST TIME THROUGH HERE   
   8 7GQ4 ...      ADN   6  #2000
  20 7HJ8 ...      SLC   6  2                   [SET B11 FOR INTERVAL;SET B23 IF 
  18 7H_= ...      ERN   6  1                   [MINUTE DELAY REQUIRED   
  19 7JB# ...      LDX   5  ZJKEN(1)            [PLEASE LINE UP UNIT IXXN.   
  21 7JRB ...      STO   7  IWORK14(2)          [SET -VE. SO ENTRY IS UNALTERED IF CD
  10 7K8D ...      MHUNTW   3,A7020,ALIST
   7 7KB_ ...      LDN   0  0
   9 7KKG ...      LDX   3   EWAIT(3)
   7 7L2J ...      ANDN  3  4
   8 7LCL ...      BNZ   3  TWINF
  13 7LSN ...      DEMANDY  0,5,XCDY1,TERMA,YMOC,6   
   9 7M9Q ...      TESTMOVE 4,NOMOV  
  10 7MLS ...      MHUNTW   3,A7020,ALIST
   8 7N3W ...NOMOV LDCT  0  #003 
   9 7NDY ...      ANDX  0  EWAIT(3) 
  18 7NW2 ...      BNZ   0  XCD                 [J. IF INOP OR MOP OFF   
   8 7P?4 ...      LDCT  0  #040 
   9 7PN6 ...      ANDX  0  EWAIT(3) 
  15 7Q58 ...      BZE   0  XOK                 [J IF ENGAGED
   9 7QG= ...      LONGSET  #36,XFAIL
  16 7QX# ...      BRN      YMOC                [RE-ENTER DEMAND.
  17 7R#B ...XOK   CALL  5  RIDEM               [FREE DEMAND BLOCKS  
  21 7RPD ...XOKX  CALL  4  SETUPWORK           [SET UP IWORK WORDS FOR SETUP FILE   
   6 7S6G ...      REWIND
  15 7SHJ ...      UP                           [UP TO RMTLF 
   7 7SS= ...TWINF LDX   1  0
   9 7T4Y ...      INFORMX   1,5,1   
   8 7T*N ...      BRN      TERMA
   4 7TQQ ...[   
  11 7W7S ...[ TERMINATE FROM PLEASE LINE UP 
  17 7WJW ...TERMA REWIND                       [REWIND SETUP FILE   
  18 7W_Y ...      LDX   5  IWORK5(2)           [KEEP SETUP FILE'S GQUAL 
  21 7XC2 ...      BPZ   5  XTRANSF2            [J. IF WASN'T TRANSFORMING SETUP FILE
  10 7XS4 ...      MFREEW   ADATA,ACONV  
   5 7Y96 ...XTRANSF2
   9 7YL8 ...      LDN   0  IWORK6(2)
   9 7_3= ...      LDN   1  IWORK1(2)
  19 7_D# ...      MOVE  0  5                   [MOVE MAIN FILE'S IWORK WDS  
  10 7_TB ...      STO   5  IWORK10(2)   
  19 82=D ...      FILEMOVE 1,0                 [MOVE MAINFILE TO TOP LEVEL  
   8 82MG ...      LDX   0  GSIGN
  21 834J ...      ERS   0  IWORK20(2)          [UNSET 'LISTING SETUP FILE' MARKER   
   8 83FL ...      LDCT  0  #40  
  10 83WN ...      ORS   0  IWORK20(2)   
  10 84?Q ...      MHUNTW   3,A7020,ALIST
   7 84G? ...      LDN   6  1
   7 84NS ...      LDN   0  4
   9 855W ...      ANDX  0  EWAIT(3) 
   8 85GY ...      BZE   0  NSPLN
  11 85Y2 ...      MHUNTW   3,ADATA,IPTEMP   
  10 86*4 ...      LDX   1  ALOGLEN(3)   
   7 86Q6 ...      LDX   7  1
   7 8778 ...      SBN   7  1
   8 87J= ...      LDX   5  A1(3)
   7 87NK ...      ADN   5  1
   8 87SY ...      STO   5  A1(3)
   7 87_# ...      SBN   1  2
   8 88BB ...      ADN   3  A1+1 
   7 88RD ...      NGN   0  1
   8 88__ ...      STOC  0  0(3) 
   7 898G ...      LDX   4  3
   8 89KJ ...      MOVE  3  0(1) 
   8 8=2L ...      LDX   1  FX2  
   7 8=7S ...      LDX   6  5
   8 8=G8 ...      ANDN  6  #37  
  10 8=MB ...      STO   6  IWORK11(1)   
   8 8=SJ ...      NGN   6  100  
   7 8=_W ...      DVS   4  7
   9 8?FG ...      LDX   0  IWORK1(1)
   7 8?_6 ...      SMO      4
   8 8#DQ ...      STO   0  0(3) 
   5 8*?6 ...NSPLN   
  15 8B5=          FREELINK                     [FREE LINKS  
  18 8BJW ...      ACROSS   PCTLISTA,4             [AND OVER TO PCTLISTA 
   4 8C4G    #   
   4 8CJ6    #   
   4 8D3Q    #END
   8 ____ ...53150712001300000000
