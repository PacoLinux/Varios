  15 22FL    #SEG  ENGAGE                       [LEN WAGSTAFF
  16 22_=    # (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982.   
   7 23DW    #OPT  K0ENGAGE=0
   9 23YG    #OPT  K6ENGAGE=K6PERI   
  11 24D6    #LIS  K0ENGAGE>K0PERI>K0ALLGEO  
   9 24XQ                   8HENGAGE 
   4 25CB    #   
  21 25X2    #     THIS SEGMENT IS PART OF THE PERIPHERAL CONTROLLER AND HAS ENTRY   
  10 26BL    #     POINTS AS FOLLOWS:-   
   4 26W=    #   
  20 27*W    #     K1  SERVICES ENGAGE BUTTON INTERRUPTS NOTIFIED BY SPRING CLEAN
   9 27TG    #     K10 ENGPER MACRO  
  10 28*6    #     K30 DISENGAGE COMMAND 
   4 28SQ    #   
  10 29#B    #              ENTRY POINTS 
   4 29S2    #   
  11 2=?L          SEGENTRY K1ENGAGE,X1ENGAGE
  12 2=R=          SEGENTRY K10ENGAGE,X10ENGAGE  
  12 2?=W          SEGENTRY K11ENGAGE,X11ENGAGE  
  12 2?QG          SEGENTRY K30ENGAGE,X30ENGAGE  
   6 2#=6    #UNS CFMDME 
  12 2#PQ          SEGENTRY K31ENGAGE,X31ENGAGE  
   4 2*9B    #   
   4 2*P2    #   
   5 2B8L    TYPESIZE
  13 2BN=    #HAL           CPAT,CPATLEN        [TR  
  16 2C7W    #HAL  CENTRAL+ACTLF,CENTRALLEN+ACTLFLEN        [TP  
  16 2CMG    #HAL  CENTRAL+ACTLF,CENTRALLEN+ACTLFLEN        [LP  
  13 2D76    #HAL           CPAT,CPATLEN        [CR  
  16 2DLQ    #HAL  CENTRAL+ACTLF,CENTRALLEN+ACTLFLEN        [CP  
  13 2F6B    #HAL           ACTDOC,ACTDOCLEN    [MT  
  13 2FL2    #HAL           ACTDOC,ACTDOCLEN    [EDS 
   5 2G5L    PLINKS  
  13 2GK=          PAIR     COMPROA,1           [TR  
  13 2H4W          PAIR     LISTINTC,5          [TP  
  10 2HJG          PAIR     LISTINIT,5   
  13 2J46          PAIR     COMPROA,1           [CR  
  10 2JHQ          PAIR     LISTINTC,5   
  13 2K3B          PAIR     LIBLOAD,1           [MT  
  10 2KH2          PAIR     CARTCONT,1   
   6 2L2L    #UNS EPLOM  
   9 2LG=          PAIR     LIBREQ,4 
   6 2L_W    #UNS CFMDME 
   4 2MFG    (   
   5 2M_6    PLINKS8 
   9 2NDQ          PAIR     ENGAGE,31
   4 2NYB    )   
   5 2PD2    PLINKS9 
   9 2PXL          PAIR     ENGAGE,11
   9 2QC=    XJRRWI         +JRRWI   
   4 2QWW    [   
   6 2RBG    #UNS CFMDME 
   4 2RW6    (   
  21 2S*Q    XOUTPACK                           [ SUBROUTINE TO 'OUTPACKX' 1 PARAM.  
   8 2STB          SBX   7  FX1  
   8 2T*2          OUTPACKX 4,5,6
   8 2TSL          ADX   7  FX1  
   7 2W#=          EXIT  7  0
   4 2WRW    )   
   4 2X?G    [   
   8 2XR6    SIX            +6   
   8 2Y=Q    NINE           +9   
   8 2YQB    THIRTEEN       +13  
   8 2_=2    XIPB           +24  
   8 2_PL    XDRUM          +25  
   8 329=    XEMDS          +26  
   8 32NW    XIPC           +28  
   9 338G    XJPROP         +JUNPROP 
   6 33N6    #UNS CFMDME 
   4 347Q    (   
  21 34MB    X34            +34                    [PSEUDO DEVICE FOR CONTROLLER FAIL
   4 3572    )   
   8 35LL    TP             +1   
   8 366=    XLP            +2   
   8 36KW    XCR            +3   
   8 375G    XCP            +4   
   8 37K6    MT             +5   
   4 384Q    #   
   6 38JB    #UNS CFMDME 
   4 3942    (   
  20 39HL    PCODES                             [ TABLE OF DEVICE CODES FOR DME  
  13 3=3=                   #1                  [ GPC
  13 3=GW                   #5                  [ DFC
  14 3?2G                   #42                 [ (=X^B^)
  14 3?G6                   #43                 [ (=X^C^)
  15 3?_Q                   #46                 [ (=X^F^) CSC
  16 3#FB                   #100                [ (=X^10^) CSC   
  16 3#_2                   +0                  [ END OF TABLE   
  20 3*DL    XTYPES                             [ TABLE  OF CONTROLLER NEMONICS  
  17 3*Y=                   4H???               [ DEVICE CODE UNKNOWN
  16 3BCW                   4HGPC               [ = X^01XX^   !  
  16 3BXG                   4HDFC               [ = X^05XX^   !  
   9 3CC6                   4HDCU1   
   9 3CWQ                   4HDCU2   
  20 3DBB                   4HCSC               [ = X^0FXX^   !-- REPLY FROM '153
  16 3DW2                   4HCSC               [ = X^10XX^   !  
   9 3F*L    XFAILED        8H FAILED
  12 3FT=    XRELOAD        20H: PLEASE RELOAD IT
  12 3G#W    XRECVRD        16H BEEN RECOVERED   
   4 3GSG    )   
  16 3H#6    #SKI  JSKI38                       [GLASGOW SPECIALS
   4 3HRQ    (   
   9 3J?B    PARS           4HCODE   
   9 3JR2    ZCODE          4H1900   
   9 3K=L                   4HEBCD   
   9 3KQ=                   4HKDF9   
   8 3L9W                   +0   
   9 3LPG    ZPT            #40000000
   8 3M96                   +3   
   8 3MNQ                   +0   
   4 3N8B    )   
   4 3NN2    #   
  20 3P7L    #     THIS ROUTINE SERVICES ENGAGE BUTTON INTERRUPTS FOR ALL DEVICES
  21 3PM=    #     OTHER THAN OPERATORS CONSOLE. ENTERED FROM SEGMENT CHARGESC WITH  
  21 3Q6W    #     THE DEVICE LIST POINTER OF THE ENGAGED DEVICE IN X3. ACTION TAKEN 
  21 3QLG    #     VARIES ACCORDING TO DEVICE TYPE, BUT IN GENERAL AN ACTIVITY WILL  
  21 3R66    #     BE CREATED AND FPUT IF DEVICE WAS UNALLOCATED, ELSE LONGON WILL BE
  21 3RKQ    #     USED TO WAKE ACTIVITIES WAITING FOR THEIR PERIPHERAL TO BE ENGAGED
  13 3S5B    #     THE CURRENT ACTIVITY IS BLANKET.  
   4 3SK2    #   
   5 3T4L    X1ENGAGE
  21 3TJ=          JWRONG   3,XIT               [IGNORE INTERRUPT IF DEVICE WRONGED  
  16 3W3W          LTYPE    3,4                 [GET DEVICE TYPE 
   9 3WHG          JANON    3,XANON  
   9 3X36          TXU   4  XLP(1)   
   9 3XGQ          BCS      NOGROP   
   9 3Y2B          JRGROP   3,XANON  
   5 3YG2    NOGROP  
  17 3Y_L          JONL     3,ONLINE            [BR IF DEVICE ONLINE 
   5 3_F=    NOTDRM  
  21 3_YW                               [TREAT AS UNALLOCATED IF IT IS A FILE DEVICE 
   9 42DG          TXU   4  SIX(1)   
   8 42Y6          BCC      TOK  
  15 43CQ          TXU   4  NINE(1)             [IS IT DRUM  
  13 43XB          BCC      TOK                 [EDS 
  14 44C2          TXU   4  THIRTEEN(1)         [ OR FDS?
  17 44WL          BCC      TOK                 [IF SO JUMP,OTHERWISE
  18 45B=          TXU   4  XDRUM(1)            [IS IT HIGHSPEED DRUM?   
   8 45TW          BCC      TOK  
   9 46*G          TXU   4  XEMDS(1) 
  15 46T6          BCC      TOK                 [J IF EDS(30)
   9 47#Q          TXU   4  XIPC(1)  
  17 47SB          BCC      TIPC                [BR IF TYPE 28 IPB   
  17 48#2                               [SPECIAL TREATMENT FOR IPB   
   9 48RL          TXU   4  XIPB(1)  
  15 49?=          BCS      NIPB                [J NOT IPB   
  18 49QW          LAMOP     3,2                [ADDRESS OF AMOP/IPBIN   
  10 4==G          LDX   0  CHAINADD(2)  
  20 4=Q6          BPZ   0  XGEOG               [J TO AWAKE IT IF ALREADY ACTIVE 
  19 4?9Q          JSBUSY   CPPTR(2),XGEOG         [J IF READ UP (IF AMOP... 
  20 4?PB    [                                   ...RECOVERED FROM INOPERABILITY)
  16 4#92          FPUT                         [ELSE ACTIVATE IT
   8 4#NL          BRN      XIT  
  21 4*8=    NIPB                       [SPECIAL TREATMENT FOR 96 COLUMN CR(CS5556)  
  21 4*MW                                       [IF 96 COLUMN CR IS NOT ONLINED JUST 
  16 4B7G                                       [DISENGAGE IT.   
  14 4BM6          BXU   4  XCR(1),TLP          [NOT CR  
  21 4C6Q                                       [B15 OF CR PROPERTY CODE INDICATES A 
  20 4CLB                                       [96 COLUMN CR (THAT ONLY ACCEPTS 
  18 4D62                                       [CARD IMAGE MODE PERI'S).
   8 4DKL          LPROP    3,0  
  15 4F5=          ANDN  0  4                   [B15 NOW B21 
  16 4FJW          BZE   0  TLP                 [NOT 96 COL CR   
  19 4G4G          JONL     3,TLP               [ONLINED : PROCEED AS USUAL  
   8 4GJ6          DISENGPER 3   
   8 4H3Q          BRN      XIT  
  21 4HHB    TLP                        [TREAT AS ALLOCATED IF NOT MT OR BASIC DEVICE
  10 4J32          BXGE  4  SIX(1),XGEOG 
   9 4JGL          TXU   4  XLP(1)   
  14 4K2=          BCS      NLP                 [J NOT LP
  21 4KFW          LPUNSET  3                   [ENSURE PAPER ALIGNED MARKER IS UNSET
  20 4K_G    NLP                        [WAKEN ACTIVITY UNLESS DEVICE UNALLOCATED
   9 4LF6          JFREE    3,UNALL  
   5 4LYQ    XGEOG   
   8 4MDB          LGEOG    3,5  
   8 4MY2          TXU   4  MT(1)
  14 4NCL          BCS      WAKE                [J NOT MT
   6 4NX=    #UNS EPLOM  
  21 4PBW          DFJUMPS  3,LOAD,XPLOM        [J IF TAPE MARKED OFFICIALLY LOADED  
   8 4PWG          LONGON   #65,5
   8 4QB6          BRN      XIT  
   6 4QTQ    #UNS EPLOM  
   4 4R*B    (   
   5 4RT2    XPLOM   
  21 4S#L          GETACTOP TOCHARG,ACTDOC,,1   [SET UP LONGLOCK ACTDOC IF POSSIBLE  
   9 4SS=          LDX   2  BPTR(2)  
  19 4T?W          STO   3  CPPTR(2)            [STORE DEVICE LIST POINTER   
   7 4TRG          LDN   4  7
   7 4W?6          SMO      4
  17 4WQQ          LDN   1  PLINKS(1)           [SET LINK TO LIBREQ,4
   9 4X=B          LINKSET  2,0(1)   
   6 4XQ2          FPUT  
   8 4Y9L          BRN      XIT  
   4 4YP=    )   
   4 4_8W    WAKE
   6 4_NG    #UNS CFMDME 
   4 5286    (   
  19 52MQ          JENVNOT  X1900A,DME          [ JIF NOT IN DME ENVIRONMENT 
  21 537B          TXU   4  X34(1)              [ SEE IF PSEUDO DEVICE (CTLR FAILURE)
  15 53M2          BCC      XCONTFAIL           [ JIF IT IS  
   5 546L    X1900A  
   4 54L=    )   
   6 555W    #SKI  IPL   
   4 55KG    (   
   9 5656          SEGENTRY K98ENGAGE
   6 56JQ          NULL  
   4 574B    )   
  20 57J2          LONGON1  #77,5               [WAKE UP ACTIVITY AWAITING ENGAGE
   4 583L    XIT 
   8 58H=          SMO      FX2  
   8 592W          LDX   0  ATYPE
   8 59GG          SRL   0  12   
   8 5=26          SBN   0  BAT  
  16 5=FQ          BZE   0  XITA                [J IF IN BLANKET 
   7 5=_B          SUICIDE   
   4 5?F2    XITA
  10 5?YL          ACROSS   CHARGESC,1   
  20 5#D=                               [CREATE ACTIVITY FOR UNALLOCATED DEVICE  
  20 5#XW    TOK   LDN   4  6                   [SET TYPE=6 FOR ALL FILE DEVICES 
  18 5*CG    UNALL SETALLOC 3                   [MARK DEVICE ALLOCATED   
  16 5*X6          JMT      3,XMT               [BR IF MAG TAPE  
  19 5BBQ          SMO      4                   [USE TYPE AS TABLE MODIFIER  
  20 5BWB          LDX   7  TYPESIZE(1)         [GET ACTIVITY BLK TYPE AND LENGTH
  15 5CB2          LDEX  6  7                   [GET LENGTH  
  21 5CTL          ERS   6  7                   [REMOVE LENGTH TO LEAVE TYPE IN B0-11
  14 5D*=          GETACTXOP NGIVEN,7,6         [OPTIONAL
  14 5DSW          BRN      MACT                [J GIVEN 
   5 5F#G    NGIVEN  
  20 5FS6          CLALLOC  3                   [UNSET DEVICE ALLOCATED MARKER   
   5 5G?Q    TOCHARG 
  10 5GRB          ACROSS   CHARGESC,2   
   4 5H?2    MACT
   8 5HQL    #SKI  K6ENGAGE>99-99
  10 5J==          TRACE    3,ALLOTPER   
  18 5JPW          LDX   2  BPTR(2)             [GET ADDR OF NEW ACTIVITY
  19 5K9G          STO   3  CPPTR(2)            [STORE DEVICE LIST POINTER   
   8 5KP6          TXL   4  MT(1)
  18 5L8Q          BCC      NBASIC              [J IF MT OR FILE DEVICE  
  15 5LNB          DCH   4  JSOURCE1(2)         [B0-5=TYPE   
  15 5M82          LGEOG    3,0                 [B6-14=ZERO  
  16 5MML          DEX   0  JSOURCE1(2)         [B15-23=GEOG.NO. 
   5 5N7=    NBASIC  
   7 5NLW    #SKI  CLONK<1$1 
   7 5P6G          SLL   4  1
   7 5PL6          SMO      4
   9 5Q5Q          LDN   1  PLINKS(1)
   9 5QKB          LINKSET  2,0(1)   
   6 5R52          FPUT  
   8 5RJL          BRN      XIT  
  17 5S4=    TIPC                               [TYPE 28 IPB (IPC)   
  19 5SHW          FPUTCHA  3                   [WAKE UP ACTIVITY IF REQUIRED
   8 5T3G          BRN      XIT  
   5 5TH6    ONLINE  
   8 5W2Q          TXU   4  MT(1)
  18 5WGB          BCC      NOTDRM              [IF MT NO DRM TEST WANTED
  17 5X22          JDRM     3,XINDRM            [BR IF ONLINE IN DRM 
   9 5XFL          BRN      NOTDRM   
   5 5X_=    XINDRM  
  15 5YDW          LJOBNO   3,6                 [LOAD JOB NO 
  10 5YYG          TRACE    3,DRM ENG!   
  16 5_D6          TICKLE   6                   [TICKLE PROGRAM  
   8 5_XQ          BRN      XGEOG
   4 62CB    XMT 
  15 62X2    #              LONGLOCK AREA GETACT FOR MAG TAPE
  11 63BL          GETACTOP NGIVEN,ACTDOC,,1 
  16 63W=          BRN      MACT                [REJOIN MAIN PATH
   4 64*W    #   
   5 64TG    XANON   
  11 65*6          GETACTOP TOCHARG,ACTCDT,,1
   9 65SQ          LDX   2  BPTR(2)  
   9 66#B          STO   3  CPPTR(2) 
  10 66S2          LDN   1  PLINKS9(1)   
   9 67?L          LINKSET  2,0(1)   
   6 67R=          FPUT  
   8 68=W          BRN      XITA 
   4 68QG    #   
   6 69=6    #UNS CFMDME 
  20 69PQ    (                                  [ ENTRY FOR PSEUDO DEVICE TYPE 34
  21 6=9B    XCONTFAIL                          [ (CTLR FAILURE IN DME ENVIRONMENT   
  16 6=P2    #     CREATE ACTIVITY TO OUTPUT CTLR FAIL MESSAGE   
  11 6?8L          GETACTOP TOCHARG,ACTCDT   
   9 6?N=          LDX   2  BPTR(2)  
   9 6#7W          STO   4  AWORK1(2)
  10 6#MG          LDN   1  PLINKS8(1)   
   9 6*76          LINKSET  2,0(1)   
   6 6*LQ          FPUT  
   8 6B6B          BRN      XIT  
   4 6BL2    #   
   4 6C5L    #   
  15 6CK=    #     ENTRY FOR OUTPUTTING CTLR FAILED MESSAGE  
   4 6D4W    #   
   6 6DJG    X31ENGAGE   
   9 6F46          LDX   4  AWORK1(2)
  10 6FHQ          FINDPEREC 2,APFOTY,4  
  21 6G3B          LDN   3  0                   [ INDICATE ENQUIRY ABOUT CTLR FAIL   
  20 6GH2          '153  3  2                   [ X-CODE TO GET INFO ABOUT FAIL  
   8 6H2L          LDX   2  FX2  
  15 6HG=    [              NOW REMEMBER REPLY FROM EXTRACODE
  21 6H_W          STO   3  AWORK3(2)           [ BIT 9 INDICATES WHETHER RECOVERED  
  20 6JFG                                       [ BITS 12-23  PORT/TRUNK OF CTLR.
  20 6J_6          STO   4  AWORK4(2)           [ DEVICE CODE FOR CONTROLLER TYPE
  18 6KDQ                                       [ (SEE TABLE AT 'PCODES' 
  19 6KYB          SRL   4  12                  [ GET CODE INTO L.S. 12 BITS 
  16 6LD2          LDN   2  1                   [ TABLE MODIFIER 
   6 6LXL    SNEXTCODE   
   7 6MC=          SMO      2
  18 6MWW          LDX   0  PCODES-1(1)         [ GET NEXT TABLE ENTRY   
  17 6NBG          BZE   0  SNOTYPE             [ JIF END OF TABLE   
  19 6NW6          TXU   0  4                   [ SEE IF THIS IS FAILED TYPE 
  18 6P*Q          BCC      STHISTYPE           [ JIF IT IT IS ELSE....  
  20 6PTB          BUX   2  SNEXTCODE           [ ...LOOP FOR NEXT TABLE ENTRY   
   5 6Q*2    SNOTYPE 
  21 6QSL          LDN   2  0                    [ MODIFIER FOR ^???^ IF NOT FOUND   
   6 6R#=    STHISTYPE   
   7 6RRW          SMO      2
  16 6S?G          LDN   4  XTYPES(1)            [ => CTLR. TYPE 
  16 6SR6          LDN   5  1                    [ NO. OF WORDS  
  14 6T=Q          LDN   6  JPDVARCHAR           [ P.D.C.
  15 6TQB          CALL  7  XOUTPACK             [ SET UP %A 
   4 6W=2    [   
   9 6WPL          JENV     XCME1,CME
  13 6X9=          SLL   3  12                   [   
  20 6XNW          ADN   3  #2020                [ SET UP PORT/TRUNK IN CHARS 1,2
  16 6Y8G          SRC   3  6                    [ SPACES IN 0,3 
  16 6YN6          LDN   4  3                    [ => PORT/TRUNK 
  15 6_7Q          CALL  7  XOUTPACK             [ SET UP %B 
  19 6_MB          BRN      XRECD               [ J TO TEST FAIL RECOVERED   
   5 7272    XCME1   
  15 72LL          SRL   3  6                   [ SCU IN 3   
   8 736=          LDX   4  ACES 
   8 73KW          SLL   34 18   
  19 745G          SRC   3  6                   [ SCU IN 1,SPACES IN 0,2,3   
  14 74K6          LDN   4  3                   [ => SCU 
  15 754Q          CALL  7  XOUTPACK            [ SET UP %B  
  15 75JB          LDX   3  AWORK3(2)           [ PORT IN 3  
   8 7642          LDX   4  ACES 
   8 76HL          SLL   34 18   
  19 773=          SRC   3  6                   [ PORT IN 1,SPACES IN 0,2,3  
  14 77GW          LDN   4  3                   [ => PORT
  15 782G          CALL  7  XOUTPACK            [ SET UP %C  
   4 78G6    [   
   5 78_Q    XRECD   
  20 79FB          LDX   4  AWORK3(2)            [ NOW SEE IF FAIL WAS RECOVERED 
   8 79_2          ANDX  4  BIT9 
  16 7=DL          BNZ   4  XNOTRECD             [ JIF IT WAS NOT
  18 7=Y=          LDN   4  XRECVRD(1)           [ => ^BEEN RECOVERED^   
  16 7?CW          LDN   5  4                    [ NO. OF WORDS  
  21 7?XG          CALL  7  XOUTPACK             [ SET UP %C OR %D FOR RECOVERED CASE
  17 7#C6          BRN      XTEND                [ J. TO DO MONOUT   
   4 7#WQ    [   
   5 7*BB    XNOTRECD
  15 7*W2          LDN   4  XFAILED(1)          [ => ^FAILED^
  16 7B*L          LDN   5  2                   [ NO. OF WORDS   
  20 7BT=          CALL  7  XOUTPACK            [ SET UP %C OR %D FOR FAILED CASE
   4 7C#W    [   
  18 7CSG          LDX   0  AWORK4(2)           [ NOW SEE IF IT WAS A CSC
   8 7D#6          SRL   0  12   
   7 7DRQ          SBN   0  6
  18 7F?B          BNG   0  XTEND               [ JIF NOT A CSC ELSE.....
  20 7FR2          LDN   4  XRELOAD(1)          [ ... GET => ^: PLEASE RELOAD IT^
  16 7G=L          LDN   5  5                   [ NO. OF WORDS   
  19 7GQ=          CALL  7  XOUTPACK            [ SET UP %D OR %E IF REQUIRED
   4 7H9W    [   
   5 7HPG    XTEND   
   9 7J96          JENV     XCME2,CME
  20 7JNQ          MONOUT   CONTFAILDME         [ SEND CONTROLLER FAILED MESSAGE 
   8 7K8B          BRN      XSUI 
   5 7KN2    XCME2   
  20 7L7L          MONOUT   CONTFAILCME         [ SEND CONTROLLER FAILED MESSAGE 
   4 7LM=    [   
   4 7M6W    XSUI
   7 7MLG          SUICIDE   
   4 7N66    [   
   4 7NKQ    )   
  15 7P5B    [                 ENTRY TO GROPE FOR ANON DEVICE
   6 7PK2    X11ENGAGE   
   9 7Q4L          LDX   3  CPPTR(2) 
   8 7QJ=          LTYPE    3,4  
   9 7R3W          TXU   4  XLP(1)   
   8 7RHG          BCS      XGET 
   9 7S36          JNRGROP  3,XGET   
   8 7SGQ          LPROP    3,0  
   9 7T2B          STO   0  AWORK2(2)
   8 7TG2          LDN   7  160  
   7 7T_L          SRC   0  5
   8 7WF=          BNG   0  YWID 
   8 7WYW          LDN   7  132  
   7 7XDG          SLC   0  3
   8 7XY6          BNG   0  YWID 
   8 7YCQ          LDN   7  120  
   7 7YXB          SRC   0  2
   8 7_C2          BNG   0  YWID 
   8 7_WL          LDN   7  96   
   4 82B=    YWID
   8 82TW          LDN   0  65   
   8 83*G          SLL   0  12   
   7 83T6          ADX   7  0
   8 84#Q          LADDP    3,1  
  10 84SB          LDX   0  AREPERT(1)   
   8 85#2          LDX   1  FX1  
   7 85RL          TXU   0  7
   8 86?=          BCS      YSTOW
   7 86QW          LDN   0  0
   5 87=G    YSTOW   
   9 87Q6          STO   0  AWORK1(2)
   4 889Q    XGET
   8 88PB          LINT     3,0  
   9 8992          GROPEREC 3,APINT,0
  17 89NL          BNG   3  WDIS                [J IF GROPED FAILED  
   8 8=8=          LTYPE    3,4  
   9 8=MW          TXU   4  XLP(1)   
   8 8?7G          BCS      WEG  
   8 8?M6          JNRGROP  3,WEG
   7 8#6Q          CLRGROP  3
   9 8#LB          LDX   0  AWORK1(2)
   8 8*62          BZE   0  WPROP
   8 8*KL          LADDP    3,1  
   9 8B5=          LDEX  6  AWORK1(2)
  10 8BJW          LDEX  5  AREPERT(1)   
   7 8C4G          TXL   5  6
   8 8CJ6          BCS      REPCH
  10 8D3Q          STO   0  AREPERT(1)   
   8 8DHB          LDX   1  FX1  
   8 8F32          BRN      WPROP
   5 8FGL    REPCH   
   7 8G2=          DEX   5  0
  10 8GFW          STO   0  AREPERT(1)   
   9 8G_G          STO   0  AWORK1(2)
   8 8HF6          LGEOG    3,4  
   8 8HYQ          OUTNUM   4,0  
   9 8JDB          LDX   4  AWORK1(2)
   9 8JY2          OUTPACK  4,1,NUMA 
   9 8KCL          LDX   6  XJRRWI(1)
   7 8KX=          MONOUTX  6
   5 8LBW    WPROP   
   9 8LWG          LDX   0  AWORK2(2)
   8 8MB6          BZE   0  WEG  
   8 8MTQ          LPROP    3,0  
   9 8N*B          TXU   0  AWORK2(2)
   8 8NT2          BCC      WEG  
   8 8P#L          LGEOG    3,4  
   8 8PS=          OUTNUM   4,0  
   7 8Q?W          LDN   5  0
   9 8QRG          LDX   6  XJPROP(1)
   8 8R?6          INFORMX  5,6,1
   4 8RQQ    WEG 
   9 8S=B          JDISENG  3,WSUI   
   9 8SQ2          BRN      NOGROP   
   4 8T9L    WDIS
   9 8TP=          JDISENG   ,WSUI   
   7 8W8W          DISENGPER 
   4 8WNG    WSUI
   7 8X86          SUICIDE   
   4 8XMQ    #   
   4 8Y7B    [   
   6 8YM2    #     ENGPER
   4 8_6L    #   
  21 8_L=    #     THIS ROUTINE GETS THE SPECIFIED PERIPHERAL ENGAGED. IF DISENGAGED 
  20 925W    #     WHEN FIRST ENTERED OUTPUTS SPECIFIED MESSAGE (%D) ON RELEVANT 
  21 92KG    #     CONSOLE. REPEATS MESSAGE EVERY 'N' MINS UNTIL OPERATOR ENGAGES.   
   4 9356    #   
  16 93JQ    XMSGE          +JMARY              [PLEASE ENGAGE   
   8 944B    #HAL           1,0  
  20 94J2                   +JRESET             [PLEASE ENGAGE FOR ERROR RESTART 
   8 953L    #HAL           1,0  
  17 95H=                   +JKEN               [PLEASE LINE UP...   
   8 962W    #HAL           1,0  
   8 96GG                   +JKEN
   8 9726    #HAL           1,1  
  16 97FQ    #SKI  JSKI38                       [GLASGOW SPECIALS
   4 97_B    (   
  19 98F2                   +JPLSWIT            [PLSE SWITCH...TO...CODE...  
   8 98YL    #HAL           1,0  
   4 99D=    )   
   7 99XW    #SKI JSKI38<1$1 
   8 9=CG                   0,0  
  21 9=X6                   +JCORRUPT           [PLEASE ENGAGE UNIT N IF COMPLETION  
  20 9?BQ    #HAL           1,0                 [OF CORRUPT LISTING IS REQUIRED  
   6 9?WB    X10ENGAGE   
  18 9#B2          LDX   3  ACOMMUNE7(2)        [GET DEVICE LIST POINTER 
  17 9#TL          LDX   4  ACOMMUNE8(2)        [GET MESSAGE MODIFIER
   8 9**=    #SKI  K6ENGAGE>99-99
   9 9*SW          TRACE    3,ENGPER 
  17 9B#G          JENG     3,PENG              [J IF ALREADY ENGAGED
  15 9BS6          LGEOG    3,5                 [LOAD GEOG NO
  10 9C?Q          LONGSET  #77,PLWAIT,5 
  17 9CRB          GEOPACK  5,5,5               [PACK PDC (GEOPER)   
  18 9D?2          OUTPACK  5,1,GEOPER          [OUTPUT MESSAGE PARAMETER
  16 9DQL    #SKI  JSKI38                       [GLASGOW SPECIALS
   4 9F==    (   
  17 9FPW          LDN   0  4                   [IF %D=4, CODE TYPE..
  17 9G9G          TXU   4  0                   [..MUST BE OUTPARAMED
   8 9GP6          BCS      NOT4 
  10 9H8Q          MHUNT    2,CPB,CUNI   
   9 9HNB          LDEX  7  ANUM(2)  
  11 9J82          OUTPARAM 7,APARA,CPB,CUNI 
   4 9JML    )   
   7 9K7=    NOT4  ADX   1  4
  20 9KLW          ADX   1  4                   [MAKE POINTER TO RELEVANT MESSAGE
  18 9L6G          LDX   7  XMSGE(1)            [GET MESAGE IDENTIFIER   
  15 9LL6          LDX   6  XMSGE+1(1)          [TIME,DELAY  
  18 9M5Q          LDN   0  1                   [ROUTE TO CENTRAL CONSOLE
  11 9MKB          DEMANDY  0,7,NF6,NF5,NF7,6
  16 9N52          JENG     3,PENGD             [J IF NOW ENGAGED
  10 9NJL          LONGSET  #77,PLWAIT,5 
  16 9P4=          BRN      NF7                 [RE-ENTER DEMANDY
   5 9PHW    PENGD   
   7 9Q3G          DEMANDQUIT
  15 9QH6    PENG  UPPLUS   2                   [ENGAGED EXIT
  15 9R2Q    NF5   UP                           [BREAKIN EXIT
  16 9RGB    NF6   LTYPE    3,5                 [GET DEVICE TYPE 
   8 9S22          TXU   5  TP(1)
  14 9SFL          BCC      TBWR                [J IF TP 
   9 9S_=          TXU   5  XLP(1)   
  14 9TDW          BCC      TBWR                [J IF LP 
   9 9TYG          TXU   5  XCP(1)   
  14 9WD6          BCS      XCD                 [J NOT CP
  21 9WXQ    TBWR  SETWTBWR 3                   [MARK TO BE WRONGED WHEN NEXT FREED  
  15 9XCB    XCD   UPPLUS   1                   [CANTDO EXIT 
  20 9XX2    PLWAIT      GEOERR 1,LONGWAIT      [LONGWAIT FIELD ALREADY NONZERO  
   4 9YBL    #   
   4 9YW=    #   
   9 9_*W    #     DISENGAGE COMMAND 
   4 9_TG    #   
  15 =2*6    #     TERMINATES A CR OR TR AT DIRECTIVE LEVEL  
  16 =2SQ    #SKI  JSKI38                       [GLASGOW SPECIALS
   4 =3#B    (   
   4 =3S2    #   
  20 =4?L    ZPAREM         +JPAREM             [THIS PARAM NOT ALLOWED IN REMOTE
  17 =4R=    ZCOMIS         +JCOMIS             [CODE TYPE MISSING   
  20 =5=W    ZCODNO         +JCODNO             [CODE NOT ALLOWED ON THIS DEVICE 
   4 =5QG    #   
   8 =6=6    XER1  LDX   1  FX1  
   9 =6PQ          LDX   6  ZPAREM(1)
   8 =79B          BRN      ZER  
   8 =7P2    XER2  LDX   1  FX1  
   9 =88L          LDX   6  ZCOMIS(1)
   8 =8N=          BRN      ZER  
   8 =97W    XER3  LDX   1  FX1  
   9 =9MG          LDX   6  ZCODNO(1)
   8 ==76          BRN      ZER  
   4 ==LQ    #   
   7 =?6B    ZER   COMERRX  6
   4 =?L2    #   
  18 =#5L    PARA  PARABEG  1,4,PARS(1),,0      [LOOK FOR 'CODE' OR 'CO' 
  10 =#K=          MHUNT    3,CPB,CUNI   
   9 =*4W          LDX   0  ANUM(3)  
   8 =*JG          BNG   0  P1   
  16 =B46          BZE   0  XER2                [NO TYPE GIVEN   
  16 =BHQ          EXIT  7  1                   [CODE PARA FOUND 
   9 =C3B    P1    MFREE    CPB,CUNI 
   7 =CH2          SBN   4  2
   8 =D2L          BNZ   4  PARA 
  18 =DG=          EXIT  7  0                   [CODE PARA NOT PRESENT   
   4 =D_W    #   
   5 =FFG    WHATCODE
   9 =F_6          LDX   0  ZCODE(1) 
  17 =GDQ          BZE   0  W1                  [J IF END OF TABLE   
   9 =GYB          SBX   0  APARA(3) 
   8 =HD2          BZE   0  W2   
   9 =HXL          BUX   1  WHATCODE 
  18 =JC=    W1    BRN      XER3                [J IF TYPE NOT RECOGNISED
  18 =JWW    W2    EXIT  7  0                   [CODE TYPE OK- X1 IS PTR 
   4 =KBG    #   
   4 =KW6    )   
   4 =L*Q    #   
   6 =LTB    X30ENGAGE   
   4 =M*2    #   
  16 =MSL    #SKI  JSKI38                       [GLASGOW SPECIALS
   4 =N#=    (   
  16 =NRW          LDN   4  4                   [FOR SUBR PARA   
   8 =P?G          CALL  7  PARA 
  17 =PR6          BRN      NOCOD               [J IF NO 'CODE' PARAM
  10 =Q=Q          LDEX  0  CONTEXT(2)   
   8 =QQB          ANDN  0  #200 
  17 =R=2          BNZ   0  XER1                [J IF REMOTE CONTEXT 
   9 =RPL          CALL  7  WHATCODE 
  18 =S9=          LDX   4  ZPT(1)              [IF CODE='1900', DONT..  
  17 =SNW          BNG   4  TAPIA               [..CHECK DEVICE TYPE 
  10 =T8G          LDCH  5  JSOURCE1(2)  
   7 =TN6          TXU   4  5
  18 =W7Q          BCS      XER3                [J IF WRONG DEVICE TYPE  
  11 =WMB    TAPIA TESTCODE ,READER,ORD,SPEC 
  18 =X72    ORD   BNG   4  NOCH                [NO CHANGE SINCE APIA..  
  20 =XLL    #                                  [..AND COMMAND BOTH SAY '1900'   
  18 =Y6=          BRN      CH                  [ELSE J TO CHANGE CODE   
  18 =YKW    SPEC  BPZ   4  NOCH                [NO CHANGE SINCE APIA..  
  20 =_5G    #                                  [..AND COMMAND BOTH SAY 'SPECIAL'
   7 =_K6    CH    DISENGPER 
  16 ?24Q          ENGPER   NOCH,,ZCD,4         ['PLSE SWITCH..' 
   9 ?2JB          SETCODE  ,READER  
   6 ?342    NOCH  ENDCOM
   4 ?3HL    )   
   4 ?43=    #   
  18 ?4GW    NOCOD RMDIS                        ['DISENGAGE'REMOTE DEVICE
  20 ?52G    ZCD   FREEPER                      [FREE THE (REMOTE/LOCAL) DEVICE  
  21 ?5G6          ACROSS   LOGOUT,7            [CLOSE FILES, FREE JOBLOCK & SUICIDE 
   4 ?5_Q    #   
   4 ?6FB    #   
  10 ?6_2          MENDAREA 20,K100ENGAGE
   6 ?7DL    #SKI  IPL   
   4 ?7Y=    (   
  10 ?8CW          MENDAREA 20,K99ENGAGE 
   4 ?8XG    )   
   4 ?9C6    #END
   6 ____ ...756546220004
