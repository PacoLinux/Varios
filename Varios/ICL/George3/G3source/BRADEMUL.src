  16 22FL ...      SEG      BRADEMUL,860,FRANK BLUNDELL,BRADEMUL 
  13 22_=          SEGENTRY K1BRADEMUL,QK1BRADEMUL   
  16 2394 ...#     (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23DW    [   
  17 23YG    [    THIS SEGMENT IS EQUIVALENT TO BRADDATA IN FUNCTION 
  17 24D6    [ EXCEPT THAT THE CHAPTERS ARE FORCED TO START ON 1KWORD
  16 24XQ    [ BOUNDARIES. THIS CHAPTER WILL ONLY BE ENTERED WHEN
  16 25CB    [ GEORGE IS RUNNING UNDER AN EMULATION ENVIRONMENT. 
   4 25X2    [   
   4 26BL    [   
   8 26W=    SCFK           #2000
   9 27*W    SCFMASK        #77777600
   9 27TG    SCFBIT         #00020000
   9 28*6    XRR   MHUNT 2,BSTB,BREAD
   7 28SQ          EXIT  1  0
   6 29#B    QK1BRADEMUL 
   4 29S2    #   
  14 2=?L    #     EVALUATE TOTAL SIZE OF SCF REQUIRED   
   4 2=R=    #   
   9 2?=W          LDX   5  SCFK(1)  
  16 2?QG          LDN   6  0                [NEXT ADDRESS ON SCF
  10 2#=6          MHUNT    1,BSTB,FSTAB 
  15 2#PQ ...      LDN   7  AENDCHAPS-KTAB      [NO. OF CHAPS
  16 2*9B ...WAGN  LDN   3  AENDCHAPS-KTAB      [NO. OF ENTRIES  
  16 2*P2          SBX   3  7                [POINT TO POSITION  
   7 2B8L          SMO      3
  15 2BN=          LDX   0  A1(1)               [CHAPTER NO  
  19 2C7W          BZE   0  WNEXTA              [IF ZERO THEN END OF CHAPS   
   7 2CMG          ADX   6  5
   8 2D76         BCT   7  WAGN  
   5 2DLQ    WNEXTA  
  15 2F6B          STO   6  ACOMMUNE4(2)     [TOTAL SIZE REQD
   4 2FL2    #   
  12 2G5L    #     EVALUATE SIZE OF THE SCF GIVEN
   4 2GK=    #   
   9 2H4W          LDX   1  BINDEX   
   7 2HJG          ADN   1  6
  13 2J46          LDCH  4  A1(1)            [TYPE   
   7 2JHQ          SBN   4  9
  13 2K3B          BZE   4  TDRM             [DRUM   
   8 2KH2          SBN   4  16   
  13 2L2L          BZE   4  TDRM             [DRUM   
   7 2LG=          LDN   5  0
  14 2L_W          LDX   4  A1+3(1)          [NO. OF CYL 
   9 2MFG    WADD  ADX   5  A1+4(1)  
   8 2M_6          BCT   4  WADD 
  13 2NDQ          SLL   5  7                [SIZE   
   8 2NYB          BRN      WSIZ 
   9 2PD2    TDRM  GEOERR   1,DRUM   
  15 2PXL    WSIZ  STO   5  ACOMMUNE5(2)     [SIZE OF FILE   
   9 2QC=          TRACE 5,SIZEAVAL  
  15 2QWW    [   CALCULATE TRANSFER START ADDRESS WITIN SCF  
  13 2RBG    [ SO THAT IT IS ON A 1K WORD BOUNDARY.  
  15 2RW6          LDX   5  A1+1(1)             [START CYL   
  19 2S*Q          MPY   5  A1+4(1)             [TIMES NO OF BLOCKS PER CYL  
  16 2STB          ADX   6  A1+2(1)             [PLUS START BLK  
  21 2T*2          ANDN  6  7                   [AND MASK OFF = STRT BLK ON 1K WORD B
  17 2TSL          SLL   6  7                   [BLK TO WORD ADDRESS 
  17 2W#=          STO   6  AWORK2(2)           [SCF START ADDRESS   
   4 2WRW    #   
  11 2X?G    #     START TRANSFERING CHAPTERS
   4 2XR6    #   
  16 2Y=Q ...      LDN   7  AENDCHAPS-KTAB      [NO. OF ENTRIES  
  16 2YQB ...XAGN  LDN   3  AENDCHAPS-KTAB      [NO. OF ENTRIES  
  19 2_=2          SBX   3  7                [POINTER TO POSITION IN TABLE   
  10 2_PL          MHUNT    1,BSTB,FSTAB 
   7 329=          SMO      3
   8 32NW          LDX   1  A1(1)
  17 338G          BZE   1  YFAL             [THATS ALL NO CHAPS LEFT
  15 33N6          STO   1  AWORK1(2)        [CHAPTER NUMBER 
  14 347Q          LDX   5  KTAB(1)          [CHAP WORD  
   8 34MB          BPZ   5  MCIN 
   7 3572          SMO   5   
  16 35LL          LDX   5  BACK2            [CHAPTER WAS IN CORE
  16 366=    MCIN  SLC   5  10                  [ SCFBIT IN B0   
   8 36KW          BPZ   5  NTYT 
   8 375G          BCT   7  XAGN 
   8 37K6          BRN      YFAL 
   7 384Q    NTYT  SLC   5  1
   8 38JB          ANDN  5  #3774
   8 3942          BNZ   5  XW1  
   8 39HL          LDN   5  1024 
  17 3=3=    XW1   STO   5  ACOMMUNE1(2)     [STORE SIZE OF TRANSFER 
   8 3=GW          SMO      FX1  
   8 3?2G          LDX   5  SCFK 
  17 3?G6          LDX   0  AWORK2(2)        [SIZE OF SCF USED SO FAR
  15 3?_Q          STO   0  AWORK4(2)        [DA ADDRESS SCF 
  18 3#FB          STO   0  ACOMMUNE3(2)     [DA ADDRESS SCF FOR TRANSFER
  16 3#_2          ADX   0  5                [ADD THIS CHAP SIZE 
  14 3*DL          BXE   0  ACOMMUNE5(2),SJF [JUST ROOM  
  13 3*Y=          BXGE  0  ACOMMUNE5(2),YFAL[NO ROOM
  18 3BCW    SJF   ADS   5  AWORK2(2)        [INCREMENT POSITION IN SCF  
  10 3BXG          MHUNT    1,BSTB,FSTAB 
   7 3CC6          SMO      3
   8 3CWQ          LDX   3  A1(1)
  16 3DBB          LDX   1  KTAB(3)          [DA OR CORE ADDRESS 
  18 3DW2          BPZ   1  XBACK            [JUMP AS ON BACKING STORE   
  14 3F*L          LDX   1  BACK2(1)         [DA ADDRESS 
  18 3FT= ...XBACK SLL   1  11                   [REMOVE BITS 0-10 INCL. 
  16 3G#W ...      SRL   1  4                    [= DA ADDR * 128
  14 3GSG          STO   1  ACOMMUNE2(2)     [DA ADDRESS 
  15 3H#6          STO   1  AWORK3(2)        [DA ADDRESS ICF 
   9 3HRQ          TRACE 3,CHAPOVER  
  15 3J?B          STO   7  ACOMMUNE6(2)     [SAVE LOOP COUNT
   4 3JR2    #   
  11 3K=L    #     DO READ(S) AND WRITE(S)   
   4 3KQ=    #   
  11 3L9W    #     ACOMMUNE1 = SIZE OF CHAPTR
  11 3LPG    #     ACOMMUNE2 = ICF ADDRESS   
  11 3M96    #     ACOMMUNE3 = SCF ADDRESS   
   4 3MNQ    #   
  19 3N8B    XTRAN SETNCORE 1024,3,BSTB,BREAD[GET A BLOCK TO READ AND WRITE  
  18 3NN2    RPTZ  LOCKC 3,XRR               [LOCK IT DOWN IN LONG LOCK  
   9 3NXS ...      MHUNT 3,BSTB,BREAD
   8 3P7L          LDX   2  FX2  
  14 3PM=          LDX   5  ACOMMUNE2(2)     [D ADDRESS  
  16 3Q6W          LDX   6  ACOMMUNE1(2)     [SIZE OF TRANSFER   
  17 3QLG          DATUMADD 5,BINDEX,6       [DTUMISE TO EXEC INFO   
   9 3R66          BNZ   2  TRANOK   
   9 3RKQ          BNZ   1  TRANOK   
  16 3S5B          GEOERR 1,SPLITF11         [INVALID TRANSFER   
   5 3SK2    TRANOK  
  14 3T4L          LDX   5  2                [KEEP SIZE  
   8 3TJ=          LDX   2  FX2  
  15 3W3W          BNZ   5  SAB              [NOT ALL OF TRAN
  15 3WHG          LDX   5  ACOMMUNE1(2)     [SET SIZE AS ALL
  18 3X36    SAB   SBS   5  ACOMMUNE1(2)     [NE SIZE FOR NEXT TRANSFR   
  15 3XGQ          ADS   5  ACOMMUNE2(2)     [NEW GR ADDRESS 
  14 3Y2B          LDN   6  1                [BSUN FOR GR
  15 3YG2          LDN   4  0                [MODE IS READ   
  18 3Y_L          ADN   3  A1               [X3 ABSOLUTE TRANSFR ADDRESS
  15 3_F=          BACKTRAN EX4,BSET+BONL,EX5,,EX6,1,3[READ  
   8 3_YW          LDCT  4  #150 
  14 42DG          ANDX  4  ACOMMUNE9(2)     [REPLY WORD 
   9 42Y6          BZE   4  REPOKA   
   8 43CQ          LDCT  7  #010 
   7 43XB          ANDX  7  4
   9 44C2          BNZ   7  NOPICF   
   9 44WL          GEOERR 1,ICF-FAIL 
   9 45B=    NOPICF GEOERR 1,ICF-INOP
   5 45TW    REPOKA  
   4 46*G    #   
  14 46T6    #     CHANGE CHECKSUMM FOR NEW DA ADDRESS   
   4 47#Q    #   
  17 47SB          LDX   7  ACOMMUNE1(2)     [SIZE LEFT TO TRANSFER  
  17 48#2          BNZ   7  XNSCY            [NOT END OF CHAPTER YET 
   7 48RL          SBN   3  1
  15 49?=          LDX   7  AWORK3(2)        [ICF DA ADDRESS 
   7 49QW          SMO      5
  18 4==G          SBS   7  0(3)            [REDUCE LAST WORD IN CHAPTER 
   9 4=Q6          SMO      AWORK1(2)
   8 4?9Q          LDX   4  KTAB 
  16 4?PB          BPZ   4  NIC                 [J IF NOT IN CORE
  14 4#92          LDXC  1  4                   [CLEAR B0
  15 4#NL          BCS      /                   [FORGET CARRY
  10 4#YD ...      ADX   1  ALOGLEN(1)   
  17 4*8=          ADN   1  A1-1                [ELSE UPDATE CHECKSUM
  18 4B7G          SBS   7  0(1)                 [SUBTRACT OLD DA ADDRESS
  16 4BM6    NIC   LDX   7  AWORK4(2)            [SCF DA ADDRESS 
   7 4C6Q          SMO      5
  19 4CLB          ADS   7  0(3)            [FORM CORRECT CHECKSUMM WITH LUCK
  16 4D62          BPZ   4  XNIC                [J IF NOT IN CORE
   8 4F5=          ADS   7  0(1) 
   7 4FJW    XNIC  ADN   3  1
   9 4G4G    XNSCY LDX   7  BINDEX   
  21 4GJ6          ADN   7  6                [MOVER POINTER TO SECOND AREA IN CCTABLE
  17 4H3Q          LDX   4  ACOMMUNE3(2)     [SPLIT RESIDENCE ADDRESS
  17 4HHB          DATUMADD 4,7,5            [DTUMISE TO EXEC INFO   
   9 4J32          BNZ   2  TRANOKA  
   9 4JGL          BNZ   1  TRANOKA  
  16 4K2=          GEOERR 1,SPLITF13         [INVALID TRANSFER   
   5 4KFW    TRANOKA 
   7 4K_G          LDX   7  2
   8 4LF6          LDX   2  FX2  
  15 4LYQ          BNZ   7  SAA              [NOT ALL OF TRAN
  15 4MDB          LDX   7  5                [SET SIZE AS ALL
  20 4MY2    SAA   ADS   7  ACOMMUNE3(2)     [UPDTE SPLIT ADDRESS BY TRAN SIZE   
  15 4NCL          LDX   6  BRADSPLTBSU      [BSUN FOR SPLIT 
  15 4NX=          LDN   4  1                [MODE IS WRIT   
  15 4PBW          BACKTRAN EX4,BSET+BONL,EX7,,EX6,1,3[WRITE 
   8 4PWG          LDCT  4  #150 
  14 4QB6          ANDX  4  ACOMMUNE9(2)     [REPLY WORD 
   9 4QTQ          BZE   4  REPOKB   
   8 4R*B          LDCT  7  #010 
   7 4RT2          ANDX  7  4
   8 4S#L          BNZ   7  SCFI 
   9 4SS=          GEOERR 1,SCF-FAIL 
   9 4T?W    SCFI  GEOERR 1,SCF-INOP 
   5 4TRG    REPOKB  
  17 4W?6          ADX   3  7                [INCREMENT COR ADDRESS  
  17 4WQQ          SBX   5  7                [SUBTRCT SIZE WRITTEN   
   9 4X=B          BPZ   5  SIZEOK   
  15 4XQ2          GEOERR 1,SPLITF15         [WRITTN TOO MUCH
   5 4Y9L    SIZEOK  
  16 4YP=          BNZ   5  XNSCY            [MORE TO WRITE OUT  
  16 4_8W          HUNT 3,BSTB,BWRITE        [GET BLOCK ADDRESS  
   9 4_NG          BPZ   3  OKFIND   
   9 5286          HUNT 3,BSTB,BREAD 
   9 52MQ          BPZ   3  OKFIND   
   9 537B          GEOERR 1,SPLITF16 
   5 53M2    OKFIND  
   7 546L          UNLOCK 3  
  17 54L=          LDX   5  ACOMMUNE1(2)     [SIZE LEFT TO TRANSFR   
   8 555W          BPZ   5  TRSZ 
   9 55KG          GEOERR 1,SPLITF17 
  19 5656    TRSZ  BNZ   5  RPTZ             [REPEAT FOR THE RST OF CHAPTER  
   7 56JQ          FREECORE 3
   4 574B    #   
  15 57J2    #     NOW UPDATE THE CHAPTER TABLE IN CORE TO   
  11 583L    #     SHOW CHAPTER ON THE SCF   
   4 58H=    #   
  16 592W    #     CHAPTERS ARE MARKED AS SCF BY SETTING BIT 10  
   4 59GG    #   
  15 5=26    XMARK LDX   3  AWORK1(2)        [CHAPTER NUMBER 
  18 5=FQ          LDX   6  AWORK4(2)        [NEW DA ADDRESS ON THE SCF  
   7 5=_B          SRL   6  7
   8 5?F2          SMO      FX1  
  17 5?YL          ADX   6  SCFBIT           [SET BIT SAYING ON SCF  
   9 5#2J ...      LDX   1  KTAB(3)  
  15 5#4G ...      BPZ   1  TBACK            [JIF NOT IN CORE
  20 5#6D ...      LDX   1  BACK2(1)         [ELS GET KTAB ENT FROM CHAPTER BLOCK
   8 5#8B ...TBACK LDCT  0  #777 
  18 5#=# ...      ORX   0  BIT9             [MASK FOR BITS 0-9 INCLUSIVE
  16 5##= ...      ANDX  0  1                [REMOVE OLD DA ADDR 
  17 5#B8 ...      ADX   0  6                [ADD DA ADDRESS + SCFBIT
  16 5#D=          LDX   1  KTAB(3)          [DA OR CORE ADDRESS 
  18 5#XW          BPZ   1  YBACK            [JUMP AS ON BACKING STORE   
  16 5*CG ...      STO   0  BACK2(1)         [MARK AS ON SPLIT   
   8 5*X6          BRN      YNEXT
  16 5BBQ ...YBACK STO   0  KTAB(3)          [MARK AS ON SPLIT   
  10 5BWB    YNEXT LDX   7  ACOMMUNE6(2) 
  17 5CB2          BCT   7  XAGN             [BACK FOR NEXT CHAPTER  
  14 5CTL    YFAL  LDN   7  10               [BLOCK SIZE 
  14 5D*=          OUTBLOCK 7                [GET A BLOCK
  15 5DSW          OUTNUM ACOMMUNE5(2),0     [SCF FILE SIZE  
  15 5F#G          OUTNUM ACOMMUNE4(2),0     [SCF CHAPS SIZE 
  10 5FS6          INFORM 1,BRADSPT2,1   
  10 5G?Q          MFREE    BSTB,FSTAB   
   5 5GRB    #SKI G4 
   4 5H?2    (   
  16 5HQL          LDX   0  GIVE6REP            [PROCESSOR TYPE  
   8 5J==          SBN   0  21   
  15 5JPW          BZE   0  PROC                [J IF 1904A  
   7 5K9G          SBN   0  4
  15 5KP6          BZE   0  PROC                [J IF 1904S  
   7 5L8Q          SBN   0  4
  16 5LNB          BNZ   0  NPROC               [J IF NOT 1903T  
  17 5M82    PROC  LDX   1  BINDEX              [PTR TO BSTB CCTABLE 
   9 5MML          LDCH  0  A1+6(1)  
   8 5N7=          SBN   0  25   
  16 5NLW          BNZ   0  NPROC               [J IF NOT ON HSD 
   9 5P6G          LDN   0  A1B-A1   
   9 5PL6          STO   0  BSTSPROC 
   5 5Q5Q    NPROC   
   4 5QKB    )   
  14 5R52          UP                        [THATS ALL  
   4 5RJL    #END
   6 ____ ...272133560001
