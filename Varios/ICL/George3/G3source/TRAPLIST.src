  18 22FL ...      SEG   TRAPLIST,85,FILESTORE    [P.R. CRUICKSHANK,FILE 
   3 22_= ...
  15 23DW ...#     THIS SEGMENT IMPLEMENTS THE TRAPLIST (TL) 
  15 23YG ...#     COMMAND, OUTPUTING EITHER TO MONITORING   
  16 24D6 ...#     FILE OR SPECIFIED FILE. ERRORS ARE REPORTED   
  16 24XQ ...#     WHEN A USER ATTEMPTS TO ASCRTAIN THE ACCESS   
  15 25CB ...#     OF ANOTHER USER IN ANOTHER USER'S FILE.   
  10 25X2 ...#     USE OF AWORK WORDS :  
  13 26BL ...#     AWORK1 ... NUMBER OF TRAP RECORDS 
  16 26W= ...#                ONE FINTER BLOCK FOR EACH RECORD   
   3 27*W ...
  12 27TG ...      SEGENTRY   K1TRAPLIST,XLISTRAP
   5 28*6 ...PARA1   
   7 28SQ ...      8H,GROUP  
   3 29#B ...
   5 29S2 ...PARA2   
   6 2=?L ...      8H,ALL
   3 2=R= ...
   5 2?=W ...PARA3   
   7 2?QG ...      8H,ERASE  
   7 2#=6 ...      8H,WRITE  
   7 2#PQ ...      8H,APPEND 
   7 2*9B ...      8H,READ   
   7 2*P2 ...      8H,EXECUTE
   3 2B8L ...
   4 2BN= ...MASK
  20 2C7W ...      CATMASK  FILES,COMMENT       [ USED TO CHECK TRACING LEVELS   
   3 2CMG ...
   5 2D76 ...NUSERP  
  16 2DLQ ...      40HNO USER IS PERMITTED ACCESS TO THIS FILE   
   3 2F6B ...
   4 2FL2 ...#   
  15 2G5L ...#     SUBROUTINE TO CHECK FILE NAME PARAMETERS  
  16 2GK= ...#     ENTRY X3->CPB/CUNI BLOCK, EXIT SAME, LINK X6  
  11 2H4W ...#     FILE/FABSNB BLOCK CREATED 
   4 2HJG ...#   
   3 2J46 ...
   6 2JHQ ...SCHECKPAR   
  17 2K3B ...      SBX   6  FX1                 [ SET LINK RELATIVE  
  17 2KH2 ...      NAMETOP  3,FILE,FNAME        [ RENAME FOR FNORM   
   4 2L2L ...#   
  19 2LG= ...#     IF ADJUNCTS ARE PRESENT IN THE FILENAME, THEY ARE ANALYSED
  18 2L_W ...#     THE INFORMATION PUT INTO A FILE/ADJUNCTS BLOCK AND THE
  17 2MFG ...#     REPLY ADJUNCTS GIVEN. IN THE CASE OF A WORKFILE   
  16 2M_6 ...#     SET UP A 10 WORD FILE/FABSNB CONTAINING THE   
  18 2NDQ ...#     WORKFILE NAME. OTHERWISE, CONVERTS A FILE NAME FROM   
  17 2NYB ...#     ITS EXTERNAL WRITTEN FORM TO A FILE ABSOLUTE NAME 
   4 2PD2 ...#   
  16 2PXL ...      FNORM   3                    [ ERRORS REPORTED
  18 2QC= ...      TESTREP2   NAMEFORM,SERROR [ J - IF ERROR IN PARAMETER
  10 2QWW ...      MHUNT   3,FILE,FNAME  
  20 2RBG ...      NAMETOP  3,CPB,CUNI          [ RENAME FOR ANY ERROR MESSAGES  
  15 2RW6 ...      ADX   6  FX1                 [ RESET LINK 
  18 2S*Q ...      EXIT  6  0                   [ END CHECKPARAM ROUTINE 
   4 2STB ...#   
  15 2T*2 ...#     FREE REMAINING FILE.FINTER BLOCKS ROUTINE 
   4 2TSL ...#   
   6 2W#= ...SFREEFINT   
   8 2WRW ...      SMO      FX2  
  16 2X?G ...      LDX   5  AWORK1      [ NUMBER OF TRAPS RECORDS
  15 2XR6 ...      BZE   5  SFEXIT      [ J - IF NONE LEFT   
   5 2Y=Q ...SFREELP 
  15 2YQB ...      MFREE   FILE,FINTER      [ FREE NEXT ONE  
   9 2_=2 ...      BCT   5  SFREELP  
   5 2_PL ...SFEXIT  
  15 329= ...      EXIT  6  0    [ END OF FREE FINTER ROUTINE
   4 32NW ...#   
  12 338G ...#     FREE CPB.CUNI BLOCK ROUTINE   
   4 33N6 ...#   
   5 347Q ...SFREEUNI
   7 34MB ...      UNIFREE   
  15 3572 ...      EXIT  6  0    [ END OF FREE UNI ROUTINE   
   4 35LL ...#   
  16 366= ...#     FREE FILE.FABSNB & FILE.ADJUNCTS BLOCK ROUTINE
   4 36KW ...#   
   5 375G ...SFREEFAB
  10 37K6 ...      VFREE   FILE,FABSNB   
  16 384Q ...      VFREE   FILE,ADJUNCTS      [ MAY NOT BE THERE 
  15 38JB ...      EXIT  6  0    [ END OF FREE FAB ROUTINE   
   3 3942 ...
   4 39HL ...#   
  14 3=3= ...#     OUTPUT TO FILE/MONITORING FILE ROUTINE
  14 3=GW ...#     X7 -VE MEANS O/P TO FILE . X6 = LINK  
   8 3?2G ...#     X1=FX1, X2=FX2
   4 3?G6 ...#   
   5 3?_Q ...SOUTPUTF
  17 3#FB ...      SBX   6  FX1                 [ SET LINK RELATIVE  
  16 3#_2 ...      BNG   7  STARTOP             [ J - IF NOT M/F 
   4 3*DL ...#   
  12 3*Y= ...#     CHECK TRACING LEVELS OF M/F   
   4 3BCW ...#   
  20 3BXG ...      TESTRACE   MASK(1),SNCCF     [ NOT COMMENT NOR FILE CATAGORY  
   9 3CC6 ...      BRN      STARTOP  
   5 3CWQ ...SNCCF   
  20 3DBB ...      MONOUT   JMTRACE1            [ CURRENT TRACING LEVELS NOT OKAY
   8 3DW2 ...      ADX   6  FX1  
  13 3F*L ...      EXIT  6  0      [ TRACE ERROR EXIT
   4 3FT= ...#   
  16 3G#W ...#     SET UP BUFFER FOR O/P LINES 18 WORDS LOG LEN  
  17 3GSG ...#     DATA WORD 1 = ^:^ RED TAPE 0 = REC LENGTH, 1 = #41
   4 3H#6 ...#   
   5 3HRQ ...STARTOP 
  15 3J?B ...      SETNCORE   18,3,FILE,FWB     [ X3 -> BLOCK
  14 3JR2 ...      LDN   0  #41                 [ PFCC   
  19 3K=L ...      STO   0  A1+1(3)             [ STORED IN 2ND RED TAPE WORD
  13 3KQ= ...      LDCT  0  #120                [ ^:^
  18 3L9W ...      STO   0  A1+2(3)             [ STORED IN 1ST DATA WORD
   4 3LPG ...#   
  12 3M96 ...#     CHECK NO OF TRAPS RECORDS (X5)
   4 3MNQ ...#   
  18 3N8B ...      LDX   5  AWORK1(2)           [ NUMBER OF TRAPS RECORDS
   9 3NN2 ...      TRACE   5,NU TRAPS
  16 3P7L ...      BZE   5  SNOTRAPS            [ J - IF NOTRAPS 
   4 3PM= ...#   
  19 3Q6W ...#     TRAPS RECORDS BLOCK LOOP. CONVERTS TRAPS RECORD IN FINTER 
  19 3QLG ...#     BLOCK TO CHAR FORM IN FWB. FREES FINTER BLOCK AND OUTPUTS 
  21 3R66 ...#     FWB TO FILE (MONITORING FILE) . X5 = COUNT,X6 = LINK,X7 =0 IF NOT 
  13 3RKQ ...#     X0 = COUNT OF WORDS USED IN FWB   
   4 3S5B ...#   
   5 3SK2 ...STRBL   
  18 3T4L ...      MHUNT   2,FILE,FINTER        [ NEXT TRAPS RECORD BLOCK
  17 3TJ= ...      MHUNT   3,FILE,FWB           [ REHUNT BUFFER BLOCK
  16 3W3W ...      LDX   4  A1+4(2)             [ TRAP MODES OPEN
   4 3WHG ...#   
  10 3X36 ...#     MOVE ACROSS INTO FWB  
   4 3XGQ ...#   
  17 3Y2B ...      ADN   2  A1+1                [ START OF USERNAME  
  20 3YG2 ...      ADN   3  A1+2                [ WORD IN BUFFER CONTAINING ^:^  
  18 3Y_L ...      LDCT  0  #200                [ SPECIFY CHAR POSITION 1
  16 3_F= ...      ORX   3  0                   [ READY FOR MVCH 
  17 3_YW ...      MVCH  2  12                  [ MOVE IN USERNAME   
  21 42DG ...      MFREE   FILE,FINTER          [ FREE FINTER BLOCKS ( ONE BY ONE)   
  14 42Y6 ...#     DECREMENT AWORK1 IN CASE OF BREAKINS  
  19 43CQ ...      LDX   0  5      [ NUMBER OF REMAINING TRAPS RECORDS + 1   
   7 43XB ...      SBN   0  1
   8 44C2 ...      SMO      FX2  
   9 44WL ...      STO   0  AWORK1   
   4 45B= ...#   
  16 45TW ...#     DECODE TRAP RECORDS X3 -> CURRENT CHAR IN FWB 
   4 46*G ...#   
  19 46T6 ...      LDN   0  6                   [ X0 ACCUMULATES RECORD SIZE 
  18 47#Q ...      BPZ   4  SNGRP               [ J - IF NOT GROUP TRAP  
  18 47SB ...      ADN   0  2                   [ 2 WORDS FOR ^ GROUP ^  
  18 48#2 ...      LDN   2  PARA1(1)            [ ADDREASS OF CHAR STRING
  15 48RL ...      MVCH  2  8                   [ MOVE TO FWB
   5 49?= ...SNGRP   
  15 49*F ...      ANDN  4  #37    [ REMOVE ALLBUT TRAP BITS 
   4 49CN ...#   
  18 49FX ...# INCASE WE OBTAIN A TRAPS RECORD WHICH HAS NO TRAP BITS SET
   9 49J6 ...# WE DEFAULT TO ^ALL^.  
   4 49L* ...#   
  19 49NJ ...      BZE   4  SALLTRAPS    [ SOMETHING WRONG WITH TRAPS RECORD 
  16 49QW ...      LDN   2  #37                 [ TEST FOR ALL   
  17 4==G ...      BXU   2  4,SNALL             [ J - IF NOT ^ ALL ^ 
   6 4=G# ...SALLTRAPS   
  17 4=Q6 ...      ADN   0  2                   [ 2 WORDS FOR ^ ALL ^
  18 4?9Q ...      LDN   2  PARA2(1)            [ ADDRESS OF CHAR STRING 
  15 4?PB ...      MVCH  2  8                   [ MOVE TO FWB
  18 4#92 ...      BRN      STRAPSDONE          [ J TO TRAP MODES DONE   
   4 4#NL ...#   
  11 4*8= ...#     LOOP TO CHECK ALL TRAPS   
   4 4*MW ...#   
   5 4B7G ...SNALL   
  18 4BM6 ...      SLL   4  18                  [ ERASE TRAP AT B1 ETC...
  19 4C6Q ...      LDN   2  PARA3-2(1)          [ ADDRESS OF TRAPS STRINGS   
   5 4CLB ...STRAPSL1
  13 4D62 ...      ADN   2  2      [ TRAP ABSENT CASE
   5 4DKL ...STRAPSL 
  16 4F5= ...      SLL   4  1                   [ NEXT TRAP BIT  
  17 4FJW ...      BPZ   4  STRAPSL1             [ J - IF B0 NOT SET 
  18 4G4G ...      ADN   0  2                   [ 2 WORDS FOR TRAP STRING
  17 4GJ6 ...      MVCH  2  8                   [ MOVE ACROSS STRING 
  15 4H3Q ...      ERX   4  GSIGN               [ REMOVE BIT 
  10 4HHB ...      BZE   4  STRAPSDONE   
   9 4J32 ...      BRN      STRAPSL  
   4 4JGL ...#   
  12 4K2= ...#     NO TRAPS RECORDS , X3 -> FWB  
   4 4KFW ...#   
   5 4K_G ...SNOTRAPS
  16 4LF6 ...      LDN   2  NUSERP(1)           [ MESSAGE ADDRESS
  20 4LYQ ...      ADN   3  A1+2                [ POINTS TO 1ST DATA WORD IN FWB 
  17 4MDB ...      MOVE  2  10                  [ 10 WORDS OF MESSAGE
  17 4MY2 ...      LDN   0  12                  [ 12 WORDS OF DATA   
  19 4NCL ...      LDN   5  1                   [ FOR BCT TO TERMINATE LOOP  
  21 4NX= ...      BRN      SNTDONE             [ NO TRAPS DONE        [ J - IF NO MO
   3 4PBW ...
   4 4PWG ...#   
  19 4QB6 ...#     TRAPS DONE, FWB ALMOST READY ( SPACE FILL LAST CHARACTER  
   4 4QTQ ...#   
   6 4R*B ...STRAPSDONE  
  17 4RT2 ...      LDN   2  ACES        [ SPACE FILLING CHARACTERS   
  20 4S#L ...      MVCH  2  3                   [ MOVE ACROSS SPACE FILLING CHAR 
   4 4SS= ...#   
  19 4T?W ...#     X3 SHOULD NOW POINT TO 1ST CHAR OF 1ST UNUSED WD OF FWB   
  12 4TRG ...#     EXCEPT IN THE NO TRAPS CASE   
   4 4W?6 ...#   
   5 4WQQ ...SNTDONE 
  19 4X=B ...      BNG   7  SFILE               [ J - IF NOT MONITORING FILE 
  17 4XQ2 ...      SLL   0  2                   [ NO CHARS IN MESSAGE
  14 4Y9L ...      SBN   0  8      [ OMIT A1 & PFCC WORDS
  13 4YP= ...      OUTMON   0,A1+2,MASK(1),FILE,FWB  
  19 4Y_4 ...      TESTBRKIN   XBREAKIN2      [ TEST IF USER HAS BROKEN IN   
  17 4_8W ...      BRN      SBCTEST             [ J TO BCT TEST ON X5
   5 4_NG ...SFILE   
  14 5286 ...      LDX   2  0      [ FOR RECORD HEADER   
  14 52MQ ...      MHUNT   3,FILE,FWB     [ REHUNT FWB   
  14 537B ...      STO   2  A1(3)      [ RECORD HEADER   
  17 53M2 ...      STEP   ,0(2),XBREAKIN     [ X3 -> PLACE IN FUWB   
  17 546L ...      TESTREP2   FILEFULL,XFILEFULL    [ FILE FULL CASE 
   4 54L= ...#   
  11 555W ...# NOW WE MOVE ACROSS THE RECORD 
   4 55KG ...#   
  13 5656 ...      MHUNT   2,FILE,FWB    [ X2 -> FWB 
  14 56JQ ...      ADN   2  A1      [ MOVING RECORD FROM 
  16 574B ...      SMO      0(2)     [ NUMBER OF WORDS TO MOVE   
   7 57J2 ...      MOVE  2  0
   5 583L ...SBCTEST 
  19 58H= ...      BCT   5  STRBL               [ X5 +VE MORE TRAPS RECORDS  
   9 59GG ...      MFREE   FILE,FWB  
  16 5=26 ...      ADX   6  FX1                 [ RESTORE LINK   
  18 5=FQ ...      EXIT  6  0                   [ END OF SOUTPUTF ROUTINE
   4 5=_B ...#   
   3 5?F2 ...
   3 5?YL ...
   3 5#D= ...
   4 5#XW ...#   
  15 5*CG ...#     ****************************************  
   4 5*X6 ...#   
  13 5BBQ ...#     ENTRY POINT TO ANALYSE PARAMETERS 
   4 5BWB ...#   
  15 5C3J ...#     ****************************************  
   4 5C8Q ...#   
   5 5CB2 ...XLISTRAP
  10 5CF6 ...      FSHCODE  B,SKIPINA1   
   4 5CJ= ...(   
  16 5CMB ...      TRANSBEG FSHTRAPID,TRAPLIST,1,CLB,,ACOMMUNE1  
  16 5CQG ...      BRN      TOMONOUT       [ RETURN TO DO MONOUT 
  14 5CTL ...      BRN      TOENDCOM       [ OR TO ENDCOM
  15 5CYQ ...      BRN      TOCOMBRK       [ OR FOR COMBRKIN 
  15 5D3W ...      BRN      TOABAND        [ OR TO ABANDCOM  
   5 5D72 ...SKIPINA1
   4 5D=6 ...)   
  18 5D*= ...      STOZ     AWORK1(2)      [ FOR NUMBER OF TRAP RECORDS  
  18 5DSW ...      SPARAPASS                    [ PASS ENTRANT PARAMATER 
   9 5F#G ...      MHUNT   3,CPB,CUNI
   4 5FS6 ...#   
  16 5G?Q ...#     IF NULL ANUM = 0, IF NON EXISTANT ANUM = -1   
   4 5GRB ...#   
   9 5H?2 ...      NGX   0  ANUM(3)  
  18 5HQL ...      BNG   0  YESENTRY            [ J - IF THERE IS A PARAM
  18 5J== ...      COMERR   JPARMIS,JFENTRANT   [ FILENAME PARAM MISSING 
   5 5JPW ...YESENTRY
  16 5K9G ...      CALL  6  SCHECKPAR           [ CHECK FILENAME 
  10 5KP6 ...      MHUNT   3,FILE,FABSNB 
   4 5L8Q ...#   
  13 5LNB ...# TEST TO SEE IF TRAPLISTING A WORKFILE.
   4 5M82 ...#   
  16 5MML ...      BS   3,BFABANY      [ ANY ENTRANT ACCEPTABLE  
  16 5N7= ...      JBC   NOTWF,3,BFABWORK      [ J - NOT WORKFILE
   4 5NLW ...#   
  19 5P6G ...#  IN THE WORKFILE CASE, WE HAVE TO SET UP OUR OWN FILE.FINTER  
  19 5PL6 ...#  (5 WDS) BLOCK AND PUT THE JOB'S USERNAME AND THE WORKFILE'S  
  18 5Q5Q ...#  TRAPWORD IN THE FEXTRA BLOCK. ALSO WE SET AWORK1 TO 1.   
   4 5QKB ...#   
  15 5SHW ...      FINDWFN   ,2      [ X2-> FCB OF WORKFILE  
  20 5T3G ...      FINDFEXTRA   2,2   [ X2 -> FEXTRA OF WORKFILE CONTAINS TRAPS  
  10 5TH6 ...      LDX   5  FWORKTRAP(2) 
  16 5TN# ...      BZE   5  XCLEARUP1    [ IF TRAPWORD IS ZERO   
  21 5TTG ...      SETNCORE   5,3,FILE,FINTER    [ X3 -> FILE FINTER TO STORE TRAPS  
  18 5W2Q ...      STO   5  A1+4(3)      [ STORE TRAPS IN FINTER BLOCK   
  15 5WGB ...      FJOCA   2,FX2         [ X2 -> JOB BLOCK   
  16 5X22 ...      LDN   2  JUSER(2)     [ X2-> JOB'S USERNAME   
  19 5XFL ...      ADN   3  A1+1         [ X3 -> START OF U.N IN FINTER BLOCK
  18 5X_= ...      MOVE  2  3            [ MOVE ACROSS U.N INTO FINTER   
   7 5Y57 ...      LDN   0  1
   8 5Y94 ...      SMO      FX2  
  14 5Y#_ ...      STO   0  AWORK1    [ 1 TRAPS RECORD   
   9 5YDW ...      BRN      XCLEARUP1
   5 5YYG ...NOTWF   
   4 5_D6 ...#   
  14 5_XQ ...#     OPEN DIRECTORY IN MODE READ (QUERY)   
   4 62CB ...#   
  12 62X2 ...      OPENDIR  XBREAKIN1,READ,QUERY 
   4 63BL ...#   
  16 63W= ...#     IF NAME RECORD IS FOUND, BLOCK FILE/ENT SET UP
   8 64*W ...#     CONTAINING IT 
   4 64TG ...#   
  18 65*6 ...      TESTRPN2   OK,REPERROR       [ J - IF NOT OPEN OKAY   
  19 65SQ ...      OWNERCHECK                   [ CHECK IF OWNER X2 -> FX2   
  16 66#B ...      TESTREP2   OK,OKOWNED        [ J - IF OWNED   
   9 66S2 ...      SETREP2  NOTOWN   
   9 67?L ...      BRN      REPERROR 
   5 67R= ...OKOWNED 
   6 67WB ...#UNS ILOGACC
   4 67_G ...(   
   8 684L ...      LOGACCESS  10 
   4 687Q ...)   
  17 68=W ...      MHUNT   3,FILE,ENT           [ HUNT NAME RECORD   
  20 68QG ...      LDX   7  ENUSE(3)            [ STORE NUMBER OF TRAPS RECORDS  
  15 69=6 ...      STO   7  AWORK1(2)           [ IN AWORK1  
  18 69PQ ...      BZE   7  XCLEARUP            [ J - IF NO TRAPS RECORDS
  18 6=9B ...      JBC   NOINDX,3,BEINDEX       [ J - IF NO INDEX RECORD 
  18 6=P2 ...      STEP                         [ STEP OVER INDEX RECORD 
   5 6?8L ...NOINDX  
  18 6?N= ...      READ                         [ GET NEXT TRAPS RECORD  
  19 6#7W ...      MHUNT   3,FILE,FRB           [ HUNT TRAPS RECORD BUFFER   
  20 6#MG ...      NAMETOP  3,FILE,FINTER       [ RENAME - SO IT REMAINS THERE   
   9 6*76 ...      BCT   7  NOINDX   
   4 6*LQ ...#   
  14 6B6B ...#     CLEAR UP BLOCKS AND CLOSE DIRECTORY   
   4 6BL2 ...#   
   5 6C5L ...XCLEARUP
   7 6CK= ...      CLOSETOP  
   9 6D4W ...      MFREE   FILE,ENT  
   6 6DJG ...XCLEARUP1   
   9 6F46 ...      CALL  6  SFREEUNI 
   9 6FHQ ...      CALL  6  SFREEFAB 
   4 6G3B ...#   
  14 6GH2 ...#     NOW CHECK SECOND PARAMETER (FILENAME) 
   4 6H2L ...#   
  16 6HG= ...      SPARAPAS                     [ NEXT PARAMETER 
   9 6H_W ...      MHUNT   3,CPB,CUNI
   4 6JFG ...#   
  15 6J_6 ...#     IF NULL OR NON EXISTANT SEND TO MONITORING
  17 6KDQ ...#     FILE ELSE SEND TO SPECIFIED FILE AFTER CHECKING   
   4 6KYB ...#   
  21 6LD2 ...      NGX   7  ANUM(3)             [ ANUM -VE IF NON EXISTANT ,0 IF NULL
  18 6LXL ...      BNG   7  YESFILE             [ J - IF FILENAME PRESENT
   4 6MC= ...#   
  19 6MWW ...#     X7 IS USED AS A FLAG (-VE IF FILE) ELSE MONITORING FILE   
   4 6NBG ...#   
  10 6NDP ...      FSHCODE  A,SKIPINB1   
   4 6NGY ...(   
   9 6NK7 ...      TRANSFIN ,,ALIEN  
   5 6NMB ...SKIPINB1
   4 6NPK ...)   
   5 6NRS ...TOMONOUT
  15 6NW6 ...      CALL  6  SOUTPUTF            [ M/F OUTPUT 
   9 6P*Q ...      BRN      SFINISHED
   4 6PTB ...#   
  15 6Q*2 ...#     PARAM 2 NOT NULL, CHECK FILENAME AS BEFORE
  10 6QSL ...#     X3-> CPB/CUNI BLOCK   
   4 6R#= ...#   
   5 6RRW ...YESFILE 
  16 6S?G ...      CALL  6  SCHECKPAR           [ CHECK FILENAME 
  19 6SR6 ...      CREATEB  2                   [ CREATES FILE/CREATES (X2->)
   8 6T=Q ...      LDCT  0  #400 
   8 6TQB ...      ORN   0  #12  
  19 6W=2 ...      STO   0  CETM(2)             [ INDICATES GRAPHIC MODE FILE
  15 6WPL ...      BS   2,CESERIAL    [ MAINLY FOR WORKFILES 
   4 6X9= ...#   
   7 6XNW ...#     OPEN FILE 
   4 6Y8G ...#   
  15 6YN6 ...      USEROPEN   XBREAKIN1,APPEND,CREATE,EMPTY  
   9 6_7Q ...      REPERR   OPENOK   
   9 6_MB ...      BRN      SFINISHED
   4 7272 ...#   
  13 72LL ...#     OUTPUT FILE READY FOR APPENDING   
   4 736= ...#   
   5 73KW ...OPENOK  
  15 745G ...      CALL  6  SOUTPUTF            [ FILE OUTPUT
   4 74K6 ...#   
   7 754Q ...#     CLOSE FILE
   4 75JB ...#   
   6 7642 ...      CLOSE 
   9 76HL ...      BRN      SFINISHED
   4 76JN ...#   
  13 76KQ ...# BREAKIN WITH FILE OPEN DURING A STEP. 
   4 76LS ...#   
   5 76MW ...XBREAKIN
   9 76NY ...      TRACE   5,TL BRKIN
  11 76Q2 ...      CLOSE   [ THE OUTPUT FILE 
   4 76R4 ...#   
  14 76S6 ...# BREAKIN DURING OUTPUT TO MONITORING FILE  
   4 76T8 ...#   
   6 76W= ...XBREAKIN2   
   9 76X# ...      MFREE   FILE, FWB 
   9 76YB ...      CALL  6  SFREEFINT
   9 76_D ...      CALL  6  SFREEUNI 
   9 772G ...      CALL  6  SFREEFAB 
  10 772L ...      FSHCODE  A,SKIPINB4   
   4 772Q ...(   
   7 772W ...      LDN   7  3
   9 7732 ...      TRANSFIN 7,,ALIEN 
   5 7736 ...SKIPINB4
   4 773= ...)   
   5 773B ...TOABAND 
  14 773J ...      ABANDCOM          [ NON-RESTARTABLE   
   4 774L ...#   
  14 775N ...# BREAKIN DURING USEROPEN, CLEAR UP BLOCKS  
   4 776Q ...#   
   6 777S ...XBREAKIN1   
   9 778W ...      CALL  6  SFREEFINT
   9 779Y ...      CALL  6  SFREEUNI 
   9 77?2 ...      CALL  6  SFREEFAB 
  10 77?6 ...      FSHCODE  A,SKIPINB3   
   4 77?= ...(   
   7 77?B ...      LDN   7  2
   9 77?G ...      TRANSFIN 7,,ALIEN 
   5 77?L ...SKIPINB3
   4 77?Q ...)   
   5 77?W ...TOCOMBRK
  13 77#4 ...      COMBRKIN          [ RESTARTABLE   
   3 77*6 ...
   4 77B8 ...#   
   8 77C= ...# OUTPUT FILE FULL  
   4 77D# ...#   
   6 77FB ...XFILEFULL   
   9 77GW ...      TRACE   2,TL FILEF
  16 782G ...      MFREE   FILE,FWB    [ IN THE CASE OF FILE FULL
   8 7849 ...      CLOSEABANDON  
   9 785Y ...      CALL  6  SFREEFINT
   9 787M ...      CALL  6  SFREEFAB 
   9 789B ...      COMERR   ERTOOBIG 
   3 78?5 ...
   4 78#S ...#   
  10 78BH ...# CLEAR UP BLOCKS AND FINISH
   4 78D= ...#   
   3 78G6 ...
   5 78_Q ...REPERROR
   7 79FB ...      REPALLER  
   5 79_2 ...SERROR  
   9 7=DL ...      TRACE   2,TL ERROR
   6 7=Y= ...SFINISHED   
   9 7?CW ...      CALL  6  SFREEFINT
   9 7?XG ...      CALL  6  SFREEUNI 
   9 7#C6 ...      CALL  6  SFREEFAB 
  10 7#F4 ...      FSHCODE  A,SKIPINB2   
   4 7#H2 ...(   
   7 7#JY ...      LDN   7  1
   9 7#LW ...      TRANSFIN 7,,ALIEN 
   5 7#NS ...SKIPINB2
   4 7#QQ ...)   
   5 7#SN ...TOENDCOM
   6 7#WQ ...      ENDCOM
   4 7*BB ...#   
   4 7*W2 ...#END
   4 7B*L ...#   
   6 ____ ...112721230001
