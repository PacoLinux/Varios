  14 22FL    #LIS  K0HALT>K0ALLGEO>K0GREATGEO>K0COMMAND  
  16 22_=    #SEG  HALT5                        [R.H.THOMPSON  F6
   7 23DW    #OPT  K6HALT=0  
   9 23YG                   8HHALT   
  10 24D6          SEGENTRY K1HALT,XX1H  
   9 24XQ    PHCATCH        #77777700
  10 25CB    ZMEM           12HIN MEMBER 
  21 25X2    #DEF           XBLEN=3             [SO THAT APEDSP1+XBLEN IS A RELATIVE 
  20 26BL                                       [POINTER TO MESSAGE IN APED BLOCK
   4 26W=    # 1 
  20 27*W    # 1   THE HALT SEGMENT HANDLES THE COMMAND -HALT-. THE ACTION TAKEN 
  19 27TG    # 1   DEPENDS ON THE CONDITIONS UNDER WHICH THE HALT WAS ISSUED 
   4 28*6    # 1 
   4 28SQ    # 2 
  20 29#B    # 2   INPUT- 1. CPB/CALAS BLOCK GENERATED BY THE COMMAND PROCESSOR. 
  17 29S2    # 2          2. X2 POINTS TO THE COMMAND ACTIVITY BLOCK.
   4 2=?L    # 2 
   4 2=R=    # 3 
  15 2?=W    # 3   OUTPUT-1. OLD EVENT BLOCK DESTROYED (APED)
  15 2?QG    # 3       2. NEW APED BLOCK FORMED FOR -HALT-   
  21 2#=6    # 3       3. CATEGORY BITS CHANGED TO 6 IF COMMAND ISSUED AT BREAK-IN   
  17 2#PQ    # 3          DURING THE RUNNING OF THE OBJECT PROGRAM.  
   4 2*9B    # 3 
  18 2*P2    # 4   ERROR ACTION- 1. GEOERR IF CPB/CALAS BLOCK NOT FOUND  
   4 2B8L    # 4 
  20 2BN=    # 4                    NO CHECK IS MADE TO SEE WHETHER CATEGORY BITS
  12 2C7W    # 4                    ARE ALREADY 6
   4 2CMG    # 4 
   4 2D76    # 7 
  18 2DLQ    # 7   FIXED STORE SEGMENTS CALLED - NONE EXCEPT VIA MACROS  
  18 2F6B    # 7   BACKING STORE CHAPTERS CALLED - NONE EXCEPT VIA MACROS
   4 2FL2    # 7 
   4 2G5L    XX1H
   4 2GK=    PHA1
   8 2H4W    #SKI  K6HALT>699-699
   8 2HJG          TRACE 1,PHA1  
   8 2J46          FPCACA   3,2  
   9 2JHQ          LDX   7  ANM(3)   
   7 2K3B          ANDN  7  3
  16 2KH2          STO   7  AWORK1(2)           [STORE MEMBER NO.
  10 2L2L          LDN   5  ACOMMUNE4(2) 
   9 2LG=          LDN   4  ZMEM(1)  
   7 2L_W          MOVE  4  3
   8 2MFG          LDX   2  FX2  
   9 2M_6          LDEX  7  EXT+10(2)
   5 2NDQ    #SKI  G4
   4 2NYB    (   
  10 2PD2          HUNT2    3,BSTB,BSCB  
  14 2PXL          LDX   4  AQUOTA(3)           [QUOTA   
   8 2QC=          ANDN  4  #7777
  15 2QWW          LDX   5  ATSWAPS(3)          [PAGETURNS   
   8 2RBG          LDX   3  FX2  
   9 2RW6          STO   4  AWORK3(3)
   9 2S*Q          STO   5  AWORK4(3)
   4 2STB    )   
  10 2T*2          LDX   4  CONTEXT(2)   
   7 2TSL          SLL   4  9
  17 2W#=          BPZ   4  PHA4                [JUMP IF NOT BREAK-IN
   5 2WRW    #SKI  G3
   8 2X?G          LDX   3  FX2  
  16 2XR6    PHA2  HUNT2    3,CPB,CALAS         [FIND THE CALAS  
  16 2Y=Q          LDEX  4  CPBCPLEV(3)         [BLOCK AT FIRST  
  15 2YQB          TXU   4  7                   [BRK IN LEVEL
   8 2_=2          BCS      PHA2 
   4 2_PL    PHA3
   8 329=    #SKI  K6HALT>699-699
   8 32NW          TRACE 1,PHA3  
  17 338G          LDX   4  CPBFSD(3)           [GET CATEGORY BITS   
   8 33N6    #SKI  K6HALT>699-699
   9 347Q          TRACE 4,CATEGIN   
  17 34MB          LDX   5  4                   [SAVE CATEGORY BITS  
  19 3572          ANDN  4  #77                 [CLEAR ALL BUT LAST 6 BITS   
  17 35LL          SBN   4  5                   [ASSUME CATEGORY IS 5
  15 366=          BNZ   4  PHA4                [DON'T TOUCH 
  17 36KW          ANDX  5  PHCATCH(1)          [MASK OUT LAST 6 BITS
  14 375G          ORN   5  #6                  [SET TO 6
  16 37K6          STO   5  CPBFSD(3)           [STOW BACK IN CPB
   4 384Q    PHA4
   8 38JB    #SKI  K6HALT>699-699
   9 3942          TRACE    1,PHA4   
  11 39HL          HUNTW    3,APED,ADELETE   
  17 3=3=          BNG   3  PHA42               [NO, TRY FOR ANEVENT 
  16 3=GW          FREECORE 3                   [YES, SO FREE IT 
  21 3?2G          BRN      PHA44               [EITHER ONE OR THE OTHER, NOT BOTH   
  11 3?G6    PHA42 HUNTW    3,APED,ANEVENT   
  18 3?_Q          BNG   3  PHA44               [NONE, SO DON'T FREE IT  
  16 3#FB          FREECORE 3                   [YES, SO SCRUB IT
   5 3#_2    PHA44   
  18 3*DL          SPARANOT 1                   [GET USER MESSAGE, IF ANY
  20 3*Y=          HUNT     3,CPB,CUNI          [FIND BLOCK FORMED BY PARA-MACRO 
  18 3BCW          LDX   6  ANUM(3)             [TEST FOR NO PARAMETERS  
  15 3BXG          BNG   6  PHA5                [IF NEGATIVE 
  16 3CC6          BNZ   6  PHA6                [PARAMETER EXISTS
   4 3CWQ    PHA5
   8 3DBB    #SKI  K6HALT>699-699
   9 3DW2          TRACE    1,PHA5   
  20 3F*L          NAME     3,APED,ANEVENT      [FROM CPB/CUNI TO APED/ANEVENT   
   7 3FT=          PHOTO    5
  18 3G#W          ALTLEND  3,XBLEN,APED,ANEVENT    [JUST PREFACE LENGTH 
  18 3GSG          TESTMOVE 5,PHA55             [BRANCH IF NO HUNT NEEDED
   5 3H#6    PHA53   
   8 3HRQ    #SKI  K6HALT>699-699
   9 3J?B          TRACE    1,PHA53  
  11 3JR2          HUNTW    3,APED,ANEVENT   
   5 3K=L    PHA55   
   8 3KQ=    #SKI  K6HALT>699-699
   9 3L9W          TRACE    1,PHA55  
  18 3LPG          STOZ     APEDSP1+2(3)        [ZERO FOR CHARACTER COUNT
   9 3M96          LDX   7  AWORK1(2)
  15 3MNQ          STO   7  APEDSP1(3)          [MEMBER NO.  
  19 3N8B          LDN   5  APEDHALT            [CONTROL VALUE FOR IF COMMAND
  10 3NN2          STO   5  APEDSP1+1(3) 
   5 3P7L    #SKI  G4
  21 3PM=          LDN   5  7                   [NO. OF WDS. FOR PARAMS. +1 WORD EACH
   5 3Q6W    #SKI  G3
  20 3QLG          LDN   5  3                   [2-WORD TIME + 1 WORD EACH PARAM.
   8 3R66          BZE   7  PHA57
  21 3RKQ          ADN   5  4                   [3-WORD PARAM FOR MEMBER NO. + 1 WORD
  20 3S5B    PHA57 OUTBLOCK 5                   [GET THE MONITORING FILE BLOCK   
  17 3SK2          OUTMILL                      [TAG WITH THE TIME   
   5 3T4L    #SKI  G4
   4 3TJ=    (   
   7 3W3W    #SKI  JNLPACK   
   4 3WHG    (   
   9 3X36          LDX   4  AWORK3(2)
   9 3XGQ          LDX   5  AWORK4(2)
  15 3Y2B          OUTPACKC 4,1,QUOTA           [OUTPUT QUOTA
  16 3YG2          OUTPACKC 5,1,PAGETURN        [OUTPUT PAGETURNS
   4 3Y_L    )   
   8 3_F=    #SKI  JNLPACK<1$1   
   4 3_YW    (   
  15 42DG          OUTNUM   AWORK3(2),0         [OUTPUT QUOTA
  16 42Y6          OUTNUM   AWORK4(2),0         [OUTPUT PAGETURNS
   4 43CQ    )   
   4 43XB    )   
  16 44C2          BZE   7  PHA10               [NO MEMBER NO.   
   8 44WL          LDX   3  GSIGN
   7 45B=          ADX   3  2
  21 45TW          DCH   7  ACOMMUNE6(3)        [APPEND MEMBER NO. TO CHAR. STRING   
  19 46*G          OUTPARN  11,ACOMMUNE4        [PREPARE PARAM FOR MEMBER NO.
  10 46T6    #              NO MESSAGE   
  17 47#Q          BRN      PHA10               [CARRY ON SERGEANT   
   4 47SB    PHA6
   8 48#2    #SKI  K6HALT>699-699
   9 48RL          TRACE    1,PHA6   
  17 49?=          LDEX  5  ANUM(3)             [GET CHARACTER COUNT 
  20 49QW          SBN   5  41                  [MAX. TO BE PASSED ON ANYWAY (+1)
  18 4==G          BNG   5  PHA7                [40 OR LESS CHARACTERS   
   5 4=Q6    PHA65   
   8 4?9Q    #SKI  K6HALT>699-699
   9 4?PB          TRACE    1,PHA65  
  16 4#92          LDN   5  40                  [TRUNCATE TO 40  
  18 4#NL          BRN      PHA8                [SKIP ROUND TO GET CORE  
   4 4*8=    PHA7
   8 4*MW    #SKI  K6HALT>699-699
   9 4B7G          TRACE    1,PHA7   
  19 4BM6          ADN   5  41                  [CONFLUENCE OF CODE STREAMS  
   4 4C6Q    PHA8
   8 4CLB    #SKI  K6HALT>699-699
   9 4D62          TRACE    1,PHA8   
  19 4DKL          LDX   6  5                   [SAVE COUNT FOR GETTING CORE 
  19 4F5=          LDX   7  6                   [AND AGAIN TO MOVE MESSAGE   
  20 4FJW          ADN   5  XBLEN*4+3           [ROUND AND INCLUDE APED/ANEVENT  
  16 4G4G          SRL   5  2                   [REDUCE TO WORDS 
  20 4GJ6          SETUPCORE 5,5,APED,ANEVENT   [GET SPACE FOR AND NAME NEW BLOCK
  18 4H3Q          HUNT     3,CPB,CUNI          [GET MESSAGE BLOCK BACK  
  18 4HHB          LDX   4  5                   [SAVE BLOCK START ADDRESS
  17 4J32          ADN   7  3                   [ROUND FOR WORD MOVE 
  16 4JGL          SRL   7  2                   [REDUCE TO WORDS 
  19 4K2=          ADN   3  APARA               [MOVE UP TO MESSAGE -FROM-   
  18 4KFW          ADN   4  APEDSP1+XBLEN       [MOVE UP TO MESSAGE -TO- 
  18 4K_G          SMO      7                   [PURE PROCEDURE MODIFY   
  16 4LF6          MOVE  3  0                   [MOVE THE MESSAGE
  17 4LYQ          LDX   3  5                   [OTHER INITIALISATION
   4 4MDB    PHA9
   8 4MY2    #SKI  K6HALT>699-699
   9 4NCL          TRACE    1,PHA9   
  16 4NX=          STO   6  APEDSP1+2(3)        [CHARACTER COUNT 
  18 4PBW          LDX   5  7                   [SAVE MESSAGE WORD COUNT 
   9 4PWG          LDX   7  AWORK1(2)
  15 4QB6          STO   7  APEDSP1(3)          [MEMBER NO.  
  19 4QTQ          LDN   4  APEDHALT            [CONTROL VALUE FOR IF COMMAND
  10 4R*B          STO   4  APEDSP1+1(3) 
   5 4RT2    #SKI  G4
  21 4S#L          ADN   5  9                   [NO. OF WDS. FOR PARAMS. +1 WORD EACH
   5 4SS=    #SKI  G3
  20 4T?W          ADN   5  5                   [2-WORD TIME + 1 WORD EACH PARAM.
   8 4TRG          BZE   7  PHA91
  15 4W?6          ADN   5  3                   [3-WORD PARAM
  19 4WQQ    PHA91 OUTBLOCK 5                   [GET MONITORING FILE BLOCK   
  14 4X=B          OUTMILL                      [TIME TAG
   5 4XQ2    #SKI  G4
   4 4Y9L    (   
   7 4YP=    #SKI  JNLPACK   
   4 4_8W    (   
   9 4_NG          LDX   4  AWORK3(2)
   9 5286          LDX   5  AWORK4(2)
  15 52MQ          OUTPACKC 4,1,QUOTA           [OUTPUT QUOTA
  16 537B          OUTPACKC 5,1,PAGETURN        [OUTPUT PAGETURNS
   4 53M2    )   
   8 546L    #SKI  JNLPACK<1$1   
   4 54L=    (   
  15 555W          OUTNUM   AWORK3(2),0         [OUTPUT QUOTA
  16 55KG          OUTNUM   AWORK4(2),0         [OUTPUT PAGETURNS
   4 5656    )   
   4 56JQ    )   
   8 574B          BZE   7  PHA92
   8 57J2          LDX   3  GSIGN
   7 583L          ADX   3  2
  21 58H=          DCH   7  ACOMMUNE6(3)        [APPEND MEMBER NO. TO CHAR. STRING   
  19 592W          OUTPARN  11,ACOMMUNE4        [PREPARE PARAM FOR MEMBER NO.
   8 59GG          BRN      PHA93
  19 5=26    PHA92 OUTNULL                      [MEMBER NO. NOT APPLICABLE   
  14 5=FQ    PHA93 OUTPARAM 6,APEDSP1+XBLEN,APED,ANEVENT 
   5 5=_B    PHA10   
   8 5?F2    #SKI  K6HALT>699-699
   9 5?YL          TRACE    1,PHA10  
   5 5#D=    #SKI  G4
   9 5#XW          MONOUT   HUSH2G4  
   5 5*CG    #SKI  G3
  16 5*X6          MONOUT   HUSH2               [SHOOT IT AWAY   
  21 5BBQ    #              PRODUCES - TIME:HALTED IN MEMBER N: MESSAGE        (N>0) 
  21 5BWB    #                    OR - TIME:HALTED  : MESSAGE                  (N=0) 
   5 5CB2    PHA11   
   8 5CTL    #SKI  K6HALT>699-699
   9 5D*=          TRACE    1,PHA11  
  19 5DSW          ENDCOM                       [RETURN TO COMMAND PROCESSOR 
  10 5F#G          MENDAREA 20,K100HALT  
   4 5FS6    #END
   8 ____ ...21014551000100000000
