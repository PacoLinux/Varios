   5 22FL    #SKI  G3
   4 22_=    (   
  11 23DW    #OPT  K0LOADER=K0KERNEL>K0ALLGEO
   7 23YG    #LIS  K0LOADER  
  15 24D6 ...#SEG  LOADERII86                   [M. DRUKKER  
  18 24GP ...#  ######################################################## 
   4 24K# ...#   
  16 24MX ...#  (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982   
   4 24QG ...#   
  18 24T5 ...#  #########################################################
   4 24XQ    #   
  15 25CB    #              NEW INTERFACE VERSION FOR G3 ONLY
  20 25X2    #              HAVING LOADED GEORGES FIXED CORE, THIS PART OF LOADER
  20 26BL    #              PROCEEDS TO SET UP PM LINK, FORM THE FREE CORE CHAIN,
  21 26W=    #              THE TRACE BUFFER, CREATE EMS ACTIVITY AND THE BSA WHICH  
  13 27*W    #              READS CHAPTERS INTO CORE.
   4 27TG    #   
   6 28*6    K99LOADERII 
  14 28SQ    #GAP  20                           [MENDS   
   4 29#B    #   
  20 29S2    TOPEND         +0                  [POINTER TO END OF VARIABLE CORE 
  20 2=?L    ZINTBS         +0                  [INTERNAL NO. OF GEORGE DEVICE   
   5 2=R=    XECWORDS
   9 2?=W                   +KTAB+1  
   9 2?QG                   +G3MARK  
   9 2#=6 ...               +KTAB+1  
   4 2#PQ    #   
  10 2*9B    #     VARIOUS BLOCK TYPES   
   4 2*P2    #   
   5 2B8L    TYPEA   
   6 2BN=    #HAL  BSA,0 
   5 2C7W    TYPEB   
   8 2CMG    #HAL  BSA+BTFAIL,0  
   5 2D76    TYPEC   
   8 2DLQ    #HAL  CONSA+CNTRAL,0
   5 2F6B    SLINK6  
  15 2FL2          PAIR     TRANFAIL,1          [LINK TO TFA 
   5 2G5L    SLINK7  
   9 2GK=          PAIR     EMSENG,1 
  19 2H4W    #     TABLE OF BEATTIMES AND CHAPTIMES OF CENTRAL PROCESSORS.   
  20 2HJG    #     PROCESSOR TYPE GIVEN BY THE GIVE/6 EXTRACODE.X WILL CONTAIN   
  16 2J46    #     A NUMBER CORRESPONDING TO THE PROCESSOR TYPE. 
   4 2JHQ    #   
   5 2K3B    JTCPU   
   8 2KH2    TCPU           +0   
  14 2L2L    #HAL  555,1320                     [ 1 1904 
  14 2LG=    #HAL  555,1320                     [ 2 1905 
  14 2L_W    #HAL  182,1320                     [ 3 1909 
  18 2MFG    #HAL  769,880                      [ 4 1906 STORE CYCLE 1.1 
  18 2M_6    #HAL  588,880                      [ 5 1906 STORE CYCLE 2.1 
  18 2NDQ    #HAL  769,880                      [ 6 1907 STORE CYCLE 1.1 
  18 2NYB    #HAL  588,880                      [ 7 1907 STORE CYCLE 2.1 
  14 2PD2    #HAL  714,1320                     [ 8 1904E
  21 2PXL    #HAL  909,1320                     [ 9 1904E WITH HARDWARE ACCUMULATORS 
  14 2QC=    #HAL  714,1320                     [10 1905E
  21 2QWW    #HAL  909,1320                     [11 1905E WITH HARDWARE ACCUMULATORS 
  14 2RBG    #HAL  667,1320                     [12 1906E
  21 2RW6    #HAL  833,1320                     [13 1906E WITH HARDWARE ACCUMULATORS 
  14 2S*Q    #HAL  667,1320                     [14 1907E
  21 2STB    #HAL  833,1320                     [15 1907E WITH HARDWARE ACCUMULATORS 
  14 2T*2    #HAL 1111,1320                     [16 1904F
  14 2TSL    #HAL 1111,1320                     [17 1905F
  14 2W#=    #HAL  909,1320                     [18 1906F
  14 2WRW    #HAL  909,1320                     [19 1907F
  14 2X?G    #HAL 1000,1750                     [20 1903A
  14 2XR6    #HAL 2000,1900                     [21 1904A
  14 2Y=Q    #HAL 1200,350                      [22 1906A
  14 2YQB    #HAL  500,1750                     [23 1902S
  14 2_=2    #HAL 1000,1750                     [24 1903S
  14 2_PL    #HAL 2600,1900                     [25 1904S
  14 329=    #HAL 1700,350                      [26 1906S
  16 32NW    #HAL 2000,1900                     [27 1904S DUAL   
  16 338G    #HAL 1600,1900                     [28 1904A DUAL   
  14 33N6    #HAL 1500,1900                     [29 1903T
  14 33XY ...#HAL  875,1750                     [30 1902T
  17 342H ...#HAL 1000,750                      [31 2960/DME REL. 2  
  17 3456 ...#HAL 1000,580                      [32 2960/DME REL. 3  
   5 347Q    TCPUEND 
   5 34MB    #GAP  5 
   6 3572    K1LOADERII  
  10 36KW          LINT     GBSDL(2),0   
   9 375G          STO   0  ZINTBS(2)
  18 37=N ...      PUDDL    5                   [MOVE DEVICE LIST DOWN   
  19 37CW ...      STO   5  TOPEND(2)           [STORE END OF VARIABLE CORE  
   9 37J3 ...      JENVNOT  XDLY2,DME
  16 37N8 ...      NGN   4  1                   [DELAY FOR DME   
   8 37S* ...XDLY1 BCT   4  XDLY1
  15 37X* ...#    TO USE FINDPEREC WITH APINT AND A CONSOLE  
  14 382* ...#   WE MUST USE %G AS A  CONSOLE WILL APPEAR
  14 385* ...#    AS ANONYMOUS AS IT HAS A UNIT NO. OF 0 
  21 388* ...XDLY2 FINDPEREC 3,APINT,CONDL(2),,,,XFIND   [CONVERT CONSOLE INTERNAL NO
  18 38?* ...      BPZ   3   XFIND              [JUST IN CASE ITS GONE!  
  10 38B* ...      GEOERR      1,NOCON?  
  16 38F* ...XFIND STO   3  CONDL(2)            [DEVICE POINTER  
  16 38JB          LDX   4  FCORES              [UPDATED BY PUDDL
  16 3942 ...      ADX   4  CIRNDA            [ROUND UP TO NEXT  
  16 39HL          ANDX  4  IROUNDNG            [MULTIPLE OF 4   
  18 3=3=          STO   4  FCORES              [MOVABLE CORE AND STORE  
  14 3=GW          LDN   3  BFREE               [SET UP  
  15 3?2G          MOVE  3  2                   [FREE CORE   
   7 3?G6          LDN   1  1
   7 3?_Q          SMO      4
   8 3#FB          STO   1  AFLAG
  14 3*DL          STO   4  BFREE               [CHAIN   
   9 3*Y=          STO   4  BFREE+1  
  17 3BCW          LDX   1  TOPEND(2)           [TOP OF VARIABLE CORE
  21 3BXG          STO   1  JTRATOP             [SET ADDRESS OF TOP OF TRACE BUFFER  
  18 3CC6          SBX   1  JTRALEN             [LENGTH OF TRACE BUFFER  
  19 3CWQ          STO   1  JTRABUFF            [SET ADDRESS OF TRACE BUFFER 
  17 3DBB          STO   1  JTRAPNTR            [SET TRACE POINTER   
  18 3DW2          SBX   1  GSCANLEN            [LENGTH OF GSCAN BUFFER  
  19 3F*L          STO   1  GSCANBUFF           [SET ADDRESS OF GSCAN BUFFER 
  17 3FT=          STO   1  GSCANPNTR           [SET GSCAN POINTER   
   5 3G#W    JLMEAS  
  19 3GSG          BRN      JLNOMEAS            [THIS IS NULLED TO INCLUDE   
  17 3H#6                                       [MEASUREMENT BUFFERS 
  19 3HRQ          SBN   1  512                 [SET UP TWO BLOCKS FOR THE   
  17 3J?B ...JLMEASA                            [MEASUREMENT PACKAGE 
  21 3JR2 ...      STO   1  0                   [MENDED WITH ADDR OF 1ST BUFFER PTR  
   8 3K=L          SBN   1  512  
   5 3KQ=    JLMEASB 
  21 3L9W ...      STO   1  0                   [MENDED WITH ADDR OF 2ND BUFFER PTR  
   5 3LPG    JLNOMEAS
  17 3M96          SBX   1  4                   [TAKE OFF FIXED CORE 
  17 3MNQ          STO   1  AFREE               [TOTAL MOVABLE CORE  
   7 3N8B          SMO      4
  18 3NN2          STO   1  ASIZE               [INSERT SIZE OF FC BLOCK 
  17 3P7L          STO   1  CFREE               [TOTAL FREE AVAILABLE
  20 3P## ...      LDX   1  BFREE                [ ADDED FOT ICT AND CAS IN MK865
   9 3PF2 ...      CALL  0  (GFRIN)  
   4 3PM=    #   
  20 3Q6W    #     SET UP BSTB/CCTABLE TO HOLD INFORMATION ABOUT THE GEORGE FILE 
   4 3QLG    #   
  15 3R66    #     WD 0     B0-B5   DEVICE TYPE             *
  17 3RKQ    #     WD 1     B0-B23  DATUM OF GEORGE FILE    *  DRUMS 
  15 3S5B    #     WD 2     B0-B23  LENGTH OF GEORGE FILE   *
   4 3SK2    #   
  17 3T4L    #     WD 0     B0-B5   DEVICE TYPE B18-B23 SUBTYPE   *  
  17 3TJ=    #     WD 1     B0-B23  ABSOLUTE STARTING CYLINDER    *  
  19 3W3W    #     WD 2     B0-B23  STARTING SEGMENT              *  DISCS   
  17 3WHG    #     WD 3     B0-B23  NUMBER OF CYLINDERS           *  
  17 3X36    #     WD 4     B0-B23  NUMBER OF SEGMENTS            *  
  17 3XGQ    #     WD 5     B0-B23  SERIAL NUMBER                 *  
   4 3Y2B    #   
   4 3YG2    #   
   8 3Y_L          LDN   5  12   
  15 3_F=          LDX   6  BLTYPE(2)           [DEVICE TYPE 
   7 3_YW          SBN   6  9
  16 42DG          BZE   6  N5                  [J IF SLOW DRUM  
   8 42Y6          SBN   6  16   
  16 43CQ    N5    CALL  7  N90                 [CORE FOR CCTABLE
   8 43XB          LDX   2  BACT 
   8 44C2          STO   2  ACCTP
  11 44WL          NAME     2,BSTB,CCTABLE   
  19 45B=          CHAIN    2,BINDEX+1          [CHAIN CCTABLE TO INDEX CHAIN
   9 45TW          LDX   2  BINDEX   
   8 46*G          LDX   1  G3DOL
   8 46T6          BZE   6  N6   
  18 47#Q          LDCT  0  #60                 [B0-B5 =6 FOR ALL DISCS  
   8 47SB          STO   0  A1(2)
  16 48#2          LDX   0  BLSUB(1)            [DEVICE SUBTYPE  
  15 48RL          ORS   0  A1(2)               [TO CCTABLE  
  10 49?=          LDN   4  BLDATUM(1)   
   9 49QW          LDN   5  A1+1(2)  
   7 4==G          MOVE  4  4
  10 4=Q6          LDX   0  BLFAC+3(1)   
  15 4?9Q          STO   0  A1+5(2)             [SERIAL NO.  
   8 4?PB          BRN      N8   
   9 4#92    N6    LDX   4  BLDRUM(1)
  15 4#NL          LDX   0  BLTYPE(1)           [DEVICE TYPE 
   8 4*8=          SBN   0  25   
  16 4*MW          BNZ   0  NX1                 [J IF SLOW DRUM  
  14 4B7G          SRL   45 12                  [X4=DATUM
  15 4BM6          SRL   5  12                  [X5=LENGTH   
   8 4C6Q          BRN      NX2  
   8 4CLB    NX1   SRL   45 10   
   8 4D62          ANDN  4  #3777
  17 4DKL          SRL   5  14                  [CLEAR LS PART OF X5 
  14 4F5=          SLL   4  10                  [DATUM   
  18 4FJW          SLL   5  10                  [AND LENGTH FOR SLOW DRUM
   9 4G4G    NX2   LDX   0  BLTYPE(1)
   8 4GJ6          SLL   0  18   
  21 4H3Q          STO   0  A1(2)               [DEVICE TYPE TO B0-B5 OF A1 OF CCTABL
  14 4HHB          STO   4  A1+1(2)             [DATUM   
  18 4J32          STO   5  A1+2(2)             [AND LENGTH OF DRUM FILE 
   8 4JGL          BRN      N9   
   4 4K2=    #   
  16 4KFW    #     SET UP EWDAS/EXOF BLOCK FOR THE GEORGE DEVICE 
   4 4K_G    #   
  16 4LF6    N8    LDN   5  FDCELLS-A1          [LENGTH OF EXOF  
  17 4LYQ          CALL  7  N90                 [CORE FOR EXOF BLOCK 
   8 4MDB          LDX   2  G3DOL
   8 4MY2          LDX   1  BACT 
  10 4NCL          NAME     1,EWDAS,EXOF 
   9 4NX=          STOZ     BACK1(1) 
   7 4PBW          LDN   0  1
  16 4PWG          STO   0  BACK2(1)            [EXOFILE NUMBER  
  16 4QB6          STO   0  COPEN(1)            [COUNT OF OPENS  
   9 4QTQ          STO   0  FBUK(1)  
   8 4R*B          LDCT  0  #500 
  20 4RT2          STO   0  EXMARK(1)           [OPEN FOR WRITING TO THE SYSTEM  
  21 4S#L          LDX   0  BLADDR(2)           [WD ADD OF FIP CELL ON BACKING STORE 
   9 4SS=          STO   0  HOME(1)  
  14 4T?W          LDX   0  BLFAC+3(2)          [GET SUSN
   9 4TRG          STO   0  ASUSN(1) 
  10 4W?6          LDN   4  BLDISC+2(2)  
   9 4WQQ          LDN   5  FIP(1)   
  18 4X=B          MOVE  4  15                  [FILE INDEX AND PARAMS   
  15 4XQ2          CHAIN    1,BCCB              [PUT BLOCK IN
   4 4Y9L    N9  
   4 4YP=    #   
   4 4_8W    #   
  12 4_NG    #     SET UP OPS CONSOLE ACTIVITY   
   4 5286    #   
  19 52MQ          LDN   5  CONSALEN            [LENGTH OF CONSOLE ACTIVITY  
   8 537B          LDX   3  G3DOL
  15 53M2          LDX   6  TYPEC(3)            [TYPE/SUBTYPE
  17 546L          CALL  7  N90A                [GETCORE AT TOP END  
  16 54L=          LDX   2  BPTR(2)             [PTR TO GOT BLOCK
  15 555W          LINKSET  2,SLINK7(3)         [INSERT LINK 
   9 55KG          LDX   0  CONDL(3) 
  18 5656          STO   0  CPPTR(2)            [PTR TO DL INTO ACTIVITY 
  19 56JQ          STO   0  JCTWPTR             [& INTO PSEUDO PM ACTIVITY   
  18 574B          FWAIT    #50                 [SET WAITING FOR CONSOLE 
   4 5752 ...#   
   7 575L ...#UNS  ENHOPER   
   4 576= ...(   
   4 576W ...#   
  12 577G ...#     SET UP CONBUFF/CNTRL BLOCK ON 
  15 5786 ...#     ON MISCELLANEOUS CHAIN IF RUNNING IN DME  
   4 578Q ...#   
  10 579B ...      JENVNOT  NOTDME,DME   
   9 57=2 ...      LDN   5  CNTRLEN  
   8 57=L ...      CALL  7  N90  
   9 57?= ...      LDX   3  FPTR(2)  
  17 57?W ...      STO   3  CONSCNTRL           [SET UP POINTER TO IT
  11 57#G ...      NAME     3,CONBUFF,CNTRL  
  18 57*6 ...      LDN   0  0                   [INITIALISE THE BLOCK,   
   9 57*Q ...      STO   0  COIN(3)  
   9 57BB ...      LDN   5  COIN(3)  
  19 57C2 ...      LDN   6  COIN+1(3)           [ALL BEING ZERO EXCEPT ...   
  19 57CL ...      MOVE  5  CNTRLEN-1           [1ST BLOCKLET TO BE USED ... 
  19 57D= ...      LDN   0  1                   [IN RECALL FILE, WHICH IS 1  
   9 57DW ...      STO   0  COBLK(3) 
   9 57FG ...      CHAIN    3,BMISC+1
   5 57G6 ...NOTDME  
   4 57GQ ...)   
   4 57HB ...#   
   4 57J2    #   
   4 583L    #   
   4 58H=    #   
  15 592W    #     CREATE ACT BLK FOR TRANSFER FAIL ACTIVITY 
   8 59GG          LDX   5  BSABL
  16 5=26          LDX   3  G3DOL               [LOADER'S BASE   
  19 5=FQ          LDX   6  TYPEB(3)            [TRANSFER FAIL ACTIVITY TYPE 
   8 5=_B          CALL  7  N90A 
   9 5?F2          LDX   1  BPTR(2)  
  17 5?YL          STO   1  ATFAPTR             [REMEMBER IN FIXED   
  15 5#D=          LINKSET  1,SLINK6(3)         [INSERT LINK 
   8 5#XW          LDCT  0  #200 
  14 5*CG          STO   0  CLONG1(1)           [PRIV BIT
   9 5*X6          LDN   5  AFRAILLEN
  17 5BBQ          GETEMSCR 5                   [CORE FOR FAIL LIST  
   8 5BWB          LDX   3  BACT 
  10 5CB2          NAME     3,BSTB,BFRAIL
  16 5CTL          LDN   6  A1(3)               [CLEAR OUT BLOCK 
   9 5D*=          LDN   7  A1+1(3)  
   8 5DSW          STOZ     A1(3)
  10 5F#G          MOVE  6  AFRAILLEN-1  
   7 5FS6          NGN   7  1
   9 5G?Q          STO   7  A1+1(3)  
  16 5GRB          CHAIN    3,ATFAPTR           [CHAIN BEHIND TFA
   4 5H?2    #   
   4 5HQL    #   
  15 5J==    #     CREATE ACT BLK FOR BS CONTAINING GEORGE   
   8 5JPW          LDX   5  BSABL
   8 5K9G          LDX   3  G3DOL
   9 5KP6          LDX   6  TYPEA(3) 
   8 5L8Q          CALL  7  N90A 
  15 5LNB          LDX   1  BPTR(2)             [NEW BLOCK   
  21 5M82          STO   1  BBAC                [BASE OF BACKING STORE ACTIVITY BLOCK
   9 5MML          LDX   0  GEOBSPTR 
  14 5N7=          STO   0  CPPTR(1)            [DEVICE  
  15 5NLW          LDX   4  BSUN                [BS UNIT NO  
  17 5P6G          STO   4  BACK2(1)            [GEORGES BSUN TO BSA 
   7 5PL6          LDN   4  1
  14 5Q5Q          ADS   4  BSUN                [UPDATE  
   9 5QKB          LDX   4  BSTSEP   
   6 5R52    #SKI  CLINK 
  17 5RJL          STO   4  ALINK1(1)            [BSTS ENTRY POINT   
   7 5S4=    #SKI  CLINK<1$1 
  16 5SHW          STO   4  ALINK2(1)           [BSTS ENTRY POINT
   9 5T3G          LDX   4  BLSUB(3) 
   8 5TH6          SLL   4  12   
  19 5W2Q          ORX   4  BLTYPE(3)           [SUBTYPE/TYPE FOR G. DEVICE. 
  15 5WGB          STO   4  AWORK1(1)           [STORE IN BSA
  21 5X22          LDX   4  ZINTBS(3)           [INTERNAL NO. FOR BUTTON UP MACRO.   
   8 5XFL          LDN   0  24   
   7 5X_=          DVS   3  0
  21 5YDW          STO   4  BA3(1)              [WORD 0,1 OR 2 OF ENGAGE BUTTON AREA 
   8 5YYG          LDCT  0  #400 
  16 5_D6          SRL   0  0(3)                [BIT WITHIN WORD 
  15 5_XQ          STO   0  BA2(1)              [STORE IN BSA
   9 62CB          LDN   5  AFAILLEN 
  18 62X2          GETEMSCR 5                   [GETCORE FOR FAIL LISTS  
   8 63BL          LDX   2  BACT 
  10 63W=          NAME     2,BSTB,BFAIL 
   8 64*W          LDN   6  A1(2)
   9 64TG          LDN   7  A1+1(2)  
   8 65*6          STOZ     A1(2)
  16 65SQ          MOVE  6  AFAILLEN-1          [CLEAR OUT BLOCK 
   7 66#B          NGN   7  1
   9 66S2          STO   7  A1+1(2)  
  17 67?L          CHAIN    2,BBAC              [AND CHAIN BEHIND BSA
   4 67#= ...#   
   7 67#W ...#UNS  ENHOPER   
   4 67*G ...(   
  17 67B6 ...#     SET UP A GMON/JRNAL BLOCK FOR EACH CONSOLE MESSAGE
  15 67BQ ...#     TO PUT IT ONTO THE JOURNAL CHAIN (FOR DME)
   4 67CB ...#   
  18 67D2 ...      JENVNOT  MSS8,DME            [APPARENTLY ONLY FOR DME 
  18 67DL ...      STO   2  FX2                 [NEEDED FOR FPUT LATER   
   8 67F= ...      LDX   2  G3DOL
   9 67HQ ...      LDX   1  OPMESS(2)
  16 67JB ...      LDX   1  0(1)                [MESSAGE BUFFER  
  18 67K2 ...      BZE   1  MSS9                [NO MESSAGES (VIA GELL)  
  15 67KL ...MSS1  LDX   5  0(1)                [COUNT WORD  
  16 67L= ...      BZE   5  MSS9                [NO MORE MESSAGES
  18 67LW ...      ADN   5  6                   [6 MORE WORDS IN BLOCK   
  17 67MG ...      STO   1  FX1                 [GETEMSCR CORRUPTS   
  17 67N6 ...      GETEMSCR 5                   [GET CORE FOR BLOCK  
   8 67NQ ...      LDX   3  BACT 
  10 67PB ...      NAME     3,GMON,JRNAL 
  15 67Q2 ...      STOZ     JWAITER(3)          [AUTONOMOUS  
   7 67QL ...      LDN   0  5
   8 67R= ...      SLL   0  11   
  20 67RW ...      STO   0  JCATS(3)            [SET JL & CL; UNSET PA - UNPACKED
  14 67SG ...      STOZ     JJOBNO(3)           [NO JOB  
   8 67T6 ...      LDN   0  274  
   8 67TQ ...      SLL   0  12   
  15 67WB ...      STO   0  JPACKORG(3)         [MESSAGE NO  
   8 67X2 ...      LDX   1  FX1  
   7 67XL ...      LDX   5  1
  16 67Y= ...      ADN   5  1                   [START OF MESSAGE
  10 67YW ...      LDN   6  JMESSAGE(3)  
   8 67_G ...      SMO      0(1) 
  16 6826 ...      MOVE  5  0                   [COPY IN MESSAGE 
   8 682Q ...      LDX   5  0(1) 
  16 683B ...      SBN   5  1                   [TURN EXCLUSIVE  
  18 6842 ...      SLL   5  2                   [CHAR COUNT FOR GMONTIDY 
  19 684L ...      GMONTIDY 3,5,UNPACKED        [INSERTS RED TAPE & CHECKSUM 
  18 685= ...      CHAIN    3,BSJC+BPTR         [STICK ON JOURNAL CHAIN  
   8 685W ...      LDX   1  FX1  
  18 686G ...      ADX   1  0(1)                [NEXT MESSAGE IN BUFFER  
   8 6876 ...      BRN      MSS1 
   8 687Q ...MSS9  LDX   2  FX2  
   4 688B ...MSS8
   4 6892 ...)   
   5 689L ...GIVE6   
   8 68=W          SMO      G3DOL
   9 68QG          LDN   6  XECWORDS 
   8 69=6          LDN   7  25   
   7 69PQ          MOVE  6  3
   9 6=9B          LDX   3  GIVE6REP 
  10 6=P2          LDN   0  TCPUEND-TCPU 
   7 6?8L          TXL   3  0
  16 6?N=          BCC      PN1                 [GIVE6 REPLY FROM
  17 6#7W          BZE   3  PN1                 [EXEC OUT OF RANGE   
   9 6#MG          LDX   6  BEATTIME 
   9 6*76          LDX   7  CHAPTIME 
   8 6*LQ ...      SMO      G3DOL
  20 6B6B          LDX   4  TCPU(3)             [BEATTIME & CHAPTIME FROM TABLE  
   8 6BL2          SRL   45 12   
   8 6C5L          SRL   5  12   
  19 6CK=          BNZ   6  PN2                 [J IF BEATTIME ALREADY SET   
  17 6D4W          STO   4  BEATTIME            [ELSE STORE DEFAULT  
  17 6DJG    PN2   BNZ   7  PN3                 [J IF CHAPTIME SET   
  16 6F46          STO   5  CHAPTIME            [ELSE SET DEFAULT
  17 6FHQ    PN3   STO   3  GIVE6REP            [STORE REPLY IN FIXED
  21 6G3B                                       [GEORGE TO USE.CURRENTLY GIVE6REP=19 
  14 6GH2          FPUT                         [LIST IT 
  11 6GJ3 ...#  CODE TO CLEAR EBA FOR CONSOLE
   9 6GK4 ...      LDX   2   G3DOL   
   8 6GL5 ...      LDN   5   24  
  10 6GM6 ...      LINT     CONDL(2),7   
   8 6GN7 ...      DVS   6   5   
   7 6GP8 ...      SMO      7
   8 6GQ9 ...      LDX   5   EBA 
   9 6GR= ...      BZE   5   NOBUTTON
   9 6GS? ...      LDX   4   GSIGN   
   7 6GT# ...      SMO      6
   8 6GW* ...      SRL   4   0   
   8 6GXB ...      ANDX  4   5   
   8 6GYC ...      ERS   4   5   
   5 6G_D ...NOBUTTON
  17 6H2L          STOZ     K7                  [ZEROISE MILL TIMER  
  20 6HG=          LDX   5  GEN0                [SET UP EXEC : GEORGE POST-MORTEM
  13 6H_W          STO   5  K0                  [LINK
  18 6JFG          STOZ     GEN0                [AND CLEAR GEN0 AND GEN1 
   8 6J_6          STOZ     GEN1 
  20 6KDQ          SMO      G3DOL               [PICK UP THE REPLY FROM LOADERFS 
  20 6KYB          LDX   0  JLGENRESFLG         [AND TELL BACKMAP WHETHER OR NOT 
  19 6LD2          STO   0  G3DOL               [A GENERAL RESTORE IS NEEDED 
   7 6LG9 ...JICTA         0 
  18 6LJD ...              0                   [GAP  FOR  RINGMEL  MACRO 
   7 6LLM ...               0
   7 6LNW ...               0
   8 6LR5 ...                0   
   8 6LT# ...                0   
  14 6LXL          COOR1X                       [START   
  10 6MC=    PN1   GEOERR   1,GIVE6REP   
   7 6MWW    N90   EMSCORE  5
   8 6N6N ...      BRN      XCASB
  21 6NBG ...#GAP  15                            [GAP IN CASE NEEDED FOR CAS OR ICT I
   9 6NL# ...XCASB      EXIT  7  0   
  16 6NW6    N90A  EMSACTX  6,5                 [ACT BLOCK CORE  
   9 6P5Y ...      BRN       XCASD   
  21 6P*Q ...#GAP  10                            [GAP IN CASE NEEDED FOR CAS OR ICT I
   9 6PKJ ...XCASD      EXIT  7  0   
   6 6PN7 ...K77LOADERII 
   6 6PQQ ...#GAP  250   
   4 6PTB    #END
   4 6Q*2    )   
   6 ____ ...727151210001
