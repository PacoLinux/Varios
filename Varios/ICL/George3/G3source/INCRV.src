  14 22FL ...      SEG   INCRV,850,MIKE PUTNAM,INCDUMP   
   4 22_=    #   
  11 23DW          SEGENTRY K1INCRV,X1INCRV  
   4 23YG    #   
  21 24D6    #  THIS SEGMENT ISSUES RETRIEVES FOR ANY FILES NOT ON-LINE WHICH HAVE TH
  15 24XQ ...#  LAST RELIABLE DUMP ON PROCESSED INCREMENTS . 
  16 25CB ...#  IF ALL TAPES FOR SUCH AN INCREMENT ARE WRONGED IT
  21 26BL    #  LAUNCHES INTO A DIALOGUE WITH THE OPERATORS TO SEE IF THEY ARE EVER G
  21 26W= ...#  TO RIGHT A TAPE CONTAINING THIS INCR.. IF THEY ARE NOT, IT MARKS THE 
  20 27*W    #  INCR. AS OBSOLETE.  ON ENTRY, THERE ARE  FABSNB BLOCKS FOR EACH  
  20 27TG    #  FILE NEEDING RETRIEVING FROM OBSOLETE INCR. (PLUS A COUNT OF SUCH
  12 28*6    #  BLOCKS IN INCRV IN FINC BLOCK).  
   4 28SQ    #   
  12 29#B ...NOTAPES         14HLOSE THE FILES   
  17 29S2 ...QUEST           36HQN 1,WILL A TAPE EVER BE AVAILABLE?  
   4 2=?L    #   
   8 2=R=    #     SUBROUTINES   
   4 2?=W    #   
  21 2?QG    #     TO LOOK AT THE NEXT BLOCK IN THE ACT. CHAIN & CHECK IT IS OF SPECI
  12 2#=6    #     TYPE (GEOERR INCBLOCK IF NOT) 
  21 2#PQ    #     ON ENTRY,X1-> BLOCK & LINK+1 CONTAINS BLOCK TYPE.    X3 IS S/R LIN
  19 2*9B    #     ON EXIT,X1-> NEXT BLOCK.     USES X0.  NO COORDINATION.   
   4 2*P2    #   
   5 2B8L    NEXTBL  
   9 2BN=          LDX   1  FPTR(1)  
   7 2C7W    #SKI  K6INCRV   
   4 2CMG    (   
   9 2D76          LDX   0  ATYPE(1) 
  19 2DLQ          BXU   0  0(3),XWR            [GEOERR IF WRONG BLOCK TYPE  
   4 2F6B    )   
   7 2FL2          EXIT  3  1
   7 2G5L    #SKI  K6INCRV   
  11 2GK=    XWR   GEOERR   BRIEFPM,INCBLOCK 
   4 2H4W    #   
  21 2HJG    #     FINDS THE INCINDEX RECORD WHOSE NUMBER IS IN X5 & THEN STEPWRITES 
  20 2J46    #     ON ENTRY,X5= INCR NO. TO FIND, INCINDEX OPEN IN GENERAL MODE. 
  11 2JHQ    #              X3 IS S/R LINK.  
  21 2K3B    #     ON EXIT, X1=FX1, X3-> RECORD JUST REWRITTEN. USES X0,X2,AWORK3.   
  11 2KH2    #              MAY COORDINATE.  
   4 2L2L    #   
   6 2LG=    STEPWRITE   
   8 2L_W          SBX   3  FX1  
   8 2MFG          SMO      FX2  
  18 2M_6          STO   3  AWORK3              [REMEMBER LINK IN AWORK3 
   4 2NDQ    STEP
   6 2NYB          STEP  
   7 2PD2    #SKI  K6INCRV   
  21 2PXL          BZE   3  SEND                [GEOERR INC NORV IF INCR NOT FOUND   
  20 2QC=          BXU   5  AINCNOR(3),STEP     [J IF NOT REQUIRED INCR RECORD   
   7 2QWW          STEPWRITE 
   9 2RBG          LDX   2  AWORK3(2)
   8 2RW6          ADX   2  FX1  
   7 2S*Q          EXIT  2  0
   4 2STB    #   
   4 2T*2    #   
   5 2TSL    X1INCRV 
   6 2TXQ ...#UNS  FPD   
  11 2W2W ...      MHUNTW   1,FILE,FINCPROC  
   6 2W62 ...#UNS  FPD   
   4 2W96 ...#SKI
  10 2W#=          MHUNTW   1,FILE,FINC  
  21 2WRW          LDX   4  INCRV(1)            [X4= COUNT OF FABSNBS FOR RETRIEVES  
  19 2X?G          BZE   4  NORV                [J IF NO RETRIEVES TO BE DONE
  17 2XH# ...      STOZ     INCRV(1)           [CLEAR COUNT OF RVS   
  20 2XR6          LDN   7  0                   [ZEROISE COUNT OF APROC BLOCKS   
  16 2Y=Q          OPENINC  ,GENERAL            [OPEN INCINDEX   
  16 2YQB          MHUNTW   1,FILE,FABSNB       [X1-> PST FABSNB 
  18 2_=2          LDN   6  1                   [COUNT OF FABSNBS READ =1
   5 2_PL    NEXTINC 
  18 329=          LDX   5  A1(1)               [X5= INCR NO. IN FABSNB  
  19 32NW          CALL  3  STEPWRITE           [FIND & REWRITE INCR RECORD  
  19 338G ...      LDX   2  AMTSR(3)            [NO OF TAPES FOR INCREMENT   
  21 33N6 ...      BZE   2  YTAPES              [SHOULDNT HAPPEN THAT THERE ARE NONE 
  19 347Q ...                                   [BUT IF SO IGNORE INCREMENT. 
  17 34MB ...      LDX   1  3                   [COPY PTR TO RECORD  
   6 3572 ...TAPECHECK   
  19 35LL ...      JMBAC    YTAPES,1,BMOPWRR,BMUSWRR [J IF TAPE NOT WRONGED  
  19 366= ...      ADN   1  INCMAGLEN           [UPDATE PTR TO GIVE NEXT TAPE
  15 36KW ...      BCT   2  TAPECHECK           [LOOP IF MORE
  19 38JB          ADN   7  1                   [UPDATE COUNT OF APROC BLOCKS
   7 3942          READAGAIN 
  10 39HL          MHUNTW   1,FILE,FRB   
  21 3=3=          NAME     1,FILE,APROC        [PRESERVE INCR RECORD AS PROC BLOCK  
  11 3=GW          MHUNTW   2,FILE,FINCPROC  
  21 3?2G          CHAIN    1,BPTR(2)           [CHAIN APROC BEFORE FINCPROC BLOCK   
   5 3?G6 ...YTAPES  
  16 3?_Q          MHUNTW   1,FILE,FABSNB       [X1-> 1ST FAB SNB
  21 3#FB          LDN   2  1                   [INITIALISE TEMP. COUNT OF FABSNBS RE
  20 3#_2    OLDFAB                             [FIND FABSNB WHICH HAD REACHED   
  19 3*DL          BXE   2  6,NEWFAB            [J IF REACHED REQUIRED FABSNB
   7 3*Y=          ADN   2  1
  18 3BCW          CALL  3  NEXTBL              [X1-> NEXT BLOCK(FABSNB) 
   8 3BXG    #HAL +FILE+FABSNB,0 
   9 3CC6          BRN      OLDFAB   
   5 3CWQ    NEWFAB  
  20 3DBB          BXE   6  4,NOFAB             [J IF NO MORE FABSNBS TO LOOK AT 
  21 3DW2          ADN   6  1                   [UPDATE COUNT OF FABSNBS LOOKED AT   
  18 3F*L          CALL  3  NEXTBL              [X1-> NEXT BLOCK(FABSNB) 
   8 3FT=    #HAL +FILE+FABSNB,0 
   7 3G#W          LDX   0  5
   8 3GSG          SBX   0  A1(1)
  18 3H#6          BPZ   0  NEWFAB              [J IF INCR NO. NOT HIGHER
   9 3HRQ          BRN      NEXTINC  
   5 3J?B    NOFAB   
   7 3JR2          CLOSETOP  
   4 3K=L    #   
   4 3KQ=    #   
  21 3L9W          NGN   5  1                   [INITIALISE INCR NO. IN NEXT APROC BL
  19 3LPG          BZE   7  NXTFAB              [J IF NO APROC BLOCKS PRESENT
  10 3M96          MHUNTW   2,FILE,APROC 
  20 3MNQ          LDX   5  AINCNO(2)           [X5= INCR NO. IN 1ST APROC BLOCK 
   5 3N8B    NXTFAB  
  10 3NN2          MHUNTW   1,FILE,FABSNB
   8 3P7L          LDX   0  A1(1)
  21 3PM=          BXL   0  5,NOQUEST           [J IF CAN ISSUE THIS RETRIEVE WITHOUT
  17 3Q6W                                       [ ASKING QUESTIONS   
   4 3QLG    #   
   4 3R66    #   
  11 3RKQ          SETNCORE 2,3,FLIB,FLINC   
   7 3S5B          LDN   0  1
  18 3SK2          STO   0  A1(3)               [INITIALISED FLIB/FLINC  
   5 3T4L    NEWINC  
  10 3TJ=          LDX   0  ALOGLEN(3)   
   7 3W3W          ADN   0  1
  10 3WHG          STO   0  ACOMMUNE1(2) 
  21 3X36          ALTLEN   3,ACOMMUNE1(2),FLIB,FLINC  [LENGTHEN FLIB/FLINC BY 1 WORD
  10 3XGQ          MHUNTW   1,FLIB,FLINC 
   7 3Y2B          LDN   0  1
  17 3YG2          ADS   0  A1(1)               [UPDATE REC HEADER   
  10 3Y_L          SMO      ALOGLEN(1)   
  20 3_F=          STO   5  A1-1(1)             [INSERT INCR NO. INTO FLIB/FLINC 
  20 3_YW          SBN   7  1                   [DECREASE COUNT OF APROC BLOCKS  
  19 42DG          BZE   7  MNOPROC             [J IF NO MORE APROC BLOCKS   
  10 42Y6          MHUNTW   1,FILE,APROC 
  16 43CQ          LDX   2  1                   [X2-> 1ST APROC  
  16 43XB          CALL  3  NEXTBL              [X1-> 2ND APROC  
   8 44C2    #HAL  +FILE+APROC,0 
  20 44WL          LDX   5  AINCNO(1)           [X5= INCR NO. IN NEXT APROC BLOCK
  19 45B=          LDX   6  AMTS(2)             [X6= NO. OF MTS IN 1ST APROC 
  21 45TW          BXU   6  AMTS(1),MDIFF       [J IF NO. OF MTS IN THE 2 RECORDS DIF
   4 46*G    M2ND
  18 46T6          LDX   3  AMAG(1)             [X3= NEXT MT IN 2ND APROC
  16 47#Q          LDX   0  AMTS(2)             [X0= NO. OF MTS  
   4 47SB    M1ST
  19 48#2          BXE   3  AMAG(2),MFOU        [J IF FOUND MT IN 1ST APROC  
  16 48RL          ADN   2  INCMAGLEN           [UPDATE POINTER  
  18 49?=          BCT   0  M1ST                [J IF MORE MTS TO SEARCH 
  19 49QW          BRN      MDIFF               [1ST & 2ND APROCS DIFFERENT  
   4 4==G    MFOU
  16 4=Q6          MHUNTW  2,FILE,APROC        [X2-> 1ST APROC   
  19 4?9Q          ADN   1  INCMAGLEN           [UPDATE POINTER IN 1ST APROC 
  18 4?PB          BCT   6  M2ND                [J IF MORE MTS TO COMPARE
  18 4#92          MFREEW   FILE,APROC          [APROCS CONTAIN SAME MTS 
  10 4#NL          MHUNTW   3,FLIB,FLINC 
   9 4*8=          BRN      NEWINC   
   5 4*MW    MNOPROC 
  17 4B7G          NGN   5  1                   [NO MORE APROC BLOCKS
   5 4BM6    MDIFF   
  21 4C6Q          OUTINCS                      [OUTPUT AS PARAMETER INCR NOS. IN FLI
  21 4CLB          OUTTSNS                      [OUTPUT AS PARAM, TSNS IN 1ST APROC  
  18 4D62 ...      MONOUT   INCUSEFUL           [TELL OPS INCS AND TAPES 
   4 4DLS ...#   
  14 4DN2 ...[ ******************************************
  13 4DP8 ...[  CSS SPECIAL TO INHIBIT THE QUESTION  
  15 4DQB ...[  TO PREVENT THEIR LOW QUALITY OPERATORS FROM  
  11 4DRJ ...[  ANSWERING NO UNNECESSARILY   
   4 4DSQ ...[   
   9 4DTY ...      SEGENTRY K80INCRV 
  18 4DX6 ...      NULL                         [MACRO REPLACES THIS WITH
  15 4DY# ...                                   [BRN K81INCRV
   4 4D_G ...#   
  15 4F2N ...[  __________________________________________   
   4 4F3W ...#   
  21 4FHG ...      MONOUT   INCTAPESWR          [WARN OPS OF SIGNIFIANCE OF QUESTION 
  20 4G36 ...      SETNCORE 9+CPDATA-A1,2,ADATA,CREADL  [DATA BLOCK FOR ISSUCOM  
   8 4GGQ ...      LDN   0  36   
  16 4LF6          STO   0  A1(2)               [SET CHAR. COUNT 
  18 4LYQ          STOZ     A1+1(2)             [CLEAR 2ND HEADER WORD   
   8 4MDB          ADN   1  QUEST
   9 4MY2          ADN   2  CPDATA   
  21 4NCL ...      MOVE  1  9                   [MOVE QUESTION TO ADATA/CREADL BLOCK 
  21 4NX=          ISSUCOM  QUESTERR            [ASK OPS: ^WILL A TAPE BE AVAILABLE?^
  19 4PBW          MHUNT 2,APED,ADISPLAY        [X2-> BLOCK WITH ANSWER IN   
  10 4PWG          LDX   0  APEDDISM(2)  
   8 4QB6          BZE   0  WRANS
  18 4QTQ          LDCH  6  APEDDISM+1(2)       [X6= 1ST CHAR. OF REPLY  
   8 4R*B          SBN   6  #71  
  18 4RT2          BZE   6  REPYES              [J IF ANSWER ^Y........^ 
   8 4R_= ...      SBN   0  14   
  21 4S5G ...      BNG   0  WRANS               [J IF NOT ENOUGH CHARS TO BE CORRECT 
  17 4S9Q ...      LDN   6  14                  [NO OF CHARS TO CHECK
   5 4SB2 ...TESTCHAR
  18 4SG= ...      LDCH  0  APEDDISM+1(2)       [PICK UP CHAR OF REPLY   
  18 4SLG ...      LDCH  3  NOTAPES(1)          [CHAR OF 'LOSE THE FILES'
  17 4SQQ ...      BXU   0  3,WRANS             [ERROR IF NOT SAME   
  20 4SX2 ...      BCHX  2  /                   [UPDATE MODS AND TEST NEXT CHAR  
  16 4T3= ...      BCHX  1  /                   [IF THERE IS ONE 
   9 4T7G ...      BCT   6  TESTCHAR 
  21 4T?W          OUTINCS                      [OUTPUT AS PARAM. LIST OF INCR NOS.  
  16 4TRG                                       [   IN FLIB/FLINC
  20 4W?6          STO   5  AWORK2(2)           [REMEMBER INCR NO. IN NEXT APROC 
  20 4WQQ          OUTTSNS                      [OUTPUT AS PARAM. LIST OF TSNS   
  16 4X=B                                       [   IN FILE/APROC
  21 4XQ2          MONOUT   INCOBSMT2           [^INCREMENT%A %B OBSOLETE ON MT%C %D^
   9 4Y9L          OPENINC  ,GENERAL 
  21 4YP=          LDN   6  0                   [INITIALISE POINTER DOWN FLIB/FLINC  
   9 4_8W          BRN      NXTINC   
   5 4_NG    NFIND   
   7 5286          SMO      6
  17 52MQ          LDX   5  A1+1(2)             [X5= NEXT INCR NO.   
  19 537B          CALL  3  STEPWRITE           [LOCATE & REWRITE INCRRECORD 
  18 53M2          BS    3,BIOBSR               [SET ^INCR ONSOLETE^ BIT 
  21 546L          BC    3,BIPRNOBSR            [REMOVE ^PROC BUT NOT OBSOLETE^ BIT  
  10 54L=          MHUNTW   1,FILE,FABSNB
  17 555W          LDX   2  4                   [X2= NO. OF FABSNBS  
   4 55KG    TINC
   8 5656          LDX   0  A1(1)
   7 56JQ          SBX   0  5
  19 574B          BZE   0  RFRFAB              [J IF FAB NB FOR THIS INCR   
  21 57J2          BPZ   0  NXTINC              [J IF NO MORE FABSNBS FOR THIS INCR  
   5 583L    TNXTFAB 
   7 58H=          SBN   2  1
  17 592W          BZE   2  NXTINC              [J IF NO MORE FABSNBS
  16 59GG          CALL  3  NEXTBL              [X1-> NEXT FABSNB
   8 5=26    #HAL +FILE+FABSNB,0 
   8 5=FQ          BRN      TINC 
   5 5=_B    NXTINC  
  10 5?F2          MHUNTW   2,FLIB,FLINC 
   7 5?YL          ADN   6  1
  20 5#D=          BXU   6  A1(2),NFIND         [J IF MORE INCR RECS TO UPDATE   
   7 5#XW          CLOSETOP  
  19 5*CG          LDX   5  AWORK2(2)           [X5=INCR NO. IN NEXT APROC   
   8 5*X6          BRN     RFREE 
   5 5BBQ    WRANS   
  11 5BLJ ...      MHUNT    2,APED,ADISPLAY  
  21 5BWB          OUTPARAM APEDDISM(2),APEDDISM+1,APED,ADISPLAY  [SET ^REPLY^ AS PAR
  10 5CB2          MFREEW   APED,ADISPLAY
  19 5CTL          MONOUT   ANOTVAL             [^%A IS NOT A VALID ANSWER^  
   8 5D9* ...      BRN      MDIFF
   9 5DK4 ...      SEGENTRY  K81INCRV
  17 5DSW    REPYES                             [ANSWER ^YES^ (X6=0) 
  16 5F#G          MONOUT   INCMTAV             [TO OPS CONSOLE =
  21 5FS6                                       [   ^MAKE TAPE AVAILABLE NOW PLEASE^ 
   5 5G?Q    RFREE   
  10 5GRB          MFREEW   FLIB,FLINC   
  10 5H?2          MFREEW   FILE,APROC   
   8 5HQL          BRN      TMORE
   5 5J==    RFRFAB  
  18 5JPW          LDX   3  BPTR(1)             [PRESERVE FABSNB POINTER 
  20 5K9G          SMO      FX2                 [   & COUNT OF FABSNBS LOOKED AT 
   9 5KP6          STO   2  ACOMMUNE1
  15 5L8Q          FREECORE 1                   [FREE FABSNB 
  10 5LNB          LDX   2  ACOMMUNE1(2) 
   7 5M82          LDX   1  3
  19 5MML          SBN   4  1                   [DECREASE COUNT OF FABSNBS   
   9 5N7=          BRN      TNXTFAB  
   4 5NLW    #   
   4 5P6G    #   
   5 5PL6    NOQUEST 
   8 5Q5Q          LDN   0  10   
  19 5QKB          STO   0  A1(1)               [RESTORE FABSNB REC HEADER   
  16 5R52          RV                           [RETRIEVE FILE   
  18 5RJL          SETNCORE CPDATA-A1+10,2,ADATA,CREADL [BLOCK FOR UNNORM
  17 5S4=          STOZ     A1(2)               [ZEROISE CHAR. COUNT 
  18 5SHW          STOZ     A1+1(2)             [CLEAR 2ND RED-TAPE WORD 
   6 5T3G          UNNORM
  11 5TH6          MHUNTW   2,ADATA,CREADL   
  13 5W2Q          OUTPARAM A1(2),CPDATA,ADATA,CREADL
  21 5WGB          MONOUT   BEREST              [O MON FILE ^%A IS BEING RETRIEVED^  
  10 5X22          MFREEW   ADATA,CREADL 
  10 5XFL          MFREEW   FILE,FABSNB  
  19 5X_=          SBN   4  1                   [DECREASE COUNT OF FABSNBS   
   5 5YDW    TMORE   
  17 5YYG          BNZ   4  NXTFAB              [J IF MORE FABSNBS   
   7 5_D6    #SKI  K6INCRV   
  20 5_XQ          BNZ   7  SEND                [GEOERR IF APROC BLOCKS REMAIN   
   4 62CB    NORV
   5 62X2          UP
   4 63BL    #   
   5 63W=    QUESTERR
   7 64*W    #SKI  K6INCRV   
  20 64TG          GEOERR   BRIEFPM,INCQUES2    [NO ERROR FROM QUESTION COMMAND  
   4 65*6    SEND
   7 65SQ    #SKI  K6INCRV   
  20 66#B          GEOERR   BRIEFPM,INC NORV    [END OF INCINDEX WITHOUT FINDING 
  20 66S2                                       [                   ALL RETRIEVES
   4 67?L    #   
  10 67R=          MENDAREA 40,K99INCRV  
   4 68=W    #   
   4 68QG    #END
   8 ____ ...23141562001000000000
