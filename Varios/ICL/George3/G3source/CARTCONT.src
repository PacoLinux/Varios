  10 22FL          SEG      CARTCONT,867 
   4 22_=    #   
  16 23DW    #    COPYRIGHT INTERNATIONAL COMPUTERS LTD - 1982   
   4 23YG    #   
  17 24D6    #    CARTCONT AND KARTCONT PROCESS EVENTS ON DA DEVICES 
   4 24XQ    #   
  19 25CB    #    INITIAL ENTRY IS ALWAYS TO CARTCONT, WHICH GOES TO KARTCONT
  11 25X2    #    TO PROCESS ENGAGED DEVICES 
   4 26BL    #   
  19 26W=    #    DETAILS OF THE DEVICE ARE SET UP BY CARTCONT AS FOLLOWS:   
   9 27*W    #       AWORK1=MARKER WD
  13 27TG    #       AWORK2=POINTER TO APIA RECORD   
  13 28*6    #       AWORK3=POINTER TO DEVICE LIST   
  10 28SQ    #       AWORK4=GEOG UNIT NO 
   4 29#B    #   
   4 29S2    #   
  14 2=?L          SEGENTRY K1CARTCONT,QK1CART  [VIA EBA 
  14 2=R=          SEGENTRY K2CARTCONT,QK2CART  [VIA DOWN
  21 2?=W          SEGENTRY K3CARTCONT,QK3CART  [FROM KARTCONT TO PROCESS DISENGAGE  
  15 2?QG          SEGENTRY K4CARTCONT,QK4CART  [REDUNDANT   
  20 2#=6          SEGENTRY K5CARTCONT,QK5CART  [FROM SWITCH TO PROCESS SW OUT   
   4 2#PQ    #   
   8 2*9B    RDRUM          +9   
   8 2*P2    RFSDM          +25  
   4 2B8L    #   
   9 2BN=    XMASK          #71777777
   9 2C7W    RMASK          #71767777
   4 2CMG    #   
   4 2D76    #   
  12 2DLQ    #              MESSAGE IDENTIFIERS  
   4 2F6B    #   
  16 2FL2    XEWFAIL        +EWFAIL             [UNIT NN FAILED  
  20 2G5L    XENTIN12       +ENTIN12            [UNIT NN CANNOT BE DISENGAGED... 
  18 2GK=    XEWSYSAT       +EWSYSAT            [SAFE NOW TO STOP UNIT NN
  19 2H4W    XEWSYS         +EWSYS              [UNIT NN IS IN USE BY SYSTEM 
  21 2HJG    XWATSW         +JWATSW             [UNIT NN IS WAITING TO BE SWITCHED OU
  18 2J46    XUNITSWO       +JUNITSWO           [UNIT NN IS SWITCHED OUT 
  20 2JHQ    XSWOREJ        +JSWOREJ            [SWITCHED OUT REJECTED ON UNIT NN
   4 2K3B    #   
  16 2KH2    #              S/R TO FIND PREVIOUS STATE FROM APIA 
  10 2L2L    #   EXITS +0 IF DISENGAGED  
  10 2LG=    #         ;]+1 IF ENGAGED   
  11 2L_W    #         ;]+2 IF ILLEGAL STATE 
   9 2MFG    #         ;]+3 IF HELD  
   4 2M_6    #   
   4 2NDQ    #   
   7 2NYB    STATE SLC   5  5
   7 2PD2          ANDN  5  3
   7 2PXL          ADX   0  5
   7 2QC=          EXIT  0  0
   4 2QWW    #   
   4 2RBG    #   
  15 2RW6    #              S/R TO OUTPACK OPS NO PARAMETER  
   5 2S*Q    XOUTOPNO
   8 2STB          SBX   7  FX1  
  12 2T*2          OUTPACK  AWORK4(2),1,GEOPER   
   8 2TSL          ADX   7  FX1  
   7 2W#=          EXIT  7  0
   4 2WRW    #   
  13 2X?G    #              S/R TO O/P MESSAGE IN X6 
   5 2XR6    XINFORM 
   8 2Y=Q          SBX   7  FX1  
   7 2YQB          LDN   1  1
   8 2_=2          INFORMX  1,6,1
   8 2_PL          ADX   7  FX1  
   7 329=          EXIT  7  0
   4 32NW    #   
   4 338G    #   
  19 33N6    #              S/R TO FIND ASTUC FOR A GIVEN GEOGU(IN AWORK4)   
  12 347Q    #              EXITS 0 IF NOT FOUND 
  18 34MB    #              EXITS 1 IF ASTUC FOUND,WITH X3 POINTING TO IT
  17 3572    #              S/R CHECKS THAT CHAIN IS IN GOOD ORDER.  
   4 35LL    #   
   6 366=    RFINDASTUC  
  20 36KW          LDN   3  BCCB                [START AT BEGINNING OF CCB CHAIN 
  15 375G    RB    LDX   3  FPTR(3)             [NEXT BLOCK  
   8 37K6          TXU   3  CXCC 
  17 384Q          BCC      RC                  [J IF END OF CHAIN   
   9 38JB          LDX   6  BACK1(3) 
  19 3942          BNZ   6  RD                  [J IF THIS IS AN ASTUC,ELSE  
  17 39HL    RC    EXIT  0  0                   [EXIT 0 IF NOT FOUND 
  17 3=3=    RD    BNG   6  RB                  [J IF DEAD,TRY NEXT  
   9 3=GW          LDX   6  GEOGU(3) 
  19 3?2G          TXL   6  AWORK4(2)           [ELSE,IS GEOG LESS THAN OURS,
  19 3?G6          BCS      RB                  [J IF SO TO CONTINUE SEARCH  
  21 3?_Q          TXU   6  AWORK4(2)           [IF GEOG DIFFERENT FROM OURS,WE HAVE 
  14 3#FB          BCS      RC                  [NO ASTUC
  18 3#_2          EXIT  0  1                   [EXIT,X3 POINTS TO ASTUC 
   4 3*DL    #   
   4 3*Y=    #   
  11 3BCW    #              S/R TO FREE ASTUC
  17 3BXG    #              OUTPUTS DISENGAGED MESSAGE TO CONSOLE AND
  15 3CC6    #              SJ. ALSO MEDIA UNLOAD LOG IF CME 
  15 3CWQ    #              X3 PTS TO ASTUC UNTIL IT IS FREED
  19 3DBB    #              ON EXIT  X1=FX1, X2=FX2, X5 AND X6 ARE CORRUPTED 
  19 3DW2    #              DRUM EVENTS ALSO USE THE CODE FROM XDRZ TO PERIN;
  11 3F*L    #              IN THIS CASE X3=0
   4 3FT=    #   
   4 3G#W    #   
   6 3GSG    RFREEASTUC  
   8 3H#6          SBX   7  FX1  
  15 3HRQ          CALL  0  RFINDASTUC          [FIND ASTUC  
  17 3J?B          BRN      RS2                 [BRANCH IF NO ASTUC  
  11 3JR2          TRACE    BACK1(3),FREEAST?
  21 3K=L          LDCT  0  #200                [STOP SOMEONE ELSE PICKING UP DISC   
  21 3KQ=          ORS   0  ASMARK(3)           [WHILE CHURNING THRU SJ BY MARKING   
  17 3L9W                                       [DISENGAGED IN ASTUC 
   7 3LPG          LOCK     3
  17 3M96    XDRZ  LGPERIS   AWORK3(2),6        [COUNT OF TRANSFERS  
  18 3MNQ          BZE   6  PERIN               [BRANCH IF DEVICE UNUSED 
  20 3N8B          BSON     EMSBIT,PERIN        [J IF EMS AS SJ NOT SET UP YET   
  12 3NN2          OUTPACK  AWORK4(2),1,GEOPER   
  19 3P7L          JENVNOT  YNOT,CME            [FOR NON-CAFS DEVICES UNDER  
  20 3PM=          JCAFSDV  AWORK3(2),YNOT      [CME PRODUCE MEDIA UNLOAD ERROR  
  13 3Q6W          LDN   6  22                  [LOGS
   9 3QLG          LDX   5  AWORK3(2)
   8 3R66          SETELOG  1,6,5
  18 3RKQ          LDN   6  #5060               [RECORD AND TRAILER TYPES
  10 3S5B          ORS   6  ELHEADER+1(1)
   7 3SK2          STOZ     5
  18 3T4L          LGPERIS  AWORK3(2),6         [TRANSFER CT TO ERROR LOG
   7 3TJ=          SLL   56 8
  10 3W3W          ORS   5  ELHEADER+9(1)
  11 3WHG          STO   6  ELHEADER+10(1)   
   9 3X36    YNOT  LDN   6  BACK1(3) 
   7 3XGQ          LDN   2  1
   9 3Y2B          LDN   5  JPDCSN   
   8 3YG2          BNZ   3  RR1A 
   9 3Y_L          LDN   5  JPDSKIP  
   7 3_F=          LDN   2  0
  17 3_YW    RR1A  OUTPACKX 6,2,5               [OUTPUT SKIP OR CSN  
  10 42DG          LGPERIS  AWORK3(2),6  
  10 42Y6          OUTPACK  6,1,TRANSCT  
  11 43CQ          CLCOUNT  AWORK3(2),GEORGE 
  10 43XB          LREPEATS  AWORK3(2),6 
  10 44C2          OUTPACK  6,1,REPEATS  
  10 44WL          LFAILS    AWORK3(2),6 
  10 45B=          OUTPACK  6,1,FAILCT   
  16 45TW          OUTPAR   TIMENOW             [TIME TO MESSAGE 
   9 46*G          MONOUT   GDISENGDA
  10 46T6          LREPEATS AWORK3(2),5  
  18 47#Q          CLCOUNT  AWORK3(2),REPEAT    [CLEAR REPEAT CT IN DL   
  10 47SB          LFAILS   AWORK3(2),6  
  17 48#2          CLCOUNT  AWORK3(2),FAIL      [CLEAR FAIL CT IN DL 
  10 48RL          HUNTW    1,ADATA,ELOUT
  18 49?=          BNG   1  PERIN               [_ IF NO ERROR LOGGING   
  16 49QW          STO   5  ELHEADER+11(1)      [REPEAT CT TO LOG
   8 4==G          BZE   6  YELOG
   7 4=Q6          STOZ     5
   8 4?9Q          SLL   56 16   
  16 4?PB          STO   5  ELHEADER+12(1)      [FAIL CT TO LOG  
  11 4#92          ORS   6  ELHEADER+13(1)   
  17 4#NL    YELOG SMO      BELOGACT            [PTR TO BSA/ELOG ACT 
   9 4*8=          LDX   2  ACTRING  
   9 4*MW          SBN   2  ACTRING  
   9 4B7G          LDX   2  BPTR(2)  
  20 4BM6          CHAIN    1,2                 [CHAIN ADATA/ELOUT TO BSA/ELOG   
  17 4C6Q          STARTACT ELOG                [ACT AND ACTIVATE IT 
   8 4CLB          LDX   2  FX2  
   8 4D62    PERIN LDX   1  FX1  
  16 4DKL          BZE   3  XDR1                [BRANCH IF DRUM  
  10 4F5=          LDX   6  CSCRATCH(3)  
  20 4FJW          BZE   6  RFREE               [BRANCH IF NO OPEN SCRATCH AREAS 
   9 4G4G          STOZ     BPERN(3) 
  19 4GJ6          STOZ     HTYPE(3)            [ASTUC NOW HAS MEANINGLESS...
  15 4H3Q          STOZ     HUNIT(3)            [...CONTENTS!
   9 4HHB          STOZ     GEOGU(3) 
   9 4J32          STOZ     ASUNUM(3)
  15 4JGL          LDCT  6  256                 [MARK AS DEAD
   9 4K2=          ORS   6  BACK1(3) 
  17 4KFW          LDCT  0  #200                [REMOVE DISENGAGE BIT
  17 4K_G          ANDX  0  ASMARK(3)           [IF SCRATCH FILE OPEN
   9 4LF6          ERS   0  ASMARK(3)
   7 4LYQ          UNLOCK   3
   8 4MDB          BRN      RS2  
   7 4MY2    RFREE FREECORE 3
   4 4NCL    RS2 
  21 4NX=    #     FOR CAFS WE HAVE TO INFORM THE CAFS ACTIVITY THAT THE CARTRIDGE   
   8 4PBW    #     HAS GONE AWAY 
   4 4PWG    #   
  20 4QB6          CAFSINFO DISENGAGE,AWORK3(2) [TELL CAFS SUBSYSTEM ABOUT EVENT 
  15 4QTQ          ADX   7  FX1                 [RESTORE LINK
  16 4R*B          LDX   1  AWORK2(2)           [POINTER TO APIA 
   7 4RT2          LDN   5  0
  18 4S#L          DCH   5  BSUNIT1(1)          [ZEROISE COUNT OF ASTUCS 
   7 4SS=          EXIT  7  0
   4 4T?W    #   
   4 4TRG    #   
  18 4W?6    #              S/R TO O/P PLEASE SAFE NOW TO STOP UNIT NN   
  15 4WQQ    #              FOR DEVICES WITHOUT HOLD BUTTON  
  16 4X=B    #              ON EXIT X2=FX2, X1 PTS TO APIA RECORD
  12 4XQ2    #              X0,X6,X7 CORRUPTED   
   4 4Y9L    #   
   5 4YP=    STOPMESS
   8 4_8W          SBX   5  FX1  
  13 4_NG          JPROP    AWORK3(2),MESS,MESS,MESS 
   8 5286          BRN      NOMES
   8 52MQ    MESS  LDX   2  FX2  
   8 537B          SMO      FX1  
  18 53M2          LDX   6  XEWSYSAT            [SAFE NOW TO STOP UNIT NN
   9 546L          CALL  7  XOUTOPNO 
   9 54L=          CALL  7  XINFORM  
   8 555W    NOMES LDX   2  FX2  
   9 55KG          LDX   1  AWORK2(2)
   8 5656          ADX   5  FX1  
   7 56JQ          EXIT  5  0
   4 574B    #   
   4 57J2    #   
   4 583L    #   
   5 58H=    QK1CART 
  21 592W          NGNC  5  1                   [=1 FROM K1 I.E. BUTTON EVENT-FROM BU
   5 59GG    QK2CART 
  20 5=26          LDN   5  0                   [=0 FROM K2 I.E. DOWN TO CARTCONT
  18 5=FQ          STO   5  AWORK1(2)           [AWORK1=1 FOR K1,0 FOR K2
   9 5=_B          LDX   1  CPPTR(2) 
  17 5?F2          BZE   5  XJ2                 [BRANCH IF FROM K2   
  21 5?YL          STO   1  AWORK3(2)           [D.L. PTR ALREADY IN AWORK3 FOR K2   
  11 5#D=          TRACE    AWORK3(2),K1CART 
  10 5#XW    XJ2   LGEOG    AWORK3(2),4  
  18 5*CG          STO   4  AWORK4(2)           [AWORK4=GEOGRAPHICAL NO. 
  17 5*X6          LTYPE    AWORK3(2),7         [GET PERIPHERAL TYPE 
  18 5BBQ          LADDP    AWORK3(2),1         [GET ADDRESS OF APIA...  
  18 5BWB          STO   1  AWORK2(2)           [...AND STORE IN AWORK2  
   8 5CB2          SMO      FX1  
   8 5CTL          TXU   7  RDRUM
  16 5D*=          BCC      XDRUM               [BRANCH IF DRUM  
   8 5DSW          SMO      FX1  
   8 5F#G          TXU   7  RFSDM
  19 5FS6          BCC      XDRUM               [BRANCH IF HIGH SPEED DRUM   
   9 5G?Q    XJ4   LDXC  5  BSUNIT(1)
  19 5GRB          BCC      XJ3                 [BRANCH IF RECORD NOT LOCKED 
   8 5H?2          COOR3    #41  
   9 5HQL          LDX   1  AWORK2(2)
  17 5J==          BRN      XJ4                 [WAIT TILL UNLOCKED  
   9 5JPW    XJ3   JENVNOT  XJ3B,CME 
  20 5K9G          JANON    AWORK3(2),XDC       [IGNORE IF ANONYMOUS(EMS ONLY)   
  18 5KP6          JSWOUT   AWORK3(2),XDC       [OR HAS JUST BEEN SW OUT 
  10 5L8Q    XJ3B  LDX   5  BSUNIT3(1)   
  21 5LNB          ANDN  5  #10                 [CHECK IF TWINNING STATUS CHANGE IN P
  14 5M82          BZE   5  XJ3A                [_ IF NOT
   9 5MML          COOR3    KTSCTF   
   9 5N7=          LDX   1  AWORK2(2)
   8 5NLW          BRN      XJ4  
   9 5P6G    XJ3A  LDX   5  BSUNIT(1)
   9 5PL6          ANDX  5  BITS22LS 
  15 5Q5Q          ORX   5  GSIGN               [LOCK RECORD 
   9 5QKB          STO   5  BSUNIT(1)
  17 5R52          BSOFF    EMSBIT,XJ5          [BRANCH IF NOT EMS   
  16 5RJL          JSHARED  AWORK2(2),REND      [FORGET IF SHARED
   9 5S4=    XJ5   LDX   1  AWORK2(2)
  17 5SHW          JUNBUSY  AWORK3(2),XJ5B      [_ IF DEVICE NOT BUSY
  16 5T3G          BSOFF    EMSBIT,XJ5A         [OTHERWISE WAIT  
  15 5TH6          COOR2                        [COOR2 IN EMS
   8 5W2Q          BRN      XJ5  
  20 5WGB    XJ5A  COOR3    #41                 [OTHERWISE WAIT FOR SPRING CLEAN 
   8 5X22          BRN      XJ5  
  11 5XFL    XJ5B  JUNDE    AWORK2(2),REND   
  19 5X_=          JDISENG  AWORK3(2),RDIS      [JUMP IF DISENGAGED(IE INOP) 
  15 5YDW          JHOLD    AWORK3(2),RHELD     [JUMP IF HELD
  10 5YYG          LDX   0  BSUNIT3(1)   
   9 5_D6          ANDN  0  BNOENG   
   8 5_XQ          BZE   0  RSOK 
  20 62CB          TRACE    AWORK4(2),CGENGAGE  [FORGET ENGAGE IF CG IN PROGRESS 
  16 62X2          LONGON   CGENG               [WAKE UP MOVCART 
   8 63BL          BRN      REND 
   4 63W=    RSOK
   4 64*W    #   
   9 64TG    #     CURRENTLY ENGAGED 
   4 65*6    #   
   4 65SQ    #   
  19 66#B          CALL  0  STATE               [FIND OUT PREVIOUS STATE AND 
  17 66S2          BRN      RENDIS              [JUMP IF DISENGAGED  
  16 67?L          BRN      RENEN               [JUMP IF ENGAGED 
  18 67R=          BRN      RILLG               [JUMP IF ILLEGAL STATE   
  15 68=W          BRN      RENHD               [JUMP IF HELD
  10 68QG    RILLG GEOERR   1,DISHELD?   
  10 69=6    RENDIS ACROSS  KARTCONT,1   
  10 69PQ    RENEN  ACROSS  KARTCONT,2   
  10 6=9B    RENHD  ACROSS  KARTCONT,3   
   4 6=P2    #   
  10 6?8L    #      CURRENTLY DISENGAGED 
   4 6?N=    #   
   4 6#7W    #   
   4 6#MG    RDIS
   8 6*76          CALL  0  STATE
  18 6*LQ          BRN      RDISDIS             [PREVIOUSLY DISENGAGED   
  17 6B6B          BRN      RDISEN              [PREVIOUSLY ENGAGED  
  16 6BL2          BRN      RILLG               [ILLEGAL STATE   
  16 6C5L          BRN      RDISHD              [PREVIOUSLY HELD 
   4 6CK=    #   
  12 6D4W    #     CURRENTLY HELD (AND ENGAGED)  
   4 6DJG    #   
   4 6F46    #   
   5 6FHQ    RHELD   
   8 6G3B          CALL  0  STATE
  18 6GH2          BRN      RHLDIS              [PREVIOUSLY DISENGAGED   
  17 6H2L          BRN      RHLDENG             [PREVIOUSLY ENGAGED  
  16 6HG=          BRN      RILLG               [ILLEGAL STATE   
  16 6H_W          BRN      RHLDHD              [PREVIOUSLY HELD 
   4 6JFG    #   
   4 6J_6    #   
   4 6KDQ    #   
  11 6KYB    #     HELD PREVIOUSLY ENGAGED   
   4 6LD2    #   
   5 6LXL    RHLDENG 
   7 6MC=    #SKIP K6CARTCONT
  11 6MWW          TRACE    AWORK4(2),HLDENG 
   8 6NBG          LDCT  4  #040 
  20 6NW6          ORS   4  BSUNIT(1)           [MARK HELD IN PERIPHERAL RECORD  
  10 6P*Q          LDX   3  BSUNIT2(1)   
  20 6PTB          BPZ   3  U1                  [BRANCH IF NOT IN USE BY SYSTEM  
   8 6Q*2          SMO      FX1  
  18 6QSL          LDX   6  XEWSYS              [UNIT NN IN USE BY SYSTEM
   9 6R#=          CALL  7  XOUTOPNO 
   9 6RRW          CALL  7  XINFORM  
   8 6S?G          BRN      REND 
   4 6SR6    #   
  10 6T=Q    #     HELD PREVIOUSLY HELD  
   4 6TQB    #   
   5 6W=2    RHLDHD  
   7 6WPL    #SKIP K6CARTCONT
  11 6X9=          TRACE    AWORK4(2),HLDHD  
  10 6XNW          LDX   3  BSUNIT2(1)   
  19 6Y8G          BNG   3  REND                [BRANCH IF IN USE BY SYSTEM  
   8 6YN6    U1    LDN   3  0(3) 
  17 6_7Q          BZE   3  U2                  [BRANCH IF NO USERS  
   8 6_MB          LDCT  4  128  
  18 7272          ORS   4  BSUNIT(1)           [SET WAKE UP CARTCONT BIT
   8 72LL          BRN      REND 
  17 736=    U2    JDED     AWORK2(2),WYES      [JUMP IF DEDICATED   
   9 73KW          LDXC  0  ETWINMK  
  21 745G          BCS      REND                [DON'T BOTHER IF TWINNING NOT INIT   
  10 74K6          LDX   4  BSUNIT3(1)   
   7 754Q          ANDN  4  4
  18 75JB          BNZ   4  WRI                 [BRANCH IF WE DISENGAGE  
   8 7642          SMO      FX1  
   8 76HL          LDX   4  XMASK
  16 773=          ANDS  4  BSUNIT(1)           [MARK DISENGAGED 
  15 77GW          CALL  7  RFREEASTUC          [FREE ASTUC  
  20 782G          PHOLD    AWORK4(2),WIPB,WG3  [TELL OTHER PROCESSOR OK WITH US 
   8 78G6          BRN      REND 
   9 78_Q    WRI   LDX   4  BSUNIT(1)
   8 79FB          SLL   4  11   
  19 79_2          BNG   4  WYES                [BRANCH IF OK TO DISENGAGE   
  15 7=DL          TESTLINK WLINK,WYES          [TEST LINK   
   8 7=Y=          BRN      REND 
  15 7?CW    WYES  CALL  7  RFREEASTUC          [FREE ASTUC  
   8 7?XG          SMO      FX1  
  16 7#C6          LDX   4  RMASK               [MARK DISENGAGED 
   9 7#WQ          ANDS  4  BSUNIT(1)
  15 7*BB    WG3   DISENGPER AWORK3(2)          [DISENGAGE   
  20 7*W2          CALL  5  STOPMESS            [EXTRA MESS FOR ED80,FD160,FD640 
   8 7B*L          BRN      REND 
   8 7BT=    WLINK SMO      FX1  
  16 7C#W          LDX   4  XMASK               [MARK DISENGAGED 
   9 7CSG          ANDS  4  BSUNIT(1)
  15 7D#6          CALL  7  RFREEASTUC          [FREE ASTUC  
   8 7DRQ    WIPB  SMO      FX1  
  19 7F?B          LDX   6  XENTIN12            [UNIT NN CANNOT BE DISENGAGED
   9 7FR2          CALL  7  XOUTOPNO 
   9 7G=L          CALL  7  XINFORM  
   8 7GQ=          BRN      REND 
   4 7H9W    #   
   4 7HPG    #   
  11 7J96    #     HELD PREVIOUSLY DISENGAGED
   4 7JNQ    #   
   5 7K8B    RHLDIS  
   7 7KN2    #SKIP K6CARTCONT
  11 7L7L          TRACE    AWORK4(2),HLDIS  
  16 7LM=          DISENGPER AWORK3(2)          [DISENGPER UNIT  
  20 7M6W          CALL  5  STOPMESS            [EXTRA MESS FOR ED80,FD160,FD640 
  10 7MLG          JDED     AWORK2(2),WAZ
  10 7N66          BUTTONOLE AWORK4(2)   
   9 7NKQ          LDX   1  AWORK2(2)
   4 7P5B    WAZ 
   4 7PK2    #   
  13 7Q4L    #     DISENGAGED PREVIOUSLY DISENGAGED  
   4 7QJ=    #   
   5 7R3W    RDISDIS 
   7 7RHG    #SKIP K6CARTCONT
  11 7S36          TRACE    AWORK4(2),DISDIS 
  15 7SGQ          SMO      BSUNIT2(1)          [DISENGAGED  
   7 7T2B          LDN   0  0
  17 7TG2          BZE   0  RS1                 [BRANCH IF NO USERS  
   8 7T_L          LDCT  5  128  
  18 7WF=          ORS   5  BSUNIT(1)           [SET WAKE UP CARTCONT BIT
   8 7WYW          BRN      REND 
  15 7XDG    RS1   CALL  7  RFREEASTUC          [FREE ASTUC  
  15 7XY6    RS1A  BRN      REND                [AND FINISH  
   4 7YCQ    #   
   4 7YXB    #   
  11 7_C2    #     DISENGAGED PREVIOUSLY HELD
   4 7_WL    #   
   5 82B=    RDISHD  
   7 82TW    #SKIP K6CARTCONT
  11 83*G          TRACE    AWORK4(2),DISHD  
   8 83T6          SMO      FX1  
  16 84#Q          LDX   6  RMASK               [ENSURE B11 CLEAR
   8 84SB          BRN      WBZ  
   4 85#2    #   
  12 85RL    #     DISENGAGED PREVIOUSLY ENGAGED 
   4 86?=    #   
   5 86QW    RDISEN  
   7 87=G    #SKIP K6CARTCONT
  11 87Q6          TRACE    AWORK4(2),DISEN  
   8 889Q          SMO      FX1  
  17 88PB          LDX   6  XMASK               [MARK APIA DISENGAGED
   9 8992    WBZ   ANDS  6  BSUNIT(1)
  10 89NL          SMO      BSUNIT2(1)   
   7 8=8=          LDN   0  0
  17 8=MW          BZE   0  RU2                 [BRANCH IF NO USERS  
   8 8?7G          LDCT  6  128  
  18 8?M6          ORS   6  BSUNIT(1)           [SET WAKE UP CARTCONT BIT
  16 8#6Q          CALL  0  RFINDASTUC          [SEARCH FOR ASTUC
  15 8#LB          BRN      RU7                 [J NOT FOUND 
   7 8*62    #SKIP K6CARTCONT
   4 8*KL    (   
   8 8B5=          LDCT  6  128  
   9 8BJW          ANDX  6  ASMARK(3)
   8 8C4G          BZE   6  RU4  
   9 8CJ6          GEOERR   1,NOTENG 
   4 8D3Q    RU4 
   4 8DHB    )   
   8 8F32          LDCT  6  128  
  16 8FGL          ORS   6  ASMARK(3)           [MARK DISENGAGED 
   8 8G2=          BRN      RU7  
  15 8GFW    RU2   CALL  7  RFREEASTUC          [FREE ASTUC  
  10 8G_G    RU7   LDX   0  BSUNIT3(1)   
  20 8HF6          ANDN  0  BCGCOM              [SEE IF CG COMMAND IN PROGRESS   
  19 8HYQ          BNZ   0  REND                [NO FAILED MESSAGE IF IT IS  
  20 8JDB          LDX   0  BSUNIT2(1)          [OR IF IN USE BY SYSTEM AS BSTS  
  17 8JY2          BNG   0  REND                [WILL OUTPUT MESSAGE 
   8 8KCL          SMO      FX1  
  16 8KX=          LDX   6  XEWFAIL             [UNIT NN FAILED  
   9 8LBW          CALL  7  XOUTOPNO 
   9 8LWG          CALL  7  XINFORM  
   8 8MB6          BRN      REND 
   4 8MTQ    #   
   4 8N*B    #   
   5 8NT2    QK3CART 
   9 8P#L          LDX   1  AWORK2(2)
   9 8PS=          BRN      RDISEN   
   4 8Q?W    #   
   5 8QRG    QK4CART 
   9 8R?6          GEOERR   1,K4CART 
   4 8RQQ    #   
   4 8S=B    #   
   4 8SQ2    #   
   9 8T9L    REND  LDX   1  AWORK2(2)
   8 8TP=          LDCT  5  256  
  16 8W8W          ERS   5  BSUNIT(1)           [UNLOCK RECORD   
  20 8WNG          FON      ENDGRU              [WAKE UP ACTIVITIES WAITING FOR  
  17 8X86    #                                  [GRUESOME TO FINISH  
   9 8XMQ    #SKIP K6CARTCONT>99-99  
  12 8Y7B          TRACE    AWORK4(2),ENDGRUE!   
   8 8YM2          BRN      XDR1 
   4 8_6L    #   
  12 8_L=    #              HERE IF EVENT ON DRUM
   4 925W    #   
  11 92KG    XDRUM JBUSY    AWORK3(2),XDRUM  
   7 9356          LDN   3  0
  10 93JQ          JDISENG AWORK3(2),XDRZ
  16 944B    XDR1  JONL  AWORK3(2),XDC          [J IF DRUM ONLINE
  10 94J2          LBSUN    AWORK3(2),7  
   7 953L          SBN   7  1
  14 95H=          BZE   7  XDC                 [J IF ICF
   7 962W          ADN   7  1
  10 96GG          TXU   7  BRADSPLTBSU  
  14 9726          BCC      XDC                 [J IF SCF
   7 97FQ          LDN   5  0
   8 97_B          LDX   1  ALFTP
   8 98F2          BZE   1  XDC3 
  10 98YL          LDX   6  ALOGLEN(1)   
   4 99D=    XDC2
   7 99XW          TXU   5  6
  17 9=CG          BCC      XDC3                [J IF END OF ALFTABLE
   7 9=X6          SMO      5
  16 9?BQ          LDX   0  A1(1)               [GET NEXT ENTRY  
  18 9?WB          BNG   0  XDC1                [J IF RESIDENCE NOT USED 
   8 9#B2          SRL   0  12   
  15 9#TL          ANDN  0  #7777               [ISOLATE BSUN
   7 9**=          TXU   0  7
  16 9*SW          BCC      XDC                 [J IF FILESTORE  
   4 9B#G    XDC1
  18 9BS6          ADN   5  2                   [UPDATE ALFTABLE POINTER 
   8 9C?Q          BRN      XDC2 
   4 9CRB    XDC3
  18 9D?2          CLALLOC  AWORK3(2)           [ELSE CLEAR ALLOCATED BIT
   9 9DQL    XDC   LDXC  5  AWORK1(2)
   8 9F==          BNZ   5  RS7  
  14 9FPW          UP                           [FROM K2 
  14 9G9G    RS7   ACROSS   SUICIDE,1           [FROM K1 
   4 9GP6    #   
   4 9H8Q    #   
  10 9HNB    #       PROCESS SWITCH OUT  
   4 9J82    #   
   5 9JML    QK5CART 
   9 9K7=          LDX   0  CPPTR(2) 
  14 9KLW          STO   0  AWORK3(2)           [D.L. PTR
  10 9L6G          LGEOG    AWORK3(2),4  
  14 9LL6          STO   4  AWORK4(2)           [GEOG NO 
  10 9M5Q          LADDP    AWORK3(2),5  
  14 9MKB          STO   5  AWORK2(2)           [APIA PTR
  17 9N52          STO   5  AWORK1(2)           [FIRST TIME INDICATOR
  20 9NJL    XSW1  JNWTBSW  ,XCANCSW            [_ IF SW OUT CANCELLED BY SW IN  
   9 9P4=          LDX   3  AWORK2(2)
   9 9PHW          LDXC  5  BSUNIT(3)
   8 9Q3G          BCC      XSW2 
   8 9QH6          COOR3    #41  
   8 9R2Q          BRN      XSW1 
  16 9RGB    XSW2  JDISENG  AWORK3(2),XSW4      [_ IF DISENGAGED 
   9 9S22          LDX   7  AWORK1(2)
  17 9SFL          BZE   7  XSW3                [_ IF NOT FIRST TIME 
   9 9S_=          STOZ     AWORK1(2)
   8 9TDW          SMO      FX1  
  19 9TYG          LDX   6  XWATSW              [UNIT NN WAITING TO SW OUT   
   9 9WD6          CALL  7  XOUTOPNO 
   9 9WXQ          CALL  7  XINFORM  
   8 9XCB          BRN      XSW1 
   9 9XX2    XSW3  SETHOLD  AWORK3(2)
  15 9YBL          DOWN     CARTCONT,2          [ACTION HOLD 
  19 9YW=    XSW3A JDISENG  ,XSW1               [AND WAIT FOR DEVICE TO BE   
  15 9_*W          COOR3    #41                 [DISENGAGED  
   8 9_TG          BRN      XSW3A
   9 =2*6    XSW4  LDX  0  BSUNIT2(3)
  16 =2SQ          BZE   0  XSW5                [_ IF NO USERS   
   8 =3#B          BRN      XSW3A
  10 =3FJ ...XSW5  LDCH  4  BSUNIT1(3)   
  17 =3LQ ...      BZE   4  XSW5A                  [OK IF NO ASTUC   
  18 =3RY ...      CALL  7  RFREEASTUC             [OTHERWISE FREE ASTUC 
   9 =3_6 ...      LDX   3  AWORK2(2)
  19 =46# ...XSW5A LDCT  0  #400                [SET BEING CARTCONTED BIT TO 
  20 =4?L          ORS   0  BSUNIT(3)           [PREVENT ANY CHANGE WHILST BEING 
  15 =4R=          SWOUT    AWORK3(2),XBRK,XREJ [SWITCHED OUT
   9 =5=W          LDX   3  AWORK2(2)
  10 =5QG          STOZ     BSUNIT5(3)   
  18 =6=6          LDX   6  XUNITSWO(1)          [UNIT NN SWITCHED OUT   
   8 =6PQ          BRN      XSW6 
  19 =79B    XREJ  LDX   6  XSWOREJ(1)           [SW OUT ON UNIT NN REJECTED 
   9 =7P2    XSW6  CALL  7  XOUTOPNO 
   9 =88L          CALL  7  XINFORM  
   9 =8N=          LDX   3  AWORK2(2)
   9 =97W          LDN   0  BSWOUT   
   7 =9MG    #SKIP K6CARTCONT
   4 ==76    (   
  10 ==LQ          ANDX  0  BSUNIT3(3)   
   8 =?6B          BNZ   0  XSW7 
  10 =?L2          GEOERR   1,NWTSWOUT   
   4 =#5L    XSW7
   4 =#K=    )   
  20 =*4W          ERS   0  BSUNIT3(3)          [CLEAR WAITING TO BE SW OUT BITS 
  17 =*JG          CLWTBSW                      [IN APIA AND JPSTATE 
   8 =B46          LDCT  0  #400 
   7 =BHQ    #SKIP K6CARTCONT
   4 =C3B    (   
   9 =CH2          ANDX  0  BSUNIT(3)
   8 =D2L          BNZ   0  XSW8 
  10 =DG=          GEOERR   1,NOTBCART   
   4 =D_W    XSW8
   4 =FFG    )   
   9 =F_6          ERS   0  BSUNIT(3)
   9 =GDQ          FON      ENDGRU   
   7 =GYB          SUICIDE   
   4 =HD2    #   
  13 =HXL    #     HERE IF SW OUT CANCELLED BY SW IN 
   4 =JC=    #   
   5 =JWW    XCANCSW 
   9 =KBG          LDX   3  AWORK2(2)
   9 =KW6          LDN   0  BSWOUT   
   7 =L*Q    #SKIP K6CARTCONT
   4 =LTB    (   
  10 =M*2          ANDX  0  BSUNIT3(3)   
   8 =MSL          BNZ   0  XCA1 
  10 =N#=          GEOERR   1,DASWCANC   
   4 =NRW    XCA1
   4 =P?G    )   
  10 =PR6          ERS   0  BSUNIT3(3)   
   7 =Q=Q          SUICIDE   
   4 =QQB    #   
   4 =R=2    #   
   9 =RPL    XBRK  GEOERR   1,BREAKIN
   4 =S9=    #   
  11 =SNW          MENDAREA 30,K99CARTCONT   
   4 =T8G    #END
   6 ____ ...647025010002
