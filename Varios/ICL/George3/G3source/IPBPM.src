  11 22FL ...      SEG      IPBPM,,BOB WEYAND
  11 23YG    #LIS  K0IPBPM>K0POST>K0ALLGEO   
   9 24D6                   8HIPBPM  
   4 24XQ    #   
  11 25CB ...      SEGENTRY K1IPBPM,XK1IPBPM 
   4 26BL    #   
   4 26W=    #   
  17 27*W ...# THIS SEGMENT INITIALISES EACH 7900 AND OUTPUTS A CLOSE
  18 27TG ...# DOWN MESSAGE TO EACH OPERABLE MOP TERMINAL AND RJE CONSOLE
   4 29#B    #   
   4 29S2    #   
   8 2=?L    PMRW           +0   
   8 2=R=    PMCHA          +0   
  17 2?=W ...PMADDR1        #400:0,#02000000,#01030103  [INITIALISE  
   9 2?QG ...#DEF  PMLEN1=0?-PMADDR1 
  16 2#=6    PMADDR2        #400:0,#01120000            [REOPEN  
   9 2#PQ ...#DEF  PMLEN2=0?-PMADDR2 
  15 2B8L    PMADDR4        #400:0,#01200000            [OPEN
   9 2BN= ...#DEF  PMLEN4=0?-PMADDR4 
  16 2C7W ...PMADDR5        +0,+0,#400:52       [FOR MOP CONSOLE 
   9 2CMG                   #76327635
   9 2D76                   #76206450
  17 2DLQ                   36HE SYSTEM HAS TEMPORARILY CLOSED DOWN  
   9 2F6B                   #76327635
   9 2FL2                   #76207613
   9 2FWX ...#DEF  PMLEN5=0?-PMADDR5 
  16 2G7T ...PMADDR6        +0,+0,#400:50       [FOR MOP DISPLAYS
  19 2G=4 ...               #76331076           [OUTPUT ON LINE 24 COLUMN 19 
  18 2G#? ...               #31037602           [THE FLASHING MESSAGE:   
  18 2GBG ...               40H THE SYSTEM HAS TEMPORARILY CLOSED DOWN   
   9 2GDP ...               #76030000
   9 2GG_ ...#DEF  PMLEN6=0?-PMADDR6 
   5 2GK=    PMADDR  
   5 2H4W    #REP  67
   8 2HJG                   +0   
   9 2J46 ...#DEF  PMLEN=0?-PMADDR   
  18 2JHQ    PMCOUNT        +0                  [SYSTEM OVERLOAD COUNT   
  19 2K3B    PRIORITY       +0                  [PRIORITY FOR STOPPING OUTPUT
  21 2KH2    SFOUND         +0                  [SWITCH TO INDICATE WHETHER S.S. FD. 
   5 2L2L    PDEVS   
  10 2LG=    #HAL           AMXOR+ADEVS,0
  19 2L_W    PMADEVSADDR    +0                  [START ADDRESS OF AMXOR/ADEVS
   9 2M_6    SSMASK         #770000  
  18 2NDQ    POPENED        #200000             [ILLEGAL CHECK FOR OPEN  
   8 2PD2    SLINK          +0   
  21 2PXL    WHICHCONS      +0                  [INDICATES WHETHER 7071 DEALT WITH   
  21 2QC=    READCT         +0                  [COUNT OF READS REPEATED FOR TIME OUT
   5 2QWW    PMIDENTS
   8 2RBG    #REP           200  
   8 2RW6                   +0   
  12 2S*Q ...#DEF           TABLEN=0?-PMIDENTS   
   4 2STB    #   
   4 2T*2    #   
  19 2TSL ...# MACRO PMSTOPR RESETS THE PRIORITY FOR STOP OUTPUT IF NECESSARY
   4 2W#=    #   
  19 2WRW ...# MACRO PMADD ADDS 1 TO THE SYSTEM OVERLOAD COUNT IF NECESSARY  
   4 33N6    #   
  21 347Q ...# MACRO PMSUB SUBTRACTS 1 FROM THE SYSTEM OVERLOAD COUNT IF NECESSARY   
   4 3942    #   
  20 39HL ...# MACRO PMREADCT GETS THE COUNT OF THE NUMBER OF WORDS READ INTO X6 
   4 3CC6    #   
  21 3CWQ ...# MACRO PMOLCH CHECKS TO SEE IF THE SYSTEM OVERLOAD COUNT IS NON-ZERO,  
  20 3KQ=    # AND TO SEE IF THE HIGHEST PRIORITY FOR STOPPING OUTPUT IS > 10.   
  19 3L9W    # IF EITHER OF THESE CONDITIONS EXISTS THE MACRO JUMPS TO %A.   
   4 3LPG    #   
   4 3R66    #   
   4 3RKQ    #   
  20 3S5B    # THIS SUBROUTINE ISSUES A READ ORDER AND DEALS WITH THE INPUT IF A 
  20 3SK2    # WRITE HAS BEEN REJECTED OR A SYSTEM OVERLOAD CONDITION HAS BEEN   
  17 3T4L    # REPORTED. THE LINK IS IN X6 AND X0,X5 ARE OVERWRITTEN.
  21 3TJ=    # IT EXITS 0 IF THE READ WAS SUCCESSFUL, AND 1 IF THE IPB IS INOPERABLE.
   4 3W3W    #   
   4 3WHG    #   
   8 3WTX ...READ  SMO      FXPM1
  19 3X8# ...      STO   6  SLINK               [REMEMBER THE LINK ADDRESS   
  18 3XGQ          CALL  6  SETRD               [SET UP C.A. FOR READING 
  17 3Y2B          CALL  6  RPCH1               [READ AND CHECK REPLY
  18 3YG2          BRN      NEXT                [GIVE UP IF READ REJECTED
  18 3Y_L          BRN      SOUT1               [EXIT +1 IF INOPERABLE   
  20 3_F=          BRN      NEXT                [GIVE UP IF TIME OUT OR NOT IMP. 
  18 3_YW          PMREADCT NEXT                [GET COUNT OF WDS. READ  
   8 42?? ...W3    SMO      FXPM1
   9 42KN ...      LDCH  5  PMADDR(1)
  18 42Y6          BZE   5  W1                  [J IF NO MORE SUPERVISORY
   7 43CQ          SBX   6  5
   8 43MJ ...      SMO      FXPM1
  18 43XB          LDX   0  PMADDR(1)           [EXTRACT SUPERVISORY CODE
   8 44C2          SRC   0  12   
   8 44WL          ANDN  0  #77  
  18 45B=          SBN   0  40                  [SUSPEND IPB OUTPUT (40) 
   8 45TW          PMSTOPR  W2   
  17 46*G          SBN   0  2                   [SYSTEM OVERLOAD (42)
   8 46T6          PMADD    W2   
  17 47#Q          SBN   0  1                   [SYSTEM CONTINUE (43)
   8 47SB          PMSUB    W2   
  17 48#2    W2    BZE   6  W1                  [J IF NO MORE INPUT  
  18 48RL          ADX   1  5                   [UPDATE BUFFER MODIFIER  
   8 49?=          BRN      W3   
   8 49KM ...W1    SMO      FXPM1
   8 49Y4 ...      LDX   6  SLINK
  15 4==G          EXIT  6  0                   [EXIT IF OK  
   8 4=JX ...SOUT1 SMO      FXPM1
   8 4=X# ...      LDX   6  SLINK
  17 4?9Q          EXIT  6  1                   [EXIT IF INOPERABLE  
   4 4?PB    #   
   4 4#92    #   
  21 4#NL    # THIS SUBROUTINE ISSUES A READ WHEN SOME INPUT IS EXPECTED, AND GETS   
  20 4*8=    # THE COUNT OF THE NUMBER OF WDS. READ TO X6. THE LINK IS IN X0, AND
  20 4*MW    # X6 IS OVERWRITTEN. IT EXITS 0 IF THE IPB IS INOPERABLE, AND +1 IF 
  11 4B7G    # THE READ TERMINATED NORMALLY. 
   4 4BM6    #   
   4 4C6Q    #   
   5 4CLB    READSS  
   8 4CW8 ...      SMO      FXPM1
  18 4D62          STO   0  SLINK               [REMEMBER LINK ADDRESS   
  17 4DKL    SP2   CALL  6  SETRD               [SET UP C.A. FOR READ
   8 4F5=          CALL  6  RPCH1
  18 4FJW          BRN      NEXT                [GIVE UP IF READ REJECTED
  18 4G4G          BRN      SOUT0               [EXIT 0 IF IPB INOPERABLE
  19 4GJ6          BRN      SP3                 [J IF TIME OUT OR NOT IMP.   
   8 4GRY ...      SMO      FXPM1
   9 4H3Q          STOZ     READCT   
   8 4HHB          PMREADCT NEXT 
   8 4HR8 ...      SMO      FXPM1
   8 4J32          LDX   0  SLINK
  15 4JGL          EXIT  0  1                   [EXIT IF OK  
   8 4JQD ...SOUT0 SMO      FXPM1
   9 4K2= ...      STOZ     READCT   
   8 4K=4 ...      SMO      FXPM1
   8 4KFW          LDX   0  SLINK
  17 4K_G          EXIT  0  0                   [EXIT IF INOPERABLE  
   8 4L?X ...SP3   SMO      FXPM1
   9 4LL# ...      LDX   0  READCT   
   9 4LYQ          SBN   0  IMREPT   
  21 4MDB          BZE   0  NEXT                [GIVE UP IF TIMED OUT IMREPT TIMES   
  19 4MY2          ADN   0  IMREPT+1            [OTHERWISE ADD 1 TO COUNT OF 
   8 4N7S ...      SMO      FXPM1
  15 4NCL          STO   0  READCT              [NO. OF READS
   8 4NX=          BRN      SP2  
   4 4PBW    #   
   4 4PWG    #   
   4 4QB6    #   
  17 4QTQ    # THIS SUBROUTINE CHECKS THE REPLY WORD FOR IPB PERIS.  
  14 4R*B    # IT EXITS 0 IF THE TRANSFER WAS REJECTED   
  19 4RT2    # IT EXITS 1 IF THE IPB WAS INOPERABLE OR REPLY INDICATED ERROR 
  20 4S#L    # IT EXITS 2 IF THE REPLY INDICATED TIMED OUT AND/OR NOT IMPLEMENTED
  14 4SS=    # IT EXITS 3 IF THE PERI TERMINATED NORMALLY
  18 4T?W    # X6 CONTAINS THE LINK AND PMCA+1 CONTAINS THE REPLY WORD   
   8 4TRG    # X0 IS OVERWRITTEN.
   4 4W?6    #   
   4 4WQQ    #   
   5 4X=B    REPCHEK 
   8 4XG8 ...      SMO      FXPM1
   8 4XQ2          LDN   0  PMRW 
  19 4Y9L          DCA      CPPTR(2),STO,0,REP1 [SET RW ADDR IN CURRENT AREA 
   5 4Y#9 ...RPCH1   
   8 4YBS ...#SKI K6IPB>100-100  
   4 4YFC ...(   
   9 4YJ2 ...      SMO      CPPTR(2) 
   8 4YLK ...      LDX   0  IREP1
  21 4YP8 ...      STO   0  GEN0                [DENOTES PERI TYPE TO TRACE ROUTINE  
   4 4YRR ...)   
   9 4YWB ...      PERI  0  CPPTR(2) 
   8 4Y_4 ...      SMO      FXPM1
   8 4_8W          LDX   0  PMRW 
  18 4_NG          BPZ   0  TER                 [J IF TRANSFER TERMINATED
   7 5286          SLC   0  5
  17 52MQ          BPZ   0  OP                  [J IF IPB OPERABLE   
  19 537B    RINOP EXIT  6  1                   [OTHERWISE EXIT INOPERABLE   
   7 53M2    OP    SRC   0  1
  16 546L          BNG   0  REJ                 [J IF REJECTED   
  20 54F3 ...WAIT  SMO      FXPM1               [SEE IF TRANSFER HAS TERMINATED  
   8 54RD ...      LDX   0  PMRW 
  17 555W          BNG   0  WAIT                [WAIT FOR TERMINATION
   7 55KG    TER   SLC   0  5
  16 5656          BPZ   0  RINOP               [J IF INOPERABLE 
   7 56JQ          SRC   0  2
  15 574B          BNG   0  RINOP               [J IF ERROR  
   7 57J2          SLC   0  1
  17 583L          BNG   0  NOTIMP              [J IF NOT IMPLEMENTED
   7 58H=          SRC   0  3
  16 592W          BNG   0  NOTIMP              [J IF TIMED OUT  
   8 593? ...#SKI K6IPB>100-100  
   4 593N ...(   
   9 5945 ...      LDX   0  ASWITCH1 
   7 594G ...      SLL   0  7
  21 594X ...      BPZ   0  TRACEX              [SWITCH NOT SET(BY MOP TRACE COMMAND)
  20 595# ...[TRACES (ONLINE) SUCCESSFUL CC TRANSFERS IF ^MOP TRACE^ SWITCHED ON 
   8 595P ...      STO   3  GEN3 
   8 5966 ...      STO   4  GEN4 
   8 596H ...      STO   5  GEN5 
   7 596Y ...      LDN   5  0
   8 597* ...      LDX   4  GEN0 
   8 597Q ...      SMO      FXPM1
   8 5987 ...      LDEX  0  PMRW 
   7 598J ...      NGX   0  0
  15 598_ ...      BZE   4  TRACEL1             [READ PERI   
   9 599B ...      SMO      CPPTR(2) 
   9 599R ...      ADX   0  ICOUNT   
   8 59=8 ...      SBN   0  PMLEN
   5 59=K ...TRACEL1 
   8 59?2 ...      ADN   0  PMLEN
   9 59?C ...      LDX   3  CPPTR(2) 
   9 59?S ...      ADN   3  ISCTSA   
  15 59#9 ...      BZE   4  TRACEL2             [READ PERI   
  10 59#L ...      SBN   3  ISCTSA-ICTSA 
   5 59*3 ...TRACEL2 
   8 59*D ...      LDX   3  0(3) 
   5 59*T ...TRACEL3 
   7 59B= ...      SMO      5
   8 59BM ...      '177  0  0(3) 
   7 59C4 ...      8HIPBPM   
   7 59CF ...      ADN   5  1
   9 59CW ...      BCT   0  TRACEL3  
   8 59D? ...      LDX   3  GEN3 
   8 59DN ...      LDX   4  GEN4 
   8 59F5 ...      LDX   5  GEN5 
   5 59FG ...TRACEX  
   4 59FX ...)   
  15 59GG          EXIT  6  3                   [EXIT IF OK  
   5 5=26    NOTIMP  
  21 5=FQ          EXIT  6  2                   [EXIT FOR TIMED OUT/NOT IMPLEMENTED  
  17 5=_B    REJ   EXIT  6  0                   [EXIT FOR REJECTED   
   4 5?F2    #   
   4 5?YL    #   
  18 5#D=    # THIS SUBROUTINE SETS UP THE CONTROL AREA FOR A READ PERI. 
   8 5#XW    # IT OVERWRITES X0  
   7 5*CG    # THE LINK IS X6
   4 5*X6    #   
   4 5BBQ    #   
  16 5BWB    SETRD LDN   0  2                   [MODE FOR READ   
  20 5CB2          DSCA     CPPTR(2),DLA,0,MODE [MODE FOR READ IN DL -SPOUT AREA 
  16 5CTL          LDN   0  PMLEN               [LENGTH OF BUFFER
  16 5D*=          DSCA     CPPTR(2),DLA,0,COUNT[SET COUNT IN DL 
   8 5DK4 ...      SMO      FXPM1
  16 5DSW          LDN   0  PMADDR              [BUFFER ADDRESS  
  12 5F#G          DSCA     CPPTR(2),STO,0,CTSA  
   8 5FJ# ...      SMO      FXPM1
   8 5FS6          LDN   0  PMRW 
  18 5G?Q          DSCA     CPPTR(2),STO,0,REP1 [SET REPLY WORD IN SPOUT 
   8 5GMF ...      LDCT  0  #400 
   8 5H38 ...      SMO      FXPM1
   8 5HBX ...      ORN   0  PMCHA
  17 5HQL          DSCA     CPPTR(2),STO,0,CHADD[SET CHAINING ADDRESS
   7 5J==          EXIT  6  0
   4 5JPW    #   
   4 5K9G    #   
  20 5KP6    # THIS SUBROUTINE ATTEMPTS TO REOPEN THE IPB IF A REPLY INDICATED   
   9 5L8Q    # ERROR OR INOPERABLE.  
  20 5LNB    # IT ASSUMES THE UNIT NO. OF THE IPB IS IN X4 AND THE LINK IS IN X7 
  11 5M82    # IF OVERWRITES X0,X5 AND X6.   
   4 5MML    #   
   4 5N7=    #   
   5 5NLW    PMINOP  
  16 5P6G          LDN   0  #11                 [END WRITE MODE  
  12 5PL6          DCA      CPPTR(2),DSA,0,MODE  
  20 5Q5Q          LDN   0  PMLEN2              [SET UP CONTROL AREA TO OUTPUT   
  12 5QKB          DCA      CPPTR(2),DSA,0,COUNT 
   8 5QT8 ...      SMO      FXPM1
   9 5R52          LDN   0  PMADDR2  
  12 5RJL          DCA      CPPTR(2),STO,0,CTSA  
   8 5RSD ...      SMO      FXPM1
  19 5S4=          LDN   0  PMRW                [SET RW ADDR IN CURRENT AREA 
  12 5SHW          DCA      CPPTR(2),STO,0,REP1  
  15 5T3G          CALL  6  REPCHEK             [CHECK REPLY 
  18 5TH6          BRN      PMINP1              [TRY TO READ IF REJECTED 
  19 5W2Q          BRN      NEXT                [GIVE UP IF STILL INOPERABLE 
  21 5WGB          BRN      NEXT                [OR IF TIMED OUT OR NOT IMPLEMENTED  
   8 5WQ8 ...      SMO      FXPM1
   8 5X22          LDX   0  PMRW 
   8 5XFL          ANDN  0  #777 
  19 5X_=          BNZ   0  PMINP1              [ISSUE READ IF WRITE ENDED   
  21 5YDW    S1    CALL  0  READSS              [ISSUE READ FOR 'READY'; CHECK REPLY 
  19 5YYG          BRN      NEXT                [J IF INOPERABLE TO GIVE UP  
   8 5_=X ...S4    SMO      FXPM1
  19 5_K# ...      LDCH  5  PMADDR(1)           [J TO GIVE UP IF ONLY B.S.W. 
  17 5_XQ          BZE   5  S5                  [J IF DATA SEGMENT   
   7 62CB          SBX   6  5
   8 62M8 ...      SMO      FXPM1
  17 62X2          LDX   0  PMADDR(1)           [GET SUPERVISORY CODE
   8 63BL          SRC   0  12   
   8 63W=          ADN   0  #77  
  14 64*W          SBN   0  32                  [32=READY
   8 64TG          BNZ   0  S2   
  19 65*6          LDN   0  1                   [SET FOUND MARKER IF READY   
   8 65JY ...      SMO      FXPM1
   9 65SQ          STO   0  SFOUND   
   8 66#B          BRN      S3   
  15 66S2    S2    SBN   0  5                   [37=ILLEGAL  
  17 67?L          BZE   0  S3                  [IGNORE  IF ILLEGAL  
  18 67R=          SBN   0  3                   [40=SUSPEND IPB OUTPUT   
  16 68=W          PMSTOPR  S3                  [STORE PRIORITY  
  17 68QG          SBN   0  2                   [42=SYSTEM OVERLOAD  
  19 69=6          PMADD    S3                  [ADD TO SYSTEM OVERLOAD COUNT
  17 69PQ          SBN   0  1                   [43=SYSTEM CONTINUE  
  19 6=9B          PMSUB    S3                  [SUBTRACT FROM OVERLOAD COUNT
  17 6=P2    S3    BZE   6  S5                  [J IF NO MORE INPUT  
  18 6?8L          ADX   1  5                   [UPDATE BUFFER MODIFIER  
  16 6?N=          BRN      S4                  [GET NEXT SEGMENT
   8 6#2M ...S5    SMO      FXPM1
   9 6#*4 ...      LDX   0  SFOUND   
  20 6#MG          BZE   0  S1                  [ISSUE ANOTHER READ IF NO READY  
   8 6#X# ...      SMO      FXPM1
   9 6*76          STOZ     SFOUND   
   7 6*LQ          EXIT  7  0
   5 6B6B    PMINP1  
  19 6BL2          CALL  6  READ                [ISSUE READ AND CHECK REPLY  
  14 6C5L          BRN      WHATNOW1            [J IF OK 
  19 6CK=          BRN      NEXT                [J IF INOPERABLE TO GIVE UP  
   5 6D4W    WHATNOW1
   8 6D#N ...      SMO      FXPM1
   9 6DJG          LDX   0  PMCOUNT  
  17 6F46          BZE   0  PMINOP              [TRY TO OUTPUT REOPEN
  17 6FHQ          BRN      PMINP1              [OR TRY ANOTHER READ 
   4 6G3B    #   
   4 6GH2    #   
   4 6H2L    #   
   6 6HG=    # ENTER HERE
   4 6H_W    #   
   4 6JFG    #   
   5 6J_6    XK1IPBPM
  20 6KDQ          LDX   2  IPBADDR             [BASE OF LIST OF IPBIN ACTIVITIES
   8 6KYB          BRN      T1   
  16 6LD2    NEXT1 LDX   2  IPBL(2)             [GET NEXT IPBIN  
  17 6LXL ...T1    BZE   2  (JPMSEG)            [EXIT IF NONE LEFT   
  19 6MC= ...      PMCORES  2,(JPMSEG)          [EXIT IF OUTSIDE RESERVATIONS
   8 6MWW          LDN   5  #30  
  21 6NBG          FINDPERE 3,APFOTY,5,RET      [J. IF CONTENTS OF CPPTR ARE INVALID.
   8 6NW6          BNG   3  NEXT1
  10 6P*Q          BXU   3  CPPTR(2),RET 
  20 6PTB          JWRONG   CPPTR(2),NEXT1      [IF IBB WRONGED TAKE A NEXT ONE  
  21 6P_? ...      JEXOTIC  ,NEXT1              [IF IPB IS AN EXOTIC TAKE NEXT ONE   
  20 6Q58 ...      JONL     ,NEXT1              [IF IPB IS ONLINE TAKE NEXT ONE  
  21 6Q95 ...      JFREE    ,NEXT1              [IF IPB IS UNALLOCATED TAKE NEXT ONE 
  21 6Q*2    WAIT1 JBUSY    ,WAIT1              [WAIT AROUND UNTIL CURRENT IS FREE.  
   8 6QSL          LDN   4  #15  
  12 6R#=          DCA      CPPTR(2),STO,4,MODE  
  16 6RRW          LDN   4  PMLEN1              [SET COUNT IN DL 
  12 6S?G          DCA      CPPTR(2),DSA,4,COUNT 
   8 6SH# ...      SMO      FXPM1
  18 6SR6          LDN   4  PMADDR1             [SET BUFFER ADDRESS IN DL
  12 6T=Q          DCA      CPPTR(2),STO,4,CTSA  
   8 6TLF ...      LDCT  4  #400 
   8 6W28 ...      SMO      FXPM1
   8 6W*X ...      ORN   4  PMCHA
  12 6WPL          DCA      CPPTR(2),STO,4,CHADD 
  17 6X9= ...P1    CALL  6  REPCHEK             [OUTPUT INITIALISE   
  15 6XNW                                       [CHECK REPLY 
  18 6Y8G          BRN      TINP                [TRY INPUT IF REJECTED   
  15 6YN6          BRN      SINOP               [INOPERABLE  
  21 6_7Q          BRN      P1                  [TRY AGAIN IF TIMED OUT OR NOT IMP.  
   8 6_CJ ...      SMO      FXPM1
  18 6_MB          LDX   0  PMRW                [LOOK AND SEE IF WRITE   
  15 7272          ANDN  0  #777                [WAS ENDED   
  14 72LL          BZE   0  OK1                 [J IF NOT
  21 736=    TINP  JSBUSY   ,TINP               [MAY BE BUSY IF WRITE WAS REJECTED,  
  19 73KW          CALL  6  READ                [IF NOT : ATTEMPT TO READ.   
  14 745G          BRN      PFIN                [J IF OK 
  16 74K6    SINOP CALL  7  PMINOP              [TRY TO REOPEN   
  16 754Q    SETW1 LDN   0  #11                 [END WRITE MODE  
  12 75JB          DCA      CPPTR(2),DSA,0,MODE  
  19 7642 ...      LDN   0  PMLEN1              [RESET C.A. FOR INITIALISE   
  12 76HL          DCA      CPPTR(2),DSA,0,COUNT 
   8 76RD ...      SMO      FXPM1
   9 773=          LDN   0  PMADDR1  
  12 77GW          DCA      CPPTR(2),STO,0,CTSA  
   8 782G          BRN      P1   
   8 78#X ...PFIN  SMO      FXPM1
   9 78M# ...      LDX   0  PMCOUNT  
  17 78_Q          BZE   0  SETW1               [WRITE IF NO OVERLOAD
   8 79FB          BRN      TINP 
   5 79_2    SINOP1  
  16 7=DL          CALL  7  PMINOP              [TRY TO REOPEN   
  17 7=Y= ...OK1   CALL  0  READSS              [TRY TO READ READY   
  16 7?CW          BRN      SINOP1              [J IF INOPERABLE 
   8 7?Q? ...NXTSS SMO      FXPM1
   9 7#4N ...      LDCH  5  PMADDR(1)
  19 7#C6          BZE   5  TFOUND              [J IF DATA SEGMENT REACHED   
  15 7#WQ          SBX   6  5                   [UPDATE COUNT
   8 7*6J ...      SMO      FXPM1
  17 7*BB          LDX   0  PMADDR(1)           [GET SUPERVISORY CODE
   8 7*W2          SRC   0  12   
   8 7B*L          ANDN  0  #77  
  14 7BT= ...      SBN   0  32                  [32=READY
   8 7C#W          BZE   0  SETM 
  17 7HPG    TMOR  BZE   6  TFOUND              [J IF NO MORE INPUT  
  18 7J96          ADX   1  5                   [UPDATE BUFFER MODIFIER  
  16 7JNQ          BRN      NXTSS               [GET NEXT INPUT  
  18 7K8B ...SETM  LDN   0  1                   [INDICATE READY RECEIVED 
   8 7KN2 ...      SMO      FXPM1
   9 7L7L ...      STO   0  SFOUND   
   8 7MLG          BRN      TMOR 
   5 7N66    TFOUND  
   8 7N*Y ...      SMO      FXPM1
   9 7NKQ          LDX   0  SFOUND   
  19 7P5B ...      BZE   0  OK1                 [READY NOT FOUND : READ MORE 
   8 7PSS ...      SMO      FXPM1
  17 7Q4L          STOZ     SFOUND              [OTHERWISE LOOK FOR  
  17 7QJ=    SEARCH                             [AMXOR/ADEVS BLOCK   
   7 7R3W          LDX   1  2
   9 7RHG    NBLK  LDX   1  FPTR(1)  
   8 7S36          TXL   1  CORES
  18 7SGQ          BCC      NEXT                [GIVE UP IF CHAIN BROKEN 
   9 7T2B          LDX   0  ATYPE(1) 
  19 7TG2          BXGE  0  CACT,NEXT           [GIVE UP IF ACTIVITY BLOCK   
   8 7TPS ...      SMO      FXPM1
   8 7T_L          SBX   0  PDEVS
   8 7WF=          BNZ   0  NBLK 
  20 7WYW          LDN   3  0                   [X3 USED TO INDEX IDENT. TABLE   
  20 7XDG          LDX   7  A1+FOURTHWD(1)      [GET LOWEST IDENT. NO.-1 TO X7   
   8 7XN# ...      SMO      FXPM1
  20 7XY6          STO   1  PMADEVSADDR         [PRESERVE PTR. TO START OF ADEVS 
  10 7YCQ          ADX   1  ALOGLEN(1)   
   8 7YXB          TXL   1  CORES
  19 7_C2          BCC      NEXT                [GIVE UP IF LOG. LENGTH SILLY
   5 7_WL    NENTRY  
   7 82B=          SBN   1  4
   8 82L4 ...      SMO      FXPM1
  17 82TW          BXE   1  PMADEVSADDR,TABLEND [J IF END OF BLOCK   
   8 83*G          LDCT  0  #40  
  11 83T6          ANDX  0  A1+FOURTHWD(1)   
  17 84#Q          BNZ   0  NENTRY              [J IF TYPE NOT SET UP
  17 84SB          LDX   0  A1+FOURTHWD(1)      [ISOLATE TYPE NUMBER 
   8 85#2          ANDN  0  #777 
   8 85HS ...      SMO      FXPM1
   9 85RL          LDX   6  WHICHCONS
  18 86?=          BNZ   6  TYPE7023            [J IF SETTING 7023 TABLE 
  15 86JL ...TYPE  BZE   0  TMOP                [J IF 7071   
   8 86W2 ...      SBN   0  10   
  16 877B ...      BNZ   0  NENTRY              [J IF NOT 7181/2 
  11 879P ...TMOP  LDXC  0  A1+FOURTHWD(1)   
  21 87#4 ...      BCC      STORE               [J IF IDENTIFIER IS MARKED AVAILABLE 
  10 87BC ...      LDX   0  A1+BPTR(1)   
  21 87DQ ...      BZE   0  NENTRY              [IDENTIFIER UNAVAILABLE & IDLE:IGNORE
  19 87H5 ...STORE LDX   0  1                   [CALCULATE IDENTIFIER NUMBER 
   8 87KD ...      SMO      FXPM1
  10 87Q6          SBX   0  PMADEVSADDR  
   7 889Q          SRC   0  2
   7 88PB          ADX   0  7
   8 88_8 ...      SMO      FXPM1
  19 8992          STO   0  PMIDENTS(3)         [STORE IDENT. NO. IN TABLE   
  18 89NL          ADN   3  1                   [UPDATE TABLE MODIFIER   
   8 89YD ...      SMO      FXPM1
  11 8=8=          BXE   3  TABLEN,TABLEND   
   9 8=MW          BRN      NENTRY   
   5 8?7G    TABLEND 
  16 8?M6          BZE   3  T7023               [J IF TABLE EMPTY
   7 8#6Q          SBN   3  1
   4 8#LB ...#   
   5 8*62 ...NXMOP   
   8 998D ...      SMO      FXPM1
   9 99D=          STOZ     SFOUND   
   8 99N4 ...      SMO      FXPM1
  17 99XW          LDX   0  PMIDENTS(3)         [INSERT IDENT. NO. IN
   8 9=7N ...      SMO      FXPM1
  15 9=CG          DSA   0  PMADDR4+1           [OPEN BUFFER 
   8 9=PX ...TOPEN SMO      FXPM1
   9 9?4# ...      LDX   0  PMCOUNT  
  18 9?BQ          BNZ   0  TINP3               [READ IF SYSTEM OVERLOAD 
  16 9?WB          LDN   0  #11                 [SET UP C.A. FOR 
  12 9#B2          DCA      CPPTR(2),DSA,0,MODE  
   9 9#TL          LDN   0  PMLEN4   
  12 9**=          DCA      CPPTR(2),DSA,0,COUNT 
   8 9*K4 ...      SMO      FXPM1
   9 9*SW          LDN   0  PMADDR4  
  12 9B#G          DCA      CPPTR(2),STO,0,CTSA  
  17 9BS6    P7    CALL  6  REPCHEK             [ISSUE WRITE FOR OPEN
  19 9C?Q          BRN      TINP3               [TRY INPUT IF WRITE REJECTED 
  15 9CRB          BRN      SINOP4              [J IF INOP.  
  21 9D?2          BRN      P7                  [TRY AGAIN IF NOT IMP. OR TIME OUT   
   8 9DGS ...      SMO      FXPM1
   8 9DQL          LDX   0  PMRW 
   8 9F==          ANDN  0  #777 
  17 9FPW          BZE   0  OK3                 [J IF WRITE NOT ENDED
  21 9G9G    TINP3 CALL  6  READ                [READ IF WR. ENDED OR REJ. OR IF S.O.
  20 9GP6          BRN      TOPEN               [J IF OK, OTHERWISE INOPERABLE   
   5 9H8Q    SINOP4  
  17 9HNB          CALL  7  PMINOP              [TRY TO REOPEN IPB   
   8 9J82          BRN      TOPEN
  20 9JML    OK3   CALL  0  READSS              [TRY TO READ IDENTIFIER OPENED   
  17 9K7=          BRN      SINOP5              [J IF IPB INOPERABLE 
   8 9KFM ...NCH2  SMO      FXPM1
   9 9KS4 ...      LDCH  5  PMADDR(1)
  16 9L6G          BZE   5  TFOUND2             [J IF DATA SEG.  
  16 9LL6          SBX   6  5                   [UPDATE WD. CT.  
   8 9LTY ...      SMO      FXPM1
  18 9M5Q          LDX   0  PMADDR(1)           [ISOLATE SUPERVISORY CODE
   8 9MKB          SRC   0  12   
   8 9N52          ANDN  0  #77  
  15 9NJL          SBN   0  37                  [37=ILLEGAL  
   8 9P4=          BZE   0  TILL2
  17 9PHW          SBN   0  3                   [40=STOP IPB OUTPUT  
   8 9Q3G          PMSTOPR  PNXT2
  17 9QH6          SBN   0  2                   [42=SYSTEM OVERLOAD  
   8 9R2Q          PMADD    PNXT2
  17 9RGB          SBN   0  1                   [43=SYSTEM CONTINUE  
   8 9S22          PMSUB    PNXT2
  17 9SFL          SBN   0  5                   [48=IDENTIFIER OPENED
  15 9S_=          BNZ   0  PNXT2               [J IF NOT 48 
   8 9T94 ...      SMO      FXPM1
  18 9TDW          LDX   0  PMADDR(1)           [CHECK IDENT. NO. CORRECT
   8 9TYG          ANDN  0  #7777
   8 9W8# ...      SMO      FXPM1
  10 9WD6          SBX   0  PMIDENTS(3)  
   8 9WXQ          BNZ   0  PNXT2
  21 9XCB          LDN   0  1                   [SET MARKER TO SAY REQD. S.S. FOUND  
   8 9XM8 ...      SMO      FXPM1
   9 9XX2          STO   0  SFOUND   
   8 9Y6S ...      SMO      FXPM1
  10 9YBL          LDXC  0  PMADDR+2(1)  
  16 9YW=          BCS      UNAV                [J IF UNAVAILABLE
   8 9_64 ...      SMO      FXPM1
  10 9_*W          LDCH  0  PMADDR+2(1)  
   7 9_TG          SBN   0  3
  20 =2*6          BNZ   0  UNAV                [J IF NOT INPUT AND OUTPUT DEVICE
  17 =5QG    PNXT2 BZE   6  TFOUND2             [J IF NO MORE INPUT  
  18 =6=6          ADX   1  5                   [UPDATE BUFFER MODIFIER  
  18 =6PQ          BRN      NCH2                [GET NEXT INPUT SEGMENT  
   8 =747 ...TILL2 SMO      FXPM1
   9 =7BJ ...      LDX   0  PMADDR(1)
  21 =7P2          ANDN  0  #7777               [CHECK IF ILLEGAL FOR CURRENT IDENT. 
   8 =7YS ...      SMO      FXPM1
  10 =88L          SBX   0  PMIDENTS(3)  
  16 =8N=          BNZ   0  PNXT2               [IGNORE IF NOT   
   8 =8Y4 ...      SMO      FXPM1
  17 =97W          LDX   0  PMADDR+1(1)         [CHECK IF ILLEGAL FOR
   8 =9CN ...      SMO      FXPM1
  17 =9MG          ANDX  0  SSMASK              [OPEN IDENTIFIER AND 
   8 =9X# ...      SMO      FXPM1
  16 ==76          BXU   0  POPENED,PNXT2       [IGNORE IF NOT   
  20 ==LQ          LDN   0  1                   [MARK TO SAY REQUIRED S.S. FOUND 
   8 ==WJ ...      SMO      FXPM1
   9 =?6B          STO   0  SFOUND   
  20 =?DR ...UNAV  SMO      FXPM1               [ZEROISE TABLE ENTRY TO GEORGE   
  10 =?R8 ...      STOZ     PMIDENTS(3)  
   8 =#5L          BRN      PNXT2
   5 =#K=    SINOP5  
  17 =*4W          CALL  7  PMINOP              [TRY TO REOPEN IPB   
   8 =*JG          BRN      OK3  
   5 =B46    TFOUND2 
   8 =B?Y ...      SMO      FXPM1
   9 =BHQ          LDX   0  SFOUND   
  21 =C3B          BZE   0  OK3                 [READ AGAIN IF REQD. S.S. NOT FOUND  
   8 =C?8 ...      SMO      FXPM1
   9 =CH2          STOZ     SFOUND   
   8 =CQS ...      SMO      FXPM1
  10 =D2L          LDX   0  PMIDENTS(3)  
  18 =DG=          BZE   0  OK4                 [J IF NO OUTPUT REQUIRED 
   8 =DQ4 ...      SMO      FXPM1
  21 =D_W          STO   0  PMADDR5+1           [OTHERWISE INSERT IDENT. NO. IN THE  
   8 =F9N ...      SMO      FXPM1
  18 =FFG ...      STO   0  PMADDR6+1           [^SYSTEM CLOSED^ MESSAGES
  20 =F_6    TAGEN PMOLCH   TINP4               [CHECK WHETHER OVERLOAD ETC., AND
  16 =GDQ                                       [J TO INPUT IF SO
   8 =GGS ...      SMO      FXPM1
  21 =GJX ...      LDX   0  PMIDENTS(3)         [CHECK DEVICE TYPE FOR 7071 OR 7181/2
   8 =GL_ ...      SMO      FXPM1
  10 =GP3 ...      LDX   6  PMADEVSADDR  
   7 =GR5 ...      SMO      6
  19 =GT9 ...      SBX   0  A1+FOURTHWD         [- LOWEST IDENTIFIER NUMBER  
  18 =G_B ...      SLL   0  2                   [RELATIVE ENTRY POINTER  
   8 =H3D ...      SMO      FXPM1
  10 =H5H ...      ADX   0  PMADEVSADDR  
   7 =H9N ...      SMO      0
  15 =H*T ...      LDEX  0  A1+FOURTHWD         [DEVICE TYPE 
   8 =HDK ...      SBN   0  10   
  16 =HH* ...      BNZ   0  TTW                 [J IF NOT 7181   
  21 =HL7 ...      LDN   0  PMLEN6[SET UP CONTROL AREA FOR 7181/2 ^SYSTEM CLOSED^MESS
  12 =HQ# ...      DCA      CPPTR(2),DSA,0,COUNT 
   8 =HSB ...      SMO      FXPM1
   9 =HWF ...      LDN   0  PMADDR6  
  12 =J2L ...      DCA      CPPTR(2),STO,0,CTSA  
   8 =J6R ...      BRN      SMESS
  21 =J?2 ...TTW   LDN   0  PMLEN5[SET UP CONTROL AREA FOR 7071/7023^SYSTEM CLOSED^  
  12 =JC=          DCA      CPPTR(2),DSA,0,COUNT 
   8 =JM4 ...      SMO      FXPM1
   9 =JWW          LDN   0  PMADDR5  
  12 =KBG          DCA      CPPTR(2),STO,0,CTSA  
  20 =KHN ...SMESS LDN   0  #11                 [OUTPUT ^SYSTEM CLOSED^ MESSAGE  
  12 =KNW ...      DCA      CPPTR(2),DSA,0,MODE  
   9 =KW6    P10   CALL  6  REPCHEK  
  18 =L*Q          BRN      TINP4               [TRY INPUT IF REJECTED   
  15 =LTB          BRN      SINOP6              [INOPERABLE  
  15 =M*2          BRN      P10                 [T.O. OR N.I.
   8 =MJS ...      SMO      FXPM1
   8 =MSL          LDX   0  PMRW 
   8 =N#=          ANDN  0  #777 
  17 =NRW          BZE   0  OK4                 [J IF WRITE NOT ENDED
  15 =P?G    TINP4 CALL  6  READ                [TRY TO READ 
  14 =PR6          BRN      TAGEN               [J IF OK 
   5 =Q=Q    SINOP6  
  17 =QQB          CALL  7  PMINOP              [TRY TO REOPEN IPB   
   8 =R=2          BRN      TAGEN
   7 =RPL    OK4   SBN   3  1
  21 =S9= ...      BPZ   3  NXMOP               [GO TO NEXT TABLE ENTRY IF PRESENT   
   8 =SHM ...T7023 SMO      FXPM1
   9 =SW4 ...      LDX   0  WHICHCONS
  21 =T8G          BNZ   0  NEXT                [GET NEXT UNIT IF FINISHED WITH THIS 
  18 =TN6          LDN   0  1                   [IF NOT DEAL WITH 7023   
   8 =TXY ...      SMO      FXPM1
   9 =W7Q          STO   0  WHICHCONS
  20 =WMB          LDN   0  50                  [AMEND CHARACTER COUNT TO IGNORE 
   8 =WX8 ...      SMO      FXPM1
  16 =X72          DSA   0  PMADDR5+2           [#7613 FOR 7023  
   9 =XLL          BRN      SEARCH   
  20 =Y6=    TYPE7023                           [COME HERE TO TEST FOR 7023 TYPE 
   7 =YKW          SBN   0  5
   8 =_5G          BRN      TYPE 
   8 =_G2 ...NEXT  LDX   1  FXPM1
  18 =_WG ...      STOZ     WHICHCONS(1)        [CLEAN UP FOR NEXT UNIT  
  10 ?2?2 ...      STOZ     PRIORITY(1)  
  10 ?2MG ...      STOZ     PMCOUNT(1)   
  21 ?342          LDN   0  52                  [RESET CHARACTER COUNT FOR 7071 ON   
  18 ?3HL ...      DSA   0  PMADDR5+2(1)        [NEXT IPB, IF PRESENT.   
  17 ?43=          BRN      NEXT1               [LOOK FOR NEXT UNIT  
   4 ?4GW    #END
   8 ____ ...53060030000500000000
