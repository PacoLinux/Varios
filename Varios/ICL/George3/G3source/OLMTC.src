   7 22FL    #LIS  K0OLMTC   
  15 22_=    #SEG           OLMTC65             [N.R.BOULT   
   9 23DW                   8HOLMTC  
  11 23YG          SEGENTRY K1OLMTC,QK1OLMTC 
  11 24D6          SEGENTRY K2OLMTC,QK2OLMTC 
  11 24XQ          SEGENTRY K3OLMTC,QK3OLMTC 
   8 25CB    [LIST OF CONSTANTS  
  20 25X2    OWNMON         #34000              [OWN MONITORING(OPENFILE) BITS   
  19 26BL    MASKMON        #76000              [MASK FOR OWN MONITORING BITS
   9 26W=    QMT            #55640000
   8 27*W    QTEN           100  
   8 27TG    REELMAX        +512 
   9 28*6    REMPROP        #77700077
   9 28JY ...PRIVFR         8HFREEZE 
   4 28SQ    [   
  15 29#B    [ENTRY POINT FOR PERIS MODE #100,#200,AND #300  
  18 29S2    [ENTRY IS FROM PERIXCOD, WITH X4=TYPE(B0-8)/UNIT NO(B9-23), 
   4 2=?L    [   
   5 2=R=    QK1OLMTC
  10 2?=W          ANDX  4  REMPROP(1)   
  21 2?QG    [SET UP CPAR/GPERCON BLOCK & INSERT TYPE,UNIT NR & REQUIREMENTS FOR WP R
  11 2#=6          SETNCORE 3,3,CPAR,GPERCON 
  16 2#PQ          STO   4  A1(3)               [TYPE/UNIT NO.   
  21 2*9B          STOZ     A1+1(3)             [NO WPR CONDITIONS UNLESS PROVED OTHE
   9 2*P2          STOZ     A1+2(3)  
  18 2B8L          LDX   0  EVENT4(2)           [1ST WORD OF CONTROL AREA
   9 2BN=          SETUPMODE 7,,READ 
   8 2C7W          ANDN  0  #777 
  15 2CMG          SRL   0  6                   [MODE/#100   
   7 2D76          SMO      0
  21 2DLQ    SWIT  BRN      SWIT                [SWITCH ON MODE TO INSERT WP RING DET
  20 2F6B          BRN      Q1                  [MODE #100,WP RING NOT SPECIFIED 
  21 2FL2          BRN      Q2                  [MODE #200,WP RING SPECIFIED ABSENT  
  21 2G5L          BRN      Q3                  [MODE #300,WP RING SPECIFIED PRESENT 
   4 2GK=    Q3  
   9 2H4W          SETUPMODE 7,,WRITE
   7 2HJG          LDN   0  1
  16 2J46          STO   0  A1+1(3)             [SET WP RING BIT 
   7 2JHQ    Q2    LDN   0  1
  18 2K3B          STO   0  A1+2(3)             [SET WPR SPECIFIED BIT   
  17 2KH2    [SET UP FILE/FABSNB AND MOVE PERI CONTROL AREA INTO IT. 
   4 2L2L    Q1  
  11 2LG=          SETNCORE 11,1,FILE,FABSNB 
  16 2L_W          LDN   6  9                   [LENGTH OF AREA  
  15 2MFG          CHECKEXT EVENT2(2),6,RERR2B,,W,Q5,HLOCK1  
   9 2M_6    [CONTROL AREA IN CORE   
  10 2NDQ    Q5    HUNT     1,FILE,FABSNB
   9 2NYB          LDN   1  A1+4(1)  
   7 2PD2          LDN   3  2
  21 2PXL          ADX   3  EVENT2(2)           [ADDRESS OF WORD 2 OF CONTROL AREA   
   7 2QC=          LDN   6  6
   9 2QWW          OUTMOVE  FX2,3,1,6
  21 2RBG ...      LDX   5  EVENT4(2)           [X5 => FIRST WORD OF PERI CONTROL ARE
  18 2RW6 ...      LDX   6  EVENT2(2)           [X6 -> PERI CONTROL AREA 
  17 2S*Q ...      ADN   6  2                   [X6 -> REPLY WORD + 1
  17 2STB    [MAKE OBJ PROG SWAPPABLE,HAVING EXTRACTED REQUIRED DATA.
  13 2T*2          CAPCA    FILE,FABSNB,CPAR,GPERCON 
   7 2TD6 ...#UNS  JWPHASE4  
   4 2TH= ...(   
  21 2TLB ...      HLSXONCH PBRKIN,4,QHLSMODE   [ASK HLS IF JOB CAN HAVE ANUVER TAPE 
   4 2TPG ...)   
  12 2TSL    [REARRANGE DETAILS IN FABSNB BLOCK  
  15 2W#=          HUNT     3,FILE,FABSNB       [LOCATE BLOCK
   8 2WRW          LDN   0  10   
  21 2X?G          STO   0  A1(3)               [PUT IN RECORD HEADER FOR 10 WD RECOR
  10 2XR6          LDN   0  CPREFIX(2)   
   9 2Y=Q          LDN   1  A1+1(3)  
  19 2YQB          MOVE  0  3                   [MOVE IN CURRENT USER NAME   
   9 2_=2          LDX   0  A1+9(3)  
  18 2_PL          STO   0  A1+10(3)            [MOVE UP RETENTION PERIOD
  18 329=          STOZ     A1+9(3)             [SET LANGUAGE WORD ZERO  
  13 32NW          LDX   0  A1+7(3)             [REEL
   8 338G          SMO      FX1  
   9 33N6          TXL   0  REELMAX  
  20 347Q          BCC      NERIL               [BRANCH IF REEL NO OUT OF RANGE  
   8 34MB          BNZ   0  ZER  
   8 3572          LDCT  0  #100 
  16 35LL          STO   0  A1+7(3)             [ZERO MEANT BIT  
   4 366=    ZER 
  15 36KW          LDX   0  A1+8(3)             [GENERATION  
  16 375G          BZE   0  QGIA                [ANY GENERATION  
  14 37K6          ANDN  0  #7777               [TRUNCATE
  15 384Q          BNZ   0  QGI                 [NOT NOW ZERO
  17 38JB          LDCT  0  #100                [ZERO SPECIFIED BIT  
  14 3942    QGI   STO   0  A1+8(3)             [RESET   
  18 39HL    QGIA  LDN   0  #1001               [SET LOCAL NAME ONLY AND 
  17 3=3=          ORS   0  ATYPE(3)            [MT BITS FOR USEROPEX
   7 3=GW    [GO TO GET TAPE 
  18 3?2G          SETUPMODE ,3,HDDRLAB         [LEAVE FLIB/GOLMT BLOCK  
   8 3?G6          LDX   1  FX1  
   7 3?_Q          LDX   0  5
  10 3#FB          ANDX  0  MASKMON(1)   
   9 3#_2          TXU   0  OWNMON(1)
  17 3*DL          BCS      QON1                [NOT OWN MONITORING  
  19 3*Y=          ADDINMODE ,3,TAPEUP          [DONT WAIT IF TAPE NOT LOADED
   4 3BCW    QON1
   7 3BG5 ...#UNSET   FTS1   
   4 3BJ# ...(   
  21 3BLH ...      CHEKPRIV 0,PRIVFR,NOFP       [CHECK IF PROPER USER HAS FREEZE PVLG
  19 3BNQ ...      ADDINMODE ,3,FROZEN          [USER CAN OPEN TAPE IF FROZEN
   4 3BQ_ ...NOFP
   4 3BT8 ...)   
  10 3BXG          USEROPEX PBRKIN,7,3   
  18 3CC6    [TEST IF TAPE OBTAINED AND IF SO CONNECT TO OBJECT PROGRAM  
  15 3CWQ          TESTREPNOT OK,Q8             [J IF NO TAPE
  17 3DBB          MFREE    CPAR,GPERCON        [RELEASE DATA BLOCKS 
  10 3DW2          MFREE    FILE,FABSNB  
  21 3F*L          VFREE    FILE,ENT            [THIS BLOCK MAY BE SET UP BY T.R.H.  
  20 3FT=          GSCAN    4,OLMCPER           [TRACE X4 MODES #100,#200,#300,  
   9 3G#W          LDX   3  CPPTR(2) 
  18 3GJN ...      LDX   7  6                   [PUTPERENT CORRUPTS X6   
   8 3GSG          PUTPERENT 4,3 
  15 3H4# ...      LDX   6  7                   [REINSTATE X6
   9 3H#6          STOZ     CPPTR(2) 
  13 3HRQ    [SET REPLY AND RESTART OBJECT PROGRAM   
   9 3J?B          OUTPAR   TIMENOW  
   8 3JR2          LGEOG    3,5  
   4 3K=L    SJ1 
  11 3KQ=          OUTPACK  5,1,GEOPER,SJ1   
   4 3L9W    SJ2 
  11 3LPG          OUTPACKB 4,1,PERINAME,SJ2 
   9 3M96          DFLOAD   3,TSN,5  
   4 3MNQ    SJ2A
  10 3N8B          OUTPACKB 5,1,TSN,SJ2A 
   4 3NN2    SJ3 
  10 3P7L          MHUNT    3,FLIB,GOLMT 
  12 3PM=          OUTPACKB A1+4(3),3,LOCNAME,SJ3
   4 3Q6W    SJ4 
  10 3QLG          MHUNT    3,FLIB,GOLMT 
  12 3R66          OUTPACKB A1+8(3),1,GENNUM,SJ4 
   4 3RKQ    SJ5 
  10 3S5B          MHUNT    3,FLIB,GOLMT 
  12 3SK2          OUTPACKB A1+7(3),1,REELNUM,SJ5
   8 3T4L          OUTPARB  SKIP 
   8 3TJ=          MONOUT   JPER 
  21 3TRC ...      LDN   4  8                   [NUMBER OF WORDS TO CHECK AND MOVE   
  16 3W2J ...      SBN   6  1                   [X6 -> REPLY WORD
  21 3W9P ...      LDN   7  0                   [X7 USED IF CONTROL AREA OVER PAGE BO
  20 3WDW ...SJLOOP1                            [LOOP IF TWO GETWORDS NECESSARY  
  16 3WN3 ...      GETWORD  6,3,WRITE,5,4       [GET CORE IMAGE  
  13 3WX8 ...      TESTREPN OK,NERR2          [GEOERR
  18 3X6* ...      MHUNT    2,FLIB,GOLMT        [CONTAINS REPLY DETAILS  
  12 3X*G ...      SMO      7                   [
  19 3XJM ...      LDN   2  A1+3(2)             [X2 -> FIRST WORD TO BE MOVED
  20 3XRS ...      BXL   5  4,SJLOOP2           [J IF ANOTHER GETWORD NECESSARY  
  20 3Y2_ ...      LDX   5  4                   [X5 => NUMBER OF WORDS TO MOVE   
  12 3Y=6 ...SJLOOP3                            [
  20 3YF? ...      SMO      5                   [X5 => NUMBER OF WORDS TO MOVE   
  17 3YND ...      MOVE  2  0                   [UPDATE CONTROL AREA 
  21 3YXK ...      BXU   4  5,SJLOOP1           [J IF ANOTHER GETWORD IS NECESSARY   
  17 3_6Q ...      MFREE    FLIB,GOLMT          [NO LONGER REQUIRED  
  19 3_*X ...      WORDFIN                      [TERMINATE GETWORD SEQUENCE  
  16 3_K4 ...      RUNPROG                      [RERUN PROGRAM   
  12 3_S9 ...SJLOOP2                            [
  21 423B ...      SBX   4  5                   [X4 => REMAINING WORDS FOR NEXT GETWO
  21 42=H ...      LDX   7  5                   [X7 => NUMBER OF WORDS MOVED THIS TIM
  19 42FN ...      ADX   6  5                   [UPDATE CONTROL AREA POINTER 
  19 42NT ...      BRN      SJLOOP3             [DO MOVE AND SECOND GETWORD  
  16 42Y6    [PREPARTION FOR SETTING REPLY IN OWN MONITORING CAS[
   4 43CQ    Q7  
  14 43XB          LDCT  4  #400                [REPLY   
  17 44C2          BRN      SETIN               [J TO INSERT REPLY   
   5 44WL    PBRKIN  
   7 45B=          STEPBACK  
  19 45TW          PROGBRKIN                    [RE-ENTER COMMAND PROCESSOR  
   7 45YF ...#UNS JWPHASE4   
   4 4634 ...(   
   5 465M ...QHLSMODE
  21 468= ...      MONOUT   ERHLSMODE           [HLS HAS FORBID ACQUISITION OF TAPE  
   4 46=T ...)   
  17 46*G    Q8    VFREE    FILE,ENT            [RELEASE DATA BLOCKS 
  10 46T6          MFREE    FILE,FABSNB  
  10 47#Q          MFREE    CPAR,GPERCON 
  16 47SB          ANDX  5  MASKMON(1)          [MONITORING BITS 
  18 48#2          TXU   5  OWNMON(1)           [TEST IF OWN MONITORING  
  21 48RL          BCC      Q7                  [YES-J TO SET SIGN BIT OF REPLY WORD 
   4 49?=    QI  
   7 49QW ...      LDCT  5  5
   8 4==G ...      ANDX  4  BSP16
   7 4=Q6 ...      ADX   5  4
  11 4?9Q ...      OUTPACK 5,1,PERINAME,QI   
  18 4J32          STEPBACK                     [STEPBACK OBJECT PROGRAM 
   9 4JGL          ILLEGAL  PERFAIL  
  15 4K2=    [ENTRY POINT FOR OPEN MODE #500 (RENAME) PERIS  
  12 4KFW    [X4,5 HOLD A COPY OF THE PRB ENTRY  
   5 4K_G    QK2OLMTC
   8 4LF6    #SKI  K6OLMT>99-99  
   4 4LYQ    (   
  10 4MDB          SWITRACE 4,OLTCPRB1   
  10 4MY2          SWITRACE 5,OLTCPRB2   
   4 4NCL    )   
  19 4NX=          DFJUMPS  5,NONS,NERO         [J/MP IF NON STANDARD TAPE   
  21 4PBW    [NOW CHECK RESERVATIONS OF CONTROL AREA :EXEC2 OF PCA CONTAINS PROGRAM  
   9 4PWG    [ADDRESS OF CONTROL AREA
  11 4QB6          SETNCORE 7,1,AONBS,GREN   
   7 4QTQ          LDN   1  9
  15 4R*B          CHECKEXT EVENT2(2),1,RERR2A,,W,P3,HLOCK1  
  10 4RT2    P3    MHUNT    1,AONBS,GREN 
  21 4S#L    [MOVE OUT WORDS OF CONTROL AREA REQUIRED IN THE RENAMING OF THE TAPE.   
   9 4SS=          LDN   1  A1+1(1)  
   9 4T?W          LDX   6  EVENT2(2)
  20 4TRG          ADN   6  2                   [ADD. NAME IN CA. (USED AT SETIN)
   7 4W?6          LDN   3  6
   9 4WQQ          OUTMOVE  FX2,6,1,3
  13 4X=B    #     CHECK VALIDITY OF NEW TAPE NAME   
  16 4XQ2          MHUNT    3,AONBS,GREN        [FIND NAME BLOCK 
   9 4Y9L          LDCH  0  A1+1(3)  
   8 4YP=          SBN   0  #41  
  15 4_8W          BNG   0  NERRR               [ERROR < A   
   8 4_NG          SBN   0  #32  
  15 5286          BPZ   0  NERRR               [ERROR > Z   
  15 52MQ          LDN   1  11                  [COUNT OF 11 
  15 537B    NMCH1 BCHX  3  /                   [STEP POINTER
   9 53M2          LDCH  0  A1+1(3)  
   8 546L          SBN   0  #41  
  14 54L=          BPZ   0  NMCH2               [J >= A  
   8 555W          ADN   0  23   
  15 55KG          BNG   0  NMCH3               [OK IF DIGIT 
   7 5656          SBN   0  6
  15 56JQ          BZE   0  NMCH3               [OK IF SPACE 
   8 574B          SBN   0  #15  
  15 57J2          BZE   0  NMCH3               [OK IF HYPHEN
  15 583L          BRN      NERRR               [ELSE ERROR  
   8 58H=    NMCH2 SBN   0  #32  
  15 592W          BPZ   0  NERRR               [ERROR > Z   
  21 59GG    NMCH3 BCT   1  NMCH1               [IF CHECK NOT FINISHED,BACK FOR NEXT 
   7 5=26          BCHX  3  /
   9 5=FQ          LDX   0  A1+1(3)  
   8 5=_B          SMO      FX1  
   9 5?F2          TXL   0  REELMAX  
   8 5?YL          BCC      NERRR
  20 5#D=    [CHANGE INTO COMMAND ACTIVITY,RECHAINING DATA BLOCK AND LEAVING THE 
  15 5#XW    [OBJECT PROGRAM SWAPPABLE WHILE RENAMING IS DONE
  16 5*CG          LDX   7  EVENT4(2)           [USED AT LABEL P2
  10 5*X6          CAPCA    AONBS,GREN   
   9 5BBQ          STO   5  CPPTR(2) 
  10 5BWB          STO   4  ACOMMUNE1(2) 
  10 5CB2          STOZ     ACOMMUNE2(2) 
  10 5CTL          STOZ     ACOMMUNE3(2) 
  17 5D*=          GSCAN    4,OLCRMT            [TRACE X4- MODE #400 
  17 5DSW          DOWN     RENAMEMT,1          [GO TO RENAME TAPE   
  16 5F#G          BRN      NEREL               [NO WPR,ILLEGAL  
   8 5FS6          TESTREPN OK,P2
  10 5G?Q          MHUNT    1,AONBS,GREN 
   8 5GRB          LDX   4  A1(1)
   7 5H?2          FREECORE 1
  12 5HQL    [FILL IN REPLY WORD OF CONTROL AREA 
   5 5J==    SETIN   
  18 5JPW          SBN   6  1                   [ADDRESS OF REPLY WORD   
   9 5K9G          GETWORD  6,3,WRITE
  10 5KP6          TESTREPNOT  OK,NERR2  
   8 5L8Q          STO   4  0(3) 
  16 5LNB          WORDFIN                      [RESET OBJ PROG  
   7 5M82          RUNPROG   
   4 5MML    [   
  12 5N7=    [DEAL WITH FAILURE TO RENAME CASE   
   4 5NLW    P2  
  10 5P6G          MFREE    AONBS,GREN   
  10 5PL6          ANDX  7  MASKMON(1)   
  10 5Q5Q          BXU   7  OWNMON(1),QI 
   8 5QKB          LDX   4  GSIGN
  20 5R52          BRN      SETIN               [IF THERE,J TO SET REPLY NEGATIVE
   4 5RJL    #   
   4 5S4=    #   
  10 5SHW    NEREL MFREE    AONBS,GREN   
   8 5T3G          ILLEGAL  NOWPR
  10 5TH6    NERIL MFREE    FILE,FABSNB  
   8 5W2Q          BRN      NERRR
   5 5WGB    RERR2A  
  10 5X22          MFREE    AONBS,GREN   
   8 5XFL          BRN      RERR2
   5 5X_=    RERR2B  
  10 5YDW          MFREE    FILE,FABSNB  
  10 5YYG          MFREE    CPAR,GPERCON 
   8 5_D6          BRN      RERR2
   4 5_XQ    RERR
  10 62CB          MFREE    FLIB,GOLMT   
   8 62X2    RERR2 ILLEGAL  NOCA 
   9 63BL    NERRR ILLEGAL  ILLINS   
   8 63W=    NERO  ILLEGAL  MODE 
   9 64*W    NERR2 GEOERR   1,NOIMAGE
   5 64TG    #PAGE   
   4 65*6    #   
  20 65SQ    #                                                          K3OLMTC  
   4 66#B    #   
  10 66S2    [ENTRY POINT FOR MODE #600  
   4 67?L    [   
   5 67R=    QK3OLMTC
  10 68=W          ANDX  4  REMPROP(1)   
   9 68QG          LDX   6  EVENT2(2)
   7 69=6          ADN   6  2
  20 69PQ          LDN   1  9                     [CHECK RESERVATION AND ENSURE  
  15 6=9B          CHECKEXT EVENT2(2),1,RERR2,,W,QZ,HLOCK1   
   4 6=P2    QZ  
   9 6?8L          LDX   5  EVENT4(2)
   7 6?N=          LDN   3  0
  16 6#7W          LDX   7  EVENT3(2)           [X OR REPLACED X 
   8 6#MG    #SKI  K6OLMT>99-99  
  10 6*76          SWITRACE 7,OLTCPROP   
   7 6*LQ          SRL   7  6
  19 6B6B          ANDN  7  #777                [EXTRACT ANY PROPERTY CODE   
  20 6BL2          BZE   7  QPR9                [BRANCH IF NO PROPERTY SPECIFIED 
   7 6BNK ...#UNS  JMODGCR   
   8 6BR8 ...      MTHWSPD  7,3  
   7 6BTR ...#UNS  JMODGCR   
   4 6BYB ...#SKI
   4 6C2_ ...(   
   8 6C5L          LDN   0  #400 
   7 6CK=          ANDX  0  7
  17 6D4W          BZE   0  QPR2                [BRANCH UNLESS 'NONE'
  15 6DJG          LDN   0  #200                [9 TRACK BIT 
   7 6F46          ANDX  0  7
   8 6FHQ          BNZ   0  QPR1 
   9 6G3B          LDN   3  GLIBTRA9 
   8 6GH2          BRN      QPR9 
   4 6H2L    QPR1
   9 6HG=          LDN   3  GLIBTRA7 
   8 6H_W          BRN      QPR9 
   4 6JFG    QPR2
  15 6J_6          LDN   0  #200                [9 TRACK BIT 
   7 6KDQ          ANDX  0  7
  17 6KYB          BNZ   0  QPR3                [BRANCH IF 9 TRACK   
   9 6LD2          LDN   3  GLIBTRA7 
   8 6LXL          BRN      QPR6 
   4 6MC=    QPR3
   7 6MWW          LDN   0  3
   7 6NBG          ANDX  0  7
   7 6NW6          SBN   0  1
   8 6P*Q          BNZ   0  QPR4 
   9 6PTB          LDN   3  GLIBNRZI 
   8 6Q*2          BRN      QPR6 
   4 6QSL    QPR4
   7 6R#=          SBN   0  1
   8 6RRW          BNZ   0  QPR5 
   9 6S?G          LDN   3  GLIBPE   
   8 6SR6          BRN      QPR6 
   4 6T=Q    QPR5
   9 6TQB          LDN   3  GLIBTRA9 
   4 6W=2    QPR6
  15 6WPL          LDN   0  #140                [B11-12 SPEED
   7 6X9=          ANDX  0  7
   8 6XNW          BZE   0  QPR9 
   8 6Y8G          ANDN  0  #100 
   8 6YN6          BZE   0  QPR7 
   9 6_7Q          ADN   3  GLIBFAST 
   8 6_MB          BRN      QPR9 
   4 7272    QPR7
   9 72LL          LDN   0  GLIBTRA7 
   7 736=          ANDX  0  3
   8 73KW          BZE   0  QPR9 
   9 745G          ADN   3  GLIBSLOW 
   4 74*# ...)   
   4 74K6    QPR9
   7 754Q          LDX   7  3
   8 75JB    #SKI  K6OLMT>199-199
   9 7642          SWITRACE 7,OLTCSPD
   6 76HL          CAPCA 
   7 76NS ...#UNS JWPHASE4   
  21 76W2 ...      HLSXONCH PBRKIN,4,QHLSWK     [ASK HLS IF JOB CAN HAVE ANUVER TAPE 
   7 773=          LDX   0  5
  10 77GW          ANDX  0  MASKMON(1)   
   9 782G          TXU   0  OWNMON(1)
  16 78G6          BCS      QN1                 [NOT MONITORED   
  15 78_Q          SETUPMODE ,0,TAPEUP          [OWN MONITOR 
   8 79FB          BRN      QN2  
   7 79_2    QN1   LDN   0  0
   9 7=DL    QN2   STO   0  AWORK2(2)
   7 7=Y=          LDX   3  4
  21 7?CW          LDN   4  7                   [WORKTAPE,UPDATE C O R ,SET UP FABSKB
  10 7?XG          STO   7  ACOMMUNE1(2) 
  16 7#C6          DOWN     LIBREQC,1           [GET WORK TAPE   
  14 7#WQ          BRN      PBRKIN              [BREAKIN 
   7 7*BB          LDX   4  3
  17 7*W2          TESTREPNOT OK,QJ             [TAPE NOT AVAILABLE  
  19 7B*L          GSCAN    4,OLCPLT            [TRACEX4-MODE #600- POOL TAPE
   9 7BT=          LDX   3  CPPTR(2) 
   9 7C#W          STOZ     CPPTR(2) 
  17 7CSG          STO   6  7                   [PUTPERENT USES X6   
  16 7D#6          PUTPERENT 4,3                [SET UP PRB ENTRY
   9 7DRQ          OUTPAR   TIMENOW  
   8 7F?B          LGEOG    3,5  
   4 7FR2    QSJ1
  11 7G=L          OUTPACK  5,1,GEOPER,QSJ1  
   4 7GQ=    QSJ2
  11 7H9W          OUTPACKB 4,1,PERINAME,QSJ2
   9 7HPG          DFLOAD   3,TSN,5  
   4 7J96    QSJ3
  10 7JNQ          OUTPACKB 5,1,TSN,QSJ3 
   4 7K8B    QSJ4
  10 7KN2          MHUNT    2,FILE,FABSNB
   9 7L7L          SMO      HDREC(2) 
   8 7LM=          ADN   2  A1-6 
  12 7M6W          OUTPACKB 0(2),3,LOCNAME,QSJ4  
   4 7MLG    QSJ5
  10 7N66          MHUNT    2,FILE,FABSNB
   9 7NKQ          SMO      HDREC(2) 
   8 7P5B          ADN   2  A1-6 
  12 7PK2          OUTPACKB 4(2),1,GENNUM,QSJ5   
   4 7Q4L    QSJ6
  10 7QJ=          MHUNT    2,FILE,FABSNB
   9 7R3W          SMO      HDREC(2) 
   8 7RHG          ADN   2  A1-6 
  12 7S36          OUTPACKB 3(2),1,REELNUM,QSJ6  
   8 7SGQ          OUTPARB  SKIP 
   8 7T2B          MONOUT   JPER 
   7 7TG2          SBN   7  1
   9 7T_L          GETWORD  7,3,WRITE
   8 7WF=          TESTREP  OK,QA
  11 7WYW          GEOERR   BRIEFPM,GETWORD  
   8 7XDG    QA    STO   5  0(3) 
   7 7XY6          WORDFIN   
   7 7YCQ          RUNPROG   
   7 7YG* ...#UNS  JWPHASE4  
   4 7YJY ...(   
   5 7YMH ...QHLSWK  
   9 7YQ6 ...      MONOUT   ERHLSMODE
   4 7YSP ...)   
  20 7YXB    QJ    ANDX  5  MASKMON(1)          [MASKOUT 'OWN MONITORING' BITS   
  16 7_C2          TXU   5  OWNMON(1)           [TEST IF PRESENT 
  15 7_WL          BCC      Q7                  [INSERT REPLY
   8 82B=          BRN      QI   
  10 82TW          MENDAREA 50,K99OLMTC  
   4 83*G    #END
   8 ____ ...63672345000400000000
