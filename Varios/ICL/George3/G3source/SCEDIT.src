  15 22FL    #SEG  SCEDIT                       [LEN WAGSTAFF
   7 22_=    #OPT  K0SCEDIT=0
   9 23DW    #LIS  K0SCEDIT>K0ALLGEO 
   7 23YG          8HSCEDIT  
   4 24D6    #   
   4 24XQ    #   
  11 25CB          SEGENTRY K1SCEDIT,X1SCEDIT
  15 25X2    # ENTRY POINT TO IMPLEMENT 'SCREENEDIT' COMMAND 
  11 2648 ...      SEGENTRY K2SCEDIT,X2SCEDIT
  15 269B ...# ENTRY POINT TO WRAP UP FOR E AND Q COMMANDS   
   4 26BL    #   
   4 27*W    #   
  20 27B9 ...ZCOMDEV        +JCOMDEV            [MAY NOT BE ISSUED FR THIS DEVICE
  17 27BJ ...ZPARMIS        +JPARMIS            [PARAMETER MISSING   
  20 27BX ...ZEDSHRIEK      +EDSHRIEK           [NULL 2NDPARA NOT ALLOWED WITH ! 
  18 27C= ...ZNOEDIT        +JNOEDIT            [NOT SUITABLE FOR EDITING
  17 27CK ...ZPERITYPE      +ERPERITYPE         [WRONG TYPE OF FILE  
   4 27CY ...#   
   9 27D? ...ZER2  LDN   6  ZCOMDEV  
   8 27DL ...      BRN      XER3 
   9 27D_ ...ZER3  LDN   6  ZPARMIS  
   8 27F# ...      BRN      XER3 
   9 27FM ...ZER4  LDN   6  ZEDSHRIEK
   8 27G2 ...      BRN      XER2 
   9 27G* ...ZER5  LDN   6  ZNOEDIT  
   8 27GN ...      BRN      XER1 
   9 27H3 ...ZER6  LDN   6  ZPERITYPE
   8 27HB ...      BRN      XER2 
   4 27HP ...#   
   8 27J4 ...XER1  CLOSEABANDON  
   6 27JC ...XER2  CLOSE 
   8 27JQ ...XER3  LDX   1  FX1  
   7 27K5 ...      ADX   1  6
   8 27KD ...      LDX   6  0(1) 
   7 27KR ...      COMERRX  6
   4 27L6 ...#   
   8 27LF ...PARA  SBX   7  FX1  
   7 27LS ...      SPARAPAS  
  10 27M7 ...      MHUNT    3,CPB,CUNI   
   8 27MG ...      ADX   7  FX1  
   7 27MT ...      EXIT  7  0
   4 27N8 ...#   
   8 27NH ...CREA  SBX   7  FX1  
  17 27NW ...      CREATEB  3                   [CREATE FILE/CREATE  
   8 27P9 ...      LDCT  0  #400 
  15 27PJ ...      STO   0  CEINF1(3)           [SERIAL FILE 
   9 27Q= ...      LDX   0  AWORK2(2)
  16 27QK ...      STO   0  CETM(3)             [TYPE/MODE WORD  
   8 27QY ...      ADX   7  FX1  
   7 27R? ...      EXIT  7  0
   4 27RL ...#   
   9 27R_ ...# USE OF AWORK WORDS:   
  13 27S# ...#     AWORK1: ZERO=> OLDFILE IS WORKFILE
  17 27SM ...#     AWORK2: TYPE/MODE WORD FOR OLDFILE - USED BY S/R  
  16 27T2 ...#             'CREA' TO ASK FOR NEWFILE OF SAME TYPE
   4 27TG    #   
   9 27WR ...NOCOMBIT       #77776777
  19 27Y5 ...XISBITNG        #00010000         [FLAG FOR NON GRAPHIC FILES   
   5 282N ...SCOMMENT
   9 287W ...      CATMASK  COMMENT  
   5 28*6    X1SCEDIT
  17 28SQ ...      HUNTW    3,AMXOR,ADCB        [MAKE SURE ITS VDU   
   8 29#B ...      BNG   3  ZER2 
  10 2=?L          LDX   3  A1+FPTR(3)   
  10 2=R=          LDX   0  FOURTHWD(3)  
   8 2?=W          ANDN  0  #77  
   8 2?QG          SBN   0  10   
  15 2#=6          BNZ   0  ZER2                [J IF NOT VDU
   4 2#PQ    #   
  18 2*9B ...      CALL  7  PARA                [GET ^OLDFILE^ PARAMETER 
   9 2B8L          LDX   0  ANUM(3)  
  15 2BN=          BNG   0  ZER3                [PARA MISSING
   8 2C7W          BZE   0  ZER3 
  16 2CMG          LDCH  0  APARA(3)            [FLAG IN AWORK1..
  16 2D76          SBN   0  #21                 [..ZERO=>WORKFILE
  16 2DLQ          STO   0  AWORK1(2)           [..NON-ZERO ELSE 
   4 2F6B    #   
   7 2FL2          PHOTO    7
  10 2G5L          NAMETOP  3,FILE,FNAME 
  17 2GK= ...      FNORM    3                   [NORMALISE FILENAME  
   9 2H4W          TESTMOVE 7,NOHNT  
  10 2HJG          MHUNT    3,FILE,FNAME 
  19 2J46    NOHNT NAMETOP  3,CPB,CUNI          [IN CASE NEEDED FOR ERRORS   
  18 2JHQ          TESTREP  NAMEFORM,ZZZ        [J IF 1ST PARA RUBBISH   
   4 2K3B    #   
  11 2L_W          USEROPEN XBRK,READ,LEAVE  
  16 2MFG          REPERR   REPOK               [J IF OPENED OK  
  20 2M_6 ...      BRN      ZZZ                 [ELSE ERROR MESSAGE ALREADY GIVEN
   4 2NDQ    #   
  10 2NNJ ...REPOK MHUNTW   3,FILE,ENT   
   9 2NYB ...      LDX   0  ETM(3)   
  18 2P88 ...      BPZ   0  ZER6                [J IF FILE IS AMORPHOUS  
   9 2PD2 ...      STO   0  AWORK2(2)
  18 2PMS ...      CALL  7  PARA                [GET ^NEWFILE^ PARAMETER 
   9 2PXL          LDX   0  ANUM(3)  
  15 2QC=          BNG   0  NULL                [J IF NULL   
   8 2QWW          BZE   0  NULL 
   8 2RBG          BRN      NTNUL
   4 2RW6    #   
  17 2S*Q    NULL  LDX   0  AWORK1(2)           [IF 1ST PARA=!,NULL..
  18 2STB          BZE   0  ZER4                [..2ND PARA NOT ALLOWED  
  17 2T*2 ...      CALL  7  CREA                [SET UP FILE CREATE  
  16 2YQB          MHUNT    3,FILE,FABSNB       [LEFT BY USEROPEN
   9 2_5H ...      LDX   2  HDREC(3) 
   7 2_DN ...      ADX   2  3
  15 2_RT ...      LDX   0  A1-2(2)          [FGN OF OLDFILE 
   7 3272 ...      ADN   0  1
  16 32G7 ...NT4K  STO   0  A1-2(2)         [ADD 1 FOR NEWFILE   
  18 32T# ...      LDN   4  A1-6(2)          [FIND FILENAME FROM FABSNB  
   8 338F ...      LDX   2  FX2  
  10 33HL ...      MHUNT    3,CPB,CUNI   
  14 33WR ...      LDN   0  12               [NO OF CHARS
  16 349Y ...      STO   0  ANUM(3)          [STORE IN CPB CUNI  
   7 34K5 ...      LDX   5  3
   8 34Y= ...      ADN   5  APARA
  18 35?C ...      MOVE  4  3                [MOVE FILENAME INTO CPB CUNI
   8 35LL          BRN      OPEN 
   2 366=
   4 36KW    #   
  17 375G    NTNUL VFREE    FILE,FABSNB         [2ND PARA NOT NULL   
  10 37*# ...      VFREE    FILE,ADJUNCTS
   7 37K6          PHOTO    7
  10 384Q          NAMETOP  3,FILE,FNAME 
  15 38JB          FNORM    3                   [NORMALISE IT
   9 3942          TESTMOVE 7,NOHT2  
  10 39HL          MHUNT    3,FILE,FNAME 
  10 3=3=    NOHT2 NAMETOP  3,CPB,CUNI   
  18 3=GW          TESTREP  NAMEFORM,ZZ1        [J IF 2ND PARA RUBBISH   
   4 3?2G    #   
  16 3?G6 ...      TESTREPN ADJUNCTS,OPEN1      [J IF NO ADJUNCTS
  11 3?_Q          MHUNTW   3,FILE,ADJUNCTS  
  17 3#FB          LDCT  1  #400                [LOOK AT APPEND BITS 
   7 3#_2          ORS   3  1
   9 3*DL          LDCH  5  A1+1(3)  
   8 3*Y=          ANDN  5  #30  
   8 3BCW ...      ERN   5  #30  
  18 3BXG ...      BNZ   5  OPEN1               [J IF APPEND NOT GIVEN   
   4 3CC6    #   
  13 3CG= ...      USEROPEN XBRK2,APPEND,CREATE,LEAVE
  16 3CKB ...      REPERR   RPOK3               [J IF OPENED OK  
   8 3CNG ...      BRN      ZZ1  
   4 3CRL ...#   
   6 3CWQ ...RPOK3 WIND  
   8 3C_W ...      BRN      RPOK2
   4 3D52 ...#   
  17 3D86 ...OPEN1 CALL  7  CREA                [SET UP FILE'CREATE  
  15 3D?= ...OPEN  USEROPEN XBRK2,GENERAL,EMPTY,CREATE,LEAVE 
  16 3DBB          REPERR   RPOK2               [J IF OPENED OK  
   8 3DW2          BRN      ZZ1  
   4 3DYN ...#   
  10 3F3B ...RPOK2 MHUNTW   3,FILE,ENT   
   9 3F64 ...      LDX   0  ETM(3)   
   9 3F8Q ...      TXU   0  AWORK2(2)
  20 3F?D ...      BCS      ZER5                [J IF O/F NOT SAME TYPE AS N/F   
   7 3F#5 ...      SLL   0  1
  19 3F#Q ...      SBN   0  #24                   [LOOK AT INPUT FILE TYPE   
  14 3F*C ...      STO   0  7                    [SAVE IT
  21 3F*J ...      LDX   4  ZNEWNPU(1)           [CHANGE NPU SIZE DURING SCEDITING   
  10 3F*P ...      CHANGMOD  4,XBRK3,XREJ
   5 3F*W ...XASCR   
  19 3FB6 ...      ASCEDIT  ON                  [SET SCREEN EDITING MODE FLAG
  14 3FDS ...      SETNCORE ASCREENLEN,3,AMXOR,ASCREEN   
  18 3FHG ...      LDN   4  A1(3)               [ZEROISE ASCREEN BLOCK   
   9 3FL8 ...      LDN   5  A1+1(3)  
   8 3FNW ...      STOZ     A1(3)
  10 3FRJ ...      MOVE  4  ASCREENLEN-1 
  19 3FW= ...      LDX   0  AMXMSK(2)            [PRESERVE MULTIPLEXOR MASK  
   9 3FYY ...      STO   0  ISMXMS(3)
  17 3G3L ...      ANDX  0  NOCOMBIT(1)         [UNSET COMMANDS BIT  
  19 3G4_ ...      ORX   0  SCOMMENT(1)     [ENSURE ERROR MESSAGES NOT LOST  
   9 3G6# ...      STO   0  AMXMSK(2)
  19 3G92 ...      LDCT  0  #002                [SO SCEDITOR KNOWS TO CLEAR  
  18 3G?N ...      ORS   0  ISMFORM(3)          [SCREEN ON 1ST PTR READY 
  18 3G#9 ...      BZE   7  XACR               [LOOK IF FILES ARE GRAPHIC
  10 3G#Q ...      LDX   0  XISBITNG(1)  
  18 3G*? ...      ORS   0  ISBITS(3)              [SET NON GRAPHIC FLAG 
   4 3G*S ...XACR
  10 3GBB ...      ACROSS   SCEDITOR,5   
   4 3GF4 ...#   
   4 3GHQ ...#   
   4 3GJ2 ...#   
  19 3GJ= ...XREJ  TRACE    4,SCNEWNPU          [NEW NPU SIZE WAS REJECTED   
  18 3GJG ...      LDX   4   ZOLDNPU(1)            [RESET TO ORRIGINAL   
  10 3GJQ ...      CHANGMOD  4,XBRK3,XGEO
  13 3GK2 ...      BRN      XASCR           [CARRY ON
  20 3GK= ...XGEO  TRACE    4,SCOLDNPU            [SHOULDNT REJECT OLD NPUSIZE   
   9 3GKG ...      GEOERR   1,SCEDNPU
   4 3GKQ ...#   
  19 3GL2 ...XBRK3  CLOSE                         [MOP OFF - 2 FILES TO CLOSE
  18 3GLD ...XBRK2 CLOSE                        [BRK-IN; 1 FILE TO CLOSE 
   4 3GP6 ...#   
  19 3GRS ...XBRK  VFREE    FILE,CREATE         [BRK-IN; NO FILES TO CLOSE   
   7 3GWG ...      COMBRKIN  
   4 3G_8 ...#   
   6 3H3W ...ZZ1   CLOSE 
   4 3H6J ...#   
   6 3H9= ...ZZZ   ENDCOM
   4 3H9B ...#   
   5 3H9G ...X2SCEDIT
  20 3H9L ...XEANDQ  CLOSE                        [WRAP UP FOR E AND Q COMMANDS  
   9 3H9Q ...      VFREEW   FILE,FRB 
  11 3H9W ...      MHUNTW   3,AMXOR,ASCREEN  
   9 3H=2 ...      LDX   0  ISMXMS(3)
  19 3H=6 ...      STO   0  AMXMSK(2)             [RESTORE ORIGINAL MXOR MASK
  10 3H== ...      VFREEW   AMXOR,ASCREEN
   8 3H=B ...      ASCEDIT  OFF  
  10 3H=G ...      LDX   4  ZOLDNPU(1)   
  19 3H=L ...      CHANGMOD   4,XEND,XGEO         [RESTORE ORIGINAL NPU SIZE 
   6 3H=Q ...XEND  ENDCOM
   4 3H=W ...#   
  18 3H?2 ...#     NPU SIZES STORED HERE BY NPUSIZE RESTORE TIME MACRO   
   4 3H?6 ...#   
   9 3H?= ...      SEGENTRY K50SCEDIT
   9 3H?B ...ZOLDNPU     #02400065   
   9 3H?G ...      SEGENTRY K51SCEDIT
   9 3H?L ...ZNEWNPU     #02400065   
   4 3H?Q ...#   
   4 3H#6    #   
   4 3HRQ    #END
   6 ____ ...207242200008
