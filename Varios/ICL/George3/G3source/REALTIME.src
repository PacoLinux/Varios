  17 22FL ...#SEG  REALTIME                     [JOHN P. TITORENKO   
   9 22_=    #OPT        K0REALTIME=0
  14 23DW    #OPT        K6REALTIME=K6GREATGEO>K6ALLGEO  
  15 23YG    #LIS           K0REALTIME>K0GREATGEO>K0ALLGEO   
  10 24D6                   8HREALTIME   
   4 24XQ    #   
   4 25CB    #   
  11 25X2    #     THE  REALTIME  COMMAND .  
  20 26BL    #     IF THE PARAMETER OF THE COMMAND IS THE CHARACTER STRING 'ON'  
  21 26W=    #     THEN THE ROUTINE FIRST OF ALL CHECKS THAT THE USER HAS SUFFICIENT 
  20 27*W    #     REALTIME BUDGET AVAILABLE AND , IF HE HAS , THEN SETS A MARKER
  20 27TG    #     BIT TO INDICATE THAT REALTIME WILL TAKE EFFECT FROM THE NEXT  
  10 28*6    #     ENTER/RESUME COMMAND .
  21 28SQ    #     IF THE COMMAND PARAMETER IS THE CHARACTER STRING 'OFF' THEN THE   
  20 29#B    #     ROUTINE UNSETS A REALTIME MARKER TO ALLOW THE PROGRAM TO BE   
  12 29S2    #     TREATED AS ANY OTHER PROGRAM .
  21 2=?L    #     BUDGETING TAKES PLACE ON BOTH PATHS UNLESS THE PROGRAM IS SYSTEM  
  11 2=R=    #     ISSUED OR SYSTEM STARTED .
  21 2?=W    #     ANY PARAMETER OTHER THAN 'ON' OR 'OFF' OR A NULL PARAMETER RESULTS
  20 2?QG    #     IN A COMMERR. A COMERR IS ALSO OBEYED IF INSUFFICIENT REALTIME
  12 2#=6    #     BUDGET IS AVAILABLE FOR USE . 
   4 2#PQ    #   
  16 2*9B    #     SUBROUTINE TO SET UP MONITORING FILE MESSAGES 
   4 2*P2    #   
  11 2B8L          SEGENTRY K1REALTIME,XX1R  
  11 2CMG          SEGENTRY K3REALTIME,XX3R  
  12 2DN# ...      FSHENTRY K4REALTIME,XX4R,,XX4R
  12 2DPW ...      FSHENTRY K5REALTIME,XX5R,,XX5R
  12 2DRD ...      FSHENTRY K7REALTIME,XX7R,,XX7R
   4 2DT2 ...#   
   8 2DWJ ...      FSHCODE  AORB 
   4 2DY6 ...(   
  19 2D_N ...ULINK                              [UNANTICIPATED CLOSEDOWN LINK
  10 2F3= ...      LINK     REALTIME,7   
   4 2F4S ...)   
   5 2F6B    XPARA   
  15 2FL2          SBX   6  FX1                 [CHANGE LINK 
   8 2G5L    #SKI  JNLPACK<1$1   
   4 2GK=    (   
   7 2H4W          LDN   4  4
  17 2HJG          OUTBLOCK  4                  [SET UP MONOUT BLOCK 
   9 2J46          TIMECON  CLEANCT  
   7 2JHQ          LDN   1  8
  15 2K3B          OUTPARAM 1,ACOMMUNE1         [ACTUAL TIME 
  17 2KH2          OUTMILL                      [MILL TIME PARAMETER 
   4 2L2L    )   
   7 2LG=    #SKI  JNLPACK   
   4 2L_W    (   
  17 2MFG          OUTBLOCN 8                   [SET UP MONOUT BLOCK 
  11 2M_6          OUTPARC  TIMENOW,JOBMILL  
   6 2NDQ    #SKI  JNLPER
   7 2NYB          OUTPMILL  
   4 2PD2    )   
   8 2PXL          ADX   6  FX1  
   7 2QC=          EXIT  6  0
   4 2QWW    #   
  19 2R35 ...TELLHLS                           [INFORM HLS OF REALTIME EVENT 
   8 2R4G ...      SBX   7  FX1  
   8 2R5X ...      LDX   2  FX2  
  14 2R7# ...      HLSINFORM XBRKIN,REALTIME,JOBNO(2),6  
   5 2R8P ...XBRKIN  
   8 2R=6 ...      ADX   7  FX1  
   7 2R?H ...      EXIT  7  0
   8 2RBG    PON            4HON 
   8 2RW6    POFF           4HOFF
   8 2S*Q    X4             +4   
   8 2STB    XNUM           +9   
   8 2T*2    XPR            4HPR 
  21 2TSL    MSK            #77777377           [MASK FOR ALL EXCEPT B15 OF A WORD   
  21 2W72 ...MILLP          #36363636           [DEFAULT MILL PRI.FOR RE OFF IS 30   
   9 2W8# ...REMILL         #74727170
   8 2W9L ...               +0   
   4 2W#=    #   
   4 2WRW    XX1R
   4 2X?G    #   
   7 2XR6          LDN   5  0
   8 2Y=Q          PARAPASS  5   
  10 2YQB          MHUNT    3,CPB,CUNI   
   9 2_=2          LDX   5  ANUM(3)  
  18 2_PL          BZE   5  XMIS                [ERROR IF NULL PARAMETER 
   7 329=          ADN   5  1
  20 32NW          BZE   5  XMIS                [ERROR IF NON EXISTENT PARAMETER 
   9 338G          LDX   6  POFF(1)  
   9 33N6          LDX   7  APARA(3) 
   9 347Q          BXU   6  7,XNOTOFF
   7 34MB          LDN   5  0
  16 3572          CALL  6  XOFF                [ DO REALTIME OFF
  16 35BR ...      LDN   6  0                [=> RE OFF TO HLS   
   9 35FB ...      CALL  7  TELLHLS  
   6 35LL          ENDCOM
   4 366=    #   
   4 36KW    #   
   5 375G    XNOTOFF 
   9 37K6          LDX   6  PON(1)   
   7 384Q          TXU   6  7
  18 38JB          BCS      XSTR                [BRANCH IF NOT 'RT ON'   
  18 3942    XON                                ['REALTIME ON' ROUTINE   
  20 3DW2          FJOCA    3,FX2               [POINT TO APPROPRIATE JOB BLOCK  
  19 3F*L          RTTEST   SBIT,3              [JUMP IF RE ON COMMAND ISSUED
  18 3FT=          SPRIVTEST JSSTART,3,UDBUG    [IS JOB 00STEM STARTED ? 
  16 3G#W          BRN      SBIT                [BRANCH IF YES   
  15 3HRQ    UDBUG SMO      JCSIZE(3)           [PROG SIZE   
   7 3J?B          LDN   5  0
   4 3KR7 ...#   
   4 3KS4 ...XX4R
   9 3KS_ ...      FSHCODE  B,XENDB1 
   4 3KTW ...(   
  20 3KWR ...#     REALTIME BUDGET CHECKING FOR SHARED FILESTORE IS DEALT WITH IN
  20 3KXN ...#     'A' TO ALLOW PROPER CLOSEDOWN ACTION TO BE TAKEN IF 'B' FAILS 
  12 3KYK ...#     WITH REALTIME PROGRAMS RUNNING
   4 3K_G ...#   
  17 3L2C ...      TRANSBEG FSHNOID,REALTIME,4,NOBLOCKS,,ACOMMUNE1   
   4 3L3# ...#   
  18 3L49 ...      BRN      XBRK                [BREAK IN WHILE IN 'A'   
   4 3L56 ...#   
   5 3L63 ...XENDB1  
   4 3L6Y ...)   
   9 3L7T ...      FSHSKIP  B,XNOTB1 
   4 3L8Q ...(   
  16 3L9W          OPENBUDG  XBRK               [OPEN DICTIONARY 
  19 3LPG          BUDGINCR GREALTIME,5         [INCREMENT REALTIME CORE USED
  21 3M96          TESTREPNOT  OK,XNOK          [BRANCH IF BUDET INCREMENT WAS NOT OK
   7 3MNQ    XCL   CLOSBUDG  
   4 3MPB ...#   
   5 3MQ2 ...XNOTB1  
   4 3MQL ...)   
   9 3MR= ...      FSHCODE  A,XENDA1 
   4 3MRW ...(   
  19 3MSG ...      NOTALIEN XENDA1,2            [JIF JOB IS AT HOME IN 'A'   
   4 3MT6 ...#   
  20 3MTQ ...#     WE NOW HAVE TO GUARD AGAINST THE PROBLEM THAT REALTIME BUDGET 
  21 3MWB ...#     GETS 'LOST' IN 'B' M/C BREAKS UNTIL THE WHOLE SYSTEM IS RELOADED  
  20 3MX2 ...#     AND GOES THROUGH BACKMAP.   TO DO THIS WE SET UP A /FSHAMBLES 
  20 3MXL ...#     BLOCK INDICATING THE SIZE OF THE REALTIME CORE IMAGE FOR THE  
  19 3MY= ...#     JOB.   THIS WILL BE PICKED UP BY UNANTICIPATED CLOSEDOWN. 
   4 3MYW ...#   
  19 3M_G ...      SETNCORE 2,2,FSH,FSHAMBLES   [GET UNANT. CLOSEDOWN BLOCK  
  19 3N26 ...      LDX   0  ULINK(1)            [& SET INTO IT A LINK AND THE
  19 3N2Q ...      STO   0  FSHCDLINK(2)        [SIZE OF THE CURRENT REALTIME
  15 3N3B ...      STO   5  FSHWORK(2)          [CORE IMAGE  
  19 3N42 ...      LDN   2  1                   [SET 'OK' OFFSET FOR TRANSFIN
   4 3N4L ...#   
  18 3N5= ...XFIN  TRANSFIN 2                   [RETURN TO 'B' MACHINE   
   4 3N5W ...#   
   5 3N6G ...XENDA1  
   4 3N76 ...)   
   8 3N8B          CALL  6  XPARA
  18 3NN2          MONOUT   JRTST               [OUTPUT LOGGING MESSAGE  
   7 3P7L          PERCOUNT  
   4 3PLR ...SBIT
   7 3Q5Y ...      LDN   7  2
  20 3QK5 ...      PARANEXT ,,7                 [GET SECOND PARA TO RE COMMAND   
  21 3R4= ...      TESTREP2 UNPAIR,TERR         [ERROR IF CONTAINS UNPAIRED PARENTHES
   7 3R9? ...#UNS ANSTOOMANY 
  18 3RB# ...      TESTREP2 TOOMANY,TERR        [ERROR IF OVER 24 PARAS  
  10 3RHC ...      MHUNT    2,CPB,CUNI   
  15 3S2J ...      NGX   0  ANUM(2)             [JUMP IF.... 
  21 3SFP ...      BPZ   0  PTR                 [....SECOND PARA NULL OR NON-EXISTENT
   4 3SYW ...[   
   4 3TD3 ...[   
   4 3TX8 ...[   
   4 3WB* ...[   
  16 3WTG ...      LDX   0  APARA(2)            [CHECK FOR....   
  20 3X#M ...      ERX   0  XPR(1)              [....PARAMETER STARTING WITH.... 
  19 3XRS ...      SRL   0  12                  [....THE CHARACTER STRING....
  19 3Y=_ ...      BNZ   0  TERR                [....PR    (J TO TERR IF NOT)
   4 3YQ6 ...[   
   4 3_9? ...[   
   4 3_ND ...[   
   4 427K ...[   
  16 42LQ ...      PARANUMB 7                   [CHECK FOR....   
  21 435X ...      SBN   7  2                   [....TWO PARAMETERS I.E. PR & (*)....
  19 43K4 ...      BNZ   7  TERR                [....CONTAINED IN CPB,CMULTI 
   9 4449 ...      MFREE    CPB,CUNI 
   4 44HB ...[   
   4 452H ...[   
   4 45FN ...[   
   4 45YT ...[   
   7 46D2 ...      PARAPASS  
  17 46X7 ...      MHUNT    2,CPB,CUNI          [GET NEXT CPB,CUNI   
  19 47B# ...      LDX   0  ANUM(2)             [CHECK FOR 2ND HALF OF ....  
  21 47TF ...      SRL   0  12                  [....2ND PARAMETER TO RE COMMAND.... 
  21 48#L ...      SBN   0  2                   [....IS COMPOSITE, I.E. TYPE 2....   
  15 48RR ...      BNZ   0  TERR                [....J IF NOT
   7 493G ...      PARABRAC  
  10 495D ...      MHUNT     2,CPB,CUNI  
   9 497B ...      LDX   0  ANUM(2)  
  18 499# ...      BZE   0  TERR               [J IF NOTHING IN BRACKETS 
   7 49?=          CHNUMCON 1
   9 49QW          TESTREP2 OK,TOK   
   7 4=2N ...TERR  LDN   7  0
   8 4==G ...      SPARANOT 2,7  
   9 4=G# ...      ERROR    APFERR   
   8 4=Q6          BRN      PTR  
   8 4?9Q    TOK   FJOCA    3,FX2
   7 4?PB          UNSETRTP 3
   7 4#92          LDX   4  3
   9 4#NL          LDX   7  JOBNUM(3)
  11 4*8=          MHUNT    3,CPAR,GNUMCON   
   8 4*MW          LDX   5  A1(3)
   7 4B7G    TOK1  SMO      5
   8 4BM6          LDX   6  A1(3)
   8 4C6Q          BNG   6  TERR 
   8 4CLB          TXL   6  X4(1)
   8 4D62          BCC      TERR 
   8 4DKL          SETRTP   4,6  
   8 4F5=          BCT   5  TOK1 
   5 4FCM ...#UNS ICT
   8 4FQ4 ...      APREALT  HIGH 
   8 4G4G          FJOCA    3,FX2
   8 4GJ6          BRN      RTP  
   4 4GQB ...PTR 
   5 4GYL ...#UNS ICT
   8 4H6W ...      APREALT  ON   
   8 4H*6 ...      FJOCA    3,FX2
   7 4HHB          UNSETRTP 3
   9 4J32          LDX   7  JOBNUM(3)
  10 4K2=    RTP   VFREE    CPAR,GNUMCON 
  12 4K3C ...[     CALCULATE MEMBER PRIORITIES   
   4 4K3D ...[   
   8 4K3F ...      FPCACA   1,2  
  10 4K3G ...      HUNT2    1,AOBRES,0   
   9 4K3H ...      LDX   0  A1+7(1)  
  10 4K3J ...      STO   0   ACOMMUNE1(2)
   9 4K3K ...      LDX   0  A1+9(1)  
  10 4K3L ...      STO   0  ACOMMUNE2(2) 
   9 4K3M ...      LDX   0  A1+11(1) 
  10 4K3N ...      STO   0  ACOMMUNE3(2) 
   9 4K3P ...      LDX   0  A1+8(1)  
  10 4K3Q ...      STO   0  ACOMMUNE4(2) 
   7 4K3R ...      LDN   5  4
   5 4K3S ...XCALC1  
   7 4K3T ...      SMO      5
  11 4K3W ...      LDX   0  ACOMMUNE1-1(2)   
   7 4K3X ...      LDN   1  4
   9 4K3Y ...      BZE   0  XCALC3   
   7 4K3_ ...      LDN   1  0
   7 4K42 ...      LDN   4  4
   5 4K43 ...XCALC2  
   7 4K44 ...      SMO      4
  11 4K45 ...      TXL   0  ACOMMUNE1-1(2)   
   7 4K46 ...      ADN   1  0
   9 4K47 ...      BCT   4  XCALC2   
   5 4K48 ...XCALC3  
   7 4K49 ...      SMO      5
  11 4K4= ...      STO   1  ACOMMUNE5-1(2)   
   9 4K4? ...      BCT   5  XCALC1   
   7 4K4# ...      LDN   4  4
   5 4K4* ...XCALC4  
   7 4K4B ...      SMO      4
  11 4K4C ...      LDX   1  ACOMMUNE5-1(2)   
   7 4K4D ...      SRC   1  2
   8 4K4F ...      SMO      FX1  
   9 4K4G ...      LDCH  0  REMILL(1)
   7 4K4H ...      DCH  0  7 
   7 4K4J ...      SRC   7  6
   9 4K4K ...      BCT   4  XCALC4   
   4 4K4L ...[   
   7 4K54 ...      FPCACA 1,2
  19 4K68 ...      ADN   1  AMR1                [START OF MILL PRI'S IN APET 
  21 4K6Q ...      ORX   1  GSIGN               [CHARACTER 2 IS MILL PRI. IN AMR'S   
   7 4K7# ...      LDN   6  4
   4 4K7W ...XAMR
   7 4K8P ...      SLC   7  6
  18 4K9J ...      DCH   7  0(1)                [STORE MILL PRI.IN APET  
  16 4K=6 ...      DCH   7  AMILLP(3)           [ & IN JOB BLOCK 
   5 4K=N ...XAMR1   
   7 4K?= ...      BCHX  3  /
   7 4K?S ...      ADN   1  1
  18 4K#B ...      BCT   6  XAMR                [J IF STILL MORE MEMBERS 
  17 4K#Y ...      SBN   3  1                   [RESET JOB BLOCK PTR.
  16 4K*G ...      SBN   1  AMR1+4              [RESET PCA PTR   
  15 4KB4 ...      PCATEST 1,NORM               [J IF NOT TP 
  20 4KBL ...      FINDPUC 1,1                  [AS TP,FIND PUC & STORE MILL PRI.
  16 4KC8 ...      ORX   1  GSIGN               [ IN PUC'S APET  
   7 4KCF ...      SLC   7  6
   9 4KCQ ...      DCH   7  AMR1(1)  
   4 4KD# ...NORM
   4 4KDW ...)   
   9 4KFW          TESTRTQ  XEND,3   
   9 4K_G          TESTRT   SEND,3   
   8 4LF6          BRN      SEND1
   8 4LYQ    SEND  FPCAJO   2,3  
   8 4MDB          MELRERING   2 
   8 4MY2          BRN      XEND 
   5 4NCL    SEND1   
  21 4NX=          LDN   0  #400                [SET B15 OF JMISC TO INDICATE THAT RE
  18 4PBW          ORS   0  JMISC(3)            [ COMMAND HAS BEEN ISSUED
   7 4PWG    #SKI  CAMK7*G3  
   7 4QB6          SWAPOUT   
   4 588# ...XEND
  16 589W ...      LDN   6  1                  [=> RE ON TO HLS  
   9 58?D ...      CALL  7  TELLHLS  
   6 58*2 ...      ENDCOM
   9 58ND ...      FSHSKIP  B,XNOTB2 
   4 58TL ...(   
  15 592W    #     ROUTINE IF NOT ENOUGH REALTINE BUDGET LEFT
  21 59GG    XNOK  TESTREP  NOMORE,XCL          [TEST IN CASE RT CAN STILL BE USED   
  16 5=26          CLOSE                        [CLOSE  DIRECTORY
   4 5=63 ...#   
   5 5=9Y ...XNOTB2  
   4 5=*T ...)   
  21 5=FQ    XCER  COMERR   JNORT               [ERROR AS NO REALTIME BUDGET FOR USE 
   4 5=_B    #   
  19 5?F2    #     THIS ROUTINE DOES THE REALTIME OFF. ALSO USED BY PROSTART.
   7 5?YL    #     X6 = LINK 
   4 5#D=    #   
  16 5#XW    XOFF                               ['RT OFF' ROUTINE
   8 5*CG          SBX   6  FX1  
   9 5*X6          STO   6  AWORK1(2)
  16 5B6Y ...      STOZ     BIGPROGCP           [CORRECTS B9237  
   5 5B9H ...#UNS ICT
   8 5B#6 ...      APREALT  OFF  
   8 5BBQ          LDN   3  BJOBQ
   9 5BWB    XOFF1 LDX   3  FPTR(3)  
   8 5CB2          TXU   3  CXJO 
   8 5CTL          BCC      XOFF2
  10 5D*=          FLIPS    JMISC(3),17  
   8 5DSW          BRN      XOFF1
   5 5F#G    XOFF2   
  16 5FS6          FJOCA    3,FX2               [LOCATE JOB BLOCK
   7 5G?Q          UNSETRTP 3
   9 5GRB          LDX   4  JOBNUM(3)
   8 5HQL          BNG   5  XUNI 
   9 5J==          LDX   4  AEXCO(3) 
   8 5JPW          BNZ   4  RPERL
   4 5K9G    XUNI
  19 5KP6          TESTRT   XUNPL,3             [BRANCH IF 'RT ON' EFFECTED  
  20 5L8Q          TESTRTQ  XTPR,3              [BRANCH IF 'RE ON' TO BE EFFECTED
   9 5LNB          LDX   6  AWORK1(2)
   8 5M82          ADX   6  FX1  
   7 5MML          EXIT  6  0
  18 5N7=    XUNPL UNPLUGRT                     [UNPLUG THE RT PROGRAM   
  17 5NLW          FJOCA    3,FX2               [RELOCATE JOB BLOCK  
  20 5P6G    XTPR  SPRIVTEST JSSTART,3,SBBUD    [TEST IF PROGRAM SYSTEM STARTED  
   6 5P?N ...#UNS  IMR   
  19 5PDW ...      LDX   7  ISISSMP             [MILL PRI'S FOR SYSTEM JOBS  
  16 5PL6          BRN      XBUNS               [BRANCH IF IT WAS
  15 5Q5Q    SBBUD SMO      JCSIZE(3)           [PROG SIZE   
   7 5QKB          NGN   5  0
   4 5QL* ...#   
   4 5QM# ...XX5R
   9 5QN? ...      FSHCODE  B,XENDB2 
   4 5QP= ...(   
  20 5QQ9 ...#     AS WITH ESTABLISHING REALTIME PROGRAMS, FOR REALTIME OFF, WE  
  13 5QR8 ...#     DEAL WITH BUDGET UPDATES IN 'A'   
   4 5QS7 ...#   
  17 5QT6 ...      TRANSBEG FSHNOID,REALTIME,5,NOBLOCKS,,ACOMMUNE1   
   4 5QW5 ...#   
  18 5QX4 ...      BRN      XBRK                [BREAK IN WHILE IN 'A'   
   4 5QY3 ...#   
   5 5Q_2 ...XENDB2  
   4 5Q__ ...)   
   9 5R2Y ...      FSHSKIP  B,XNOTB3 
   4 5R3X ...(   
  16 5R52          OPENBUDG  XBRK               [OPEN DICTIONARY 
  20 5RJL          BUDGINCR  GREALTIME,5        [INCREASE USERS REALTIME BUDGET  
  16 5S4=          CLOSBUDG                     [CLOSE DICTIONARY
   4 5S4Y ...#   
   5 5S5L ...XNOTB3  
   4 5S6# ...)   
   9 5S72 ...      FSHCODE  A,XENDA3 
   4 5S7N ...(   
  20 5S8B ...      NOTALIEN XENDA3,2            [JIF THIS IS AN 'A' MACHINE JOB  
   4 5S94 ...#   
  20 5S9Q ...#     WE MUST NOW GET RID OF THE /FSHAMBLES BLOCK THAT WAS SET UP   
   8 5S=D ...#     BY REALTIME ON
   4 5S?6 ...#   
  21 5S?S ...      LDX   1  ULINK(1)            [LINK ADDR FOR BLOCK IDENTIFICATION  
   4 5S#G ...#   
   6 5S*8 ...XLOOKAGAIN  
  21 5S*W ...      HUNT2J   2,FSH,FSHAMBLES,,(GEOERR) [LOOK FOR A /FSHAMBLES BLOCK   
  21 5SBJ ...      BXU   1  FSHCDLINK(2),XLOOKAGAIN [JIF NOT THE RIGHT LINK ADDRESS  
   7 5SC= ...      FREECORE 2
  19 5SCY ...      LDN   2  1                   [SET 'OK' OFFSET FOR TRANSFIN
   8 5SDL ...      BRN      XFIN 
   4 5SF# ...#   
   5 5SG2 ...XENDA3  
   4 5SGN ...)   
  16 5SHW          CALL  6  XPARA               [SET UP MESSAGE  
  16 5T3G          MONOUT   JRTFIN              [OUTPUT MESSAGE  
   7 5TH6          PERCOUNT  
   8 5W2Q          FJOCA    3,FX2
   7 5W*7 ...      LDN   7  0
  17 5WGB    XBUNS LDX   5  MSK(1)              [UNSET B15 OF JMISC  
   9 5X22          ANDS  5  JMISC(3) 
  20 5X3_ ...      STO   7  AMILLP(3)           [STORE MILL PRI'S IN JOB BLOCK   
   7 5X4L ...      FPCACA 3,2
  19 5X5? ...      ORX   3  GSIGN               [MILL PRI IN CHARA.2 IN AMR'S
  15 5X5Y ...      LDN   6  4                   [4 MEMBERS   
   7 5X6= ...      STOZ     5
   8 5X6K ...      BRN      YAMR1
   4 5X7= ...YAMR
  17 5X7X ...      SRL   7  6                   [GET MILL PRI FOR N-1
   5 5X8J ...YAMR1   
   7 5X8M ...      DCH   7  5
  21 5X8Q ...      BNZ   5  YAMR2               [IF JOBBLOCK MILL PRIORITY IS ZERO   
  18 5X8T ...      LDN   5  30                  [PCA MP IS DEFAULT OF 30 
   7 5X8Y ...      DCH   5  7
   5 5X93 ...YAMR2   
   7 5X99 ...      SMO      6
  18 5X9W ...      DCH   7  AMR1-1(3)           [MILL PRI FOR MEMBER N   
  18 5X=H ...      BCT   6  YAMR                [J IF ANY MEMBERS LEFT   
  15 5X?8 ...      PCATEST 3,XNORM              [J IF NOT TP 
   8 5X?T ...      FINDPUC 3,3   
   8 5X#G ...      ORX   3  GSIGN
  17 5X*7 ...      DCH   7  AMR1(3)             [RESET PUC'S MILL PRI
   5 5X*S ...XNORM   
   9 64TG          LDX   6  AWORK1(2)
   8 65*6          ADX   6  FX1  
   7 65SQ          EXIT  6  0
   4 66#B    #   
  18 66S2    XMIS  FREECORE  3                  [FREE THE CPB/CUNI BLOCK 
  18 67?L          COMERR   JPARMIS             [PARAMETER MISSING ERROR 
   4 67R=    #   
  18 68=W    XSTR  COMERR   ERMCH               [PARAMETER NOT RECOGNISED
   4 68QG    #   
   4 6#MG    #   
   4 6#PP ...XBRK
   9 6#RY ...     FSHCODE  A,XENDA2  
   4 6#W7 ...(   
  20 6#YB ...     NOTALIEN XENDA2,2             [JIF THIS IS AN 'A' MACHINE JOB  
   4 6*2K ...#   
  16 6*4S ...#    WE RETURN TO 'B' BEFORE DEALING WITH BREAKIN   
   4 6*73 ...#   
  20 6*9= ...     LDN   2  0                    [SET BREAK IN OFFSET FOR TRANSFIN
   8 6*?F ...     BRN      XFIN  
   4 6**N ...#   
   5 6*CX ...XENDA2  
   4 6*G6 ...)   
  20 6*J* ...      MHUNT    3,CPB,CUNI          [END OF COMMAND AFTER BREAK-IN   
   7 6*LQ          FREECORE 3
   7 6B6B          COMBRKIN  
  19 6BL2    RPERL COMERR   JRTPER              [RT PERIPHS. STILL ALLOCATED 
  20 6MWW    XX3R                               [ENTRY FOR RE OFF FROM PROSTART  
   7 6NBG          NGN   5  1
   8 6NW6          CALL  6  XOFF 
   5 6P*Q          UP
   4 6PTB    #   
   4 6Q*2    #   
   4 9P4=    #   
   9 9P4P ...      FSHCODE  A,XENDA4 
   4 9P58 ...(   
   4 9P5M ...XX7R
   4 9P66 ...#   
  19 9P6K ...#     DURING UNANTICIPATED CLOSEDOWN WE ARE ENTERED HERE IF THE 
  20 9P74 ...#     JOB HAD A REALTIME CORE IMAGE IN 'B'.  WE HAVE TO RETURN THE  
  14 9P7H ...#     REALTIME BUDGET THAT WAS IN USE IN 'B'
   4 9P82 ...#   
  20 9P8F ...      MHUNTW   1,FSH,FSHAMBLES     [FIND THE UNANTICIPATED CLOSEDOWN
  21 9P8Y ...      NGX   5  FSHWORK(1)          [BLOCK AND RECOVER CORE IMAGE SIZE   
  18 9P9C ...      FREECORE 1                   [GET RID OF DATA BLOCK   
   4 9P9W ...#   
   4 9P=* ...#   
  19 9P=S ...#     TO OVERCOME POSSIBLE PROBLEMS WITH BREAKIN BEING SET WHEN 
  20 9P?? ...#     THE DICTIONARY IS OPENED, WE FIDDLE JOBEVENTS TO INHIBIT ANY  
  20 9P?Q ...#     BREAKIN AND WE RESET IT AFTER THE BUDGET UPDATE IS COMPLETE   
   4 9P#9 ...#   
  21 9P#N ...      LDX   4  JOBEVENTS(2)        [HOLD ORIGINAL CONTENTS OF JOBEVENTS 
   8 9P*7 ...      LDCT  0  #40  
  20 9P*L ...      STO   0  JOBEVENTS(2)        [FORCE 'INHIBIT BREAKIN' STATE   
   4 9PB5 ...#   
  21 9PBJ ...      OPENBUDG (GEOERR)            [OPEN DICTIONARY & READ USER'S ENTRY 
  18 9PC3 ...      BUDGINCR GREALTIME,5         [UPDATE REALTIME BUDGET  
   7 9PCG ...      CLOSBUDG  
   4 9PC_ ...#   
  20 9PDD ...      STO   4  JOBEVENTS(2)        [RESTORE ORIGINAL 'BREAKIN' STATE
   4 9PDX ...#   
  20 9PFB ...      TRANSDIE                     [RETURN TO NORMAL CLOSEDOWN CODE 
   4 9PFT ...#   
   5 9PG# ...XENDA4  
   4 9PGR ...)   
  11 9PHW          MENDAREA 20,K99REALTIME   
   4 9Q3G    #END
   8 ____ ...23575621000100000000
