  14 22FL ...      SEG   INCWAIT,860,MIKE PUTNAM,INCDUMP 
   4 22_=    #   
  12 23DW          SEGENTRY K2INCWAIT,UPGMTNOW   
   9 23YG          SEGENTRY K1INCWAIT
   4 24D6    #   
  21 24XQ    #  2 ENTRY POINTS FOR 2 COMPLETELY SEPARATE PURPOSES - IN SAME SEGMENT S
  21 25CB    #  BOTH QUITE SMALL & (FOR MOST OF TIME WHEN GEORGE IS RUNNING) BOTH ARE
  10 25X2    #  ENTERED ONCE A MINUTE.   
   4 26BL    #   
   6 26W=    #  K1 ENTRY 
   6 27*W    #  -------- 
   4 27TG    #   
  21 28*6    #  ENTERED FROM 1) INCOM SEGMENT (INCDUMP DUMP  OR  INCDUMP NOW  COMMAND
  21 28SQ    #               2) INCRV SEGMENT (WHEN INCR. NOT SUITABLE FOR GENERAL RE
  21 29#B    #                                                              PRODUCED)
  21 29S2    #               3) INCSTART SEGMENT (WHEN INCR. ABANDONED & OBLITERATED)
  21 2=?L    #  ON ENTRY, X7 SAYS WHETHER TO WAIT (A TIME OF INSTALLATION PARARAMETER
  19 2=R=    #                                    'ADUMPTIME' MINS.) OR NOT. 
  21 2?=W    #           IF X7=0, ONLY WAIT IF LAST INCR. SUITABLE FOR GEN. RESTORE  
  16 2?QG    #                      (INCDUMP DUMP COMMAND ONLY)  
  21 2#=6    #           IF B0=1, NEVER WAIT (INCDUMP NOW COMMAND & ENTRY FROM INCRV)
  17 2#PQ    #           IF B1=1, ALWAYS WAIT (ENTRY FROM INCSTART). 
   4 2*9B    #   
   6 2*P2    #  K2 ENTRY 
   6 2B8L    #  -------- 
   4 2BN=    #   
  21 2C7W    #  ENTERED FROM GMTUPDATE MACRO (USED BY CHARGESC SEGMENT & OBEYED ONCE 
  21 2CMG    #  MINUTE) TO ALTER GMTNOW & POSSIBLY TO UPDATE ITS VALUE ON BACKING-STO
   4 2D76    #   
   4 2DLQ    #   
  21 2F6B    #  K1INCWAIT - DECIDE WHETHER OR NOT TO WAIT & SET UP MARKER WORD (IN X6
  17 2FL2    #  IN X6, B0=1 IF LAST INCR IS SUITABLE FOR GEN RESTORE 
  20 2G5L    #         B1=1 IF RETRIEVABLE INCR SINCE LAST GEN RESTOREABLE INCR  
  19 2GK=          OPENINC  ,GENERAL            [OPEN INCINDEX (GENERAL MODE)
  17 2H4W          LDN   6  0                   [CLEAR MARKER WORD   
   6 2H=4 ...#UNS  JSKI33
  10 2HC= ...      BS       2,EXDUMPER   
   4 2HJG    STEP
   6 2J46          STEP  
  17 2JHQ          BZE   3  YEND                [J IF END OF INCINDEX
   6 2JLW ...#UNS  FPD   
  20 2JQ2 ...      JMBAC    NGR,3,BIFINALR,BIGENRESR [J IF NOT LAST INC OF DUMP  
   6 2JT6 ...#UNS  FPD   
   4 2JY= ...#SKI
  21 2K3B          JBC   NGR,3,BIGENRESR        [J IF INCR. NOT SUITABLE GENERAL REST
  21 2KH2          LDCT  6  #400                [SET B0 (SUITABLE FOR GEN. RESTORE), 
   8 2L2L          BRN      STEP 
   4 2LG=    NGR 
   7 2L_W          SLL   6  1
  20 2MFG          SRL   6  1                   [CLEAR GEN. RESTORE MARKER (B0)  
  21 2M_6          JBS   STEP,3,BINSUITRVR      [J IF INCR. NOT SUITABLE RETRIEVING  
  21 2NDQ          LDCT  6  #200                [SET 'INCR. SUITABLE FOR RETRIEVING (
  19 2NYB                                       [   NOT GEN. RESTORE)' MARKER
   8 2PD2          BRN      STEP 
   4 2PXL    YEND
  16 2QC=          REWIND                       [REWIND INCINDEX 
  18 2QWW          BNG   7  NOWAIT              [J IF TOLD NOT TO WAIT   
   7 2RBG          SLL   7  1
  17 2RW6          BNG   7  YWAIT               [J IF TOLD MUST WAIT 
  21 2S*Q          BPZ   6  NOWAIT              [J IF LAST INCR. IN INCINDEX NOT SUIT
  21 2STB                                       [               FOR A GENERAL RESTORE
   5 2T*2    YWAIT   
  16 2TSL          CLOSETOP                     [CLOSE INCINDEX  
   4 2W#=    #   
  21 2WRW    #  NOW GO WAITING (A) FOR ^ADUMPTIME^ MINS., (B) UNTIL ^FINISHED^ OCCURS
  20 2X?G    #  (C) UNTIL ^DUMP^ IS TYPED OR (D) UNTIL KICKED BY THE BS UNJAMMER.
   4 2XR6    #   
  19 2Y=Q          STOZ     7                   [CLEAR COUNT OF MINS. WAITED 
  18 2YQB          LDN   4  60/FONINTP          [X4= NO. OF WAITS PER MIN
  20 2_=2          BRN      MISSTIME            [CHECK FIRST FOR ^FINISH^ ETC.   
   4 2_PL    WAIT
   7 2_SQ ...#UNS FREEBUFF   
   4 2_XW ...(   
  14 3232 ...      FREEBUFF      [ FREE FURBS AND FUWBS  
   4 3266 ...)   
  17 329=          LONGHALT MFIN,ADUMPWAIT,4    [WAIT FOR 1 MINUTE   
  19 32NW                                       [   OR UNTIL WOKEN UP TO DUMP
  20 338G          ADN   7  1                   [INCREASE COUNT OF MINS. WAITED. 
   5 33N6    MISSTIME
   8 347Q          LDCT  0  #040 
   9 34MB          ANDX  0  FINISH   
  16 3572          BNZ   0  MFIN                [J IF ^FINISHED^.
   5 35=6 ...#UNS IDC
  20 35*= ...      JMBS     WAITFIN,,BNOWD,BDCISS     [ J IF TO DUMP IMMEDIATELY 
   5 35DB ...#UNS IDC
   4 35HG ...#SKI
  18 35LL          JBS   WAITFIN,,BNOWD         [J IF TO DUMP IMMEDIATELY
  19 366=          BXL   7  ADUMPTIME,WAIT      [J IF NOT YET TIME TO DUMP   
   5 36KW    WAITFIN 
   4 375G    #   
  21 37K6    # INHIBIT BREAK-IN DURING DUMPING SO THAT IF 'FINISHED' OCCURS WE ARE NO
  21 384Q    #  INTERRUPTED WHEN WE DO ISSUCOMS - OPS WOULD GET CONFUSED OTHERWISE   
  21 38JB    #  IF BEFORE INHIBITING, BREAKIN ALREADY SET( IE 'FINISHED' HAS OCCURRED
  21 3942    #   ) OR 'FINISHED' OCCURRED WHILE WAITING  GOES BACK TO COMMAND LEVEL T
   9 39HL    #  DEAL WITH FINISHED   
   4 3=3=    #   
   5 3=8D ...#UNS IDC
   9 3=*L ...      BC      ,BDUMPAB  
  19 3=GW          OPENINC  ,GENERAL            [OPEN INCINDEX (GENERAL MODE)
   5 3?2G    NOWAIT  
  20 3?G6          TESTINBRK XINHIB             [J IF BREAK-IN ALREADY INHIBITED 
  21 3?_Q          INHIBITBRK MBREAK            [INHIBIT BREAK-IN - J IF BREAK-IN ALR
   5 3#FB    XINHIB  
  18 3#_2          ACROSS   INCSTART,1          [X6 IS SET AS FOLLOWS:   
  21 3*DL                                       [   B0=1 IF LAST INCR. SUITABLE FOR  
  18 3*Y=                                       [       GENERAL RESTORE  
  21 3BCW                                       [   B1=1 IF RETRIEVABLE INCR. SINCE L
  20 3BXG                                       [       GENERAL RESTOREABLE INCR.
   5 3CC6    MBREAK  
  16 3CWQ          CLOSETOP                     [CLOSE INCINDEX  
   4 3DBB    MFIN
   5 3DD5 ...#UNS IDC
   9 3DFS ...      BC      ,BDUMPAB  
   6 3DHJ ...#UNS  JSKI33
  10 3DNQ ...      BC       FX2,EXDUMPER 
  21 3DW2          BC       ,BIDCOM             [REMOVE 'INCDUMP DUMP (OR NOW) COMMAN
  20 3F*L                                       [               BEING OBEYED' BIT
  21 3FT=          COMBRKIN                     [GO BACK TO COMMAND PROCESSOR TO DEAL
  17 3G#W                                       [    WITH FINISHED   
   4 3GSG    #   
   4 3H#6    #   
   4 3HRQ    #   
   5 3J?B    UPGMTNOW
   9 3JR2          LDX   0  APROCTIME
  18 3K=L          STO   0  GMTLUP              [RESET TIME LAST UPDATED 
   7 3KQ=          LDN   0  1
  17 3L9W          ADS   0  GMTNOW              [INCREASE CURRENT GMT
   9 3LPG          LDX   1  GMTNOW   
   9 3M96          LDN   2  GMTINT   
  21 3MNQ          DVS   0  2                   [CHECK IF NEED TO UPDATE ON B.STORE  
  18 3N8B          BNZ   0  NOUPD               [J IF NON-ZERO REMAINDER 
  11 3NN2          OPENMAS  NOFMAPP,GENERAL  
   7 3P7L    #SKI   IFS<1-1  
  16 3PM=          LDX   2  BFILE               [FIND MASTERS FCB
   6 3Q6W    #SKI   IFS  
  16 3QLG          MASFCB   2                   [X2->MASTERS FCB 
   9 3R66          LDX   0  GMTNOW   
  18 3RKQ          STO   0  FGMTBS(2)           [AND STORE CURRENT GMT   
  18 3S5B          MBS      2,BFALTR,BFALTB     [SET FILE ALTERED BITS   
   7 3SK2    #SKI   IFS<1-1  
   4 3T4L    (   
   5 3TJ=    NEXTB   
  20 3W3W          LDX   2  FPTR(2)             [SEARCH FILE CHAIN FOR F/FMAPP   
   9 3WHG          LDX   0  ATYPE(2) 
  17 3X36          BXE   0  FILEPLUSFCB,NOFMAPP [NO F/FMAPP PRESENT ?
  11 3XGQ          BXU   0  TMAPP(1),NEXTB   
   4 3Y2B    )   
   6 3YG2    #SKI   IFS  
  15 3Y_L          SFMAPP   2,2,(GEOERR)        [X2->FMAPP   
   7 3_F=          LDN   0  3
  19 3_YW          ORS   0  FBCOMM(2)           [REQUIRED IN UPDATING MASTER 
   7 42DG          CLOSETOP  
   5 42Y6    NOUPD   
   5 43CQ          UP
   7 43XB    #SKI   IFS<1-1  
   4 44C2    (   
   5 44WL    TMAPP   
   8 45B=    #HAL  FILE+FMAPP,0  
   5 45TW    NOFMAPP 
  21 46*G          GEOERR   1,GMT ALT           [ NO FMAP OR B/IN WHILE MASTER OPEN  
   4 46T6    )   
   4 47#Q    #   
  10 47SB          MENDAREA 20,K99INCWAIT
   4 48#2    #   
   4 48RL    #END
   6 ____ ...536720120001
