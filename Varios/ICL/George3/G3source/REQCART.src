  16 22FL    #SEG  REQCART65                    [BOB NICHOLSON   
   7 22_=    #LIS  K0REQCART 
   7 23DW          8HREQCART 
   4 23YG    #   
  20 24D6    #     IMPLEMENTS USEST,MUSEST,USESTX,SUSEST,SMUSEST AND RELST MACROS
  20 24XQ    #     IF UNIT IS NOT ONLINE USEST,SUSEST GO UP MUSEST,SMUSEST OUTPUT
  20 25CB    #     MESSAGES AND WAIT FOR THE UNIT TO COME ONLINE (OR BREAKIN OR  
   7 25X2    #     CANTDO)   
   4 26BL    #   
   4 26W=    #   
  20 27*W    #     THE MACROS USEST,USESTX,MUSEST WILL NOT ACCEPT A UNIT WHICH   
  21 27TG    #     IS IN USE BY S-TRUSTED. MUSEST WAITS FOR THE UNIT TO BE RELEASED  
   9 28*6    #     THE OTHERS GO UP. 
   4 28SQ    #   
  19 29#B    #     IF THE UNIT IS OK 1 IS ADDED TO THE COUNT OF USERS IN THE 
   9 29S2    #     PERIPHERAL LIST.  
   4 2=?L    #   
  20 2=R=    #     RELST SUBTRACTS ONE FROM COUNT OF USERS AND SETS UP AN ACTDOC 
  20 2?=W    #     WITH A LINK TO K1GRUESOME IF B18 OF WORD 0 OF THE PERIPHERAL  
   8 2?QG    #     RECORD IS SET 
   4 2#=6    #   
  11 2#PQ          SEGENTRY K1REQCART,QK1REQ 
  11 2*9B          SEGENTRY K2REQCART,QK2REQ 
  11 2*P2          SEGENTRY K3REQCART,QK3REQ 
  11 2B8L          SEGENTRY K4REQCART,QK4REQ 
  11 2BN=          SEGENTRY K5REQCART,QK5REQ 
  11 2C7W          SEGENTRY K7REQCART,QK7REQ 
   6 2C97 ...#UNS  IPBD  
   4 2C=D ...(   
  16 2C?P ...      SEGENTRY K8REQCART,XER1      [NO LONGER USED  
  14 2C*2 ...      SEGENTRY K9REQCART,XER1      [DITTO   
  15 2CB? ...      SEGENTRY K10REQCART,QK10REQ  [LOCKM MACRO 
  14 2CCJ ...      SEGENTRY K11REQCART,QK11REQ  [UNLOCKSU
  14 2CDT ...      SEGENTRY K12REQCART,QK12REQ  [TWAIT   
   4 2CG6 ...)   
   6 2CHC ...#UNS  IPBD  
   4 2CJN ...#SKI
   4 2CK_ ...(   
  11 2CMG          SEGENTRY K8REQCART,QK8REQ 
  11 2D76          SEGENTRY K9REQCART,QK9REQ 
   4 2DBY ...)   
   4 2DLQ    #   
   6 2F6B    #SKI  CARTON
  10 2FL2    ZLINK PAIR     CARTCONT,1   
   7 2G5L    #SKI  CARTON<1$1
  10 2GK=    ZLINK PAIR     GRUESOME,1   
   4 2H4W    #   
   7 2HJG    #     GEOERRS   
   4 2J46    #   
  19 2JHQ    XER1  GEOERR   1,NOPEREC           [PERIPHERAL RECORD MISSING   
  20 2K3B    XER2  GEOERR   1,USEBITS           [IF CLONG1,CLONG2 HAVE BITS SET  
   4 2KH2    #   
   4 2L2L    #   
   6 2L*3 ...#UNS  IPBD  
   4 2LMD ...#SKI
   4 2L_W    (   
   9 2MFG    QK9REQ         [TSMUSEST
   8 2M_6          LDCT  7  #400 
   7 2NDQ          ORN   7  2
   8 2NYB          BRN      XJ   
   4 2PD2    )   
   4 2PXL    #   
   9 2QC=    QK7REQ         [USESTX  
  16 2QWW          LDN   7  0                   [ORDINARY MACRO  
  10 2RBG          LDX   6  ACOMMUNE9(2) 
   9 2RW6    #SKI  K6REQCART>199-199 
   4 2S*Q    (   
  12 2STB          TRACE    ACOMMUNE8(2),USESTX  
  10 2T*2          TRACE    ACOMMUNE9(2) 
   4 2TSL    )   
  10 2T_S ...      LDX   0  ACOMMUNE8(2) 
  15 2W72 ...      STO   0  AWORK1(2)           [SAVE EXOF NO
   7 2W#=          NGN   0  1
   9 2WRW          STO   0  AWORK2(2)
   8 2X?G          BRN      XC   
   4 2XR6    #   
   9 2Y=Q    QK5REQ         [SMUSEST 
  15 2YQB          LDCT  7  #400                [S-VERSION   
  15 2_=2          ORN   7  1                   [M-VERSION   
   7 2_PL    XJ    LDN   5  0
   9 329=    #SKI  K6REQCART>199-199 
  12 32NW          TRACE    ACOMMUNE9(2),SMUSEST 
   8 338G          BRN      XB   
   4 33N6    #   
  15 347Q    QK4REQ                             [S-VERSION   
  15 34MB          LDCT  7  #400                [S-VERSION   
   9 3572    #SKI  K6REQCART>199-199 
  12 35LL          TRACE    ACOMMUNE9(2),SUSEST  
   8 366=          BRN      XB   
   4 36KW    #   
  15 375G    QK2REQ                             [M-VERSION   
  15 37K6          LDN   7  1                   [M-VERSION   
   7 384Q          LDN   5  0
   9 38JB    #SKI  K6REQCART>199-199 
   4 3942    (   
  12 39HL          TRACE    ACOMMUNE8(2),MUSEST  
  10 3=3=          TRACE    ACOMMUNE9(2) 
   4 3=GW    )   
   8 3?2G          BRN      XA   
   4 3?G6    #   
   9 3?_Q    QK1REQ         [USEST   
  16 3#FB          LDN   7  0                   [ORDINARY VERSION
   9 3#_2    #SKI  K6REQCART>199-199 
   4 3*DL    (   
  12 3*Y=          TRACE    ACOMMUNE8(2),USEST   
  10 3BCW          TRACE    ACOMMUNE9(2) 
   4 3BXG    )   
  10 3CC6    XA    LDX   0  ACOMMUNE8(2) 
  16 3CWQ          STO   0  AWORK1(2)           [SAVE EXOF NUMBER
  10 3DBB    XB    LDX   6  ACOMMUNE9(2) 
   9 3DW2          STOZ     AWORK2(2)
   5 3F*L    XSTART  
  20 3FT=          FINDSU   3,6,NOSTUC          [FIND ASTUC,IF ANY,FOR THIS UNIT 
   8 3G#W          LDCT  0  #200 
   9 3GSG          ANDX  0  ASMARK(3)
  17 3H#6          BNZ   0  NOSTUC              [JUMP IF DISENGAGED  
  15 3HRQ          LADDP    HUNIT(3),2          [PTR TO APIA 
   9 3HXM ...      LDCT   0   #200   
  10 3J3J ...      ANDX   0   BSUNIT(2)  
   9 3J7F ...      BNZ   0   NOSTUC  
   7 3J?B          LDN   0  1
  18 3JR2          ADS   0  BSUNIT2(2)          [UPDATE COUNT FO USERS   
   6 3K5C ...#UNS  IPBD  
   4 3KCS ...#SKI
   4 3KQ=    (   
   7 3L9W          LDXC  0  7
  17 3LPG          BCS      /                   [FORGET STRUSTED BIT 
   7 3M96          SBN   0  2
  16 3MNQ          BZE   0  XD                  [J IF TSMUSEST   
   7 3N8B          LDN   0  1
  17 3NN2          ADS   0  BSUNIT4(2)          [ELSE UPDATE COUNT   
   4 3P7L    )   
  17 3PM=          BNG   7  XD                  [JUMP IF S-VERSIONS  
  16 3RKQ    XC    PLOCKSU  6,XM,XN             [GET THE LOCK-OUT
   8 3S5B          MFINDSU  3,6  
  20 3SK2          LDCT  4  #140                [JUMP IF REDONED AND/OR S TRUSTED
   9 3T4L          ANDX  4  ASMARK(3)
   8 3TJ=          BNZ   4  XEA  
  21 3W3W          LDN   1  BCCB                [CHECK IF UNIT IN USE BY S-TRUSTED   
   9 3WHG    XG    LDX   1  BPTR(1)  
   8 3X36          TXU   1  CXCC 
  17 3XGQ          BCC      XD                  [JUMP IF END OF CHAIN
   9 3Y2B          LDX   0  BACK2(1) 
  18 3YG2          BZE   0  XD                  [JUMP IF NO MORE EXOFS   
   8 3Y_L          SMO      FX2  
   9 3_F=          TXU   0  AWORK1   
  16 3_YW          BCC      XG                  [JUMP IF OUR EXOF
   7 42DG          LDCT  0  4
   9 42Y6          ANDX  0  EXMARK(1)
  18 43CQ          BZE   0  XG                  [JUMP IF NOT S-TRUSTED   
   9 43XB          TXU   6  ASUSN(1) 
  16 44C2          BCC      XE                  [JUMP IF IN USE  
   7 44WL          LDN   4  0
  16 45B=          LDEX  0  FIP+5(1)            [NUMBER OF AREAS 
   8 45TW    XH    BZE   0  XG   
   7 46*G          SMO      4
  10 46T6          TXU   6  FDCELLS(1)   
  16 47#Q          BCC      XE                  [JUMP IF IN USE  
   7 47SB          ADN   4  6
   7 48#2          SBN   0  1
  17 48RL          BRN      XH                  [BACK FOR NEXT AREA  
  15 49?=    XD    LDXC  6  7                   [UNIT IS OK  
  16 49QW          BNZ   6  XF                  [J IF M-VERSION  
   7 4==G    #SKI  ENTWINE   
   4 4=Q6    (   
   7 4?9Q          LDXC  6  7
  18 4?PB          BCS      XR                  [J IF ORDINARY S-VERSION 
   7 4#92          UPPLUS   3
   4 4#NL    )   
   7 4*8=    XR    UPPLUS   1
   8 4*MW    XF    BZE   5  XK   
  17 4B7G          BNG   5  XL                  [JUMP IF NO DEMAND   
  20 4BM6          VFREE    ADATA,ADEMAND       [FREE ANY BLOCKS LEFT BY DEMAND  
   9 4C6Q          VFREE    GMON,AOUT
   7 4CLB    XL    LDN   4  6
   8 4D62          LDX   2  FX2  
   9 4DKL          OUTPARAM 4,AWORK3 
  18 4F5=          MONOUT   EWAV                [OUTPUT 'NOW AVAILABLE'  
   8 4FJW    #SKI  ENTWINE<1$1   
   7 4G4G    XK    UPPLUS   2
   7 4GJ6    #SKI  ENTWINE   
   4 4H3Q    (   
   7 4HHB    XK    LDXC  7  7
  16 4J32          BCS      XQ                  [J IF S-M VERSION
   7 4JGL          UPPLUS   3
   7 4K2=    XQ    UPPLUS   2
   4 4KFW    )   
  16 4K_G    NOSTUC                             [UNIT NOT ONLINE 
   7 4LF6          LDXC  0  7
  17 4LYQ          BNZ   0  YA                  [JUMP IF M-VERSIONS  
   5 4MDB          UP
  17 4MY2    YA    LONGSET  #67,XER2            [SET WAITING STYLE   
   7 4NCL          LDXC  0  5
  17 4NX=          BNZ   0  REENT               [JUMP IF NOT 1ST TIME
   8 4PBW          LDX   2  FX2  
   9 4PWG          LDN   1  AWORK3(2)
   7 4QB6          LDX   4  6
   7 4QTQ          LDN   0  6
   7 4R*B          SLL   4  3
   7 4RT2    YC    SLC   4  6
   8 4S#L          DCH   4  0(1) 
   7 4SS=          BCHX  1  /
   7 4T?W          SRL   4  3
   8 4TRG          BCT   0  YC   
   7 4W?6          LDN   4  6
   9 4WQQ          OUTPARAM 4,AWORK3 
  15 4X=B          MONOUT   EWPUT               [WAITING ... 
   7 4XQ2          LDN   5  1
   9 4Y9L          OUTPARAM 4,AWORK3 
   6 4Y?J ...#UNS  IPBD  
   4 4Y*G ...(   
  11 4YCD ...      OUTPACK  JOBNO(2),1,VARDEC
  15 4YFB ...      DEMAND   1,EWSULOAD,XCANT,XBREAK,REENT,2  
   4 4YH# ...)   
   6 4YK= ...#UNS  IPBD  
   4 4YM8 ...#SKI
  14 4YP=          DEMAND   1,EWLOAD,XCANT,XBREAK,REENT,2
   9 4_8W          BRN      XSTART   
   4 4_NG    #   
   7 5286    #SKI  ENTWINE   
   4 52MQ    (   
   9 537B    XN    LDX   0  AWORK2(2)
  15 53M2          BNG   0  XT                  [J IF USESTX 
   7 546L          NGNC  4  1
  14 54L=    XM    LDN   4  0                   [BREAKIN 
   6 54NT ...#UNS  IPBD  
  18 54RD ...      RELST    6                   [REDUCE COUNT OF USERS   
   6 54W3 ...#UNS  IPBD  
   4 54YL ...#SKI
   4 5539 ...(   
   8 555W          MFINDSU  3,6  
  10 55KG          LADDP    HUNIT(3),2   
   7 5656          LDN   0  1
  10 56JQ          SBS   0  BSUNIT2(2)   
   4 56M* ...)   
   6 56PY ...#UNS  IPBD  
   4 56X6 ...#SKI
  10 574B          SBS   0  BSUNIT4(2)   
   9 57J2          BZE   4  XBREAK   
   7 583L    XT    UPPLUS   2
   4 58H=    )   
  14 592W    XBREAK                             [BREAKIN 
   7 59GG          UPPLUS   1
   4 5=26    #   
  14 5=FQ    XCANT                              [CANTDO  
   7 5=_B          LDN   4  6
   9 5?F2          OUTPARAM 4,AWORK3 
  16 5?YL          MONOUT   EWNOTAV             [NOT AVAILABLE   
   5 5#D=          UP
   4 5#XW    #   
  10 5*CG    #     IN USE BY S-TRUSTED   
  19 5*X6    XEA   SLL   4  3                   [MOVE REDONED BIT TO BIT 0   
   7 5BBQ          LDXC  4  4
  16 5BWB    XE    LDN   4  0                   [X4=1 IF REDONED 
  17 5CB2          LONGSET  #71,XER2            [SET WAITING STYLE   
   7 5CTL          UNLOCKSU 6
  15 5D*=          LADDP    HUNIT(1),3          [PTR TO AIPA 
   9 5DSW          LDX   0  AWORK2(2)
   8 5F#G          BNG   0  YH   
   7 5FS6          LDN   0  1
  20 5G?Q          SBS   0  BSUNIT2(3)          [SUBTRACT 1 FROM COUNT OF USERS  
   6 5GDY ...#UNS  IPBD  
   4 5GL6 ...#SKI
  10 5GRB          SBS   0  BSUNIT4(3)   
  17 5H?2          BNZ   7  YD                  [JUMP IF M-VERSIONS  
   7 5HQL    YH    LONGOFF   
   5 5J==          UP
  18 5JPW    YD    BNG   5  YF                  [JUMP IF NOT FIRST TIME  
   7 5K9G          LDXC  0  5
  19 5KP6          BZE   0  YE                  [JUMP IF NO PREVIOUS DEMAND  
  17 5L8Q          VFREE    ADATA,ADEMAND       [FREE DEMAND BLOCKS  
   9 5LNB          VFREE    GMON,AOUT
  17 5M82    YE    SMO      FX2                 [OUTPUT SERIAL NUMBER
   9 5MML          LDN   1  AWORK3   
   7 5N7=          LDX   3  6
   7 5NLW          LDN   0  6
   7 5P6G          SLL   3  3
   7 5PL6    YG    SLC   3  6
   8 5Q5Q          DCH   3  0(1) 
   7 5QKB          BCHX  1  /
   7 5R52          SRL   3  3
   8 5RJL          BCT   0  YG   
   7 5S4=          LDN   3  6
  19 5SHW          BZE   4  YEA                 [JUMP IF HASN'T BEEN REDONED 
  16 5T3G          OUTPARAM 3,AWORK3            [SUSN TO MESSAGE 
  21 5TH6          MONOUT   EWREDS              [MAY ONLY BE ACCESSED BY TRUSTED...  
  18 5W2Q    YEA   OUTPARAM 3,AWORK3            [...PROGS-SUSN TO MESSAGE
   7 5WGB    #SKI  ALWMK7<1$1
  18 5X22          MONOUT   EWREL               [WAITING FOR S-TRUSTED   
   8 5XFL          LDCT  5  #400 
   7 5X_=    #SKI  ALWMK7<1$1
   9 5YDW    YF    LONGSTOP XBREAK   
   6 5YYG    #SKI  ALWMK7
  18 5_D6          LONGSTOP XBREAK,,EWREL,YF    [WAITING FOR S-TRUSTED   
   9 5_XQ          BRN      XSTART   
   4 62CB    #   
   4 62X2    #   
  10 63BL    #     ENTRY POINT FOR RELST 
   4 63W=    #   
   4 64*W    #   
   5 64TG    QK3REQ  
   6 657X ...#UNS  IPBD  
   4 65G# ...#SKI
   4 65SQ    (   
   7 66#B          NGNC  7  1
   9 66S2    QK8REQ         [TRELSTX 
   7 67?L          LDN   7  0
   4 67R=    )   
  10 68=W          LDX   6  ACOMMUNE9(2) 
   9 68QG    #SKI  K6REQCART>99-99   
   9 69=6          TRACE    6,RELST  
  15 69PQ          MFINDSU  3,6                 [FIND ASTUC  
  15 6=9B          LADDP    HUNIT(3),2          [PTR TO AIPA 
   6 6=HR ...#UNS  IPBD  
   4 6=W8 ...#SKI
   4 6?8L    (   
  16 6?N=          BZE   7  ZE                  [J IF T-VERSION  
   9 6#7W    #SKI  K6REQCART>99-99   
   4 6#MG    (   
  10 6*76          SMO      BSUNIT4(2)   
   7 6*LQ          LDN   0  0
   8 6B6B          BNZ   0  ZC   
   9 6BL2          GEOERR   1,TRELSTX
   4 6C5L    ZC  
   4 6CK=    )   
  18 6D4W          SBS   7  BSUNIT4(2)          [UPDATE COUNT OF USERS   
  10 6DJG          SMO      BSUNIT4(2)   
   7 6F46          LDN   0  0
  16 6FHQ          BNZ   0  ZE                  [J IF SOME USERS 
   7 6G3B          LDCT  0  2
   9 6GH2          ANDX  0  BSUNIT(2)
  21 6H2L          BZE   0  ZE                  [J IF OTHER SIDE DOES NOT WANT LOCK  
  17 6HG=          AUTWIN   GLKT,6              [GIVE IT THE LOCK-OUT
   4 6H_W    ZE  
   8 6JFG          MFINDSU  3,6  
  10 6J_6          LADDP    HUNIT(3),2   
   4 6KDQ    )   
   9 6KYB    #SKI  K6REQCART>99-99   
   4 6LD2    (   
  19 6LXL          SMO      BSUNIT2(2)          [CHECK CUNT FO USERS OON-ZERO
  12 6MC=          LDN   0  0                   [
   8 6MWW          BNZ   0  ZA   
   9 6NBG          GEOERR   1,RELST  
   4 6NW6    ZA  
   4 6P*Q    )   
   7 6PTB          LDN   0  1
  20 6Q*2          SBS   0  BSUNIT2(2)          [SUBTRACT 1 FROM COUNT FO USERS  
  10 6QSL          SMO      BSUNIT2(2)   
   7 6R#=          LDN   0  0
  17 6RRW          BNZ   0  ZB                  [JUMP IF STILL IN USE
   8 6S?G          LDCT  0  #200 
   9 6SR6          ANDX  0  BSUNIT(2)
  20 6T=Q          BZE   0  ZB                  [JUMP IF NOT TO WAKE UP GRUESOME 
   9 6TQB          LDX   6  HUNIT(3) 
   9 6W=2          GETACT   ACTDOC   
   9 6WPL          LDX   2  BPTR(2)  
  18 6X9=          LINKSET  2,ZLINK(1)          [SET LINK TO K1CARTCONT  
  21 6XNW          STO   6  CPPTR(2)            [STORE DEVICE LIST PTR FOR GRUSARSE  
  15 6Y8G          LDX   6  2                   [SAVE POINTER
  17 6YN6          LDN   2  BACT                [ADDRESS OF BLANKET  
  21 6_7Q          RINGACT  6,2                 [PUT ACTIVITY AT END OF CHAIN/RING   
   7 6_MB          LDX   2  6
   6 7272          FPUT  
   5 72LL    ZB    UP
   4 736=    #   
   6 736# ...#UNS  IPBD  
   4 736B ...(   
   4 736D ...[   
   8 736G ...[     LOCKM MACRO   
   4 736J ...[   
  15 736L ...[     ON ENTRY ACOMMUNE8 CONTAINS HOME UNIT SUSN
  15 736N ...[              ACOMMUNE9 CONTAINS AREA UNIT SUSN
   4 736Q ...[   
  19 736S ...[     PLOCKSU IS OBEYED FOR THE LOWEST GEOGRAPHICAL   NO. FIRST 
   4 736W ...[   
  20 736Y ...[     ON EXIT FOR ERRORS ANY LOCKED UNIT IS UNLOCKED AND THE SUSN   
  13 7372 ...[     OF THE UNIT IS STORED IN ACOMMUNE9
   4 7374 ...[   
   5 7376 ...QK10REQ 
  15 7378 ...      LDX   6  ACOMMUNE8(2)        [X6=HOME UNIT
  16 737= ...      LDX   5  6                   [=X5 HOME UNIT   
  15 737# ...      LDX   7  ACOMMUNE9(2)        [X7=AREA UNIT
  19 737B ...      MFINDSU  1,6                 [X1 PTS TO ASTUC OF HOME UNIT
  20 737D ...      LDX   4  GEOGU(1)            [X4=GEOGRAPHICAL NO OF HOME UNIT 
   7 737G ...      TXU   6  7
  17 737J ...      BCC      XLB                 [J IF HOME=AREA UNIT 
  19 737L ...      MFINDSU  1,7                 [X1 PTS TO ASTUC OF AREA UNIT
   9 737N ...      SBX   4  GEOGU(1) 
  21 737Q ...      BNG   4  XLB                 [J IF HOME UNIT LOWER THAN AREA UNIT 
  17 737S ...      LDX   5  7                   [ELSE LOAD AREA UNIT 
  19 737W ...XLB   STO   5  AWORK1(2)           [REMEMBER IN CASE OF FAILURE 
  19 737Y ...      PLOCKSU  5,XBRK1,XIPB        [GET LOCK OUT OF LOWEST UNIT 
   7 7382 ...      TXU   6  7
  17 7384 ...      BCC      UP2                 [J IF HOME=AREA UNIT 
  18 7386 ...      LDX   5  6                   [RELOAD X5 TO HOME UNIT  
  21 7388 ...      BPZ   4  XLC                 [J IF HOME UNIT HIGHER THAN AREA UNIT
  16 738= ...      LDX   5  7                   [LOAD AREA UNIT  
  19 738# ...XLC   PLOCKSU  5,XBRK2,XPB2        [GET LOCK-OUT FOR OTHER UNIT 
  14 738B ...UP2   UPPLUS   2                   [EXIT  OK
   4 738D ...[   
  11 738G ...[     BREAKIN ON FIRST PLOCKSU  
   4 738J ...[   
   7 738L ...XBRK1 NGNC  3  1
   4 738N ...[   
  11 738Q ...[     IPB FAIL ON FIRST PLOCKSU 
   4 738S ...[   
  19 738W ...XIPB  LDN   3  0                   [X3=0 IF IPB; X3=1 IF BREAKIN
   8 738Y ...      BRN      XEND 
   4 7392 ...[   
  11 7394 ...[     BREAKIN ON SECOND PLOCKSU 
   4 7396 ...[   
   7 7398 ...XBRK2 NGNC  3  1
   4 739= ...[   
  11 739# ...[     IPB FAIL ON SECOND PLOCKSU
   4 739B ...[   
  20 739D ...XPB2  LDN   3  0                   [X3=0 FOR IPB;X3=1 FOR BREAKIN   
  20 739G ...      UNLOCKSU AWORK1(2)           [UNLOCK PREVIOUSLY LOCKED UNIT   
  21 739J ...XEND  STO   5  ACOMMUNE9(2)        [STO UNLOCKABLE FOR HIGHER ROUTINE   
  15 739L ...      BNZ   3  UP                  [J IF BREAKIN
   7 739N ...      UPPLUS   1
   5 739Q ...UP    UP
   4 739S ...[   
   8 739W ...[     UNLOCKSU MACRO
   4 739Y ...[   
  14 73=2 ...[     ACOMMUNE9=STORAGE UNIT SERIAL NUMBER  
   4 73=4 ...[   
  19 73=6 ...[     IT WAKES UP ANYONE WAITING FOR LOCK OUT IN THIS PROCESSOR 
  21 73=8 ...[     AND THE OTHER IF SO INDICATED. THE COUNT OF USERS IN BSUNIT4 IS   
  16 73== ...[     REDUCED AND ANY EWDAS/ITCH BLOCKS ARE FREED   
   4 73=# ...[   
   5 73=B ...QK11REQ 
  10 73=D ...      LDX   0  ACOMMUNE9(2) 
  15 73=G ...      STO   0  AWORK1(2)           [SAVE SUSN   
  21 73=J ...      FON      #72                 [WAKE UP ACTIVITIES IN THIS MACHINE..
  18 73=L ...                                   [..WAITING FOR LOCK OUT  
  16 73=N ...      MFINDSU  1,AWORK1(2)         [GET PTR TO ASTUC
   8 73=Q ...      LDCT  0  #400 
   9 73=S ...#SKI  K6EXOFILE>199-199 
   4 73=W ...(   
   9 73=Y ...      ANDX  0  ASMARK(1)
   8 73?2 ...      BNZ   0  ND1  
  21 73?4 ...      GEOERR   1,UNLOCKSU          [TRYING TO UNLOCK WHEN ALREADY UNLOCK
   4 73?6 ...ND1 
   4 73?8 ...)   
  17 73?= ...      ERS   0  ASMARK(1)           [REVERSE LOCK-OUT BIT
  16 73?# ...      LADDP    HUNIT(1),1          [X1 PTR TO APIA  
  10 73?B ...      LDX   0  BSUNIT4(1)   
  19 73?D ...      SBN   0  1                   [TAKE ONE FROM COUNT OF USERS
   8 73?G ...      BPZ   0  ND2  
  19 73?J ...      GEOERR   1,USER CT           [COUNT OF USERS WOULD BE -VE 
  18 73?L ...ND2   STO   0  BSUNIT4(1)          [PUT BACK REDUCED COUNT  
  18 73?N ...      BNZ   0  ND3                 [J IF STILL SOME USERS.. 
  20 73?Q ...      LDCT  0  2                   [..ELSE CHECK IF OTHER MACHINE.. 
  16 73?S ...      ANDX  0  BSUNIT(1)           [..WANTS LOCK OUT
  16 73?W ...      BZE   0  ND3                 [J IF IT DOES NOT
  18 73?Y ...      AUTWIN   GLKT                [ELSE GIVE LOCK OUT AWAY 
  10 73#2 ...ND3   HUNTW    1,EWDAS,ITCH 
  16 73#4 ...      BNG   1  ND4                 [J IF NOT THERE  
   9 73#6 ...      LDX   0  ENSUSN(1)
   8 73#8 ...      SMO      FX2  
   9 73#= ...      TXU   0  AWORK1   
  18 73## ...      BCS      ND4                 [OR FOR A DIFFERENT SUSN 
  17 73#B ...      FREECORE 1                   [OTHERWISE FREE IT   
  15 73#D ...ND4   UP                           [..AND FINISH
   4 73#G ...[   
  12 73#J ...[     TWAIT MACRO  ACOMMUNE9=SUSN   
   4 73#L ...[   
   5 73#N ...QK12REQ 
   6 73#Q ...#UNS  ERATR 
   7 73#S ...      LDN   6  0
  17 73#W ...      LDN   5  0                   [MKR FIRST TIME ROUND
  10 73#Y ...      LDX   0  ACOMMUNE9(2) 
   9 73*2 ...      STO   0  AWORK1(2)
  18 73*4 ...RQ4   LONGSET  IPBDE,XER2          [WE ARE TO WAIT PERHAPS  
  15 73*6 ...      FINDSU   1,AWORK1(2),RQ3     [FIND ASTUC  
  13 73*8 ...      LADDP    HUNIT(1),3          [APIA
  15 73*= ...      JSHARED  3,RQ3               [J IF SHARED 
  18 73*# ...      JDED     3,RQ1               [J IF DEDICATED TO END   
   6 73*B ...#UNS  ERATR 
   4 73*D ...(   
   9 73*G ...RQ3   LDXC  0  ETWINMK  
  20 73*J ...      BCS      NOTIN               [J IF TWINNING NOT INITIALISED   
   7 73*L ...      LDXC  5  5
  14 73*N ...      BCS      /                   [CLEAR B0
   7 73*Q ...      LDN   6  0
   9 73*S ...      LDX   0  ETWINMK  
   4 73*W ...)   
   6 73*Y ...#UNS  ERATR 
   4 73B2 ...#SKI
   9 73B4 ...RQ3   LDX   0  ETWINMK  
  11 73B6 ...      ANDN  0  IPBDOWN+GEODOWN  
  15 73B8 ...      BZE   0  RQ1                 [J IF IPB OK 
   9 73B= ...      ANDN  0  GEODOWN  
  19 73B# ...      BNZ   0  RQ1                 [OR IF OTHER PROCESSOR DOWN  
  20 73BB ...      BNZ   5  RQ5                 [J IF WE'VE BEEN THIS WAY BEFORE 
  18 73BD ...      LDN   5  1                   [MESS ALREADY OUTPUT MKR 
  12 73BG ...      OUTPACK  AWORK1(2),1,CSN     [
  20 73BJ ...      MONOUT   IPBSTB              [OUTPUT WAITING FOR IPB OR SUSN  
  11 73BL ...      OUTPACK  JOBNO(2),1,VARDEC
  11 73BN ...      OUTPACK  AWORK1(2),1,CSN  
   6 73BQ ...#UNS  ERATR 
   4 73BS ...(   
  15 73BW ...      LDX   7  XMESS1(1)           [JOB N NEEDS 
  10 73BY ...XLON  LONGSTOX RQ8,RQ7,7,RQ5
   4 73C2 ...)   
   6 73C4 ...#UNS  ERATR 
   4 73C6 ...#SKI
  20 73C8 ...      LONGSTOP RQ8,RQ7,IPBSTA,RQ5  [WAIT AND OUTPUT JOB N NEEDS SU  
  18 73C= ...      BRN      RQ4                 [SEE IF EVENT OCCURRED   
   6 73C# ...#UNS  ERATR 
   4 73CB ...(   
  11 73CD ...[     TWINNING NOT INITIALISED  
   5 73CF ...NOTIN   
  15 73CG ...      NGN   0  2                   [X0=#77777776
  16 73CH ...      ANDX  5  0                   [CLEAR B1 OF X5  
   8 73CJ ...      LDX   1  FX1  
   9 73CL ...      LDX   0  ETWINMK  
  11 73CN ...      ANDN  0  INITCOM+INITRQ   
  21 73CQ ...      BZE   0  NOWT                [J IF NO INIT REQUESTED OR RUNNING   
   9 73CS ...      ANDN  0  INITCOM  
  19 73CW ...      BNZ   0  RQ5                 [J IF INITIALISE IN PROGRESS 
  20 73CY ...      BNZ   6  XRENT               [REENTER DEMAND IF WE DEMANDED   
  20 73D2 ...      BNG   5  RQ5                 [J IF ALREADY OUTPUT TO MON FILE 
  17 73D4 ...      LDX   7  XMESS2(1)           [WAITING FOR TN IN   
   8 73D6 ...      LDCT  0  #400 
   7 73D8 ...      ORS   0  5
  16 73D= ...      BRN      XLON                [ENTER LONGSTOP  
   7 73D# ...NOWT  LDN   6  1
   9 73DB ...      LDN   0  INITRQ   
  20 73DD ...      ORS   0  ETWINMK             [SET INITIALISE IN REQESTED BIT  
  17 73DG ...      MONOUTX  XMESS2(1)           [WAITING FOR TN IN   
  13 73DJ ...      DEMAND   1,ENTIN25,RQ7,RQ8,XRENT,2
   8 73DL ...      BRN      RQ4  
   4 73DN ...[   
   4 73DQ ...[   
  15 73DS ...XMESS1         +IPBSTA             [JOB N NEEDS 
  19 73DW ...XMESS2         +EINWT              [WAITING FOR TN INITIALISE   
   4 73DY ...)   
   7 73F2 ...RQ7   LONGOFF   
   8 73F4 ...      BRN      RQ4  
   7 73F6 ...RQ1   LONGOFF   
   7 73F8 ...      UPPLUS   1
  14 73F= ...RQ8   UP                           [BREAKIN 
   4 73F# ...)   
  10 73KW          MENDAREA 20,K99REQCART
   4 745G    #   
   4 74K6    #END
   8 ____ ...06424615000100000000
