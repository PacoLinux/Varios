  16 22FL    #LIS           K0OPCA>K0OBJPROG>K0ALLGEO>K0GREATGEO 
  14 22RB ...      SEG      OPCA,867,SECTION CENT,,G400  
   4 2357 ...[   
  15 2394 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23#_ ...[   
   6 23HF ...#UNS G400   
  16 23P_ ...#      THIS CHAPTER IS MODIFIED FOR G3PLUS-IH MK 2  
  10 23YG          SEGENTRY K1OPCA,XENT1 
  21 24D6    #     THIS SEGMENT IS ENTERED TO DECODE AN EVENT IN AN OBJECT PROGRAM   
  21 24XQ    #     IT EXAMINES EVENT1,2,3 & JUMPS OR SWITCHES TO APPROPRIATE ROUTINE 
  13 25CB    #     E0ENT1   POINTER TO CURRENT MEL   
  20 25X2    #     EVENT2   N(M) AFTER ALL MODIFICATION(EXTRACODE EVENTS ONLY)   
  18 26BL    #     EVENT3   X OR REPLACED X (15 GROUP EXTRACODES ONLY)   
  21 26W=    #     EVENT4   CONTENTS OF THE 1ST WORD OF THE PERIPHERAL CONTROL AREA  
  12 27*W    #              (157 EXTRA CODE ONLY)
  19 27TG    #     EVENT5   COPY OF THE ORIGINAL ORDER(EXTRACODE EVENTS ONLY)
  10 28*6    #     EVENT6   ERROR CODES: 
   4 28SQ    #   
  16 29#B    #              0=EXTRACODE NOT IMPLEMENTED BY EXEC  
  13 29S2    #              1=INOPERABLE PERIPHERAL  
  21 2=?L    #              2=PAGE REQUEST(GEORGE ERROR-PAGETURN SHOULD BE WOKEN UP) 
  14 2=R=    #              3=FLOATING POINT UNDERFLOW   
  13 2?=W    #              4=FLOATING POINT OVERFLOW
  11 2?QG    #              5=PARITY FAILURE 
  12 2#=6    #              6=HARDWARE ILLEGAL   
  15 2#JH ...#              8=MEMBERS MILL TIMER OVERFLOWED  
  17 2#WY ...#              7,9 UPWARDS=VARIOUS ILLEGAL EXTRACODES   
   6 2*9B    # PRESETS   
   9 2*P2    QMASK          #77777700
   9 2B8L    QMASK1         #60000000
   9 2BN=    QMASK2         #07700000
   9 2C7W    QMASK3         #77777   
  19 2CCN ...QMAXPOS        #37777777           [MAX. POSITIVE NUMBER IN WORD
   9 2CMG    XCHOBEY        +CHOBEY  
   8 2D76    XK61           +K61 
   7 2DLQ    #  ENTRY POINTS 
   4 2F6B    #   
  13 2FL2    # THE STANDARD ENTRY POINT FOR THE PCA  
   5 2G5L    XENT1   
  10 2GK=    #SKIP          K6OPCA>99-99 
   4 2H4W    (   
  12 2HJG          TRACE    EVENT1(2),OPCEVEN1   
  12 2J46          TRACE    EVENT6(2),OPCEVEN6   
   4 2JHQ    )   
  11 2L2L    #SKIP          K6OPCA>999-999   
   4 2LG=    (   
  10 2L_W          HUNT     3,AOBJPROG   
   6 2M3? ...#UNS CA1D   
   4 2M4N ...(   
   8 2M65 ...      ADX   3  CA1D 
  10 2M7G ...      TRACE    9(3),OBJWORD9
  10 2M8X ...      TRACE    8(3),OBJWORD8
   8 2M=# ...      SBX   3  CA1D 
   4 2M?P ...)   
   6 2M*6 ...#UNS CA1D   
   4 2MBH ...#SKI
   4 2MCY ...(   
  11 2MFG          TRACE    A1D+9(3),OBJWORD9
  11 2M_6          TRACE    A1D+8(3),OBJWORD8
   4 2N8Y ...)   
   4 2NDQ    )   
   8 2PD2          TESTPUC  ,XPUC
  16 2PXL          BRN      XNPUC               [ J IF NON PUC   
  15 2QC=    XPUC  LDX   3  EVENT6(2)           [ERROR CODE  
  17 2QWW          BZE   3  XPEX                [J IF PUC EXTRACODE  
  17 2RBG          LDCT  5  #500                [PUC ILLEGAL -B0/2   
  15 2RW6          CALL  6  XTPFN               [FOR TP WD 31
   8 2S*Q          MACCS    ,FX2 
   8 2STB          BRN      X31ST
   4 2T*2    XPEX
  17 2TSL          LDX   3  EVENT5(2)           [COPY OF EXTRACODE   
  17 2W#=          SLC   3  10                  [ISOLATE FUNCTION AND
  16 2WRW          ANDN  3  #77                 [SAVE L.S. 6 BITS
  18 2X?G          STO   3  7                   [REMBER LEAST SIG 6 BITS 
   8 2XR6          SBN   3  #65  
  16 2Y=Q          BNZ   3  XPEX1               [J IF NOT GIVE   
  15 2YQB          LDX   3  EVENT2(2)           [ENTER TP IF 
  14 2_=2          SBN   3  3                   [GIVE/3  
   8 2_PL          BZE   3  XPEX1
  14 329=          SBN   3  1                   [GIVE/4  
   8 32NW          BZE   3  XPEX1
  14 338G          SBN   3  1                   [GIVE 5  
  13 33N6          BZE   3  XPEX1               [OR  
  14 347Q          SBN   3  5                   [GIVE 10 
  13 34MB          BZE   3  XPEX1               [OR  
  14 3572          SBN   3  2                   [GIVE 12 
   8 35LL          BZE   3  XPEX1
   7 366=          SBN   3  4
  18 36KW          BNG   3  XGIVE               [REPORT PERM XCODES TO TP
   8 375G    XPEX1 CALL  6  XTPFN
   9 37K6          LDX   3  EVENT2(2)
  16 384Q          SMO      4                   [SET TP WD 5 TO  
  16 38JB          STO   3  5                   [ N(M) OF XCOD   
  17 3942          LDX   3  EVENT5(2)           [COPY  OF EXTRA CODE 
   7 39HL          SLC   3  3
  15 3=3=          ANDN  3  7                   [SET TP WD 4 
  14 3=GW          SMO      4                   [TO  X OF
  14 3?2G          STO   3  4                   [ XCOD   
  17 3?G6          PMODES   3                   [PICK UP PUC MODES   
   8 3?_Q          ANDN  3  #50  
   8 3#FB          BZE   3  XP6  
  16 3#_2          LDCT  3  #177                [IF EBM OR 22 AM 
  18 3*DL          BRN      XP6A                [ISOLATE B2-23 OF PUC ONR
  18 3*Y=    XP6   LDCT  3  0                   [OTHERWISE ISOLATE B9-23 
   9 3BCW    XP6A  ADX   3  QMASK3(1)
   8 3BXG          MACCS    ,FX2 
   7 3CC6          SMO      0
   7 3CWQ          LDX   5  8
   7 3DBB          ANDX  5  3
  18 3DW2          SMO      4                   [STORE ISOLATE OF PUC ONR
  15 3F*L          STO   5  6                   [IN TP WD 6. 
   7 3FT=          SMO      7
   7 3G#W          LDN   5  0
   7 3GSG          SMO      4
   8 3H#6          DCH   5  31   
  21 3HRQ          LDCT  5  #600                [SET B0/1 FOR PUC XCOD IN TP WD 31   
   7 3J?B    X31ST SMO      4
   8 3JR2          ORS   5  31   
   7 3K=L          STEPBAC  0
   7 3KQ=          EVENTFIN  
  15 3L9W    XTPFN FINDTP   ,4                  [FIND TP X0  
  14 3LPG          MACCS    ,4,4                [ADDRESS 
   7 3M96          EXIT  6  0
   5 3MNQ    XNPUC   
   9 3N8B          LDX   0  EVENT6(2)
   4 3N=_ ...[   
   7 3N*J ...#UNS  ADUALDME  
   8 3ND7 ...      SBN   0  10   
   7 3NGQ ...#UNS  ADUALDME  
   4 3NK* ...#SKI
   7 3NN2 ...      SBN   0  9
   4 3NXS ...[   
  17 3P7L          BPZ   0  XILL                [ILLEGAL EXTRACODE   
   9 3PM=          SMO      EVENT6(2)
   9 3Q6W          BRN      TABERR   
   5 3QLG    TABERR  
  20 3R66          BRN      XTRACOD             [XTRAC NOT IMPLEMENTED BY EXEC   
  18 3RKQ          BRN      XGEOG               [INOPERABLE PERIPHERAL   
   9 3S5B          BRN      (GEOERR) 
  18 3SK2          BRN      XILL1               [FLOATING POINT:UNDERFLOW
  18 3T4L          BRN      XILL1               [              :OVERFLOW 
  16 3TJ=          BRN      XILLPF              [PARITY FAILURE  
  16 3W3W          BRN      XILL1               [HARDWARE ILLEGAL
  17 3WHG          BRN      XILL                [ILLEGAL EXTRACODE   
   7 3WJ6 ...#UNS  ADUALDME  
   4 3WJQ ...(   
   8 3WKB ...      BRN      XMTO 
   9 3WL2 ...      BRN      XILLOCP  
   4 3WLL ...XMTO
   4 3WM= ...)   
  21 3WMY ...#     OTHERWISE,MEMBER MILL TIMER OVERFLOW( E.G. LOOPING PROGRAM HIGHER 
  10 3WQ7 ...#     PRIORITY THAN GEORGE) 
  20 3WSB ...#     WILL ONLY RE-ENTER FROM  UNPLUG IF NOT TIME UP(TO DO RUNPROG) 
  12 3WSD ...#              CORRECTION FOR B8250 
  21 3WSG ...#              (EXEC NOTICES MILL TIMER OVERFLOW, INCREMENTS WORD 39,   
  20 3WSJ ...#              LEAVES ONLY REMAINDER IN MEL, HENCE LITTLE CHANCE OF 
   9 3WSL ...#              TIME UP) 
  18 3WSN ...#              SO CONVERT OVERFLOW AND ADJUST VARIOUS TIMERS
  20 3WSQ ...      LDX   5  QMAXPOS(1)          [MAXIMUM POSITIVE NUMBER IN WORD 
  18 3WSS ...      DVS   4  BEATTIME            [CONVERT TO MILLISECONDS 
   8 3WSW ...      LDN   6  AFW1 
  18 3WSY ...      ADX   6  FX2                 [X6 PTS TO 1ST MEL IN PCA
  21 3WT2 ...      LDX   7  EVENT1(2)           [X7 PTS TO MEL WHICH OVERFLOWED TIME 
  20 3WT4 ...      SBX   7  6                   [X7 NOW PTS TO THISMEL,RELATIVE  
  14 3WT6 ...                                  [TO FIRST 
   7 3WT8 ...      SMO      7
  20 3WT= ...      ADS   4  ATM1(2)             [ADD REMAINDER FROM CONVERSION TO
  18 3WT# ...                                   [WHAT EXEC LEFT IN TIMER 
   7 3WTB ...      SMO      7
  18 3WTD ...      LDX   6  ARM1(2)             [PICK UP MEMBER NUMBER   
   8 3WTG ...      SRL   6  18   
   7 3WTJ ...      SMO      6
  21 3WTL ...      ADS   5  ATU1(2)             [ADD MILLISECONDS TO TIME USED THIS  
  18 3WTN ...                                   [MILL SCHEDULING PERIOD  
  16 3WTQ ...      FJOPCA   3,2                 [FIND JOB BLOCK  
   7 3WTS ...      LDN   4  0
  20 3WTW ...      ADSC  5  HCLOCKTIME+1(3)     [ADD TO TIME CLOCKED BY PROGRAM  
  10 3WTY ...      ADS   4  HCLOCKTIME(3)
  20 3WW2 ...      ADSC  5  GPROGTIME+1         [ADD TO TOTAL OBJECT PROGRAM TIME
   9 3WW4 ...      ADS   4  GPROGTIME
  16 3WW6 ...      ADSC  5  HTIMEJ+1(3)         [ADD TO JOB TIMER
   9 3WW8 ...      ADS   4  HTIMEJ(3)
  21 3WW= ...      SBSC  5  APROGTIME+1(3)      [SUBTRACT FROM TIME LEFT FOR PROG.   
  10 3WW# ...      SBS   4  APROGTIME(3) 
  18 3WWB ...      ADS   5  HTIMES(3)           [ADD TO SLOT TIME USED   
   6 3WWK ...      UNPLUG
   7 3WYS ...      RUNPROG   
   5 3X36    XTRACOD 
   8 3XGQ    #SKI  K6OPCA>99-99  
   4 3Y2B    (   
  12 3YG2          TRACE    EVENT2(2),OPCEVEN2   
  12 3Y_L          TRACE    EVENT5(2),OPCEVEN5   
   4 3_F=    )   
  17 3_YW          LDX   3  EVENT5(2)           [COPY OF EXTRACODE   
  17 42DG          SLC   3  10                  [GET FUNCTION BITS   
   8 42Y6          ANDN  3  #177 
   8 43CQ          SBN   3  #150 
  16 43XB          SMO      3                   [USE AS MODIFIER 
   9 44C2          BRN      XTABLE   
   9 44WL    # EXTRACODE SWITCH TABLE
   5 45B=    XTABLE  
   9 45TW          BRN      XSUSBY   
   8 46*G          BRN      XREL 
   8 46T6          BRN      XDIS 
   8 47#Q          BRN      XISBY
   8 47SB          BRN      XCONT
   9 48#2          BRN      XSUSDP   
   9 48RL          BRN      XALLOT   
   8 49?=          BRN      XPERI
   9 49QW          BRN      XSDDTY   
   8 4==G          BRN      XSDD 
   9 4=Q6          BRN      XSUSMA   
   8 4?9Q          BRN      XAUTO
   9 4?PB          BRN      XSUSAR   
   8 4#92          BRN      XGIVE
   9 4#NL          BRN      XRQSLP   
   9 4*8=          BRN      XTPINS   
   5 4*MW    #REP 8  
   8 4B7G          BRN      XGEO 
  18 4BM6    #     ACROSS TO DISEVENT TO DEAL WITH DISENGAGED PERIPHERAL 
  10 4C6Q    XGEOG ACROSS   DISEVENT,1   
  21 4CLB    #     HARDWARE ILLEGAL:ENTER ILLEGAL ROUTINE WITH NO MESSAGE SPECIFIED  
   5 4D62    XILL1   
   9 4DKL    #     ILLEGAL EXTRACODE 
   4 4F5=    XILL
   6 4FJW          UNPLUG
   7 4G4G          ILLEGAL   
   8 4GJ6    #     PARITY FAILURE
   5 4H3Q    XILLPF  
   7 4HHB          UNPLUG   2
  12 4HLM ...# OUTPUT CORE PARITY FAIL TO JL & CL
   9 4HMQ ...      LDX   4  EVENT7(2)
   9 4HNT ...      ADX   4  GEODATUM 
   9 4HPY ...      LDX   5  EVENT5(2)
   7 4HR3 ...      OUTBLOCN 5
   9 4HS6 ...      OUTPAR   TIMENOW  
   9 4HT9 ...      OUTPACK  4,1,OCTA 
   9 4HW# ...      OUTPACK  5,1,OCTB 
   9 4HXC ...      MONOUT   JLCPAR   
  13 4H_K ...# OUTPUT PARITY FAIL TO OBJECT PROGRAM  
   9 4J32          ILLEGAL  PARFAIL  
   7 4J45 ...#UNS  ADUALDME  
   4 4J58 ...(   
   5 4J6? ...XILLOCP 
   7 4J7B ...      UNPLUG   2
   8 4J8F ...      OUTBLOCN 10   
   8 4J9J ...      FJOPCA   3,2,N
  12 4J=M ...      OUTPACK  JUSER(3),3,USERNAME  
   8 4J?Q ...      FJOPCA   3,2,N
  12 4J#T ...      OUTPACK  JNAME(3),3,JOBNAME   
  11 4J*Y ...      OUTPACK  EVENT7(2),1,NUMA 
   9 4JC3 ...      MONOUT   JPROGFAIL
   9 4JD6 ...      ILLEGAL  PROCFAIL 
   4 4JF9 ...)   
  11 4JGL    # FURTHER DECODING OF EXTRACODES
  13 4K2=    XSDDTY                        [160 GROUP
   9 4KFW          CALL  4  XCHECK   
  10 4K_G          ACROSS   OPCAXCOD,1   
  15 4LF6    XSDD                               [161 GROUP   
   9 4LYQ          CALL  4  XCHECK   
  10 4MDB          ACROSS   OPCAXCOD,2   
  11 4MY2    # EXTRACODES NOT YET DEALT WITH 
   5 4NCL    XSUSBY  
   5 4NX=    XISBY   
   5 4PBW    XSUSDP  
   5 4PWG    XSUSMA  
   4 4QB6    XGEO
   5 4QTQ    XILLXC  
   5 4R*B    XILLINS1
   6 4RT2          UNPLUG
   5 4S#L    XILLINS 
   9 4SS=          ILLEGAL  ILLINS   
   5 4T?W    XSUSAR  
   9 4TRG          LDX   3  EVENT5(2)
   7 4W?6          SLC   3  3
   7 4WQQ          ANDN  3  7
   7 4X=B          SBN   3  7
   9 4XQ2          BNZ   3  XILLXC   
   5 4Y9L    XACTP   
  15 4YP=          ACROSS   TPINS,2             [164/7 ORDER 
   5 4_8W    XTPINS  
  15 574B          ACROSS   TPINS,1             [167  ORDER  
  16 57J2    XCONT                              [CONT EXTRACODE  
  10 583L          ACROSS   CONTXCOD,1   
   7 58H=    # GIVE EXTRACODE
   5 592W    XGIVE   
   9 5#_S ...      LDX   4  EVENT2(2)
   7 5*3Q ...      SBN   4  4
  15 5*5N ...      BZE   4  XGVE               [J IF N(M) = 4
   7 5*7L ...      SBN   4  8
  16 5*9J ...      BZE   4  XGVE               [J IF N(M) = 12   
   9 5*?G ...      ACROSS   GIVEA,1  
   4 5**D ...XGVE
   9 5*CG          ACROSS   GIVE,1   
   5 5*X6    XRQSLP  
   9 5BBQ          LDX   4  EVENT5(2)
   8 5BWB          SRL   4  21   
   8 5CB2          BZE   4  S1   
   7 5CTL          SBN   4  1
   9 5D*=          BNZ   4  XILLXC   
  17 5DSW          ACROSS   REQESLIP,1          [WRITE REQUEST SLIP  
  17 5F#G    S1                                 [ENTRY FOR RRQ (X=0) 
   8 5FS6          LDN   6  16   
  13 5G?Q          CHECKB   EVENT2(2),6,XRESVIOL,,W  
  16 5GRB          MHUNTW   3,AOBRES            [FIND REQ. SLIP  
  16 5H?2          LDN   4  A1(3)               [START OF SLIP   
  13 5HQL    #     MOVE REQUEST SLIP TO PROGRAM AREA 
   9 5J==          LDX   5  EVENT2(2)
  11 5JPW          FINMOVE  2,5,4,6,HLOCK1   
   7 5K9G          EVENTFIN  
   4 5KP6    #   
  19 5L8Q    # THIS SECTION DEALS WITH AUTO N(M) NON ZERO(ESTABLISH MEMBER)  
   4 5LNB    #   
  16 5M82    # THE MEMBER MUST EXIST AND NOT ALREADY ESTABLISHED 
   9 5MML    XAUTO LDX   5  EVENT5(2)
  18 5N7=          BNG   5  XAIL1               [ERROR IF MEMBER NO > 3  
   7 5NLW          SLC   5  3
  18 5P6G          ANDN  5  7                   [MEMBR TO BE ESTAOLISHED 
  16 5PL6          LDX   3  5                   [KEEP IT IN X3   
  21 5Q5Q          MPY   5  XK61(1)             [GET PTR TO RELEVANT SUSPENSION WORD 
   7 5QKB          SMO      6
   9 5R52          LDX   4  ASU1(2)  
  19 5RJL          BNG   4  XAIL1               [ERROR IF MEMBER NONEXISTENT 
   7 5S4=          SLL   4  1
  21 5SHW          BPZ   4  XAIL2               [ERROR IF MEMBER ALREADY ESTABLISHED 
  17 5T3G          LDX   4  EVENT2(2)           [ENTRY FOR NEW MEMBER
  13 5TH6          CHECKA   4,,XRESVIOL,,XCHOBEY(1)  
   8 5W2Q          LDN   1  2(3) 
  20 5WGB          SLL   1  4                   [ADR OF ACC DUMP FOR NEW MEMBER  
   8 5X22          MACCS    ,2   
   7 5XFL          ADX   1  0
  17 5X_=          STO   4  8(1)                [WORD 8 OF NEW MEMBER
   8 5YDW          LDCT  4  #200 
   7 5YYG          SMO      6
  19 5_D6          ERS   4  ASU1(2)             [CLEAR B1 IN SUSPENSION WORD 
   8 5_XQ          LDCT  5  #600 
  18 62CB          ORS   5  2                  [SET UP CHARACTER MODIFIER
  18 62X2          LDCH  1  ALMT(2)             [MODES OF CURRENT MEMBER 
  20 63BL          SMO      6                   [TRANSFER MODES TO ESTABLISHED   
  14 63W=          DCH   1  ARM1(2)             [MEMBER  
   7 64*W          EVENTFIN  
   6 64TG    XAIL1 UNPLUG
   8 65*6          ILLEGAL  AUTO 
   6 65SQ    XAIL2 UNPLUG
   8 66#B          ILLEGAL  AUTO1
   4 66S2    #   
   5 67?L    XRESVIOL
   6 67R=          UNPLUG
   9 68=W          ILLEGAL  RESVIOL  
   4 68QG    #   
   8 69=6    # CHECK VALUE OF X  
   5 69PQ    XCHECK  
  17 6=9B          LDX   3  EVENT5(2)           [COPY OF EXTRACODE   
   7 6=P2          SLC   3  3
   7 6?8L          ANDN  3  7
   7 6?N=          SBN   3  3
   9 6#7W          BPZ   3  XILLXC   
   7 6#MG          EXIT  4  0
  13 6*76    #     THIS DEALS WITH THE PERI EXTRACODE
  18 6*LQ    Q4             +4                  [LENGTH OF CONTROL AREA  
  16 6B6B    XMT            +5                  [MAG TAPE TYPE   
  14 6BL2    XED            +6                  [EDS TYPE
  14 6C5L    XFD            +13                 [FDS TYPE
   5 6CK=    XMCF  +7
  14 6D4W    XTYPE60        +60                 [TYPE 60 
   6 6DJG    XMX   +11   
   6 6F46    XIT   +10   
   6 6FHQ    XGD   +21   
   6 6G3B    XID   +14   
   8 6GH2    XIPB           +24  
   8 6H2L    XIPC           +28  
   6 6HG=    XMOD  #1000 
   6 6HL7 ...XOMODA #200 
   6 6HQ4 ...XOMODE #400 
  21 6H_W    TABA                               [BRANCHES/MONITOR TYPES FOR UNALLOC M
  15 6JFG    #HAL  ZA3,0                        [ MODE #000  
  15 6J_6    #HAL  WN8,BBOPEN                   [      #100  
  15 6KDQ    #HAL  WN8,BBOPEN                   [      #200  
  15 6KYB    #HAL  WN8,BBOPEN                   [      #300  
  15 6LD2    #HAL  WN11,BBOPEN                  [      #400  
  15 6LXL    #HAL  ZA3,0                        [      #500  
  15 6MC=    #HAL  WN10,BBOPEN                  [      #600  
  15 6MWW    #HAL  ZA3,0                        [      #700  
   4 6NBG    #   
  21 6NW6    TABB1                              [BRANCHES/MONITOR TYPES FOR ALLOC MT 
  15 6P*Q    #HAL  WN5,BBCLOSE                  [ MODE #1007 
   5 6PTB    TABB2   
  15 6Q*2    #HAL  WN6,BBCLOSE                  [      #1000 
   5 6QSL    TABB3   
  15 6R#=    #HAL  WN9,BBRENAME                 [      #500  
   4 6RRW    #   
  21 6S?G                                       [BRANCHES/MONITOR TYPES FOR UNALLOC  
  15 6SR6                                       [ ED/FD/MC   
   5 6T=Q    TABC1   
  19 6TQB    #HAL  WM1,BBOPEN                   [MODES #100 #200 #300 #400   
   5 6W=2    TABC2   
  15 6WPL    #HAL  WM3,BBOPEN                   [MODE  #600  
   5 6WWS ...TABC3   
  17 6X42 ...#HAL  WM5,BBOPEN                   [MODES #1600 #1700   
   9 6X9=    WMX            #71777   
   9 6XF4 ...XEDC           #00600000
  12 6XNW    #     THIS SECTION DEALS WITH A PERI
  20 6Y8G    #     PROVIDED THE FIRST FOUR WORDS OF THE CONTROL AREA ARE WITHIN  
  21 6YN6    #     RESERVATIONS IT MAY BE ASSUMED THAT THE PERI IS ON AN UNALLOCATED 
  21 6_7Q    #     OR ONLINE DEVICE SINCE PERIS ON OFFLINE DEVICES ARE NOTIFIED TO   
   6 6_MB    #     AOLPT.
  21 7272    #     THE TYPE & MODE ARE EXAMINED & IF THE PERI SHOULD BE DEALT WITH   
  16 72LL    #     BY GEORGE THE APPROPRIATE ROUTINE IS ENTERED  
  21 736=    #     OTHERWISE THE'ILLEGAL'ROUTINE IS ENTERED WITH NO MESSAGE SPECIFIED
  16 73KW    #     SO THAT IT WILL TRY & FIND OUT WHAT IS WRONG  
  21 745G    #     IT WOULD BE AN IDEA IF THIS SEGMENT SENT THE MESSAGE'UNALLOCATED  
  21 74K6    #     X%A'IF THE DEVICE IS NOT ALLOCATED & IN FACT THIS IS DONE IN THE  
  21 754Q    #     CASE OF EDS-HOWEVER FOR THE TIME BEING THIS WILL BE LEFT TO THE   
   9 75JB    #     ILLEGAL ROUTINE.  
   5 7642    XPERI   
  15 76HL          CHECKB   EVENT2(2),Q4(1),ZA2,,W,,,,K1OPCA 
  18 773=          LDX   5  EVENT4(2)           [1ST WORD OF CONTROL AREA
   8 77GW    #SKI  K6OPCA>99-99  
   9 782G          TRACE 5,PERIAREA  
  14 78G6          LDX   6  EVENT3(2)           [UNIT NO 
   8 79FB          LDCT  4  #377 
  15 79_2          ANDX  4  5                   [TYPE IN X4  
   8 7=3K ... SEGENTRY K90OPCA   
  17 7=68 ...      ANDN  6  #77                 [FOR MANYDA SPECIAL  
  10 7=8R ...      BXE   4  XEDC(1),TYPE6
   8 7=?B ...      ANDN  6  #77  
   5 7=*_ ...TYPE6   
  15 7=DL          STO   4  GEN6                [REMEMBER IT 
  17 7=Y=          ORX   4  6                   [TYPE/UNIT NO IN X4  
   9 7?CW          CALL  7  XFPERENT 
   7 7?XG          SMO   FX2 
   9 7#C6          STO   2  AWORK2   
  16 7#WQ          BNG   2  ZA5                 [J IF UNALLOCATED
   8 7*BB          LDX   2  1(2) 
  17 7*W2          ANDX  2  BITS22LS            [PTR TO DEVICE LIST  
   8 7B*L          SMO      FX2  
   9 7BT=          STO   2  AWORK4   
  17 7C#W          CHECKFLAG 2,ZNFW             [J IF NOT FLAGWAVER  
   8 7CSG          BRN      XRELP
   4 7D#6    ZNFW
  16 7DRQ          CHECKXOT 2,ZA5               [J IF NOT EXOTIC 
  18 7F?B          J151L    2,ZA2               [I-LEGAL IF REL ALLOWED  
   8 7FR2          BRN      XRELP
   4 7G=L    ZA5 
   8 7GQ=          LDX   2  FX2  
  13 7H9W          LDX   7  GEN6                [TYPE
   7 7HPG          SLC   7  9
   9 7J96          TXU   7  XIPC(1)  
  14 7JNQ          BCC      PCI1                [J IF IPC
  12 7K8B    # AT THIS STAGE WE SWITCH ON TYPE   
  18 7KN2    # NOTE THAT X4 IS IN THE SAME FORMAT AS THE RELPERENT REPLY 
  13 7L7L    # X5-TYPE/MODE WORD,X6-UNIT NR,X7-TYPE  
  16 7L#S ...      TXU   7  XMT(1)              [JUMP IF MAG TAPE
   9 7LG2 ...      BCC      XADRIAN  
   6 7LM=          UNPLUG
   9 7M6W          TXU   7  XIPB(1)  
   8 7MLG          BCC      XRELP
  15 7P5B          TXU   7  XED(1)              [JUMP IF EDS 
   8 7PK2          BCC      XANDY
  15 7Q4L          TXU   7  XFD(1)              [JUMP IF FDS 
   8 7QJ=          BCC      XANDY
   9 7R3W          TXU   7  XMCF(1)  
   8 7RHG          BCC      XANDY
  17 7S36          TXU   7  XTYPE60(1)          [TYPE 60-JUMP TO DEAL
  15 7SGQ          BCS      ZA3                 [WITH ILLEGAL
   8 7T2B          ANDN  5  #77  
   8 7TG2          SBN   5  #73  
   9 7T_L          BNG   5  XPROGISH 
   7 7WF=          SBN   5  2
   8 7WYW          BPZ   5  XBEXT
   9 7XDG          ACROSS   MEXTSJ,1 
   5 7XY6    XBEXT   
   9 7YCQ          ACROSS   BEXTSJ,1 
   5 7YXB    XPROGISH
   9 7_C2          ACROSS   PROGISH,1
   9 7_J8 ...PCI1  LDX   3  QMASK3(1)
   7 7_PB ...      ANDX  3  5
   9 7_R5 ...      TXU   3  XOMODA(1)
  17 7_SS ...      BCC      PCI2                [J IF OPEN MODE #200 
   9 7_WJ ...      TXU   3  XOMODE(1)
  19 8222 ...      BCS      PC1                 [JUMP IF NOT OPEN MODE #400  
   4 825D ...PCI2
   9 828Y ...      LDN   1  BBOPEN   
   9 82B=          CALL  3  ZMONITOR 
  10 82TW    PC1   ACROSS   IPCOPENA,1   
   6 83*G    ZA2   UNPLUG
   7 83T6    ZA3   ILLEGAL   
   5 84#Q    XADRIAN 
   9 84SB          LDX   0  AWORK2(2)
   8 85#2          BPZ   0  WN1  
   6 85HS ...      UNPLUG
   7 85RL          LDX   0  5
  21 86?=          ANDN  0  #1077               [MODE ILLEGAL IF NOT #000 #100 #200  
  21 86QW          BNZ   0  ZA3                 [ #300 #400 #500 #600 #700-J IF ILLEG
  21 87=G          LDX   0  5                   [ (#000,#500,#700 ILLEG BUT DETECTED 
  14 87Q6          SRL   0  6                   [ LATER) 
   7 889Q          ANDN  0  7
   7 88PB          SMO      0
  21 8992          LDX   3  TABA(1)             [GET BRANCH/MONTRG TYPE ACCORDING TO 
  20 89NL          BRN      TMON                [ MODE & J TO TEST FOR MONITORING
   4 8=8=    WN1 
   7 8=9S ...      LDX   0  5
  13 8=?B ...      ANDN  0  #1777               [MODE
  17 8=#Y ...[     IF MODE 1 (WRITE) OR 5 (WRITE TAPE MARK) IN DIRECT
  13 8=BG ...[     RESPONSE MODE,DON'T UNPLUG PROGRAM
   7 8=D4 ...      SBN   0  1
   8 8=FL ...      BZE   0  WN12 
   7 8=H8 ...      SBN   0  4
   8 8=JQ ...      BZE   0  WN12 
   6 8=L# ...      UNPLUG
   7 8=MW          LDX   0  5
   9 8?7G          LDX   5  AWORK4(2)
  13 8?M6          ANDN  0  #1777               [MODE
   7 8#6Q          SBN   0  4
   8 8#LB          BZE   0  WN2  
   7 8*62          SBN   0  2
  19 8*KL          BZE   0  WN3                 [J IF MODE 6 -SKIP BACKWARD  
   7 8B5=          SBN   0  1
  17 8BJW          BZE   0  WN4                 [J IF MODE 7 -REWIND 
   8 8C4G          SBN   0  #1000
  16 8CJ6          BZE   0  WN5X                [J IF MODE #1007 
   7 8D3Q          ADN   0  7
  16 8DHB          BZE   0  WN6X                [J IF MODE #1000 
   8 8F32          ADN   0  #300 
  16 8FGL          BZE   0  WN9X                [J IF MODE #500  
  17 8G2=          BRN      ZA3                 [J TO REPORT ILLEGAL 
   9 8GFW    WN2   ACROSS   OLMTB,7  
   9 8G_G    WN3   ACROSS   OLMTB,5  
   9 8HF6    WN4   ACROSS   OLMTB,3  
   9 8HYQ    WN5   ACROSS   OLMTB,11 
   9 8JDB    WN6   ACROSS   OLMTB,9  
   9 8JY2    WN8   ACROSS   OLMTC,1  
   9 8KCL    WN9   ACROSS   OLMTC,2  
   9 8KX=    WN10  ACROSS   OLMTC,3  
   9 8L9M ...WN11  ACROSS   GETONA,1 
   9 8LJ4 ...WN12  LDX   5  AWORK4(2)
   9 8LP= ...      ACROSS   OLMTE,3  
   9 8LWG    WN5X  LDX   3  TABB1(1) 
   8 8MB6          BRN      TMON 
   9 8MTQ    WN6X  LDX   3  TABB2(1) 
   8 8N*B          BRN      TMON 
   9 8NT2    WN9X  LDX   3  TABB3(1) 
   8 8P#L          BRN      TMON 
   5 8PS=    XANDY   
   7 8Q?W          LDX   0  5
  20 8QRG          ANDX  0  WMX(1)              [REMOVE ADDITIVE BITS FROM MODE  
   7 8R?6          LDN   3  4
   8 8RQQ    WM2   SBN   0  #100 
  20 8S=B          BZE   0  WM1X                [J IF MODES #100 #200 #300 #400  
   8 8SQ2          BCT   3  WM2  
   8 8T9L          SBN   0  #200 
  18 8TP=          BZE   0  WM3X                [J IF OPEN SCRATCH(#600) 
   8 8W8W          SBN   0  #400 
  19 8WNG ...      BZE   0  WM1X                [JUMP IF CREATE (MODE #1200) 
  15 8XMQ          SBN   0  #400                [MODE #1600  
   8 8Y7B ...      BZE   0  WM5X 
   8 8YM2          SBN   0  #100 
  15 8Y_C ...      BZE   0  WM5X                [MODE #1700  
  20 8_?S ...                                   [OTHERWISE UNALLOCATED CHANNEL   
   9 8_L=          BRN      UNALLOC  
   4 925W    #   
   9 92KG    WM1   ACROSS   SOUP,2   
   9 9356    WM3   ACROSS   SCRATCH,1
   9 93JQ    WM5   ACROSS   SQOPEN,1 
   9 944B    WM1X  LDX   3  TABC1(1) 
   8 94J2          BRN      TMON 
   9 953L    WM3X  LDX   3  TABC2(1) 
   8 95H=          BRN      TMON 
   9 95ND ...WM5X  LDX   3  TABC3(1) 
   8 95TL ...      BRN      TMON 
  15 962W    XRELP ANDN  5  #1777               [ISOLATE MODE
  18 96GG          TXU   5  XMOD(1)             [#1000 IS ONLY VALID MODE
   8 9726          BCS      ZA2  
   8 97FQ          BRN      ZREL2
   4 97_B    #   
  19 98F2    TMON           [ENTRY WITH B0-11 OF X3 ADDR TO J TO IF NO MNTRG 
  16 98YL                   [           B12-23   X3 MNTRG TYPE   
  17 99D=                   [ X4=TYPE/UNIT NO OF PERIPHERAL X2=FX2   
   7 99XW          LDX   1  3
  16 9=CG          ANDN  1  #7777               [MONITORING TYPE 
  16 9=X6          SRL   3  12                  [RETURN ADDRESS  
  16 9?BQ          BZE   1  TMON1               [J IF NO CHECKING
  20 9?WB          BRN      ZMONITOS            [ELSE J TO TEST IF MNTRG REQUD   
   8 9#B2    TMON1 ADX   3  FX1  
   7 9#TL          EXIT  3  0
   4 9**=    #   
  20 9*SW    #     THIS SECTION DEALS WITH THE EXTRACODES 'REL','DIS' AND 'ALLOT'
  16 9B#G    #     ON ENTRY:ANM    HOLDS MEMBER NUMBER (B22-3)   
  16 9BS6    #              EVENT5 HOLDS COPY OF THE EXTRACODE   
  15 9C?Q    #              EVENT2 HOLDS N(M) OF INSTRUCTION 
  12 9CRB    #     THIS SECTION DEALS WITH REL.  
  20 9D?2    # THE EXTRACODE IS ILLEGAL IF THE PERIPH IS NOT OF AN ALLOWED TYPE  
  13 9DQL    # THESE ARE TR,TP,LP,CR,CP,FR,FW,TYPE 60
  17 9F==    # IT IS ALSO ILLEGAL IF IT IS NOT        ALLOCATED TO   
  19 9FPW    # OTHERWISE THIS SEGMENT USES'RELPERENT'TO RELEASE THE PERIPH   
   5 9G9G    XUNALLOC
   6 9GP6          UNPLUG
   5 9H8Q    UNALLOC 
  10 9HNB          OUTPACK  4,1,PERINAME 
   9 9J82          ILLEGAL  UNALLOC  
   4 9JML    XREL
  17 9K7=          CALL  7  XFTU                [FIND TYPE & UNIT NO.
   9 9KLW          CALL  7  XFPERENT 
   9 9L6G          BNG   2  XUNALLOC 
   8 9LL6          LDX   3  1(2) 
   8 9M5Q          BNG   3  ZREL0
   9 9MKB          ANDX  3  BITS22LS 
  16 9N52          J151L    3,ZREL1             [J IF REL ALLOWED
   5 9NJL    ZREL0   
   7 9P4=          LDX   6  4
  15 9PHW          SRL   6  15                  [TYPE IN X6  
   7 9Q3G          SBN   6  5
   8 9QH6          BNG   6  ZREL1
  15 9R2Q          SBN   6  45                  [JUMP IF FR  
   8 9RGB          BZE   6  ZREL1
  15 9S22          SBN   6  1                   [     OR FW  
   8 9S?B ...      BZE   6  ZREL1
  14 9SJQ ...      SBN   6  1                  [ OR FH   
   8 9SW6 ...      BZE   6  ZREL1
  18 9T7G ...      SBN   6  8                   [OR UNLESS CI (TYPE 60)  
  19 9TDW          BNZ   6  XILLINS1            [JUMP IF NOT OF SUITABLE TYPE
   5 9TYG    ZREL1   
   8 9WD6    #SKI  K6OPCA>999-999
  21 9WXQ          TRACE    0(2),RELXCOD        [PRB  ENTRY FOR DEVICE TO BE'REL'ED  
   8 9XCB    ZREL2 LDN   1  BBREL
   9 9XX2          CALL  3  ZMONITOR 
   6 9YBL          UNPLUG
  18 9YW=          CAPCA                        [GET TO SUITABLE STATE TO
   8 9_*W          RELPERENT 4   
  17 9_TG          RUNPROG                      [MAKE PROG RUNNABLE  
  12 =2*6    #     THIS SECTION DEALS WITH DIS.  
  20 =2SQ    # THE EXTRACODE IS ILLEGAL IF THE PERIPH IS NOT OF AN ALLOWED TYPE  
  12 =3#B    # THESE ARE TR,TP,LP,CR,CP,FR,FW.   
  17 =3S2    # IT IS ALSO ILLEGAL IF IT IS NOT ALLOCATED TO THE P_OG.
  20 =4?L    # IT CAUSES AN ONLINE DEVICE TO BE DISENGAGED AND AN OFFLINE OUTPUT 
  14 =4R=    # DEVICE TO BE STEPPED ON TO THE NEXT STREAM
   4 =5=W    XDIS
  17 =5QG          CALL  7  XFTU                [LOOK UP TYPE & UNIT 
   9 =6=6          CALL  7  XFPERENT 
   9 =6PQ          BNG   2  XUNALLOC 
   8 =79B          LDX   3  1(2) 
  15 =7P2          BNG   3  ZDIS0               [J IF OFFLINE
   8 =88L          J152L 3,ZDIS1 
   5 =8N=    ZDIS0   
   7 =97W          LDX   0  4
   8 =9MG          SRL   0  15   
  17 ==76          SBN   0  5                   [JUMP IF SLOW PERIPH 
   8 ==LQ          BNG   0  ZDIS1
  13 =?6B          SBN   0  45                  [ FR 
   8 =?L2          BZE   0  ZDIS1
  13 =?TS ...      SBN   0  1                   [ FW 
   8 =#5L ...      BZE   0  ZDIS1
  13 =#*D ...      SBN   0  1                   [ FH 
   9 =#K=          BNZ   0  XILLINS1 
   5 =*4W    ZDIS1   
   8 =*JG    #SKI  K6OPCA>999-999
  21 =B46          TRACE    0(2),DISXCOD        [PRB ENTRY FOR DEVICE TO BE 'DIS'ED  
  16 =BHQ          LDX   4  0(2)                [PICK UP ENTRY   
   8 =C3B          LDN   1  BBDIS
   7 =CH2          LDX   6  3
   9 =D2L          CALL  3  ZMONITOR 
  15 =DG=          BNG   6  ZDIS3               [J IF OFFLINE
   9 =D_W    NBUSY JBUSY    6,XBUSY  
   8 =FFG          DISENGPER 6   
   7 =F_6    ZDIS3 EVENTFIN  
   8 =GDQ    XBUSY COOR3    #41  
   8 =GYB          BRN      NBUSY
  12 =HD2    #     THIS SECTION DEALS WITH ALLOT 
  21 =HXL    #  'ALLOT'IS NEVER ILLEGAL INSTEAD A REPLY IS GIVEN IN WORD 9 AS FOLLOWS
  10 =JC=    #  B0=1-PERIPH NOT ALLOCATED
  18 =JWW    #  B9=1-PROPS IMPLEMENTED & B10-17 GIVE PROPS  B9=0 NO PROPS
  14 =KBG    #  B18-23-GEOGRAPHIC NUMBER-ZERO IF OFFLINE.
   5 =KW6    XALLOT  
  16 =L*Q          CALL  7  XFTU                [FIND TYPE & UNIT
  17 =LTB          CALL  7  XFPERENT            [LOOK UP ENTRY IN PRB
  19 =M*2          LDCT  5  #400                [SET -VE REPLY IF UNALLOCATED
   9 =MSL          BNG   2  ZALL3X   
   8 =N#=          LDX   0  1(2) 
  15 =NRW          BPZ   0  NOFL1               [J IF ONLINE 
   8 =P?G          CHECKSTATE 2,0
   8 =PR6          BPZ   0  ZALL3
   8 =Q=Q          SMO      FX2  
   9 =QQB          LDX   7  EVENT2   
   8 =R=2          SLC   7  11   
  17 =RPL          ANDN  7  3                   [ISOLATE B9,10 OF DN 
  21 =S9=          BZE   7  NOFL                [J IF  DRM   NOT TO BE SET OR UNSET  
  19 =SNW          BCT   7  SETDR               [J IF DRM TO BE SET  (B9 SET)
  17 =T8G          FDRMOFF  2                   [UNSET DRM  (B10 SET)
   8 =TN6          BRN      NOFL 
  14 =W7Q    SETDR FDRMON   2                   [SET DRM 
   4 =WMB    NOFL
  16 =X72          STOZ     5                   [SET ZERO REPLY  
  15 =XLL          LDX   0  0(2)                [OFFLINE-TYPE
   8 =Y6=          SRL   0  15   
   7 =YKW          SBN   0  5
  16 =_5G          BZE   0  ZALLMT              [J IF MAG. TAPE  
   7 =_K6          SBN   0  4
  15 ?24Q          BZE   0  ZALLDR              [J IF DRUM   
   8 ?2JB          SBN   0  16   
  17 ?342          BZE   0  ZALLHD              [J IF HIGH SPEED DRUM
   7 ?3HL          SBN   0  3
  20 ?43=          BNZ   0  ZALL3               [ZERO REPLY IF NOT MT,HSD,DR,IPC 
  15 ?4GW          LDX   3  1(2)                [PTR TO OLPA 
  16 ?52G          LDX   5  IPPROPS-CHAINADD(3) [GET PROPERTIES  
   8 ?5G6          BRN      ZALL3
   4 ?5_Q    #   
   8 ?6FB    #     MAGNETIC TAPE 
   5 ?6_2    ZALLMT  
   8 ?7DL          SMO      1(2) 
  11 ?7Y=          LDX   0  ATMARK-CHAINADD  
   7 ?8CW          SBN   0  2
   9 ?8XG          BNZ   0  ZALLMT1  
   8 ?9C6          LDX   3  1(2) 
   9 ?9WQ          SBN   3  CHAINADD 
   8 ?=BB          MTPTR    3,3  
   8 ?=W2          BRN      NOFL2
   5 ??*L    ZALLMT1 
   8 ??T=          LDN   5  #3000
  18 ?##W          BRN      ZONL1               [BS 9 13 14 IF MT OFFLINE
   8 ?#SG    #     DRUM TYPE 9   
   5 ?*#6    ZALLDR  
   8 ?*RQ          LDX   3  1(2) 
  18 ?B?B          ANDX  3  BITS22LS            [PTR TO CHAINADD OF OLPA.
  17 ?BR2          LDX   3  CPPTR-CHAINADD(3)   [PTR TO DEVICE LIST  
  15 ?C=L          BZE   3  ZALL3               [J IF OFFLINE
   8 ?CQ=          BRN      ZONL 
   7 ?D9W    #     HSD TYPE  
   5 ?DPG    ZALLHD  
  17 ?F96          LDN   5  #400                [HSD MARKER IN REPLY 
   8 ?FNQ          LDX   3  1(2) 
  18 ?G8B          ANDX  3  BITS22LS            [PTR TO CHAINADD OF OLPA 
  17 ?GN2          LDX   3  CPPTR-CHAINADD(3)   [PTR TO DEVICE LIST  
   8 ?H7L          BRN      ZGEOG
   4 ?HM=    ZONL
  16 ?J6W          LPROP    3,5                 [GET PROPERTIES  
  16 ?JLG          SLL  5  6                    [PROPS.INB9-18   
  15 ?K66    ZGEOG LGEOG    3,4                 [GET GEOG.NO.
  16 ?KKQ          ANDN  4  #77                 [USE M.S. 6 BITS 
  17 ?L5B          ORX   5  4                   [ACCUMULATE REPLIES  
   7 ?LK2    ZONL1 LDCT  0  1
   7 ?M4L          SRL   0  1
  20 ?MJ=          ORX   5  0                   [SET B9=1 AS PROPS. IMPLEMENTED  
   5 ?N3W    ZALL3   
  17 ?NHG          MACCS ,FX2,3                 [PTR TO MEMBERS X0   
   8 ?P36          LDCT  0  #377 
   8 ?PGQ          ANDX  0  9(3) 
   7 ?Q2B          ORX   5  0
  15 ?QG2          STO   5  9(3)                [SET REPLY   
   8 ?Q_L    #SKI  K6OPCA>999-999
  17 ?RF=          TRACE 5,ALOTXCOD             [TRACE REPLY TO ALLOT
   7 ?RYW          EVENTFIN  
   5 ?SDG    ZALL3X  
   8 ?SY6          LDX   2  FX2  
   9 ?TCQ          LDX   0  EVENT2(2)
   8 ?TXB          ANDN  0  512  
  21 ?WC2          BNZ   0  ZALL3               [J IF ALLOT WAS NOT REQUESTING PERIPH
   9 ?WWL          LDN   1  BBALLOT  
   9 ?XB=          CALL  3  ZMONITOR 
   8 ?XTW          BRN      ZALL3
   5 ?Y*G    NOFL1   
   8 ?YT6          LDX   3  1(2) 
   8 ?_#Q    NOFL2 SMO      FX2  
   9 ?_SB          LDX   7  EVENT2   
   8 #2#2          SLC   7  11   
   8 #2RL          ANDN   7  3   
  16 #3?=          BZE   7  ZONL                [J IF NO REQUEST 
  17 #3QW          BCT   7  SETDRON             [J IF DRM TO BE SET  
  15 #4=G          CLDRM    3                   [CLEAR DRM   
   8 #4Q6          BRN      ZONL 
   5 #59Q    SETDRON 
   9 #5PB          JDRMABLE 3,SETABLE
   8 #692          BRN      ZONL 
   5 #6NL    SETABLE 
   7 #78=          SETDRM   3
   8 #7MW          BRN      ZONL 
   4 #87G    #   
  14 #8M6    #                              SUBROUTINES  
   4 #96Q    #   
  21 #9LB    # THIS SUBROUTINE CALCULATES THE TYPE & UNIT NO FOR A 'REL'TYPE XCODE   
   7 #=62    # FROM EVENT2&3 
   4 #=KL    #   
   8 #?5=    # X7 HOLDS THE LINK 
   4 #?JW    #   
  19 ##4G    # ON EXIT X4 HOLDS TYPE &UNIT NO.IN B0-8 & B9-23 RESPECTIVELY   
   4 ##J6    XFTU
   8 #*3Q    #SKI  K6OPCA>99-99  
  12 #*HB          TRACE    EVENT3(2),OPCEVEN3   
  14 #B32          LDX   5  EVENT3(2)           [UNIT NO 
   8 #C2=          LDN   4  #77  
  15 #CFW          ANDX  4  EVENT2(2)           [TYPE IN X4  
   8 #CJF ... SEGENTRY K91OPCA   
   8 #CM4 ...      ANDN  5  #77  
  17 #CPM ...      BXE   4  XED(1),TYPE6A       [FOR MANYDA SPECIAL  
   8 #CS= ...      ANDN  5  #77  
   5 #CWT ...TYPE6A  
   8 #C_G          LDN   0  13   
   7 #DF6          TXU   4  0
   8 #DYQ          BCS      XFTU1
  18 #FDB          LDN   4  6                   [REGARD TYPE 13 AS TYPE 6
   5 #FY2    XFTU1   
  16 #GCL          SRC   4  9                   [TYPE IN BITS 0-9
   7 #GX=          ORX   4  5
   8 #HBW    #SKI  K6OPCA>999-999
  10 #HWG          TRACE    4,TYPEUNIT   
   7 #JB6          EXIT  7  0
  19 #JTQ    # THIS S/R SEARCHES THE PRB FOR A PERIPHERAL OF THE TYPE/UNIT NO
  11 #K*B    # SPECIFIED IN B0-8/B9-23 OF X4 
   4 #KT2    #   
  16 #L#L    # ON ENTRY THE PROGRAM MUST BE PLUGGED IN &X2=FX2   
   4 #LS=    #   
   8 #M?W    # X7 HOLDS THE LINK 
   4 #MRG    #   
  18 #N?6    # ON EXIT 1)X2->TO THE ENTRY IF FOUND.OTHERWISE X2 IS -VE   
  20 #NQQ    #         2)X3 HOLDS A COUNT OF OFFLINE ENTRIES UP TO AND INCLUDING 
  10 #P=B    #           THE ONE FOUND   
   5 #PQ2    XFPERENT
   9 #Q9L          LDX   2  APRB(2)  
   9 #QP=          FPERENT  4,2,2,3  
   7 #R8W          EXIT  7  0
   4 #RNG    #   
  21 #S86    # THIS SUBROUTINE CHECKS IF 'OPEN PERI' TYPE MONITORING IS REQUIRED,AND,
  16 #SMQ    # IF IT IS,GENERATES THE APPROPRIATE MONITOR EVENT. 
  18 #T7B    # ON ENTRY X4=TYPE/UNIT NO.OF PERIPHERAL,X1=MONITORING TYPE 
  19 #TM2    # PROGRAM MUST BE PLUGGED IN FOR ZMONITOR,UNPLUGGED FOR ZMONITOS
  20 #W6L    # LINK IN X3 - ABSOLUTE IF ZMONITOR,REL TO START OF SEG IF ZMONITOS 
  15 #WL=    # ON EXIT:IF NO MNTRG,X0 DESTROYED,X1=FX1,X2=FX2
  15 #X5W    #        :IF MNTRG,NO EXIT TO CALLING ROUTINE   
   5 #XKG    ZMONITOR
   8 #Y56          SBX   3  FX1  
   8 #YJQ          ORX   1  GSIGN
   5 #_4B    ZMONITOS
   8 #_J2          SMO      FX2  
   9 *23L          STO   1  AWORK1   
   9 *2H=          ANDX  1  BITS22LS 
   6 *2JH ...#UNS G400   
   4 *2KS ...(   
  11 *2M5 ...      HUNT     2,IUSER,G400BLK  
  14 *2NB ...      BNG   2  XDSK2          [NOT EXIT MR  
   8 *2PM ...      LDX   0  A1(2)
   7 *2QY ...      SBN   0  5
   8 *2S9 ...      BNZ   0  XDSK2
   8 *2TG ...      FREECORE   2  
  14 *2WR ...      BRN      XDSK1         [NORMAL PATH   
   8 *2Y4 ...XDSK2 LDX   2  FX2  
   4 *2_* ...)   
  10 *32W          TESTMON  ,1,4,ZMON1   
   6 *33H ...#UNS G400   
   4 *348 ...(   
   7 *34T ...      SMO    FX2
   9 *35G ...      LDX   1  AWORK1   
   9 *367 ...      ANDX  1  BITS22LS 
   9 *36S ...      SBN   1  BBOPEN   
   8 *37F ...      BNZ   1  XDSK1
   8 *386 ...      LDX   2  FX2  
   7 *38R ...      LDX   0  4
  14 *39D ...      SRL   0  15              [TYPE - X0   
   7 *3=5 ...      SBN   0  5
   9 *3=Q ...      SEGENTRY G400HKMT 
   8 *3?C ...      BRN      XDSK3
  13 *3#4 ...      BZE   0  XDSK4         [DO HOOK   
   5 *3#P ...XDSK3   
   7 *3*B ...      SBN   0  1
   9 *3B3 ...      SEGENTRY G400HKDA 
   8 *3BN ...      BRN      XDSK1
  13 *3C* ...      BNZ   0  XDSK1          [CONTINUE 
   9 *3D2 ...XDSK4 ACROSS   DOMONOP,1
   5 *3DM ...XDSK1   
   4 *3F# ...)   
   8 *3GG          ADX   3  FX1  
   7 *426          EXIT  3  0
   7 *4FQ    ZMON1 LDX   6  4
   9 *4_B          LDX   7  EVENT2(2)
   9 *5F2          LDXC  5  AWORK1(2)
   8 *5YL          BCC      ZMON2
   6 *6D=          UNPLUG
   6 *6XW    ZMON2 REPMON
   4 *7CG    #   
   4 *7X6    #   
   4 *8BQ    #   
  10 *8WB          MENDAREA 25,K100OPCA  
   4 *9B2    #END
   8 ____ ...11231367000400000000
