  16 22FL    #LIS  K0EXPRESS>K0HLS>K0COMMAND>K0GREATGEO>K0ALLGEO 
  16 22_=    #SEG  EXPRESS60                    [RICHARD GRIMWADE
   9 23DW                   8HEXPRESS
   4 23YG    #   
  20 24D6    #   THIS COMMAND  INFORMS THE HLS SUBJECT PROGRAM THAT A JOB MUST BE
   8 24XQ    #   RUN IMMEDIATELY 
  19 25CB    #   TO  DO THIS IT PUTS AN ENTRY FOR THE JOB ON THE TEMP HLSQ   
   4 25X2    #   
  10 26BL          SEGENTRY K1EXPRESS,XK1
   4 26W=    #   
   8 27*W    STRING         +1   
   8 27TG                   4H:  
   8 28*6    TWO            +2   
   4 28SQ    XK1 
   9 28W_ ...      FSHCODE  B,XNOTB  
   4 28_8 ...(   
  21 293C ...      COMERR   INCDUMP             [THIS COMMAND CANNOT BE USED ON 'B'  
   4 295L ...#   
   5 297T ...XNOTB   
   4 29=4 ...)   
  20 29#B          BSOFF    HLSBIT,ON           [DROP THRU IF HLS SWITCHED OFF   
   8 29S2          LDX   6  Y8(1)
   8 2=?L          BRN      RERR 
   4 2=R=    ON  
  17 2?=W          SPARABEG 1,STRING(1)         [GET USERNAME PARAM  
  19 2?QG          CHEKLFN2 XJONO,NLFN          [J TO XJONO IF NO USERNAME   
  20 2#=6                                       [AND TO NLFN  IF NOT L.F.N.FORMAT
  16 2#PQ          LDX   4  JPARNUM(3)          [GET PARAM NO.   
   7 2*9B          LDN   5  3
   7 2*P2          TXL   4  5
  19 2B8L          BCC      NUNA                [J IF NOT 1ST OR 2ND PARAM   
   7 2BN=          LDN   5  1
   9 2C7W          TXU   4  TWO(1)   
  19 2CMG          BCC      X1                  [J IF USERNAME IS 2ND PARAM  
   7 2D76          LDN   5  2
  18 2DLQ    X1    SPARANOX 5                   [GET JOBNAME PARAMETER   
  19 2F6B          CHEKLFN2 NJBNA,NLFN          [J TO NJBNA IF NO JOBNAME AND
  19 2FL2                                       [  TO NLFN IF FORMAT ERROR   
  15 2G5L          LDX   5  ACES                [LOAD SPACES 
  10 2GK=          STO   5  ACOMMUNE1(2) 
  10 2H4W          LDN   5  ACOMMUNE1(2) 
  17 2HJG          LDN   6  ACOMMUNE2(2)        [ SPACEFILL  ACOMMUNE
  14 2J46          MOVE  5  5                   [ AREA   
  17 2JHQ          LDEX  6  ANUM(3)             [NO. CHARS IN JOBNAME
  16 2K3B          LDN   4  APARA(3)            [PTR TO JOBNAME  
  10 2KH2          LDN   5  ACOMMUNE1(2) 
   7 2L2L          SMO      6
  16 2LG=          MVCH  4  0                   [MOVE IN JOBNAME 
  19 2L_W          STO   6  AWORK1(2)           [SAVE COUNT OF JOBNAME CHARS 
  20 2MFG          HUNT2    3,CPB,CUNI          [FIND /CUNI CONTAINING USERNAME  
   9 2M_6          LDEX  6  ANUM(3)  
   9 2NDQ          LDN   4  APARA(3) 
  10 2NYB          LDN   5  ACOMMUNE4(2) 
   7 2PD2          SMO      6
  20 2PXL          MVCH  4  0                   [MOVE USERNAME INTO ACOM. AREA   
   9 2QC=          STO   6  AWORK2(2)
   8 2QWW          CALL  6  SRNAM
   7 2RBG          PARAPASS  
   7 2RW6          PARAPASS  
   7 2S*Q          UNIFREE   
   4 2STB    #   
  16 2T*2    #                      RETURNS WITH JOBNUMBER IN X4 
   4 2TSL    #   
   8 2W#=    #SKI  K6ALLGEO>99-99
  10 2WRW          TRACE    4,*NUMBER*   
   7 2X?G          OUTBLOCN 8
  12 2XR6          OUTPARAM AWORK1(2),ACOMMUNE1  
  12 2Y=Q          OUTPARAM AWORK2(2),ACOMMUNE4  
  18 2YQB          LDX   0  Z2(1)               [LOAD MESSAGE IDENTIFIER 
   4 2_=2    X2  
  16 2_PL          MONOUTX  0                   [OUTPUT MESSAGE  
   8 329=          LDN   7  #40  
  17 32NW          SPARAPAS                     [GET TEXT PARAMETER  
   7 32QD ...#SKI  JWPHASE4  
   4 32S2 ...(   
  10 32TJ ...      MHUNT    3,CPB,CUNI   
  11 32X6 ...      NAME     3,ADATA,CSTORE   
   7 32YN ...      LDX   6  4
  10 332= ...      ACROSS   JWELLONE,7   
   4 333S ...)   
   8 335B ...#SKI  JWPHASE4<1$1  
   4 336Y ...(   
  20 338G          TEMPQ    7,4                 [ENTRX TO HLSQ,MUST DF AFTER COOR
  10 33N6          HUNT     3,CPB,CUNI   
   8 347Q          LDN   1  24   
   9 34MB          NGX   0  ANUM(3)  
  17 3572          BPZ   0  THEND               [J IF MISSING OR MVLL
   8 35LL          ADN   0  24   
   8 366=          BNG   0  MOVE 
   9 36KW          LDEX  1  ANUM(3)  
  18 375G    MOVE  LDX   2  BHTEMP+1            [X1 = MIN OF 24 AND ANUM 
  19 37K6                                       [X2 -> LAST BLOCK IN TEMPQ   
   8 384Q          ADN   3  APARA
  10 38JB          LDN   4  HLSJNAM(2)   
  17 3942          MVCH  3  0(1)                [MOVE IN CHAR STRING 
   4 39?S ...)   
   5 39HL    THEND   
   6 3=3=          ENDCOM
   4 3=GW    #   
  21 3?2G    #  THIS PATH IS ENTERED WHEN THERE IS NO USERNAME PARAM. , AND THEREFORE
  12 3?G6    #  A  JOBNUMBER PARAMETER IS ASSUMED
   4 3?_Q    #   
  16 3#FB    XJONO FREECORE 3                   [FREE CPB/CUNI   
  16 3#_2          PARAPASS                     [GET JOBNO. PARAM
  10 3*DL          MHUNT    3,CPB,CUNI   
   9 3*Y=          NGX   4  ANUM(3)  
  17 3BCW          BPZ   4  NJBNO               [JOBNO. PARAM MISSING
  18 3BXG          CHNUMCOD 1,3                 [CONVERT NO. TO BINARY   
  18 3CC6          TESTREP  CHNUMERR,NUMERR     [TO REPORT NUMBER ERROR  
  10 3CWQ          LDX   4  ACOMMUNE1(2) 
   8 3DBB    #SKI  K6ALLGEO>99-99
  10 3DW2          TRACE    4,*JOBNO**   
  19 3F*L          BZE   4  NOTVAL              [  JOBNUMBER MUST BE NON-ZERO
  15 3FT=          BNG   4  NOTVAL              [  AND +VE.  
  19 3G#W          CALL  6  SRNUM               [  S/R TO CHECK IF JOB EXISTS
   8 3GSG          OUTNUM   4,0  
  18 3H#6          LDX   0  Z1(1)               [LOAD MESSAGE IDENTIFIER 
   8 3HRQ          BRN      X2   
   4 3J?B    #   
  19 3JR2    #  THE  FOLLOWING S/R CHECKS FOR THE EXISTENCE OF JOB , GIVEN   
   7 3K=L    #  THE JOBNUMBER
   4 3KQ=    #   
  17 3L9W    SRNUM JOBLOCK  4,3                 [LOOK FOR JOBBLOCK   
  16 3LPG          BNG   3  SRNUM1              [J IF NO JOBBLOCK
  17 3M96          SPRIVJUMP  JSISSUE,3,SYS     [J IF SYSTEM ISSUED  
   5 3MNQ    SRNUM2  
   7 3N8B          EXIT  6  0
   5 3NN2    SRNUM1  
  19 3P7L          SHLSQ    4,3,NONX            [SEARCH HLSQ FOR JOB ENTRY   
  15 3PM=          BRN      SRNUM2              [J IF FOUND  
   4 3Q6W    #   
  19 3QLG    #     THE FOLLOWING  S/R CHECKS FOR THE EXISTENCE OF JOB, GIVEN 
  11 3R66    #     THE JOBNAME AND USERNAME  
   4 3RKQ    #   
   8 3S5B    SRNAM LDN   3  BJOBQ
   5 3SK2    SRNAM2  
   9 3T4L          LDX   3  FPTR(3)  
   8 3TJ=          TXU   3  CXJO 
  17 3W3W          BCC      SRNAM1              [J IF END OF Q FOUND 
   7 3WHG          LDN   0  3
   5 3X36    SRNAM3  
   7 3XGQ          SMO      0
  10 3Y2B          LDX   4  JNAME-1(3)   
   7 3YG2          SMO      0
  11 3Y_L          TXU   4  ACOMMUNE1-1(2)   
  16 3_F=          BCS      SRNAM2              [J IF NO MATCH   
   9 3_YW          BCT   0  SRNAM3   
   7 42DG          LDN   0  3
   5 42Y6    SRNAM4  
   7 43CQ          SMO      0
  10 43XB          LDX   4  JUSER-1(3)   
   7 44C2          SMO      0
  11 44WL          TXU   4  ACOMMUNE4-1(2)   
  16 45B=          BCS      SRNAM2              [J IF NO MATCH   
   9 45TW          BCT   0  SRNAM4   
  17 46*G          SPRIVJUMP  JSISSUE,3,SYS     [J IF SYSTEM ISSUED  
  19 46T6          LDX   4  JOBNUM(3)           [BLOCK FOUND - LOAD JOBNUMBER
   8 47#Q          EXIT  6   0   
   4 47SB    #   
  15 48#2    #   THE FOLLOWING IS THE SEARCH DOWN THE HLSQ   
   4 48RL    #   
   5 49?=    SRNAM1  
   8 49QW          LDN   3  BHLSQ
   5 4==G    SRNAM5  
   9 4=Q6          LDX   3  FPTR(3)  
   8 4?9Q          TXU   3  CXHL 
  18 4?PB          BCC      NONX                [J IF JOB NON EXISTENT   
   7 4#92          LDN   0  3
   5 4#NL    SRNAM6  
   7 4*8=          SMO      0
  10 4*MW          LDX   4  HLSJNAM-1(3) 
   7 4B7G          SMO      0
  11 4BM6          TXU   4  ACOMMUNE1-1(2)   
  16 4C6Q          BCS      SRNAM5              [J IF NO MATCH   
   9 4CLB          BCT   0  SRNAM6   
   7 4D62          LDN   0  3
   5 4DKL    SRNAM7  
   7 4F5=          SMO      0
  10 4FJW          LDX   4  HLSUNAM-1(3) 
   7 4G4G          SMO      0
  11 4GJ6          TXU   4  ACOMMUNE4-1(2)   
  16 4H3Q          BCS      SRNAM5              [J IF NO MATCH   
   9 4HHB          BCT   0  SRNAM7   
  16 4J32          LDX   4  HLSJONO(3)          [GET JOBNUMBER   
   7 4JGL          EXIT  6  0
   8 4K2=    SYS   MONOUT   EXSYS
   8 4KFW          BRN      THEND
   4 4K_G    #   
  16 4LF6    #     THE FOLLOWING IS THE ERROR REPORTING ROUTINE  
   4 4LYQ    #   
  16 4MDB    NONX  LDX   6  Y1(1)               [JOB NON EXISTENT
   8 4MY2          BRN      RERR 
  16 4NCL    NLFN  LDX   6  Y2(1)               [NOT LFN FORMAT  
   8 4NX=          BRN      RERR 
  18 4PBW    NUNA  LDX   7  Y3(1)               [USERNAME PARAM MISSING  
   8 4PWG          BRN      RER1 
   5 4QB6    NJBNA   
  18 4QTQ          LDX   7  Y4(1)               [JOBNAME PARAM MISSING   
   8 4R*B    RER1  LDX   6  Y5(1)
   8 4RT2          BRN      RER2 
  15 4S#L    NJBNO FREECORE 3                   [FREE /CUNI  
   8 4SS=          BNZ   4  X3   
  19 4T?W    #     IF FIRST PARAMETER NULL SEE IF A SECOND PARAMETER EXISTS  
   7 4TRG          PARANOT  2
  10 4W?6          MHUNT    3,CPB,CUNI   
   9 4WQQ          NGX   6  ANUM(3)  
   7 4X=B          FREECORE 3
   8 4XQ2          BPZ   6  X3   
   8 4Y9L          LDX   0  Z3(1)
   8 4YP=          BRN      X2   
   4 4_8W    X3  
   8 4_NG          LDX   6  Y5(1)
   8 5286          BRN      RERR 
   5 52MQ    NUMERR  
  16 537B          LDX   6  Y6(1)               [NO. FORMAT ERROR
   8 53M2          BRN      RERR 
   5 546L    NOTVAL  
   8 54L=          LDX   6  Y7(1)
   7 555W    RERR  NGN   7  1
   4 55KG    RER2
  15 5656          COMERRX  6,7                 [REPORT ERROR
   7 56JQ    Y1    +JXEXIST  
   7 574B    Y2    +JNLFNF   
   6 57J2    Y3    +JUSNA
   7 583L    Y4    +JJOBNA   
   7 58H=    Y5    +JPARMIS  
   7 592W    Y6    +CHNUMERR 
   7 59GG    Y7    +FWHSTAT  
   7 5=26    Y8    +EPHLSOFF 
   4 5=FQ    #   
  14 5=_B    #     THE FOLLOWING ARE MESSAGE IDENTIFIERS 
   4 5?F2    #   
   7 5?YL    Z1    +EXJOBNO  
   7 5#D=    Z2    +EXJOBNA  
   7 5#XW    Z3    +EXMESS   
   4 5*CG    #   
  10 5*X6          MENDAREA 50,K99EXPRESS
   4 5BBQ    #END
   8 ____ ...55021474000100000000
