  12 22FL ...#LIS  K0GETONA>K0LIBRARY>K0ALLGEO   
  15 22_=    #SEG           GETONA65             [N.R.BOULT  
   4 2357 ...[   
  15 2394 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23#_ ...[   
   9 23DW                   8HGETONA 
  15 23YG    #     THIS SEGMENT CONTROLS IMPLEMENTATION OF   
  12 24D6    #      UNANTICIPATED OPEN MODE PERI 
   8 24XQ    [       MODE #400   
  12 25CB          SEGENTRY K1GETONA,QK1GETONA   
   6 25X2    QK1GETONA   
  15 26BL    [ENTRY POINT FOR MODE 400 UNANTICIPATED MT PERI 
   7 26W= ...#SKIP K6GETONA  
   9 27*W ...      TRACE    2,GETONA2
  10 27TG          ANDX  4  REMPROP(1)   
  18 28*6    #     SET UP FILE/FABSNB BLOCK AND FILL FROM OBJECT PROGRAM 
  11 28SQ          SETNCORE 11,3,FILE,FABSNB 
   7 29#B          LDN   6  9
  14 29S2          CHECKEXT EVENT2(2),6,NOCA,,W,Q1,HLOCK1
   4 2=?L    Q2  
   9 2=R=          LDN   7  A1+2(3)  
  11 2?=W          OUTMOVE  2,EVENT2(2),7,6  
  17 2?QG          LDX   7  EVENT2(2)           [CONTROL AREA ADDRESS
   9 2#=6          STO   4  AWORK1(2)
   9 2#PQ          STO   7  AWORK3(2)
  19 2*9B          MHUNT    3,FILE,FABSNB       [TEST VALIDITY OF NEW NAME   
   9 2*P2          LDCH  0  A1+4(3)  
   8 2B8L          SBN   0  #41  
  15 2BN=          BNG   0  QILP                [ERROR < A   
   8 2C7W          SBN   0  #32  
  15 2CMG          BPZ   0  QILP                [ERROR > Z   
  15 2D76          LDN   1  11                  [COUNT OF 11 
   7 2DLQ    QVT1  BCHX  3  /
   9 2F6B          LDCH  0  A1+4(3)  
   8 2FL2          SBN   0  #41  
  14 2G5L          BPZ   0  QVT2                [J >= A  
   8 2GK=          ADN   0  23   
  15 2H4W          BNG   0  QVT                 [OK IF DIGIT 
   7 2HJG          SBN   0  6
  15 2J46          BZE   0  QVT                 [OK IF SPACE 
   8 2JHQ          SBN   0  #15  
  15 2K3B          BZE   0  QVT                 [OK IF HYPHEN
   8 2KH2          BRN      QILP 
   8 2L2L    QVT2  SBN   0  #32  
  15 2LG=          BPZ   0  QILP                [ERROR > Z   
  21 2L_W    QVT   BCT   1  QVT1                [IF CHECK UNFINISHED,BACK FOR NEXT   
   7 2MFG          BCHX  3  /
   9 2M_6          LDX   0  A1+4(3)  
   8 2NDQ          SMO      FX1  
   9 2NYB          TXL   0  REELMAX  
  17 2PD2          BCC      QILP                [REEL NR OUT OF RANGE
   8 2PXL          LDN   0  #7777
  17 2QC=          ANDS  0  A1+5(3)             [TRUNCATE GENERATION 
   9 2QWW          LDX   3  EVENT3(2)
  16 2RBG          SRL   3  6                   [PROPERTY CODE   
   8 2RW6          ANDN  3  #743 
   7 2S*Q          LDN   6  0
  20 2STB          BZE   3  QPR9                [BRANCH IF NO PROPERTY SPECIFIED 
   8 2T*2          MTHWSPD  3,6  
   4 2TSL    QPR9
  18 2W#=    #     TRANSFER TO COMMAND ACTIVITY AND PREPARE TO GET TAPE  
   4 2WRW    QVT3
  10 2X?G          CAPCA    FILE,FABSNB  
  21 2XR6          HLSXONCH QBRKIN,AWORK1(2),QHLSIL [ASK HLS IF ANUVER TAPE ALLOWED  
  10 2Y=Q          MHUNT    3,FILE,FABSNB
   9 2YQB          LDX   0  A1+9(3)  
  16 2_=2          STO   0  A1+10(3)            [RETENTION PERIOD
   9 2_PL          STOZ     A1+9(3)  
  10 329=          LDN   0  CPREFIX(2)   
   9 32NW          LDN   1  A1+1(3)  
  15 338G          MOVE  0  3                   [USER NAME   
   8 33N6          LDN   0  10   
  16 347Q          STO   0  HDREC(3)            [RECORD HEADER   
  11 34MB    #     CHECK OWNERSHIP OF TAPE   
   9 3572          LDX   0  ATYPE(3) 
   8 35LL          ANDN  0  #200 
  18 366=          BNZ   0  PO4                 [BRANCH IF NAMED WORKTAPE
   9 36KW          LDX   0  HDREC(3) 
   8 375G          SBN   0  10   
   8 37K6          BNG   0  PE1  
   7 384Q          ADN   0  4
  18 38JB          STO   0  HDREC(3)            [INSERT REDUCED LENGTH   
   8 3942          LDX   0  ACES 
   9 39HL          SMO      HDREC(3) 
   9 3=3=          STO   0  A1+5(3)  
  18 3=GW          NGN   0  #101                [REMOVE  LANG CODE BIT   
   9 3?2G          ANDS  0  ATYPE(3) 
   4 3?G6    PO1 
  10 3?_Q          FSHCODE  B,XFSHBOPEN  
   4 3#FB    (   
  16 3#_2          OPEN     PG4,GENERAL,QUERY   [OPEN DIRECTORY  
   6 3*DL    XFSHBOPEN   
   4 3*Y=    )   
  10 3BCW          FSHSKIP  B,XFSHNBOPEN 
   4 3BXG    (   
  11 3CC6          OPEN     PG4,READ,QUERY   
   6 3CWQ    XFSHNBOPEN  
   4 3DBB    )   
   9 3DW2          TESTREP  OK,P03   
   9 3F*L          TESTERR  NAME,TERR
  10 3FT=          TESTREP  NOFILE,PE4   
  10 3G#W          TESTREP  VRYWRONG,PE4 
  10 3GSG          TESTREP  NOUSER,PE4   
   9 3H#6          GEOERR   1,REPLY  
   4 3HRQ    P03 
  19 3J?B          MHUNT    2,FILE,FABSNB       [THE FABSNB NEEDS TO BE SET  
  20 3JR2          LDN   0  6                   [TO CONTAIN :USER.LOCALNAME  ONLY
  19 3K=L          ADS   0  A1(2)               [THIS IS FOR LATER USE WHEN  
  16 3KQ=          LDX   0  A1(2)               [UPDATING BUDGETS
   8 3L9W          SBN   0  10   
   8 3LPG          BZE   0  P02  
   7 3M96          LDX   3  2
   7 3MNQ          TOPFCB   1
   8 3N8B          ADN   1  FME1 
   8 3NN2          ADN   2  A1+1 
   7 3P7L          MOVE  1  3
   8 3PM=          SMO      A1(3)
   9 3Q6W          LDN   1  A1-6(3)  
   9 3QLG          LDN   2  A1+4(3)  
  10 3R66          LDX   5  ALOGLEN(3)   
   8 3RKQ          SBX   5  A1(3)
   7 3S5B          ADN   5  6
   7 3SK2          SMO      5
  19 3T4L          MOVE  1  0                   [MOVE LOCAL NAME UP FABSNB   
   7 3TJ=          ADN   5  4
   8 3W3W          LDN   1  10   
   8 3WHG          STO   1  A1(3)
  11 3X36          ALTLEN   3,5,FILE,FABSNB  
   4 3XGQ    P02 
  21 3Y2B          TOPFCB   3                   [X3 POINTS TO DIRECTORY'S FILE-FCB   
  21 3YG2          JFREZFCB 3,PERR3,NOADJ       [TEST FOR FROZEN DIRS & OWNACC+INFACC
  21 3Y_L          OWNERCHECK NOADJ             [CHECK OWNERSHIP;WITH PRIVS IF NECSRY
   7 3_F=          PICKREP  3
  16 3_YW          CLOSETOP                     [CLOSE DIRECTORY 
   7 42DG          PUTREP   3
  16 42Y6          TESTREP  OK,P03D             [OWNERSHIP OK?   
   7 43CQ          LDX   0  5
  10 43XB          ANDX  0  MASKMON(1)   
  18 44C2          TXU   0  OWNMON(1)           [OWN MONITORING REQUIRED?
  20 44WL          BCC      Q4                  [J TO SET B0 OF REPLY WORD ETC   
  20 45B=          MONOUT   ILACCFB             [MF COMMENT - UNAUTHORISED ACCESS
  18 45TW ...      BRN      Q7                  [J FOR ILLEGAL MESSAGE   
   4 46*G    P03D
  10 46T6          MHUNT    3,FILE,FABSNB
   4 47#Q    PO4 
   7 47SB          LDN   0  1
  20 48#2          ORS   0  ATYPE(3)            [NON-FILESTORE ENTRANT REQUIRED  
   8 48RL    #     GO TO GET TAPE
   8 49?=          LDX   1  FX1  
   7 49QW          LDX   0  5
  10 4==G          ANDX  0  MASKMON(1)   
   9 4=Q6          TXU   0  OWNMON(1)
  17 4?9Q          BCS      QN1                 [NOT OWN MONITORING  
  10 4?PB          SETUPMODE ,0,TAPEUP   
   8 4#92          BRN      QN2  
   7 4#NL    QN1   LDN   0  0
  13 4*8=    QN2   STO   0  AWORK2(2)           [MODE
  19 4*MW          STO   6  ACOMMUNE1(2)        [SPECIAL PROPERTY DESCRIPTION
   8 4B7G          DOWN     GET,4
  15 4BM6          BRN      QBRKIN              [J IF BREAKIN
   8 4C6Q          TESTREP  OK,Q3
   7 4CLB          LDX   0  5
  10 4D62          ANDX  0  MASKMON(1)   
   9 4DKL          TXU   0  OWNMON(1)
  17 4F5=          BCC      Q4                  [J IF OWN MONITORING 
  10 4FJW          TESTREP  NOTAV,QN2A   
  10 4G4G          TESTREPN NODECKS,PQ   
   4 4GJ6    QN2A
   7 4H3Q          NGN   4  1
   8 4HHB          BRN      Q5   
   7 4J32    Q4    LDN   4  0
  15 4JGL    Q5    LDX   5  TFAIL(1)            [FAIL REPLY  
   8 4K2=          BRN      Q6   
  10 4KFW    Q1    MHUNT    3,FILE,FABSNB
   8 4K_G          BRN      Q2   
  10 4LF6    NOCA  MFREE    FILE,FABSNB  
   8 4LYQ          ILLEGAL  NOCA 
   9 4MDB    Q3    LDX   4  AWORK1(2)
   9 4MY2          LDX   5  CPPTR(2) 
   8 4NCL          PUTPERENT 4,5 
   9 4NX=          OUTPAR   TIMENOW  
   8 4PBW          LGEOG    5,3  
   4 4PWG    QSJ1
  11 4QB6          OUTPACK  3,1,GEOPER,QSJ1  
   4 4QTQ    QSJ2
  11 4R*B          OUTPACK  4,1,PERINAME,QSJ2
   9 4RT2          DFLOAD   5,TSN,3  
   4 4S#L    QSJ3
  10 4SS=          OUTPACK  3,1,TSN,QSJ3 
   4 4T?W    QSJ4
  10 4TRG          MHUNT    3,FILE,FABSNB
   9 4W?6          ADX   3  HDREC(3) 
  13 4WQQ          OUTPACK  A1-6(3),3,LOCNAME,QSJ4   
   4 4X=B    QSJ5
  10 4XQ2          MHUNT    3,FILE,FABSNB
   9 4Y9L          ADX   3  HDREC(3) 
  12 4YP=          OUTPACK  A1-2(3),1,GENNUM,QSJ5
   4 4_8W    QSJ6
  10 4_NG          MHUNT    3,FILE,FABSNB
   9 5286          ADX   3  HDREC(3) 
  13 52MQ          OUTPACK  A1-3(3),1,REELNUM,QSJ6   
   8 537B          OUTPAR   SKIP 
   8 53M2          MONOUT   JPER 
  19 546L          DFLOAD   5,TSN,5           [LOAD TSN FOR REPLY TO PROG.   
   7 54L=          LDN   4  0
   9 555W    Q6    LDX   7  AWORK3(2)
   7 55KG          ADN   7  1
  15 5656          GETWORD  7,3,WRITE           [SET REPLY   
   9 56JQ          TESTREPNOT OK,PG2 
   8 574B          STO   5  0(3) 
   7 57J2          WORDFIN   
   8 583L          BNZ   4  Q7   
  16 58H=          RUNPROG                      [RESTART PROGRAM 
   4 592W    Q7  
   9 59GG          LDX   4  AWORK1(2)
  17 5=26          ANDN  4  #777                [PROGRAM UNIT UNUBER 
   7 5=FQ ...      LDCT  0  5
   7 5=_B ...      ORX   4  0
  10 5?F2 ...      OUTPACK  4,1,PERINAME 
   5 5L8Q    QPERF   
   9 5LNB          ILLEGAL  PERFAIL1 
   9 5M82    QILP  ILLEGAL  ILLINS   
   9 5MML    #     ERROR REPORTING   
   5 5N7=    QHLSIL  
   9 5NLW          MONOUT   ERHLSMODE
   7 5P6G          LDX   0  5
  10 5PL6          ANDX  0  MASKMON(1)   
   9 5Q5Q          TXU   0  OWNMON(1)
  17 5QKB          BCC      Q4                  [J IF OWN MONITORING 
   8 5R52          BRN      QN2A 
   5 5RJL    PERR3   
   7 5S4=          CLOSETOP  
   7 5SHW          LDX   0  5
  10 5T3G          ANDX  0  MASKMON(1)   
  18 5TH6          TXU   0  OWNMON(1)           [IF OWN MONITORING REQD -
  20 5W2Q          BCC      Q4                  [J TO SET B0 OF REPLY WORD ETC   
  21 5WGB          MONOUT   FROZDIRFB           [ELSE GIVE MF COMMENT - DIR FROZEN   
  18 5WHR ...      BRN      Q7                  [AND GIVE MT FAIL MESSAGE
   4 5WK8 ...PQ  
  10 5WLK ...      TESTREPN ODRAWN,PQ1   
  11 5WN2 ...      ERROR    BUDGODR,BUDGSPA  
   6 5WPC ...TERR  ENDCOM
   4 5WQS ...PQ1 
  10 5WS9 ...      TESTREPN INCREASE,PQ2 
  11 5WTL ...      ERROR    BUDGINC,BUDGSPA  
   8 5WX3 ...      BRN      TERR 
   4 5WYD ...PQ2 
  10 5W_T ...      TESTREP  ALREADY,TERR 
   9 5X3= ...      TESTERR  NAME,TERR
  10 5X4M ...      TESTREP  WRGEN,TERR   
  10 5X64 ...      TESTREP  VRYWRONG,TERR
   9 5X7F ...      TESTREP  TEMP,TERR
  10 5X8W ...      TESTREP  NODECKS,PE5  
   9 5X=? ...      GEOERR   1,GETREP 
  17 5X?N ...PE1   COMERR   AMTDESC,JLOCN       [SERIAL NUMBER GIVEN 
   4 5X*5 ...PE4 
   9 5XBG ...      COMERR   ERNAME   
   9 5XCX ...PE5   COMERR   ERNODECKS
   8 5XFL    #     BREAKIN EXITS 
   5 5X_=    QBRKIN  
   7 5YDW          STEPBACK  
  10 5YYG          MFREE    FILE,FABSNB  
   7 5_D6           PROGBRKIN
   7 5_XQ    #     CONSTANTS 
   8 62CB    QTEN           100  
   9 62X2    QMT            #55640000
   9 64TG    OWNMON         #34000   
   9 65*6    MASKMON        #76000   
   9 65SQ    TFAIL          #40000000
   9 68=W    REMPROP        #77700077
   9 68GN ...REELMAX             +512
   8 68QG    #     GEORGE ERRORS 
  11 69PQ    PG2   GEOERR   BRIEFPM,SHORTBLK 
  11 6=P2    PG4   GEOERR   BRIEFPM,BREAKIN  
  11 6?8L          MENDAREA  1023-0?,K99GETON
   4 6?N=    #END
   6 ____ ...447373500001
