  11 22FL    #LIS  K0OLMTA>K0LIBRARY>K0ALLGEO
  15 22_=    #SEG           OLMTA6              [N.R.BOULT   
   9 23DW                   8HOLMTA  
  11 23YG          SEGENTRY K1OLMTA,QK1OLMTA 
  11 24D6          SEGENTRY K2OLMTA,QK2OLMTA 
   9 24XQ    TLINK PAIR     OLMTD,1  
   4 25CB    TPRB
  11 25X2    #HAL           APERI+APERIRES,0 
  18 26BL    WMODE          #300                [OPEN MODE FOR WRITING   
   8 26W=    RMODE          #200 
   9 27*W    ZDET           8H(/C1)  
   9 27TG    ZANUM          #30021   
   8 28*6    TMSK           #74  
  11 28SQ    [ENTRY POINT FOR ONLINE COMMANDS
   5 29#B    QK1OLMTA
   7 29B_ ...#UNS  JWPHASE4  
   4 29FJ ...(   
  11 29J7 ...      MHUNT    3,CPAR,GPERCON   
  21 29LQ ...      HLSXONCH XBRKIN,A1(3),PERR2  [ASK HLS IF JOB CAN HAVE ANUVER TAPE 
   4 29P* ...)   
  17 29S2          STOZ     AWORK1(2)           [CLEAR AWORK WORDS   
   9 2=?L          STOZ     AWORK2(2)
   9 2=R=          STOZ     AWORK3(2)
   9 2?=W          STOZ     AWORK4(2)
   9 2?QG          PROPERTY NOP,1,2  
  19 2#=6          MTEXPROP DECK                [EXTRACT SPECIAL PROPERTIES  
   9 2#PQ          TESTREPN OK,PERR  
  10 2*9B          LDX   0  ACOMMUNE1(2) 
  19 2*P2          STO   0  AWORK3(2)           [SPECIAL PROPERTY DESCRIPTION
  10 2B8L          LDX   0  ACOMMUNE2(2) 
  18 2BN=          STO   0  AWORK4(2)           [ILLEGAL PROPERTY MASK   
   9 2C7W          MFREE    CPB,CUNI 
  10 2CMG          MFREE    APERI,APROPS 
   4 2D76    NOP 
  15 2DLQ          SPARANOT 2                   [PASS 2ND PAR
  10 2F6B          HUNT     3,CPB,CUNI   
   9 2FL2          NGX   0  ANUM(3)  
  19 2G5L          BNG   0  PNA                 [BRANCH IF PARAM SPECIFIED   
   9 2GK=          MFREE    CPB,CUNI 
  14 2H4W          LDN   4  7                   [MODES   
   9 2HJG          LDX   0  AWORK3(2)
  10 2J46          STO   0  ACOMMUNE1(2) 
  18 2JHQ          DOWN     LIBREQC,1           [ELSE REQUEST POOLTAPE   
   9 2K3B          BRN      XBRKIN   
   8 2KH2          LDN   0  #600 
   9 2L2L          STO   0  AWORK2(2)
  12 2LG=          TESTREP  NOTAV,NOWT,TEMP,TEMP 
  11 2L_W          SETNCORE 10,2,FLIB,GOLMT  
   8 2MFG          STOZ     A1(2)
   8 2M_6          LDN   5  A1(2)
   9 2NDQ          LDN   6  A1+1(2)  
   7 2NYB          MOVE  5  9
  10 2PD2          HUNT     3,FILE,FABSNB
   9 2PXL          SMO      HDREC(3) 
  10 2QC=          LDX   5  HDREC+1(3)   
   9 2QWW          STO   5  A1+3(2)  
   9 2RBG          SMO      HDREC(3) 
  10 2RW6          LDN   5  HDREC-6(3)   
   9 2S*Q          LDN   6  A1+4(2)  
   7 2STB          MOVE  5  3
   8 2T*2          BRN   PTREP   
   8 2TSL    NOWT  COMERR   JNWT 
   4 2W#=    TEMP
  10 2WRW          SETNCORE 7,3,CPB,CUNI 
   8 2X?G          FJOCA    2,2  
   9 2XR6          LDN   4  JNAME(2) 
   9 2Y=Q          LDN   5  APARA(3) 
   7 2YQB          MOVE  4  3
   9 2_=2          LDN   4  ZDET(1)  
   7 2_PL          ADN   5  3
   7 329=          MOVE  4  2
   7 32NW          LDN   0  1
   9 338G          DSA   0  ATYPE(3) 
  10 33N6          STO   0  JPARNUM(3)   
   9 347Q          LDX   0  ZANUM(1) 
   9 34MB          STO   0  ANUM(3)  
   9 3572          COMERR   ERALREX  
   4 35LL    PNA 
  17 366=          NAMETOP  3,FILE,FNAME        [NAME BLOCK FOR FNORM
   7 36KW          FNORM    7
  10 375G          TESTREP  NAMEFORM,PH3 
   7 37K6          PICKREP  7
  10 384Q          MHUNT    3,FILE,FNAME 
  10 38JB          NAMETOP  3,CPB,CUNI   
   7 3942          PUTREP   7
  16 39HL          NGN   7  1                   [NO ADJUNCTS YET 
  16 3=3=          TESTREP  OK,PGI2             [J NO ADJUNCTS   
  11 3=GW          MHUNT    3,FILE,ADJUNCTS  
   9 3?2G          LDX   0  A1+2(3)  
  16 3?G6          BNG   0  XBO                 [J IF SLOW PERI  
   8 3?_Q          SRL   0  15   
  18 3#FB         BZE   0  XBO1                [J IF NO PERI SPECIFIED   
   7 3#_2          SBN   0  5
  14 3*DL          BZE   0  XBO1                [J IF MT 
  18 3*Y=    XBO   STOZ     A1+2(3)             [CLEAR AS NOT MEANINGFUL 
   9 3BCW    XBO1  LDX   0  A1+2(3)  
   7 3BXG          ANDN  0  2
  20 3CC6          BZE   0  PNA1                [BRANCH IF NO ENTRANT PROPERTIES 
   8 3CWQ          MTEXPROP TAPE 
   9 3DBB          TESTREPN OK,PERR  
  21 3DW2          MTMERGPROP ACOMMUNE1(2),ACOMMUNE2(2),AWORK3(2),AWORK4(2),3,4,PILLC
   9 3F*L          STO   3  AWORK3(2)
   9 3FT=          STO   4  AWORK4(2)
  10 3G#W          MFREE    FILE,FPROPS  
   4 3GSG    PNA1
  11 3H#6          MHUNT    3,FILE,ADJUNCTS  
   8 3HRQ          LDX   1  FX1  
   9 3J?B          LDX   0  TGI(1)   
   9 3JR2          ANDX  0  A1+1(3)  
  18 3K=L          BZE   0  PNA2                [J IF NO 'GIVE' QUALIFIER
   8 3KQ=          LDCT  0  #400 
   9 3L9W          STO   0  AWORK1(2)
   9 3LPG    PNA2  LDX   4  A1+2(3)  
   7 3M96          ANDN  4  1
  20 3MNQ          BZE   4  PNA3                [J IF NO 'NONSTANDARD' QUALIFIER 
   9 3N8B          LDN   4  GLIBNONS 
   9 3NN2          ORS   4  AWORK3(2)
  10 3P7L    PNA3  LDX   2  ALOGLEN(3)   
   9 3PM=          SBN   2  ADPAIR-A1
  17 3Q6W          SRL   2  1                   [NO. OF ADJUNCT PAIRS
   8 3QLG          BZE   2  PM3  
   9 3R66    PM1   LDX   0  ADPAIR(3)
   8 3RKQ          SBN   0  15   
  18 3S5B          BNZ   0  PM2                 [J IF NOT MODE ADJUNCT   
  10 3SK2          LDX   7  ADPAIR+1(3)  
   8 3T4L          BRN      PM3  
   7 3TJ=    PM2   ADN   3  2
  16 3W3W          BCT   2  PM1                 [TRY NEXT PAIR   
   4 3W=6 ...PM3 
   7 3WDB ...#UNSET   FTS1   
   5 3WLL ...#SKIP   
  10 3WSW ...      MFREE    FILE,ADJUNCTS
   8 3WY2 ...      LDX   2  FX2  
  11 3X36          MHUNT    3,CPAR,GPERCON   
  17 3XGQ          BNG   7  PGI2                [J IF NO MODE ADJUNCT
   9 3Y2B          LDX   0  A1+2(3)  
   9 3YG2          ANDX  0  TMSK(1)  
  18 3Y_L          BNZ   0  PGI2                [J IF MODE ALREADY GIVEN 
   9 3_F=          LDX   0  TMSK(1)  
   9 3_YW          ORS   0  A1+2(3)  
  16 42DG          ORS   7  A1+1(3)             [STORE MODE VALUE
   9 42Y6    PGI2  LDX   5  AWORK3(2)
  17 43CQ          BZE   5  PM4                 [J IF NO PROPERTIES  
  10 43XB          SETNCORE 1,3,FLIB,FSPD
   8 44C2          STO   5  A1(3)
  11 44WL          MHUNT    3,CPAR,GPERCON   
   9 45B=          ANDN  5  GLIBMODE 
  18 45TW          BZE   5  PM4                 [J IF NO MODE PROPERTY   
   9 46*G          LDX   5  AWORK3(2)
   8 46T6          LDN   0  #74  
   9 47#Q          ORS   0  A1+1(3)  
   9 47SB          ERS   0  A1+1(3)  
   9 48#2          ORS   0  A1+2(3)  
   8 48RL          SRL   5  18   
  16 49?=          ORS   5  A1+1(3)             [STORE MODE VALUE
   4 49QW    PM4 
  11 4=2N          MHUNT    3,CPAR,GPERCON   
  16 4==G          LDX   0  A1+1(3)             [PROPERTY WORD   
  16 4=Q6          ADX   0  A1+2(3)             [PLUS SPECIFIER  
   7 4?9Q          ANDN  0  3
   7 4?PB          ADN   0  1
  13 4#92          SLL   0  6                   [MODE
   9 4#NL          STO   0  AWORK2(2)
  10 4*8=          MHUNT    3,FILE,FABSNB
   7 4*MW          LDN   0  1
   9 4B7G          ORS   0  ATYPE(3) 
   9 4BM6          LDX   0  ATYPE(3) 
   8 4C6Q          ANDN  0  #200 
  19 4CLB          BNZ   0  PM6                 [BRANCH IF NAMED WORK TAPE   
   9 4D62          LDX   0  AWORK2(2)
   8 4DKL          LDN   7  AREAD
  19 4F5=          BXU   0  WMODE(1),PH4        [READ UNLESS WRITE SPECIFIED 
   9 4FJW          LDN   7  AWRITE   
   4 4G4G    PH4 
  11 4GJ6    #     ADJUST GENERATION NUMBER  
   8 4H3Q          LDCT  0  #600 
   9 4HHB          SMO      HDREC(3) 
   9 4J32          ANDX  0  A1-2(3)  
  21 4JGL          BZE   0  PGN                 [BRANCH UNLESS RELATIVE GENERATION   
   9 4K2=          LDX   0  AWORK3(2)
   9 4K=4 ...      ANDN  0  GLIBNONS 
  18 4KFW          BNZ   0  PH4A                [BRANCH IF NONSTANDARD   
   9 4K_G          LDX   0  ATYPE(3) 
   8 4LF6          ANDN  0  #2000
  18 4LYQ          BZE   0  PH4B                [BRANCH IF NOT XENOTAPE  
   9 4MDB          SMO      HDREC(3) 
   9 4MY2          LDX   0  A1+1(3)  
  18 4NCL          BPZ   0  PH4B                [BRANCH IF NOT XENOTAPE  
   4 4NX=    PH4A
   9 4PBW          COMERR   ERGENERR 
   4 4PWG    PH4B
  14 4QB6          LDCT  0  #200                [SET B1  
   9 4QTQ          SMO      HDREC(3) 
   9 4R*B          ANDX  0  A1-2(3)  
  15 4RT2          BZE   0  PGN                 [J IF NOT +  
   9 4S#L          SMO      HDREC(3) 
  15 4SCQ ...      LDX   0  A1-2(3)             [SET +N TO +0
   8 4SGW ...      ANDN  0  #7777
   8 4SL2 ...      BZE   0  PGN  
  15 4SP6 ...      COMERR   ERNOFILE            [ERROR IF +N 
   2 4SS=
   4 4T?W    PGN 
   9 4TRG    #     ADJUST REEL NUMBER
   9 4W?6          SMO      HDREC(3) 
   9 4WQQ          LDX   0  A1-3(3)  
  18 4X=B          BNZ   0  PGN1                [BRANCH IF REEL NO. GIVEN
  17 4XQ2          LDCT  0  #100                [DEFAULT SETTING=0   
   9 4Y9L          SMO      HDREC(3) 
   9 4YP=          STO   0  A1-3(3)  
   4 4_8W    PGN1
   8 4_NG          SMO      FX2  
   9 5286          LDX   0  AWORK3   
   6 52MQ    #SKI  FLIBP 
   9 537B          ANDN  0  GLIBNONS 
   8 53M2          BZE   0  PH7  
   9 546L          DOWN     XENON,2  
   9 54L=          BRN      XBRKIN   
   9 555W          BRN      PTREPX   
   9 55KG    PH7   LDX   0  ATYPE(3) 
   8 5656          ANDN  0  #2000
  14 56JQ          BZE   0  PH8                 [NOT XENO
   9 574B          SMO      HDREC(3) 
   9 57J2          LDX   0  A1+1(3)  
  14 583L          BPZ   0  PH8                [NOT  XENO
   9 58H=          DOWN     XENON,1  
   9 592W          BRN      XBRKIN   
   9 59GG          BRN      PTREPX   
   4 5=26    PM6 
   8 5=FQ          LDCT  0  #200 
   9 5=_B          SMO      HDREC(3) 
  17 5?F2          STO   0  A1-2(3)             [TAKE HIGHEST LEVEL  
  16 5?YL          STOZ     AWORK3(2)           [STANDARD TAPE   
   9 5#D=          LDX   0  AWORK2(2)
   9 5#XW          LDN   7  AWRITE   
  19 5*CG          BXE   0  WMODE(1),PH8        [BRANCH IF WRITE SPECIFIED   
  10 5*X6          BXU   0  RMODE(1),PM6A
   8 5BBQ          LDN   7  AREAD
   8 5BWB          BRN      PH8  
   4 5CB2    PM6A
  11 5CTL          MHUNT    3,CPAR,GPERCON   
   7 5D*=          LDN   0  1
   9 5DSW          ORS   0  A1+1(3)  
   9 5F#G          ORS   0  A1+2(3)  
   4 5FS6    PH8 
  19 5G?Q          SETUPMODE ,6,HDDRLAB         [SET UP FLIB/GOLMT WITH DATE 
  10 5GRB          USEROPEX XBRKIN,7,6   
   7 5GYJ ...#UNSET   FTS1   
  10 5H5Q ...      VFREE    FILE,ADJUNCTS
   5 5H?2    PTREP   
   8 5HQL          REPERR   PH2  
  15 5J==    PH3   ENDCOM                       [FINISH-ERROR
   5 5JPW    PTREPX  
  15 5K9G          TESTREP  OK,PH2              [TAPE ONLINE 
  10 5KP6          TESTREPN NONSER,PH10  
   8 5L8Q          COMERR   HTSN 
   4 5LNB    PH10
  16 5M82 ...      ERRTEST  3,PH3,NOTAV,NOXENO,XENONAME,CLUDGE   
   9 5MML          GEOERR   1,XREP   
  18 5N7=    TGI            +AGIVE              [MASK FOR GIVE QUALIFIER 
  19 5NLW    [NOW FIND THE FLIB/GOLMT BLOCK AND PRB ENTRY WORD IN X5 SET UP  
   4 5P6G    PH2 
  11 5PL6          HUNT     3,CPAR,GPERCON   
  16 5Q5Q          LDX   5  A1(3)               [TYPE/UNIT NO.   
  10 5QKB          MHUNT    3,FLIB,GOLMT 
   7 5R52          LDCT  0  5
   8 5RJL          SMO      FX2  
   9 5S4=          ADX   0  AWORK2   
  17 5SHW          STO   0  A1+2(3)             [TYPE/EFFECTIVE MODE 
   8 5T3G          BRN      PA   
  21 5TH6    [THIS SECTION INSERTS AN ENTRY INTO THE PERIPHERAL RESERVATION BLOCK,   
  21 5W2Q    [SETS UP AN OFFLINE ACTIVITY BLOCK,TYPE AOLPT, WHICH IT CHAINS IN THE   
  21 5WGB    [APPROPRIATE PLACE AFTER THE PERIPHERAL RESERVATION BLOCK IN ORDER TO   
  20 5X22    [MONITOR THE FIRST OBJECT PROGRAM PERI,4 IF IT IS AN OPEN MODE PERI 
  21 5XFL    [TO FILLIN THE REPLY INFORMATION FROM THE DATA BLOCK,TYPE CPAR/GOLMT,   
  14 5X_=    [WHICH IS ALSO CHAINED AFTER THE NEW AOLPT  
   4 5YDW    [   
   4 5YYG    [   
   5 5_D6    QK2OLMTA
  10 5_XQ          STOZ      AWORK3(2)   
   4 62CB    PA  
   8 62X2          LDX   2  FX2  
   9 63BL          LDX   4  CPPTR(2) 
   9 63W=          LDX   0  AWORK1(2)
  18 64*W          BPZ   0  PA1A                [BRN IF NO GIVE QUALIFIER
   9 64TG          LDX   0  AWORK3(2)
  17 65*6          BZE   0  PA1                 [BRN IF NOT NONS TAPE
   4 65SQ    PA1A
   8 66#B          ORX   4  GSIGN
   4 66S2    PA1 
  21 67?L          PUTPERENT 5,4                [SET UP PRB ENTRY & OUTPUT MESSAGE   
   9 67R=          OUTPAR   TIMENOW  
   4 68=W    SJ  
   8 68QG          LGEOG    ,3   
  10 69=6          OUTPACK  3,1,GEOPER,SJ
   4 69PQ    SJ1 
  11 6=9B          OUTPACKB 5,1,PERINAME,SJ1 
   9 6=P2          DFLOAD   ,TSN,3   
  19 6?8L          BZE   3  SJ3                 [BRANCH IF NONSTANDARD TAPE  
   4 6?N=    SJ2 
  10 6#7W          OUTPACKB 3,1,TSN,SJ2  
   8 6#MG          BRN      SJ4  
   4 6*76    SJ3 
   8 6*LQ          OUTPARB  SKIP 
   4 6B6B    SJ4 
  10 6BL2          MHUNT    3,FLIB,GOLMT 
  12 6C5L          OUTPACKB A1+4(3),3,LOCNAME,SJ4
   4 6CK=    SJ5 
  10 6D4W          MHUNT    3,FLIB,GOLMT 
  12 6DJG          OUTPACKB A1+8(3),1,GENNUM,SJ5 
   4 6F46    SJ6 
  10 6FHQ          MHUNT    3,FLIB,GOLMT 
  12 6G3B          OUTPACKB A1+7(3),1,REELNUM,SJ6
   9 6GH2          DFLOAD   ,TSN,3   
   8 6H2L          BZE   3  SJ7  
   8 6HG=          OUTPARB  SKIP 
   8 6H_W          BRN      SJ8  
   4 6JFG    SJ7 
   8 6J_6          OUTPARB  NULL 
   4 6KDQ    SJ8 
   8 6KYB          MONOUT   JPER 
   9 6LD2          LDX   0  AWORK3(2)
  19 6LXL          BNZ   0  PA2                 [IF NONS TAPE SET UP AOLPT   
   9 6MC=          LDX   0  AWORK1(2)
  20 6MWW          BNG   0  QGI1                [BRANCH IF GIVE QUALIFIER GIVEN  
   4 6NBG    PA2 
  16 6NW6          GETACT   AOLPT,,1            [GET NEW ACTIVITY
  18 6P*Q          LDX   3  BPTR(2)             [POINTER TO NEW ACTIVITY 
   9 6PTB          LDX   0  JOBNO(2) 
  16 6Q*2          STO   0  JOBNO(3)            [INSERT JOB NR   
   7 6QSL          LDN   0  2
  17 6R#=          STO   0  ATMARK(3)           [TYPE MARKER,2 FOR MT
  10 6RRW          LINKSET  3,TLINK(1)   
   9 6S?G          LDX   0  AWORK3(2)
   9 6SR6          ANDN  0  GLIBNONS 
  17 6T=Q          STO   0  AWORK3(3)           [NONSTANDARD MARKER  
   9 6TQB          STOZ     ACOUNT(3)
   9 6W=2          STO   5  ACC4(3)  
   9 6WPL          STO   4  ACC5(3)  
   8 6X9=          LDX   0  GSIGN
  19 6XNW          STO   0  FRECHADD(3)         [CHAIN ACTIVITY WHEN EVENT   
   9 6Y8G          LDX   0  AWORK1(2)
  18 6YN6          BPZ   0  PA3                 [BRN IF NO GIVE QUALIFIER
   8 6_7Q          LDX   0  GSIGN
  10 6_MB          STO   0  ALTMARK(3)   
   4 7272    PA3 
  16 72LL          CHANGEPER 3                  [HAND OVER TAPE  
  19 736=          ROLPA                        [CHAIN AOLPT IN CORRECT PLACE
  19 73KW          LOCK     6                   [GIVEN TO THE MONITORING FILE
  10 745G          MHUNT    1,FLIB,GOLMT 
   8 74K6          CHAIN    1,6  
   7 754Q          UNLOCK   6
   4 75JB    QGI4
   6 7642          ENDCOM
   4 76HL    QGI1
   9 773=          STOZ     CPPTR(2) 
  10 77GW          MFREE    FLIB,GOLMT   
   8 782G          BRN      QGI4 
   4 78G6    PERR
  10 78_Q          TESTREPN ILLCOMB,PERR1
   5 79FB    PILLC   
   9 79_2          COMERR   ERILLCOMB
   5 7=DL    PERR1   
   9 7=Y=          COMERR   ERILLMODE
   7 7?2T ...#UNS  JWPHASE4  
   4 7?5D ...(   
   5 7?83 ...PERR2   
  20 7?=L ...      COMERR   ERHLSONL            [HLS FORBIDS FURTHER ACQUISITION 
   4 7?*9 ...)   
   9 7?CW    OERR1 GEOERR   1,XPROG  
   5 7?XG    XBRKIN  
   7 7#C6          COMBRKIN  
  10 7#WQ          MENDAREA 50,K99OLMTA  
   4 7*BB    #END
   8 ____ ...35735642000500000000
