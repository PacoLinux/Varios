  15 22FL ...#SEG  RIGHT                        [BOB WEYAND  
  16 22PD ...[  (C)  COPYRIGHT INTERNATIONAL COMPUTERS LTD. 1982 
   7 22_=    #OPT  K0RIGHT=0 
  13 23DW    #LIS  K0RIGHT>K0PERI>K0COMMAND>K0ALLGEO 
   9 23YG                   8HRIGHT  
  17 24D6 ...#     THIS SEGMENT SERVICES THE RIGHT AND WRONG COMMANDS
   4 28SQ    #   
   4 29#B    #   
  10 29S2    #              ENTRY POINTS 
   4 2=?L    #   
  11 2=R=          SEGENTRY K2RIGHT,X2RIGHT  
  11 2?=W          SEGENTRY K3RIGHT,X3RIGHT  
  11 2?BR ...      SEGENTRY K4RIGHT,X4RIGHT  
  21 2?GN ...#               K4 ACROSS FROM SWITCH IN WITH CPAR/JSCES & AWORK2 SET UP
   4 2?LK ...#   
   4 2?QG    #   
   9 2#=6    XJRIGHT        +JRIGHT  
   9 2#PQ    XJWRONG        +JWRONG  
   9 2*9B    XJWATWR        +JWATWR  
   9 2*P2    XJCANWR        +JCANTWR 
   9 2*YS ...XJMNRI         +JMNRI   
   8 2B8L    XJRES          +JRES
   9 2BN=    XNOPER         +JNOPER  
   9 2C7W    XPFERR         +APFERR  
   9 2C=F ...XJWTSW         +JWTBSW  
  10 2C*4 ...XJMPON         +JMOPPEDON   
   9 2CCN ...XERAT          +JERAT   
   9 2CD_ ...XERTEST        +JERTEST 
   6 2CG? ...#UNS ISUMBI 
  10 2CJW ...XJIDEBUSY      +JIDEBUSY1   
   8 2CMG    RMPX           +11  
   8 2D76    RUPX           +10  
   8 2DLQ    RFD            +13  
   8 2F6B    RIPB           +24  
   9 2FL2    RPRPMSK        #37777   
   9 2G5L    RUMSK          #777000  
   9 2GK=    PROPMASK       #37700   
   8 2H4W    RMTYP          +5   
   8 2HJG    XTP            +1   
   8 2J46    XLP            +2   
   8 2JHQ    XCP            +4   
   8 2K3B    RMAXTP         +6   
   5 2KH2    PLINK   
  10 2L2L          PAIR     LISTINIT,5   
  14 2LG=    PLINKTC                            [TP OR CP
   9 2L_W          PAIR LISTINTC,5   
   5 2MFG    RLINK   
  10 2M_6          PAIR     PCTSTART,1   
  18 2NDQ    TERROR                             [USED FOR COMMAND ERRORS 
   8 2NYB          SBX   7  FX1  
  18 2PD2          MHUNTW   2,CPAR,JSCE         [LOCATE CPAR/JSCE BLOCK  
   9 2PXL          LDX   5  JSCEQ(2) 
  14 2QC=          FREECORE 2                   [FREE IT 
  16 2QWW          SRL   5  12                  [PARAMETER NUMBER
  18 2RBG          LDN   4  0                   [PARAMETER ANALYSIS LEVEL
  17 2RW6          SPARANOX 5,4                 [REACCESS PARAMETER  
   7 2S*Q          ERRORX   6
  17 2STB          UNIFREE                      [FREE CPB,CUNI BLOCK 
   8 2T*2          ADX   7  FX1  
   7 2TSL          EXIT  7  0
  19 2W#=    RHUNT                              [HUNTS FOR PARAMETER BLOCK   
  17 2WRW          HUNTW    2,CPAR,JSCE         [FIND CPAR/JSCE BLOCK
   7 2X?G          EXIT  6  0
  20 2XR6    ROUTSS                             [OUTPUTS SS SEGMENT,CODE IN X0   
   8 2Y=Q          SBX   7  FX1  
   8 2YQB          LDX   1  FX2  
  10 2_=2          STO   0  ACOMMUNE1(1) 
  16 2_PL          LDX   2  JSCEA(2)            [IDENTIFIER NO   
   8 329=          ANDN  2  #7777
  10 32NW          CCOUT    2,AWORK1(1)  
   8 338G          ADX   7  FX1  
   7 33N6          EXIT  7  0
   4 347Q    #   
   7 34MB    RCH1  #770000   
  20 3572    RPCT                               [SETS WAITING TO BE WRONGED BIIT 
  18 35LL          LDX   4  APCTDEV(3)          [... IN INTERFACE BLOCK  
   8 366=          SMO      FX1  
   8 36KW          ANDX  4  RCH1 
   8 375G          SMO      FX2  
  21 37K6          LDX   0  AWORK3              [REL POINTER TO A PCT RECORD WHITHIN 
  18 384Q          ADX   4  0                   [THE A7020/INFORM BLOCK  
   7 38JB          SBX   1  0
   8 3942          PCT7020  1,4  
   8 39HL          LDCT  0  #4   
   9 3=3=          ORS   0  EWAIT(1) 
   7 3=GW          EXIT  6  0
   6 3=HJ ...#UNS AV7900S
   4 3=J= ...(   
   4 3=JY ...#   
  20 3=KL ...#     SUBROUTINE TO TEST IF AN IPB IS A VIRTUAL 7900 AND IF TRUE TO 
  14 3=L# ...#     RIGHT OR WRONG IT IN THE DEVICE LIST  
  15 3=M2 ...#     ON ENTRY X3 -> AN IPB (7900) DEVICE LIST  
  21 3=MN ...#     IF THE V7900 IS RIGHTED/WRONGED IN DL, IT WILL BE WRONGED/RIGHTED 
   8 3=NB ...#     RESPECTIVELY  
  19 3=P4 ...#     EXITS+1 IF THE 7900 IS NOT A VIRTUAL 7900, ELSE EXITS+0   
   7 3=PQ ...#     LINK IN X7
   6 3=QD ...RIORWRV7900 
   9 3=R6 ...      JV7900   3,RIORWR 
  21 3=RS ...      EXIT  7  1                   [7900 IS NOT A VIRTUAL 7900 : EXIT +1
   5 3=SG ...RIORWR  
  21 3=T8 ...      LDCT  0  #400                [REVERVSE 'DEVICE WRONGED' BIT IN DL 
   9 3=TW ...      ERS   0  JK52B(3) 
  21 3=WJ ...      CLWTBWR  3                   [CLEAR  WAITING TO BE WRONGED' BIT   
  21 3=X= ...                                   [IN DL AS IT IS NOT DONE AT LABEL XL 
  20 3=XY ...      EXIT  7  0                   [V7900 WRONGED/RIGHTED : EXIT +0 
   4 3=YL ...#   
   4 3=_# ...)   
  20 3?2G    X2RIGHT                            [ENTRY POINT FOR RIGHT COMMAND   
  15 3?G6          NGN   6  1                   [SET X6 -VE  
   8 3?_Q          BRN      RSTT 
  20 3#FB    X3RIGHT                            [ENTRY POINT FOR WRONG COMMAND   
   7 3#_2          LDN   6  6
   4 3*DL    RSTT
  17 3*Y=          STO   6  AWORK2(2)           [STORE RI/WR MARKER  
  17 3BCW          PARSORT  YUK,ALL             [ANALYSE PARAMETERS  
   5 3BMN ...X4RIGHT 
   5 3BXG    RRSTT   
  19 3CC6          CALL  6  RHUNT               [HUNT CPAR/JSCE BLOCK ON X2  
  17 3CWQ          BNG   2  OVER                [PACK UP IF NONE LEFT
  18 3DBB          LDX   3  JSCEP(2)            [LOAD DEVICE LIST POINTER
  14 3DW2          SAWCEJX  JSCEB(2),T1,RMRIGHT,RMRIGHT  
  20 3F*L    #                                   BR TO RMRIGHT IF DEVICE REMOTE  
   4 3FT=    T1  
  16 3G#W          LDX   5  JSCEA(2)            [LOAD JSOURCE1   
   8 3GSG          LDX   2  FX2  
  18 3H#6          LTYPE    3,4                 [PERIPHERAL TYPE TO X4   
   9 3HRQ          LDX   0  AWORK2(2)
  18 3J?B          BPZ   0  RWRONG              [BR IF ENTERED FOR WRONG 
  11 3JR2    # CODING FOR LOCAL RIGHT COMMAND
   9 3K2S ...      JWTBSW   3,XWTBSW 
  21 3K=L          CLWTBWR  3                   [CLEAR 'WAITING TO BE WRONGED' BIT   
  17 3KQ=          JRIGHT   3,RALRI             [BR IF ALREADY RIGHT 
   9 3L9W          JEXOTIC  3,XNFW   
   9 3LPG          TXU   4  RIPB(1)  
  15 3M96          BCC      RXIPB               [BR IF IPB   
  19 3MNQ          CHECKFLA 3,XNFW              [BR IF DEVICE NOT FLAGWAVER  
   8 3MYJ ...#SKI  IDFMARK8<1$1  
  20 3N8B          XRIGHT   3                   [RIGHT THE MPX OR UPX IN THE IDF 
   7 3N*J ...#SKI  IDFMARK8  
  18 3NGQ ...      XRIGHT   YUK,3               [RIGHT MX OR UX IN IDF   
   8 3NN2          BRN      XNFW 
   5 3P7L    RXIPB   
   8 3PCD ...#SKI  IDFMARK8<1$1  
  18 3PM=          YRIGHT   3                   [RIGHT THE IPB IN THE IDF
   7 3PSD ...#SKI  IDFMARK8  
  16 3P_L ...      YRIGHT   YUK,3               [RIGHT IPB IN IDF
   6 3Q2B ...#UNS AV7900S
   4 3Q36 ...(   
  21 3Q3W ...      CALL  7  RIORWRV7900         [IF A V7900, RIGHT IT IN DEVICE LIST 
  21 3Q4L ...      BRN      RIGHTEDINDL         [V7900 RIGHTED : SKIP RIGHT MACRO AS 
  19 3Q5B ...                                   [IT IS NOT VALID FOR V7900'S 
   4 3Q66 ...)   
   4 3Q6W    XNFW
  16 3QLG          RIGHT    3                   [RIGHT THE DEVICE
   6 3QW# ...RIGHTEDINDL 
  17 3R66          JMT      3,XMT               [BR IF MAGNETIC TAPE 
   9 3RKQ          TXU   4  XLP(1)   
  18 3S5B          BCS      XNOTLP              [BR IF NOT LINEPRINTER   
  19 3SK2          LPUNSET  3                   [UNSET 'PAPER ALIGNED' MARKER
   9 3T4L          BRN      XSLOPP   
   5 3TJ=    XNOTLP  
   9 3W3W          TXU   4  XTP(1)   
  16 3WHG          BCC      XSLOPP              [BR IF TAPE PUNCH
   9 3X36          TXU   4  XCP(1)   
  17 3XGQ          BCS      XNSLOPP             [BR IF NOT CARD PUNCH
   5 3Y2B    XSLOPP  
  17 3YG2    # NOW TO SET UP AN ACTIVITY FOR THE SLOW OUTPUT DEVICE  
  18 3Y_L          SETALLOC 3                   [MARK DEVICE ALLOCATED   
  17 3_F=          GETACT   ACTLF,CENTRAL       [SET A CENTRAL ACTLF 
  18 3_YW          LDX   2  BPTR(2)             [X2 POINTS TO NEW ACTLF  
  17 42DG          STO   3  CPPTR(2)            [GIVE DEVICE TO ACTLF
  19 42Y6          STO   5  JSOURCE1(2)         [STORE GEOG NO IN JSOURCE1   
   9 43CQ          TXU   4  XLP(1)   
  14 43XB          BCC      TOLP                [J IF LP 
  10 44C2          LINKSET 2,PLINKTC(1)  
   8 44WL          BRN      NOTLP
   4 45B=    TOLP
   9 45TW          LINKSET 2,PLINK(1)
   5 46*G    NOTLP   
   6 46T6          FPUT  
   8 47#Q          BRN      RALRI
   5 47SB    XNSLOPP 
  17 48#2          BZE   4  RIN                 [BR IF TAPE READER   
   7 48RL          SBN   4  3
  18 49?=          BNZ   4  RALRI               [BR IF NOT CARD READER   
   4 49QW    RIN 
  21 4==G          LONGON   #60                 [WAKE UP ACTIVITIES WAITING FOR DOC  
   5 4=Q6    RALRI   
  10 4?9Q          LDX   7  XJRIGHT(1)   
  17 4?PB          BRN      RLOCRT              [BR TO OUTPUT MESSAGE
   9 4#92    # INSERT MT CODING HERE 
   4 4#NL    XMT 
   8 4*8=          LPROP    3,7  
   7 4*MW          SRL   7  7
   7 4B7G          ANDN  7  1
  17 4BM6          BNZ   7  RALRI               [BRANCH IF NINE TRACK
  19 4C6Q          DFLOAD   3,SMODE,7           [LOAD STANDARD MODE INTO X7  
  18 4CLB          SETMODEQ 3,7                 [AND STORE IN DEVICE LIST
  17 4D62          BRN      RALRI               [BR TO OUTPUT MESSAGE
  17 4DKL    RWRONG                             [ENTERED FOR WRONG   
  18 4F5=    #                                  [X3 = DEVICE LIST POINTER
  15 4FJW    #                                  [X4 = TYPE   
   9 4FQ4 ...      TXU   4  RIPB(1)  
  15 4FX= ...      BCC      ZIPB                [BR IF IPB   
  20 4G4G          JMNBWR   3,RCNWRL            [BR IF DEVICE CANNOT BE WRONGED  
  18 4GJ6          JALLOC   3,RALLOC            [BR IF DEVICE ALLOCATED  
  18 4H3Q          JWRONG   3,XDWUA             [BR IF ALREADY WRONGED   
  15 4HHB          JEXOTIC  3,XNOTFW            [BR IF EXOTIC
  17 4K2=          CHECKFLA 3,XNOTFW            [BR IF NOT FLAGWAVER 
   8 4K=4 ...#SKI  IDFMARK8<1$1  
  20 4KFW          XWRONG   RCNWRL,3            [WRONG THE MPX OR UPX IN THE IDF 
   7 4KM4 ...#SKI  IDFMARK8  
  20 4KS= ...      XWRONG   YUK,RCNWRL,3        [WRONG THE MPX OR UPX IN THE IDF 
   9 4K_G          BRN      XNOTFW   
   4 4LF6    ZIPB
   9 4LG5 ...      MOPSTATE 3,7,AMOP 
  16 4LH4 ...      BZE   7  XMPON               [J IF MOPPED ON  
  16 4LJ3 ...      BNG   7  ZIPBWR              [J IF MOPPED OFF 
   7 4LK2 ...      ANDN  7  1
  18 4LK_ ...      BNZ   7  XMPON               [ J IF BEING MOPPED ON   
  20 4LLY ...      BRN      RALLOC              [J IF BEING MOPPED OFF OR ONLINE 
   5 4LMX ...ZIPBWR  
   8 4LNY ...#SKI  IDFMARK8<1$1  
  18 4LYQ          YWRONG   RCNWRL,3            [WRONG THE IPB IN THE IDF
   7 4M5Y ...#SKI  IDFMARK8  
  18 4M?6 ...      YWRONG   YUK,RCNWRL,3        [WRONG THE IPB IN THE IDF
   6 4M?R ...#UNS AV7900S
   4 4M#D ...(   
  21 4M*5 ...      CALL  7  RIORWRV7900         [IF A V7900, WRONG IT IN DEVICE LIST 
  21 4M*Q ...      BRN      XDWUA               [V7900 WRONGED : SKIP DISENGPER AND  
  21 4MBC ...                                   [WRONG MACROS AS THEY ARE NOT VALID  
  15 4MC4 ...                                   [FOR V7900'S 
   4 4MCP ...)   
   5 4MDB    XNOTFW  
  16 4MY2          JMT      3,XWRMT             [BR IF MAG TAPE  
  17 4NCL          DISENGPE 3                   [DISENGAGE THE DEVICE
   4 4NX=    XL  
  21 4PBW          CLWTBWR  3                   [CLEAR 'WAITING TO BE WRONGED' BIT   
  16 4PWG          WRONG    3                   [WRONG THE DEVICE
   5 4QB6    XDWUA   
  10 4QTQ          LDX   7  XJWRONG(1)   
  17 4R*B          BRN      RLOCRT              [BR TO OUTPUT MESSAGE
  18 4RT2    RALLOC                             [DEVICE ALLOCATED (WR)   
  20 4S#L          SETWTBWR 3                   [SET 'WAITING TO BE WRONGED' BIT 
   5 4SS=    RWTWMESS
  17 4T?W          LDX   7  XJWATWR(1)          [MESSAGE IDENTIFIER  
   9 4TRG          BRN      RLOCRT   
   5 4W?6    XWRMT   
   7 4WQQ    # WR MT CODING  
  17 4X=B          DFJUMPS  3,LOAD,TL           [BR IF TAPE LOADED   
  18 4XQ2          BRN      XL                  [NO 3APE UP - AS NORMAL  
   4 4Y9L    TL  
  19 4YP=          SETALLOC 3                   [MARK ALLOCATED FOR UNLOAD   
  19 4YT7 ...      SETWTBWR 3                   [SET 'WAITINGTOBEWRONGED' &  
  21 4Y_4 ...#                                  [..FREEPER(ISSUED BY UNLOAD MACRO..  
  19 4_4_ ...#                                  [..WILL WR IT & GIVE MESSAGE 
  15 4_8W          UNLOAD   3,,,UNL             [UNLOAD DECK 
   9 4_H? ...      MFREEW   CPAR,JSCE
   8 4_TN ...      BRN      RRSTT
   5 5286    RLOCRT  
   8 52MQ          CALL  6  RHUNT
  10 537B          GEOPACK  4,JSCEA(2)   
   7 53M2          FREECORE 2
  10 546L          OUTPACK  4,1,GEOPER   
  21 54L=          LDN   5  0                   [CLUSTER CONSOLE ROUTING PARAMETER   
  15 555W          INFORMX  5,7,1               [SEND MESSAGE
   8 55KG          BRN      RRSTT
   4 5656    OVER
   6 56JQ          ENDCOM
   4 574B    YUK 
  19 57J2          GEOERR   1,BREAKIN           [BREAKIN LABEL FROM PARSORT  
   4 583L    #   
  17 58H=    RMRIGHT                            [WR&RI REMOTE DEVICES
  14 58ND ...      LDEX  0  JSCEB(2)            [J. IF CI
   8 58TL ...      BZE   0  RMRCI
   6 58WB ...#UNS ISUMBI 
   4 58X6 ...(   
  21 58XW ...      SAWCEJX  JSCEB(2),USTATE,UWRONG [IF ON 7900,SKIP UNIT MOPPED OFF  
  16 58YL ...                                   [/ONLINED CHECK  
   5 58_B ...USTATE  
   4 5926 ...)   
  10 592W          JFREE    3,NOTMOPPEDON
  10 59GG          JONL     3,NOTMOPPEDON
   6 59K5 ...#UNS ISUMBI 
   4 59MN ...(   
   5 59Q? ...UWRONG  
  10 59SW ...      JWRONG   3,NOTMOPPEDON
   4 59XF ...)   
  18 5=26          LAMOP    3,3                 [LOAD ADDRESS OF THE AMOP
  16 5=44 ...      BRN      RSKIPCI              [MISS CI CODE   
  16 5=62 ...RMRCI ANDX  3  BITS22LS            [PROG. PROP. NO. 
  15 5=7Y ...      ORX   3  GSIGN               [PLUS BIT 0  
  17 5=9W ...      FMOPG    1,3,IPB             [FIND RELEVANT AMOP  
  10 5=?S ...      BNG   1  NOTMOPPEDON  
  18 5=*Q ...      LDX   3  1                   [CONTINUE DOWN COMMONPATH
   5 5=CN ...RSKIPCI 
   8 5=FQ          SMO      FX2  
   9 5=_B          STO   3  AWORK1   
   9 5?F2          LDCH  0  JSCEA(2) 
   8 5?YL          SBN   0  #77  
  20 5#D=          BNZ   0  NOTCONSOLE          [BR IF DEVICE NOT OP'S CONSOLE   
   8 5#XW          SMO      FX2  
   9 5*CG          LDXC  0  AWORK2   
  15 5*X6          BCS      RALRI               [BR IF RIGHT 
  18 5BBQ          BRN      RCNWRL              [... AND ERROR IF WRONG  
   6 5BWB    NOTCONSOLE  
  19 5CB2          SAWCEJX  JSCEB(2),T1,R7900   [BR TO R7900 IF 7900 DEVICE  
   9 5CFX ...      LDX   0  JSCEA(2) 
  17 5CKS ...      BNZ   0  NOTMP               [J IF NOT MOP LINE   
   8 5CPP ...      SMO      FX2  
  15 5CTL ...      LDX   0  AWORK2              [RI OR WR?   
  17 5C_H ...      BPZ   0  RCNWR7              [CANNOT BE WRONGED   
  16 5D5D ...      BRN      NORI                [MAY NOT BE RTED 
  18 5D9* ...NOTMP HUNT2    3,AMXOR,ALINES      [X3 TO HOLD ADDS ALINES  
   9 5D*=          LDX   0  JSCEB(2) 
  16 5DSW          SRL   0  7                [LINE NUMBER TIMES 4
   8 5F#G ...      ANDN  0  #174 
   7 5FS6          SMO      0
  15 5G5W ...      LDX   1  A1+BPTR(3)          [POINT TO LCB
  16 5GCL ...      BZE   1  NOPER               [BR IF NOT THERE 
   7 5GJG ...      SMO      0
  11 5GPB ...      LDX   0  A1+FOURTHWD(3)   
   7 5H36 ...      SLL   0  1
   8 5H#W ...      BNG   0  RATS 
   9 5HLL ...      SBN   1  A1+FPTR  
  11 5HYB ...      HUNT2B   3,A7020,INFORM,1 
   7 5J==          STOZ     7
  18 5JPW          LDX   0  JSCEA(2)            [PCT TERMINAL IDENTIFIER 
   7 5K9G          SRL   0  6
   8 5KP6          ANDN  0  #7777
  19 5L86    XFR2  SMO      7                   [FIND,USING THE PCT TERMINAL 
  10 5LNB          LDX   5  IDUNIQUE(3)  
  20 5M82          SRL   5  12                  [IDENTIFIER AN APPROPRIATE ENTRY 
  18 5MML          TXU   0  5                   [IN A7020 INFORM BLOCK   
   8 5N7=          BCC      XFR1 
   9 5NLW          ADN   7  APCTRLEN 
   8 5P6G          BRN      XFR2 
   8 5PL6    XFR1  SMO      FX2  
  21 5Q5Q          STO   7  AWORK3              [STORE REL.POINTER TO A PCT RECORD   
   7 5QKB          ADX   3  7
  21 5R52          LDX   1  3                   [STORE ABS.POINTER TO A PCT RECORD   
   9 5RJL          LDX   5  JSCEA(2) 
  15 5S4=          ANDN  5  #77                 [INTERFACE NO
  19 5SHW          BZE   5  PFERR               [ERROR IF NO INTERFACE NUMBER
   7 5T3G          LDN   6  6
   5 5TH6    RLOOP   
  10 5W2Q          LDX   7  APCTDEV(3)   
  15 5WGB          LDCT  2  #200                [PT TO CHAR1 
   7 5X22          SMO      3
  10 5XFL          LDCH  0  APCTDEV+1(2) 
   9 5X_=          BXE   0  5,RFND72 
   7 5YDW          ADN   3  2
   8 5YYG          BCT   6  RLOOP
   9 5_D6          BRN      RCNWRR   
   5 5_XQ    RFND72  
   7 62CB          SMO      3
  17 62X2          LDCH  7  APCTDEV(2)          [LOADS INT BIT POSN  
   8 63BL          SMO      FX2  
   9 63W=          LDX   6  AWORK2   
  16 64*W          BPZ   6  RMWRG               [BRANCH FOR WRONG
  21 64TG          LDCH  0  APCTD(1)            [LOAD CH SHOWING WHICH INTS RIGHTED  
   7 65*6          ANDX  0  7
  18 65SQ          BNZ   0  RMALRT              [BR IF ALREADY RIGHTED   
  17 66#B          LDX   0  APCTDEV+1(3)        [LOOK AT DEVICE TYPE 
   8 66S2          ANDN  0  #77  
   7 67?L          SBN   0  3
  19 67R=          BZE   0  RMALR               [NO FURTHER ACTION FOR 7023  
   7 68=W          ADN   0  3
   7 68QG          ANDN  0  1
  17 69=6          BZE   0  RMRIN               [BR IF INPUT DEVICE  
   9 69PQ          LDX   0  APCTD(1) 
  20 6=9B          SRL   0  6                   [CH WITH ACT MASK TO RT HAND END 
   7 6=P2          ANDX  0  7
  20 6?8L          BZE   0  RMALR               [NO FURTHER ACTION IF ACTIVITY   
  19 6?N=    #                                  [ALREADY SET UP ON LP OR TP  
  10 6#7W          LDX   6  APCTSTAT(1)  
  21 6#MG          BNG   6  RMALR               [NO FURTHER ACTION IF TERMINAL INOP  
   9 6*76          LDX   6  APCTD(1) 
   8 6*LQ          SRL   6  12   
   7 6B6B          ANDX  6  7
  18 6BL2          BZE   6  RMALR               [.. OR IF INTERFACE INOP 
   9 6C5L          BRN      RMALRA   
   5 6CK=    RMRIN   
   7 6D4W          NGN   0  1
   9 6DJG          BRN      RMALRA   
   5 6F46    RMALRT  
   9 6FHQ          LDX   0  APCTD(1) 
   7 6G3B          SRL   0  6
   7 6GH2          ANDX  0  7
  19 6H2L          BNZ   0  RMALR               [BR IF NO ACT FOR INTERFACE  
   8 6HG=          CALL  6  RHUNT
   8 6H_W          SMO      FX2  
  15 6JFG          STO   1  ACOMMUNE1           [PRESERVE X1 
   8 6J_6          CALL  6  RPCT 
  20 6KDQ          ERS   0  EWAIT(1)            [UNSET WAITING TO BE WRONGED BIT 
   8 6KYB          SMO      FX2  
  15 6LD2          LDX   1  ACOMMUNE1           [RESTORE X1  
   5 6LXL    RMALR   
   7 6MC=          LDN   0  0
   5 6MWW    RMALRA  
   7 6NBG          SRC   7  6
  18 6NW6          ORS   7  APCTD(1)            [MARK INTERFACE RIGHTED  
  18 6P*Q          BZE   0  RMEND               [BR IF ACTLF NOT REQUD   
   4 6PTB    #   
   9 6Q*2          BNG   0  RMRINA   
  12 6QSL    #              ACTLF SET UP BELOW   
   8 6R#=          SRL   7  12   
  21 6RRW          ERS   7  APCTD(1)            [INDICATE ACT SET UP FOR INTERFACE   
  10 6S?G          LDX   5  IDUNIQUE(1)  
  10 6SR6          LDX   7  APCTDEV(3)   
  10 6T=Q          LDX   6  APCTDEV+1(3) 
  18 6TQB          GETACT   ACTLF,GREMOTE       [SET MP A REMOTE ACTLF   
   8 6W=2          CALL  6  RHUNT
   8 6WPL          SMO      FX2  
   9 6X9=          LDX   0  AWORK3   
  20 6XNW          DSA   0  7                   [X7 HAS FORM OF REQD APCTID WORD 
   8 6Y8G          SMO      FX2  
  18 6YN6          LDX   6  ACC6                [RESTORE X6 TO PRE-GETACT
   8 6_7Q          SMO      FX2  
  16 6_MB          LDX   3  BPTR                [POINT TO ACTLF  
  19 7272          STO   5  JSOURCE1(3)         [JSOURCE1 FOR JOHN WRATTEN   
   8 72LL          SMO      FX2  
  15 736=          LDX   3  AWORK1              [AMOP ADDRESS
  11 73KW          HUNT2    3,AMXOR,ALINES   
  14 745G          LDX   0  JSCEB(2)            [LINE NO 
   7 74K6          SRL   0  9
   8 754Q          ANDN  0  #77  
   7 75JB          SLL   0  2
   7 7642          SMO      0
  10 76HL          LDX   3  A1+BPTR(3)   
  16 773=          SBN   3  A1+FPTR             [X3 POINTS TO LCB
  17 77GW          HUNT2B   3,A7020,INFORM      [BACK TO INFORM BLOCK
   5 782G    RHNTF   
  17 78G6          LDX   3  BPTR(3)             [HUNT BACK FOR ACT   
   9 78_Q          LDX   0  ATYPE(3) 
  10 79FB          BXL   0  CACT,RHNTF   
  10 79_2    #              ACT FOUND.   
   8 7=DL          LDX   2  FX2  
  16 7=Y=          LDX   1  BPTR(2)             [POINT TO NEW ACT
  18 7?CW          ENRING   JOBRING(1),JOBRING(3) [RING INTO TEMP RING   
   9 7?XG          LDX   2  BPTR(2)  
   9 7#C6          STO   6  ACC4(2)  
   9 7#WQ          STO   7  ACC5(2)  
  17 7*BB          LINKSET  2,RLINK(1)          [SET LINK TO PCTSTART
  17 7*W2          FPUT                         [PUT NEW ACT ON LIST 
   8 7B*L          BRN      RMEND
   5 7BT=    RMRINA  
   8 7C#W          LONGON   #60  
   5 7CSG    RMEND   
   8 7D#6          LDX   1  FX1  
   8 7DRQ          BRN      RALRI
   4 7F?B    #   
  17 7FR2    RMWRG                              [WRONGS 7020 DEVICE  
  10 7G=L          LDX   0  APCTDEV+1(3) 
   8 7GQ=          ANDN  0  #77  
   7 7H9W          SBN   0  3
  17 7HPG          BZE   0  RCNWRR              [CAN'T WRONG A 7023  
   9 7J96          LDX   0  APCTD(1) 
  19 7JNQ          SRL   0  6                   [CH WITH ACT MASK TO RH END  
   7 7K8B          ANDX  0  7
  20 7KN2          BZE   0  RWTBR               [BR IF THERE IS AN ACT FOR DEVICE
   7 7L7L          SRC   7  6
   9 7LM=          ORS   7  APCTD(1) 
  18 7M6W          ERS   7  APCTD(1)            [MARK INTERFACE WRONGED  
   8 7MLG          LDX   1  FX1  
   8 7N66          BRN      XDWUA
   4 7NKQ    #   
   5 7P5B    RWTBR   
   8 7PK2          CALL  6  RHUNT
  21 7Q4L          CALL  6  RPCT                [IF ALLOCATED MARK WAITING TO BE WRD 
   8 7QJ=          LDX   1  FX1  
   9 7R3W          BRN      RWTWMESS 
   4 7RHG    #   
  17 7S36    R7900                              [RI & WR 7900 DEVICES
   5 7SGQ    RHNTA   
  17 7T2B          LDX   3  FPTR(3)             [HUNT FOR ADEVS BLOCK
   9 7TG2          LDX   0  ATYPE(3) 
   6 7THP ...#UNS ISUMBI 
   4 7TKD ...(   
   8 7TM7 ...      SMO      FX2  
   9 7TNW ...      LDX   6  AWORK2   
  18 7TQK ...      BXGE  0  CACT,R79MPDOFF      [NO ADEVS : NOT MOPPED ON
   4 7TS# ...)   
   6 7TW3 ...#UNS ISUMBI 
   4 7TXQ ...#SKI
  19 7T_L          BXGE  0  CACT,NOTMOPPEDON    [BR IF IPB MOPPED OFF OR INOP
   8 7WF=          SRL   0  12   
  10 7WYW          SBN   0  AMXOR+ADEVS  
   8 7XDG          BNZ   0  RHNTA
  13 7XY6    #                  X3 POINTS TO ADEVS   
  16 7YCQ          LDX   0  JSCEA(2)            [IPB IDENTIFIER  
   8 7YXB          ANDN  0  #7777
  11 7_C2          SBX   0  A1+FOURTHWD(3)   
   7 7_WL          SLL   0  2
  20 82B=          ADX   3  0                   [X3 POINTS TO ADEVS ENTRY FOR IDE
   7 82C? ...      LDN   0  1
   8 82D# ...      SLL   0  12   
  16 82F* ...      ANDX  0  A1+FOURTHWD(3)      [MASK ON TEST BIT
  15 82GB ...      BNZ   0  RTEST               [J IF ON TEST
  17 82HC ...      LDN   0  #2100               [MASK ATTACHED BITS  
   8 82JG ...      SLL   0  12   
  11 82ML ...      ANDX  0  A1+FOURTHWD(3)   
  16 82QQ ...      BNZ   0  RATS                [J IF ATTACHED   
   6 82R2 ...#UNS ISUMBI 
   4 82R= ...(   
  11 82RG ...      LDX   0  A1+FOURTHWD(3)   
   8 82RQ ...      ANDN  0  #17  
  15 82S2 ...      BZE   0  R79MOP              [MOP CONSOLE 
   8 82S= ...      SBN   0  10   
  15 82SG ...      BZE   0  R79MOP              [MOP DISPLAY 
   4 82SQ ...)   
   6 82T2 ...#UNS ISUMBI 
   4 82T= ...#SKI
   4 82TG ...(   
   8 82TW          SMO      FX2  
   9 83*G          LDX   6  AWORK2   
   4 83K# ...)   
  18 83T6          BPZ   6  R79WR               [BR IF ENTRY FOR WRONG   
  10 84#Q          LDX   1  A1+BPTR(3)   
  17 84SB          BZE   1  R79FR               [BR IF NOT ALLOCATED 
   9 85#2          SBN   1  A1+FPTR  
  16 85RL          NGN   0  #1001               [LOADS #77776777 
  19 86?=          ANDS  0  IWAIT(1)            [UNSET WAITING TO BE WRONGED 
   5 86QW    R79FR   
  14 87=G          LDCT  0  #2                  [BIT 7   
  11 87Q6          ANDX  0  A1+FOURTHWD(3)   
  18 889Q          BZE   0  R7ALR               [BR IF ALREADY RIGHTED   
  17 88PB          ERS   0  A1+FOURTHWD(3)      [UNSET WRONGED BIT   
  11 8992          LDX   0  A1+FOURTHWD(3)   
   8 89G5 ...      ANDN  0  #17  
   6 89R9 ...#UNS ISUMBI 
   4 89TS ...#SKI
  17 89YD ...      BZE   0  NORI                [MOP-MAY NOT BE RTED 
   7 8=8=          SBN   0  3
  14 8=MW          BZE   0  R79OT               [BR IF LP
   7 8?7G          SBN   0  1
  13 8?M6          BZE   0  R79IN               [ CR 
   7 8#6Q          SBN   0  1
  17 8#LB ...      BZE   0  R79TW              [ T/W,ALWAYS RIGHTED  
   7 8*62          SBN   0  1
  13 8*KL          BZE   0  R79IN               [ TR 
   7 8B5=          SBN   0  1
  16 8BJW          BNZ   0  R7ALR               [ IGNOR IF NOT TP
  16 8C4G    R79OT                              [OUTPUT DEVICES  
  10 8CJ6          LDX   0  A1+BPTR(3)   
  15 8D3Q          BNZ   0  R79ALR              [BR IF RINGED
  18 8DHB    #                                  [OTHERWISE SET UP ACTLF  
   9 8F32          DOWN     IPBINB,4 
   9 8FGL          BRN      R79ALR   
   5 8FLH ...R79TW   
  21 8FQD ...      DOWN     IPBINB,20            [SETUP ACTIVITY FOR INITIALISATION  
   9 8FW* ...      BRN      R79ALR   
   5 8G2=    R79IN   
  18 8G67 ...      LDX   0  A1+FOURTHWD(3)          [DONT SEND 1-SHOT..  
  18 8G=4 ...      SLL   0  4                   [..INPUT IF ACTIVITY..   
  18 8G*_ ...      BNG   0  NOCCO               [..ALREADY BEING SET UP  
   8 8GFW          LDN   0  #122 
   9 8G_G          CALL  7  ROUTSS   
   8 8HF6 ...NOCCO LONGON   #60  
   5 8HYQ    R7ALR   
   5 8JDB    R79ALR  
   8 8JY2          LDX   1  FX1  
   8 8KCL          BRN      RALRI
   6 8KCY ...#UNS ISUMBI 
   4 8KD= ...(   
   5 8KDJ ...R79MOP  
   8 8KDW ...      LDCT  0  #121 
  11 8KF8 ...      ANDX  0  A1+FOURTHWD(3)   
  10 8KFG ...      ORX   0  A1+BPTR(3)   
  21 8KFS ...      BNZ   0  R79MOPBUSY          [ATTACHING,INITIALISING,MOPPINGOFF OR
  20 8KG6 ...                                   [RINGED : REPORT IDENTIFIER BUSY 
   7 8KGD ...      LDCT  5  2
  17 8KGQ ...      ORN   5  #40                 [WRONGED BITS(7&18)  
  16 8KH4 ...      BPZ   6  R79WRMOP            [WRONG COMMAND   
   5 8KHB ...R79RIMOP
  11 8KHN ...      LDX   0  A1+FOURTHWD(3)   
   7 8KJ2 ...      SRC   0  6
  17 8KJ# ...      BPZ   0  R79ALR              [ALREADY RIGHTED(B18)
   7 8KJL ...      SRC   0  9
  20 8KJY ...      BPZ   0  R79MOPBUSY          [NOT YET CLOSED(B9) : REPORT BUSY
  11 8KK= ...      ORS   5  A1+FOURTHWD(3)   
  17 8KKJ ...      ERS   5  A1+FOURTHWD(3)      [MARK RIGHTED IN CORE
  14 8KKW ...      LDX   7  JSCEA(2)            [ID NO   
  15 8KL8 ...      LDN   3  7                   [PTR TO ID NO
  15 8KLG ...      LDX   1  JSCEP(2)            [DEV LIST PTR
  17 8KLS ...      LAMOP    1,4                 [AMOP ADDR INTO X4   
  16 8KM6 ...      DOWN  IPBINB,20              [TO INITIALISE   
   6 8KMD ...R79MPDOFF   
  16 8KMQ ...      BPZ   6  R79WIDF             [WRONG COMMAND   
   5 8KN4 ...R79RIDF 
  17 8KNB ...      CALL  6  RHUNT               [RE-FIND JSCE BLOCK  
  21 8KNN ...      IRIGHT   YUK,NOTMOPPEDON,JSCEP(2),JSCEA(2)[MARK RIGHTED IN IDF,   
  17 8KP2 ...                                   [IF MOP IDENTIFIER   
   8 8KP# ...      BRN      RALRI
   5 8KPL ...R79WRMOP
  11 8KPY ...      LDX   0  A1+FOURTHWD(3)   
   7 8KQ= ...      SRC   0  6
  17 8KQJ ...      BNG   0  RNTR                [ALREADY WRONGED(B18)
   6 8KQL ...#UNS  ATELE 
   4 8KQN ...#SKI
   4 8KQQ ...(   
   7 8KQW ...      SLC   0  1
  15 8KR8 ...      BNG   0  RCNWR7              [PSEUDO VDU  
   4 8KR* ...)   
  17 8KRG ...      ORS   5  A1+FOURTHWD(3)      [MARK WRONGED IN CORE
   6 8KRJ ...#UNS  ATELE 
   8 8KRL ...      SLC   0  15   
   6 8KRN ...#UNS  ATELE 
   4 8KRQ ...#SKI
   8 8KRS ...      SLC   0  14   
  17 8KS6 ...      BNG   0  R79WIDF             [ALREADY CLOSED(B9)  
   8 8KSD ...      LDN   0  #227 
  16 8KSQ ...      CALL  7  ROUTSS              [SEND CRASH CLOSE
   5 8KT4 ...R79WIDF 
  17 8KTB ...      CALL  6  RHUNT               [RE-FIND JSCE BLOCK  
  21 8KTN ...      IWRONG   YUK,NOTMOPPEDON,JSCEP(2),JSCEA(2)[MARK WRONGED IN IDF,   
  17 8KW2 ...                                   [IF MOP IDENTIFIER   
   8 8KW# ...      BRN      XDWUA
   4 8KWL ...)   
  18 8KX=    R79WR                              [WRONGS 7900 IDENTIFIER  
  11 8LBW          LDX   0  A1+FOURTHWD(3)   
  15 8LWG          ANDN  0  #77                 [DEVICE TYPE 
   7 8MB6          SBN   0  3
  20 8MTQ ...      BNG   0  RCNWR7              [CANT WRONG IF NOT LP,CR,TR OR TP
  16 8N*B          SBN   0  2                   [ TYPES 3,4,6,7  
   9 8NT2          BZE   0  RCNWR7   
   7 8P#L          SBN   0  3
   9 8PS=          BPZ   0  RCNWR7   
  10 8Q?W          LDX   1  A1+BPTR(3)   
  17 8QRG          BZE   1  R7NR                [BR IF NOT ALLOCATED 
   8 8R?6          LDN   0  #1000
  19 8RQQ          ORS   0  IWAIT-A1-FPTR(1)    [MARK WAITING TO BE WRONGED  
   8 8S=B          LDX   1  FX1  
   8 8SQ2          BRN   RWTWMESS
   4 8T9L    #   
  17 8TP=    R7NR                               [IDE NOT ALLOCATED   
   8 8W8W          LDCT  0  #2   
  15 8WNG          ORS   0  A1+FOURTHWD(3)      [MARK WRONGED
  11 8X86          LDX   0  A1+FOURTHWD(3)   
   8 8XMQ          ANDN  0  #77  
   7 8Y7B          SBN   0  4
  14 8YM2          BZE   0  RCR                 [BR IF CR
   7 8_6L          SBN   0  2
  15 8_L=          BNZ   0  RNTR                [BR IF NOT TR
   4 925W    RCR 
  19 92KG          LDN   0  #124                [STOP INPUT & CLEAR SS CODE  
   9 9356          CALL  7  ROUTSS   
   4 93JQ    RNTR
   8 944B          LDX   1  FX1  
   8 94J2          BRN      XDWUA
   5 953L    RCNWRL  
   5 95H=    RCNWRR  
   5 962W    RCNWR7  
   8 96GG          LDX   1  FX1  
  10 9726          LDX   6  XJCANWR(1)   
   6 97FQ    XERRORMESS  
   9 97_B          CALL  7  TERROR   
   8 98F2          BRN      RRSTT
   6 98YL    NOTMOPPEDON 
   8 99D=          LDX   1  FX1  
   9 99XW          LDX   6  XJRES(1) 
  10 9=32 ...      BRN      XERRORMESS   
   5 9=66 ...XWTBSW  
   8 9=9= ...      LDX   1  FX1  
   9 9=#B ...      LDX   6  XJWTSW(1)
  10 9=CG ...      BRN      XERRORMESS   
   5 9=GL ...XMPON   
   8 9=KQ ...      LDX   1  FX1  
   9 9=NW ...      LDX   6  XJMPON(1)
  10 9=S2 ...      BRN      XERRORMESS   
   4 9=X6    #   
  17 9?BQ    NOPER                              [NOT ON INSTALLATION 
   8 9?WB          LDX   1  FX1  
   9 9#B2          LDX   6  XNOPER(1)
  10 9#TL          BRN      XERRORMESS   
   4 9**=    #   
  18 9*SW    PFERR                              [INTERFACE NUMBER MISSING
   8 9B#G          LDX   1  FX1  
   9 9BS6          LDX   6  XPFERR(1)
  10 9C?Q          BRN      XERRORMESS   
   4 9CRB    #   
   8 9CWT ...NORI  LDX   1  FX1  
   9 9D2# ...      LDX   6  XJMNRI(1)
  10 9D5R ...      BRN      XERRORMESS   
   4 9D9= ...#   
   8 9D#P ...RATS  LDX   1  FX1  
   9 9DD8 ...      LDX   6  XERAT(1) 
  10 9DHM ...      BRN      XERRORMESS   
   4 9DJ* ...#   
   8 9DK3 ...RTEST LDX   1  FX1  
  10 9DKP ...      LDX   6  XERTEST(1)   
  10 9DLC ...      BRN      XERRORMESS   
   4 9DM6 ...#   
   6 9DMG ...#UNS ISUMBI 
   4 9DMW ...(   
   6 9DN= ...R79MOPBUSY  
   8 9DNL ...      LDX   1  FX1  
  10 9DP2 ...      LDX   6  XJIDEBUSY(1) 
  10 9DPB ...      BRN      XERRORMESS   
   4 9DPQ ...#   
   4 9DQ6 ...)   
  10 9DQL          MENDAREA 55,K100RIGHT 
   4 9F==    #END
   8 ____ ...33035005000100000000
