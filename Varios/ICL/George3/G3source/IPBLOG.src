  15 22FL    #SEG  IPBLOG                       [COMM - IPB  
   7 22PD ...#OPT  K0IPBLOG=0
  13 22_=    #LIS  K0IPBLOG>K0IPB>K0ALLGEO>K0GREATGEO
   7 23DW          8HIPBLOG  
  11 23YG          SEGENTRY K1IPBLOG,X1IPBLOG
   4 24D6    [   
  21 24XQ    [     THIS SEGMENT IS ENTERED WHEN DATA HAS BEEN RECEIVED FROM DEVICE   
  21 25CB    [     TYPE 13 (THE ERROR LOGGER).  DATA COMES IN FROM THE ERROR LOGGING 
  21 25X2    [     DEVICE CERTAIN DETAILS ARE ADDED AND THEN THE INFO IS WRITTEN OUT 
  12 26BL    [     TO THE SYSTEM JOURNAL FILE.   
   4 26W=    [   
  11 27TG    XERR              +CCWRONGILL   
  17 282N ...XMASK       #77600000                  [TOP BYTE MASK   
  20 287W ...XFC         #77741703                  [FF IN HEX AND CC IN EBSDIC  
   4 28*6    [   
  21 28SQ    [     SEND SUPERVISORY SEGMENT AND AWAIT REPLY CODE IN X5 REPLY IN X6   
   4 29#B    [   
   5 29S2    SUPER   
   8 2=?L          SBX   7  FX1  
  10 2=R=          MHUNTW   3,AMXOR,ADCB 
  21 2?=W          LDCT  0  #100                [SKIP SUPER IF IDENTIFIER IS CLOSED  
  18 2?QG          ANDX  0  ISTATUS(3)          [MOP OFF OR FINISH NOW   
   8 2#=6          BZE   0  SOUT 
   8 2#PQ          ERN   5  #120 
  19 2*9B          BNZ   5  SEX                 [UNLESS SENDING OPEN SUP SEG 
  14 2*P2          ORX   5  #120                [RESET X5
   7 2B8L    SOUT  LDX   3  5
   7 2BN=          SRL   3  6
  14 2C7W          SETNCORE IPBIDE-A1(3),1,AMXOR,IOUT,1  
  10 2CMG          MHUNTW   3,AMXOR,ADCB 
   8 2D76          LDX   0  GSIGN
  19 2DLQ          SLL   5  12                  [WD CT = 1 + SUPERVISORY CODE
   9 2F6B          STO   0  IBSW(1)  
  10 2FL2          ADX   5  IDENTNO(3)   
  18 2G5L          STOZ     IMOPTY(1)           [DONT WAKE AFTER OUTPUT  
   9 2GK=          STO   5  IPBIDE(1)
  19 2H4W          LDX   2  IADIN(3)            [ADDRESS OF IPBIN ACTIVITY   
  10 2HJG          LDX   2  IPBRING(2)   
  19 2J46          SBN   2  IPBRING             [ADDRESS OF IPBOUT ACTIVITY  
   7 2JHQ          LDX   4  2
  10 2K3B          LDX   2  ACTRING(2)   
   9 2KH2          SBN   2  ACTRING  
   9 2L2L          LDX   2  BPTR(2)  
  19 2LG= ...      CHAIN    1,2                 [PUT ON END OF IPBOUT'S QUEUE
   7 2L_W          LDX   2  4
   8 2MFG          LDCT  0  #100 
  21 2M_6          ANDX  0  IPBSW(2)            [TEST TO SEE IF OUTPUT ACTIVITY ASLEE
   8 2NDQ          BZE   0  WAKAL
   9 2NYB          ERS   0  IPBSW(2) 
   6 2PD2          FPUT  
   7 2PXL    WAKAL LDN   4  0
   9 2QC=          SMO      IADIN(3) 
   8 2QWW          LDXC  0  IPBSW
  18 2RBG          BCS      SCCX                [J. IPB HAS BECOME INOP  
  21 2RW6          LDCT  0  #401                [SET B0 AND B8 TO INDICATE RECEIVE   
  19 2S*Q          ORS   0  IWAIT(3)            [SUPERVISORY INPUT AND ASLEEP
   6 2STB          COOR1 
  10 2T*2          MHUNTW   3,AMXOR,ADCB 
   8 2TSL          LDN   4  11   
  11 2W#=    YSUG  HUNTW    2,AMXOR,IDATSG   
   9 2WRW          BNG   2  WAKALA   
   9 2X?G          LDX   0  A1+1(2)  
   8 2XH# ...      BZE   6  SEX  
  19 2XR6          BNG   0  SSE                 [J IF A SUPERVISORY SEGMENT  
   7 2Y=Q          LDX   0  6
   8 2YQB          SBN   0  52   
  20 2_=2          BNZ   0  NOSSE               [J NOT AWAITING INPUT CLEARED REP
  10 2_PL          NAME     2,AMXOR,#77  
   8 329=          BRN      YSUG 
   5 32NW    WAKALA  
  10 338G          LDX   2  A1+FPTR(3)   
  17 33N6          LDX   0  FOURTHWD(2)         [LD FOURTHWD OF ADEVS
   9 347Q          ORX   0  FINISH   
   7 34MB          SLL   0  2
  19 3572          BPZ   0  WAKAL               [J NOT FINISH OF MOPPING OFF 
   7 35LL          LDX   0  6
   8 366=          SBN   0  54   
  16 36KW          BNG   0  OPOF                [J. CRAH CLOSSING
   9 375G          SMO      IADIN(3) 
  17 37K6          LDX   2  IPBRING             [PTR TO AMOP IPBOUT  
   8 384Q          LDCT  0  #420 
  19 38JB          ANDX  0  IPBSW(3)            [TEST B4 AMOP - MOP OFF,PM   
  17 3942          BZE   0  WAKAL               [J. BIT NOT SET WAIT 
   8 39HL          BRN      SCCX 
   4 3=3=    SSE 
   7 3=GW          BCHX  2  /
  18 3?2G          LDCH  4  A1+2(2)             [PICK UP SUPERVISORY CODE
   9 3?G6          ANDX  2  BITS22LS 
   8 3?_Q          SBN   4  #45  
  17 3#FB          BZE   4  XILL                [J. SUP SEG = ILLEGAL
   8 3#_2          ADN   4  #45  
   7 3*DL          SBX   4  6
   7 3*Y=    NOSSE FREECORE 2
  20 3BCW          BNZ   4  YSUG                [J NOT SUP SEG WE ARE WAITING FOR
  11 3BXG          MHUNTW    2,AMXOR,IDATSG  
   8 3CC6 ...      BPZ   2  NOSSE
   8 3CWQ    SEX   LDX   2  FX2  
   8 3DBB          ADX   7  FX1  
   7 3DW2          EXIT  7  0
   4 3F*L    XILL
   9 3FT=          LDX   5  XERR(1)  
   9 3G#W          LDX   6  A1+2(2)  
   8 3GSG          CCERROR  5,6  
  19 3H#6          BRN      MOPOF               [REPORT DCP PROTOCOL ERROR   
   4 3HRQ    [   
   7 3J?B    [     K1IPBLOG  
   4 3JR2    [   
   5 3K=L    X1IPBLOG
  20 3KQ=          MHUNTW   3,AMXOR,ADCB        [HUNT IDENTIFIER CONTROL BLOCK   
  19 3L9W          LDX   2  A1+FPTR(3)          [PTR. TO AMXOR ADEVS ENTRY   
  18 3LPG          LDX   0  FOURTHWD(2)         [BIT 2 SET IF MOPPING OFF
  19 3M96          ORX   0  FINISH           [BIT 2 IF FINISH NOW REQUIRED   
   7 3MNQ          SLL   0  2
  19 3N8B ...      BNG   0  OPOF                [J MOPPING OFF OR FINISH NOW 
  19 3NN2          LDCT  0  #10                 [TEST IF LINE TERMINAL FAIL  
  18 3P7L          ANDX  0  ISTATUS(3)          [HAS OCCURRED IE BIT 5   
  14 3PM=          ERS   0  ISTATUS(3)         [CLEAR BIT
  17 3Q6W          BNZ   0  TLFAIL              [J FAIL BIT IS SET   
   5 3QLG    START   
  16 3R66          MHUNTW   2,AMXOR,IDATSG      [HUNT DATA BLOCK 
  17 3RKQ          BNG   2  XWAIT               [NO MORE DATA BLOCKS 
  15 3S5B          LDX   6  A1(2)               [LD HEADER WD
  18 3SK2          SLC   6  2                   [TEST BIT 2 = END OF DATA
  17 3T4L          BNG   6  XEND                [J END OF DATA STREAM
   5 3TJ=    XCONT   
   7 3W3W          SRC   6  2
  18 3WHG          ANDN  6  #7777               [MASK OUT COUNT OF CHARS 
   7 3X36 ...      ADN   6  3
  17 3XGQ ...      SRL   6  2                   [CONVERT CHARS TO WDS
  20 3Y2B ...      SBN   6  2                    [REMOVE SEG. HEADER AND IDENT   
   9 3YG2 ...      SMO      IADIN(3) 
   8 3Y_L ...      LDX   7  CPPTR
  17 3_F= ...      SETELOG  3,6,7               [CREATE ADATA ELOUT  
   9 3_YW ...      LDN   4  A1+2(2)  
  18 42DG ...      LDN   5  A1+11(3)            [LD, PTRS. FOR DATA MOVE 
   7 42Y6 ...      SMO      6
  18 43CQ ...      MOVE  4  0                   [MOVE ERROR LOG RECORDS  
  21 43XB ...      FREECORE 2                    [FREE ADATA IDATSG - NO LONGER REQUD
  10 44C2 ...      MHUNTW   3,ADATA,ELOG 
   8 44WL ...      LDCT  0  #014 
  17 45B= ...      STO   0  A1+11(3)            [ST UNIT CLASS = 6   
   9 45TW ...      LDX   6  XMASK(1) 
  21 46*G ...      ANDX  6  A1+17(3)            [MASK OUT BYTE 1 OF DEVICE IDENTITY  
   9 46T6 ...      STOZ     A1+16(3) 
   9 47#Q ...      STOZ     A1+17(3) 
   9 47SB ...      LDX   5  XFC(1)   
  20 48#2 ...      SLL   56 8                    [GET INFO INTO THE RIGHT BITS   
  17 48RL ...      STO   5  A1+16(3)              [FF IN END OF ROUTE
  19 49?= ...      STO   6  A1+17(3)            [DEVICE MNEMONIC = CC LINE NO
   9 49QW ...      LDX   2  BELOGACT 
  10 4==G ...      LDX   2  ACTRING(2)   
   9 4=JX ...      SBN   2  ACTRING  
   9 4=X# ...      LDX   2  BPTR(2)  
  21 4?9Q ...      CHAIN     3,2                 [CHAIN ADATA ELOUT BLOCK AT END OF  
  16 4?PB ...                                     [BSA ELOG CHAIN
  21 4#92 ...      STARTACT  ELOG                         [START BSA ELOG ACTIVITY TO
  14 4#NL ...                                   [MESSAGE 
  20 4*8= ...      BRN      X1IPBLOG             [RETURN TO DEAL WITH NEXT BLOCK 
   4 4DKL    [   
  15 4F5=    [     MOP OFF OR FINISH NOW   X3 = AMXOR ADCB   
   4 4FJW    [   
  10 4G4G    MOPOF MHUNTW  3,AMXOR,ADCB  
  19 4GJ6    OPOF  LDCT  0  4                [B6 SET IF END OF DATA STREAM   
  10 4H3Q          ANDX  0  ISTATUS(3)   
  17 4HHB          BNZ   0  XEDS               [J CRASH CLOSE SENT   
  18 4J32          SMO      IADIN(3)            [USING PTR TO AMOP@IPBIN 
  17 4JGL          LDX   2  IPBRING            [LD PTR TO AMOP IPBOUT
   8 4K2=          LDCT  0  #420 
  19 4KFW          ANDX  0  IPBSW-IPBRING(2)    [B0 : INOP B4 : MOP OFF,PM   
  19 4K_G          BZE   0  SCC                 [BRANCH TO SEND CRASH CLOSE  
   4 4LF6    SCCX
   8 4LYQ          LDCT  0  #100 
  19 4MDB          ORS   0  ISTATUS(3)          [SIMULATE IDENTIFIER CLOSED  
   8 4MY2          BRN      XEDS 
   5 4NCL    TLFAIL  
   8 4NG9 ...      LDN   5  #124 
   8 4NJS ...      LDN   6  52   
  18 4NMC ...      CALL  7  SUPER                   [STOP INPUT AND CLEAR
  10 4NQ2 ...      MHUNTW     3,AMXOR,#77
  19 4NSK ...      BPZ   3  RENAM             [DATA BEING CLEARED OUT OF FEP 
   4 4NX=    SCC 
   8 4PBW          LDN   5  #127 
   8 4PWG          LDN   6  54   
  16 4QB6          CALL  7  SUPER               [SEND CRASH CLOSE
  11 4QC# ...      MHUNTW     3,AMXOR,ADCB   
  10 4QDG ...      LDX   2  A1+FPTR(3)   
  10 4QFN ...      LDX   0  FOURTHWD(2)  
   7 4QGW ...      SLL   0 2 
  19 4QJ4 ...      BNG   0  XEDS               [J. MOPPPING OFF OR FINISH NOW
   8 4QK= ...      LDN   5  #120 
   8 4QLD ...      LDN   6  48   
   8 4QML ...      CALL  7  SUPER
   8 4QNS ...      LDN   5  #121 
   7 4QQ2 ...      LDN   6  0
  17 4QR8 ...      CALL  7  SUPER               [START INPUT NO REPLY
   8 4QSB ...      BRN      START
   4 4QTQ    XEDS
  20 4R*B          MOPCLOSE                     [GET RID OF ADCB BLOCK AND RING  
   7 4RT2          SUICIDE   
   4 4S#L    XEND
   7 4SS=          LDCT  0  4
  10 4T?W          ORS   0  ISTATUS(3)   
   8 4TRG          BRN      XCONT
  11 4W?6    [     WAIT DOR MORE INPUT BLOCKS
   5 4WQQ    XWAIT   
  10 4X=B          MHUNTW   3,AMXOR,ADCB 
   9 4XQ2          LDX   0  FINISH   
  16 4Y9L          BNZ   0  OPOF                [J IF FINISHING  
  16 4YP=          LDCT  0  4                   [B6 OF IDSTAUT   
  10 4_8W          ANDX  0  ISTATUS(3)   
  19 4_NG          BNZ   0  XENDX               [END OF DATA STREAM INDICATED
  20 5286          LDCT  4  #450                [IF IDENTIFIER IS INOPERABLE OR  
  20 52MQ          ANDX  4  ISTATUS(3)          [STPPED BY STOP INPUT AND CLEAR  
  16 537B          BNZ   4  TINOP               [J TO INOP PATH  
  20 53M2          LDCT  0  #400                [WAKE UP IF UNAVAILABLE RECEIVED 
  19 546L          ADN   0  #2000               [INDICATE 'ACTIVITY ASLEEP'  
   9 54L=          ORS   0  IWAIT(3) 
   6 555W          COOR1 
  19 55KG          BRN      X1IPBLOG            [J TO START DEALING WITH DATA
  17 5656    TINOP                              [X4 CONTAINS ISTATUS 
   7 56JQ          SLC   4  5
  21 574B          BNG   4  TLFAIL              [B5 SET IF LINE OR TERMINAL FAILURE  
   5 57J2    XENDX   
   7 583L          SRC   4  2
  21 58H=          BNG   4  SCC                 [B3 SET STOP INPUT AND CLEAR ALREADY 
  19 592W                                       [SEND SO GO TO CLOSE IDENT   
  19 59GG          LDN   5  #124                [SEND 'STOP INPUT AND CLEAR' 
   8 5=26          LDN   6  52   
   8 5=FQ          CALL  7  SUPER
   8 5=_B          LDCT  0  #40  
  18 5?F2          ORS   0  ISTATUS(3)          [SET IDENT STOPPED FLAG  
  16 5?YL          HUNTW    3,AMXOR,#77         [ANY DATA CLEARED
   8 5#D=          BNG   3  SCC  
   5 5#XW    RENAM   
  11 5*CG          NAME     3,AMXOR,IDATSG   
  10 5*X6          HUNTW    3,AMXOR,#77  
   8 5BBQ          BPZ   3  RENAM
  19 5BWB          BRN      START               [J BACK TO SERVICE DATA BLOCK
   4 5CB2    #END
   6 ____ ...406300760005
