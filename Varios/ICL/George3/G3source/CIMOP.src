  17 22FL    #SEG  CIMOP8                       [G. CONSTANTINIDES   
  13 22PD ...# COPYRIGHT INTERNATIONAL COMPUTERS 1982
   9 22_=    #OPT           K0CIMOP=0
   9 23DW    #OPT           K6CIMOP=0
  14 23YG    #LIS           K0CIMOP>K0ALLGEO>K0COMMAND   
   9 24D6                   8HCIMOP8 
   7 24XQ    # ENTRY POINTS  
  11 25CB          SEGENTRY K1CIMOP,Z1CIMOP  
  11 25X2          SEGENTRY K2CIMOP,Z2CIMOP  
  11 26BL          SEGENTRY K3CIMOP,Z3CIMOP  
  11 26W=          SEGENTRY K4CIMOP,Z4CIMOP  
  11 27*W          SEGENTRY K5CIMOP,Z5CIMOP  
  11 27TG          SEGENTRY K7CIMOP,Z7CIMOP  
  10 28*6          MENDAREA 50,K100CIMOP 
   4 28F3 ...#   
  19 28JY ...#     SEGENTRY K25CIMOP IS USED BY RESTORE TIME MACRO PRESETRP  
   4 28NT ...#   
  19 28SQ    #                              THIS  SEGMENT  DEALS  WITH  THE  
  21 29#B    #     COMMAND  ISSUER  EXTRACODES,  SO  THAT  THE  EFFECT  OF  A  MOP   
  10 29S2    #     CONSOLE  IS  SIMULATED
   9 2=?L    XMASK          #77777   
   9 2=R=                   #37777777
   9 2?=W                   #777777  
   9 2?QG                   #37577777
   9 2#=6                   #77577777
   9 2#PQ                   #71777777
   8 2*9B    XMODE          +11  
   8 2*P2                   +12  
   8 2B8L                   +13  
   9 2BN=    ZJYMODE        +JYMODE  
  10 2C7W    ZJYRESVIOL     +JYRESVIOL   
   9 2CMG    ZILLINS        +JYILLINS
   9 2D76    ZJYCHAR        +JYCHAR  
   8 2DLQ    ZCOUNT         +512 
   9 2F6B    ZJYCOUNT       +JYCOUNT 
   9 2FL2    PAIR1 PAIR     MORNING,2
   9 2G5L    PAIR2 PAIR     CIMOP,2  
   9 2GK=    PAIR3 PAIR     CIMOP,3  
   9 2H4W    PAIR4 PAIR     LOGOUT,7 
   9 2HJG    PAIR5 PAIR     CIMOP,1  
   9 2J46    PAIR7 PAIR     CIMOP,7  
   9 2JHQ    PAIR8 PAIR     CIMOP,5  
   9 2K3B    XCONTEXT       #20412   
   9 2K?8 ...      SEGENTRY K25CIMOP 
   9 2KH2    XAMXMSK        +AMXMSKPR
  21 2L2L    #     THIS IS THE ENTRY POINT FOR THE FIRST PERI ISSUED BY  A  PROGRAM  
  21 2LG=    #     THE PERI MUST BE MODE 13 & A CPA IS SET UP TO PROCESS THE COMMANDS
  11 2L_W    #     ISSUED BY SUBSEQUENT PERIS
   4 2MFG    #   
   5 2M_6    Z1CIMOP 
  14 2NDQ          LDX   7  APETADDR(2)         [PCA ADDR
   7 2NYB          LDX   3  7
  15 2PD2          LDCH  5  CINUM(2)            [CHANNEL NO  
  10 2PXL          LOCKCA   3,EVENT2(2),4
   7 2QC=          LDX   3  4
  15 2QWW          LDX   0  0(3)                [EXTRACT MODE
   9 2RBG          ANDX  0  XMASK(1) 
  17 2RW6          TXU   0  XMODE+2(1)          [ERROR UNLESS MODE 13
   9 2S*Q          BCS      XILLMODE1
   9 2STB          LDX   3  EVENT2(2)
  12 2T*2          ADN   3  1                   [
   9 2TSL          LDX   1  EVENT1(2)
  21 2W#=          LDCH  6  ARM1-AFW1(1)        [MEMBER NO THAT STARTED UP THE CHANL 
   8 2WRW          LDX   1  FX1  
   7 2X?G          LDX   2  7
  10 2XR6          FADDRESS 2,3,HLOCK1   
  16 2Y=Q          STOZ     0(3)                [SET REPLY WORD  
   8 2YQB          SMO      FX2  
  14 2_=2          LDX   4  ACTNUM              [OLPA'S  
  19 2_PL          TOPCA    PAIR3(1)            [PUT PCA ON LIST,LINK K3CIMOP
  17 329=          COOR1Y                       [PUT OLPA TO SLEEP   
   9 32NW          CALL  0  ZFINDLCB 
  17 338G          BNG   3  K1CIMOP             [J IF LCB NOT PRGSENT
   9 33N6          BRN      Z2ENTRY  
  20 347Q    #     THIS IS THE CONTINUATION OF THE ROUTINE WHICH DEALS WITH AN   
  16 34MB    #     INITIAL MODE 13 PERI IT SETS UP A CPA & LCB   
   4 3572    #   
   5 35LL    Z3CIMOP 
   6 366=          UNPLUG
  16 36KW          CAPCA                        [CHANGE TO CPA   
   8 375G          GETACT   CPAT 
  16 37K6          STO   6  AWORK1(2)           [SAVE MEMBER NO  
   9 384Q          LDX   2  BPTR(2)  
  15 38JB          STO   5  JSOURCE2(2)         [CHANNEL NO  
  17 3942          LINKSET  2,PAIR1(1)          [LINK TO K2MORNING   
  10 39HL          LDX   5  XCONTEXT(1)  
  10 3=3=          STO   5  CONTEXT(2)   
  10 3=GW          LDX   5  XAMXMSK(1)   
   9 3?2G          STO   5  AMXMSK(2)
   8 3?G6          LDN   5  60   
  10 3?_Q          DCH   5  JSOURCE1(2)  
   8 3#FB          SMO      FX2  
   9 3#_2          LDX   0  JSOURCE3 
  17 3*DL          STO   0  JSOURCE3(2)         [HAVE SAME CONS PROP 
  17 3*Y=          HUNTW    3,APERI,ASSOCS      [ANY ASSOCIATIONS?   
  14 3BCW          BNG   3  ZZ1                 [J IF NOT
  14 3BXG          SETUPCORE ALOGLEN(3),3,APERI,ADUMMY   
  10 3CC6          MHUNTW 2,APERI,ASSOCS 
   8 3CWQ          LDN   0  A1(2)
   8 3DBB          LDN   1  A1(3)
  10 3DW2          SMO      ALOGLEN(2)   
  19 3F*L          MOVE  0  0                   [MAKE A COPY OF THE ASSOCS   
   8 3FT=          LDX   2  FX2  
  10 3G#W          NAME    3,APERI,ASSOCS
  19 3GSG          CHAIN   3,BPTR(2)            [AND CHAIN IT TO THE NEW CPA 
  13 3H#6    ZZ1   SETNCORE CILCBLEN,3,AMXOR,CILCB,,1
   8 3HRQ          FINDACTNO 2,4 
  13 3J?B          RINGTWO  CIFPTR(3),BWORKRING(2)   
   8 3JR2          LDX   2  FX2  
   9 3K=L          LDX   4  JOBNO(2) 
  10 3KQ=          STO   4  CIJOBNUM(3)  
  10 3L9W          STOZ     CIREADY(3)   
  15 3LPG          LDX   0  AWORK1(2)           [MEMBER NO   
  16 3M96          STO   0  CIMEMBER(3)         [STORE IN CILCB  
  17 3MNQ          CHAIN    3,BPTR(2)           [CHAIN LCB AFTER CPA 
  17 3N8B          HUNTACT  CPAT,2              [HUNT BACK FOR CPAT  
  15 3NN2          FPUT                         [ACTIVATE CPA
  16 3P7L          RUNPROG                      [RESTART PROGRAM 
  21 3PM=    #     THIS SECTION SETS A SPECIFIED REPLY IN THE PROGRAM'S CONTROL AREA 
  20 3Q6W    #     AND RESTARTS THE PROGRAM IT MUST ONLY BE ENTERED IN THE AOLPA 
   4 3QLG    #   
  19 3R66    Z41   LDCT  6  #420                [REJECTED BUSY REPLY:B0=B4=1 
  16 3RKQ    Z40   LINKSET  FX2,PAIR7(1)        [LINK TO K7CIMOP 
  15 3S5B    Z42   LDX   2  7                   [PCA ADDRESS 
   8 3SK2          SMO      FX2  
   9 3T4L          LDX   3  EVENT2   
  19 3TJ=          ADN   3  1                   [O/P ADDRESS OF REPLY WORD   
  19 3W3W          FADDRESS 2,3,HLOCK1          [CONVERT TO GEORGE ADDRESS   
   8 3WHG          STO   6  0(3) 
  18 3X36          OLPFINX  7                   [RESTART PROG-D LINK SET 
  19 3XGQ    #     THIS IS THE ENTRY POINT FOR THE SECOND & SUBSEQUENT PERIS 
  21 3Y2B    #     THE FIRST SECTION PICKS UP THE CONTROL AREA & ANALYSES THE MODE   
  18 3YG2    #     IF IT IS BREAKIN (MODE 13) WE SWITCH TO DEAL WITH IT; 
  21 3Y_L    #     IF IT IS A TRANSFER PERI-MODE 11& 12 THE COUNT & BUFFER ADDRESS   
   8 3_F=    #     ARE CHECKED   
   4 3_YW    #   
   5 42DG    Z7CIMOP 
   5 42Y6    Z2ENTRY 
  14 43CQ          LDX   1  EVENT1(2)           [MEL PTR 
  19 43XB          LDCH  0  ARM1-AFW1(1)        [MEMBER THAT ISSUED THIS PERI
   8 44C2          LDX   1  FX1  
  16 44WL          STO   0  ACOMMUNE1(2)        [SAVE MEMBER NO  
  10 45B=          LDX   7  APETADDR(2)  
   7 45TW          LDX   3  7
  17 46*G          LOCKCA   3,EVENT2(2)         [CHECK CONTROL AREA  
   9 46T6          LDX   4  EVENT2(2)
   9 47#Q          LDN   5  AWORK1(2)
   7 47SB          LDN   6  4
  18 48#2          FOUTMOVE 3,4,5,6,HLOCK1      [MOVE OUT CONTROL AREA   
  15 48RL          CALL  0  ZFINDLCB            [FIND CI LCB 
  17 49?=          LDEX  0  AWORK1(2)           [EXTRACT BASIC MODE  
  16 49QW          TXU   0  XMODE+2(1)          [JUMP IF MODE 13 
   8 4==G          BCC      N10  
  17 4=Q6          BNG   3  REJINOP             [SET INOPERABLE REPLY
  10 4?9Q          LDX   4  ACOMMUNE1(2) 
  15 4?PB          STO   4  CIMEMBER(3)         [MEMBER NO   
  13 4#92          LDX   4  0                   [MODE
  10 4#NL          LDX   3  CIREADY(3)   
  21 4*8=          SLL   3  7                   [B7 IS 1 IF READY WAS SET IN PROGRAM 
  18 4*MW          LDXC  3  3                   [S LAST PERI REPLY WORD  
   7 4B7G          ADN   4  0
  20 4BM6          TXU   4  XMODE+1(1)          [JUMP IF ILLEGAL MODE.MODE 11 IS 
  21 4C6Q          BCS      XILLMODE            [ONLY LEGAL IF READY IS SET & MODE   
  20 4CLB                                       [12 IS ONLY LEGAL IF IT IS NOT   
  21 4D62          LDN   4  CHREAD              [SET T X4 TO CONTAIN TYPE OF ACCESS  
  21 4DKL          TXU   0  XMODE(1)            [REQUIRED FOR BUFFER:READ ACCESS IF  
  20 4F5=          BCC      Z24                 [MODE 11,WRITE ACCESS IF MODE 12 
   9 4FJW          LDN   4  CHWRITE  
  14 4G4G    Z24   LDX   6  AWORK3(2)           [COUNT   
  17 4GJ6          BZE   6  XCOUNT              [ZERO COUNT ILLEGAL  
   9 4H3Q          TXL   6  ZCOUNT(1)
  21 4HHB          BCC      XCOUNT              [COUNT>511 ILLEGAL-INCLUDE -VE COUNT 
  18 4J32          ADN   6  3                   [CONVERT COUNT TO WORDS  
   7 4JGL          SRL   6  2
   8 4K2=          LDCT  5  #600 
   9 4KFW          ANDX  5  AWORK4(2)
   8 4K_G          BNZ   5  XCHAR
   9 4LF6          LDX   5  AWORK4(2)
   8 4LYQ          PMODES   3,7  
   8 4MDB          ANDN  3  #10  
   8 4MY2          BNZ   3  Z23A 
  20 4NCL          ANDX  5  XMASK(1)            [MASK IF 15 BIT ADDRESSING MODE  
  21 4NX=    Z23A  CHECKB   5,6,RESV2,7,4,Z25   [CHECK BUFFER IS WITHIN RESERVATIONS 
   4 4PBW    Z25 
   9 4PWG          SBN   4  CHREAD   
  15 4QB6          CALL  0  ZFINDLCB            [LOCATE LCB  
   9 4QTQ          BNG   3  REJINOP  
  16 4R*B          BZE   4  N20                 [JUMP IF MODE 11 
  18 4RT2    #     THIS SECTION DEALS WITH MODE 12 PERI I.E.READ PERIS   
  21 4S#L    #     IF INTERLOCK BIT 1 OF CIREADY IN THE LCB IS SET THEN THERE IS DATA
  17 4SS=    #     WAITING IN THE LCB & IT IS MOVED INTO THE PROGRAM.
  21 4T?W    #     IF THE BIT IS CLEAR NO DATA IS WAITING & THE PERI IS REJECTED-BUSY
   4 4TRG    #   
  21 4W?6          LDCT  0  #200                [TEST IF DATA READY-B1 OF CIREADY SET
  20 4WQQ          ANDX  0  CIREADY(3)          [(IT COULD JUST WORTHWHILE MAKING
  21 4X=B          BZE   0  Z41                 [ THIS TEST BEFORE THE CHECKB SINCE  
  21 4XQ2                                       [THE PERI WILL OFTEN BE REJECTED.)   
  17 4Y9L          LDX   6  CIREADY(3)          [NO OF CHARS IN LCB  
   8 4YP=          ANDN  6  #7777
  18 4_8W          LDX   0  AWORK3(2)           [COUNT FROM CONTROL AREA 
   7 4_NG          TXL   0  6
   8 5286          BCC      Z37  
  20 52MQ          LDX   6  0                   [SET X6 TO SMALLER OF TWO COUNTS 
  15 537B    Z37   BZE   6  Z37A                [JUMP IF ZERO
   9 53M2          LDN   4  CIMESS(3)
   7 546L          LDX   1  7
  18 54L=          FINMVCH  1,5,4,6,HLOCK2      [MOVE DATA INTO PROGRAM  
   8 555W          LDX   1  FX1  
   9 55KG    Z37A  LDX   0  AWORK1(2)
   8 5656          ANDN  0  #1000
  19 56JQ          BZE   0  X1                  [J IF NOT CONTINUATION READ  
  18 574B          LDX   0  CIREADY(3)          [CONTINUATION READ(#1014)
  17 57J2          ANDN  0  #7777               [CHARS IN CI BUFFER  
  20 583L          TXL   6  0                   [J IF CHARS WANTED EQUAL TO CHARS
  15 58H=          BCC      X1                  [IN CI BUFFER
   7 592W          SBX   0  6
  10 59GG          DSA   0  CIREADY(3)   
   9 5=26          LDN   5  CIMESS(3)
   7 5=FQ    P1    LDEX  2  0
   8 5=_B          MVCH  4  0(2) 
   8 5?F2          BNZ   2  P2   
   8 5?YL          LDN   2  512  
   7 5#D=    P2    SBX   0  2
   8 5#XW          BNZ   0  P1   
  21 5*CG          LDCT  6  #100                [READ LIMITED - MORE CHARS TO BEREAD 
   8 5*X6          BRN      Z40  
  17 5BBQ    X1    SBX   6  AWORK3(2)           [REMAINDER CHAR COUNT
  19 5BWB          LDCT  0  #200                [NO MORE DATA IN CILCB BIT   
  10 5CB2          ERS   0  CIREADY(3)   
  16 5CTL          NGX   6  6                   [REMAINDER COUNT 
  21 5D*=          LDCT  0  #100                [IF INTERLOCK BIT 2 IS SET DATA IS   
  21 5DSW          ANDX  0  CIREADY(3)          [WAITING TO BE MOVED INTO THE LCB:   
  21 5F#G          BZE   0  N30                 [THE BIT MUST BE CLEARED & THE CPA   
  17 5FS6          ERS   0  CIREADY(3)          [LOCATED & WOKEN UP  
   9 5G?Q          HUNTACT  CPAT,2,3 
   6 5GRB          FPUT  
  19 5H?2          BRN      Z40                 [SET REPLY & RESTART PROG.   
  10 5HQL    N30   LDX   5  CIREADY(3)   
  18 5J==          BPZ   5  Z40                 [JUMP UNLESS READY IS SET
   9 5K9G          TRACE    5,READY  
  15 5KP6          LDCT  4  #20                 [READY BIT   
  21 5L8Q          SLL   5  3                   [JUMP UNLESS THE COM PROC BIT INDICAT
  21 5LNB          BPZ   5  W10                 [ING READY AT THE TOP LEVEL IS SET   
   8 5M82          LDCT  0  #40  
  10 5MML          ERS   0  CIREADY(3)   
  18 5N7=          HUNTACT  CPAT,2,3            [JUMP IF AT USER CONTEXT 
  10 5NLW          LDX   0  CONTEXT(2)   
   7 5P6G          SRC   0  1
   8 5PL6          BNG   0  W10  
  21 5Q5Q          SLL   5  1                   [JUMP IF SECOND COM PROC BIT IS SET  
  21 5QKB          BNG   5  W30                 [IE.IF READY AT NO USER HAS OCCURRED 
  14 5R52                                       [BEFORE  
  18 5RJL    W20   ORS   4  CIREADY(3)          [COMMAND PROCESSOR READY 
  21 5S4=    W10   ORX   6  4                   [SET READY BIT IN PROGS REPLY WORD   
   7 5SHW          SRL   4  3
  20 5T3G          ORS   4  CIREADY(3)          [SET B7 OF CIREADY TO INDICATE   
  19 5TH6                                       [READY SET IN PROG REPLY WORD
   8 5W2Q          BRN      Z40  
   4 5WGB    #   
  21 5X22    #     THIS SECTION IS ONLY ENTERED WHEN READY AT NO USER CONTEXT OCCURRS
  21 5XFL    #     FOR THE SECOND TIME.IN WHICH CASE THE LINE IS CLOSED DOWN.THE PROG
  21 5X_=    #     IS GIVEN A REJECTED INOPERABLE REPLY & MUST ISSUE MODE 13 TO START
   7 5YDW    #     UP AGAIN  
   4 5YYG    #   
  16 5_D6    W30   LINKSET  2,PAIR4(1)          [LINK TO K7LOGOUT
  14 5_XQ          FPUT                         [FPUT CPA
  17 62CB          DERINGD  CIFPTR(3)           [CILCB RING DERINGED 
  21 62X2    #     THIS SETS THE REJECTED INOPERABLE REPLY & RESTARTS THE PROGRAM.   
  13 63BL    #     THE NEXT PERI WILL GO TO K1CIMOP  
   5 63W=    REJINOP 
   8 64*W          LDCT  6  #410 
  16 64TG          LINKSET  2,PAIR5(1)          [LINK TO K1CIMOP 
   8 65*6          BRN      Z42  
  21 65SQ    #     THIS SECTION DEALS WITH MODE 11 PERIS I.E.WRITE PERIS,WHICH ARE   
  21 66#B    #     ONLY ALLOWED AFTER A READY.THE DATA IS MOVED OUT OF THE PROGRAM   
  20 66S2    #     INTO AN ADATA,CREADL WHICH IS PASSED TO THE COMMAND PROCESSOR 
  21 67?L    #     BY CHAINING IT AFTER THE CPA & ACTIVATING THE PCA READY IS CLEARED
  11 67R=    #     FROM THE HOLDA & THE LCB  
   4 68=W    #   
   4 68QG    N20 
   7 69=6          LDCT  0  4
  10 69PQ          ANDX  0  CIREADY(3)   
  20 6=9B          BNZ   0  N25C                [IGNORE PERI IF ABANDON HAS BEGUN
   9 6=P2          ADN   6  CPDATA-A1
  10 6?8L          SETUPCORE 6,3,FILE,FRB
  17 6?N=          SBN   6  CPDATA-A1           [BACK TO DATA WDCT   
   7 6#7W          LDX   4  6
   9 6#MG          LDN   6  CPDATA(3)
  16 6*76          LDX   0  AWORK3(2)           [STORE CHAR COUNT
   7 6*LQ          LDX   1  0
  10 6B6B          ADN   0  CPDATA-A1*4+3
   7 6BL2          SRL   0  2
  15 6C5L          STO   0  A1(3)               [WORD COUNT  
   7 6CK=          ANDN  1  3
   7 6D4W          SRC   1  2
   8 6DJG          ADN   1  #41  
   9 6F46          STO   1  A1+1(3)  
  19 6FHQ          STO   3  AWORK2(2)           [SAVE ADDRESS OF ADATA,CREADL
   7 6G3B          LDX   2  7
  16 6GH2          FOUTMOVE 2,5,6,4,HLOCK2      [MOVE DATA OUT   
  17 6H2L          CALL  0  ZFINDLCB1           [FIND LCBFRESETS X2) 
  15 6HG=          BNG   3  REJINOP1            [J IF NO LCB 
   7 6H_W          STO   3  4
  16 6JFG          LDX   3  AWORK2(2)           [POINTER TO FRB  
  20 6J_6          LDX   1  A1+1(3)             [FIND THE LAST USED CHAR IN FRB  
  13 6KDQ          SBN   1  #41                 [PFCC
  18 6KYB          BZE   1  T1                  [NO SPACEFILLING NEEDED  
  20 6LD2          LDX   2  1                   [FORM COUNT OF CHARS IN LAST WORD
  21 6LXL          SLC   2  2                   [THAT NEED TO BECOME SPACES(UNUSED)  
   7 6MC=          SBN   2  4
   7 6MWW          NGX   2  2
  21 6NBG          ADX   1  3                   [FORM POINTER TO FIRST UNUSED CHAR   
   8 6NW6          ADX   1  A1(3)
   7 6P*Q          SBN   1  1
  15 6PTB          LDN   0  #20                 [SPACEFILL   
   8 6Q*2    T2    DCH   0  A1(1)
   7 6QSL          BCHX  1  /
   8 6R#=          BCT   2  T2   
   8 6RRW          LDX   2  FX2  
   7 6S?G    T1    LDX   3  4
   9 6SR6          HUNTACT  CPAT,3   
  18 6T=Q          CHAIN    AWORK2(2),3         [CHAIN ADATA AFTE CPAT   
  14 6TQB          LDX   2  3                   [FPVT CPA
   6 6W=2          FPUT  
   7 6WPL          LDX   3  4
   4 6X9=    N25 
  19 6XNW          LDX   0  XMASK+3(1)          [CLEAR READY IN LCB AND THE  
  19 6Y8G                                       [REMEMBRANCE OF READY (B7)   
  10 6YN6          ANDS  0  CIREADY(3)   
   4 6_7Q    N25A
  18 6_MB          LDN   6  0                   [JUMP TO SET ZERO REPLY  
   8 7272          BRN      Z40  
  15 72LL    N25AB BNZ   6  REJINOP             [BI REJECTED 
  13 736=          BRN      N25A                [OK  
  20 73KW    N25C  HUNTACT  CPAT,2,3            [FPUT CPA SINCE IT WAS ON A READY
  18 745G          READYON  2                   [INDICATE NO ADATA/CREADL
   6 74K6          FPUT  
   8 754Q          BRN      N25  
  20 75JB    #     IF THERE IS NO LCB FREE ADATA,CREADL & SET INOPERABLE REPLY   
   5 7642    REJINOP1
   9 76HL          FREECORE AWORK2(2)
   9 773=          BRN      REJINOP  
  21 77GW    #     THIS SECTION DEALS WITH MODE 13 PERIS APART FROM THE INIIAL ONE,  
  19 782G    #     THE EFFECT OF WHICH IS TO BREAK IN ON THE JOB BEING RUN.  
  20 78G6    #     THE ACTION TAKEN IS TO CHANGE TO THE CPA & ENTER PROCUNTX TO  
  21 78_Q    #     SEE IF BREAKIN IS POSSIBLE. IF IT IS THE LINE IS SET WAITING FOR  
  20 79FB    #     THE READY FOLLOWING THE BROKEN IN MESSAGE OTHERWISE THE PERI  
   8 79_2    #     IS REJECTED   
   4 7=DL    #   
  21 7=Y=    N10   BNG   3  N25A                [IF LCB ABS ACCEPT PERI BUT DO NOWT  
  13 7?CW    # NB THAT NEXT MODE  12 WOULD BE REJINOP
  10 7?XG          LDX   0  ACOMMUNE1(2) 
  15 7#C6          STO   0  CIMEMBER(3)         [MEMBER NO   
   9 7#WQ          HUNTACT  CPAT,2,3 
   7 7D#6          LDN   6  1
   4 7DRQ    #   
   4 7F?B    #   
   9 7FR2 ...      TRACE    1,CIBRKIN
  10 7JHG ...      NBREAKIN 2,4,ZBIREJ   
  17 7JNQ          LDN   6  0                   [MARK B.I. EFFECTED  
   5 7JYJ ...ZBIREJ  
  20 7K8B          CALL  0  ZFINDLCB            [IF NO LCB IT IS BEING ABANDONED 
  19 7KN2          BNG   3  N25AB               [SO ACCEPT BREAKIN (IF OKED) 
  10 7L7L          LDX   5  CIREADY(3)   
  18 7LM=          BNZ   6  Z41                 [J IF BREAKIN REJECTED   
  19 7M6W          LDX   0  XMASK+4(1)          [CLEAR REMEMBRANCE OF READY  
  10 7MLG          ANDS  0  CIREADY(3)   
   4 7N66    #   
  17 7NKQ    #     IT COULD BE THAT LOGOUT HAS JUST CHANGED JOBNO AND
  19 7P5B    #     PROCONTX REJECTED BREAKIN SINCE IT DIDNOT FIND THE CPAT   
   4 7PK2    #   
  10 7Q4L    #     BREAK IN SUCCESSFUL   
   4 7QJ=    #   
  15 7R3W          HUNTACT  CPAT,2,3            [FIND CI CPAT
  10 7RHG          LDX   0  CONTEXT(2)   
   7 7S36          ANDN  0  1
  17 7SGQ          BNZ   0  NAB                 [J IF IN USER CONTEXT
  20 7T2B          LDX   0  XMASK+5(1)          [LET IT DO A READY IN BREAK-IN   
  17 7TG2          ANDS  0  CIREADY(3)          [AND NO-USER CONTEXT 
   4 7T_L    NAB 
  20 7WF=          BPZ   5  N25A                [J IF READY NOT SET IN CIREADY   
  17 7WYW          LDX   0  XMASK+1(1)          [TO MISS FPUT OF CPA 
  17 7XDG          ANDS  0  CIREADY(3)          [CLEAR READY IN LCB  
  16 7XY6          READYON  2                   [SET BREAK-IN BIT
  16 7YCQ          FPUT                         [ACTIVATE CPAT   
   8 7YXB          BRN      N25A 
  21 7_C2    #     THIS SUBR LOCATES THE CILCB BY FOLLOWING THE FILERING OF THE OLPA.
  21 7_WL    #     ENTRY IS USUALLY FROM THE OLPA BUT CAN BE FROM OTHER ACTIVITIES   
  16 82B=    #     PROVIDED THAT ON ENTRY X2 POINTS TO THE OLPA. 
  21 82TW    #     ON EXIT X3 POINTS TO LCB,OR IS SET NEGATIVE :X2 IS RESET TO FX2   
   4 83*G    #   
   6 83T6    ZFINDLCB1   
  19 84#Q          LDX   2  FX2                 [ENTRY HERE ONLY FROM AOLPA  
   5 84SB    ZFINDLCB
   7 85#2          NGN   3  1
  10 85RL          LDN   1  BWORKRING(2) 
  17 86?=          BXE   1  BWORKRING+1(2),Y12  [J IF RING IS EMPTY  
  10 86QW          LDX   3  BWORKRING(2) 
  16 87=G          SBN   3  CIFPTR              [X3=ADDR OF LCB  
   8 87Q6    Y12   LDX   1  FX1  
   8 889Q          LDX   2  FX2  
   7 88PB          EXIT  0  0
   4 8992    #   
  16 89NL    #     THIS SECTION REPORTS THE VARIOUS ILLEGAL CASES
  11 8=8=    #     ILLEGALS ON FIRST ENTRY   
   5 8=MW    XILLINS1
  18 8?7G          LDX   5  ZILLINS(1)          ['ILLEGAL INSTRUCTION'   
  14 8?M6    XILL1 TOPCA    PAIR2(1)            [TO PCA  
  17 8#6Q          COOR1Y                       [PUT AOLPA TO SLEEP  
   9 8#LB          BRN      Z1CIMOP  
   5 8*62    Z2CIMOP 
   6 8*KL          UNPLUG
  16 8B5=          ILLEGALX 5                   [REPORT ILLEGAL  
   6 8BJW    XILLMODE1   
  16 8C4G          LDX   5  ZJYMODE(1)          ['MODE IN ERROR' 
   8 8CJ6          BRN      XILL1
   4 8D3Q    #   
  12 8DHB    #     ILLEGALS ON SUBSEQUENT ENTRIES
   4 8F32    #   
   5 8FGL    XILLINS 
  17 8G2=          LDX   5  ZILLINS(1)          ['ILLEGAL INSTRUCTION
   9 8GFW    XILL  TOPCA    PAIR2(1) 
  17 8G_G          COOR1Y                       [PUT AOLPA TO SLEEP  
   9 8HF6          BRN      Z2ENTRY  
   5 8HYQ    XILLMODE
  16 8JDB          LDX   5  ZJYMODE(1)          ['MODE IN ERROR  
   8 8JY2          BRN      XILL 
   5 8KCL    XCOUNT  
  17 8KX=          LDX   5  ZJYCOUNT(1)         ['COUNT OUT OF RANGE 
   8 8LBW          BRN      XILL 
  18 8LWG    XCHAR LDX   5  ZJYCHAR(1)          ['CHAR ADDRESS INVALID'  
   8 8MB6          BRN      XILL 
  18 8MTQ    RESV2 LDX   5  ZJYRESVIOL(1)       ['RESERVATION VIOLATION' 
   8 8N*B          BRN      XILL 
   4 8NT2    #   
   8 8P#L    #     GEORGE   ERROR
   4 8PS=    #   
   5 8Q?W    XGEOERR 
   9 8QRG          GEOERR   0,CIMOP  
   4 8R?6    #   
   4 8RQQ    #   
   5 8S=B    Z4CIMOP 
   4 8SQ2    #   
  20 8T9L    #                  THIS  SECTION  DEALS  WITH  THE  'REL'  EXTRACODE
  21 8TP=    #              ON  ENTRY,  X3  POINTS  TO  THE  OLPA,  WHICH  HAS  BEEN 
   9 8W8W    #              LOCKED.  
   4 8WNG    #   
  15 8X86          LDX   7  3                   [RERING OLPA 
  18 8XMQ          HUNTACT  APET,2,3            [FIND APET,TO FIND CPA   
   8 8Y7B          FCAPCA   2,,N 
   9 8YM2          LDX   5  BPTR(3)  
  10 8_6L          LDX   3  ACTRING(3)   
   9 8_L=          SBN   3  ACTRING  
   9 925W          BXE   3  2,P18B   
   8 92KG          RINGACT  7,2  
  10 9356    P18A  BXE   5  BPTR(3),P18B 
   9 93JQ          CHAIN    BPTR(3),7
   8 944B          BRN      P18A 
   7 94J2    P18B  LDX   3  7
  15 953L          NAME     3,GENERAL           [RENAME OLPA 
  16 95H=          LINKSET  3,PAIR8(1)          [LINK TO K5CIMOP 
   7 962W          LDX   2  3
   6 96GG          FPUT  
  16 9726          UP                           [BACK TO RELPER  
  20 97FQ    Z5CIMOP                            [CURRENT ACT IS GENERAL(OLD OLPA)
   7 97_B          STOZ     6
  14 98F2    N200  CALL  7  N250                [FIND LCB
  18 98YL          BNG   6  N300                [J IF STOPJOB ACCEPTED   
   7 9#B2          LDN   6  1
   7 9*K4 ...      LDX   3  2
   9 9C7T ...      NBREAKIN 2,5,N240 
  16 9C?Q          LDCT  6  #400                [SET ACCPT BIT   
   8 9CRB    N240  CALL  7  N250 
  10 9D?2    N300  LDXC  5  CIREADY(3)   
  17 9DQL          BCC      N258                [J IF READY NOT SET  
  15 9F==          STO   5  CIREADY(3)          [UNSET READY 
   7 9FPW          READYON  2
   6 9G9G    N260  FPUT  
  13 9GP6    N259  LONGSET  CIREL,N261,CIJOBNUM(3)   
   8 9H8Q    N261  LONGCOOR N200 
  17 9HNB          BRN      N200                [SEE IF JOB DONE YET.
   7 9J82    N258  SLL   5  2
  18 9JML          BPZ   5  N259                [J IF NO OUTPUT WAITING  
   8 9K7=          LDCT  5  #300 
  17 9KLW          ANDX  5  CIREADY(3)          [UNSET MESSAGE BITS  
  19 9L6G          ERS   5  CIREADY(3)          [UNSET AS THOUGH OUTPUT TAKEN
  16 9LL6          BRN      N260                [AND RESTART CPA 
   4 9M5Q    #   
  19 9MKB    N250  CALL  0  ZFINDLCB1           [IS LCB STILL ON OLPAS RING? 
  14 9N52          BPZ   3  N255                [J IF YES
  17 9NJL          LONGOFF                      [CLEAR WAITING STYLE 
  13 9P4=          SUICIDE                      [END 
  21 9PHW    N255  HUNTACT  CPAT,2,3            [LOCATE CPAT OF JOB BEING ABANDONED  
   7 9Q3G          EXIT  7  0
   4 9QH6    #   
   4 9R2Q    #END
   8 ____ ...47664661000200000000
