  13 22FL    #LIS  K0SWAP>K0ALLGEO>K0GREATGEO>K0EMS  
  15 22_=    #SEG  SWAP                         [RUTH PORTER 
   6 23DW          8HSWAP
   4 23YG    #   
   4 24D6    #   
   9 24XQ    #     VERSION FOR G3 MK8
   4 25CB    #   
   4 25X2    #   
   4 26BL    #   
  18 26W=    #     THIS SEGMENT SWAPS OBJECT PROGRAMS IN AND OUT OF CORE 
   4 27*W    #   
  18 27TG    #     ENTRY 1 PERFORMS A SWAP IN FOR THE PROCESS CONTROLLER 
  18 28*6 ...#     ENTRY 2 PERFORMS A SWAP OUT FOR THE PROCESS CONTROLLER
  21 28SQ    #     ENTRY 3 PERFORMS A SWAP OUT FOR A ROUTINE IN A COMMAND ACTIVITY   
  17 28_2 ...#     ENTRY 4 PERFORMS A SWAP OUT FOR THE CORE UNJAMMER 
  17 295= ...#             WHEN THE PROGRAM WAS PLUGGED IN OR STOPPED
  21 299G ...#     ENTRY 5 FROM TRUEDORM MACRO TO SWAP OUT A TRULY DORMANT PROGRAM   
  21 29*Q ...#     ENTRY 7 FROM STRATEGY SEGMENT TO 'SWAP' A PROGRAM INTO ITS VALID  
  13 29G2 ...#             FREE PROGRAM BLOCK IN CORE
   4 29L= ...[   
  18 29QG ...[     DURING SEGMENT,X7 USED AS INDICATOR FOR TYPE OF SWAP  
  14 29WQ ...[     X7 = 0  FOR VALID FREE PROGRAM BLOCK  
  10 2=32 ...[     X7 = 1  FOR SWAP IN   
  10 2=7= ...[     X7 =-1  FOR SWAP OUT  
   4 2=?L    #   
  21 2=R=    # BECAUSE OF THE EXTRACOR MECHANISM THERE MAY BE AMBIGUITY BETWEEN ALOGL
  21 2?=W    # OF AOBJPROG AND ACORSZ OF SCB.THE SCB IS ALWAYS THE DEFINITIVE VERSION
   9 2?QG    # OF THE PROGRAM STATE  
  10 2#=6          SEGENTRY K1SWAP,XX1S  
  10 2#PQ          SEGENTRY K2SWAP,XX2S  
  10 2*9B          SEGENTRY K3SWAP,XX3S  
  10 2*P2          SEGENTRY K4SWAP,XX4S  
  10 2*W8 ...      SEGENTRY K5SWAP,XX5S  
  10 2B3B ...      SEGENTRY K7SWAP,XX7S  
   7 2B8L    #     CONSTANTS 
   4 2BN=    #   
   9 2C7W    ZBITS16MS      #77777400
   9 2CMG    ZBIT9          #40000   
   9 2D76    ZBIT11         #10000   
   9 2DLQ    ZBITS1012      #24000   
   8 2F6B    ZBITS1112   #14000  
  10 2FL2    ZLINKPAIR      PAIR   SWAP,2
  18 2FPX ...P12   COMPROLINK                   ['PROGBRKIN' TYPE LINK   
   6 2FTS ...SCHEDLINK   
   9 2F_P ...      PAIR     SCHEDIN,1
   4 2G5L    #   
   4 2GK=    #   
   9 2H4W    #     LOCAL DEFINITIONS 
   9 2HJG    #OPT  ASHEETSIZE=BSSS   
   8 2J46    #DEF  ZAC3=ACOMMUNE3
   8 2JHQ    #DEF  ZAC4=ACOMMUNE4
   8 2K3B    #DEF  ZAC5=ACOMMUNE5
   8 2KH2    #DEF  ZACOMMNUM=9   
   8 2L2L    #DEF  ZCLONGNUM=1   
   4 2LG=    #   
   4 2L_W    #   
   4 2MFG    #   
  16 2M_6    #     TABLE OF BLOCK TYPES TO CHECK AT SWAP-OUT TIME
   4 2NDQ    #   
   4 2NYB    ZTAB
   4 2PD2    #   
  17 2PXL    #     LIST OF NON CHAIN TERMINATING ACTIVITY BLOCK TYPES
   4 2QC=    #   
   7 2QWW    #HAL  AOLPT,Z2  
   4 2RBG    #   
  18 2RW6    #     LIST OF DATA BLOCK TYPES WHICH REQUIRE SPECIAL ACTION 
   4 2S*Q    #   
   8 2STB    #HAL  BSTB+BQBLK,Z3 
  15 2T*2    #HAL  0,Z100                       [DUMMY ENTRY 
  19 2TSL    #DEF  ZTOT=0?-ZTAB-1               [NUMBER OF ENTRIES LESS DUMMY
   4 2W#=    #   
   4 2WRW    #   
   4 2X?G    #   
   4 2XR6    #   
   4 2Y=Q    #   
   4 2Y?B ...[   
  13 2Y#2 ...[  SUBROUTINE TO CLEAR BIGBIT MARKERS   
   4 2Y#L ...[   
   4 2Y*= ...#   
   5 2Y*W ...TCLEAR  
   6 2YBG ...#UNS  ECS   
   4 2YC6 ...(   
   9 2YCQ ...      LDX   0  BIGPROGCP
  18 2YDB ...      BZE   0  (6)                 [EXIT IF NO BIG PROG'S   
   9 2YF2 ...      STOZ     BIGPROGCP
   4 2YFL ...)   
   6 2YG= ...#UNS  ECS   
   4 2YGW ...#SKI
   4 2YHG ...(   
  10 2YJ6 ...      BSOFF    BIGBIT,(6)   
   9 2YJQ ...      OFF      BIGBIT   
   4 2YKB ...)   
   8 2YL2 ...      LDN   3  BJOBQ
   9 2YLL ...NN    LDX   3  FPTR(3)  
   9 2YM= ...      BXE   3  CXJO,(6) 
  19 2YMW ...      BC       3,JBBIG             [REMOVE ANY BIG PROG MARKERS 
   8 2YNG ...      BRN      NN   
   4 2YP6 ...[   
  21 2YQB    #     ROUTINES TO DEAL WITH BLOCK TYPES CHECKED AGAINST ENTRIES IN ZTAB 
   4 2_=2    #   
  16 2_PL    Z2    BRN      N201                [J TO CONTINUE   
   4 329=    Z3  
   8 32NW    #SKI  K6SWAP>199-199
  10 338G          TRACE    3,SWAPWAIT   
  17 33N6          SWAPWAIT N201                [AUTO TFR IN PROGRESS
  19 347Q          BRN      N400                [TFR WAS OUT OF O/P-RESTART  
  16 34MB    Z100  LDX   4  ATYPE(3)            [DUMMY BLOCK TYPE
   8 3572          TXL   4  CACT 
  19 35LL          BCS      N201                [J TO CONTINUE IF DATA BLOCK 
  19 366=          BRN      N205                [CHAIN TERMINATING ACTIVITY  
   4 36KW    #   
   4 375G    #   
   8 37K6    #     PCA ROUTINES  
   4 384Q    #   
   5 38JB    PCAFINDC
   8 3942          STO   1  GEN0 
   8 39HL          LDX   2  FX2  
  10 3=3=          JOBLOCK  JOBNO(2),2   
   8 3=77 ...      BPZ   2  PCAOK
  10 3=?4 ...      GEOERR   0,NOJOBBLK   
   5 3=B_ ...PCAOK   
   2 3=GW
  18 3?2G          JMBAC    PFC,2,JBPISO,JBSTSO [J IF NOT SPECIAL SWAPOUT
  18 3?G6          LDN   6  1                   [ SET SPEC OUT MARKER.   
   4 3?_Q    PFC 
   7 3#FB          FPCAJO   2
   9 3#_2          BRN      (GEN0)   
   5 3*DL    PCAFINDT
   8 3*Y=          SMO      FX2  
   8 3BCW          LDX   2  ZAC3 
   8 3BXG          FINDTP   2,2  
   7 3CC6          EXIT  1  0
   5 3CWQ    PCAFINDP
   8 3DBB          SMO      FX2  
   8 3DW2          LDX   2  ZAC3 
   8 3F*L          FINDPUC  2,2  
   7 3FT=          EXIT  1  0
  14 3G#W    PCAMSK         BITMASK 0,1,18,19,20,21,22,23
  19 3GSG    PCACHADP                           [SET UP EXEC WORDS IN PUC PCA
  19 3H#6          LDX   0  ADATUM(2)           [ADATUM HAS BEEN RELATIVISED 
   8 3HRQ          BRN      PCAC1
  21 3J?B    PCACHADT                           [SET UP EXEC WORDS IN TP/NORMAL PCA  
   7 3JR2          LDN   0  0
   8 3K=L    PCAC1 SMO      FX2  
   8 3KQ=          ADX   0  ZAC4 
   6 3KTB ...#UNS CA1D   
   9 3KYG ...        ADX   0  CA1D   
   6 3L3L ...#UNS CA1D   
   4 3L6Q ...#SKI
   8 3L9W          ADN   0  A1D  
   9 3LPG          STO   0  ADATUM(2)
   9 3M96          ADX   0  GEODATUM 
   9 3MNQ          STO   0  ADTM(2)  
   9 3N8B          LDX   0  ALMT(2)  
   8 3NN2          SMO      FX1  
   9 3P7L          ANDX  0  PCAMSK   
   9 3PM=          ORX   0  ADTM(2)  
   9 3Q6W          ADX   0  ALIMIT(2)
   9 3QLG          STO   0  ALMT(2)  
  10 3R66          LDN   0  CHAINADD(2)  
   9 3RKQ          STO   0  AECA(2)  
   9 3S5B          LDN   0  ASTOP(2) 
   9 3SK2          STO   0  APD1(2)  
   9 3T4L          STO   0  APD2(2)  
   9 3TJ=          STO   0  APD3(2)  
   9 3W3W          STO   0  APD4(2)  
   7 3WHG          EXIT  1  0
   4 3X36    #   
   4 3XGQ    XX1S
  15 3Y2B    #     ENTRY IN THE ASWINT.ACC6 CONTAINS JOB NO. 
  17 3Y7J ...      LDN   7  1                   [READ MODE INDICATOR 
   7 3Y#Q ...#UNS  ADPLLSKIP 
   4 3YFY ...(   
  17 3YM6 ...      LDN   0  1                    [- DATAPASS COUNT OF
  15 3YS# ...      SMO      ADPSWPPTR            [- SWAPINS  
   7 3Y_G ...      ADS   0  3
   4 3_6N ...)   
  10 3_?W ...XFPB  MHUNTW   3,AOBJPROG   
   9 3_F=          STO   3  ZAC4(2)  
  17 3_P4 ...      BZE   7  XFPB1               [J IF VALID FPB ENTRY
  11 3_YW          MHUNTW   3,ADATA,ASTRAT   
   9 42DG          LDX   6  GPROG(3) 
  10 42Y6 ...XFPB1 STO   6  ACOMMUNE6(2) 
   9 43CQ          STO   6  JOBNO(2) 
   8 43XB          BRN      N1   
   4 43Y5 ...[   
  20 43YS ...[     ENTRY FROM STRATEGY SEGMENT TO 'SWAP' A PROGRAM INTO ITS VALID
  21 43_H ...[     FREE PROGRAM BLOCK(I.E. JUST LOCK PCA ETC.,AS NO SWAP FILE TRANSFE
  20 442= ...[     NECESSARY).THE EXIT WILL BE BACK TO CORESHED AND WILL TAKE THE
  21 442_ ...[     OK EXIT FROM THE STRATEGY MACRO WITH NO ADATA/ASTRAT BLOCK PRESENT
  21 443N ...[     IMPLYING CORESHED CAN TAKE ITS NORMAL SUCCESSFUL END OF SWAP IN   
   7 444C ...[     ROUTINE.  
   4 4456 ...[   
   4 445T ...XX7S
   7 446J ...#UNS  ADPLLSKIP 
   4 447? ...(   
  17 4482 ...      LDN   0  1                    [- DATAPASS COUNT OF
  16 448P ...      SMO      ADPSWPPTR            [- RE-USED FPB'S
   7 449D ...      ADS   0  4
   4 44=7 ...)   
  17 44=W ...      LDN   7  0                   [VALID FPB INDICATOR 
  16 44?K ...      LDX   6  ACOMMUNE4(2)        [LOAD JOB NUMBER 
   8 44## ...      BRN      XFPB 
   4 44*3 ...[   
   4 44*Q ...[   
   4 44C2    XX4S
   8 44WL    N400  FJOPCA   3,2  
   9 45B=          TESTRCTP 3,N401   
   8 45TW          BRN      N200 
   4 46*G    N401
  16 46T6          TESTTP   ,N200               [J IF APET/ATP   
   8 47#Q          FINDTP   ,3   
   8 47SB          BRN      N201 
  15 48#2    N200  LDX   3  FX2                 [START AT PCA
  15 48RL    N201  LDX   3  FPTR(3)             [NEXT BLOCK  
  17 49?=          LDN   5  ZTOT                [COUNT OF BLOCK TYPES
   9 49QW    N203  LDX   4  ATYPE(3) 
   9 4==G          ERX   4  ZTAB(1)  
   9 4=Q6          ANDX  4  HALFTOP  
  15 4?9Q          BZE   4  N202                [J IF MATCH  
   7 4?PB          ADN   1  1
  19 4#92          BCT   5  N203                [DROP THROUGH IF DUMMY MATCH 
   9 4#NL    N202  LDX   4  ZTAB(1)  
   8 4*8=          ANDN  4  #7777
  18 4*MW          ADX   4  FX1                 [SET UP BRANCHING POINT  
   8 4B7G          LDX   1  FX1  
   8 4BM6    #SKI  K6SWAP>199-199
  11 4C6Q          TRACE    ATYPE(3),SWAPCHEK
  19 4CLB          EXIT  4  0                   [J TO TAKE APPROPRIATE ACTION
   4 4D62    XX2S
   7 4D89 ...#UNS  ADPLLSKIP 
   4 4D=D ...(   
  17 4D#M ...      LDX   1  ADPSWPSW             [- DATAPASS COUNTS  
  19 4DBW ...      LDN   0  1                    [- DECIDE IF THIS SWAPOUT   
  16 4DF5 ...      SMO      ADPSWPPTR            [- DUE TO LLS   
  16 4DH# ...      ADS   0  0(1)                 [- OR UNJAMMER  
   4 4DKH ...)   
  15 4DMQ ...N205  NGN   7  1                   [WRITE MODE  
   7 4DP_ ...#UNS FREEBUFF   
   4 4DS8 ...(   
  18 4DWC ...      FREEBUFF AOLPT,APET   [ FREE FURBS & FUWBS OF AOLPTS  
   4 4DYL ...)   
   4 4F2T ...#   
   4 4F5=    #   
  14 4FJW    #     THIS PIECE OF GARBAGE LOCKS PCA'S ETC.
   4 4G4G    #   
   4 4GJ6    N1  
   7 4H3Q          LDN   6  0
  16 4HHB          CALL  1  PCAFINDC            [ FIND CURRENT.  
  10 4J32          PCATEST  2, , ,XPUC   
   7 4JGL                   [
  11 4K2=                   [   NORMAL/TP.   
   7 4KFW                   [
  16 4K_G          BNZ   6  X0                  [ J IF SPECOUT   
  10 4LF6          LOCKC    2,PCAFINDC   
  17 4LYQ          CALL  1  PCAFINDC            [ LOCK AND REFIND.   
   4 4MDB    X0  
   8 4MY2          SMO      FX2  
  15 4NCL          STO   2  ZAC3                [ DUMP PTR.  
  15 4NX= ...      BNG   7  START               [ J IF S/O   
   9 4PBW          CALL  1  PCACHADT 
   8 4PWG          TESTTP   2,X1 
  16 4QB6          BRN      START               [ J IF NORMAL.   
   4 4QTQ    X1  
   9 4R*B          CALL  1  PCAFINDP 
   8 4RT2          BNZ   6  X2   
  10 4S#L          LOCKC    2,PCAFINDP   
   9 4SS=          CALL  1  PCAFINDP 
   9 4T?W    X2    CALL  1  PCACHADP 
   8 4TRG          BRN      START
   7 4W?6                   [
   8 4WQQ                   [ PUC
   7 4X=B                   [
  16 4XQ2 ...XPUC  BNG   7  XP1                 [ J IF SWAPOUT.  
  16 4Y9L          BNZ   6  XP0                 [ J IF SPECOUT.  
  10 4YP=          LOCKC    2,PCAFINDC   
   9 4_8W          CALL  1  PCAFINDC 
   9 4_NG    XP0   CALL  1  PCACHADP 
   4 5286    XP1 
   8 52MQ          SMO      FX2  
   8 537B          STO   2  ZAC3 
   9 53M2          CALL  1  PCAFINDT 
   8 546L          BNZ   6  XP2  
  10 54L=          LOCKC    2,PCAFINDT   
   9 555W          CALL  1  PCAFINDT 
   8 55KG ...XP2   BNG   7  XP3  
   9 5656          CALL  1  PCACHADT 
   4 56JQ    XP3 
   8 574B          SMO      FX2  
   8 57J2          STO   2  ZAC3 
   8 583L    START LDX   1  FX1  
   8 58H=          LDX   2  FX2  
  21 592W    #     ADDR OF APET OR APET/ATP IN ACOMMUNE3 OF ACTIVITY CONTROLLING SWAP
  12 59GG          HUNT2    3,BSTB,BSCB,ZAC3(2)  
  16 5=26 ...      BNG   7  N27                 [J IF SWAP OUT   
  21 5=FQ          CHAIN    ZAC4(2),3           [ ELSE CHAIN PROGRAM BLOCK AFTER SCB 
  20 5=PJ ...      BZE   7  N10                 [J IF VALID FPB AS NO SWAP REQ'D 
  15 5=_B ...      STOZ     AQMODE(3)           [READ MODE 0 
   8 5?98 ...      BRN      N27A 
  15 5?F2 ...N27   NGS   7  AQMODE(3)           [MODE WORD 1 
   9 5?NS ...N27A  LDX   4  ASHEET(3)
  17 5?YL          BZE   4  N26                 [J IF S/I FOR LOAD   
  20 5#D=          SBN   4  1                   [COUNT OF TFRS TO BE ATTEMPTED   
  19 5#XW          LDX   5  ASHEETSIZE          [               AUTONOMOUSLY 
  17 5*CG          STO   5  AWORK2(2)           [LENGTH OF AUTO TFR  
  21 5*X6          LDX   5  AOBJLF(3)           [NO OF LOG. FILE CONTAINING PROGRAM  
   9 5BBQ          STO   5  AWORK3(2)
  14 5BWB    N2    LDN   5  0                   [SCB PTR 
  14 5CB2          LDN   6  0                   [O/P PTR 
  16 5CTL          BZE   4  N15                 [J IF ONLY 1 TFR 
  12 5D*=    N6    HUNT2    3,BSTB,BSCB,ZAC3(2)  
   7 5DSW          SMO      5
  15 5F#G          LDX   1  AOBJST(3)           [SHEET HOME  
   9 5FS6          STO   1  AWORK4(2)
  16 5G?Q    #     AN ATTEMPT IS MADE TO QUEUE AN AUTONOMOUS TFR 
  19 5GRB ...N20   SWAPTRAN AQMODE(3),AWORK2(2),AWORK3(2),,EX6,ZAC3(2),N21   
  17 5H?2    N7    SBN   4  1                   [STEP COUNT OF TFRS  
  15 5HQL          ADN   5  1                   [STEP SCB PTR
  15 5J==          ADX   6  ASHEETSIZE          [STEP O/P PTR
  16 5JPW          BZE   4  N4                  [J IF LAST TFR   
  18 5K9G          BPZ   4  N6                  [J IF MORE TFRS TO QUEUE 
  20 5KP6          BACKWAIT N8                  [WAIT FOR ALL AUTO TFRS TO FINISH
  12 5L8Q          HUNT2    3,BSTB,BSCB,ZAC3(2)  
  15 5LNB ...      BPZ   7  N10                 [J IF SWAP-IN
  17 5M82          LDX   0  CLEANCT             [ SPRING CLEAN COUNT.
  16 5MML          STO   0  ASTIME(3)           [TIME AT SWAP-OUT
  10 5N7=          HUNTACTF AOBJPROG,3   
   8 5NLW          TESTTP   ,N40 
   8 5P6G          TESTPUC  ,N41 
  15 5PL6          BRN      N42                 [J IF APET/0 
   8 5Q5Q    N40   FINDPUC  ,2   
   6 5Q7N ...#UNS CA1D   
   4 5Q9L ...(   
   7 5Q?J ...N41   LDX   0  3
   8 5Q*G ...      ADX   0  CA1D 
   4 5QCD ...)   
   6 5QFB ...#UNS CA1D   
   4 5QH# ...#SKI
   9 5QKB    N41   LDN   0  A1D(3)   
  17 5R52          SBS   0  ADATUM(2)           [RELATIVISE PUC DATUM
   8 5RJL          LDX   2  FX2  
   4 5S4=    N42 
  17 5SHW ...      LDX   1  3                   [SET X1 = AOBJPROG   
  16 5T3G ...      FJOPCA 3,2                   [FIND JOB BLOCK  
  17 5TH6 ...      FREEPROG 1,3                 [SET AOBJPROG AS FPB 
  15 5XFL          JMBS     N18,3,JBPISO,JBSTSO [J IF SPECIAL
  18 5X_=          UNLOCK   ZAC3(2)             [UNLOCK APET OR APET/ATP 
  15 5YDW          TESTRCTP 3,N17A              [J IF RCTP   
   8 5YYG          BRN      N17  
   4 5_D6    N17A
   9 5_XQ          FINDPUC  ZAC3(2),3
  16 62CB          UNLOCK   3                   [UNLOCK APET/APUC
   4 62G7 ...#   
   9 62JY ...#     SWAP OUT FINISHED 
   4 62MP ...#   
   4 62QG ...N17 
  11 62T? ...      GSCAN    JOBNO(2),SWAPOUTF
  21 62Y4 ...      OFF      CSWAPAB             [CLEAR THE 'ABORT FOR SWAPOUT' MARKER
   9 632T ...      CALL  6  TCLEAR   
   8 635L ...      FJOPCA   3,2  
   7 638C ...      ENDPAXES 3
  21 63?8 ...      JBSC     SOMAC,3,JBSOMAC    [J UNLESS S/O MACRO & CLEAR OTHERWISE 
  21 63*_ ...      OFF      BSTRAT              [CLEAR 'STRATEGY IN PROGRESS' MARKER 
  18 63DQ ...      BC       ,ASWOUTING          [MARK NO SWAPOUT GOING ON
   5 63HH ...SOMAC   
   9 63L# ...      BC       3,JBBEING
  14 63P5 ...      BS       3,JBSO              [MARK S/O
   8 63RW ...      FCAJO    2,3  
  11 63WM ...      JBSC     WAITCPA,3,JBCPASO
  20 63_D ...      JMBS     R5,3,JBDORM,JBPCACA [J IF DORMANT OR WAITING IN PCACA
   9 6449 ...      BC       3,JBSTOP 
  18 6472 ...      BS       3,JBSUSP            [ENSURE MARED SUSPENDED  
  16 649R ...      JBC      R5,3,JBBIN          [J IF NO BREAK IN
  17 64#J ...      MBI      3,JBSUSP,JBDORM     [SUSPENDED TO DORMANT
  17 64C* ...      LINKSET  2,P12(1)            [FORCE PROGBRKIN EXIT
   5 64G6 ...WAITCPA 
   6 64JX ...      FPUT  
  10 64MN ...R5    KICKASWI SCHEDLINK(1) 
   6 64QF ...RCOOR COOR1X
   4 653R ...N18 
   9 65*6    #SKI  K6COREJAM>499-499 
  11 65SQ          TRACE    JOBNO(2),SPECOUTF
   7 66#B          LDN   4  1
  19 66S2          ERS   4  INOUTPC             [INDICATR SWAPOUT FINISHED   
  10 67?L          LDX   4  ZBITS1112(1) 
  17 67R=          ERS   4  JMISC(3)            [INDICATE SWAPPED OUT
  20 68=W          ENDPAXES 3                   [CLEAR 'BEING ACCESSED' MARKER   
   9 68QG          LDN   4  CJACCLINK
  19 69=6          LDN   5  ACC3(2)             [RESTORE ORIGINAL INFORMATION
  19 69PQ          MOVE  4  ALINK               [TO PROGRAM CONTROL ACTIVITY 
   8 6=9B          ADN   4  ALINK
  10 6=P2          LDN   5  ACOMMUNE1(2) 
   9 6?8L          MOVE  4  ZACOMMNUM
   9 6?N=          ADN   4  ZACOMMNUM
   9 6#7W          LDN   5  CLONG1(2)
   9 6#MG          MOVE  4  ZCLONGNUM
  16 6*76          COOR1X                       [REMOVE FROM LIST
  16 6*LQ    N10   LDX   0  CLEANCT             [ CURRENT TIME   
   9 6B6B          SBX   0  ASTIME(3)
  19 6BL2          ADS   0  ATOTIME(3)          [TIME SPENT ON BACKING STORE 
  21 6BTS ...      BZE   7  N26A                [J AS COBJUSE ALREADY UPDATED FOR FPB
   4 6C5L    N26 
  10 6CK=          HUNTACTF AOBJPROG,3   
  17 6D4W          LDX   6  ALOGLEN(3)          [CORE SIZE OF PROG   
   9 6DJG          ADS   6  COBJUSE  
   5 6F46 ...N26A  UP
   4 6FHQ    #   
  20 6G3B    #     IN THE CASE OF BSTS REPORTING AN O/P RESERVATION, SWAP WILL   
  12 6GH2    #     HAVE LOST ITS PLACE. HENCE:-  
  10 6H2L    N9    GEOERR   1,SWAPVIOL   
   4 6HG=    #   
  18 6H_W    #     THIS SECTION DEALS WITH BACKING STORE READ FAILURES   
  10 6JFG    N11   GEOERR   1,READFAIL   
   4 6J_6    #   
  20 6KDQ    #     THIS SECTION IS ENTERED IF THERE ARE NO SHEETS AVAILABLE FROM 
  13 6KYB    #     EITHER THE MAIN OR RESERVE LISTS. 
  10 6LD2    N12   GEOERR   1,NO SHEET   
   4 6LXL    #   
  17 6MC=    #     AN ATTEMPT IS MADE TO PERFORM A NON-AUTONOMOUS TFR
   4 6MWW    N21 
   4 6NBG    N22 
  21 6NNX ...N5    BACKTRAN AQMODE(3),BOBJ+BSET+ASWAP,AWORK2(2),N13,AWORK3(2),,EX6,Z-
   5 6P3# ...AC3(2)  
   8 6P*Q          BRN      N7   
   4 6PTB    #   
  20 6Q*2    #     THIS SECTION IS ENTERED WHEN A FAILURE ON AN AUTONOMOUS TFR   
   8 6QSL    #     IS DETECTED   
  17 6R#= ...N8    BPZ   7  N11                 [J IF READ FAILURE   
  14 6RRW          LDX   1  BA4(2)              [O/P ADDR
   7 6S?G          LDX   6  1
  10 6SR6          DVS   0  ASHEETSIZE   
  19 6T=Q          LDX   5  1                   [PROGRAM NO. OF FAILED SHEET 
   8 6TQB          LDX   1  FX1  
  21 6W=2          LDX   0  BA3(2)              [UNDATUMIZED ADDR OF SHEET TO FLAW   
   9 6WPL          STO   0  AWORK4(2)
   9 6X9=          LDX   0  BA2(2)   
  15 6XNW          ANDX  0  ZBITS16MS(1)        [TFR LENGTH  
   7 6Y8G          SRL   0  8
   9 6YN6          STO   0  AWORK2(2)
   8 6_7Q          BRN      N14  
   4 6_MB    #   
  16 7272    #     THIS SECTION IS ENTERED WHEN A FAILURE ON A   
  12 72LL    #     NON-AUTONOMOUS TFR IS DETECTED
   8 736=    N13   BRN      N14  
   4 73KW    #   
  21 745G    #     AT THIS POINT THE PATHS FOR DEALING WITH THE TWO SORTS OF FAILED  
  21 74K6    #     TFRS MERGE. X5 CONTAINS RELATIVE POINTER TO SHEET HOME TABLE IN   
  20 754Q    #     SCB. X6 CONTAINS ADDR OF START OF TFR RELATIVE TO O/P DATUM   
   4 75JB    N14 
  12 7642    #     NEXT LINE NOT REQUIRED FOR MK4
  16 76HL    #     BADSHEET AWORK4(2)           [FLAW THE SHEET  
  19 773= ...      BPZ   7  N11                 [J IF FAIL WAS ON A READ TFR 
  19 77GW    #     THIS SECTION CONTINUES TO DEAL WITH WRITE FAILS BY FIRST  
  20 782G    #     ATTEMPTING TO OBTAIN A SHEET NORMALLY.IF THIS IS UNSUCCESSFUL 
  20 78G6    #     THE BACKING STORE ALLOCATOR WILL TRY TO OBTAIN A SHEET FROM   
   8 78_Q    #     THE RESERVE   
  11 79FB          GETBACK  AWORK3(2),PRIV   
  12 79_2          HUNT2    3,BSTB,BSCB,ZAC3(2)  
  15 7=DL          LDX   0  EXEC1(2)            [SHEETHOME   
   9 7=Y=          STO   0  AWORK4(2)
   7 7?CW          SMO      5
  16 7?XG          STO   0  AOBJST(3)           [UPDATE SCB TABLE
  19 7#C6          BRN      N21                 [REPEAT TFR NON AUTONOMOUSLY 
   4 7#WQ    #   
  19 7*BB    #     THIS SECTION IS ENTERED IF THE O/P IS SMALLER THAN 1 SHEET
  12 7*W2    N15   HUNT2    3,BSTB,BSCB,ZAC3(2)  
  18 7B*L          LDX   0  AOBJST(3)           [UNDATUMISED SHEET ADDR  
   9 7BT=          STO   0  AWORK4(2)
   8 7C#W          BRN      N16  
   4 7CSG    #   
  20 7D#6    #     THIS SECTION PREPARES TO EXECUTE THE LAST TFR NON AUTONOMOUSLY
  12 7DRQ    N4    HUNT2    3,BSTB,BSCB,ZAC3(2)  
   7 7F?B          SMO      5
  17 7FR2          LDX   0  AOBJST(3)           [UMDATUMISED BS ADDR 
  16 7G=L          STO   0  AWORK4(2)           [FOR FINAL TFR   
  17 7GQ=    N16   LDX   0  AWRDNUM(3)          [LENGTH OF FINAL TFR 
   9 7H9W          STO   0  AWORK2(2)
  15 7HPG          BRN      N21                 [J TO EXECUTE
   4 7J96    #   
   4 7JNQ    #   
  13 7K8B    #     ENTRY FROM SWAP-OUT MACRO VIA DOWN
   4 7KN2    XX3S
   9 7L7L    #SKI  K6PROCONT>99-99   
   4 7LM=    (   
  11 7M6W          TRACE    JOBNO(2),SWAPOUTS
  11 7MLG          TRACE    JOBNO(2),FROM CPA
   4 7N66    )   
   9 7NKQ          LDCH  7  ATYPE(2) 
   9 7P5B          SBN   7  CPAT/64  
  18 7PK2          BZE   7  N100                [CHECK ISSUED FROM CPA   
   9 7Q4L          GEOERR   1,NOT CPA
  17 7QJ=    N100  FJOCA    3,2                 [POINTER TO JOB BLOCK
   2 7R3W
  14 7RHG          JBC      N102,3,JBNSW        [ERROR IF
  16 7S36          GEOERR   1,NSWAPABL          [NOT SWAPPABLE   
   2 7SGQ
  19 7T2B    N102  JMBS     NUP,3,JBSO,JBREEF   [J IF SWAPPED OUT OR REALTIME
  17 7WYW          JBS      N106,3,JBBEING      [ IF BEING SWAPPED   
  21 7X4R ...      PROGAXES 3,N100              [IF CANT ACCESS PROG,WAIT & J TO N100
  21 7X8N ...      ON       CSWAPAB             [SET S/O ABORT FLAG FOR STARTSTRAT   
   3 7X#K ...
  12 7XDG    #     NO,THEREFORE S/I AND SWAPPABLE
  19 7XY6          TESTRT   NUP,3               [LEAVE SWAPPED IN IF REALTIME
   2 7YCQ
  11 7YXB         MBS       3,JBSOMAC,JBCPASO
  11 7_C2          MBI      3,JBBEING,JBSI   
  14 7_WL          FPCACA   2,2                 [FIND PCA
  16 82B=          LINKSET  2,ZLINKPAIR(1)      [LINK TO K2SWAP  
  15 82TW          FPUT                         [PCA ON LIST 
  20 83*G    N103  COOR1                        [WAIT FOR SWAP TO BE COMPLETED   
   8 83T6    #SKI  K6SWAP>199-199
  11 84#Q          TRACE    JOBNO(2),S/OWAITA
   8 84SB          FJOCA    3,2  
  19 85#2          ENDPAXES 3                   [SIGNAL END OF PROGRAM ACCESS
  20 85RL    NUP   UP                           [WOKEN HERE BY PROCESS CONTROLLER
   2 86?=
  16 86QW    N106 BS       3,JBCPASO            [MARK CPA WAITING
  20 86W7 ...      COOR1                        [WAIT FOR SWAP TO BE COMPLETED   
   8 86_D ...      BRN      NUP  
  18 874P ...[     ENTRY (IN PCA) FROM TRUEDORM MACRO TO SWAP OUT A TRULY
   9 8782 ...[     DORMANT PROGRAM   
   4 87?? ...[   
   4 87BJ ...XX5S
   8 87FT ...#SKI  K6SWAP>99-99  
  11 87K6 ...      TRACE JOBNO(2),TRUEDORM   
  16 87NC ...      FJOPCA 3,2                   [FIND JOB BLOCK  
  17 87RN ...      PROGAXES 3,XX5S              [J IF BEING ACCESSED 
   7 87W_ ...      ON CSWAPAB
   7 882= ...#UNS  ADPLLSKIP 
   4 885H ...(   
  17 888S ...      LDN   0  1                    [- DATAPASS COUNT OF
  17 88#5 ...      SMO      ADPSWPPTR            [- TRUDORM SWAPOUTS 
   7 88CB ...      ADS   0  0
   4 88GM ...)   
   8 88KY ...      BRN      N205 
  10 88PB          MENDAREA 15,K200SWAP  
   4 8992    #END
   8 ____ ...61533204000300000000
