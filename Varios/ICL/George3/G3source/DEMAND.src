  12 22FL    #LIS  K0DEMAND>K0ALLGEO>K0GREATGEO  
  16 22_=    #SEG  DEMAND60                     [RICHARD GRIMWADE
   7 23DW          8HDEMAND  
  18 23YG    #     THIS SEGMENT IMPLEMENTS THE DEMAND AND OUTPAUSE MACROS
  15 24D6    #  DEMAND : K1DEMAND  (SEE PRM FOR MACRO DEFN)  
  19 24XQ    #     DEMAND USES THE OUTPAUSE CODE TO DETERMINE WHEN TO O/P THE
   8 25CB    #     SYSTEM REQUEST
  20 25X2    #     IF DEMAND GOES UP FOR AN EVENT ACCS4-7 (INC) ARE SAVED IN AN  
  10 26BL    #     ADATA/ADEMAND BLOCK:- 
  14 26W=    #     X3    :CONTAINS A REPLACER FOR UPPLUS 
  14 27*W    #     X4     CONTAINS THE MESSAGE IDENTIFIER
  16 27TG    #     X5    :CONTAINS TIME DELAY BEFORE MESSAGE O/P 
  16 28*6    #     X6    :CONTAINS TIME FACTOR BETWEEN REPEATS   
  13 28SQ    #     X7    :CONTAINS A COPY OF EXECS   
  18 29#B    #     EXEC1 :CONTAINS MESS IDEN AND IS SET -VE ON REENTRY   
  12 29S2    #     EXEC2 :CONTAINS TIME FACTORS  
  18 2=?L    #     EXEC3 :CONTAINS STATE OF WAIT COMPATIBLE WITH OUTPAUSE
   8 2=R=    #  OUTPAUSE:K2DEMAND
  20 2?=W    #     OUTPAUSE USES X0-X3 ONLY AND KEEPS THE STATE OF THE WAIT IN   
  20 2?QG    #     EXEC3 : THIS HAS B0 SET IF REENTRY AND THE REST OF THE WORD   
  17 2#=6    #     IS THE TIME (IN SPRING CLEANS FOR THE NEXT EXIT   
  11 2#PQ          SEGENTRY K1DEMAND,SENTRY1 
  11 2*9B          SEGENTRY K2DEMAND,SENTRY2 
   7 2*P2    #DEF  XEC1=EXEC1
   7 2B8L    #DEF  XEC2=EXEC2
   7 2BN=    #DEF  XEC3=EXEC3
   4 2C7W    [   
  12 2CMG    [ WORDS IN THE ADATA/ADEMAND BLOCK  
  20 2D76    #DEF  XACCS=A1                     [4 WORDS; COPIES X4-7 INCLUSIVE  
  21 2DLQ    #DEF  XROUTE=XACCS+4               [ROUTING PARAMETER COPIED FROM AWORK4
  18 2F6B    #DEF  XDEMANDLEN=XROUTE+1-XACCS    [LOGICAL LENGTH OF BLOCK 
   4 2FL2    [   
  11 2G5L    X1             60*60/K9*1000*24 
   5 2GK=    SENTRY2 
  16 2H4W          LDN   7  1                   [MARKER FOR MACRO
   4 2HJG    SR1 
   9 2J46          LDXC  0  XEC3(2)  
  15 2JHQ          BCS      TIME                [J IF REENTRY
   9 2K3B          LDN   0  1000/K9  
   9 2KH2          MPY   0  XEC3(2)  
   7 2L2L          LDX   0  1
  18 2LG=          MPY   0  ASYSTEMMESS         [CALCULATE TIME FOR W/UP 
   8 2L_W          ADX   1  JTIME
   8 2M45 ...      SMO      FX1  
   9 2M6# ...      BXL   1  X1,UNDER 
   8 2M8H ...      SMO      FX1  
   8 2M=Q ...      SBX   1  X1   
  16 2M#_ ...      ORX   1  CACT        [B1 IS MIDNIGHT MARKER   
   5 2MC8 ...UNDER   
  19 2MFG          ORX   1  GSIGN               [SET -VE TO INDICATE REENTRY 
   9 2M_6          STO   1  XEC3(2)  
   7 2N6# ...#UNS FREEBUFF   
   9 2N7K ...      LDN   0  XEC1(2)  
   9 2N8W ...      LDN   1  AWORK1(2)
   7 2N=7 ...      MOVE  0  3
  21 2N?G ...      FREEBUFF [ FREES FURBS AND WRITES AWAY FUWBS TO CURRENT ACTIVITY  
  20 2NDQ          LDN   3  0                   [SET MARKER = REPLACER FOR B-IN  
   8 2NNJ ...      BRN      WAIT2
   8 2NYB    WAIT  LDX   2  FX2  
   9 2PD2          LDN   0  XEC1(2)  
   9 2PXL          LDN   1  AWORK1(2)
  18 2QC=          MOVE  0  3                   [SAVE TIME FACTORS ETC.  
   9 2QWW ...WAIT2 LONGSTOP XBI,SPUR 
   9 2RBG          LDN   0  AWORK1(2)
   9 2RW6          LDN   1  XEC1(2)  
   7 2S*Q          MOVE  0  3
  17 2STB          ADN   3  1                   [REPLACER FOR EVENT  
  17 2T*2          BZE   7  XEVEN               [J IF DEMAND ENTRY   
   8 2TSL    MAC   UPPLUS   0(3) 
   9 2W#=    SPUR  LDN   0  AWORK1(2)
   9 2WRW          LDN   1  XEC1(2)  
   7 2X?G          MOVE  0  3
  20 2XR6          LDN   3  3                   [REPLACER FOR TIME OR SPURIOUS   
  10 2Y=Q    XBI   HUNTW    1,GMON,AOUT  
  15 2YQB          BZE   3  YBI                 [J IF B-IN   
   8 2_=2          BNG   1  ZTIME
   8 2_PL          LDXC  0  A1(1)
   8 329=          BCC      ZTIME
  17 32NW          LDN   3  2                   [REPLACE FOR CANTDO  
  16 338G    YBI   BNG   1  NBLK                [NO BLOCK TO FREE
   7 33N6          FREECORE 1
   7 347Q    NBLK  LONGOFF   
   8 34MB          BNZ   7  MAC  
  15 3572          BZE   3  XBIN                [J IF B-IN   
   8 35LL          BRN      XCANT
   5 366=    ZTIME   
   7 36KW          NGNC  0  1
   9 375G          ANDX  0  XEC3(2)  
   8 37K6    TIME  LDX   1  JTIME
  16 384Q          LDN   3  0                   [REPLACER MARKER 
   7 38JB          TXL   1  0
   8 3942          BCS      X3   
   4 39HL    X2  
   7 3=3=          LDN   3  3
  15 3=GW          BZE   7  XTIME               [J IF TIME   
   8 3?2G    #SKI  K6DEMAND>99-99
  10 3?G6          TRACE    JTIME,TIME UP
   8 3?_Q          BRN      MAC  
   8 3#FB ...X3    LDCT   2  #200
   8 3#_2 ...      ANDX   2  0   
  20 3*DL ...      BZE   2   WAIT      [CANT BE TIME UNLESS MIDNIGHT MARKER SET  
   8 3*Y= ...      ERX   0   2   
   8 3BCW ...      SMO      FX1  
   8 3BXG ...      LDX   2  X1   
   7 3CC6 ...      SRA   2  1
  19 3CWQ ...      BXL   2   JTIME,WAIT       [J IF NOT GONE PAST MIDNIGHT   
   8 3DBB ...      LDX   2  FX2  
  12 3DW2 ...      BRN      TIME   [CHECK AGAIN  
  15 3G#W    NRENT LDX   4  XEC1(2)             [MESSAGE ID  
  14 3GSG          LDX   5  XEC2(2)             [TIME LAG
   8 3H#6          ANDN  5  4095 
  15 3HRQ          LDX   6  XEC2(2)             [TIME FACTOR 
   8 3J?B          SRL   6  12   
   9 3JR2          STO   6  XEC3(2)  
   7 3K=L          LDN   7  0
  14 3KQ=          BNZ   5  SR1                 [OUTPAUSE
   8 3L9W    NPAUS TESTBRKIN XBIN
   9 3LPG          LDEX  0  CLONG1(2)
  18 3M96          BNZ   0  ZINF                [J IF NO EVENT ALROADY   
   9 3MNQ          STOZ     XEC3(2)  
   9 3N8B    XEVEN LDX   7  XEC3(2)  
  14 3NN2          SETNCORE XDEMANDLEN,2,ADATA,ADEMAND   
   7 3P7L          LDN   1  4
   8 3PM=          ADN   2  XACCS
  15 3Q6W          MOVE  1  4                   [SAVE ACCS   
   8 3QLG          SMO      FX2  
   9 3R66          LDX   0  AWORK4   
  18 3RKQ          STO   0  XROUTE-XACCS(2)     [KEEP ROUTING PARAMETER  
  17 3S5B          LDN   3  2                   [REPLACER FOR EVENT  
   8 3SK2    #SKI  K6DEMAND>99-99
  10 3T4L          TRACE    JTIME,EVENT  
   8 3TJ=          BRN      MAC  
   5 3W3W    XCANT1  
   7 3WHG          LONGOFF   
  17 3X36    XCANT LDN   3  0                   [REPLACER FOR CANTDO 
   8 3XGQ    #SKI  K6DEMAND>99-99
  11 3Y2B          TRACE    JTIME,CANTDOUP   
   8 3YG2          BRN      MAC  
  16 3Y_L    XBIN  LDN   3  1                   [REPLACER B-IN   
   8 3_F=    #SKI  K6DEMAND>99-99
  10 3_YW          TRACE    JTIME,BRKIN  
   8 42DG          BRN      MAC  
   5 42Y6    SENTRY1 
   9 43CQ    #SKI  K6DEMAND>199-199  
   4 43XB    (   
  11 44C2          TRACE    XEC1(2),DEMAND1  
  11 44WL          TRACE    XEC2(2),DEMAND2  
   4 45B=    )   
  10 45TW          LDX   0  ACOMMUNE1(2) 
   9 46*G          STO   0  AWORK4(2)
   9 46T6          LDXC  4  XEC1(2)  
  16 47#Q          BCC      NRENT               [J IF NOT REENTRY
  10 47SB          HUNTW   1,GMON,AOUT   
  16 48#2          BNG   1  NTHER               [ TEST FOR CANTDO
   8 48RL          LDXC  0  A1(1)
  15 49?=          BCS      XCANT1              [J IF CANTDO 
  11 49QW    NTHER MHUNTW   1,ADATA,ADEMAND  
   8 4==G          ADN   1  XACCS
   7 4=Q6          LDN   2  4
  15 4?9Q          MOVE  1  4                   [RESTORE ACCS
   8 4?PB          LDX   2  FX2  
  19 4#92          LDX   0  XROUTE-XACCS(1)     [RESTORE ROUTING PARAMETER   
  19 4#NL          STO   0  AWORK4(2)           [RESTORE ROUTING PARAMETER   
   7 4*8=          LDX   0  6
   8 4*MW          SLL   0  12   
   7 4B7G          ORX   0  5
  17 4BM6          STO   0  XEC2(2)             [RESTORE TIMING WORD 
  10 4C6Q          MFREEW   ADATA,ADEMAND
  15 4CLB          BZE   7  NPAUS               [J IF NO WAIT
   9 4D62          STO   7  XEC3(2)  
  15 4DKL          LDN   7  0                   [SET MARKER  
  17 4F5=          BRN   ZTIME                  [CHECK IF TIME YET   
  10 4FJW    XTIME HUNTW    1,GMON,AOUT  
  15 4G4G          LDN   7  0                   [SET MARKER  
  16 4GJ6          BPZ   1  XBUFF               [J IF BLOCK THERE
   9 4H3Q    ZINF  LDN   0  XEC1(2)  
   9 4HHB          LDN   1  AWORK1(2)
  16 4J32          MOVE  0  3                   [SAVE EXEC WORDS 
  16 4JGL          INFORMX  AWORK4(2),4,0       [OUTPUT MESSAGE  
   4 4K2=    WSR2
   9 4KFW          LDN   0  AWORK1(2)
   9 4K_G          LDN   1  XEC1(2)  
  14 4LF6          MOVE  0  3                   [RESTORE 
   9 4LYQ    SR2   LDX   0  XEC2(2)  
   8 4MDB          SRL   0  12   
   9 4MY2          STO   0  XEC3(2)  
   7 4NCL          BRN   SR1 
   9 4NX=    XBUFF LDN   0  XEC1(2)  
   9 4PBW          LDN   1  AWORK1(2)
   7 4PWG          MOVE  0  3
  15 4QB6          DOWN     MONFILE,5           [RE-OUTPUT   
   8 4QTQ          BRN      WSR2 
  10 4R*B          MENDAREA 20,GAPDEMAND 
   4 4RT2    #END
   8 ____ ...45755417000400000000
