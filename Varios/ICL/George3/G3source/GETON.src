  11 22FL    #LIS  K0GETON>K0LIBRARY>K0ALLGEO
  15 22_=    #SEG           GETON65             [N.R.BOULT   
   4 2357 ...[   
  15 2394 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23#_ ...[   
   9 23DW                   8HGETON  
  20 23YG    #     THIS SEGMENT CONTROLS IMPLEMENTATION OF THE GETONLINE COMMAND 
  11 24D6          SEGENTRY K1GETON,QK1GETON 
  12 24XQ    [ENTRY POINT FOR GETONLINE COMMAND  
   5 25CB    QK1GETON
   7 25X2    #SKIP K6GETON   
   9 26BL          TRACE    2,GETON1 
   9 26W=          STOZ     AWORK1(2)
   9 27*W          STOZ     AWORK2(2)
   9 27TG          STOZ     AWORK3(2)
  11 28*6    #     DECODE COMMAND PARAMETERS 
  18 28SQ          PERCON   1                   [DECODE PERIPHERAL NAME  
  15 29#B          TESTREPNOT OK,TERR           [J IF ERROR  
  17 29S2          MHUNT    3,CPAR,GPERCON      [LOOK FOR REPLY BLOCK
   8 2=?L          LDX   0  A1(3)
   8 2=R=          SRL   0  15   
   9 2?=W          TXU   0  TMT(1)   
  14 2?QG          BCC      P1                  [J IF MT 
  17 2#=6          SPARANOT 1                   [PASS FIRST PARAMETER
  18 2#PQ          ERROR    JPNAL,JNOTMT        [OTHERWISE REPORT ERROR  
  17 2*9B    TERR  ENDCOM                       [TERMINATE IF ERROR  
  12 2*P2    #     RELEASE ANY PREVIOUS CHANNEL  
  16 2B8L    P1    LDX   5  A1(3)               [TYPE/UNIT NO.   
   9 2BN=          STO   5  AWORK4(2)
  21 2C7W          HLSXONCH XBRKIN,5,PHLSER     [ASK HLS IF JOB CAN HAVE ANUVER TAPE 
  20 2CMG          RELPERENT 5                  [RELEASE ANY PREVIOUS PERIPHERAL 
  12 2D76    #     ENTRANT DESCRIPTION PARAMETER 
  19 2DLQ    P4    SPARANOT 2                   [UNPACK ENTRANT DESCRIPTION  
  10 2F6B          MHUNT    3,CPB,CUNI   
   9 2FL2          LDX   0  ANUM(3)  
  19 2G5L          BPZ   0  P2                  [J UNLESS PARAMETER MISSING  
  12 2GK=           COMERR   JPARMIS,JFENTRANT   
   5 2H4W    PHLSER  
  20 2HJG          COMERR   ERHLSONL            [HLS FORBIDS FURTHER ACQUISITION 
  10 2J46    P2    NAMETOP  3,FILE,FNAME 
   7 2JHQ          FNORM    7
   7 2K3B          PICKREP  7
  10 2KH2          MHUNT    3,FILE,FNAME 
  10 2L2L          NAMETOP  3,CPB,CUNI   
   7 2LG=          PUTREP   7
   7 2L_W          NGN   7  1
  10 2MFG          TESTREP  NAMEFORM,TERR
   8 2M_6          TESTREP  OK,P5
  11 2NDQ          MHUNT    3,FILE,ADJUNCTS  
   9 2NYB          LDX   0  A1+2(3)  
  20 2PD2          BNG   0  P2A                  [J IF SLOW PERIPHERAL QUALIFIER 
   7 2PXL          ANDN  0  2
  20 2QC=          BZE   0  P2A                 [BRANCH UNLESS PROPERTY QUALIFIER
   8 2QWW          MTEXPROP TAPE 
   9 2RBG          TESTREPN OK,PERR  
   9 2RW6    #SKI  K6GETON>299-299   
  12 2S*Q          SWITRACE ACOMMUNE1(2),GTONEP  
  19 2STB          LDX   0  ACOMMUNE1(2)        [SPECIAL PROPERTY DESCRIPTION
   9 2T*2          STO   0  AWORK2(2)
  18 2TSL          LDX   0  ACOMMUNE2(2)        [ILLEGAL COMBINATION MASK
   9 2W#=          STO   0  AWORK3(2)
   9 2WRW    #SKI  K6GETON>199-199   
   4 2X?G    (   
  12 2XR6          SWITRACE AWORK2(2),GTONMPRP   
  12 2Y=Q          SWITRACE AWORK3(2),GTONMILL   
   4 2YQB    )   
  11 2_=2          MHUNT    3,FILE,ADJUNCTS  
   4 2_PL    P2A 
   9 329=          LDX   0  TADJ(1)  
   9 32NW          ANDX  0  A1+1(3)  
   8 338G          BZE   0  P6   
   8 33N6          LDCT  0  #400 
   9 347Q          STO   0  AWORK1(2)
  10 34MB    P6    LDX   2  ALOGLEN(3)   
   9 3572          SBN   2  ADPAIR-A1
  17 35LL          SRL   2  1                   [NR OF ADJUNCT PAIRS 
  13 366=          BZE   2  PM1                 [NONE
   9 36KW    PM3   LDX   0  ADPAIR(3)
   8 375G          SBN   0  15   
  16 37K6          BNZ   0  PM2                 [NOT MODE ADJUNCT
  13 384Q          LDX   7  ADPAIR+1(3)         [MODE
   8 38JB          BRN      PM1  
   7 3942    PM2   ADN   3  2
  14 39HL          BCT   2  PM3                 [TRY NEXT
   4 3=3=    PM1 
  16 3=GW          BNG   7  P5                  [NO MODE ADJUNCT 
  11 3?2G          MHUNT    3,CPAR,GPERCON   
   9 3?G6          LDX   0  A1+2(3)  
   9 3?_Q          ANDX  0  TMSK(1)  
  16 3#FB          BNZ   0  P5                  [ALREADY GIVEN   
   9 3#_2          LDX   0  TMSK(1)  
   9 3*DL          ORS   0  A1+2(3)  
  14 3*Y=          ORS   7  A1+1(3)            [INSERT   
   4 3BCW    P5  
   9 3BXG          PROPERTY P5A,1,2  
   8 3CC6          MTEXPROP DECK 
   9 3CWQ          TESTREPN OK,PERR  
  21 3DBB          MTMERGPROP ACOMMUNE1(2),ACOMMUNE2(2),AWORK2(2),AWORK3(2),3,4,PILLC
   9 3DW2          ORN   3  GLIBNONS 
  17 3F*L          ERN   3  GLIBNONS            [REMOVE NONS PROPERTY
   9 3FT=          STO   3  AWORK2(2)
   9 3G#W          STO   4  AWORK3(2)
   7 3GSG          LDX   0  3
   9 3H#6          ANDN  0  GLIBMODE 
   8 3HRQ          BZE   0  P5A  
  11 3J?B          MHUNT    1,CPAR,GPERCON   
   8 3JR2          LDN   0  #74  
   9 3K=L          ORS   0  A1+2(1)  
   9 3KQ=          ORS   0  A1+1(1)  
   9 3L9W          ERS   0  A1+1(1)  
  17 3LPG          SRL   3  18                  [STORE MODE PROPERTY 
   9 3M96          ORS   3  A1+1(1)  
   4 3MNQ    P5A 
   9 3N8B          MFREE    CPB,CUNI 
  10 3NN2          MHUNT    3,FILE,FABSNB
  11 3P7L    #     CHECK OWNERSHIP OF TAPE   
   9 3PM=          LDX   0  ATYPE(3) 
   8 3Q6W          ANDN  0  #200 
  18 3QLG          BNZ   0  PO4                 [BRANCH IF NAMED WORKTAPE
   9 3R66          LDX   0  HDREC(3) 
   8 3RKQ          SBN   0  10   
   8 3S5B          BNG   0  PE1  
   7 3SK2          ADN   0  4
  18 3T4L          STO   0  HDREC(3)            [INSERT REDUCED LENGTH   
   8 3TJ=          LDX   0  ACES 
   9 3W3W          SMO      HDREC(3) 
   9 3WHG          STO   0  A1+5(3)  
  18 3X36          NGN   0  #101                [REMOVE  LANG CODE BIT   
   9 3XGQ          ANDS  0  ATYPE(3) 
   4 3Y2B    PO1 
  11 43XB          OPEN     PG4,READ,QUERY   
   6 44C2    XFSHNBOPEN  
   9 45B=          TESTREP  OK,P03   
   9 45TW          TESTERR  NAME,TERR
  10 46*G          TESTREP  NOFILE,PE4   
  10 46T6          TESTREP  VRYWRONG,PE4 
  10 47#Q          TESTREP  NOUSER,PE4   
   9 47SB          GEOERR   1,REPLY  
   4 48#2    P03 
  19 48RL          MHUNT    2,FILE,FABSNB       [THE FABSNB NEEDS TO BE SET  
  20 49?=          LDN   0  6                   [TO CONTAIN :USER.LOCALNAME  ONLY
  19 49QW          ADS   0  A1(2)               [THIS IS FOR LATER USE WHEN  
  16 4==G          LDX   0  A1(2)               [UPDATING BUDGETS
   8 4=Q6          SBN   0  10   
   8 4?9Q          BZE   0  P02  
   7 4?PB          LDX   3  2
   7 4#92          TOPFCB   1
   8 4#NL          ADN   1  FME1 
   8 4*8=          ADN   2  A1+1 
   7 4*MW          MOVE  1  3
   8 4B7G          SMO      A1(3)
   9 4BM6          LDN   1  A1-6(3)  
   9 4C6Q          LDN   2  A1+4(3)  
  10 4CLB          LDX   5  ALOGLEN(3)   
   8 4D62          SBX   5  A1(3)
   7 4DKL          ADN   5  6
   7 4F5=          SMO      5
  19 4FJW          MOVE  1  0                   [MOVE LOCAL NAME UP FABSNB   
   7 4G4G          ADN   5  4
   8 4GJ6          LDN   1  10   
   8 4H3Q          STO   1  A1(3)
  11 4HHB          ALTLEN   3,5,FILE,FABSNB  
   4 4J32    P02 
  21 4JGL          TOPFCB   3                   [X3 POINTS TO DIRECTORY'S FILE-FCB   
  21 4K2=          JFREZFCB 3,PERR2             [TEST FOR FROZEN DIR & FREEZE PVLGE  
  19 4KFW          OWNERCHECK                   [CHECK OUT OWNERSHIP OF TAPE 
   4 4K_G    P03B
   7 4LF6          PICKREP  3
  16 4LYQ          CLOSETOP                     [CLOSE DIRECTORY 
   7 4MDB          PUTREP   3
  16 4MY2          TESTREP  OK,P03D             [OWNERSHIP OK?   
   7 4NCL          REPALLER  
  15 4NX=          BRN      TERR                [J TO ENDCOM 
   4 4PBW    P03D
  10 4PWG          MHUNT    3,FILE,FABSNB
   4 4QB6    PO4 
   7 4QTQ          LDN   0  1
  20 4R*B          ORS   0  ATYPE(3)            [NON-FILESTORE ENTRANT REQUIRED  
  11 4RT2    #     DOWN TO GET FOR NEW TAPE  
   9 4S#L          LDX   0  AWORK2(2)
  10 4SS=          STO   0  ACOMMUNE1(2) 
   7 4T?W    #SKIP K6GETON   
   9 4TRG          TRACE    2,GETONGT
   8 4W?6          DOWN     GET,3
   9 4WQQ          BRN      XBRKIN   
  18 4X=B          TESTREPNOT OK,PE             [J IF FAILED TO GET TAPE 
  16 4XQ2    #     GET UP REPLY BLOCK FOR SUBSEQUENT OPEN PERI   
  11 4Y9L          SETNCORE 10,2,FLIB,GOLMT  
   8 4YP=          STOZ     A1(2)
  16 4_8W          LDN   6  A1(2)               [ZEROISE BLOCK   
   8 4_NG          LDN   7  A1(2)
   7 5286          MOVE  6  9
  10 52MQ          MHUNT    3,FILE,FABSNB
   9 537B          LDX   0  ATYPE(3) 
   8 53M2          ANDN  0  #200 
  19 546L          BZE   0  PME1                [BRANCH IF NAMED WORK TAPE   
   9 54L=          LDN   6  WORK(1)  
   9 555W          LDN   7  A1+4(2)  
   7 55KG          MOVE  6  6
   8 5656          BRN      PME2 
   4 56JQ    PME1
   9 574B          LDX   6  HDREC(3) 
   7 57J2          ADN   6  2
  16 583L          TXU   6  ALOGLEN(3)          [GEOERR IF NO TSN
   8 58H=          BCS      PG2  
  10 592W    #     FILL  IN REPLY BLOCK  
   9 59GG          SMO      HDREC(3) 
   9 5=26          LDN   6  A1-6(3)  
   9 5=FQ          LDN   7  A1+4(2)  
  17 5=_B          MOVE  6  5                   [NAME,REEL,GENERATION
   9 5?F2          SMO      HDREC(3) 
   8 5?YL          LDX   6  A1(3)
   8 5#D=          ANDN  6  4095 
  16 5#XW          STO   6  A1+9(2)             [RETENTION PERIOD
   4 5*CG    PME2
   9 5*X6          SMO      HDREC(3) 
   9 5BBQ          LDX   6  A1+1(3)  
  16 5BWB          STO   6  A1+3(2)             [SERIAL NUMBER   
   7 5CB2          LDCT  6  5
  15 5CTL          ORN   6  #400                [TYPE/MODE   
   9 5D*=          STO   6  A1+2(2)  
  15 5DSW          FREECORE 3                   [FREE FABSNB 
   9 5F#G          LDX   5  AWORK4(2)
  20 5FS6          ACROSS   OLMTA,2             [GO TO CONNECT TO OBJECT PROGRAM 
   9 5G?Q    #     ERROR REPORTING   
  10 5GRB    PE    TESTREP  NOLOCN,PE1   
   9 5H?2          TESTREP  TSN,PE2  
   9 5HQL          TESTREP  NOTAV,PE3
   4 5J==    PQ  
  10 5JPW          TESTREPN ODRAWN,PQ1   
  11 5K9G          ERROR    BUDGODR,BUDGSPA  
   8 5KP6          BRN      TERR 
   4 5L8Q    PQ1 
  10 5LNB          TESTREPN INCREASE,PQ2 
  11 5M82          ERROR    BUDGINC,BUDGSPA  
   8 5MML          BRN      TERR 
   4 5N7=    PQ2 
  10 5NLW          TESTREP  ALREADY,TERR 
   9 5P6G          TESTERR  NAME,TERR
  10 5PL6          TESTREP  WRGEN,TERR   
  10 5Q5Q          TESTREP  VRYWRONG,TERR
   9 5QKB          TESTREP  TEMP,TERR
  10 5R52          TESTREP  NODECKS,PE5  
   9 5RJL          GEOERR   1,GETREP 
  17 5S4=    PE1   COMERR   AMTDESC,JLOCN       [SERIAL NUMBER GIVEN 
  17 5SHW    PE2   COMERR   AMTDESC,JNTSN       [LOCAL NAME NOT GIVEN
  10 5SN# ...PE3   HUNT     3,FILE,FABSNB
   8 5SSQ ...      LDCT  0  #200 
   9 5S_8 ...      ANDX  0  ATYPE(3) 
   8 5T5L ...      BZE   0  POOL 
   8 5T=4 ...      COMERR   JNWTA
  18 5TBG ...POOL  COMERR   JNPT                [POOL TAPE NOT AVAILABLE 
   4 5TH6    PE4 
   9 5W2Q          COMERR   ERNAME   
   9 5WGB    PE5   COMERR   ERNODECKS
   4 5X22    PERR
  10 5XFL          TESTREPN ILLCOMB,PERR1
   5 5X_=    PILLC   
   9 5YDW          COMERR   ERILLCOMB
   5 5YYG    PERR1   
   9 5_D6          COMERR   ERILLMODE
   5 5_XQ    PERR2   
   7 62CB          CLOSETOP  
  21 62X2          ERRTEST  3,TERR,FROZEN,NOFPRIV   [ERROR MESS FOR FROZEN OR NO-F-PR
  18 63BL          GEOERR   1,GLFRREP               [ELSE GEORGE ERROR   
   8 63W=    #     BREAKIN EXITS 
   7 64*W    XBRKIN COMBRKIN 
   7 64TG    #     CONSTANTS 
   8 66#B    TMT            +5   
   9 67?L    TADJ           +AGIVE   
   8 69=6    TMSK           #74  
  10 6=9B    WORK           12HWORK TAPE 
   8 6=P2                   0,0  
   8 6?N=    #     GEORGE ERRORS 
  11 6#MG    PG2   GEOERR   BRIEFPM,SHORTBLK 
  11 6*LQ    PG4   GEOERR   BRIEFPM,BREAKIN  
  11 6B6B          MENDAREA  1023-0?,K99GETON
   4 6BL2    #END
   6 ____ ...546123420002
