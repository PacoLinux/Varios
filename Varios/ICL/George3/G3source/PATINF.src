  14 22FL    #SEG  PATINF                       [G.OGSTON
   7 22_=    #OPT  K0PATINF=0
   7 23DW    #LIS  K0PATINF  
   7 23YG          8HPATINF  
   4 24D6    #   
  21 24XQ    #     THIS CHAPTER EXTRACTS THE PFE HEADER AND DEVICE START ADDRESSES   
  19 25CB    #     FOR THE CHAPTERS BEING PATCHED (THESE CHAPTERS HAVED BEEN 
  20 25X2    #     MENDITED ONTO THE PROGRAM FILE BUT THEIR CHAPTAB ENTRIES HAVE 
  12 26BL    #     NOT BEEN UPDATED BY NEWCHENT) 
  21 26W=    #     THE GAP AREA IS MENDED AT MENDIT TIME TO INCLUDE, FOR THE CHAPTER 
  13 27*W    #     BEING MENDITED, THE FOLLOWING:-   
  13 27TG    #              ADDRESS         CONTENTS 
  13 28*6    #              -------         -------- 
  12 28SQ    #              POINT         CHAPTER
  13 29#B    #              POINT+1       NAME AND   
  14 29S2    #              POINT+2       VERSION NUMBER 
  20 2=?L    #              POINT+3       LENGTH(B1-B10), CHAPTER NUMBER(B11-B23)
  16 2=R=    #              POINT+4       DEVICE START ADDRESS   
   4 2?=W    #   
  20 2?QG    #     THE UNIVERSAL POINT IS THE ADDRESS OF THE NEXT FREE WORD IN   
  20 2#=6    #     THE GAP AREA AND IS UPDATED AT THE END OF THE MEND. NGNUM IS  
  16 2#PQ    #     THE NUMBER OF CHAPTERS STORED IN THE GAP AREA 
   4 2*9B    #   
   4 2*P2    #   
  12 2B8L          SEGENTRY  K1PATINF,QK1PATINF  
  12 2BN=          SEGENTRY  K2PATINF,QK2PATINF  
   4 2C7W    #   
   4 2CMG    #   
   4 2D76    #   
  18 2DLQ    #DEF  SAPFE=AWORK2                 [POINTER TO EWDAS/APFE   
  19 2F6B    #DEF  PARANUM=AWORK3               [NUMBER PARAMERTRS IN COMMAND
  20 2FL2    #DEF  ANGC=0                       [NUMBER OF CHAPTER DESCRIPTIONS  
  15 2G5L                                       [IN GAP AREA 
   4 2GK=    #   
   4 2H4W    #   
  14 2HJG    SPACE           #20202020          [4 SPACES
  17 2J46    WD2                #01000004       [WORD 2 OF PFE HEADER
   6 2JHQ    QK1PATINF   
  10 2K3B          LDX   0  PARANUM(2)   
   9 2KH2          SMO      SAPFE(2) 
  20 2L2L          STO   0  APFEB1+1            [NUMBER OF OVERLAYS BEING EDITED 
  14 2LG=          LDX   0  WD2(1)              [SET UP  
  14 2L_W          SMO      SAPFE(2)            [WORD2 OF
  15 2MFG          STO   0  APFEB1+2            [PFE HEADER  
   9 2M_6          LDX   0  NGNUM(1) 
  20 2NDQ          BZE   0  XERR17              [ERROR IF NO STORED DESCRIPTIONS 
  16 2NYB          STOZ     ACOMMUNE1(2)        [ZERO ERROR FLAG 
   9 2PD2          SMO      SAPFE(2) 
  20 2PXL          STOZ     APFEB2              [ZERO NO. OF WORDS IN ERROR BLOCK
  20 2QC=          STOZ     7                   [ZERO NO. OF PARAMETERS PROCESSED
  20 2QWW          BRN      PFP                 [SKIP PARAPAS FOR 1ST PARAMETER  
  17 2RBG    NXPRM PARAPASS                     [GET NEXT PARAMETER  
  10 2RW6    PFP   MHUNT    3,CPB,CUNI   
  17 2S*Q          LDX   0  ANUM(3)             [TEST PARAMETER TYPE 
  14 2STB          BZE   0  XERR3               [...NULL 
  16 2T*2          BNG   0  XERR3               [...NON-EXISTENT 
   8 2TSL          SRL   0  12   
  20 2W#=          BNZ   0  XERR3               [...WRONG TYPE (I.E. NOT SIMPLE) 
   8 2WRW          LDX   1  FX1  
  19 2X?G          LDX   5  NGNUM(1)           [NUMBER OF STORED DESCRIPTIONS
   9 2XR6          LDX   6  ANUM(3)  
  21 2Y=Q          ANDN   6 #7777               [NUMBER OF CHARACTERS IN PARAMETER   
   7 2YQB          ADN   6  3
  19 2_=2          SRL   6  2                   [NUMBER OF WORDS IN PARAMETER
  10 2_PL          STO   6  ACOMMUNE2(2) 
   9 329=          LDX   6  NGNUM(1) 
   7 32NW          SLL   6  2
   9 338G          ADX   6  NGNUM(1) 
   7 33N6          SBN   6  5
   7 347Q          ADX   1  6
  10 34MB    NXDS  LDX   6  ACOMMUNE2(2) 
  16 3572          STOZ     4                   [ZERO MODIFIER   
   7 35LL    NXWD  SMO      4
  19 366=          LDX   0  XGAP(1)             [NEXT WORD OF CHAPTER NAME   
   7 36KW          SMO      4
  21 375G          TXU   0  APARA(3)            [COMPARE WITH NEXT WORD IN PARAMETER 
   8 37K6          BCS      NTFND
  16 384Q          ADN   4  1                   [UPDATE MODIFIER 
   8 38JB          BCT   6  NXWD 
   8 3942          BRN      PFND 
  20 39HL    NTFND SBN   1  5                   [UPDATE ADDRESS WITHIN GAP AREA  
   8 3=3=          BCT   5  NXDS 
  17 3=GW          BRN      XERR18              [PARAMETER NOT FOUND 
  10 3?2G    PFND  LDX   0  ACOMMUNE1(2) 
   8 3?G6          BNZ   0  RNC  
   9 3?_Q          LDX   3  SAPFE(2) 
  18 3#FB          LDX   0  XGAP+3(1)           [STORE PFE HEADER ENTRY  
  13 3#_2          SMO      7                   [IN  
  15 3*DL          STO   0  APFEB1+4(3)         [EWDAS/APFE  
  19 3*Y=          LDX   0  XGAP+4(1)           [STORE DEVICE START ADDRESS  
  13 3BCW          SMO      7                   [IN  
  15 3BXG          STO   0  APFEB4(3)           [EWDAS/APFE  
   7 3CC6    RNC   ADN   7  1
  20 3CWQ          TXU   7  PARANUM(2)          [TEST IF ALL PARAMETERS PROCESSED
   8 3DBB          BCS      NXPRM
   8 3DW2          BRN      RUP  
  15 3F*L    RETUP STO   0  ACOMMUNE1(2)        [ERROR FLAG  
  16 3FT=    RUP   UP                           [RETURN TO PATCH 
   7 3G#W    XERR3 LDN   0  3
   8 3GSG          BRN      RETUP
   5 3H#6    XERR17  
   8 3HRQ          LDN   0  17   
   8 3J?B          BRN      RETUP
   5 3JR2    XERR18  
   8 3K=L          LDN   0  18   
  10 3KQ=          STO   0  ACOMMUNE1(2) 
   9 3L9W          SMO      SAPFE(2) 
   9 3LPG          LDX   6  APFEB2   
   9 3M96          ADX   6  SAPFE(2) 
   9 3MNQ          ADN   6  APFEB2   
  21 3N8B          ADN   6  1                   [ADDR OF NEXT FREE LOC IN ERROR BLOCK
  13 3NN2          SMO      FX1                 [FILL
  13 3P7L          LDX   5  SPACE               [NEXT
  14 3PM=          LDX   1  6                   [THREE   
  14 3Q6W          STO   5  0(1)                [WORDS   
  13 3QLG          STO   5  1(1)                [WITH
  14 3R66          STO   5  2(1)                [SPACES  
   9 3RKQ          LDN   5  APARA(3) 
  16 3S5B          SMO      ACOMMUNE2(2)        [MOVE BAD CHAPTER
  16 3SK2          MOVE  5  0                   [INTO ERROR BLOCK
   7 3T4L          LDN   0  3
   9 3TJ=          SMO      SAPFE(2) 
  20 3W3W          ADS   0  APFEB2              [UPDATE POINTER TO NEXT FREE LOC 
   8 3WHG          BRN      RNC  
   2 3X36
   4 3XGQ    #   
   4 3Y2B    #   
  21 3YG2    #              THIS ENTRY POINT IS REACHED FROM THE PATCH ERROR HANDLING
  20 3Y_L    #              CODE. THE PURPOSE OF THE FOLLOWING CODE IS TO EXTRACT
  21 3_F=    #              THE NAMES OF ALL PFE OR PATCH CR CHAPTERS THAT ARE LINKED
  20 3_YW    #              TO ACTIVITIES DURING THE EXECUTION OF A PATCH COMMAND
   4 42DG    #   
   4 42Y6    #   
   4 43CQ    #   
   4 43XB    #   
   6 44C2    QK2PATINF   
   9 44WL          SMO      SAPFE(2) 
  20 45B=          STOZ     APFEB2              [ZERO NO OF WORDS IN ERROR BLOCK 
   9 45TW          SMO      SAPFE(2) 
  18 46*G          LDX   7  APFEB3              [NUMBER OF BAD CHAPTERS  
   9 46T6          SMO      SAPFE(2) 
   9 47#Q          LDN   3  APFEB3   
  19 47SB    NBCHP ADN   3  1                   [ADDRESS OF NEXT BAD CHAPTER 
  17 48#2          LDX   0  0(3)                [BAD CHAPTER NUMBER  
   8 48RL          LDX   1  FX1  
  20 49?=          LDX   5  NGNUM(1)           [NO OF STORED CHAPTER DESCRIPTIONS
   7 49QW          SBN   1  2
  16 4==G    NPFE  ADN   1  5                   [UPDATE POINTER  
  16 4=Q6          LDX   6  XGAP(1)             [NEXT PFE ENTRY  
  16 4?9Q          ANDN   6  #7777              [CHAPTER NUMBER  
  20 4?PB          TXU   6  0                   [CHECK IF THIS IS A BAD CHAPTER  
   8 4#92          BCC      PBCF 
   8 4#NL          BCT   5  NPFE 
  21 4*8=    PBCF  LDN   5  XGAP-3(1)           [POINT TO CHAPTER NAME, VERSION NO.  
   9 4*MW          SMO      SAPFE(2) 
   9 4B7G          LDX   6  APFEB2   
   9 4BM6          ADX   6  SAPFE(2) 
   9 4C6Q          ADN   6  APFEB2   
  20 4CLB          ADN   6  1              [START OF CHAPNAME TRANSFER ADDRESS   
  21 4D62          MOVE  5  3                    [MOVE IN CHAPNAME , VERSION NUMBER  
   7 4DKL          LDN   0  3
   9 4F5=          SMO      SAPFE(2) 
  19 4FJW          ADS   0  APFEB2              [UPDATE NO OF STORED WORDS   
   8 4G4G          BCT   7  NBCHP
  15 4GJ6          UP                           [UP TO PATCH 
   8 4H3Q    NGNUM         +ANGC 
   4 4HHB    XGAP
   5 4J32    POINT   
  18 4JGL    #ALT ANGNUM = NGNUM             [SET UNIVERSAL FOR MENDING  
   8 4K2=    #ALT AFPOINT =POINT 
   7 4KFW    #GAP  1023-0?   
   4 4K_G    #END
   8 ____ ...57567736001400000000
