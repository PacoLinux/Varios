  14 22FL    #LIS  K0JOBLIST>K0HLS>K0GREATGEO>K0ALLGEO   
  12 22MW ...      SEG   JOBLIST,867,SECTION CENT
   4 22W6 ...[   
  15 234B ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 23=L ...[   
   7 23DW    #OPT  WELLTEST=0
   4 24D6    [   
   9 24XQ    [     ENTRY    POINTS   
   4 25CB    [   
  16 25X2          SEGENTRY K1JOBLIST,XK1       [APPEND RECORD   
  16 26BL          SEGENTRY K2JOBLIST,XK2       [DELETE RECORD   
  18 26W=          SEGENTRY K3JOBLIST,XK3       [DELETE RECORD X6 = JOBNO
  17 27*W          SEGENTRY K4JOBLIST,XK4       [ DELETE A RECORD... 
   4 27TG    [   
  16 28*6    [     USED IN ABANDON BEFORE USER CONTEXT IS GAINED 
  13 28SQ    [     EG BREAKIN DURING WAIT ON LOGIN   
   4 29#B    [   
  18 29S2          SEGENTRY K5JOBLIST,XK5       [ DELETE USER ENTRY ONLY 
   4 2=?L    [   
  12 2=R=    [      USING A JWELL,COPYSYS BLOCK  
   4 2?=W    [   
   4 2?*F ...[   
  18 2?D4 ...[     THIS ENTRY POINT IS USED BY THE ABANDON(TEMP) COMMAND 
   4 2?GM ...[   
  10 2?K= ...      SEGENTRY K6JOBLIST,XK6
  10 2?LH ...      SEGENTRY K7JOBLIST,XK7
  10 2?MC ...      SEGENTRY K8JOBLIST,XK8
  10 2?MM ...      SEGENTRY K9JOBLIST,XK9
  17 2?MQ ...      SEGENTRY K10JOBLIST,XK10   [USED BY JLASLOST MACRO
   4 2?MT ...[   
  10 2?QG          SEGENTRY K99JOBLIST   
   4 2#=6    [   
   4 2#PQ    [   
  14 2#_J ...      BITDEFS  AWORK4,0,TEMP,POPPED,MONEY   
  13 2*6Q ...      BITDEFS  AWORK4,3,XFROZEN,XJLCH   
   4 2*9B    [   
  11 2*P2    #DEF  SZAJDF=JDFLANG-JOBCURDIR+3
   7 2B8L    #DEF  RRH=A1+FRH
   8 2BN=    XTAB           +IVTP
   8 2C7W                   +IVLP
   8 2CMG                   +IVCP
   9 2D76    MASQ           #20000000
   8 2DLQ    PCON        #700000 
   7 2F6B    N512        +512
   8 2FL2    MSECS          1000 
   4 2G5L    [   
   4 2GK=    [   
   8 2H4W    #DEF  SIZELIST=10   
   9 2HJG    XJOBLIST       +SIZELIST
   8 2J46                   12H  
  10 2JHQ                   12HJOBLIST   
   7 2K3B                   1
   7 2KH2                   1
   9 2L2L                   4H****   
   9 2LG=    TLANG          4HB5B1   
   8 2LQ3 ...STYLE          #31  
   8 2LSL ...               #32  
   4 2L_W    [   
   4 2MFG    [   
   4 2M_6    [   
   4 2NDQ    [   
   4 2NYB    [   
   4 2TSL    [   
  20 2W#=    [   THIS SEGMENT SETS UP THE ENTRY FOR JOBDATA IN THE SYSTEM/JOBLIST
   6 2WRW    [     FILE. 
  17 2X?G    [   THIS SEGMENT IS MAINLY USED FOR CREATING THE SYSTEM 
  20 2XR6    [     AND USER JOBLIST ENTRIES. THE USER JOBLIST ENTRY IS WRITTEN   
  19 2Y=Q    [     WITH THE ASSUMPTION THAT THE FILE IS OPEN AND POSITIONED  
  19 2YQB    [      CORRECTLY FROM A PREVIOUS GETJOB....  THE SYSTEM JOBLIST 
  15 2_=2    [     ENTRY IS LEFT IN CORE AS A FILE/FRB BLOCK 
   4 2_PL    [   
   4 329=    [   
   4 32NW    [   
   4 338G    XK1 
  10 33D? ...      LDX   0  ACOMMUNE2(2) 
  20 33GW ...      DEX   0  AWORK1(2)         [SAVE JD PARAM NO., BUT KEEP B0 !! 
  11 33N6          MHUNTW   3,JWELL,COPYSYS  
   8 347Q          ADN   3  RRH  
   7 34MB          NEWJBNUM 5
  10 3572          STO   5  JLJOBNO(3)   
   9 35LL          TRACE    5,JOBLIST
   4 366=    [   
   4 36KW    [   
  16 375G          LDX   4  CLEANCT             [ ARRIVAL TIME   
   7 37K6          LDN   5  0
   8 384Q          LDN   6  K9   
   7 38JB          MPY   4  6
   9 3942          DVR   4  MSECS(1) 
  16 39HL          STO   5  JLARRIVET(3)        [ IN JOB BLOCK   
   4 3=3=    [   
  14 3=GW          LDX   5  EDATE               [ ARRIVAL
  14 3?2G          STO   5  JLARRIVED(3)        [ DATE   
   4 3?G6    [   
  19 3?_Q          LDN   4  CPREFIX(2)          [ NO-USER CONTEXT USERNAME   
  10 3#FB          LDX   0  CONTEXT(2)   
   7 3#_2          ANDN  0  1
   8 3*DL          BZE   0  XM   
   6 3*FX ...#UNS  G400  
   4 3*H8 ...(   
   7 3*JF ...      LDX   4  2
  11 3*KQ ...      HUNTW    2,IUSER,G400USER 
   9 3*M3 ...      BNG   2  X1HOOK   
   8 3*N# ...      LDN   4  A1(2)
   8 3*PK ...      LDX   2  FX2  
   8 3*QW ...      BRN      XM   
   5 3*S7 ...X1HOOK  
   7 3*TD ...      LDX   2  4
   4 3*WP ...)   
   7 3*Y=          FJOCA    2
  18 3BCW          LDN   4  JUSER(2)            [ USER CONTEXT USERNAME  
   8 3BXG          LDX   2  FX2  
   9 3CC6    XM    LDN   5  JLUSER(3)
   7 3CWQ          MOVE  4  3
   4 3DBB    [   
  18 3DW2          LDX   0  JSOURCE3(2)         [ CONSOLE PROPERTY NUMBER
  10 3F*L          STO   0  JLPROPNO(3)  
   4 3FT=    [   
   7 3G#W          JOBSOURCE 
  11 3GSG          MHUNTW   3,JWELL,COPYSYS  
   7 3H4? ...      LDN   5  0
   6 3H6W ...SETASSOCS   
   7 3H#6          JLADJUST 3
  11 3HRQ          HUNT     2,APERI,ASSOCS   
  18 3J?B          BNG   2  NASS                [ J IF NO ASSOCIATIONS   
  19 3JR2          LDN   1  JLASSOCS(3)         [ ADDRESS FOR ASSOCIATIONS   
  16 3K=L          LDX   4  A1(2)               [ NO OF ASSOCS   
   8 3KQ=          BZE   4  NASS1
   9 3L9W    XASN  LDN   2  A1+1(2)  
   8 3LPG    XASNX LDX   3  FX1  
   7 3M96          LDN   7  2
   4 3MNQ    [   
  15 3N8B    [     LOOK FOR PERIPHERAL TYPE OF ASSOCIATION   
   4 3NN2    [   
   4 3P7L    XAS 
   7 3PM=          SMO      7
   9 3Q6W          LDX   0  XTAB(3)  
   8 3QLG          ANDX  0  0(2) 
   8 3R66          BZE   0  XNOAE
   4 3RKQ    [   
  14 3S5B    [     TYPE FOUND SO ENTER IN JOBLIST RECORD 
   4 3SK2    [   
   7 3T4L          SMO      7
   8 3TJ=          LDX   0  0(1) 
   4 3W3W    [   
  17 3WHG    [     ONLY ACCPET THEFIRST ASSOCIATION FOR A PROPERTY   
   4 3X36    [   
   8 3XGQ          BNZ   0  XNOAE
   8 3Y2B          LDX   0  1(2) 
   7 3YG2    [     PROPERTY  
   7 3Y_L          SMO      7
   8 3_F=          STO   0  0(1) 
   7 3_YW    XNOAE SBN  7  1 
   4 42DG    [   
  10 42Y6    [     CHECK OTHER PERI TYPES
   4 43CQ    [   
   8 43XB          BPZ   7  XAS  
   4 44C2    [   
  12 44WL    [     FOLLOWED BY OTHER PROPERTIES  
   4 45B=    [   
   9 45TW          ADN   2  ASSOCSLEN
   8 46*G          BCT   4  XASNX
   4 46T6    [   
  11 47#Q    [     LEAVE BLOCK AS AT START   
   4 47SB    [   
  11 48#2          MHUNT    3,JWELL,COPYSYS  
   7 48RL          JLADJUST 3
   8 49?=    NASS1 LDX   1  FX1  
   8 49QW    NASS  LDX   2  FX2  
   8 4=CN ...      BNZ   5  (5)  
   4 4=Q6    [   
  10 4?9Q          LDX   0  JSOURCE3(2)  
   8 4?PB          BZE   0  NOTR 
  15 4#92          BS       3,JLBCLUS           [ REMOTE BIT 
   4 4#NL    [   
   9 4*8=    NOTR  LDX   4  AWORK2(2)
  16 4*MW          BPZ   4  NOTM                [ J IF NOT MOP   
  15 4B7G          BS       3,JLBMOP            [ SET MOP BIT
   7 4BM6          ADN   4  1
   8 4C6Q          BZE   4  SLN  
  15 4CLB          BS       3,JLBDC             [ MUST BE DC 
   8 4D62          BRN      SALL 
  16 4DKL    SLN   BS       3,JLBLN             [ SET LOGIN BIT  
   8 4F5=          BRN      SALL 
   8 4FJW    NOTM  BZE   4  SJB  
  13 4G4G          BS       3,JLBRJ             [ RJ 
   8 4GJ6          BRN      SALL 
  13 4H3Q    SJB   BS       3,JLBJB             [ JB 
  10 4HHB    SALL  LDXC  0  CONTEXT(2)   
  18 4J32          BCC      SOUTB               [ J IF NOT SYSTEM ISSUED 
  10 4JGL          BS       3,JLBSYSIS   
  17 4K2= ...SOUTB LDX   0  CLONG1(2)           [ DEAL WITH B/S PRIVS
   9 4KFW          ANDX  0  MASQ(1)  
   8 4K_G          BZE   0  SOUT 
  10 4LF6          BS       3,JLBSPRIV   
   4 4LYQ    SOUT
   4 4QTQ    [   
   4 4R*B    [   
  13 4RT2    [   CREATE JWELL/ACLB FROM CURRENT CLB  
   4 4S#L    [   
   8 4SS=          MHUNT    3,CLB
  17 4T?W          LDX   7  ANUMCHA(3)          [ COMMAND CHAR LENGTH
   8 4TRG          ADN   7  11   
  20 4W?6          SRL   7  2                   [ ROUND UP ONE WORD PLUS RED TAPE
  11 4WQQ          SETUPCORE 7,3,JWELL,ACLB  
  16 4X=B          STO   7  RRH(3)              [ RECORD LENGTH  
  19 4XQ2          LDX   0  GSIGN               [ FOR INDEXING RECOGNITION   
   9 4Y9L          STO   0  A1+1(3)  
   8 4YP=          LDX   0  ACES 
   7 4_8W          SMO      7
  18 4_NG          STO   0  RRH-1(3)            [ SPACE-FILL LAST WORD   
   8 5286          HUNT     2,CLB
   9 52MQ          LDN   4  CLBFIR(2)
   9 537B          LDN   5  RRH+2(3) 
  16 53M2          LDX   7  ANUMCHA(2)          [ CHARS TO MOVE  
   9 546L    XL    TXL   7  N512(1)  
  18 54L=          BCC      X512                [ J IF > 512 CHARS STILL 
  14 555W          SMO      7                   [ < 512  
   7 55KG    X512  MVCH  4  0
   8 5656          SBN   7  512  
   8 56JQ          BZE   7  OUT  
  17 574B          BPZ   7  XL                  [ LOOP IF MORE TO DO 
   4 57J2    OUT 
  11 583L          HUNT     2,ADATA,CSTORE   
  18 58H=          BNG   2  WRTIT               [ J IF NO RESIDUAL PARAMS
  11 592W          MHUNTW   3,JWELL,COPYSYS  
  10 59GG          LDX   4  ALOGLEN(3)   
  10 5=26          ADX   4  ALOGLEN(2)   
   7 5=9Y ...      SBN   4  1
   8 5=FQ          BRN      STR  
   4 5=_B    [   
  17 5?F2    RHUNT MHUNTW   2,JWELL,COPYSYS     [ ROUTINE FOR ALTLEN 
   7 5?YL          EXIT  1  0
   4 5#D=    [   
   9 5#XW    STR   ALTLENG  3,4,RHUNT
  11 5*CG          MHUNTW   2,JWELL,COPYSYS  
   7 5*X6          JLADJUST 2
  11 5BBQ          MHUNTW   3,ADATA,CSTORE   
  10 5BWB          LDN   5  JLRESTF+1(2) 
   9 5CB2          LDN   4  A1+2(3)  
   8 5CTL          LDX   6  A1(3)
   7 5D*= ...      SBN   6  1
   4 5DSW    [   
  11 5F#G    [     SIZE OF REMAINDER FIELD   
   4 5FS6    [   
  10 5G?Q          STO   6  JLRESTF(2)   
   7 5GRB          SMO      6
   8 5H?2 ...      MOVE  4  511  
   9 5HQL          LDN   0  JLRESTF  
  10 5J52 ...      ADX   0  JOBDATASIZE  
  10 5J==          STO   0  JLRESTPTR(2) 
  10 5JPW          SBX   2  JOBDATASIZE  
   8 5K9G          ADS   6  0(2) 
   7 5KF# ...      FREECORE 3
   4 5KP6    [   
   5 5L8Q    WRTIT   
  11 5L?M ...      MHUNTW   2,JWELL,COPYSYS  
   8 5L#L ...      ADN   2  A1   
   9 5L*K ...      LDX   0  JOBTIME  
   9 5LBJ ...      STO   0  JLJT(2)  
  10 5LCH ...      LDX   0  COREDEFAULT  
  16 5LDG ...      SEGENTRY K50JOBLIST      [FOR NEWNEEDS MACRO  
  10 5LFF ...      STO   0  JLMAXSIZE(2) 
  10 5LGD ...      HUNT     3,CPB,CMULTI 
  19 5LHC ...      BNG   3  NOJOBD           [NO CMULTI => NO JOBDATA PARAM  
  19 5LJB ...      DOWN     JOBDATA,3        [INCORPORATE JD IN COPYSYS BLOCK
  14 5LK* ...      BRN      XJDERROR         [J IF ERROR 
   5 5LL# ...NOJOBD  
   4 5LM* ...[   
  10 5LNB          WRITEJL  JWELL,COPYSYS
  11 5M82          HUNT     3,JWELL,JPARAMS  
   8 5MML          BPZ   3  WRTP 
   8 5N7=          CALL  7  SETJW
  11 5NLW          NAME     3,JWELL,JPARAMS  
  10 5P6G    WRTP  WRITEJL  JWELL,JPARAMS
  10 5PL6          HUNT     3,JWELL,AJDF 
   8 5Q5Q          BPZ   3  WJA  
   4 5QKB    [   
  11 5R52          MHUNT    3,JWELL,COPYSYS  
   7 5RJL          JLADJUST 3
  16 5S4=          JBC      WMK,3,JLBJB         [ J IF NOT JOB   
  12 5SHW          SETNCORE SZAJDF,3,JWELL,AJDF  
  15 5T3G          LDN   5  JDFNAME(3)          [ NAME OF JDF
  11 5TH6          MHUNT    2,JWELL,COPYSYS  
  12 5W2Q          LDN   4  A1+FRH+JLJOBNAME(2)  
   7 5WGB          MOVE  4  3
   8 5X22          LDX   2  FX2  
   7 5XFL          LDN   0  1
  17 5X_=          STO   0  JDFGEN(3)           [ GEN NO OF JDF FILE 
   9 5YDW          LDX   0  TLANG(1) 
  16 5YYG          STO   0  JDFLANG(3)          [ JDF LANG CODE  
  18 5_D6          LDN   4  CPREFIX(2)          [ AS JOB THIS IS USERNAME
   9 5_XQ          LDN   5  JDFDIR(3)
   7 62CB          MOVE  4  3
   4 62X2    WJA 
  10 63BL          LDN   4  CPREFIX(2)   
   6 63D* ...#UNS G400   
   4 63G4 ...(   
  10 63HR ...      HUNTW 2,IUSER,G400USER
   9 63KG ...      BNG   2  X2HOOK   
   8 63M9 ...      LDN   4  A1(2)
   5 63NY ...X2HOOK  
   8 63QM ...      LDX   2  FX2  
   4 63SB ...)   
  10 63W=          LDN   5  JOBCURDIR(3) 
   7 64*W          MOVE  4  3
  10 64TG ...      STOZ     JDFWFNAME(3) 
   9 65SQ          LDN   0  SZAJDF   
  15 66#B          STO   0  A1(3)               [ RECORD SIZE
   8 66S2          LDX   0  GSIGN
  17 67?L          STO   0  A1+1(3)             [ MAINTAIN INDEXING  
   8 67R=          BRN      WJW  
   4 6834 ...[   
  12 68=W    WMK   SETNCORE SZAJDF,3,JWELL,AJDF  
   8 68B2 ...      STOZ     A1(3)
   8 68F6 ...      LDN   4  A1(3)
   9 68J= ...      LDN   5  A1+1(3)  
   9 68MB ...      MOVE  4  SZAJDF-1 
   8 68QG          BRN      WJA  
   4 692# ...[   
  10 69=6    WJW   WRITEJL  JWELL,AJDF   
  10 69PQ          WRITEJL  JWELL,ACLB   
   7 6=9B          CLOSETOP  
   7 6=F8 ...      UPPLUS   1
   4 6?N=    [   
   4 6?R7 ...[   
   5 6?W4 ...XJDERROR
  16 6?X3 ...      CLOSETOP                    [CLOSE USR JOBLIST
  21 6?YF ...      LDEX  0  AWORK1(2)           [B0 OF AWORK1 MEANS 'NOWELL' GIVEN !!
  17 6?_Y ...      STO   0  ACOMMUNE2(2)      [RESTORE JD PARAM NO.  
   5 6#3W ...      UP
   4 6#7W    [   
   5 6#MG    SETJW   
   8 6*76          SBX   7  FX1  
  11 6*LQ          SETNCORE 2,3,JWELL,JPARAMS
   7 6B6B          LDN   0  2
   8 6BL2          STO   0  A1(3)
   8 6C5L          LDX   0  GSIGN
   9 6CK=          STO   0  A1+1(3)  
   8 6D4W          ADX   7  FX1  
   7 6DJG          EXIT  7  0
   4 6F46    [   
   4 6FHQ    [   
   4 6G3B    [   
   5 6GH2    SCRUBJOB
  11 6GTC ...      HUNT     3,JWELL,COPYSYS  
   9 6H7S ...      BNG   3  SCRUBNO  
  15 6HG=          LDX   6  A1+FRH+JLJOBNO(3)   [ JOB NUMBER 
   4 6H_W    [   
  17 6JFG    SCRUBNO                            [ JOB NUMBER SUPPLIED
   8 6J_6          SBX   7  FX1  
  16 6KDQ          GETJOB   6,SYSTEM            [ OBTAIN ENTRY   
   8 6KYB          TESTREP2 OK,S4
   9 6LD2          GEOERR   1,NO JOB 
  21 6LKQ ...S4    HUNT     3,JWELL,COPYSYS     [ CREATE JWELL BLOCK IF NOT ONE ABOUT
   8 6LRG ...      BPZ   3  S41  
   7 6L_= ...      READAGAIN 
  10 6M72 ...      MHUNT    3,FILE,FRB   
  11 6M#Q ...      NAME     3,JWELL,COPYSYS  
   7 6MC3 ...S41   JLADJUST 3
   7 6MC* ...      LDN   1  0
  15 6MCM ...      JBC      S4B,2,TEMP      [J IF NOT TEMP AB
   8 6MC_ ...      LDN   1   1   
  14 6MD? ...S4B   HLSINFORM SBIN,LOGOUT,6,1,,JLSTAT(3)  
  11 6MDK ...SBIN  MHUNTW   3,JWELL,COPYSYS  
   7 6MDX ...      JLADJUST 3
  11 6MF# ...      JBC         S41A,3,JLBMOP 
   9 6MHK ...      BC          2,TEMP
   4 6MKX ...S41A
  11 6ML8 ...      JBC      S41B,3,JLBRUNNING
  11 6ML? ...      JBS      S41B,3,JLBSYSIS  
  10 6MLB ...      LDX   0  CONTEXT(2)   
   8 6MLF ...      ANDN  0  #4000
  18 6MLJ ...      BZE   0  S41B            [ONLY INTERESTED IN F/S JOBS 
  10 6MLL ...      ERS   0  CONTEXT(2)   
   7 6MLM ...      LDN   0  2
  10 6MLN ...     ANDX  0   CONTEXT(2)   
  17 6MLP ...     BNZ   0   S41AA               [J IF NOUSER CONTEXT 
   8 6MLQ ...      FJOCA    1,2  
   8 6MLR ...      LDX   0  GSIGN
   9 6MLS ...      ERS   0  JURGE(1) 
   5 6MLT ...S41AA   
   7 6MLW ...      LDN   1  4
  10 6MLX ...      ANDX  1  CONTEXT(2)   
   7 6MLY ...      SRL   1  2
  10 6MMD ...      LDX   0  HLSCOUNT(1)  
   7 6MMF ...      SBN   0  1
   8 6MMG ...      BPZ   0  S41C 
  10 6MMH ...      GEOERR   0,HLSCOUNT   
  10 6MMJ ...S41C  STO   0  HLSCOUNT(1)  
   8 6MMK ...      FSHCODE  AORB 
   5 6MML ...SMACHB1 
  13 6MMM ...      BZE   1  S41D            [J IF MOP
  16 6MMN ...      JBS      S41B,,HLSBFS    [J IF HLS IN CONTROL 
   8 6MMP ...      BRN      S41E 
  10 6MMQ ...S41D  JBS      S41B,,HLSMFS 
   8 6MMR ...S41E  SMO      FX1  
   9 6MMW ...      LDX   0  STYLE(1) 
   7 6MM_ ...      FOX      0
   4 6MN4 ...S41B
  16 6PL3 ...      DELETE                       [ DELETE RECORD  
   8 6PMG ...      ADX   7  FX1  
   8 6PTB ...      LDX   2  FX2  
   7 6Q*2          EXIT  7  0
   4 6QSL    [   
   6 6R#=    SCRUBUSER   
  19 6RRW          SBX   7  FX1                 [ DELETE USER JOBLIST ENTRY  
  12 6RX2 ...      JMBS     S1A,2,MONEY,XFROZEN  
  13 6S?G          SETNCORE SIZELIST,3,FILE,FABSNB   
   8 6SR6          LDN   5  A1(3)
  10 6T=Q          LDN   4  XJOBLIST(1)  
  15 6TQB          MOVE  4  SIZELIST            [ INITIALISE 
   7 6W=2          ADN   5  1
  11 6WPL          MHUNTW   2,JWELL,COPYSYS  
  15 6X9=          LDN   4  A1+FRH+JLUSER(2)    [ USERNAME   
   7 6XNW          MOVE  4  3
   7 6Y8G    #SKI  WELLTEST  
   9 6YN6          RGTRACE  8,XGEO   
   8 6_7Q          SMO      FX2  
   9 6_MB          STOZ     AWORK2   
   4 6_QG ...[   
  17 6_TL ...      USEROPEN XGEO,GENERAL,NOERREP,UNTRAP,REPLY,FROZEN 
  11 72LL          TESTREP2 OK,S0,CANT,XESS  
   9 736=    ROR   GEOERR   1,JOBLIST
  21 73KW    XESS  LDX   0  AWORK1(2)           [ ERROR IF CANT AND JOBLIST NOT OPEN 
   9 745G ...      BZE   0  XAGAIN   
  17 74K6          STO   0  AWORK2(2)           [ REMEMBER TO REOPEN 
  18 754Q          CLOSETOP                     [ CLOSE :SYSTEM.JOBLIST  
   5 75#J ...XAGAIN  
  14 75JB          USEROPEN XGEO,GENERAL,NOERREP,UNTRAP  
  10 7642    S0    MFREE    FILE,FABSNB  
   8 7698 ...      BRN      S1   
   8 76BB ...S1A   FILEMOVE 1,0  
  11 76HL    S1    MHUNTW   3,JWELL,COPYSYS  
  12 773=          LDN   4  A1+FRH+JLJOBNAME(3)  
  17 77GW          GETJOB   4,USER              [ USER JOBLIST ENTRY 
   8 782G          TESTREP2 OK,S2
   9 78G6          GEOERR   1,NOJOB  
   4 78HT ...[   
  11 78KK ...S2    JMBAC    S22,2,TEMP,XJLCH 
   7 78P4 ...      STEPAGAIN 
  10 78SH ...      ADX   3  JOBDATASIZE  
  10 78WT ...      JBS      SJL,2,XJLCH  
  10 78Y2 ...      BC       3,JLBRUNNING 
  10 78Y_ ...      BS       3,JLBNOTCAND 
  10 78_Y ...      JBS      S45,2,MONEY  
  11 7926 ...      JBC      NFZ2,2,XFROZEN   
  10 7928 ...      BS       3,JLBFROZENNC
   8 792= ...      BRN      S46  
   4 792? ...[   
  11 792# ...SJL   MHUNTW   1,JWELL,COPYSYS  
   7 792B ...      JLADJUST 1
   9 792D ...      LDX   0  JLSTAT(1)
  17 792G ...      STO   0  JLSTAT(3)        [CHANGE USER JL ENTRY   
  10 792J ...      LDX   0  JLBUDGETS(1) 
  10 792L ...      STO   0  JLBUDGETS(3) 
   8 792N ...      BRN      S46  
   4 792P ...[   
   4 792Q ...NFZ2
  10 792X ...      BS       3,JLBABAND   
  11 7938 ...      MHUNTW   2,JWELL,COPYSYS  
   7 793? ...      JLADJUST 2
  11 793B ...      JBC      S46,2,JLBRUNNING 
  10 793F ...      BS       3,JLBABANDON 
   8 793W ...      BRN      S46  
   4 794* ...[   
  10 794T ...S45   BS       3,JLBHARDUP  
   4 795S ...S46 
  10 796B ...      SBX   3  JOBDATASIZE  
   8 796Y ...      STEPREWRITE   
  17 797G ...      JBS      S32,2,XJLCH      [J IF JUST CHANGING JL  
   7 797R ...      READAGAIN 
  10 798L ...      MHUNTW   3,FILE,FRB   
  10 799F ...      NAME     3,FILE,FWB   
   7 799J ...      LDN   4  2
   6 799M ...STEP  STEP  
   8 799Q ...      BCT   4  STEP 
   4 799R ...[   
   8 799T ...      JWWFUPDATE 3  
   8 799Y ...      STEPREWRITE   
   7 79=3 ...      LDN   4  2
   7 79=6 ...SBACK BACKSPACE 
   8 79=9 ...      BCT   4  SBACK
   8 79=C ...      BRN      S31  
   4 79#6 ...[   
   8 79*W ...S22   SKIP     ,3   
   7 79FB          LDN   4  4
  16 79_2    S3    DELETE                       [ AND DELETE IT  
   8 7=DL          BCT   4  S3   
  12 7=H6 ...S31   JMBAC    S32,2,MONEY,XFROZEN  
   8 7=T6 ...      FILEMOVE 0,1  
   8 7?3B ...      BRN      S6   
  17 7?9L ...S32   CLOSETOP                     [ CLOSE USER JOBLIST 
   9 7?CW          LDX   0  AWORK2(2)
   8 7?XG          BZE   0  S6   
   4 7#C6    [   
  10 7#WQ    [     REOPEN SYSTEM JOBLIST 
   4 7*BB    [   
  12 7*W2          OPENSYS     ,JOBLIST,GENERAL  
  10 7*_X ...S6    HUNTW    3,FILE,FWB   
   8 7B5S ...      BNG   3  S6A  
  10 7B9P ...      LDX   4  A1+JLJOBNO(3)
   9 7B*L ...      GETJOB   4,SYSTEM 
   6 7BFH ...      INSERT
   9 7BKD ...      MFREEW   FILE,FWB 
   8 7BP* ...S6A   ADX   7  FX1  
   7 7BT=          EXIT  7  0
  10 7C#W    XGEO  GEOERR   1,USER BRK   
   4 7CSG    [   
   4 7D#6    [   
   8 7DRQ    TRAP  SBX   4  FX1  
   9 7F?B          STO   4  AWORK1(2)
  10 7FH8 ...      JBS      ON,2,POPPED  
   3 7FR2 ...
  11 7G2S ...POPC  POP      POPC,,JWACCESS   
  11 7G=L    ON    OPENSYS  ,JOBLIST,GENERAL 
   9 7GQ=          TESTREP2 OK,OUTR  
  10 7H9W          GEOERR   1,JOBLIST?   
   8 7HPG    OUTR  ADX   4  FX1  
   7 7J96          EXIT  4  0
   4 7JNQ    [   
   4 7K8B    [   
  16 7KN2    [     DELETE RECORDS REFERRING TO THE CURRENT JOB   
   4 7L7L    [   
  17 7L#S ...XK2   STOZ     AWORK4(2)           [ MARKER FOR TEMP AB 
  11 7LG2 ...      JBC      XK22,2,EXABTEMP  
   9 7LM8 ...      BS       2,TEMP   
   4 7LSB ...XK22
   9 7M6W          LDX   6  JOBNO(2) 
   4 7SGQ    [   
  19 7T2B    XK3   LDN   5  0                   [ AS XK2 BUT SET ELSEWHERE   
  10 7TG2          LDXC  0  CONTEXT(2)   
   8 7T_L          BCC      XT   
  16 7WF=          NGN   5  1                   [ SYSTEM ISSUED  
   4 7WYW    [   
   4 7XDG    XT  
  18 7XY6    SYST  CALL  4  TRAP                [ POP AND OPEN JOBLIST   
   4 86?=    [   
  20 86QW    THEND CALL  7  SCRUBNO             [ DELETE :SYSTEM.JOBLIST ENTRY   
   4 87=G    [   
   4 87G# ...[   
  19 87Q6 ...SEND  CALL  7  SCRUBUSER           [ DELETE USER JOBLIST ENTRY  
   4 87_Y ...[   
  18 889Q ...      MHUNT    3,JWELL,COPYSYS     [ FOR JWUPDATE TO BE DONE
   7 88FJ ...      JLADJUST 3
  17 88PB ...      JBS      SYSEND,3,JLBSYSIS   [ J IF SYSTEM ISSUED 
   7 88_8 ...      LDN   4  1
  16 8992 ...      JBS      STEMP,2,TEMP        [ J IF TEMP AB   
  16 89DS ...      JBC      NOTDG,3,JLBNOTCAND  [ J IF JOB NOT DG
  16 89NL ...      STO   4  ACOMMUNE3(2)        [ DG UPDATE COUNT
  10 89YD ...      STOZ     ACOMMUNE2(2) 
  10 8=8= ...      DOWN     JWUPDATE,1   
   9 8=D4 ...      BRN      SYSEND   
   4 8=MW ...[   
  17 8=XN ...NOTDG JBS      SLIVE,3,JLBRUNNING  [ J IF JOB STARTED   
   9 8?7G ...      LDN   4  JWELLONE 
  17 8?C# ...SLIVE STO   4  ACOMMUNE2(2)        [ WELL/LIVE UPDATE   
  18 8?M6 ...      STOZ     ACOMMUNE3(2)        [ NO DG UPDATE THIS TIME 
  19 8?WY ...      ACROSS   JWUPDATE,2          [ CLOSE AND VOP BY JWUPDATE  
   4 8#6Q ...[   
   5 8#BJ ...STEMP   
   4 8#LB ...[   
  20 8#W8 ...      JBS      SYSEND,3,JLBNOTCAND [ NO UPDATE IF ALREADY NOT CAND  
  10 8*62 ...      STO   4  ACOMMUNE3(2) 
  17 8**S ...      JBS      RUNG,3,JLBRUNNING   [ J IF WAS RUNNING   
   9 8*KL ...      LDN   4  JWELLONE 
  10 8*TD ...RUNG  STO   4  ACOMMUNE2(2) 
  10 8B5= ...      DOWN     JWUPDATE,2   
   4 8B6N ...[   
  11 8B87 ...      MHUNTW   3,JWELL,COPYSYS  
  10 8B96 ...      LDX   7  JLJOBNO+A1(3)
   7 8B=5 ...      JLADJUST 3
  14 8B?4 ...      HLSINFORM SYSEND,NONCAND,7,,,JLSTAT(3)
   5 8B*4 ...SYSEND  
  12 8BBL ...      JMBS     UP,2,MONEY,XFROZEN   
   5 8BHH ...RETURN  
   7 8BJW ...      CLOSETOP  
   9 8BSN ...      VOP      ,JWACCESS
   5 8B_J ...UP    UP
   4 8C6D ...[   
  10 8C8B ...[     DELETE AN ENTRY....   
   4 8C=# ...[   
   9 8C## ...XK4   STOZ     AWORK4(2)
  17 8CJ6 ...      JBC      XK44,2,EXABTEMP     [ SET TEMP IF TEMP AB
   9 8CRY ...      BS       2,TEMP   
   8 8D3Q ...      BRN      XK44 
   4 8D5N ...[   
  20 8D7L ...[     TEMPORARY DELETE AN ENTRY - ENTRY FROM AB (NB ALREADY POPPED) 
   4 8D9J ...[   
   9 8D?J ...XK6   STOZ     AWORK4(2)
  16 8DBN ...      MBS      2,TEMP,POPPED       [ TEMP AB ENTRY  
  16 8DFS ...      BZE   5  XK44          [J IF FROM AB COMMAND  
   8 8DGM ...      BPZ   5  NFZ3 
   9 8DGY ...      BS       2,XFROZEN
   8 8DH9 ...      BRN      NFZ4 
   4 8DH* ...[   
   9 8DHG ...NFZ3  BS       2,MONEY  
   4 8DHR ...NFZ4
   8 8DN4 ...      BRN      XK44A
   4 8DNT ...[   
   9 8DPL ...XK7   STOZ     AWORK4(2)
   9 8DQC ...      BS       2,POPPED 
  18 8DR8 ...XK44  CALL  4  TRAP                [ POP AND OPEN JOBLIST   
  21 8F6X ...XK44A CALL  7  SCRUBJOB            [ USING JWELL/COPYSYS DELETE ENTRY   
   8 8FGL          BRN      SEND 
   4 8G2=    [   
   9 8GFW    XK5   STOZ     AWORK1(2)
   9 8GPN ...      STOZ     AWORK4(2)
  19 8G_G          CALL  7  SCRUBUSER           [ DELETE USER JOBLIST ENTRY  
  19 8HF6          UP                           [ GIVEN JWELL/COPYSYS BLOCK  
  11 8HHT ...[     ENTRY FOR JLASSOCS MACRO  
   9 8HJQ ...XK8   STOZ     AWORK4(2)
  18 8HJR ...      LDN   7  0                   [INDICATES JLASSOCS MACRO
   4 8HKJ ...XK8A
   8 8HKM ...      CALL  4  TRAP 
   9 8HLJ ...      LDX   6  JOBNO(2) 
   9 8HMF ...      GETJOB   6,SYSTEM 
   7 8HNB ...      READAGAIN 
  10 8HP? ...      MHUNTW   3,FILE,FRB   
  11 8HQ8 ...      NAME     3,JWELL,COPYSYS  
  18 8HQL ...      BNZ   7  XK8B                [J IF NOT JLASSOCS MACRO 
   9 8HR5 ...      CALL  5  SETASSOCS
   8 8HR6 ...      BRN      XK8C 
   4 8HR7 ...[   
   4 8HR8 ...XK8B
  13 8HR9 ...[     JLASLOST MACRO - SHOULD BE IN 'A' 
  16 8HR= ...[     ZEROISE ANY ASSOCIATIONS IN :SYSTEM.JOBLIST   
   7 8HR* ...      JLADJUST 3
  10 8HRB ...      STOZ     JLASSOCS(3)  
  10 8HRC ...      STOZ     JLASSOCS+1(3)
  10 8HRD ...      STOZ     JLASSOCS+2(3)
   4 8HRN ...[   
   4 8HRP ...XK8C
   9 8HRQ ...[     WRITE UPDATE AWAY 
  11 8HS2 ...      MHUNTW   3,JWELL,COPYSYS  
  10 8HSX ...      NAME     3,FILE,FWB   
   7 8HTS ...      REWRITE   
   9 8HWP ...      BRN      RETURN   
  14 8HXT ...[ KEEP USER AND SYSTEM JOBLISTS CONSISTENT  
   4 8HXX ...XK9 
   9 8HY2 ...      STOZ     AWORK1(2)
   9 8HY5 ...      STOZ     AWORK2(2)
   9 8HY8 ...      STOZ     AWORK4(2)
   9 8HY? ...      BS       2,XJLCH  
   9 8HYB ...      CALL  7  SCRUBUSER
   5 8HYF ...      UP
   4 8JD8 ...[   
   4 8JXS ...XK10
  11 8KCD ...[     ENTRY FOR JLASLOST MACRO  
  15 8KX4 ...[     THIS MACRO (USED BY DEWELL) ASSUMES THAT  
  13 8LBN ...[     IT IS ISSUED IN THE 'A' MACHINE   
   9 8MTJ ...      STOZ     AWORK4(2)
  18 8N*8 ...      LDN   7  1                   [INDICATES JLASLOST MACRO
   8 8NSS ...      BRN      XK8A 
   4 8SPS ...[   
   4 8T9D ...#END
   8 ____ ...07206162000300000000
