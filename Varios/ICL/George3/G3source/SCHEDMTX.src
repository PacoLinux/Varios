  13 227T ...  SEG SCHEDMTX,866,C.R.JOHNSON,SCHEDULER
   4 22*N ...[   
  15 22HH ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
   4 22PD ...[   
  12 22_=          SEGENTRY K1SCHEDMTX,X1SCHEDMTX
   4 23DW ...[   
   4 23YG ...[   
   4 24D6 ...[   
  11 29XX ...      BITDEFS AWORK2,0,WORK2B0  
   4 2=3S ...[   
   9 2?5L ...PAIR  PAIR     DTSTART,1
   4 2JRJ ...[   
  21 2K3B ...[   ******************************************************************  
   4 2K?8 ...[   
  18 2KH2 ...[   THIS SUBROUTINE SETS THE CHAIN ALTERED BIT IN ALL THE   
   7 2KQS ...[   FDT ENTRIES.
   4 2L2L    [   
   8 2LG=    SACT  LDX   2  BOLC 
  17 2MFG          LDX   1  A1(2)               [COUNT OF FDT ENTRIES
   8 2M_6          BZE   1  SACT2
   4 2N?H ...SET8
  18 2NKY ...      BS       2,BIFDTCA           [SET 'CHAIN ALTERED' BIT 
  15 2NYB          ADN   2  FDTLEN              [NEXT ENTRY  
   8 2PD2          BCT   1  SET8 
   7 2PXL    SACT2 EXIT  7  0
   4 2PXM ...[   
  21 2PXN ...[   ******************************************************************  
   4 2PXP ...[   
  15 2PXQ ...[     THIS SUBROUTINE LOOKS FOR AN FDT ENTRY.   
   4 2PXR ...[   
  12 2PXS ...XFDTENT                            [
  16 2PXT ...      FDTENT   3,6,NOENTRY         [J IF NO ENTRY   
  15 2PXW ...      EXIT  7  1                   [ENTRY FOUND 
  12 2PXX ...NOENTRY                            [
  16 2PXY ...      EXIT  7  0                   [ENTRY NOT FOUND 
   4 2PX_ ...[   
  21 2PY2 ...[   ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++  
   4 2PY3 ...[   
  18 2PY4 ...[     THIS SUBROUTINE CHECKS IF DUMPER REQUIRES ANY TAPES   
  14 2PY5 ...[     AND TAKES APPRPRIATE ACTION IF TRUE   
   4 2PY6 ...[   
  12 2PY7 ...XINCWORD                           [
  19 2PY8 ...      JBS      XDUMPST,,BMTSREQ    [J IF DUMPER REQUIRES TAPES  
  14 2PY9 ...      EXIT  7  0                   [CONTINUE
  18 2PY= ...XDUMPST                            [DUMPER REQUIRES TAPES   
  21 2PY? ...      DOWN     DTSUBSA,1           [GO DOWN TO DEAL WITH DUMPERS TAPES  
  19 2PY# ...      BRN      STARTOLCA           [RESTART OFFLINE CHAIN SCAN  
   4 2PY* ...[   
  21 2P_4 ...[   ******************************************************************  
   4 2P_R ...[   
  21 2Q2G ...[   THIS SUBROUTINE CHECKS THE FDT FOR BIT BIFDTJL - IF IT IS SET A DTA 
  21 2Q39 ...[        (DUMP TAPE ACTIVITY) IS CREATED FOR THE APPROPRIATE TSN,THE COR
  21 2Q3Y ...[        FDINCS BLOCK IS CHAINED AFTER IT,THE BIT IS CLEARED AND THE DTA
  13 2Q4M ...[        FPUT'ED ONTO THE ACTIVITY LIST.
   4 2Q5B ...[   
   6 2Q65 ...XFDTCHECK   
  18 2Q6# ...      SBX   6  FX1                 [ALLOW FOR COORDINATION  
  12 2Q6D ...      SMO      FX2                 [
  15 2Q6J ...      STO   6  AWORK1              [SAVE LINK   
  12 2Q6N ...XFDTSTART                          [
  17 2Q6S ...      LDX   3  BOLC                [POINT TO FDT BLOCK  
  19 2Q7H ...      LDX   3  A1(3)               [X3 => NO. OF ENTRIES IN FDT 
  19 2Q8= ...      BZE   3  XFDTEND             [J IF NO ENTRIES TO SERVICE  
  17 2Q8_ ...      SLL   3  2                   [FOUR WORDS PER ENTRY
  18 2Q9N ...      ADX   3  BOLC                [X3 => LAST ENTRY IN FDT 
  17 2Q=C ...      SBN   3  FDTLEN              [LAST ENTRY IS DUMMY 
  18 2Q?6 ...      LDN   2  1                   [X2 => COUNT OF FDINCS   
  12 2Q?T ...XFDTLOOP                           [
  15 2Q#8 ...      LDX   5  FDSN(3)             [X5 => TSN   
  21 2Q#J ...      JMBS  XFDTWRONG,3,BIFDTUW,BIFDTOW  [J IF TAPE IS WRONGED IN FDT   
  18 2Q*? ...      JBS   XFDTCD,3,BIFDTCL       [J IF TAPE CANTDO LOADED 
  18 2QB2 ...      JBS   XFDTFOUND,3,BIFDTJL    [J IF TAPE JUST LOADED   
  16 2QBP ...      JBC   XFDTLOAD,3,BIFDTO      [J IF TAPE LOADED
  19 2QCD ...      ADN   2  1                   [INCREMENT NUMBER OF FDINCS  
  12 2QD7 ...XFDTLOAD                           [
  16 2QDW ...      SBN   3  4                   [UPDATE MODIFIER 
  15 2QFK ...      TXL   3  BOLC                [END OF FDT? 
  19 2QG# ...      BCC      XFDTLOOP            [GO AND LOOK AT NEXT ENTRY   
  12 2QH3 ...XFDTEND                            [
  20 2QHB ...      BC       BOLC,BIFDTLMEF      [CLEAR 'LOOK AT FDT FIRST' BIT   
  14 2QHQ ...      LDX   2  FX2                 [RESET X2
  12 2QH_ ...      LDX   6  AWORK1(2)           [
  15 2QJ8 ...      ADX   6  FX1                 [RESET LINK  
  18 2QJF ...      EXIT  6  0                   [FINISHED CHECKING FDT   
  19 2QK8 ...XFDTFOUND                          [A TAPE HAS JUST BEEN LOADED 
  19 2QKX ...      BC       3,BIFDTJL           [CLEAR THE 'JUST LOADED' BIT 
  16 2QM* ...      SMO      FX2                 [X2 IS CORRUPT   
  19 2QN4 ...      STO   5  AWORK2              [AWORK2 => TSN FOR SETUPACT  
  17 2QNR ...      LDX   3  FX2                 [X3 => DTS ACTIVITY  
  12 2QPG ...XFDTHUNT                           [
  16 2QQ9 ...      HUNT2    3,FILE,FDINCS       [FIND NEXT FDINCS
  19 2QQY ...      BCT   2  XFDTHUNT            [J IF NOT CORRECT FDINCS YET 
  14 2QRM ...      LDX   2  FX2                 [RESET X2
  20 2QSB ...      CHAIN    3,2                 [RECHAIN CORRECT FDINCS AFTER DTS
  20 2QTS ...      CALL  7  SETUPACT            [SET UP DTA FOR TSN IN AWORK2,   
  19 2QWH ...                                   [CHAIN FDINCS AND FPUT DTA.  
  20 2QXK ...      BRN      XFDTSTART           [GO AND START SCANNING FDT AGAIN 
   4 2QYN ...[   
  20 2Q_C ...[   TAPE HAS BEEN 'CANTDO LOADED'  ...  SO SET UP A FCANTDO BLOCK   
   4 2R26 ...[   
  12 2R2T ...XFDTCD                             [
  18 2R3J ...      LDX   7  2                   [SAVE FDINCS COUNT IN X7 
  18 2R4? ...      JBS      XFCANT,BOLC,BIFDTCDPR [J IF FCANTDO PRESENT  
  18 2R52 ...      SETNCORE 7,1,FILE,FCANTDO    [SET UP A FCANTDO BLOCK  
  19 2R5P ...      BS       BOLC,BIFDTCDPR      [SET THE 'FCANTDO PRESENT'BIT
  19 2R6D ...      LDN   4  1                   [SET COUNT OF ENTRIES TO ONE 
  15 2R77 ...      STO   4  A1(1)               [STORE COUNT 
  16 2R7W ...      BRN      XFCANTOK            [FCANT NOW READY 
  19 2R8K ...XFCANT                             [INCREASE LENGTH OF FCANTDO  
  16 2R8Y ...      MHUNTW   1,FILE,FCANTDO      [X1 -> FCANTDO   
  20 2R9# ...      LDX   4  A1(1)               [X4 => NO. OF ENTRIES IN FCANTDO 
  18 2R=3 ...      ADN   4  1                   [INCREMENT NO. OF ENTRIES
  18 2R=Q ...      STO   4  A1(1)               [UPDATE NO. OF ENTRIES   
  16 2R?9 ...      BXU   4  ALOGLEN(1),XFCANTOK [J IF BIG ENOUGH 
  18 2R?N ...      ADN   4  8                   [INCLUDE A1 IN NEW LENGTH
  18 2R#7 ...      ALTLEN   1,4,FILE,FCANTDO    [INCREASE LENGTH OF BLOCK
  14 2R#L ...      SBN   4  8                   [RESET X4
  16 2R*5 ...      MHUNTW   1,FILE,FCANTDO      [REHUNT FCANTDO  
  15 2R*L ...XFCANTOK                           [FCANT NOW OK
  20 2RC4 ...      SMO      4                   [X4 CONTAINS NEW NO. OF ENTRIES  
  17 2RCR ...      STO   5  A1(1)               [STORE TSN IN FCANTDO
  18 2RDG ...      LDX   2  7                   [RESET FDINCS COUNT IN X2
  19 2RF9 ...      BRN      XFDTCONT            [NOW REMOVE REDUNDANT ENTRY  
   4 2RFY ...[   
  21 2RGM ...[   TAPE HAS BEEN WRONGED (OR CANTDO LOADED)  ...  SO REMOVE REDUNDANT E
   4 2RHB ...[   
  12 2RJ5 ...XFDTWRONG                          [
  20 2RJS ...      JMBAC    XFDTLOAD,3,BIFDTJL,BIFDTO [J IF DTA SERVICING TAPT   
  18 2RJY ...      LF       3,FDTFSNO,1         [X1 => FILESTORE NUMBER  
  17 2RK4 ...      BZE   1  XFDTCONT            [J IF HOST FILESTORE 
  16 2RK8 ...      ORX   5  GSIGN               [SET B0 OF TSN   
  18 2RK# ...      BRN      XFDTCD              [INSERT ENTRY IN FCANTDO 
  12 2RKH ...XFDTCONT                           [
  17 2RL= ...      LDX   1  FX2                 [X1 => DTS ACTIVITY  
  17 2RM# ...      STO   5  AWORK2(1)           [STORE TSN IN AWORK2 
  19 2RMT ...      BC       1,WORK2B0           [CLEAR B0 OF TSN IF NECESSARY
  12 2RNC ...XFDTFIND                           [
  16 2RP6 ...      HUNT2    1,FILE,FDINCS       [FIND NEXT FDINCS
  19 2RPT ...      BCT   2  XFDTFIND            [J IF NOT CORRECT FDINCS YET 
  14 2RQJ ...      LDX   2  FX2                 [RESET X2
  20 2RR? ...      CHAIN    1,2                 [RECHAIN CORRECT FDINCS AFTER DTS
  19 2RS2 ...      CALL  7  XTAPEM              [DECREMENT ADTCOUNT IN ALL   
  19 2RSP ...                                   [APPRPRIATE REQUEST BLOCKS   
  18 2RTD ...      MFREEW   FILE,FDINCS         [FREE FILE FDINCS BLOCK  
  19 2RW7 ...      LONGOFF                      [START SCAN DOWN OFLC AGAIN  
   4 2RWW ...[   
  14 2RXK ...[   NOW REMOVE RECORD FOR THIS TAPE FROM FDT
   4 2RY# ...[   
  15 2R_3 ...      LDX   1  BOLC                [X1 -> OFLC  
  18 2R_Q ...      LDX   2  A1(1)               [X2 => COUNT OF ENTRIES  
  19 2S2F ...      SBN   2  1                   [X2 => NEW COUNT OF ENTRIES  
  19 2S38 ...      STO   2  A1(1)               [STORE NEW COUNT OF ENTRIES  
  20 2S3X ...      BZE   2  XFDTEND             [J IF THERE WAS ONLY ONE ENTRY   
  14 2S4L ...      ADN   2  1                   [RESET X2
  15 2S5* ...      SMO      FX2                 [X2 CORRUPT  
  15 2S64 ...      LDX   4  AWORK2              [X4 => TSN   
  16 2S6R ...NEXTFDSN                           [NOW REFIND ENTRY
  16 2S7G ...      BXE   4  FDSN(1),REMOVE      [J IF ENTRY FOUND
  19 2S89 ...      ADN   1  4                   [THIS SCAN IS POINTLESS ...  
  21 2S8Y ...      BCT   2  NEXTFDSN            [ ... IT JUST SETS UP ACCUMULATORS AG
  17 2S9M ...      GEOERR   1,DTSTSKIP          [ENTRY DISSAPPEARED  
  12 2S=B ...REMOVE                             [
  17 2S?5 ...      SBN   2  1                   [IS ENTRY LAST ONE?  
  14 2S?S ...      BZE   2  XFDTSTART           [J IF YES
  16 2S#H ...      LDN   0  FDSN+4(1)           [X0 -> NEXT ENTRY
  18 2S*= ...      ADN   1  FDSN                [X1 -> REDUNDANT ENTRY   
  17 2S*_ ...      SLL   2  2                   [FOUR WORDS PER TSN  
  18 2SBN ...XNMOV LDEX  7  2                   [MOVE REST OF ENTRIES UP 
  21 2SCC ...      SMO      7                   [ALLOWANCE BEING MADE FOR > 512 WORDS
  17 2SD6 ...      MOVE  0  0                   [MOVE UP TO 512 WORDS
  20 2SDT ...      BNZ   7  RSID                [J IF 512 WORDS NOT JUST MOVED   
  15 2SFJ ...      LDN   7  512                 [SET UP X7   
  18 2SG? ...RSID  SBX   2  7                   [ANY MORE WORDS TO MOVE??
  17 2SH2 ...      BNG   2  XFDTSTART           [IF NOT START ANOTHER
  17 2SHP ...      BZE   2  XFDTSTART           [SCAN DOWN THE FDT   
  18 2SJD ...      ADX   0  7                   [UPDATE MOVE ADDRESSES   
  15 2SK7 ...      ADX   1  7                   [       DITTO
  19 2SKW ...      BRN      XNMOV               [GO AND MOVE SOME MORE WORDS 
   4 2SLK ...[   
   4 2T69 ...[   
   4 2TKT ...[   
  21 3BJL ...[   ******************************************************************  
   4 3BKS ...[   
  19 3BM2 ...[   THIS SUBROUTINE SETS UP A DUMP TAPE ACTIVITY FOR THE TAPE   
  18 3BN8 ...[   WHOSE TSN IS IN AWORK2 OF THE ACTIVITY BLOCK. THE FIRST 
  18 3BPB ...[   FDINCS BLOCK FOUND AFTER THE CURRENT ACTIVITY IS THEN   
  17 3BQJ ...[   CHAINED AFTER THE DTA AND THE DTA IS THEN FPUT'ED.  
   4 3BRQ ...[   
   5 3BSY ...SETUPACT
  19 3BW6 ...      SBX   7  FX1                 [RELATIVIZE LINK ACCUMULATOR 
  19 3BXG          GETACT   ACTDOC,ADUMPT,1     [SET UP DUMP TAPE ACTIVITY   
  19 3CC6 ...      LDX   5  AWORK2(2)           [GET TSN BEFORE CORRUPTING X2
  18 3DW2          LDX   3  BPTR(2)             [X2 POINTS TO ITS BASE   
   4 3F5S ...[   
  11 3F*L    [     CHECK  LINKSET PARAMETERS 
   4 3FKD ...[   
  15 3FT=          LINKSET  3,PAIR(1)           [SET UP LINK 
  15 3G#W ...      STO   5  ADTSN(3)            [MOVE IN TSN 
  12 3GSG ...      MHUNTW   1,FILE,FDINCS       [
  21 3H#6 ...      CHAIN    1,3                 [CHAIN FDINCS AFTER NEWLY SET UP DTA 
  12 3HRQ ...      LDX   2  3                   [
  17 3K=L          FPUT                         [PUT ACTIVITY ON LIST
  15 3KQ=          ADX   7  FX1                 [RESETLINK   
  12 3L9W ...      EXIT  7  0                   [
   4 3LPG    [   
  21 3L_# ...[   ******************************************************************  
   4 3M96    [   
  21 3MNQ ...[   THIS SUBROUTINE IS DESIGNED TO DO A NON-CORDINATING SEARCH OP THE   
  20 3N8B ...[   OLC, UPDATING ALL THE RELEVANT REQUEST BLOCKS. THERE ARE FOUR   
  20 3NN2 ...[   ENTRY POINTS CORRESPONDING TO FOUR SEPERATE ACTIONS TO BE TAKEN 
   4 3P7L    [   
  18 3PM=    [     1     XTAPEM/XTAPEP : SEARCH DOWN THE OFF LINE CHAIN  
  20 3Q6W    [                           INCREMENTING/DECREMENTING ADTCOUNT IN   
  20 3QLG    [                           ALL REQUEST BLOCKS WHOSE INC. NO. IS IN 
  19 3R66    [                           THE RANGE GIVEN BY FILE FDINCS BLOCK
   4 3RKQ    [   
  12 3S5B ...[           ENTRY         : X2 = FX2
  17 3SK2 ...[                           FILE FDINCS BLOCK PRESENT   
   4 3T4L    [   
  20 3TJ=    [     2     XCOUNT        : SEARCH DOWN OLC UPDATING ADTCOUNT WITH A
  20 3W3W    [                           VALUE SPECIFIED IN X0, IN ALL REQUEST   
  20 3WHG    [                           BLOCKS WHOSE INC. NO. IS SAME AS IN X4  
  19 3WR# ...[                           AND WHOSE FILESTORE NUMBER IS IN X6.
   4 3X36    [   
  12 3XGQ    [           ENTRY         : X2 = FX2
  17 3Y2B    [                           X0 = COUNT OF DTAS AVAILABLE
  14 3YG2    [                           X4 = INC. NO.   
  16 3YSC ...[                           X6 = FILESTORE NUMBER.  
   4 3_6S ...[   
  21 3_F=    [     3     XSTATE        : SEARCH DOWN OLC SETTING APPROPRIATE BIT IN  
  21 3_YW    [                           FDTSTATE WORD IN ALL REQUEST BLOCKS WHOSE   
  21 42?? ...[                           INC.NO. IS GIVEN IN X4 AND WHOSE FILESTORE  
  14 42KN ...[                           NUMBER IS IN X6.
   4 42Y6    [   
  12 43CQ    [           ENTRY         : X2 = FX2
  14 43XB    [                           X0 = STATE WORD 
  14 44C2    [                           X4 = INC. NO.   
  15 44LS ...[                           X6 =FILESTORE NUMBER
   4 44WL    [   
  20 45B=    [     EXIT     IN ALL THESE CASES ALL THE ACCUMULATORS ARE CORRUPTED
   4 46*G    [   
  14 46T6    [     NOTE     THE LINK ACCUMULATOR IS X7   
   6 47#Q          UPDOLC
   4 47#X ...[   
  21 47*4 ...[   ******************************************************************  
   4 47*9 ...[   
  17 47*B ...[   THIS SUBROUTINE CHECKS DOWN THE FDT FOR BIT BIFDTO; 
  17 47*H ...[        IF IT IS SET THEN CACULATE THE WAITING INTERVAL
  11 47*N ...[        AND STORE IT IN CLONG3,
  12 47*T ...[        OTHERWISE SET CLONG3 ZERO. 
   4 47B2 ...[   
   6 47B7 ...RVMESSTIME  
  17 47B# ...      LDX   2  BOLC                [X2 => START OF OFLC 
  19 47BF ...      LDX   3  A1(2)               [X3 => NO. OF TAPES IN FDT   
  20 47BL ...      BZE   3  ZEROIZEA            [J IF NO TAPES, NO MESSAGE OUTPUT
  17 47BR ...      SLL   3  2                   [FOUR WORDS PER ENTRY
  18 47BY ...      ADX   3  BOLC                [X3 => LAST ENTRY IN FDT 
  17 47C5 ...      SBN   3  4                   [LAST ENTRY IS EMPTY 
  12 47C= ...RMREPEAT                           [
  16 47CC ...      JBS XCALTIME,3,BIFDTO        [J IF BIFDTO SET 
  16 47CJ ...      SBN   3  4                   [UPDATE MODIFIER 
  15 47CP ...      TXL   3  BOLC                [END OF FDT? 
  14 47CW ...      BCC      RMREPEAT            [J IF NOT
  17 47D3 ...      BRN      ZEROIZEA            [NO MESSAGE TO OUTPUT
  19 47D8 ...XCALTIME                           [CALCULATE WAITING INTERVAL  
  14 47D* ...      LDX   2  FX2                 [RESET X2
  20 47DG ...      LDX   4  AWORK4(2)           [X4 => REMAINING WAIT INTERVAL   
  21 47DM ...      BZE   4  SKIPERR             [X4 SHOULDNT BE ZERO AT THIS POINT   
  21 47DS ...      LDN   6  60/FONINTP          [60 SECS IS THE RVMESS REVIEW TIME   
  19 47D_ ...      STO   6  CLONG3(2)           [CLONG3 => REVIEW INTERVAL   
  17 47F6 ...      LDX   6  ASYSTEMMESS         [X6 => MESSAGETIME   
  21 47F? ...      LDN   0  FONINTP             [X0 => FONINTP INTERVAL (4 AT PRESENT
  18 47FD ...      DVS   5  0                   [X6 => WAITING INTERVAL  
  18 47FK ...      LDX   5  6                   [X5 => WAITING INTERVAL  
  18 47FQ ...      LDN   0  2                   [REQUEST OUTPUT EVERY TWO
  21 47FX ...      MPY   5  0                   [MESSAGETIMES ... AS TAPEOPEN DOES   
  18 47G4 ...      BNG   4  RESETIME            [J IF FIRST TIME THROUGH 
  12 47G9 ...XLOWEST                            [
  19 47GB ...      BXL   4  6,TIMELEFT          [CHOOSE LOWEST OF TIME LEFT  
  16 47GH ...                                   [AND MESSAGETIME 
  19 47GN ...      STO   6  AWORK4(2)           [STORE MESSAGETIME IN AWORK4 
  17 47GT ...      LDX   4  6                   [X4 => MESSAGETIME   
  18 47H2 ...      BRN      TESTREV             [J TO TEST REVIEW TIME   
  12 47H7 ...TIMELEFT                           [
  19 47H# ...      STO   4  AWORK4(2)           [STORE TIME LEFT IN AWORK4   
  12 47HF ...TESTREV                            [
  18 47HL ...      LDX   6  CLONG3(2)           [X6 => RVMESS REVIEW TIME
  19 47HR ...      BXL   4  6,XNOTIME           [J IF REVIEW TIME > TIME LEFT
  19 47HY ...      SBX   4  6                   [REDUCE TIME LEFT BY REVIEW  
  16 47J5 ...      STO   4  AWORK4(2)           [STORE TIME LEFT 
  13 47J= ...      EXIT  7  0                   [EXIT
  12 47JC ...XNOTIME                            [
  18 47JJ ...      STOZ     AWORK4(2)           [SET 'OUTPUT MESSAGE WHEN
  18 47JP ...      STO   4  CLONG3(2)           [CLONG3 IS ZERO' MARKER. 
  13 47JW ...      EXIT  7  0                   [EXIT
  12 47K3 ...RESETIME                           [
  18 47K8 ...      LDX   4  6                   [X4 -> WAITING INTERVAL  
  12 47K* ...      BRN      XLOWEST             [
  12 47KG ...ZEROIZEA                           [
  16 47KM ...      SMO      FX2                 [X2 IS CORRUPT   
  21 47KS ...      STOZ     AWORK4              [SET 'NO TAPES TO BE LOADED' MARKER  
  12 47K_ ...      BRN      ZEROIZE             [
   4 47L6 ...[   
  21 47L? ...[   ******************************************************************  
   4 47LD ...[   
  20 47LK ...[   THIS SUBROUTINE PROCESSES CLONG3 AND TAKES APPROPRIATE ACTION:- 
  21 47LQ ...[        IF CLONG3 = 0 OR 1 RVMESS SETS CLONG3 = 0,OUTPUTS THE MESSAGE ,
  20 47LX ...[        IF AWORK4 =-1 OR IS POSITIVE THEN NO MESSAGE WILL BE OUTPUT
  12 47M4 ...[        IF CLONG3 > 1 RVMESS EXITS 
   4 47M9 ...[   
   5 47MB ...RVMESS  
  14 47MH ...      LDX   2  FX2                 [RESET X2
  19 47MN ...      LDX   6  CLONG3(2)           [X6 => CLONG3 (TIME INTERVAL)
  21 47MT ...      SBN   6  2                   [NO MESSAGE THIS TIME IF CLONG3 > 1  
  16 47N2 ...      BPZ   6  RVMESSEND           [J IF NO MESSAGE 
  20 47N7 ...      LDX   6  AWORK4(2)           [X6 => REMAINING WAITING INTERVAL
  19 47N# ...      BNZ   6  ZEROIZE             [J IF NO MESSAGE TO BE OUTPUT
  17 47NF ...      LDX   2  BOLC                [X6 => START OF OLC  
  18 47NL ...      LDX   6  A1(2)               [X6 => NO OF TAPES IN FDT
  17 47NR ...      BZE   6  ZEROIZE             [J IF FDT IS EMPTY   
  21 47NY ...      SBX   7  FX1                 [RELATIVIZE BECAUSE OF COORDINATION  
  19 47P5 ...      DOWN DTSUBS,11               [GO DOWN TO OUTPUT MESSAGE   
  18 47P= ...      ADX   7  FX1                 [RESET LINK ACCUMULATOR  
  17 47PC ...      LDN   2  1                   [SET THE 'RECALCULATE
  18 47PJ ...      SMO      FX2                 [WAITING INTERVAL' FLAG  
  21 47PP ...      NGS   2  AWORK4              [LOOKED AT BY RVMESSTIME AND RVMESS  
  12 47PW ...ZEROIZE                            [
  16 47Q3 ...      LDX   2  FX2                 [X2 WAS CORRUPTED
  16 47Q8 ...      STOZ     CLONG3(2)           [SET CLONG3 ZERO 
  12 47Q* ...RVMESSEND                          [
  13 47QG ...      EXIT  7  0                   [EXIT
   4 47QM ...[   
   4 47QS ...[   
   6 47SB    X1SCHEDMTX  
   4 48#2    [   
  21 48F8 ...[   ******************************************************************  
   4 48LB ...[   
  19 48RL    [   IF BIT 0 OF ADUMPST WORD IN DTS BLOCK IS SET THEN DUMPER HAS
  20 49?=    [     BEEN INITIATED AND THE DTS WILL SET UP ACDOC/ADUMPTS FOR ALL  
  12 49QW    [     TAPES GIVEN IN FILE FGET BLOCK
   4 4==G    [   
  20 4=Q6    [   OTHERWISE DTS WILL SEARCH DOWN OLC FOR REQUEST BLOCK THAT NEEDS 
   7 4?9Q    [     ATTENTION 
   4 4?PB    [   
  13 4#92    [   A REQUEST BLOCK NEEDS ATTENTION IF :
  21 4#NL    [                                  (1) IT HAS REACHED A TERMINAL STATE  
  19 4*8=    [                                  (2) ADTCOUNT=0AND NOT IN A   
  18 4*MW    [                                           TERMINAL STATE  
   4 4*QF ...[   
   4 4*T4 ...[   
   4 4*XM ...[   
   4 4B2= ...[   
   4 4B4T ...[   
   4 4B7G    [   
   4 4BM6    [   
   4 4C6Q    [   
  21 4C8Q ...[   ******************************************************************  
   4 4C=Q ...[   
  21 4C#Q ...      STOZ     CLONG3(2)           [SET CLONG3 ZERO FIRST TIME AROUND   
  15 4CBQ ...      STOZ     AWORK4(2)           [AND AWORK4  
  18 4CDQ ...      BRN      STARTOLCA           [J TO SERVICE EVENT(S)   
  12 4CGQ ...MESSTIME                           [
  19 4CJQ ...      LONGSET #170,XNOEVENT        [CLONG1 => DTS WAITING STYLE 
  12 4CLQ ...XNOEVENT                           [
  20 4CNQ ...      CALL  7  RVMESS              [RVMESS PROCESSES CLONG3 WHICH IS
  21 4CQQ ...                                   [SET > 1   BY SUBROUTINE RVMESSTMIME 
  21 4CSQ ...                                   [          BIFDTO IS FOUND SET FOR AN
  21 4CWQ ...                                   [          OR IF CLONG3 IS ALREADY > 
  21 4CYQ ...                                   [          I.E. A MESSAGE IS TO BE OU
  20 4D2Q ...                                   [SET TO 0  BY COORDINATOR WHEN   
  21 4D4Q ...                                   [    OR 1  A MESSAGE IS TO BE OUTPUT 
  21 4D6Q ...                                   [          IN THIS CASE RVMESS OUTPUT
  20 4D8Q ...                                   [          AND THEN STOZ'S CLONG3
  18 4D=Q ...      BRN      XLONGSTOP           [NO EVENTS TO SERVICE?   
  19 4D#Q ...STARTOLCA                          [CHECK FOR CANTDO LOAD TAPES 
  21 4D*6 ...      JBC      STARTOLC,BOLC,BIFDTLMEF [J IF NO EXTANT LOADED TAPES ETC.
  21 4D*G ...      CALL  6  XFDTCHECK           [CHECK FDT FOR BIFDTJL - IF SET CREAT
  21 4D*W ...                                   [DTA, CHAIN FDINCS, CLEAR BITS, FPUT 
  20 4DSQ ...STARTOLC                           [RE-COMMENCE SEARCHING DOWN OLC  
  19 4DWQ ...      LONGSET #170,SETBEFORE       [CLONG1 => DTS WAITING STYLE 
  12 4DY4 ...SETBEFORE                          [
  12 4D_B ...      STOZ     AWORK1(2)           [
  19 4F2Q ...      CALL  7  RVMESS              [OUTPUT MESSAGE IF NECESSARY 
  19 4F5=          CALL  7  XINCWORD            [CHECK IF DUMPER INITIATED   
  18 4FJW          LDX   3  BOLC                [BASE OF OFF-LINE CHAIN  
  12 4G4G ...STEPOLC                            [
  12 4GJ6 ...      LDX   3  FPTR(3)             [
  16 4H3Q    SOLC  BXE   3  CXOL,XENDOLC        [J IF END OF OLC 
  21 4HHB ...      JMBS     STEPOLC,3,BHAPPY,BINCUNVERR    [J IF FREQ ALREADY DEALT W
  18 4J32 ...      JBS      TERMST,3,BTERMST    [J IF IN A TERMINAL STATE
  20 4JGL ...      LDX   6  ADTCOUNT(3)         [X6 => NUMBER OF SERVICING DTAS  
  20 4LYQ          BNZ   6  TESTNEG             [J IF THERE IS A DTA SERVICING   
  12 4M?2 ...TESTNEGP1                          [
  18 4MK= ...      LDX   4  FDTINCNUM(3)        [X4=> INCREMENT NUMBER   
  18 4MXG ...      LDX   6  FDTFILSTR(3)        [X6 => FILESTORE NUMBER  
  21 4N9Q ...      PRESINC  4,6,7               [ANY FDINCS WITH THIS INCREMENT & FIL
  14 4NJ4 ...      BZE   7  XNOINC              [J IF NOT
  17 4NL? ...      STO   7  ADTCOUNT(3)         [STORE FDINCS COUNT  
  18 4NNG ...      CALL  7  SACT                [SET CHAIN ALTERED BITS  
  17 4NQP ...      BRN      STEPOLC             [CONTINUE DOWN OFLC  
  12 4NSY ...XNOINC                             [
  21 4NX= ...      LONGOFF                      [OPENINC MIGHT WAIT...SO CLEAR CLONG1
  19 4NYM ...      BZE   6  XHOSTFS             [J IF ZERO FILESTORE NUMBER  
  19 4P24 ...      BS       BOLC,BIFDTAFS       [SET 'ALIEN FILESTORE' BIT   
  19 4P3F ...      DOWN     SCHEDMTA,1          [OPEN ALIENFS.INCINDEX, ETC. 
  17 4P4W ...      BRN      SETUP3              [J TO HUNT FOR FRB   
  18 4P6? ...      LDX   0  ACOMMUNE1(2)        [X0 => STATE OF REQUEST  
  18 4P7N ...      BRN      XSETB               [SET STATE IN FREQ BLOCKS
  12 4P95 ...SETUP3                             [
  15 4P=G ...      MHUNTW   3,FILE,FRB          [X3 -> FRB   
  12 4P?X ...      BRN      VERIF1              [
  12 4P*# ...XHOSTFS                            [
  16 4PBW          OPENINC XBR,READ             [OPEN S/INCINDEX 
   4 4PWG    [   
  12 4QB6    [     X4 CONTAINS INCREMENT NUMBER  
  14 4QTQ    [     SEARCH FOR THIS RECORD IN S/INCINDEX  
   4 4R*B    [   
  19 4RR= ...      BS       BOLC,BIFDTOPEN      [SET THE 'INCINDEX OPEN' BIT 
  18 4S96 ...      FINDREC  ,AINCNOR,4,XINCLST  [J IF INCREMENT NOT FOUND
  18 4SM2 ...      READAGAIN                    [READ INC RECORD INTO FRB
  12 4T4W ...      MHUNTW 3,FILE,FRB            [
  18 4TGQ ...      JBS      VERIF,3,BIAVEST     [J IF INCREMENT VERIFIED 
  19 4TYL ...      BSXD     0,BINCUNVERR        [X0 => UNVERIFIED INC MASK   
  18 4WBG ...      BRN      XSETB               [SET STATE IN FREQ BLOCKS
  19 4WSB ...TESTNEG                            [TEST FOR NEGATIVE ADTCOUNT  
  14 4X== ...      BPZ   6  STEPOLC             [J IF OK 
  18 4XN6 ...      STOZ     ADTCOUNT(3)         [MUST DEAL WITH THIS FREQ
  12 4Y62 ...      BRN      TESTNEGP1           [
  17 4YHW ...XINCLST                            [ INC. HAS BEEN LOST 
  19 4Y_Q ...      MBSXD    0,BPERLOST,BTERMST  [X0 => FILE PERM LOST MASK   
  12 4_CL ...XSETB                              [
  17 4_TG ...      CALL  7  XSTATE              [UPDATE FREQ BLOCKS  
  18 52?B ...XCLOSE                             [CLOSE INCINDEX IF OPEN  
  19 52P= ...      JBCC     XNOCLOSE,BOLC,BIFDTOPEN [J IF INCINDEX NOT OPEN  
  17 537B          CLOSETOP                         [CLOSE S/INCINDEX
   4 53C8 ...[   
  19 53M2    [     AFTER CLOSING INCINDEX - NEED TO FREE FRB. THIS EXISTS IN 
  18 546L    [     ALL CASES EXCEPT WHEN FINDREC FAILS TO LOCATE RECORD  
   4 54BD ...[   
  12 54G4 ...XNOCLOSE                           [
  17 54KN ...      VFREEW   FILE,FRB            [FREE INCINDEX RECORD
  20 54P# ...      VFREE    FILE,FABSNB         [PRESENT IN CASE OF ALIEN FILE   
  19 54SY ...      BC       BOLC,BIFDTAFS       [CLEAR 'ALIEN FILESTORE' BIT 
  16 54YM ...      BRN      STARTOLCA           [START SCAN AGAIN
   4 55?4 ...[   
  19 55KG    [     NO MORE REQUESTS NEED SERVICING; IF DUMPER NOT INITIATED  
  19 55XX ...[     THEN WE CHECK DOWN THE FDT FOR TAPE ENTRIES TO SERVICE,   
  20 56=# ...[     USING SUBROUTINE 'XFDTCHECK',AND THEN PUT THE DTS TO SLEEP.   
   4 56JQ    [   
   5 574B    XENDOLC 
  18 57J2          CALL  7  XINCWORD            [CHECK IF DUMPER STARTED 
  21 57LK ...      CALL  6  XFDTCHECK           [CHECK FDT FOR BIFDTJL - IF SET CREAT
  21 57P8 ...                                   [DTA,CHAIN FDINCS,CLEAR BIT,FPUT DTA 
  12 57RR ...XLONGSTOP                          [
  14 57WB ...      LDX   2  FX2                 [RESET X2
  18 57Y_ ...      LDX   7  CLONG3(2)           [X7 => WAITING INTERVAL  
  17 583J ...      BNZ   7  XLSTIME             [J IF CLONG3 STILL OK
  21 5867 ...      CALL  7  RVMESSTIME          [CHECK FDT FOR BIFDTO - IF SET CALCUL
  21 588Q ...                                   [WAITING INTERVAL AND STORE IN CLONG3
  18 58?* ...                                   [OTHERWISE STOZ CLONG3   
  17 58?Q ...      LDEX  0  CLONG1(2)           [TEST FOR MORE EVENTS
  16 58#7 ...      BZE   0  XEND                [J IF MORE EVENTS
  19 58#J ...      JBCC     XEND,BOLC,BIFDTCDPR [CLEAR 'FCANTDO PRESENT' BIT 
  17 58#_ ...      MFREEW   FILE,FCANTDO        [FREE FCANTDO BLOCK  
  12 58*B ...XEND                               [
  18 58*Y ...      LDX   7  CLONG3(2)           [X7 => WAITING INTERVAL  
  18 58DH ...      BNZ   7  XLSTIME             [J IF CLONG3 IS NOW OK   
  21 58H6 ...      LONGSTOP XBR,MESSTIME        [LONGSTOP WITHOUT A TIME INTERRUPT   
  17 58KP ...      BRN      STARTOLCA           [J WHEN EVENT OCCURS 
  12 58N# ...XLSTIME                            [
  20 58QX ...      LONGSTOP XBR,MESSTIME,,,7    [LONGSTOP WITH A TIME INTERRUPT  
  20 58TG ...      BRN      STARTOLCA           [J TO MESSTIME ON TIME INTERRUPTS
   4 58Y5 ...[   
  14 592W    [     INCREMENT DOES NOT NEED TO BE VERIFIED
   4 59=N ...[   
   5 59GG    VERIF   
  21 5=26 ...      JMBS     XINCLST,3,BIOBS,BINSUITRV [J IF OBSOLETE OR NOT SUITABLE 
  18 5=FQ ...VERIF1                                 [ENTRY FOR ALIEN TAPE
  18 5=_B          LDX   7  AMTS(3)                 [X7 = NO. OF TAPES   
  21 5?B4 ...      BZE   7  XINCLST                 [J IF NO TAPES IN INCINDEX RECORD
  20 5?QQ ...      LDN   0  INCMAGLEN               [X0 => LENGTH OF TAPE ENTRY  
  20 5#7D ...      MPY   0  7                       [X1 => LENGTH OF TAPE ENTRIES
  21 5#J6 ...      ADX   3  1                       [AMAG-2(3) NOW POINTS TO LAST ENT
  20 5#YS ...      STOZ     AWORK2(2)               [ZEROISE AWORK2 AND AWORK1   
  13 5**G ...      STOZ     AWORK1(2)               [
  19 5*Q8 ...      STOZ     ADUMTST(2)              [USED TO SAVE FDT COUNT  
  18 5B6W ...NEXTSN                                 [LOOK AT NEXT TAPE   
  20 5BHJ ...      JBS      XNOTEST,BOLC,BIFDTAFS   [J IF ALIEN FILESTORE TAPE   
  13 5BY= ...      MBSXD    5,BMRETURN,BMUSWR,BMOPWR 
  17 5C#Y ...      ANDX  5  AMAG-1(3)               [MAG TAPE STATE  
  19 5CPL ...      BNZ   5  THIS2                   [J IF WRONGED OR RETURNED
  13 5D6# ...XNOTEST                                [
  19 5DH2 ...      LDX   5  AMAG-2(3)               [NEXT TSN FROM  INCINDEX 
  18 5DXN ...      BNG   5  THIS2                   [J IF NEGATIVE TSN   
   4 5F#G    [   
  13 5FS6    [     TEST IF THE TAPE IS IN FDT BLOCK  
   4 5G?Q    [   
  21 5GRB ...      FDTENT 2,5,THIS6                 [J TO THIS6 IF TSN IS NOT IN FDT 
  13 5H?2 ...THISTSN                                [
  18 5HQL ...      JMBS     THIS2,2,BIFDTOW,BIFDTUW [J IF WRONGED IN FDT 
  13 5HQX ...      JMBAC    NODTA,2,BIFDTJL,BIFDTO   
  10 5HR8 ...      TRACE      5,INCTSNA  
   8 5HRF ...      LDX   1  FX2  
   5 5HRQ ...NXFDINCS
  11 5HS3 ...      HUNT2      1,FILE,FDINCS  
  12 5HS# ...      BXU   5  FDINCTSN(1),NXFDINCS 
   5 5HSK ...NINCF   
  10 5HSW ...      LDX   2  FDINCNO(1)   
   4 5HT7 ...NINC
   7 5HTD ...      SBN   2  1
   8 5HTP ...      BNG   2  NINC1
   7 5HW2 ...      SMO      2
  11 5HW? ...      BXE   4  FDINCINC(1),NOUT 
   7 5HWJ ...      SMO      2
  11 5HWT ...      BXL   4  FDINCINC(1),NINC 
   3 5HX6 ...
   5 5HXC ...NINC1   
  10 5HXN ...      LDX   0  FDINCNO(1)   
   7 5HX_ ...      SBX   0  2
   7 5HY= ...      SBN   0  1
   7 5HYH ...      LDN   5  1
  10 5HYS ...      LDX   2  FDINCNO(1)   
   7 5H_5 ...      SBN   2  1
  10 5H_B ...      ADS   5  FDINCNO(1)   
   8 5H_M ...      BZE   0  NINC3
   5 5H_Y ...NINC2   
   7 5J29 ...      SMO      2
  10 5J2G ...      LDX   5  FDINCINC(1)  
   7 5J2R ...      ADN   2  1
   7 5J34 ...      SMO      2
  10 5J3* ...      STO   5  FDINCINC(1)  
   7 5J3L ...      SBN   2  2
   8 5J3X ...      BCT   0  NINC2
   5 5J48 ...NINC3   
   7 5J4F ...      ADN   2  1
   7 5J4Q ...      SMO      2
  10 5J53 ...      STO   4  FDINCINC(1)  
   9 5J5# ...      TRACE    4,INCNOT 
  10 5J5K ...      LDX   5  FDINCNO(1)   
  18 5J5W ...      ADN   5  4                   [INCREASE FDINCS BY ONE  
  17 5J67 ...      LDX   4  1                  [ TO LEAVE SPARE WORD 
   8 5J6D ...      ALTLEN   4,5  
   8 5J6P ...      BRN      NOUT2
   5 5J72 ...NODTA   
   9 5J7? ...      TRACE    5,INCTSNB
   8 5J7J ...      LDX   1  FX2  
   5 5J7T ...NXTF2   
  11 5J86 ...      HUNT2    1,ACTDOC,ADUMPT  
  10 5J8C ...      HUNT2    1,FILE,FDINCS
  11 5J8N ...      BXU   5  FDINCTSN(1),NXTF2
   8 5J8_ ...      BRN      NINCF
   4 5J9= ...NOUT
   9 5J9H ...      TRACE    4,INCPRES
   5 5J9S ...NOUT2   
  18 5J== ...      LDN   0  1                       [NOW INCREASE COUNT  
  18 5JPW ...      SMO      FX2                     [OF ENTRIES IN FDT   
  13 5K9G ...      ADS   0  ADUMTST                 [
  21 5KP6 ...THIS2                                  [GET ANOTHER TSN FROM INCINDEX   
  17 5L8Q ...      SBN   3  INCMAGLEN               [X3 -> NEXT ENTRY
  18 5LNB ...      BCT   7  NEXTSN                  [J IF ANY TSNS LEFT  
   4 5NLW    [   
  15 5P6G    [     ALL TAPES IN INCINDEX HAVE BEEN EXAMINED  
  13 5PL6 ...[     ADUMTST(2) = COUNT OF TAPES IN FDT
  17 5QKB    [     TSN/STATE OF A SUITABLE UNOPENED TAPE IN AWK2/AWK3
   4 5R52    [   
  12 5R#S ...XTSNCLA                            [
  12 5RQW ...      LDX   2  FX2                 [
  20 5S8Y ...      LDX   0  ADUMTST(2)          [X0 => COUNT OF ENTRIES IN FDT   
  17 5SM2 ...      BZE   0  NONEFDT             [J IF NO TAPES IN FDT
  21 5T54 ...      CALL  7  XCOUNT              [STORE COUNT IN ALL RELEVANT FREQ BLO
  21 5TH6          CALL  7  SACT                [SET CHAIN ALTERED BITS IN ALL DTAS  
  18 5W2Q          BRN      XCLOSE              [ J TO CLOSE S/INCINDEX  
   4 5W=J ...[   
  17 5WGB    [     A TAPE HAS BEEN FOUND WHICH IS NOT TW OR RETURNED 
  17 5X22    [     AND IS NOT IN FDT  SO STORE TSN/STATE IN AWK2/AWK3
   4 5X9S ...[   
   5 5XFL    THIS6   
  20 5XRL ...      JBC      XTSNOK,BOLC,BIFDTCDPR [J IF NO CANTDO LOADED TAPES   
  18 5Y5L ...      MHUNTW   1,FILE,FCANTDO      [FIND THE FCANTDO BLOCK  
  17 5Y7S ...      LDX   2  A1(1)               [X2 => COUNT OF TAPES
  18 5Y8X ...      BZE   2  XTSNOK              [J IF NO TAPES IN FCANT  
  12 5Y=2 ...XTLOOP                             [
  21 5Y=* ...      BC       BOLC,BIFDTAW        [CLEAR 'ALIEN TAPE WRONGED OR INSECUR
  21 5Y=N ...      MHUNTW   1,FILE,FCANTDO      [X1 -> FCANTDO...SHORT OF ACCUMULATOR
  12 5Y?5 ...      SMO      2                   [
  17 5Y?L ...      LDXC  1  A1(1)               [X1 => TSN IN FCANTDO
  19 5Y#7 ...      BCC      XHOST               [J IF NOT WRONGED ALIEN TAPE 
  21 5Y#N ...      BS       BOLC,BIFDTAW        [SET 'ALIEN TAPE WRONGED OR INSECURE'
  12 5Y*9 ...XHOST                              [
  18 5Y*Q ...      BXE   5  1,XTSNCL            [J IF TAPE NOT TO BE USED
  17 5YB? ...      BCT   2  XTLOOP              [LOOK AT NEXT ENTRY  
  12 5YBS ...XTSNOK                             [
  14 5YCF ...      LDX   2  FX2                 [RESET X2
  17 5_59 ...      LDX   1  AMAG-1(3)           [X1 => STATE OF TAPE 
   9 5_73 ...      LDX   0  AWORK2(2)
  18 5_8T ...      BZE   0  XTSNOKA              [NO TAPE YET SELECTED   
  17 5_=M ...      BBS      7,1,THIS2             [TO BE TAPEWRONGED 
   5 5_#F ...XTSNOKA 
  18 5_B? ...      STO   5  AWORK2(2)             [TSN OF UNOPENED TAPE  
  17 5_D6          STO   1  AWORK3(2)           [STATE OF THIS TAPE  
  18 5_PG ...      BRN      THIS2               [LOOK AT NEXT TAPE ENTRY 
  18 622W ...XTSNCL                             [THIS TAPE CANTDO LOADED 
  21 624D ...      JBS      THIS2,BOLC,BIFDTAW  [J IF WRONGED OR INSECURE ALIEN TAPE 
  12 6262 ...      SMO      FX2                 [
  19 6296 ...      STO   5  AWORK1              [STORE CANTDO TSN IN AWORK1  
  12 62*C ...      BRN      THIS2             [  
  12 62FP ...NONEFDT                            [
  17 62M8 ...      LDX   2  FX2                 [RESET X2....BUG 3744
  15 62RF ...      LDX   0  AWORK2(2)           [X0 => TSN   
  17 62XQ ...      BNZ   0  TAPEIN              [J IF A TAPE FOUND   
  17 6343 ...      LDX   5  AWORK1(2)           [CANTDO LOADED TAPE??
  18 638# ...      JBC      NOTAPE,BOLC,BIFDTCDPR [J IF NO AVAILABLE TAPE
  15 63#K ...      BZE   5  NOTAPE              [      DITTO 
  21 63DW ...      MHUNTW   1,FILE,FCANTDO      [REMOVE THIS TSN FROM FCANTDO BLOCK  
  19 63FD ...      LDX   2  A1(1)               [IT MUST BE LOADED OR WRONGED
  16 63G2 ...      BZE   2  NOTAPE              [J IF NO ENTRIES 
  15 63GJ ...      SBN   2  1                   [REDUCE COUNT
  19 63H6 ...      BNZ   2  NOTFREE             [J IF MORE THAN ON E ENTRY   
  17 63HN ...      FREECORE 1                   [FREE FCANTDO BLOCK  
  19 63JH ...      BC       BOLC,BIFDTCDPR      [CLEAR 'FCANTDO PRESENT' BIT 
  21 63KB ...      BRN      XCLOSE              [RESTART SCAN...FIRST CLOSE INCINDEX 
  12 63KY ...NOTFREE                            [
  14 63LG ...      ADN   2  1                   [RESET X2
  12 63M4 ...XTLOOPY                            [
  12 63ML ...      SMO      2                   [
  17 63N8 ...      BXU   5  A1(1),XTSNOTF       [J IF NOT THIS TAPE  
  12 63NQ ...      SMO      2                   [
  17 63P# ...      STOZ     A1(1)               [SET THIS ENTRY ZERO 
  21 63RJ ...      BRN      XCLOSE              [RESTART SCAN...FIRST CLOSE INCINDEX 
  12 63S6 ...XTSNOTF                            [
  17 63SN ...      BCT   2  XTLOOPY             [LOOK AT NEXT TAPE   
  17 63T= ...NOTAPE                             [NO TAPES AVAILABLE  
   4 63W=    [   
  20 64*W    [     THERE ARE NO TAPES ALREADY UP WHICH COULD SERVICE THIS REQUEST
  19 64TG ...[     AND THERE ARE NO TAPES IN INCINDEX WHICH COULD BE USED;   
  21 65*6 ...[     THAT IS ALL THE TAPES IN INCINDEX RECORD ARE WRONGED OR RETURNED. 
  20 66S2    [     IN THIS CASE ALL REQUESTS FOR FILES ON THIS INCREMENT WILL BE 
  12 676C ...[     MARKED AS TEMPORARILY LOST.   
   4 67DS ...[   
  19 67R9 ...      MBSXD    0,BTEMLOST,BTERMST  [X0 => TEMPORARILY LOST MASK 
  17 685L ...      BRN      XSETB               [UPDATE FREQ BLOCKS  
   4 68D3 ...[   
  19 68QG    [     REQUIRED TO SET UP A DUMP TAPE ACTIVITY FOR TAPE GIVEN IN 
  13 69=6    [              AWORK2( STATE IN AWORK3) 
   4 69PQ    [   
   5 6=9B    TAPEIN  
  18 6=BJ ...      JBC      TAPEIN1,BOLC,BIFDTAFS [J IF TAPE NOT ALIEN   
  21 6=HQ ...      DOWN     SCHEDMTA,2          [ SET UP A FDINCS BLOCK FOR ALIEN TAP
  14 6=NY ...      BRN      TAPEIN2             [J IF OK 
  18 6=W6 ...      LDX   0  ACOMMUNE1(2)        [X0 => STATE OF REQUEST  
  18 6?3# ...      BRN      XSETB               [SET STATE IN FREQ BLOCKS
  12 6?8G ...TAPEIN1                            [
  21 6?*N ...      DOWN     DTSUBS,8            [ SET UP A FDINCS BLOCK FOR HOST TAPE
  12 6?GW ...TAPEIN2                            [
  20 6?N=          CALL  7  XTAPEP              [ INCREMENT ADTCOUNT IN ALL FREQS
  20 6#7W                                       [  WHOSE INC. NOS LIE WITHIN THE 
  20 6#MG                                       [      RANGE OF INCS IN FDINCS   
  10 6#NN ...      DOWN      DTSUBSS,16  
  20 6#PW ...                                   [BIFDTO,LENGTHEN FDT BY 4 WORDS, 
  21 6#QS ...                                   [ALSO INSERT FILESTORE NUMBER - IN X6
  14 6#RS ...      LDX   2  FX2                 [RESET X2
  21 6#T= ...      LDX   6  CLONG3(2)           [X6 => REMAINING MESSAGETIME  ..IF AN
  17 6#WN ...      BNZ   6  TSNOPEN             [J IF MESSAGETIME OK 
  20 6#Y6 ...      LDX   6  AWORK4(2)           [X6 => REMAINING WAITING INTERVAL
  21 6#_J ...      BNZ   6  TSNOPEN             [J IF MESSAGETIME DEALT WITH OR IS ST
  21 6*32 ...      LDN   6  1                   [SET THE 'RECALCULATE WAITING INTERVA
  21 6*4D ...      NGS   6  AWORK4(2)           [MARKER .... LOOKED AT BY RVMESSTIME 
  21 6*5W ...TSNOPEN                            [NOW ASK FOR TAPE USING TAPEOPEN (INF
  14 6*7# ...      LDX   6  AWORK2(2)           [X6 = TSN
  16 6*7M ...      CALL  7  XFDTENT             [X3 -> FDT ENTRY 
  17 6*82 ...      CALL  7  SKIPERR             [ENTRY DISSAPPEARED  
  17 6*8* ...      JBC      XHOSTOPEN,BOLC,BIFDTAFS [J IF HOST TAPE  
  17 6*8N ...      DOWN     SCHEDMTA,3          [GO DOWN TO OPEN TAPE
  16 6*93 ...      BRN      TOTESTRY            [J IF TAPEOPEN OK
  21 6*9B ...      BRN      TOTESTRY1           [J IF ALIEN TAPE INSECURE (REPLY OPWR
  12 6*9P ...XHOSTOPEN                          [
  17 6*=8 ...      JBS      TONULL,3,BIFDTNW    [J IF NO WPR REQUIRED
  16 6*?L ...      TAPEOPEN XBR,6,WRITE,DUMPER,INFORM,NOTOL,REPLY
  12 6**4 ...      BRN      TOTESTRY            [
  12 6*BG ...TONULL                             [
  15 6*CY ...      TAPEOPEN XBR,6,,DUMPER,INFORM,NOTOL,REPLY 
  16 6*FB ...TOTESTRY                           [NOW TEST REPLYS 
  18 6*GS ...      MFREEW   CPAR,GPERCON        [DON'T NEED THIS BLOCK   
  12 6*HH ...TOTESTRY1                          [
  16 6*J= ...      CALL  7  XFDTENT             [REFIND FDT ENTRY
  19 6*KN ...      CALL  7  SKIPERR             [ENTRY DISSAPPEARED...HELP   
  21 6*QF ...      TESTREP2 INFORM,XINFORM,OK,TRELEASE,NOTOWN,TRELEASE,QUERY,TRELEASE
  20 6*SB ...      TESTREP2 HDDRDIS,TRELEASE,OPWRONG,XOPWRONG,USWRONG,XUSWRONG   
  21 6*W# ...      BRN      XINFORM             [NOT INTERESTED IN ANY OTHER REPLYS  
  12 6*XQ ...TRELEASE                           [
  18 6*_8 ...      BC       3,BIFDTO            [CLEAR 'TO BE OPENED' BIT
  18 6B2L ...      BS       3,BIFDTJL           [SET 'JUST LOADED' BIT   
  19 6B3* ...      BS       BOLC,BIFDTLMEF      [SET 'LOOK AT FDT FIRST' BIT 
  12 6B44 ...      BRN      XINFORM             [
  16 6B95 ...XOPWRONG                           [OPERATOR WRONGED
  21 6BB6 ...      BS       3,BIFDTOW           [SET THE OPERATOR WRONGED BIT IN FDT 
  12 6BCJ ...      BRN      XINFORM             [
  15 6BF2 ...XUSWRONG                           [USER WRONGED
  19 6BGD ...      BS       3,BIFDTUW           [SET USER WRONGED BIT IN FDT 
  17 6BHW ...XINFORM                            [TAPEOPEN (INFORM) OK
  18 6BL2          BRN      XCLOSE              [CLOSE INCINDEX BEFORE   
  17 6C5L                                       [STARTING FRESH LOOP 
  12 6CK= ...SKIPERR                            [
  18 6D4W          GEOERR   1,SHMTSKIP          [GENERAL CODING ERRORS   
   4 6DJG    [   
   4 8=8= ...[   
  12 8=MW ...TERMST                             [
   9 8?7G ...      DOWN  DTSUBSS,15  
   8 8?M6 ...      BRN   STARTOLC
   4 9F==    [   
  20 9FPW    XBR   GEOERR   1,SYSBRKIN          [BREAK IN ON SYSTEM ISSUED OPEN  
  11 9G9G          MENDAREA 100,K99SCHEDMTX  
   4 9GP6    #END
   2 9H8Q
   8 ____ ...03423471000100000000
