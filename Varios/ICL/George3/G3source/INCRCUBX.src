  13 22FL ...      SEG   INCRCUBX,860,FILE,FILESTORE 
   4 22_=    #   
  21 23DW ...      BITDEFS  AWORK4,0,XBCOMM     [UPDATE OTHER COMMUNAL USERS OF THIS 
  21 23YG ...      BITDEFS  AWORK4,1,XBWHOLE    [^WHOLE FILE^ MAY BE SPECIFIED AS %C 
  17 24D6 ...      BITDEFS  AWORK4,3,XBFORCED   [^FORCED^ SPECIFIED  
  17 24XQ ...      BITDEFS  AWORK4,4,XBANSWER   [^ANSWER^ SPECIFIED  
  16 25CB ...      BITDEFS  AWORK4,5,XBSUBCUBS  [SUBCUBS MACRO   
   4 25X2 ...#   
   4 26BL ...#   
  21 26W= ...      SEGENTRY K1INCRCUBX,X1INCRCUBX     [AMBS EXCEEDED - ASK OPS FOR MO
  21 27*W ...      SEGENTRY K2INCRCUBX,X2INCRCUBX     [FILE OPEN COMMUNALLY & HAD >1 
   4 2DLQ    #   
   4 2F6B    #   
   4 2FL2    [   
   8 2GK=    MREACH         +42  
  18 2H4W                   42HDP 2,LIMIT OF ONLINE BACKING STORE REACHED
   8 2HJG    MQUES          +39  
  18 2J46                   39HQN 2,CAN THIS JOB'S LIMIT BE INCREASED?   
   8 2JHQ    MNOTINC        +24  
  14 2K3B                   24HDP 2,LIMIT NOT INCREASED  
   8 2KH2    MNEW           +31  
  16 2L2L                   31HDP 2 , LIMIT NOW         KWORDS   
   8 2LG=    MYESNO         +28  
  15 2L_W                   28HDP 2,PLEASE ANSWER YES OR NO  
   8 2MFG    MBREAK         +48  
  20 2M_6                   48HDP  0 , BREAK-IN CONTEXT     KWORDS MORE ALLOWED  
   4 2NDQ ...[   
   4 2NYB ...[   
   8 2PD2 ...[     SUBROUTINES   
   8 2PXL ...[     -----------   
   4 2QC= ...[   
   4 2QWW ...[   
  19 2RBG ...[  THIS SUBROUTINE SEARCHES FOR A FILE'S FILE CONTROL AREA (FCA)
  18 2RW6 ...[  ON ENTRY, X2=FX2, AWORK3= DEPTH OF FILE.  X6 IS S?R LINK 
  15 2S*Q ...[  ON EXIT ,  X1->FCA, X2=FX2.   X0 DESTROYED.  
   4 2STB ...SFCA
  14 2T*2 ...      SFSTACK  AWORK3(2),1         [X1-> FCA
   7 2TSL ...      EXIT  6  0
   4 6F46    [   
  21 6FHQ    [     X2-> ACT. WHOSE JOB BLOCK IS TO BE SOUGHT (USUALLY CURRENT ACT.)  
   4 6G3B    [   
   5 6GH2    SJOBLOCK
   9 6H2L          LDX   0  JOBNO(2) 
  18 6HG=          BZE   0  NOLOBJOB            [ERROR IF JOB NO. ZERO   
  16 6H_W          FCJOB 1,2,,CPA,OLPA          [X1-> JOB BLOCK  
   7 6MC=          EXIT  6  0
   4 8G2=    [   
  17 8GFW    [     S/R TO SET UP AN ADATA/CREADL CONTAINING A COMMAND
   9 8G_G    [     ON ENTRY, X2=FX2. 
   4 8HF6    [   
   5 8HYQ    SETCOM  
   8 8JDB          SBX   6  FX1  
   8 8JY2          LDX   1  FX1  
  18 8KCL          ADX   1  7                   [X1-> WORD BEFORE COMMAND
  19 8KX=          LDX   0  0(1)                [X0= NO. OF CHARS IN MESSAGE 
   7 8LBW          ADN   0  3
  20 8LWG          SLL   0  2                   [X0= LENGTH OF MESSAGE IN WORDS  
   9 8MB6          ADN   0  CPDATA-A1
  10 8MTQ          STO   0  ACOMMUNE1(2) 
  19 8N*B          SETUPCORE ACOMMUNE1(2),2,ADATA,CREADL [BLOCK FOR COMMAND  
  18 8NT2          ADX   1  7                   [X1-> WORD BEFORE COMMAND
   8 8P#L          LDX   0  0(1) 
  17 8PS=          STO   0  A1(2)               [SET UP CHAR COUNT   
  18 8Q?W          STOZ     A1+1(2)             [CLEAR 2ND RED-TAPE WORD 
   7 8QRG          ADN   1  1
   9 8R?6          ADN   2  CPDATA   
   7 8RQQ          ADN   0  3
   7 8S=B          SLL   0  2
   7 8SQ2          SMO      0
  17 8T9L          MOVE  1  0                   [MOVE COMMAND ACROSS 
   8 8TP=          ADX   6  FX1  
   7 8W8W          EXIT  6  0
   4 8WNG    [   
  16 8X86    [     S/R TO ISSUE A COMMAND IN ADATA/CREADL BLOCK  
   4 8XMQ    [   
   4 8Y7B    UCOM
   8 8YM2          SBX   6  FX1  
  16 8_6L          ISSUCOMB OTHERERR,XBRK       [ISSUE COMMAND   
   8 8_L=          ADX   6  FX1  
   7 925W          EXIT  6  0
   4 92KG ...XBRK
  16 9356 ...      CALL  6  SJOBLOCK            [X1-> JOB BLOCK  
  19 93JQ ...      TRACEIF  K6INCRCUBS,199,399,JNAME(1),BRK  JBN   [BREAK-IN 
  16 944B ...      TRACEIF  K6INCRCUBS,199,399,CUBS(1),BRK CUBS  
   9 946# ...      FSHCODE  B,XBRKA  
   4 948= ...(   
  12 94=8 ...      TRANCHKN FSHINCBX,EQUAL,XBRK1 
   7 94#6 ...      TRANSFIN  
   5 94B4 ...XBRKA   
   4 94D2 ...)   
   5 94FY ...XBRK1   
   5 94J2          UP
   4 953L    #   
   4 953R ...[   
   4 953Y ...[   
   4 9545 ...[   
   6 954= ...X1INCRCUBX  
  10 954? ...      FSHCODE  A,X1INCRCONT 
   4 954# ...(   
   9 954* ...      NOTALIEN X1INCR   
  18 954B ...      TRANSBEG FSHINCBX,INCRCUBX,1,NOBLOCKS,,ACOMMUNE1,UACD 
   8 954C ...      BRN      XBRK1
   8 954D ...UACD  BRN      UP1A 
   6 954F ...X1INCRCONT  
   4 954G ...)   
   5 954H ...X1INCR  
  16 954J ...      CALL  6  SJOBLOCK            [X1-> JOB BLOCK  
  21 954K ...      SBS   3  CUBS(1)             [RESET CUBS  TO TRUE VALUE (IN CASE J
  21 954P ...                                   [       ABANDONED DURING QUESTION)   
  17 954W ...      SPRIVJUMP JSISSUE,1,SYSISS   [J IF SYSTEM-ISSUED  
  17 9553 ...      SPRIVJUMP JSSTART,1,SYSISS   [J IF SYSTEM-STARTED 
  18 9558 ...      BCONTEXT XBR,2,BREAKIN       [J IF BREAK-IN CONTEXT   
   9 955* ...      LDN   7  MREACH   
  18 955G ...      CALL  6  SETCOM              [SET UP DISPLAY COMMAND  
  21 955M ...      CALL  6  UCOM                [ISSUE ^DP LIMIT OF ONLINE BACKING ST
  19 955S ...                                   [       REACHED^ TO MON. FILE
  19 955_ ...                                   [       & CENTRAL OPS CONSOLE
   4 9566 ...QUES
   8 956? ...      LDN   7  MQUES
  18 956D ...      CALL  6  SETCOM              [SET UP QUESTION COMMAND 
  21 956K ...      CALL  6  UCOM                [QN 2,CAN THIS JOB'S LIMIT BE INCREAS
  11 956Q ...      MHUNTW   2,APED,ADISPLAY  
  19 956X ...      LDX   6  APEDDISM(2)         [X6= NO. OF CHARS IN ANSWER  
  19 9574 ...      LDCH  7  APEDDISM+1(2)       [PICK UP 1ST CHAR. OF ANSWER 
   7 9579 ...      FREECORE 2
  18 957B ...      BZE   6  WRANS               [J IF NO CHARS IN ANSWER 
   8 957H ...      SBN   7  #71  
  18 957N ...      BZE   7  REPYES              [J IF ANSWER ^Y.......^  
   9 957T ...      ADN   7  #71-#56  
  18 9582 ...      BZE   7  REPNO               [J IF ANSWER ^N.......^  
   5 9587 ...WRANS   
   9 958# ...      LDN   7  MYESNO   
  18 958F ...      CALL  6  SETCOM              [SET UP DISPLAY COMMAND  
  19 958L ...      CALL  6  UCOM                [DP 2,PLEASE ANSWER YES OR NO
   8 958R ...      BRN      QUES 
   5 958Y ...REPYES  
  16 9595 ...      CALL  6  SJOBLOCK            [X1-> JOB BLOCK  
  21 959= ...      STOZ     ACOMMUNE2(2)        [CLEAR 'SYSTEM-ISSUED/STARTED JOB' MA
  17 959C ...      LDX   0  IDEFBS              [PICK UP IP DEFAULTBS
   7 959J ...      SLA   0  1
  21 959P ...      BVCR     NOOVRFL             [IF OVERFLOW,SET AMBS TO LARGE +IVE N
   8 959W ...      LDCT  0  #377 
   5 95=3 ...NOOVRFL 
  16 95=8 ...      LDX   5  AMBS(1)             [X5= OLD LIMIT   
  17 95=* ...      STO   0  AMBS(1)             [SET NEW LIMIT (AMBS)
   7 95=G ...      SBX   5  0
  17 95=M ...      BZE   5  UNCH                [J IF LIMIT UNCHANGED
   7 95=S ...      LDX   4  0
  18 95=_ ...      SRL   4  1                   [X4= NEW LIMIT IN KWORDS 
   8 95?6 ...      LDN   7  MNEW 
  18 95?? ...      CALL  6  SETCOM              [SET UP DISPLAY COMMAND  
   8 95?D ...      SMO      FX2  
   9 95?K ...      LDX   0  ACOMMUNE2
  20 95?Q ...      BZE   0  NSYSISS             [J IF NOT SYSTEM-ISSUED/STARTED  
   8 95?X ...      LDX   0  0(2) 
   7 95#4 ...      SBN   0  2
  20 95#9 ...      STO   0  0(2)                [CHANGE DISPLAY TO 'ONLY TO MOP' 
   5 95#B ...NSYSISS 
  11 95#H ...      MHUNTW   1,ADATA,CREADL   
   9 95#N ...      ADN   1  CPDATA+4 
  21 95#T ...      XCBINDEX 2,1,4               [INSERT NEW VALUE IN DISPLAY COMMAND 
  18 95*2 ...      CALL  6  UCOM                [DP  ,LIMIT NOW N KWORDS 
  19 95*7 ...      BPZ   5  UNCH                [J IF LIMIT HAS NOT INCREASED
  16 95*# ...      CALL  6  SJOBLOCK            [X1-> JOB BLOCK  
  15 95*F ...      ADS   3  CUBS(1)             [UPDATE CUBS 
   9 95*L ...      LDX   0  AMBS(1)  
   9 95*R ...      SBX   0  CUBS(1)  
  20 95*Y ...      BPZ   0  YOK                 [J IF ENOUGH NOW  (BXGE NOT USED 
  21 95B5 ...                                   [   SINCE IF CUBS IS -IVE THIS CAUSES
  15 95B= ...                                   [   CHAOS)   
  17 95BC ...      SBS   3  CUBS(1)             [REDUCE CUBS AGAIN   
   8 95BJ ...      BRN      REPNO
   4 95BP ...#   
  21 95BW ...#  AUTOMATICALLY INCREASE AMBS FOR SYSTEM-ISSUED & SYSTEM-STARTED JOBS. 
   4 95C3 ...#   
   5 95C8 ...SYSISS  
  21 95C* ...      STO   2  ACOMMUNE2(2)        [ACOMMUNE2 NON-ZERO MEANS 'SYSTEM-ISS
   9 95CG ...      LDX   6  CUBS(1)  
  18 95CM ...      ADX   6  3                   [X6= NEW VALUE OF CUBS   
   9 95CS ...      LDX   0  AMBS(1)  
   8 95C_ ...      ADN   0  20   
  18 95D6 ...      BXGE  0  6,NOOVRFL           [J IF 10K EXTRA IS ENOUGH
  21 95D? ...      LDX   0  6                   [IF NOT, GIVE IT WHAT ITS ASKING FOR 
   9 95DD ...      BRN      NOOVRFL  
   4 95DK ...#   
  21 95DQ ...XBR                                [QUESTION COMMAND NOT ALLOWED IN BREA
  21 95DX ...                                   [ CONTEXT SO ALLOW UP TO 10K EXTRA & 
  17 95F4 ...                                   [ THEN REFUSE REQUEST
   9 95F9 ...      LDX   4  AMBS(1)  
   8 95FB ...      ADN   4  20   
   9 95FH ...      SBX   4  CUBS(1)  
  21 95FN ...      SBX   4  3                   [X4= NO. OF BLOCKS FROM THE 20 EXTRA 
  21 95FT ...                                   [   ALLOWED IN BREAK-IN CONTEXT WHICH
  17 95G2 ...                                   [   ARE STILL SPARE  
  21 95G7 ...      BNG   4  XBRN1               [J IF MORE THAN 20 BLOCKS OVER LIMIT 
   7 95G# ...      LDX   0  3
   7 95GF ...      SBN   0  1
  20 95GL ...      BNZ   0  XBRN1               [J IF NOT JUST ASKING FOR 1 BLOCK
   7 95GR ...      LDX   0  4
   7 95GY ...      ANDN  0  1
  21 95H5 ...      BNZ   0  XBROK               [J IF ODD NO. OF BLOCKS SPARE OUT OF 
   5 95H= ...XBRN1   
   9 95HC ...      LDN   7  MREACH   
  18 95HJ ...      CALL  6  SETCOM              [SET UP DISPLAY COMMAND  
   8 95HP ...      LDX   0  0(2) 
   7 95HW ...      SBN   0  2
  21 95J3 ...      STO   0  0(2)                [CHANGE DISPLAY TO GO ONLY TO MOP CON
  21 95J8 ...      CALL  6  UCOM                [DP 0,LIMIT OF ONLINE BACKING STORE R
  19 95J* ...      BNG   4  REPNO               [J IF >20 BLOCKS OVER LIMIT  
   9 95JG ...      LDN   7  MBREAK   
  18 95JM ...      CALL  6  SETCOM              [SET UP DISPLAY COMMAND  
  21 95JS ...      LDN   1  5(2)                [X1 _ POSITION TO INSERT HOW MUCH LEF
  19 95J_ ...      SRL   4  1                   [X4= KWORDS LEFT OF EXTRA 20 
  21 95K6 ...      XCBINDEX 2                   [CONVERT TO CHARS. & INSERT IN DP COM
   8 95K? ...      SMO      FX1  
   9 95KD ...      LDX   0  MBREAK+6 
   7 95KK ...      SBN   1  2
  20 95KQ ...      STO   0  0(1)                [REINSTATE ^TEXT^ IN DP COMMAND  
  21 95KX ...      CALL  6  UCOM                [DP 0,BREAK-IN CONTEXT N KWORDS MORE 
  18 95L4 ...                                   [               ALLOWED  
  16 95L9 ...      CALL  6  SJOBLOCK            [X6-> JOB BLOCK  
   5 95LB ...XBROK   
  21 95LH ...      ADS   3  CUBS(1)             [UPDATE CUBS SINCE REQUEST GRANTED   
   4 95LN ...YOK 
   8 95LT ...      SETREP2  OK   
  21 95M2 ...      TRACEIF  K6INCRCUBS,199,399,JNAME(1),OPS  OK    [ASKED OPS & OK   
  16 95M7 ...      TRACEIF  K6INCRCUBS,199,399,CUBS(1),OPS CUBS  
   4 95M# ...UP1 
   9 95M* ...      FSHCODE  B,UP1CONT
   4 95MB ...(   
  12 95MC ...      TRANCHKN FSHINCBX,EQUAL,UP1A  
   7 95MD ...      LDN   5  1
   7 95MF ...      TRANSFIN 5
   5 95MG ...UP1CONT 
   4 95MH ...)   
   4 95MJ ...UP1A
   7 95MK ...      UPPLUS   1
   4 95ML ...UNCH
   9 95MR ...      LDN   7  MNOTINC  
  18 95MY ...      CALL  6  SETCOM              [SET UP DISPLAY COMMAND  
  18 95N5 ...      CALL  6  UCOM                [DP 2,LIMIT NOT INCREASED
   5 95N= ...REPNO   
   9 95NC ...      SETREP2  REFUSED  
  16 95NJ ...      CALL  6  SJOBLOCK            [X1-> JOB BLOCK  
  21 95NP ...      TRACEIF  K6INCRCUBS,199,399,JNAME(1),OPS  REF   [ASKED OPS & REFUS
  16 95NW ...      TRACEIF  K6INCRCUBS,199,399,CUBS(1),OPS CUBS  
   8 95P3 ...      BRN      UP1  
   4 95P8 ...[   
   4 95P* ...[   
   4 95PG ...[   
   6 95PM ...X2INCRCUBX  
  14 95PS ...      CALL  6  SFCA                [X1-> FCA
  17 95P_ ...      STO   1  ACOMMUNE2(2)        [REMEMBER CURRENT FCA
  17 95Q6 ...      PSTAC    1,1                 [X1-> FSTACK BLOCK   
  14 95Q7 ...      BFCBX    2,1                 [X2-> FCB
  21 95Q8 ...      JBS      UP,2,BFWORK         [EXIT IF WORKFILE (CAN'T BE OPEN TO A
  21 95Q9 ...                                   [ ONLY COUNT ! FILES ONCE PER JOB)   
  16 95Q? ...      LDEX  7  ARINGNO(1)          [NO.OF ELEMENTS  
  16 95QD ...      ADN   1  A1                  [FIRST ELEMENT   
   4 95QK ...[   
  19 95QQ ...[     TAKE EACH ELEMENT IN TURN,UPDATE ITS CUBS IF IT HAS ONE   
   4 95QX ...[   
   6 95R4 ...NEXTLMLOOK  
   8 95R9 ...      SMO      FX2  
  21 95RB ...      BXE   1  ACOMMUNE2,NEXTELEM  [J IF THIS FCA IS ONE SPECIFIED BY DE
  15 95RH ...      JBC      NEXTELEM,1,BACUBS   [J IF PUBLIC 
  18 95RN ...      LDX   4  1                   [X4-> FILE CONTROL AREA  
  11 95RT ...[     SEARCH FOR ACTIVITY BLOCK 
   5 95S2 ...NOTACT  
   7 95S7 ...      SMO      4
  16 95S# ...      LDX   2  FPTR                [X2-> NEXT FCA   
   7 95SF ...      LDX   4  2
  15 95SL ...      ADX   2  FBACKPOINT(2)       [X2-> BLOCK  
   9 95SR ...      LDX   0  ATYPE(2) 
  19 95SY ...      BXL   0  CACT,NOTACT         [J IF NOT AN ACTIVITY BLOCK  
   4 95T5 ...[   
   9 95T= ...[     X2-> ACT. BLOCK   
   4 95TC ...[   
   9 95TJ ...      LDX   0  JOBNO(2) 
  20 95TP ...      BZE   0  NEXTELEM            [J IF NO JOB NO. (MAY BE CLOSE)  
  14 95TW ...      LDX   5  1                   [X5-> FCA
  16 95W3 ...      CALL  6  SJOBLOCK            [X1-> JOB BLOCK  
   4 95W8 ...[   
   4 95W* ...[   
  15 95WG ...      JBS      SUBC,FX2,XBSUBCUBS  [J IF SUBCUBS
  16 95WM ...      ADS   3  CUBS(1)             [INCREASE CUBS   
  21 95WS ...      TRACEIF  K6INCRCUBS,199,399,JNAME(1),COM  JBN   [COMMUNE & THIS IS
  16 95W_ ...      TRACEIF  K6INCRCUBS,199,399,CUBS(1),COM CUBS  
   9 95X6 ...      LDX   0  CUBS(1)  
  16 95X? ...      BXL   0  GUBS(1),SUBD        [J IF CUBS < GUBS
  15 95XD ...      STO   0  GUBS(1)             [RESET GUBS  
   4 95XK ...SUBD
  14 95XQ ...      LDX   1  5                   [X1-> FCA
   5 95XX ...NEXTELEM
   9 95Y4 ...      ADN   1  FELLEN   
  10 95Y9 ...      BCT   7  NEXTLMLOOK   
   4 95Y? ...UP  
   5 95YB ...      UP
   4 95YH ...SUBC
  15 95YN ...      SBS   3  CUBS(1)             [REDUCE CUBS 
  17 95YT ...      TRACEIF  K6INCRCUBS,199,399,JNAME(1),SUBC JBN   [ 
  16 95_2 ...      TRACEIF  K6INCRCUBS,199,399,CUBS(1),SUBCCUBS  
   8 95_7 ...      BRN      SUBD 
   4 95_# ...[   
   4 95_F ...#   
   5 95_L ...NOLOBJOB
  17 95_R ...      GEOERR   1,NOLOBJOB          [ JOB BLOCK MISSING  
   5 95_Y ...OTHERERR
  19 9625 ...      GEOERR   1,INCRCUBS          [ANY OTHER CLEAR MALFUNCTION 
   4 962W    #   
   4 96GG    #END
   8 ____ ...22731127000700000000
