  15 22FL    #SEG  EXMESS70                     [JOHN BAILEY 
   7 22_=    #LIS  K0EXMESS  
   7 23DW          8HEXMESS  
   4 23YG    #   
  12 24D6          SEGENTRY K1EXMESS,QK1EXMESS   
  12 24XQ          SEGENTRY K2EXMESS,QK2EXMESS   
  12 25CB          SEGENTRY K3EXMESS,QK3EXMESS   
   4 25X2    #   
   6 26BL    QK1EXMESS   
  11 26W=          HUNTW    3,EWDAS,EWMESS   
  18 27*W          BPZ   3  Z1                  [JUMP IF ALREADY A EWMESS
  14 27TG          SETNCORE BMSTART-A1+12,3,EWDAS,EWMESS 
   9 28*6          LDN   4  BMSTART  
  15 28SQ          STO   4  A1(3)               [SET POINTER 
  10 29#B    Z1    LDN   4  ACOMMUNE7(2) 
   8 29S2          LDX   5  A1(3)
   7 2=?L          ADX   5  3
  19 2=R=          MOVE  4  3                   [MOVE MESSAGE TO EWMESS BLOCK
   7 2?=W          LDN   5  3
  16 2?QG          ADS   5  A1(3)               [UPDATE POINTER  
   8 2#=6          LDX   5  A1(3)
   8 2#PQ          SBN   5  A1   
  19 2*9B          TXU   5  ALOGLEN(3)          [ROOM FOR ANOTHER MESSAGE?   
  15 2*P2          BCS      Z2                  [JUMP IF YES 
   8 2B8L          ADN   5  12   
  19 2BN=          ALTLEN   3,5,EWDAS,EWMESS    [LENGTHEN BLOCK BY 12 WORDS  
   5 2C7W    Z2    UP
   6 2CMG    QK2EXMESS   
  18 2D76          NGNC  4  1                   [IGNORE OPTIONAL MESSAGES
   6 2DLQ    QK3EXMESS   
  17 2F6B          LDN   4  0                   [OUTPUT ALL MESSAGES 
  11 2FL2          MHUNTW   1,EWDAS,EWMESS   
   9 2G5L          LDN   3  BMSTART  
  17 2GK=    Z10   TXU   3  A1(1)               [ANY MORE MESSAGES?  
   7 2H4W          BCS   Z3  
  17 2HJG          FREECORE 1                   [FREE EWMESS BLOCK   
   5 2J46          UP
   7 2JHQ    Z3    SMO      3
  17 2K3B          LDXC  5  0(1)                [MESSAGE IDENTIFIER  
  17 2KH2          BCC      Z4                  [JUMP IF NOT OPTIONAL
  20 2L2L          BNZ   4  Z5                  [IGNORE MESSAGE IF FROM K2EXMESS 
   7 2LG=    Z4    SMO      3
  15 2L_W          LDX   6  1(1)                [SERIAL NO   
   7 2MFG          SMO      3
  14 2M_6          LDX   7  2(1)                [BLOCK NO
  16 2NDQ          BNG   6  Z9                  [BRANCH IF NULL  
  17 2NYB          BNZ   6  Z7                  [BRANCH IF EXISTANT  
  17 2PD2          BNG   7  Z8                  [BOTH NON-EXISTANT   
  18 2PXL          BRN      Z9                  [ONLY ONE NON-EXISTANT   
   7 2QC=    Z7    LDN   0  6
  15 2QWW          LDN   2  BSER(1)             [CONVERT...  
  15 2RBG          SLL   6  3                   [...SERIAL...
  15 2RW6    Z11   SLC   6  6                   [...NUMBER...
  15 2S*Q          DCH   6  0(2)                [...TO OCTAL 
   7 2STB          BCHX  2  /
   7 2T*2          SRL   6  3
   8 2TSL          BCT   0  Z11  
  17 2W#=          LDN   6  1                   [RESET X6 AS MARKER  
   7 2WRW    Z9    LDN   0  5
  18 2X?G          OUTBLOCK                     [CREATE PARAMETER BLOCK  
  13 2XR6          BNG   6  Z20                 [NULL
  15 2Y=Q          BZE   6  Z13                 [NON EXISTANT
   7 2YQB          LDN   0  6
  19 2_=2          OUTPARAM 0,BSER,EWDAS,EWMESS [SERIAL NO TO PARAMETER BLOCK
  15 2_PL    Z13   BNG   7  Z8                  [NON-EXISTANT
  19 329=          OUTNUM   7,0                 [BLOCK NO. TO PARAMETER BLOCK
   8 32NW    Z8    LDCT  0  128  
  16 338G          ANDX  0  5                   [ISOLATE BIT 1   
   9 33N6          ANDX  5  BITS22LS 
  21 347Q          BZE   0  Z21                 [BRANCH IF TO MONITORING FILE ONLY   
   7 34MB          LDN   7  1
  18 3572          INFORMX  7,5,1               [MESSAGE TO OPS CONSOLE  
   8 35LL          BRN      Z22  
   7 366=    Z21   MONOUTX  5
  17 36KW    Z22   HUNTW    1,EWDAS,EWMESS      [REFIND EWMESS BLOCK 
  15 375G    Z5    ADN   3  3                   [NEXT RECORD 
   8 37K6          BRN      Z10  
  16 384Q    Z20   OUTNULL                      [NULL PARAMETER  
   8 38JB          BRN      Z13  
   4 3942    #END
   8 ____ ...15030261000100000000
