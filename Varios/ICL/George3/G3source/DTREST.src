  13 22#C ...  SEG DTREST,860,C.R.JOHNSON,SCHEDULER  
   4 22LS ...[   
  11 22_=          SEGENTRY K1DTREST,X1DTREST
   4 23DW ...[   
  17 25CB    SENTEOF        #40000000           [TYPE IN EOF SENTINEL
   9 25X2    YARCHM         8HARCH(M)
   9 26BL    YARCH          4HARCH   
   4 26LD ...[   
  12 26W=    [     AWORK1 CONTAINS UNIT NUMBER   
  19 27*W    [     AWORK2 CONTAINS AUXILIARY REPLY-PASSED BACK IN ACOMMUNE1  
  17 27TG    [     AWORK3 PRESERVES MAIN REPLY THROUGH TAPEPOS S.R.  
   4 28SQ    [   
   4 28XF ...[   
  21 28YB ...[   ******************************************************************  
   4 28_? ...[   
  16 2928 ...[     THIS SUBROUTINE SETS DUMPER'S SEARCHING BITS. 
   4 2935 ...[   
  13 2942 ...XNOTSEARCH                          [   
  14 294X ...      LDX   2  FX2                  [SET X2 
  18 295S ...      TOPFCB2  1                    [X1 -> DIRECTORTES FCB  
  17 296P ...      JBS      XNOTSET,1,BFDUMP     [BFDUMP ALREADY SET 
  15 297L ...      MBS      1,BFDUMP,BFDIRUPDATE [SET BITS   
  13 297R ...XNOTSET                             [   
  13 297Y ...      EXIT  7  0                    [   
   4 2985 ...[   
  21 298= ...[   ******************************************************************  
   4 298C ...[   
  17 298J ...[     SUBROUTINE TO FIND FDT ENTRY FOR TSN IN ADTSN(FX2)
  12 298P ...[              X1 -> ENTRY ON EXIT  
  13 298W ...[              X6 IS LINK ACCUMULATOR   
  14 2993 ...[              IF ENTRY IS ABSENT , GEOERR  
   4 2998 ...[   
  12 299* ...POINTFDT                           [
  12 299G ...      SMO      FX2                 [
  15 299M ...      LDX   0  ADTSN               [X0 => TSN   
  15 299S ...      FDTENT   1,0                 [X1 -> ENTRY 
  12 299_ ...      EXIT  6  0                   [
   4 29=* ...[   
  21 29?* ...[   ******************************************************************  
   4 29#B    [   
  21 29S2    [     TAPE READ SUBROUTINE-TRIES ITS HARDEST TO PUT TAPE AFTER CORRECT  
  17 2=?L    [     EOF SENTINEL-X7 CONTAINS COUNT OF BLOCKS IN FILE  
   4 2=R=    [   
   5 2?=W    TAPEPOS 
   7 2?QG          LDN   5  0
   9 2#=6          BRN      SETMARK  
   5 2#PQ    TAPEREST
   7 2*9B          LDN   5  1
   5 2*P2    SETMARK 
  10 2B8L          MHUNTW   3,FILE,FRB   
  15 2BN=          LDX   7  ESENCT(3)           [BLOCK COUNT 
  19 2C7W          LDX   4  7                   [KEEP COUNT OF BLOCKS IN X4  
   8 2CMG          SBX   6  FX1  
   6 2D76    MTREADWRITE 
  11 2DLQ ...      SETNCORE GSBS,3,FILE,FWB,1
   5 2F6B    MTREADX 
  16 2FL2          MTREAD   FILE,FWB            [READ NEXT BLOCK 
   9 2G5L          BZE   7  MTFILEND 
  16 2GK=          SBN   7  1                   [DECR BLOCK COUNT
  10 2H4W          TESTRPN2  OK,TROUBLE  
   4 2H#N ...[   
  17 2HJG    [              TO STOP CORE FLOODING WITH WRITE BLOCKS  
   4 2HWX ...[   
  17 2J7J ...      LDN   1  0                   [ONLY ALLOW 4 BUFFERS
  20 2JD9 ...      LDX   0  4                   [X0 => TOTAL NUMBER OF BLOCKS.   
  21 2JNW ...      SBX   0  7                   [X0 => NUMBER OF BLOCKS TRANSFERRED. 
  12 2J_H ...      SRC   01 2                   [
   9 2K=8 ...      BNZ   1  NOWAITING
  16 2KH2          BACKWAIT                     [EVERY 4TH ONE   
   6 2LG=    NOWAITING   
  16 2MFG          BZE   5  MTREADX             [READ NEXT BLOCK 
   4 2MH# ...[   
  14 2MK6 ...[              GET READY TO WRITE A  BLOCK  
   4 2MLY ...[   
  10 2MNS ...      MHUNTW   3,BSTB,FULLB 
   8 2MSF ...      LDX   0  A1(3)
   7 2MY6 ...      SBN   0  2
   7 2N3R ...      SBX   0  7
   7 2N7D ...      SMO      0
   9 2N?5 ...      LDX   0  A1+1(3)  
  17 2NBQ ...      STO   0  AWORK4(2)           [CURRENT BLOCK NUMBER
   9 2NGC ...      LDX   0  A1+1(3)  
  16 2NL4 ...      STO   0  AWORK3(2)           [RESIDENCE NUM   
  10 2NPP ...      MHUNTW   3,FILE,FWB   
  16 2NTB ...      LDN   1  GSBS                [TRANSFER LENGTH 
   8 2N_3 ...      LDN   0  A1(3)
  18 2P4N ...      STO   0  AWORK2(2)           [TRANSFER START ADDRESS  
  18 2P8* ...      WRITEAUTO FREE+FAIL,1,,AWORK3(2),AWORK4(2),AWORK2(2)  
  10 2P#2 ...      BRN      MTREADWRITE  
   4 2PD2    [   
  21 2PXL    [     CORRECT NO.OF BLOCKS HAVE BEEN READ FROM TAPE-WE SHOULD NOW HAVE  
   7 2QC=    [     A SENTINEL
   4 2QWW    [   
   5 2RBG    MTFILEND
  10 2RW6          TESTRPN2  SENT,NOTSENT
  20 2S*Q    SCHK  LDX   0  SENTEOF(1)          [CHECK THAT IT IS END SENTINEL   
  15 2STB          BXU   0  ESENTYP(3),XFORMAT  [J IF NOT EOF
  18 2T*2          BXU   4  ESENBP(3),XFORMAT   [BLOCK COUNT INCORRECT   
   8 2TSL          KEEPREP  7,OK 
   8 2W#=          BRN      OUT  
   4 2WRW    [   
  21 2X?G    [     BLK CT EXHAUSTED,BUT NO SENT. YET.IF NOT FAIL,TRY SKIP TO SENTINEL
   4 2XR6    [   
   5 2Y=Q    NOTSENT 
  11 2YQB          TESTRPN2  FAIL,SKIPSENT   
   9 2_=2    MFAIL KEEPREP  7,FAIL   
   8 2_PL          BRN      OUT  
   4 329=    [   
  11 32NW    [     SOME SORT OF FORMAT ERROR 
   4 338G    [   
   5 33N6    XFORMAT 
   9 347Q          KEEPREP  7,FORMAT 
   8 34MB          BRN      OUT  
   4 3572    [   
  11 35LL    [     NON OK REPLY WHILE READING
   4 366=    [   
   5 36KW    TROUBLE 
  10 375G          TESTRPN2  FAIL,XFORMAT
  16 37K6    [     MUST HAVE BEEN A FAIL-TRY SKIPPING TO SENTINEL
   5 384Q    SKIPSENT
   9 38JB          MTSKIP   FILE,FWB 
  13 3942          TESTREP2 FAIL,MFAIL,SENT,XFORMAT  
   4 39HL    [   
  21 3=3=    [     NOW A SENTINEL HAS BEEN FOUND-IF IT IS THE RIGHT ONE.THEN REPLY   
  14 3=GW    [     OK FOR TAPEPOS BUT FORMAT FOR TAPEREST
   4 3?2G    [   
  21 3?G6          BZE   5  SCHK                [J IF TAPE BEING POSITIONED-SENT CHEK
  17 3?_Q          BRN      XFORMAT             [ELSE FORMAT ERROR   
  16 3#FB    OUT   STO   7  AWORK2(2)           [AUXILIARY RAEPLY
   9 3#RR ...      MFREEW   FILE,FWB 
   8 3*68 ...      ADX   6  FX1  
   7 3*DL          EXIT  6  0
   4 3*KS ...[   
  21 3*R2 ...[   ******************************************************************  
   4 3*Y=    [   
  13 3BCW    [     SEARCH FOR FREQ IN OFF-LINE CHAIN 
   4 3BXG    [   
  12 3CC6 ...SFCB                               [
  16 3CWQ ...      CALL  6  POINTFDT            [X1 -> FDT ENTRY 
  18 3DBB ...      LDX   4  FDINC(1)            [X4 => INCREMENT NUMBER  
  17 3DW2 ...      LDX   5  FDFILE(1)           [X5 => FILE NUMBER   
  18 3F*L ...      LF       1,FDTFSNO,6         [X6 => FILESTORE NUMBER  
  19 3FT= ...      LDX   2  BOLC                [X2 -> START OF OFFLINE CHAIN
  12 3G#W ...      LDN   0  BOLC                [
  12 3GSG ...MISF  LDX   2  FPTR(2)             [
  17 3H#6          BXE   2  0,XIT               [J IF END OF CHAIN   
  17 3HRQ          BXL   4  FDTINCNUM(2),XIT    [     INC NOT THERE  
  18 3J?B          BXU   4  FDTINCNUM(2),MISF   [     INC NOT YET REACHED
  18 3JDJ ...      BXL   6  FDTFILSTR(2),XIT    [     FILESTORE NOT THERE
  20 3JKQ ...      BXU   6  FDTFILSTR(2),MISF   [     FILESTORE NOT YET REACHED  
  17 3JR2          BXL   5  FDTFILNUM(2),XIT    [     FILE NOT THERE 
  19 3K=L          BXU   5  FDTFILNUM(2),MISF   [     FILE NOT YET REACHED   
  16 3KQ= ...      EXIT  7  1                   [FREQ BLOCK FOUND
  17 3L9W ...XIT   EXIT  7  0                   [FREQ BLOCK NOT FOUND
   4 3LPG ...[   
  21 3M96 ...[   ******************************************************************  
   4 3MNQ    [   
  14 3N8B ...[     SUBROUTINE FOR SYSTEM JOURNAL MESSAGES
   4 3NN2    [   
  19 3P7L    [     OUTPACKX PUT IN SUBROUTINE TO LIMIT COMPILATION EXPANSION 
  14 3PM=    [           X4=START OF MESSAGE PARAMETER   
  11 3Q6W    [           X5=LENGTH OF MESSAGE
  11 3QLG    [     X6=NUMERICAL VALUE OF PDC 
   4 3QW# ...[   
   5 3R66    PACKIT  
   8 3RKQ          SBX   7  FX1  
   8 3S5B          OUTPACKX 4,5,6
  17 3SK2          ADX   7  FX1                 [EXIT MAY HAVE CO-ORD
   7 3T4L          EXIT  7  0
   4 3TJ= ...[   
  21 3W3W ...[   ******************************************************************  
   4 3WHG ...[   
  20 3X36 ...[     DTREST IS ENTERED (DOWN) FROM DTSCAN WITH THE TAPE POSITIONED 
  21 3XGQ ...[     AFTER THE START OF FILE SENTINEL. A 10 WORD FABSNB HAS BEEN SET   
   8 3Y2B ...[     UP BY DTSCAN  
  19 3YG2 ...[     THE OCCASION OF A BACKING STORE JAM DOES NOT INHIBIT THE  
  21 3Y_L ...[     RESTORING OF FILES. IF THE JAM IS SUCH THAT NO BACKING STORE IS   
  20 3_F= ...[     AVAILABLE PROC TAPES AND NON-CURRENT DUMP TAPES WILL BE SET   
  21 3_YW ...[     WAITING FOR BACKING STORE; CURRENT DUMP TAPES WILL BE POSITIONED  
  20 42DG ...[     AT THE END OF THE FILE AND ALLOWED TO WIND ON IN CASE DUMPER  
  10 42Y6 ...[     SHOULD REQUIRE THEM.  
   4 43CQ ...[   
   4 43XB ...[   
   4 44C2 ...[   
   5 44WL ...X1DTREST
  19 45B= ...      MHUNTW   3,FILE,FRB          [START OF SUBFILE SENTINEL.  
  16 45TW ...      CALL  6  POINTFDT            [X1 -> FDT ENTRY 
  18 46*G ...      JBS      WCDUMP,1,BIFDTCD    [J IF CURRENT DUMP TAPE  
  17 47#Q ...      WHATBACK 3,ESENCT(3)         [MANDATORY BS REQUEST
   8 47SB ...      BRN      XBACK
   5 48#2 ...WCDUMP  
  17 48RL ...      WHATBACK 3,ESENCT(3),NONAV   [OPTIONAL BS REQUEST 
  20 49?= ...XBACK                              [BACKING STORE GOT. FILE IS NOW  
  16 49QW ...                                   [BROUGHT ONLINE  
  18 8Y7B          CALL  6  TAPEREST            [RESTORE FILE FROM TAPE  
  21 8YM2          BNZ   7  MTFAIL              [IF REPLY NON-OK,SET MAIN REPLY'FAIL'
  16 8YWS ...      STOZ     AWORK2(2)           [SET AUX REPLY OK
   4 8_2P ...[   
  11 8_6L    [     FILE RESTORED FROM TAPE   
   4 8_BD ...[   
  14 8_L= ...      OPENDIR  XBREAK,GENERAL,ERASING,QUERY 
   4 925W    [   
  21 92KG    [     ERASING BECAUSE IF LF,ERASE IS DONE,AND FILE HAS TO BE RESTORED,  
  20 9356    [     THEN UNLESS ERASING MODE SET,THE FILE WILL APPEAR TO HAVE BEEN
   8 93JQ    [     ERASED ALREADY
   4 944B    [   
   9 947P ...[      NOW CHECK REPLY  
   4 94?4 ...[   
  20 94BD ...      TESTREP2 OK,TESTINC,NOUSER,PLOSS,NOFILE,PLOSS,VRYWRONG,PLOSS  
  10 94HF ...      TESTREP2 NAME,PLOSS   
   9 94NG ...      GEOERR   1,DIRSHUT
  20 94W3 ...                                   [DIRECTORY OPENED OK, CHECKS ARE 
  21 953J ...                                   [NOW PERFORMED TO ENSURE THAT THIS   
  20 9595 ...                                   [IS THE LATEST DUMP OF THE FILE. 
  12 95BL ...TESTINC                            [
  12 95J7 ...      MHUNTW   3,FILE,ENT          [
  18 95PN ...      LDX   7  EINC(3)             [X7 => INCREMENT NUMBER  
  21 95X9 ...      BZE   7  OLDMP               [J IF INC NO. ZERO - FILE RE-INPUT?  
  16 964Q ...      CALL  6  POINTFDT            [X1 -> FDT ENTRY 
  21 96=? ...      LDX   6  FDINC(1)            [INC NO. OF TAPES CURRENT POSITION   
  18 96CT ...      BXU   7  6,OLDMP             [OLD DUMP IF NOT EQUAL   
  12 96JW ...      LDEX  0  ECOPS(3)            [
  19 96PX ...      BNZ   0  ONL                 [JUMP IF FILE ALREADY ONLINE 
  21 96WY ...                                   [IT HAS BEEN ESTABLISHED THAT THIS   
  20 973_ ...                                   [IS THE LATEST DUMP OF THE FILE. 
  21 9792 ...                                   [THE NAME RECORD IS UPDATED AND THE  
  19 97B3 ...                                   [BLOCKS RECORD IS INSERTED.  
  10 97H4 ...      MHUNTW   1,BSTB,FULLB 
   8 97N5 ...      LDX   0  A1(1)
  18 97T6 ...      SBN   0  2                   [NUMBER OF BLOCKS IN FILE
  16 9827 ...      SMO      0                   [ARE LOADED INTO 
  16 9878 ...      LDCT  0  0                   [THE COUNT FIELD 
  16 98#9 ...      ADN   0  1                   [ONLINE INDICATOR
   9 98F= ...      STO   0  ECOPS(3) 
  14 98L? ...      MHUNTW   1,FILE,FRB          [SENTINEL
  14 98R# ...      LDX   0  ESEN10(1)           [FENDBUCK
  10 98Y* ...      STO   0  EENDBUCK(3)  
  14 995B ...      LDX   0  ESEN11(1)           [FVERSION
  10 99=C ...      STO   0  EVERSION(3)  
  14 99CD ...      LDX   0  ESEN17(1)           [FSIZE   
   9 99JF ...      DEX   0  EINF3(3) 
   7 99PG ...#UNSET  JSKI28  
   4 99WH ...(   
  16 9=3J ...#     *******************************************   
  10 9=8K ...#     IP REDUMP CSS SPECIAL 
  18 9=?5 ...      LDX   4  EINC(3)             [X4 => INCREMENT NUMBER  
   9 9=*L ...      SEGENTRY K98DTREST
  19 9=MN ...      NULL                         [WHEN SWITCHED ON THIS LINE  
  18 9=SP ...                                   [IS 'TXL 4 INSTPARAM+1'  
   8 9=_Q ...      BCC      NTBRD
   9 9?6R ...      BS       3,BEDUMP 
  19 9?=5 ...      CALL  7  XNOTSEARCH          [SET DUMPER'S SEARCHING BITS 
   5 9??S ...NTBRD   
  16 9?DT ...#     ___________________________________________   
   4 9?KW ...)   
  18 9?M6 ...      JBC      PFCCOK1,3,BEPFCC    [J IF NO NEED FOR PFCC'S 
  18 9?NB ...      BS       3,BEDUMP            [SET 'TO BE DUMPED' BIT  
  12 9?PL ...PFCCOK1                            [
  16 9?QX ...      NAME     3,FILE,FWB          [RENAME ENT BLOCK
  15 9?XY ...      REWRITE                      [NAME RECORD 
  10 9#4_ ...      MHUNTW   3,FILE,FWB   
  17 9#=2 ...      NAME     3,FILE,ENT          [BACK TO ENT AGAIN   
  10 9#C3 ...      MHUNTW   3,BSTB,FULLB 
  15 9#J4 ...      NAME     3,FILE,FWB          [RENAME FULLB
  17 9#RK ...      STEP                         [MOVE POINTER ON ONE 
  21 9*37 ...      INSERT                       [BLOCKS RECORD INSERTED BEFORE TRAPS 
  11 9*5M ...      TESTREP  FILEFULL,XFULL   
  10 9*88 ...      MHUNTW   3,FILE,FWB   
  17 9**9 ...      NAME     3,BSTB,FULLB        [BACK TO FULLB AGAIN 
   9 9*G= ...      LDX   0  A1+1(3)  
  16 9*M? ...      STO   0  AWORK4(2)           [SAVE RES NUMBER 
   4 9*_# ...[   
  20 9B?* ...[ THIS CODING TESTS FOR THE POSSIBILITY THAT WHILE THE FILE IS BEING
  21 9BKB ...[ RETRIEVED FROM TAPE AN ALLOCATE COMMAND MAY HAVE BEEN GIVEN FOR THIS  
   5 9BXC ...[ FILE  
   4 9C9D ...[   
  10 9CHJ ...      MHUNTW   3,FILE,ENT   
  16 9CRB          LDX   4  EINC(3)             [PICK UP INC NO  
  19 9D?2          STO   4  AWORK1(2)           [PRESERVE X4 FOR SJ MESSAGE  
   9 9DQL          LDX   4  EINF2(3) 
  18 9F==          STO   4  AWORK3(2)           [PRESERVE FOR SJ MESSAGE 
  19 9FPW          LDX   5  EALLOC(3)           [VALUE OF FRN IN DIR. ENTRY  
   8 9G9G          ANDN  5  #77  
  21 9GP6          LDX   4  AWORK4(2)           [RECOVER FRN BEFORE THE TAPE RSSTORE 
   7 9H8Q          SBX   4  5
  17 9HNB          BZE   4  XCONT               [J IF FRN NOT ALTERED
   7 9J82          SBN   5  1
  19 9JML ...      BZE   5  XCONT               [J IF NEW VALUE CORRESPONDS  
  19 9K7= ...      BNG   5  XCONT               [TO CANCEL OR PUBLIC ALLOCATE
   5 9KLW    XDALL   
   7 9L6G          ADN   5  1
   9 9LH2 ...      RESQUERY 5,XCONT  
  17 9LP= ...      LDX   2  FX2                 [RESQUERY CORRUPTS X2
  17 9LXG ...      STO   5  AWORK4(2)           [SAVE NEW RES NUMBER 
  19 9L_4 ...      MHUNTW 1,FILE,FABSNB         [SET UP FABSNB FOR COPYFILE  
  17 9M2L ...      LDX   3  A1(1)               [FABSNB RECORD HEADER
  17 9M48 ...      SETUPCORE 3,2,FILE,FABSNB    [SET UP NEW FABSNB   
  17 9M5Q ...      HUNT2 1,FILE,FABSNB,2        [FIND ORIGONAL FABSNB
   8 9M7# ...      LDN   6  A1(1)
   8 9M8W ...      LDN   7  A1(2)
  15 9M=D ...      MOVE  6  0(3)                [MOVE IN DATA
   7 9MQ4 ...      COPYFILE 5
   5 9PHW    XCONT   
   4 9PK? ...[   
  12 9PLN ...[ THIS CODING INSERTS PFCC'S IF THE 
  11 9PN5 ...[ FILE IS A MARK 6 BASIC FILE   
   4 9PPG ...[   
  16 9PQX ...      MHUNTW 3,FILE,ENT            [FIND ENT BLOCK  
  21 9PS# ...      JBC PFCCOK,3,BEPFCC          [BIT BEPFCC SET IF FILE NEEDS PFCC'S 
  16 9PTP ...      BACKSPACE                    [SET UP DIRECTORY
  15 9PX6 ...      BACKSPACE                    [FOR OPENREL 
  17 9PYH ...      SETNCORE 6,2,FILE,FLOCNB     [SET UP FOR OPENREL  
  15 9P_Y ...      MHUNTW 3,FILE,ENT            [REHUNT ENT  
  17 9Q3* ...      LDN   6  ELOC1(3)            [POINT TO LOCAL NAME 
  17 9Q4Q ...      LDN   7  A1(2)               [POINTER IN FLOCNB   
  19 9Q67 ...      MOVE  6  5                   [MOVE 5 WORDS OF LOCAL NAME  
  18 9Q7J ...      LDX   7  ELAN(3)             [PICK UP LANGUAGE CODE   
  17 9Q8_ ...      STO   7  A1+5(2)             [STORE LANGUAGE CODE 
  17 9Q=B ...      OPENRELN XBREAK,READ,LEAVE,DIROPEN,ERASING,QUERY  
  20 9Q?R ...      TESTREP2 OK,XFILOPEN,NOUSER,PLOSS,NOFILE,PLOSS,VRYWRONG,PLOSS 
   9 9Q*8 ...      GEOERR 1,FILSHUT  
   5 9QBK ...XFILOPEN
  18 9QD2 ...      LDN   6  #41                 [USED TO SET UP PFCC'S   
   5 9QFC ...PLOOP   
  16 9QGS ...      STEP                         [GET NEXT RECORD 
  16 9QJ9 ...      BZE   3  PEND                [J IF END OF FILE
   8 9QKL ...      LDX   0  1(3) 
   8 9QM3 ...      ANDN  0  #77  
   8 9QND ...      BNZ   0  PLOOP
   8 9QPT ...      ORS   6  1(3) 
   7 9QR= ...      STEPREWR  
   8 9QSM ...      BRN      PLOOP
   4 9QW4 ...PEND
  21 9QXF ...      CLOSETOP                     [CLOSE THE FILE....DIRECTORY STILL  O
   6 9QXY ...#UNS FNSAD  
  19 9QYC ...      CALL  7  XNOTSEARCH          [SET DUMPERS SEARCHING BITS  
   5 9QYW ...PFCCOK  
   4 9R=4 ...[   
  21 9RH= ...[     THIS PIECE OF BORING ONLINE CODING IS TO PREPARE THE PARAMETERS   
  15 9RSD ...[     FOR OUTPUTING THE SYSTEM JOURNAL MESSAGE-:
  18 9S5L ...[     %A.%B(%C/%D)RETRIEVED ON %H AT %K(GEORGE MEAN TIME%G) 
  19 9SBS ...[     FROM TAPE %L INC %M TO RESIDENCE %E-SIZE%F BLOCKS,%J(OPT) 
   4 9SN2 ...[   
  15 9S_= ...      TOPFCB   3                   [X3  TOP FCB 
  15 9TDW ...      LDN   4  FME1(3)             [USER NAME   
   7 9TYG          LDN   5  3
  10 9WD6          LDN   6  JPDUSERNAME  
   9 9WXQ          CALL  7  PACKIT   
  21 9XCB                                       [ALWAYS ASSUME OUTPACKX CO-ORDINATES 
   9 9XX2 ...      MHUNTW  3,FILE,ENT
   9 9YBL ...      LDN   4  ELOC1(3) 
   7 9YW=          LDN   5  3
  15 9_*W          LDN   6  JPDLOCNAME          [LOCAL NAME  
   9 9_TG          CALL  7  PACKIT   
  10 =2*6 ...      MHUNTW   3,FILE,ENT   
  17 =2SQ ...      LDN   4  EGEN(3)             [GENERATION NUMBER   
   7 =3#B          LDN   5  1
   9 =3S2          LDN   6  JPDGENNUM
   9 =4?L          CALL  7  PACKIT   
  10 =4R= ...      MHUNTW   3,FILE,ENT   
  14 =5=W ...      LDN   4  ELAN(3)             [LANGUAGE
   7 =5QG          LDN   5  1
  10 =6=6          LDN   6  JPDLANGUAGE  
   9 =6PQ          CALL  7  PACKIT   
  16 =79B ...      LDN   4  AWORK4(2)           [RESIDENCE NUMBER
   7 =88L          LDN   5  1
   9 =8N=          LDN   6  JPDNUMA  
   9 =97W          CALL  7  PACKIT   
  10 =9MG ...      MHUNTW   3,BSTB,FULLB 
   8 ==76 ...      LDX   3  A1(3)
  15 ==LQ ...      SBN   3  2                   [SIZE OF FILE
   7 =?6B          LDN   4  3
   7 =?L2          LDN   5  1
  10 =#5L          LDN   6  JPDBLOCKCT   
   9 =#K=          CALL  7  PACKIT   
  16 =*JG          LDN   4  GMTNOW              [GEORGE MEAN TIME
   7 =B46          LDN   5  1
   9 =BHQ          LDN   6  JPDNUMB  
   9 =C3B          CALL  7  PACKIT   
  19 =D2L          OUTPAR   DATENOW,NULL        [PRESENT DATE THEN NULL PARAM
   8 =DG=          SMO      FX2  
  16 =D_W          LDX   4  AWORK3              [PICK UP EINF2   
   7 =FFG          SLL   4  9
  15 =F_6          BNG   4  XARC                [BIT 9 SET   
   7 =GDQ          SLL   4  2
  17 =GYB          BPZ   4  XD9A                [J IF NOT ARCHIVED   
  15 =HD2          SMO      FX1                 [BIT 11 SET  
  17 =HXL          LDN   4  YARCHM              [ MANAGER ARCHIVED   
   7 =JC=          LDN   5  2
   4 =JWW    XP1 
  10 =KBG          LDN   6  JPDVARCHAR   
   9 =KW6          CALL  7  PACKIT   
   8 =L*Q          BRN      XD9  
   4 =LTB    XARC
   8 =M*2          SMO      FX1  
   8 =MSL          LDN   4  YARCH
   7 =N#=          LDN   5  1
   8 =NRW          BRN      XP1  
   8 =P?G    XD9A  OUTPAR NULL   
   4 =PR6    XD9 
   9 =Q=Q          OUTPAR   TIMENOW  
   8 =QQB          SMO      FX2  
  17 =R=2          LDN   4  ADTSN               [TAPE SERIAL NUMBER  
   7 =RPL          LDN   5  1
   9 =S9=          LDN   6  JPDTSN   
   9 =SNW          CALL  7  PACKIT   
   8 =T8G          SMO      FX2  
   9 =TN6          LDN   4  AWORK1   
   7 =W7Q          LDN   5  1
   9 =WMB          LDN   6  JPDINCNUM
   9 =X72          CALL  7  PACKIT   
   8 =XLL          MONOUT FILERET
  10 =XS6 ...      MHUNTW   3,FILE,ENT   
  10 =X_L ...      LDX   4  EAUTOCOUNT(3)
   8 =Y76 ...      BZE   4  SKLF 
  10 =Y9K ...      MHUNTW 1,FILE,FABSNB  
  19 =Y#4 ...      LDX   6  A1(1)               [X6 => FABSNB RECORD HEADER  
   8 =YBH ...      SBN   6  10   
  17 =YF2 ...      BZE   6  XLFCLOSE            [J IF 10 WORD FABSNB 
  18 =YHF ...      SETNCORE 10,3,FILE,FABSNB    [SET UP 10 WORD FABSNB   
  19 =YKY ...      HUNT2 1,FILE,FABSNB,3        [FIND ORIGONAL FABSNB AGAIN  
   8 =YNC ...      LDN   7  10   
  17 =YQW ...      STO   7  A1(3)               [FABSNB RECORD HEADER
   7 =YT* ...      SMO   6   
  16 =YXS ...      LDN   7  A1-1(1)             [PICK UP USERNAME
   9 =_2? ...      LDN   0  A1+1(3)  
  20 =_4Q ...      MOVE  7  3                   [MOVE USERNAME INTO NEW FABSNB   
  17 =_79 ...      ADN   7  5                   [PICK UP LOCAL NAME  
   7 =_9N ...      ADN   0  3
  17 =_#7 ...      MOVE  7  6                   [MOVE IN LOCAL NAME  
  20 =_BL ...                                   [DTSCAN FREES FABSNB IF IT EXISTS
   5 =_L3 ...XLFCLOSE
  19 =_NL ...      LFCLOSE  4                   [WAKE UP LISTFILE SCHEDULER  
   4 =_W6 ...SKLF
  16 ?23L ...      CLOSETOP                     [CLOSE DIRECTORY 
   6 ?296 ...      ANSOK2
   9 ?2BL ...RIDB  VFREEW   FILE,ENT 
   9 ?2FG ...      VFREEW BSTB,FULLB 
   8 ?2JB    #SKI K6DTREST>99-99 
   4 ?342    (   
  11 ?3HL          TRACE    EXEC3(2),DTRESREP
  12 ?43=          TRACE    AWORK2(2),DTAUXREP   
   4 ?4GW    )   
   4 ?52G    [   
  13 ?5G6    [     WAKE UP ACTS WAITING FOR THIS FILE
   4 ?5_Q    [   
  19 ?6_2          CALL  7  SFCB                [FIND FREQ IN OFF-LINE CHAIN 
  15 ?7DL          BRN      RESUP               [J IF NO FREQ
  20 ?7Y= ...      BC       2,BFILBRES          [CLEAR 'FILE BEING RESTORED' BIT 
  18 ?8CW ...      TESTREP  OK,RESTORED,ONLINE,RESTORED,OLDDUMP,RESTORED 
  17 ?9C6          TESTREP  NAME,NOTRES,NOUSER,NOTRES,NOFILE,NOTRES  
  11 ?9WQ          TESTREPNOT VRYWRONG,RESUP 
   4 ?=6J ...[   
   9 ?=BB    [     FILE NOT RESTORED 
   4 ?=L8 ...[   
   5 ?=W2    NOTRES  
  20 ??*L ...      BS       2,BFILNEXI          [SET 'FILE NO LONGER EXISTS' BIT 
   9 ?BR2          BRN      WAKEUP   
   5 ?C=L    RESTORED
  18 ?CQ= ...      BS       2,BFILERES          [SET 'FILE RESTORED' BIT 
   5 ?D9W    WAKEUP  
  18 ?DFN ...      BS       2,BTERMST           [SET 'TERMINAL STATE' BIT
  15 ?DPG          STARTACT RET                 [WAKE UP DTS 
   8 ?F96    RESUP LDX   2  FX2  
   9 ?FNQ          LDX   0  AWORK2(2)
  19 ?G8B          STO   0  ACOMMUNE1(2)        [PASS AUX IREP UP IN ACOM1   
   5 ?GN2          UP
   4 ?H7L    [   
   4 ?HM= ...[   
   4 ?J6W ...[   
  18 ?JLG ...PLOSS                              [BAD REPLY FROM OPENDIR  
   7 ?JW# ...      PICKREP 7 
  17 ?K66 ...      FREEBAX                      [FREE BACKING STORE  
   7 ?K*Y ...      PUTREP2 7 
  10 ?KKQ ...      MFREEW   BSTB,EMPTYB  
   8 ?L5B ...      BRN      RIDB 
  16 ?LK2 ...NONAV                              [NO BS AVAILABLE 
   9 ?M4L ...      CALL  6  TAPEPOS  
   8 ?MJ= ...      LDX   2  FX2  
   8 ?N3W ...      SETREP2  NOBAC
  16 ?NHG ...      STOZ     AWORK2(2)           [SET AUX REPLY OK
   8 ?P36 ...      BRN      RIDB 
  14 ?PGQ ...OLDMP                              [OLD DUMP
   9 ?Q2B ...      SETREP   OLDDUMP  
   9 ?QG2 ...      BRN      PCLOSE   
  16 ?Q_L ...ONL                                [ONLINE ALREADY  
   9 ?RF= ...      SETREP   ONLINE   
   5 ?RYW ...PCLOSE  
  16 ?SDG ...      CLOSETOP                     [CLOSE DIRECTORY 
   8 ?SY6 ...      BRN      PLOSS
  17 ?TCQ ...MTFAIL                             [FAIL OR FORMAT ERROR
   8 ?TXB ...      SETREP   FAIL 
   8 ?WC2 ...      BRN      PLOSS
   5 ?WWL ...XBREAK  
  10 ?XB= ...      GEOERR   1,SYSBRKIN   
   5 ?XTW ...XFULL   
  10 ?Y*G ...      GEOERR   1,FILEFULL   
   4 #96Q    #END
   2 #9LB
   8 ____ ...61212501000400000000
