  12 22FL    #LIS  K0UNLOADMT>K0LIBRARY>K0ALLGEO 
  15 22_=    #SEG           UNLOADMT65          [N.R.BOULT   
  10 23DW                   8HUNLOADMT   
   4 23JR ...[   
  17 23NN ...[  (C)   COPYRIGHT  INTERNATIONAL COMPUTERS LTD  1982   
   4 23SK ...[   
  13 23YG          SEGENTRY K1UNLOADMT,QK1UNLOADMT   
  13 24D6          SEGENTRY K2UNLOADMT,QK2UNLOADMT   
   4 24XQ    #   
   4 25CB    #   
  17 25X2    #     THIS SEGMENT HANDLES THE FREEING (FOR USE BY OTHER
  16 26BL    #     ACTIVITIES) AND UNLOADING OF MAGNETIC TAPES   
   4 26W=    #   
   4 27*W    #   
   7 27TG    #     CONSTANTS 
   4 28*6    #   
   9 28SQ    TAUT           +AMTAUT  
   9 29#B    TUNL           +AMTUNL  
   9 29S2    TRET           +AMTRET  
   9 2=?L    TREP           +AMTREP  
   9 2=R=    TNSD           +AMTNSD  
   9 2?=W    TWQ            #3400000 
  10 2?QG    TLINK PAIR     UNLOADMT,2   
  12 2#=6    TNONS          16HNONSTANDARD TAPE  
   8 2#PQ    TMB            4HMT 
   4 2*9B    #   
   6 2*P2    #     LOCALS
   4 2B8L    #   
   4 2BN=    #   
   8 2C7W    #     SUBROUTINES   
   4 2CMG    #   
   9 2D76 ...WAKE  LDX   1  AWORK1(2)
  17 2DLQ          BZE   1  WA1                 [WAKING NOT REQUIRED 
  15 2F6B          STO   0  AWORK1(2)           [SAVE LINK   
  10 2FL2          LONGON   #61,AWORK3(2)
   8 2G5L    WA3   LONGON   #61  
  16 2GK= ...      LDX   0  AWORK1(2)           [RETRIEVE LINK   
   7 2H4W ...WA1   EXIT  0  0
   4 2HJG    #   
  15 2J46    #                                  ENTRY POINT 1
   6 2JHQ    QK1UNLOADMT 
  10 2K3B          LDX   0  ACOMMUNE2(2) 
   9 2KH2    #SKI  K6UNLOADMT>99-99  
  10 2L2L          SWITRACE 0,ULMTMODE   
   9 2LG=    #SKI  K6UNLOADMT>199-199
   4 2L_W    (   
  12 2MFG          SWITRACE ACOMMUNE1(2),ULMTDL  
  12 2M_6          SWITRACE ACOMMUNE3(2),ULMTSEC 
  12 2NDQ          SWITRACE ACOMMUNE4(2),ULMTSS5 
   4 2NYB    )   
   9 2PD2          ANDX  0  TAUT(1)  
   8 2PXL          BZE   0  PH   
  14 2QC=    #     J        SET UP AUTONOMOUS ACTIVITY   
   9 2QWW          GETACT   ACTDOC,,1
   9 2RBG          LDX   3  BPTR(2)  
  16 2RW6          LINKSET  3,TLINK(1)          [LINK WORDS ...  
  16 2S*Q          LDN   0  4                   [ACCUMULATORS ...
   9 2STB          LDN   1  ACC4(3)  
   7 2T*2          MOVE  0  4
  16 2TSL          LDN   0  ACOMMUNE1(2)        [ACOMMUNES ...   
  10 2W#=          LDN   1  ACOMMUNE1(3) 
   7 2WRW          MOVE  0  6
  10 2X?G          LDX   0  ACOMMUNE1(2) 
   8 2XR6          BZE   0  PD   
   9 2Y=Q          STO   0  CPPTR(3) 
   8 2YQB          BRN      PD1  
   4 2_=2    PD  
   7 2_PL          CHANGEPE 3
   4 329=    PD1 
  13 32NW    #     K        START AUTONOMOUS ACTIVITY
   7 338G          LDX   2  3
   6 33N6          FPUT  
  14 347Q    #     O        RETURN TO CALLING ACTIVITY   
   5 34MB          UP
   4 3572    PH  
  10 35LL          LDX   0  ACOMMUNE1(2) 
   8 366=          BZE   0  PH2  
   9 36KW          STO   0  CPPTR(2) 
   4 375G    PH2 
  15 37K6    #                                  ENTRY POINT 2
   6 384Q    QK2UNLOADMT 
  10 38JB          LDX   0  ACOMMUNE2(2) 
   9 3942          STO   0  AWORK2(2)
   8 39HL          LDX   1  FX1  
   9 3=3=          ANDX  0  TNSD(1)  
  15 3=GW          BNZ   0  PAO                 [THROW WFF   
  12 3?2G    #     I        OPEN :SYSTEM.DOCUMENT
   4 3?G6    PI  
  13 3?_Q          OPENSYS  XBRKIN,DOCUMENT,GENERAL  
  13 3#FB    #     L        FIND RECORD IN DOCUMENT  
  12 3#_2          SDSEARCH NOSD,CPPTR,CPPTR(2)  
  11 3*DL    #     M        UNLOAD REQUIRED ?
   9 3*Y=          LDX   0  AWORK2(2)
   9 3BCW          ANDX  0  TUNL(1)  
   8 3BXG          BNZ   0  PVF  
  12 3CC6    #     N        TEST STATUS OF TAPE  
  10 3CWQ          LDX   7  ACOMMUNE3(2) 
  16 3DBB          BNZ   7  PN6                 [INSECURE TAPE   
  10 3DW2          LDX   0  ACOMMUNE4(2) 
   9 3F*L          ANDX  0  TWQ(1)   
  16 3FT=          BNZ   0  PV                  [WRONG OR QUERY  
   8 3G#W    PN6   LFAILS   ,0   
  18 3GSG          BNZ   0  PVF                 [BRANCH IF FAILED IN USE 
  16 3H#6    #     P        TEST COUNT OF USERS (SECURE TAPES)   
  16 3HRQ          BNZ   7  PR                  [INSECURE TAPE   
  10 3J?B          LDX   0  ACOMMUNE4(2) 
   8 3JR2          ANDN  0  #7777
  16 3K=L          BZE   0  PR2                 [NOBODY WAITING  
  10 3KQ=    #     Q        SET RETAIN   
   9 3L9W          LDX   0  TRET(1)  
  15 3LPG          ORS   0  AWORK2(2)           [SET RETAIN  
  13 3M96    #     R        CLOSE :SYSTEM.DOCUMENT   
   8 3MNQ    PR    LDX   7  21(3)
   9 3N8B    #SKI  K6UNLOADMT>299-299
  10 3NN2          SWITRACE 7,ULMTSD21   
  17 3P7L          ANDN  7  #400                [NON-STANDARD MARKER 
   7 3PM=    PR2   CLOSETOP  
  11 3Q6W    #     S        NONSTANDARD TAPE 
  10 3QLG    #     U        READ HEADER  
  15 3R66          LDN   0  7                   [REWIND MODE 
  10 3RKQ          DCA      ,DLA,0,MODE  
   8 3S5B          LOCKC    2,/  
   6 3S=J ...#UNS   FDRMT
   6 3SCQ ...      CLDRM 
  15 3SK2          GPERI    ,2                  [REWIND TAPE 
   6 3T4L          COOR1 
   7 3TJ=          UNLOCK   2
   9 3W3W          LDCH  0  CPRW1(2) 
  18 3WHG ...      BNZ   0  PIA                 [BRANCH IF REWIND FAILED 
  16 3X36          BNZ   7  PAB                 [NONSTANDARD TAPE
  15 3XGQ          LDN   0  0                   [READ MODE   
  10 3Y2B          DCA      ,DLA,0,MODE  
  14 3YG2          LDN   0  1                   [COUNT   
  10 3Y_L          DCA      ,STO,0,COUNT 
   8 3_F=          LOCKC    2,/  
  10 3_YW          LDN   0  ACOMMUNE1(2) 
  14 42DG          DCA      ,STO,0,CTSA         [ADDRESS 
  19 42Y6          GPERI    ,2                  [POSITION TAPE AFTER HEADER  
   6 43CQ          COOR1 
  16 43XB          UNLOCK   2                   [UNLOCK ACTIVITY 
  16 44C2          LDCH  0  CPRW1(2)            [REPLY CHARACTER 
  18 44WL          ANDN  0  #57                 [MASK OUT LONG BLOCK BIT 
   8 453S ...      BZE   0  PAB  
  13 4592 ...[ READ OR REWIND FAILED - UNLOAD TAPE   
   9 45B8 ...PIA   LDX   0  TUNL(1)  
   9 45HB ...      ORS   0  AWORK2(2)
   8 45NJ ...      BRN      PI   
  11 45TW    #     AB       DEALLOCATE DECK  
   7 46*G    PAB   CLALLOC   
   9 46T6          LDX   3  CPPTR(2) 
   9 47#Q    #SKI  K6UNLOADMT>299-299
  10 47SB          SWITRACE 3,ULMTCLAL   
   9 48#2          STOZ     CPPTR(2) 
   9 48RL          STOZ     CPRW1(2) 
   9 49?=          STOZ     CPRW2(2) 
   9 49QW          STOZ     CPRSW(2) 
  15 4==G    #     AC       WAKE UP APPROPRIATE ACTIVITIES   
  17 4=Q6 ...      DFJUMPN  3,WORK,PAC3         [JUMP IF NOT WORKTAPE
  18 4?9Q          LONGON   #63                 [WAKE UP FOR WORK TAPE   
   8 4?PB          BRN      PAD  
  17 4#92    PAC3  DFJUMPN  3,POOL,PAC5         [JUMP IF NOT POOLTAPE
  18 4#NL          LONGON   #56                 [WAKE UP FOR POOL TAPE   
   8 4*8=          BRN      PAD  
  17 4*MW    PAC5  DFJUMPS  3,NONS,PAC7         [JUMP IF NON-STANDARD
   9 4B7G          DFLOAD   3,TSN,6  
  17 4B=9 ...      FDTENT    2,6,XNOFDT         [IS THIS TSN IN FDT??
  19 4B?6 ...      JBCC     XDTA,2,BIFDTO       [J IF ENTRY ALREADY LOOKED AT
  17 4B#3 ...                                   [OTHERWISE CLEAR BIT.
  19 4B#Y ...      BS       2,BIFDTJL           [SET 'TAPE JUST LOADED' BIT  
  19 4B*T ...      BS       BOLC,BIFDTLMEF      [SET 'LOOK AT FDT FIRST' BIT 
  17 4BBQ ...      STARTACT RET                 [KICK DTS ACTIVITY   
  21 4BCM ...      BRN      PAD                 [RETRIEVAL SYSTEM WILL DEAL WITH THIS
  20 4BDJ ...XDTA                               [FDT ENTRY BUT BIFDTO IS CLEAR   
  20 4BFF ...      JBS      PAD,2,BIFDTJL       [J IF NO DTA SET UP YET BY DTSA  
  20 4BGB ...                                   [DTA HAS BEEN SET UP - KICK IT   
  21 4BH? ...                                   [......MOST PROBABLY IT IS DUMPER'S D
  19 4BJ8 ...XNOFDT                             [NO FDT ENTRY - SO CONTINUE  
   8 4BM6          LONGON   #61,6
   8 4C6Q    PAC7  LONGON   #61  
  10 4CLB    #     AD       RETAIN SET ? 
   9 4D62    PAD   LDX   0  AWORK2(2)
   9 4DKL          ANDX  0  TRET(1)  
  15 4F5=          BNZ   0  PAL                 [RETAIN SET  
  16 4FJW    #     AE       CO-ORDINATE FOR MISCELLANEOUS USERS  
  15 4G4G          DFLOAD   3,TSN,6             [REMEMBER TSN
   8 4GJ6          COOR3    #62  
  19 4H3Q          DFJUMPN  3,LOAD,PAL          [BRANCH IF NO LONGER LOADED  
  18 4HHB          JALLOC   3,PAL               [BRANCH IF NOW ALLOCATED 
   9 4J32          DFLOAD   3,TSN,0  
  20 4JGL          BXU   0  6,PAL               [BRANCH IF TAPE HAS BEEN CHANGED 
  12 4K2=    #     AF       DECK NOW ALLOCATED   
   7 4KFW          SETALLOC 3
   9 4K_G          STO   3  CPPTR(2) 
   8 4LF6          LDX   1  FX1  
   9 4LYQ          LDX   0  TUNL(1)  
  15 4MDB          ORS   0  AWORK2(2)           [SET UNLOAD  
   9 4MY2    #SKI  K6UNLOADMT>299-299
  11 4NCL          SWITRACE AWORK2(2),ULMTAGN
   8 4NX=          BRN      PI   
  12 4PBW    #     VF       SET UP FOR WAKE S/R  
   8 4PWG    PVF   LDX   2  FX2  
   9 4QB6          LDX   0  CPPTR(2) 
  19 4QTQ          STO   0  AWORK1(2)           [INDICATES LONGON REQUIRED   
   8 4R*B          BRN      PV1  
  12 4RT2    #     V        COPY DOCUMENT ENTRY  
  17 4S#L    PV    STOZ     AWORK1(2)           [ZERO FOR NO WAKING  
   7 4SS=    PV1   READAGAIN 
  10 4T?W    #     W        DELETE ENTRY 
   6 4TRG          DELETE
   9 4W?6          STOZ     AWORK3(2)
   9 4WQQ          DFJUMPS  ,NONS,NWK
   9 4X=B          DFJUMPS  ,POOL,WKZ
   9 4XQ2          DFJUMPS  ,WORK,WKZ
   9 4Y9L          DFLOAD   ,TSN,0   
   9 4YP=          STO   0  AWORK3(2)
   8 4_8W          BRN      NWK  
   9 4_NG    WKZ   STOZ     AWORK1(2)
   4 5286    NWK 
  10 52MQ    #     X        RESET PLIST  
   9 52XJ ...      DFLOAD   ,TSN,7   
   8 537B          DFCLEAR  ,TSN 
   9 53M2          DFCLEAR  ,TBITS   
  13 546L    #     Y        CLOSE :SYSTEM.DOCUMENT   
  14 54L=          CLOSE                        [DOCUMENT
  14 555W    #     Z        REWIND AND DISCONNECT DECK   
  18 55KG          LDN   0  9                   [REWIND AND DISCONNECT   
  10 5656          DCA      ,DLA,0,MODE  
   8 56JQ          LOCKC    2,/  
   8 574B          GPERI    ,2   
   6 57J2          COOR1 
  14 583L          UNLOCK   2                   [ACTIVITY
   4 58H=    PAK 
  16 58JS ...[ MEDIA UNLOAD ERROR LOG- SET UP ADATA/ELOUT BLOCK  
   9 58LB ...      JENVNOT  PAKA,CME 
   8 58MY ...      LDN   4  22   
   9 58PG ...      LDX   3  CPPTR(2) 
   8 58R4 ...      SETELOG  2,4,3
   9 58RT ...      STO   7  ELDME(2) 
   8 58SL ...      LDN   5  #5060
  10 58W8 ...      ORS   5  ELHEADER+1(2)
   8 58XQ ...      LDX   2  FX2  
   4 58_# ...PAKA
   8 592W          LGEOG    ,5   
   4 59GG    PAK1
  11 5=26          OUTPACK  5,1,GEOPER,PAK1  
  10 5=FQ          MHUNT    3,FILE,FRB   
   9 5=_B          LDX   5  A1+2(3)  
  19 5?F2          BZE   5  PAK3                [BRANCH IF NONSTANDARD TAPE  
   4 5?YL    PAK2
  10 5#D=          OUTPACKB 5,1,TSN,PAK2 
   8 5#XW          BRN      PAK4 
   4 5*CG    PAK3
   8 5*X6          OUTPARB  SKIP 
   4 5BBQ    PAK4
  10 5BWB          MHUNT    3,FILE,FRB   
  13 5CB2          OUTPACKB A1+3(3),3,LOCNAME,PAK4   
   4 5CTL    PAK5
  10 5D*=          MHUNT    3,FILE,FRB   
  13 5DSW          OUTPACKB A1+6(3),1,REELNUM,PAK5   
   4 5F#G    PAK6
  10 5FS6          MHUNT    3,FILE,FRB   
  12 5G?Q          OUTPACKB A1+7(3),1,GENNUM,PAK6
   8 5GRB          LFAILS   ,4   
   8 5H?2          LREPEATS ,6   
   8 5HQL          BZE   4  PAK8 
  14 5HS* ...[ MEDIA UNLOAD ERROR LOGS-PUT IN FAIL COUNT 
  10 5HW4 ...      HUNTW    2,ADATA,ELOUT
   8 5HXR ...      BNG   2  PAK7 
   7 5H_G ...      LDN   3  0
   8 5J39 ...      SLL   34 16   
  11 5J4Y ...      STO   3  ELHEADER+12(2)   
  11 5J6M ...      ORS   4  ELHEADER+13(2)   
   8 5J8B ...      SRL   34 16   
   4 5J==    PAK7
  11 5JPW          OUTPACKB 4,1,FAILCT,PAK7  
   8 5K9G          CLCOUNT  ,FAIL
   8 5KP6          BRN      PAK9 
   4 5L8Q    PAK8
   8 5LNB          OUTPARB  SKIP 
   4 5M82    PAK9
   8 5MML          BZE   6  PAK11
   5 5N7=    PAK10   
  11 5NLW          OUTPACKB 6,1,REPEATS,PAK10
   9 5P6G          CLCOUNT  ,REPEAT  
   8 5PL6          BRN      PAK12
   5 5Q5Q    PAK11   
   8 5QKB          OUTPARB  SKIP 
   5 5R52    PAK12   
   8 5RJL          BZE   5  PAK13
   8 5S4=          OUTPARB  SKIP 
   8 5SHW          BRN      PAK14
   5 5T3G    PAK13   
   8 5TH6          OUTPARB  NULL 
   5 5W2Q    PAK14   
   6 5W4F    #SKI  JSKI06
   4 5W68    (   
  16 5W7X          OUTPAR   TIMENOW             [TIME TO MESSAGE 
  17 5W9L          LGPERIS  ,7                  [GEORGE PERI COUNT   
  17 5W?*          LOBJPERIS ,0                 [OBJECT PROGRAM PERIS
  15 5W*4          ADX   7  0                   [TOT THEM UP 
  10 5WBR          OUTPACK  7,1,TRANSCT  
   4 5WDG    )   
  15 5WDH ...[ MEDIA UNLOAD ERROR LOGS-PUT IN TRANSFER COUNT 
   7 5WDJ ...      LDX   5  6
  10 5WDK ...      HUNTW    2,ADATA,ELOUT
   9 5WDL ...      BNG   2  PAK14A   
   7 5WDM ...      LDN   6  0
   7 5WDN ...      SLL   67 8
  10 5WDP ...      ORS   6  ELHEADER+9(2)
  11 5WDQ ...      STO   7  ELHEADER+10(2)   
   5 5WDR ...PAK14A  
   8 5WDS ...      LDX   2  FX2  
   7 5WDT ...      LDN   7  0
   9 5WDW ...      JCERRCT  ,PAK16   
  18 5WDX ...      LCERRS   ,7                  [CORRECTED ERROR COUNT   
   8 5WDY ...      BZE   7  PAK16
   5 5WD_ ...PAK15   
  11 5WF2 ...      OUTPACK  7,1,NUMA,PAK15   
   8 5WF3 ...      CLCOUNT  ,CERR
   8 5WF4 ...      BRN      PAK17
   5 5WF5 ...PAK16   
   8 5WF6 ...      OUTPAR   SKIP 
   5 5WF8 ...PAK17   
  18 5WF= ...[ MEDIA UNLOAD ERROR BLOCKS-CHAIN ADATA/ELOUT BLOCK AT END  
   8 5WF# ...[ OF ELOG ACTIVITY  
  10 5WFB ...      HUNTW    2,ADATA,ELOUT
   9 5WFD ...      BNG   2  PAK17A   
   7 5WFG ...      ADX   5  7
  11 5WFJ ...      STO   5  ELHEADER+11(2)   
   9 5WFL ...      LDX   1  BELOGACT 
  10 5WFN ...      LDX   3  ACTRING(1)   
   9 5WFQ ...      SBN   3  ACTRING  
   9 5WFS ...      LDX   3  BPTR(3)  
   8 5WFW ...      CHAIN    2,3  
   8 5WFY ...      STARTACT ELOG 
   5 5WG2 ...PAK17A  
   9 5WG4 ...      MONOUT   AUNIT1   
   6 5WGB          FREEMT
   8 5X22          CALL  0  WAKE 
  10 5X_=    #     AL       AUTONOMOUS ? 
   9 5YDW    PAL1  MFREE    FILE,FRB 
   8 5YYG    PAL   LDX   2  FX2  
   8 5_D6          LDX   1  FX1  
   9 5_XQ          LDX   0  AWORK2(2)
   9 62CB          ANDX  0  TAUT(1)  
  16 62X2          BZE   0  PAM                 [NON-AUTONOMOUS  
  12 63BL    #     AN       SUICIDE TERMINATION  
  14 63W=          SUICIDE                      [FINISH  
   5 64*W    PAM   UP
  13 64TG    #     AJ       REPLY BLOCK TO BE SET UP 
   4 65*6    PAJ 
  10 65SQ          MHUNT    3,FILE,FRB   
   8 66#B          LFAILS   ,5   
   8 66S2          LREPEATS ,6   
   7 67?L          ORX   5  6
   9 67HD ...      DFLOAD   ,TSN,7   
  19 67R=          BNZ   5  PAK                 [BRANCH IF FAILS OR REPEATS  
   6 68=W          FREEMT
   8 68QG          CALL  0  WAKE 
   8 69=6          BRN      PAL1 
  11 69PQ    #     AO       THROW OFF TAPE   
   4 6=9B    PAO 
   9 6=P2    #SKI  K6UNLOADMT>299-299
   9 6?8L          SWITRACE 0,ULMTNSD
  18 6?N=          LDN   0  9                   [REWIND AND DISCONNECT   
  10 6#7W          DCA      ,DLA,0,MODE  
   8 6#MG          LOCKC    2,/  
   8 6*76          GPERI    ,2   
   6 6*LQ          COOR1 
   7 6B6B          UNLOCK   2
  10 6BL2    #     AP       RESET PLIST  
   8 6C5L          DFCLEAR  ,TSN 
   9 6CK=          DFCLEAR  ,TBITS   
   8 6D4W          LGEOG    ,5   
   6 6DJG          FREEMT
   8 6F46          OUTNUM   5,0  
   9 6FHQ          MTINFORM AZFREE   
   8 6G3B          BRN      PAL  
   4 6GH2    #   
   7 6H2L    #     GEOERRS   
   4 6HG=    #   
  10 6H_W    NOSD  GEOERR   1,NOSYSDOC   
  10 6JFG    XBRKIN      GEOERR 1,XBRKIN 
  11 6J_6          MENDAREA 50,K99UNLOADMT   
   4 6KDQ    #END
   8 ____ ...11310633000200000000
