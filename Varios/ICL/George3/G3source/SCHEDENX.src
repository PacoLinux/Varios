  13 22FL ...  SEG SCHEDENX,860,C.R.JOHNSON,SCHEDULER
  12 22_=          SEGENTRY K1SCHEDENX,X1SCHEDENX
  12 23DW          SEGENTRY K2SCHEDENX,X2SCHEDENX
   4 23YG    [   
  19 24D6    [     SEGMENT SETS UP A F/FREQ BLOCK - CHAINS IT INTO THE OLC   
  19 24XQ    [     UNLESS ONE ALREADY EXISTS OR THE FREQ ON EITHER SIDE HAS  
  18 25CB    [     THE SAME INCREMENT NUMBER BUT IS NOT IN TERMINAL STATE
  20 25X2    [      - DTS KICKED AFTER FREQ INSERTER AND FREVCT/FREQCT UPDATED   
   4 26BL ...[   
   4 26W= ...[   
   4 27*W ...[   
   6 28SQ    XFILEFREQ   
  10 29#B    #HAL           FILE+FREQ,0  
   4 29D? ...[   
   8 29DD ...SLSGMT      #177777 
   4 29DK ...[   
  19 29DQ ...[THIS SUBROUTINE INSERTS GMTNOW INTO THE FEGMTRV FIELD OF THE   
  19 29DX ...[DIRECTORY ENTRY TO PREVENT RECENTLY RETIEVED FILES BEING THROWN
   9 29F4 ...[OFFLINE BY THE UNJAMMER
   4 29F9 ...[   
   5 29FB ...SGMTNOW 
   8 29FH ...      SBX   6  FX1  
   9 29FN ...      LDX   7  GMTNOW   
   8 29FT ...      SMO      FX1  
   9 29G2 ...      ANDX  7  SLSGMT   
   8 29G7 ...      BNZ   7  SFNZ 
   7 29G# ...      LDN   7  1
   4 29GF ...SFNZ
  10 29GL ...      STF      3,FEGMTRV,7  
  10 29GR ...      NAME      3,FILE,FWB  
   7 29GY ...      REWRITE   
  10 29H5 ...      MHUNT    3,FILE,FWB   
  10 29H= ...      NAME     3,FILE,ENT   
   8 29HC ...      ADX   6  FX1  
   7 29HJ ...      EXIT  6  0
  21 29J8 ...[   ******************************************************************  
   4 29N5 ...[   
  19 29S2    [     THIS SUBROUTINE IS DESIGNED TO INSERT OR  DELETE A JOB NO.
  14 2=?L    [           AS REQUESTED FROM THE FREQ BLOCK
   4 2=R=    [   
  20 2?=W    [     IN DOING SO IT TAKES NOTE IF THE JOB NO. IS ALREADY PRESENT   
   4 2?QG    [   
  18 2#=6    [     ON ENTRY X2 -> REQUEST BLK;  X3 -> REQUESTING ACTIVITY
  18 2#PQ    [                   AWORK2(3) WILL PRESERVE LINK ACCUMULATOR
   4 2*9B    [   
  19 2*P2    [     A DELETION OR AN INSERTION WILL NECESSITATE SIZE OF FREQ  
  12 2B8L    [              BLOCK TO BE ALTERED  
   4 2BN=    [   
  16 2C7W    [     ALL ACCUMULATORS WILL BE CORRUPTED  ON EXIT   
   4 2C*4 ...[   
  17 2CG= ...[     THIS SUBROUTINE MUST NOT CORRUPT ACUMMUNE1,2,OR 3 
   4 2CMG    [   
   4 2D76    [   
   5 2DLQ    RINSERT 
  17 2F6B          LDCT  5  #400                [FLAG AN INSERTION   
   8 2FL2          BRN      XSP1 
   5 2G5L    REMOVX  
  16 2GK=          STOZ     5                   [FLAG A DELETION 
   4 2H4W    XSP1
   8 2HJG          SBX   1  FX1  
  17 2J46          STO   1  AWORK2(3)           [PRESERVE LINK ACC.  
  17 2JHQ          LDX   6  FREVCT(2)           [X6 = NO. OF REQUESTS
   7 2K3B          LDN   7  0
  18 2KH2          BRUSEN   NOJOB,3             [J IF IN NO-USER CONTEXT 
  18 2KPM ...      LDX   0  AWORK1(3)           [B0 IS SET IF AUTONOMOUS 
  19 2KRW ...      LDX   7  AWORK3(3)           [X7 => JOBNO IF AUTONOMOUS   
  16 2KW5 ...      BNG   0  NOJOB               [J IF AUTONOMOUS 
  21 2L2L          LDX   7  JOBNO(3)            [X7= JOB NO. OF REQUESTING ACTIVITY  
   5 2LG=    NOJOB   
  19 2L_W          STO   2  4                   [PRESERVE POINTER TO FREQ.   
  16 2MFG          BZE   6  NOENT               [J IF NO JOB NOS.
   4 2M_6    RET1
  19 2NDQ          BXE   7  FREVCT+1(2),REQ1    [J IF JOB NO. ALREADY THERE  
   7 2NYB          ADN   2  1
   8 2PD2          BCT   6  RET1 
  19 2PXL    NOENT                              [JOB NO. NOT ALREADY PRESENT 
  17 2QC=          BPZ   5  REXIT               [J IF NOT INSERTING  
  16 2QWW          STO   7  FREVCT+1(2)         [INSERT JOB NO.  
  17 2RBG          LDX   2  4                   [ X2 -> FREQ. BLOCK  
   7 2RW6          LDN   7  1
  17 2S*Q    RALT  ADS   7  FREVCT(2)           [UPDATE FREQ BLOCK   
  10 2STB          ADX   7  ALOGLEN(2)   
  17 2T*2          ALTLEN   4,7                 [ALTER LENGTH OF FREQ
   5 2TSL    REXIT   
   8 2W#=          LDX   2  FX2  
   9 2WRW          LDX   1  AWORK2(2)
   8 2X?G          ADX   1  FX1  
  19 2XR6          EXIT  1  0                   [ EMERGENCE FROM SUBROUTINE  
   4 2Y=Q    REQ1
  16 2YQB          BNG   5  REXIT               [J FOR INSERTION 
   7 2_=2          SBN   6  1
  20 2_PL          BZE   6  NOMOVE              [J IF POSITIONED ON LAST JOB NO. 
  10 329=          LDN   1  FREVCT+2(2)  
  19 32NW          ADN   2  FREVCT+1            [ MOVE ENTRIES AFTER DELETION
   7 338G          SMO      6
   7 33N6          MOVE  1  0
   5 347Q    NOMOVE  
  18 34MB          LDX   2  4                   [ REPLACE REQUEST POINTER
  19 3572          NGN   7  1                   [DECREASE FREVCT AND LENGTH  
  16 35LL                                       [OF FREQ BLOCK   
   8 366=          BRN      RALT 
   4 36=7 ...[   
  21 36B4 ...[   ******************************************************************  
   4 36F_ ...[   
   5 36KW    XVERF   
  17 375G ...      LDX   2  3                   [ RESET FREQ POINTER 
  18 37K6 ...      BS       1,BINCUNVERR        [SET 'UNVERIFIED INC' BIT
  18 384Q          CHAIN 1,2                    [ CHAIN  IN REQUEST BLOCK
  20 38JB          BRN      XCHALT              [ J TO SET CHAIN ALTERED BITS.   
   4 38N? ...[   
  21 38S8 ...[   ******************************************************************  
   4 38Y5 ...[   
  20 3942    NOTERM2                            [ REQUEST BLK WITH SAME INCREMENT
  20 39HL                                       [       NO. NOT IN TERMINAL STATE
  10 3=3=          LDX   0  ADTCOUNT(2)  
  17 3=GW          STO   0  ADTCOUNT(1)         [ COPY DTCOUNT VALUE 
  17 3?2G ...      LDX   2  3                   [RESET FREQ POINTER  
  17 3?G6          CHAIN 1,2                    [ CHAIN REQUEST BLK  
   9 3?_Q          BRN      XCHALT   
   5 3#FB    WAITERR 
  19 3#_2          GEOERR   1,WAITSET           [WAITING STYLE ALREADY SET   
   4 3*68 ...[   
  21 3*?B ...[   ******************************************************************  
   4 3*DL    [   
  20 3*Y=    [     SR TO SET UP 10-WORD FABSNB FROM OFF-LINE CHAIN REQUEST BLOCK 
   4 3BCW    [   
   6 3BXG    SETFABTEN   
   8 3C7# ...      SBX   7  FX1  
   8 3CC6 ...      SMO      FX2  
  19 3CLY ...      STO   7  AWORK2              [PRESERVE LINK ACCUMULATOR   
  11 3CWQ          SETNCORE 10,3,FILE,FABSNB 
   9 3DBB          CALL  7  SEARCH   
   4 3DL8 ...[   
  10 3DW2    [     X2 POINTS TO REQUEST  
   4 3F5S ...[   
  10 3F*L          MHUNTW   3,FILE,FABSNB
   8 3FT=          LDN   0  10   
   8 3G#W          STO   0  A1(3)
   8 3GSG          ADN   3  A1+1 
   9 3H#6 ...      ADN   2  FFUSER1  
   7 3HRQ          MOVE  2  9
   8 3J56 ...      SMO      FX2  
  18 3JBG ...      LDX   7  AWORK2              [SET UP LINK ACCUMULATOR 
   8 3JMW ...      ADX   7  FX1  
   7 3J_= ...      EXIT  7  0
   4 3K=L    [   
  21 3KGD ...[   ******************************************************************  
   4 3KQ=    [   
  20 3L9W    [     THIS SUBROUTINE IS DESIGNED TO SET THE APPROPRIATE REPLY IN   
  18 3LPG ...[     ACCORDANCE WITH THE BIT SETTING IN WORD FDTSTATE OF   
  13 3M96 ...[     THE FREQ BLOCK POINTED TO BY X3.  
   4 3MNQ ...[   
  12 3N8B ...SETREPLY                           [
   8 3NGR ...      LDX   2  FX2  
   8 3NT8 ...      SETREP2  OK   
  18 3P7L ...      JBS      RVOK1,3,BFILERES    [J IF FILE RETRIEVED OK  
  19 3PM= ...      JBS      NOEXIST,3,BFILNEXI  [J IF FILE NO LONGER EXISTS  
  19 3Q6W ...      JBS      PERLST,3,BPERLOST   [J IF FILE PERMANENTLY LOST  
  19 3QLG ...      JBS      TEMPLST,3,BTEMLOST  [J IF FILE TEMPORARILY LOST  
  19 3R66 ...      JBS      RVOK1,3,BFILBRES    [J IF FILE IS BEING RETRIEVED
  12 3RKQ ...XER1                               [
  16 3WHG          GEOERR 1,WRGBIT              [UNUSUAL BIT SET 
  12 3X36 ...TEMPLST                            [
   9 3X*H ...      SETREP2  TEMPLOST 
   8 3XMY ...      BRN      RVOK1
  12 3Y2B ...PERLST                             [
   8 3Y#R ...      SETREP2  LOST 
   8 3YM8 ...      BRN      RVOK1
  12 3Y_L ...NOEXIST                            [
   9 3_F= ...      SETREP2  NOFILE   
  12 3_YW ...RVOK1                              [
  12 43CQ ...      EXIT  1  0                   [
   4 43XB ...[   
  21 44C2 ...[   ******************************************************************  
   4 44WL    [   
  20 45B=    [              S.R.TO DISCOVER PRESENCE OR ABSENCE OF REQUEST BLOCK 
   9 45TW    [              IN OFLC  
   4 46*G    [   
   5 46T6    VSEARCH 
  16 47#Q          LDN   3  0                   [SET 'VARIABLE'  
   8 47SB          BRN      VS   
   4 487P ...[   
  21 48H4 ...[     SUBROUTINE TO SEARCH OFF LINE CHAIN FOR A REQUEST BLOCK SPECIFIED 
  20 48WC ...[     BY AN INCREMENT NUMBER IN X4, FILE NUMBER IN X5, AND FILESTORE
   8 499Q ...[     NUMBER IN X6. 
  19 49K5 ...[     THE CHAIN IS SORTED ON INCREMENT NUMBER, THEN FILE NUMBER,
  11 49YD ...[     AND THEN FILESTORE NUMBER.
  21 4=?R ...[     IF THE REQUEST BLOCK IS NOT FOUND,X2 POINTS TO THE NEXT BLOCK IN  
   9 4=M6 ...[     THE CHAIN,EXIT 7 1
  19 4?2F ...[     IF THE REQUEST BLOCK IS FOUND,X2 POINTS TO IT, EXIT 7 0   
   4 4?*S ...[   
   5 4?PB    SEARCH  
  16 4#92          LDN   3  1                   [SET 'MANDATORY' 
   8 4#NL    VS    LDX   2  BOLC 
   8 4*8=          LDN   0  BOLC 
   5 4*MW    M1SFOLC 
   8 4B7G          LDX   2  0(2) 
  18 4BM6 ...      BXE   2  0,XIT1                  [J IF END OF CHAIN   
  19 4C6Q ...      BXL   4  FDTINCNUM(2),XIT1       [J IF INCREMENT NOT THERE
  21 4CLB          BXU   4  FDTINCNUM(2),M1SFOLC    [J IF INCREMENT NOT YET REACHED  
  19 4CW8 ...      BXL   6  FDTFILSTR(2),XIT1       [J IF FILESTORE NOT THERE
  21 4D62 ...      BXU   6  FDTFILSTR(2),M1SFOLC    [J IF FILESTORE NOT YET REACHED  
  18 4D*S ...      BXL   5  FDTFILNUM(2),XIT1       [J IF FILE NOT THERE 
  20 4DKL          BXU   5  FDTFILNUM(2),M1SFOLC    [J IF FILE NOT YET REACHED   
   4 4DTD ...[   
  15 4F5= ...[     X2 POINTS TO A REQUEST BLOCK FOR THIS FILE
   4 4F*4 ...[   
   7 4FJW          EXIT  7  0
   4 4FSN ...[   
  19 4G4G ...[     THERE IS NO REQUEST BLOCK FOR THIS FILE IN OFF LINE CHAIN 
   4 4G## ...[   
  16 4GJ6    XIT1  BNZ   3  XGONE               [J IF MANDATORY  
   7 4H3Q          EXIT  7  1
  10 4HHB    XGONE GEOERR   1,FILEGONE   
   4 4HNJ ...[   
  21 4HTQ ...[   ******************************************************************  
   4 4J32    [   
  18 4JGL    [     S.R. TO SET CHAIN ALTERED BITS IN DUMP TAPE ACTIVITIES
   4 4K2=    [   
   4 4LF6    [   
   8 4LYQ    SACT  LDX   2  BOLC 
  17 4MY2          LDX   1  A1(2)               [COUNT OF FDT ENTRIES
   8 4NCL          BZE   1  SACT2
  18 4NX= ...SET8  BS       2,BIFDTCA           [SET 'CHAIN ALTERED' BIT 
  15 4PBW          ADN   2  FDTLEN              [NEXT ENTRY  
   8 4PWG          BCT   1  SET8 
   7 4QB6    SACT2 EXIT  7  0
   4 4QH# ...[   
  21 4QNG ...[   ******************************************************************  
   4 4QTQ    [   
  21 4R*B    [     ENTRY POINT FOR AUTONOMOUS RV ACTIVITY.IT HAS DIRECTORY OPEN,AND  
  20 4RT2    [     SOMETIMES SDF AS WELL.HAS ALREADY CHECKED FOR NON-ZERO INC.NO.
   4 4S#L    [   
   6 4SS=    X2SCHEDENX  
  21 4T6L ...      STO   6  AWORK3(2)           [AWORK3 => JOBNO PASSED DOWN FROM RET
   8 4T?W          LDCT  0  #400 
  21 4TRG          STO   0  AWORK1(2)           [SET'AUTONOMOUS RETRIEVE ACTIVITY'   
  10 4W?6          MHUNTW   3,FILE,ENT   
  15 4WQQ          LDX   4  EINC(3)             [X4=INC NO   
   9 4X=B          BRN      NOTLOST  
   4 4XG8 ...[   
  21 4XQ2 ...[   ******************************************************************  
   4 4X_S ...[   
  12 4Y9L ...[     MAIN ENTRY POINT,FROM OPENREL 
  21 4YFD ...[     THE DIRECTORY ENTRY FOR FILE TO BE RESTORED IS IN A FILE/ENT BLOCK
  16 4YP=    [     B0 AWORK1 SET IF AUTONOMOUS RV ACT,ELSE CLEAR 
  19 4_8W    [              AWORK2 IS USED TO CONTAIN TSN OF DTA IF SET UP   
  15 4_NG    [     AWORK4   TEMP X7 STORAGE IN OUTMESS S.R.  
   4 5286    [   
  14 52MQ    [     DIRECTORY POSITIONED AFTER NAME RECORD
   4 537B    [   
  20 53M2    [     REQUEST BLOCKS MUST ONLY BE CHAINED IN AND DECHANED WHILE DIR.
   7 546L    [     IS OPEN   
   4 54L=    [   
   6 555W    X1SCHEDENX  
  16 55KG          STOZ     AWORK1(2)           [SET'NOT AUTO RV'
  10 5656          MHUNTW   3,FILE,ENT   
  19 56JQ          LDX   4  EINC(3)             [INCREMENT NO.FOR THIS FILE  
   9 574B          BNZ   4  NOTLOST  
   4 57#8 ...[   
  15 57J2 ...[     INCREMENT NUMBER OF FILE IS ZERO-FILE LOST
   4 57RS ...[   
  17 583L          FREECORE 3                   [FREE FILE/ENT BLOCK 
  15 58H=          CLOSETOP                     [DIERECTORY  
   4 592W    [   
  17 59GG    [     WILL NEVER BE AVAILABLE-NOT ONLINE AND NOT DUMPED 
   4 5=26    [   
   8 5=FQ          SETREP2  LOST 
   8 5=_B          BRN      UP2  
   4 5?6J ...[   
  21 5??Q ...[   ******************************************************************  
   4 5?F2    [   
  18 5?JX ...[     REQUEST BLOCK WITH SAME FILESTORE,INCREMENT, AND FILE 
  20 5?NS ...[     NUMBERS ALREADY IN OFFLINE CHAIN - THUS FREE THE FREQ BLOCK   
  20 5?SP ...[     JUST SET UP UNLESS THE FILE DETAILS (USERNAME, LOACAL NAME,   
  19 5?YL ...[     ETC.) DIFFER, IN WHICH CASE CHAIN IN THE NEW FREQ BLOCK   
  20 5#4H ...[     BEFORE THE OLD(SO AS TO GET THE 'FILE .... IS BEING RETRIEVED'
  20 5#8D ...[     MESSAGE CORRECT,AND TO ENSURE THAT BOTH FILES ARE RETRIEVED). 
   4 5##* ...[   
  13 5#D=    [              X1 -> REQUEST BLK SET UP 
  17 5#XW    [              X2 -> REQUEST BLK ALREADY IN THE CHAIN   
   4 5*CG    [   
   5 5*X6    PREV1   
  17 5B2K ...     LDN   0  9                   [CHECK 9 WORDS OF FREQ
  12 5B64 ...PREVREP                            [
  19 5B9H ...      SMO      0                   [X0 IS MODIFIER AND COUNTER  
  17 5B*2 ...      LDX   3  FFUSER1-1(1)        [X3 => NAME FROM FREQ
  12 5BDF ...      SMO      0                   [
  16 5BHY ...      BXU   3  FFUSER1-1(2),SAME1  [J IF DIFFERENT  
  18 5BMC ...      BCT   0  PREVREP             [J IF MORE WORDS TO CHECK
  18 5BQW ...      LDX   3  2                   [PRESERVE POINTER TO FREQ
  20 5BWB          FREECORE 1                   [ FREES REQUEST ALREADY SET UP   
  21 5CB2 ...      JBS      XHAPPY,3,BHAPPY     [J IF TERMINAL & DEALT WITH BY DTS   
  12 5CTL ...NTRM                               [
  19 5D*= ...      JBC      NOTERM,3,BTERMST    [J IF NOT IN A TERMINAL STATE
  12 5DSW ...UP1                                [
  15 5F#G ...      CALL  1  SETREPLY            [SET REPLY   
  15 5FS6 ...      PICKREP2 7                   [SAVE REPLY  
  19 5KP6          CLOSETOP                     [ CLOSE SUPERIOR DIRECTORY   
  15 5L8Q ...      PUTREP2  7                   [RESET REPLY 
  12 5LNB ...      LDX   0  AWORK1(2)           [
  17 5M82 ...      BPZ   0  RVOK                  [J IF NOT AUTO RV  
  20 5N7=          UP                           [ RETURN TO REQUESTING ACTIVITY  
  12 5N9L ...XHAPPY                             [
  12 5N#2 ...      LDX   0  AWORK1(2)           [
  17 5NBB ...      BNG   0  NTRM                [J IF AUTONOMOUS RV  
  12 5NDQ ...      LDN   0  1                   [
  16 5NH6 ...      ADS   0  FREQCT(3)           [ONE MORE WAITER 
  16 5NKG ...      CLOSETOP                     [CLOSE DIRECTORY 
  12 5NMW ...      BRN      REPL                [
   4 5NQ= ...[   
  21 5NSL ...[   ******************************************************************  
   4 5NX2 ...[   
  16 5N_B ...[     NUMBER OF INCREMENT CONTAINING FILE IS IN X4  
   4 5P3Q ...[   
   5 5P6G    NOTLOST 
  17 5PL6          LDX   5  EFILNUM(3)          [FILE NUMBER IN X5   
   4 5Q5Q    [   
  14 5QKB    [     SET UP A REQUEST BLOCK FOR THE FILE   
   4 5R52    [   
  20 5RJL ...      LDN   7  FREVCT-A1+2          [SET UP FREQ. BLK WITH JOB NO.  
  17 5S4= ...      SETUPCORE 7,2,FILE,FREQ      [SET UP A FILE FREQ  
  10 5SHW          STOZ     ADTCOUNT(2)  
   9 5T3G          STOZ     FREQCT(2)
   9 5TH6          STOZ     FREVCT(2)
  10 5W2Q          STOZ     FDTSTATE(2)  
  10 5WGB          MHUNTW   3,FILE,ENT   
  21 5WL? ...      JBC      XNOTFH,3,BEFHDIRMODE [J IF DIR ENTRY NOT FILEHANDLERED   
  21 5WQ8 ...      BS       2,BFHCE              [SET 'CREATED BY FILEHANDLER' BIT   
  13 5WW5 ...XNOTFH                              [   
   9 5WXB ...      CALL  6  SGMTNOW  
  10 5WYM ...      MHUNT    2,FILE,FREQ  
  10 5X22          STO   4  FDTINCNUM(2) 
  10 5XFL          STO   5  FDTFILNUM(2) 
  17 5X_=          LDN   6  ELOC1(3)            [ MOVE IN LOCAL NAME 
   9 5YDW          LDN   7  FFLOG1(2)
   7 5YYG          MOVE  6  5
   9 5_D6          LDX   6  ELAN(3)  
   9 5_XQ          STO   6  FFLANG(2)
  18 624Y ...      LF       3,FEFSNO,6          [X6 => FILESTORE NUMBER  
  18 62=6 ...      STO   6  FDTFILSTR(2)        [STORE FILESTORE NUMBER  
  16 62CB          LDX   3  2                   [ X3-> FREQ BLOCK
  19 62X2          TOPFCB   2                   [ X2 -> SUPERIOR DIRECTORY   
   9 63BL ...      LDN   7  FME1(2)  
  10 63W= ...      LDN   0  FFUSER1(3)   
  17 64*W ...      MOVE  7  3                   [ MOVE IN USER NAME  
   9 64TG          MFREEW   FILE,ENT 
   4 655# ...[   
  12 65*6    [     PRESERVE FREQ POINTER IN X1   
   4 65JY ...[   
   7 65SQ          LDX   1  3
   4 66#B    [   
  16 66S2    [     IS THERE A REQUEST FOR THIS FILE IN THE CHAIN?
   4 67?L    [   
  20 67R= ...[     THERE MUST BE NO COORDINATION BETWEEN THIS SEARCH AND CHAINING
  17 68=W ...[     IN THE BLOCK,AND ALERTING ALL DUMP TAPE ACTIVITIES
   4 68QG    [   
  16 69=6 ...[     X4,X5,X6 = INC NO,FILE NO, AND FILESTORE NO   
   4 69PQ    [   
   9 6=9B          CALL  7  VSEARCH  
  18 6=P2          BRN      PREV1               [J IF BLOCK NOW IN CHAIN 
   4 6?8L    [   
  16 6?N=    [           X2 POINTS TO THE NEXT REQUEST IN THE OLC
  15 6#7W    [           X1 POINTS TO REQUEST BLOCK SET UP   
  15 6#MG    [              NO FREQ FOR RETRIEVE, CHAIN IT IN
   4 6#X# ...[   
  10 6*76          BXE   2  CXOL,XENDOLC 
  20 6*DG ...      BXU   6  FDTFILSTR(2),XENDOLC [   REQUEST (POSITIONED AHEAD)  
  20 6*PW ...                                    [    HAS DIFFERENT FILESTORE NO.
  20 6B3= ...      BXE   4  FDTINCNUM(2),SAME1   [    REQUEST (POSITIONED AHEAD) 
  18 6B#L ...                                    [     HAS SAME  INC. NO.
   5 6BL2    XENDOLC 
  17 6C3M ...      LDX   2  BPTR(2)              [STEP BACK ONE BLOCK
  16 6CF# ...      LDX   0  ATYPE(2)             [TEST IF FREQ   
  13 6CW_ ...      SMO      FX1                  [   
  13 6D#L ...      TXU   0  XFILEFREQ            [   
  18 6DQ? ...      BCS      NOTSAME              [J IF NOT A FREQ BLOCK  
  20 6F7Y ...      BXU   6  FDTFILSTR(2),NOTSAME [   REQUEST (POSITIONED BEFORE) 
  20 6FKK ...                                    [    HAS DIFFERENT FILESTORE NO.
  12 6G3B          BXE   4  FDTINCNUM(2),SAME2   
   5 6GH2    NOTSAME 
   4 6GQS ...[   
  14 6H2L    [     MUST CHAIN THE REQUEST BLOCK INTO OLC 
   4 6H=D ...[   
  18 6HG=          CHAIN    1,2                 [ PUT FREQ BLOCK  IN OLC 
   4 6H_W    [   
  12 6JFG    [     KICK  THE DUMP TAPE SCHEDULER 
   4 6J_6    [   
   8 6KDQ          STARTACT RET  
   5 6KYB    XCHALT  
  17 6LD2          CALL  7  SACT                [ CHAIN ALTERED BITS 
   9 6LXL          BRN      NOTERM   
  20 6MC=    SAME1                              [REQUEST IN OLC HAS SAME INC. NO.
   9 6MWW ...      LDX   3  BPTR(2)  
   8 6NBG          BRN      STX1 
   5 6NW6    SAME2   
   7 6P*Q ...      STO   2  3
   4 6PTB    STX1
  17 6Q*2 ...      JBS      XVERF,2,BINCUNVERR  [J IF NEEDS VERIFYING
  19 6QSL ...      JMBAC    NOTERM2,2,BPERLOST,BTEMLOST [J IF INCREMENT OK   
   4 6WPL    [   
  20 6X9=    [     THE STATE WORD MUST INDICATE THAT THE INCREMENT IS TEMPORARILY
  10 6XNW    [        OR PERMANENTLY LOST
   4 6Y8G    [   
  20 6YN6 ...      LDX   3  2                   [X3 -> FREQ BLOCK FOR 'SETREPLY' 
  18 6_MB          FREECORE 1                   [ FREE THE REQUEST BLK   
   9 6_R? ...      CALL   1  SETREPLY
   7 6_X8 ...      PICKREP2 7
   7 7235 ...      CLOSETOP  
   7 7272 ...      PUTREP2 7 
   9 72=X ...      LDX   0  AWORK1(2)
   8 72BS ...      BPZ   0  UP2  
   5 72GP ...      UP
   5 72LL    NOTERM  
  21 736=          CALL  7  SEARCH              [MANDATORILY SEARCH OLC;X2POINT TO FR
  20 73KW          LDX   3  FX2                 [ SET  X3 -> REQUESTING ACTIVITY 
   9 745G          LDX   0  AWORK1(3)
  16 74K6          BNG   0  RVCMD               [J IF RV COMMAND 
   7 754Q ...      LDN   1  1
  19 75JB          ADS   1  FREQCT(2)           [ INCREMENT NO. OF WAITERS   
  18 75Y5 ...      STO   4  ACOMMUNE1(3)        [ PRESERVE INC. NUMBER   
  15 76?S ...      STO   5  ACOMMUNE2(3)        [ FILE NUMBER
  18 76MH ...      STO   6  ACOMMUNE3(3)        [ AND FILESTORE NUMBER.  
  19 773=          CALL  1  REMOVX              [ REMOVES JOB NO. IF PRESENT 
  19 77B_ ...      LDX   4  ACOMMUNE1(2)        [ REPLACE INCREMENT NUMBER   
  15 77QN ...      LDX   5  ACOMMUNE2(2)        [ FILE NUMBER
  18 786C ...      LDX   6  ACOMMUNE3(2)        [ AND FILESTORE NUMBER.  
   5 78G6    XWAIT   
   8 78_Q          MAGICNO 5,4,1 
  10 79FB          LONGSET #24,WAITERR,1 
   4 79_2    [   
  19 7=DL          CLOSETOP                     [ CLOSE  SUPERIOR DIRECTORY  
   4 7=Y=    [   
  14 7?CW    [     OUTPUT MESSAGE :%A IS BEING RETRIEVED 
   4 7?XG    [   
  11 7#C6          SETNCORE 20,3,ADATA,CREADL
   8 7#WQ          STOZ     A1(3)
  20 7*BB ...      CALL  7  SETFABTEN           [SET UP A FABSNB FROM FREH BLK   
   8 7*W2          UNNORM   TWO  
  11 7B*L          MHUNTW   3,ADATA,CREADL   
  13 7BT=          OUTPARAM A1(3),CPDATA,ADATA,CREADL
   9 7C#W          MONOUT   BEREST   
  10 7CSG          MFREEW   ADATA,CREADL 
  10 7D#6          MFREEW   FILE,FABSNB  
   7 7DF# ...#UNS FREEBUFF   
  20 7DLG ...      FREEBUFF JOB [ FREES FURBS AND WRITES AWAY FUWBS TO WHOLE JOB 
   4 7DRQ    STOP
  19 7F?B ...      LONGSTOP SBREAKIN            [ ACTIVITY SET WAITING FOR   
  18 7FR2 ...                                   [ FILE TO COME ONLINE.   
  17 7G=L    REPL  CALL  7  SEARCH              [ FIND REQUEST BLOCK 
  20 7GQ= ...      JBS      NOTSPURIOUS,2,BTERMST [J IF NOT SPURIOUS REACTIVATION
  18 7K8B          MAGICNO   5,4,1              [NEARLY UNIQUE SUB STYLE 
  10 7KN2          LONGSET  #24,WAITERR,1
  18 7L7L ...      BRN      STOP                [GO AND WAIT ONCE MORE   
   6 7LM=    NOTSPURIOUS 
  18 7M6W ...      JBS      RVOK,2,BFILERES     [J IF FILE RETRIEVED OK  
  19 7MLG ...      LDX   3  2                   [X3 -> FREQ FOR 'SETREPLY'   
  18 7N66 ...      CALL  1  SETREPLY            [SET APPROPRIATE REPLY   
   5 7P5B    XACTLF  
  15 7PK2          HUNT     1,FILE,FABSNB       [SET UP COPY 
  16 7Q4L ...      LDX   7  ALOGLEN(1)          [OF FILE/FABSNB  
  15 7QJ= ...      SETUPCORE  7,3,FILE,FABSNB   [AS ORIGINAL 
  14 7R3W          HUNT2    1,FILE,FABSNB,3     [WILL BE 
  14 7RHG ...      LDN   1  A1(1)               [REMOVED 
  14 7S36 ...      LDN   2  A1(3)               [FROM THE
  14 7SGQ ...      SMO      7                   [ACTIVITY
  14 7T2B ...      MOVE  1  0                   [CHAIN   
  20 7TG2          LFLOST                       [MACRO FOR DEALING WITH LIST-LOST
  17 7T_L                                       [            FILES   
   5 7WF=    XSEARCH 
  17 7WYW          CALL  7  SEARCH              [FIND REQUEST BLOCK  
  20 7X64 ...      JBC      UP2,2,BHAPPY        [J IF NOT TERMINAL & DEALT WITH  
  14 7X?= ...                                   [BY DTS  
  19 7XDG          LDX   7  FREQCT(2)           [ REDUCE NUMBER OF WAITERS   
  16 7XY6          SBN   7  1                   [       BY ONE   
   9 7YCQ          STO   7  FREQCT(2)
  19 7YXB          BZE   7  XNOFREQ             [ J TO REMOVE REQUEST BLOCK  
   4 7_C2    UP2 
   7 7_WL          UPPLUS 1  
   5 82B=    XNOFREQ 
  17 82TW          FREECORE 2                   [REMOVE REQUEST BLOCK
  18 83*G          CALL  7  SACT                [SET CHAIN ALTERED BITS  
   8 83T6          BRN      UP2  
   4 84#Q    [   
   4 84SB    [   
   4 85#2    RVOK
  18 85RL ...      CALL  7  SETFABTEN           [SET UP A 10-WORD FABSNB 
  14 86?=          OPENDIR  XBREAKD,GENERAL,QUERY,ERASING
  10 86QW          MFREEW   FILE,FABSNB  
  17 87=G          TESTRPN2 OK,XACTLF           [J IF REPLY NOT OK   
  10 87Q6          MHUNTW   1,FILE,ENT   
   9 889Q          LDEX  0  ECOPS(1) 
  20 88PB          BZE   0  XSEARCH             [ DONT BACKSPACE IF COPIES RECORD
  16 8992                                       [    NOT  UPDATED
  19 89NL ...      PICKREP2 7                   [PRESERVING REPLY HERE MAY BE
  19 8=8=          BACKSPACE                    [A CURE TO FILE PERMANENTLY  
  16 8=MW ...      PUTREP2  7                   [LOST - BUG 7620 
   9 8?7G          BRN      XSEARCH  
   4 8?M6    [   
  17 8#6Q    [     RESTORE HAS BEEN INITIATED BY THE RETRIEVE COMMAND
   4 8#LB    [   
   5 8*62    RVCMD   
  21 8*KL ...      CALL  1  RINSERT             [ INSERT JOB NUMBER IF NOT ALREADY TH
  16 8B5= ...      CLOSETOP                     [CLOSE DIRECTORY 
  18 8BJW ...      SETREP   OK                  [FILE BEING RETRIEVED OK 
  19 8C4G ...      UP                           [BACK TO 'RETRIEVE' SEGMENT  
  12 8CJ6 ...XBREAKD                            [
  17 8D3Q          GEOERR   1,DIRBRKIN          [BREAK IN ON OPENDIR 
   5 8DHB    SBREAKIN
  19 8F32    [     IF AN ACTIVITY IS BROKEN-IN ON WHILE WAITING FOR A FILE   
  20 8FGL    [              TO BE RVED THEN THE COUNT OF THE NO. OF WAITERS IS   
  20 8G2=    [              REDUCED BY ONE AND THE JOB NUMBER IS INSERTED WITH   
  13 8GFW    [              FREVCT INCREMENTED BY ONE
   4 8G_G    [   
  10 8HF6    [              EXIT IS BY UP
   4 8HYQ    [   
  21 8JDB ...[              X4,X5,X6 = INCREMENT, FILE & FILESTORE NUMBERS RESPECTIVE
   4 8JY2    [   
  16 8KCL          CALL  7  SEARCH              [X2= FREQ BLOCK  
   7 8KX=          LDN   1  1
  19 8LBW          SBS   1  FREQCT(2)           [DECREMENT COUNT OF WAITERS  
  17 8LWG          LDX   3  FX2                 [RINSERT EXPECTS X3  
  16 8MB6                                       [CURRENT ACTIVITY
  20 8MTQ          CALL  1  RINSERT             [ INSERT JOB NO AND ALTER LENGTH 
  19 8N*B                                       [ DANGER IF TWO JOBS BREAK-IN
  18 8NT2                                       [         SIMULTANEOUSLY 
  20 8P#L          UP                           [ EXIT TO BREAK-IN ENTRY POINT   
  11 8PS=          MENDAREA 100,K99SCHEDENX  
   4 8Q?W    #END
   2 8QRG
   8 ____ ...16675733000700000000
