   7 22FL    #OPT  K0DEWELL=0
  14 22_=    #LIS  K0DEWELL>K0ALLGEO>K0GREATGEO>K0COMMAND
  16 233T ...      SEG      DEWELL,867,SECTION CENT,,G3UGSI,G400 
   4 236D ...[   
  17 2393 ...[ (C)  COPYRIGHT INTERNATIONAL COMPUTERS LIMITED  1982  
  14 23?L ...[      THIS EXCLUDES CODE UNDER #SKI G3UGSI 
   4 23B9 ...[   
   6 23C4 ...#UNS G400   
  15 23CX ...#     THIS CHAPTER IS MODIFIED FOR G3PLUS-IH MK2
   7 23DS ...#OPT  G3UGSI=0  
   6 23HC ...#SKI  G3UGSI
   4 23L2 ...(   
  16 23NK ...# MACRO TO PREVENT M551 AND SECURITY INTERFACE CLASH
   7 23R8 ...      SINM551   
   4 23TR ...)   
   7 23YG    #OPT  WELLTEST=0
   4 24XQ    [   
  13 25CB    [     THIS SEGMENT PERFORMS DEWELLMENT !
  17 25X2    [     IE. SETS UP A JOB WHICH IS REMOVED FROM THE WELL  
   4 26BL    [   
   4 26W=    [   
   7 26_B ...#DEF  XJWP4=0   
   7 27=Q ...#ALT  XJWP4=1   
  11 27*W          SEGENTRY K1DEWELL,START   
  11 27TG          SEGENTRY K2DEWELL,WAYON   
  11 27_* ...      SEGENTRY K3DEWELL,(GEOERR)
  11 2858 ...      SEGENTRY K4DEWELL,(GEOERR)
  11 2893 ...      SEGENTRY K5DEWELL,(GEOERR)
  12 28#Y ...      FSHENTRY K7DEWELL,,WMONF,WMONF
   4 28*6    [   
   4 28SQ    [   
   6 28X* ...#UNS G400   
   4 28_Y ...(   
   9 294H ...      SEGENTRY K61DEWELL
  16 2976 ...XTYPE          +0          [B22 = MOP, B23 = BACK   
   4 299P ...)   
   7 29#B    #DEF  SIZEABS=10
   8 29S2    #DEF  SIZELIST=10   
   7 2=3S ...#DEF SIZEDIR=4  
   7 2=?L    XMONE       -1  
   7 2=R=    ZERO        0   
   7 2?=W    XONE        1   
   7 2?QG    TWO         2   
   7 2#=6    THREE       3   
   9 2*9B    SOCSLEN     +ASSOCSLEN  
   8 2C7W    MFLANG      4HB1B0  
   7 2C*4 ...ODLANG      4HC1
   4 2CG= ...[   
   9 2CMG    WTEMP       #20000000   
   7 2D76 ...XJOBLIST    12H 
   9 2G5L                12HJOBLIST  
   7 2GK=                1   
   7 2H4W                1   
   8 2HJG                4H****  
   8 2J46    XTAB           +IVTP
   8 2JHQ ...               +IVLP
   8 2K3B                   +IVCP
   4 2KH2    [   
   4 2L2L    [   
   5 2LG=    XTRACT  
   8 2L_W          SBX   7  FX1  
   6 2MFG          READ  
  10 2M_6          MHUNTW   3,FILE,FRB   
   8 2NDQ          ADX   7  FX1  
   7 2NYB          EXIT  7  0
   4 2PD2 ...[   
   8 2PXL    XOPEN SBX   7  FX1  
   9 2Q5D ...      SETUPMOD ,5,FROZEN
   9 2Q7B ...      USEROPEX XBRK,6,5 
   8 2QWW          ADX   7  FX1  
   9 2RBG          TESTREP2 OK,YES   
  15 2RW6          EXIT  7  2                   [ ANY OTHER  
   8 2S*Q    XBRK  ADX   7  FX1  
  14 2STB          EXIT  7  1                   [ BREAKIN
  14 2T*2    YES   EXIT  7  0                   [ SUCCESS
   4 2TSL    [   
   4 2W#=    [   
  12 2W#W ...[ PUT JOB SOURCE INTO ACOMMUNE2/3   
  10 2W*G ...[ & PREPARE TO O/P VOUTPACK 
   5 2WB6 ...MOPID   
   8 2WBQ ...      SBX   7  FX1  
   8 2WCB ...      LDX   0  ACES 
  10 2WD2 ...      STO   0  ACOMMUNE3(2) 
  13 2WDL ...      ADN   0  #2331               [CI  
  10 2WF= ...      STO   0  ACOMMUNE2(2) 
  10 2WFW ...      LDCH  0  JSOURCE1(2)  
   8 2WGG ...      SBN   0  #74  
  14 2WH6 ...      BZE   0  XENW4               [J IF CI 
   8 2WHQ ...      LDX   0  ACES 
  10 2WJB ...      STO   0  ACOMMUNE2(2) 
  10 2WK2 ...      LDN   5  JSOURCE1(2)  
  10 2WKL ...      LDN   6  ACOMMUNE7(2) 
   7 2WL= ...      MOVE  5  3
   6 2WLW ...      ORIGIN
   7 2WMG ...XENW4 LDN   4  2
  10 2WN6 ...      LDN   6  ACOMMUNE2(2) 
   8 2WNQ ...      ADX   7  FX1  
   7 2WPB ...      EXIT  7  0
   4 2WQ2 ...[   
   4 2WQL ...[   
   5 2WRW    VOUTPACK
  17 2X?G          LDN   5  JPDVARCHAR          [ FOR VARCHAR OUTPUT 
   5 2XR6    XOUTPACK
   8 2Y=Q          SBX   7  FX1  
  19 2YQB          OUTPACKX 6,4,5               [ OUTPUT NEXT PACKED PARAM   
   8 2_=2          ADX   7  FX1  
   7 2_PL          EXIT  7  0
   4 329=    [   
   4 32NW    [   
   4 32RF ...SJOB
   8 32W4 ...      FJOCA    3,2  
   7 32YM ...      EXIT  7  0
   4 333= ...[   
   4 335T ...[   
   5 33KF ...SFABSNB 
   8 3455 ...      SBX   7  FX1  
  11 34JP ...      SETUPCORE 4,3,FILE,FABSNB 
   8 354* ...      STO   4  A1(3)
   8 35H_ ...      ADX   7  FX1  
   7 363K ...      EXIT  7  0
   4 36H9 ...[   
   5 3*_S ...OUTSKIP 
   8 3B3B ...      OUTPAR   SKIP 
   7 3B4Y ...      EXIT  7  0
   4 3B6G ...[   
   5 3B84 ...WELL3   
   8 3B9L ...      STO   0  GEN0 
  11 3B?8 ...      MHUNTW   3,JWELL,COPYSYS  
   7 3B#Q ...      JLADJUST 3
   9 3BB# ...      BRN      (GEN0)   
   4 3BCW    [   
   4 3BXG ...[   
   4 3CC6 ...[   
   5 3CWQ ...START   
   9 3DBB ...      LDN   4  SIZELIST 
  21 3DW2 ...      CALL   7  SFABSNB                  [SET UP FABSNB TO OPEN USER JOB
  10 7G29 ...      LDN   4  XJOBLIST(1)  
   9 7G2= ...      LDN   5  A1+1(3)  
  16 7G2? ...      MOVE  4  SIZELIST-1            [ INITIALISE   
  11 7G2* ...      MHUNTW   3,JWELL,COPYSYS  
  11 7G2B ...      LDN   4  A1+FRH+JLUSER(3) 
  15 7G2G ...      MOVE  4  3                   [ USERNAME   
  12 7G2Q ...      SETMODE  6,READ,NOERREP,UNTRAP
  17 7G3G ...      CALL  7  XOPEN               [ OPEN JOBLIST(/****)
   8 7G3Q ...      BRN      REDE 
  15 7G42 ...      BRN      XBR1                    [ BREAKIN
  10 7G4= ...      GEOERR   1,JOBLIST?   
  10 7G4C ...REDE  MFREE    FILE,FABSNB  
  11 7G4J ...      MHUNTW   3,JWELL,COPYSYS  
  13 7G4Q ...      TRACE    A1+FRH+JLJOBNO(3),DEWELL 
  12 7G52 ...      LDN   6  A1+FRH+JLJOBNAME(3)  
  17 7G5= ...      GETJOB   6,USER              [ ENTRY FOR THIS JOB 
   8 7G5G ...      TESTREP  OK,X 
   9 7G5Q ...      GEOERR   1,NO JOB 
  17 7G62 ...X     CALL  7  XTRACT             [ OBTAIN OTHER RECORDS
  11 7G6= ...      NAME     3,JWELL,JPARAMS  
   9 7G6G ...      CALL  7  XTRACT   
  10 7G6Q ...      NAME     3,JWELL,AJDF 
   9 7G72 ...      CALL  7  XTRACT   
  10 7G7= ...      NAME     3,JWELL,ACLB 
  16 7G7G ...      CLOSETOP                     [ CLOSE JOBLIST  
   4 7G7Q ...[   
  13 7G82 ...[     HANDLE CREATION OF MONITORING FILE
   4 7G8= ...[   
   5 7G8N ...WAYON   
  12 7G96 ...[     FIRST OPEN USER'S DIRECTORY   
   9 7G9J ...      LDN   4  SIZEDIR  
   9 7G=2 ...      CALL  7  SFABSNB  
   7 7G=G ...      LDN   7  0
  11 7G=Q ...      MHUNTW   2,JWELL,COPYSYS  
   4 7G?2 ...[   
  12 7G?= ...[     SET AWORK2 FOR MOP/BACK JOB   
   4 7G?G ...[   
   7 7G?Q ...      JLADJUST 2
  10 7G#2 ...      BS       2,JLBRUNNING 
  11 7G#= ...      JBS      SETMP,2,JLBMOP   
   8 7G#Q ...      SMO      FX2  
   9 7G*2 ...      STO   7  AWORK2   
   5 7G*6 ...SETMP   
   9 7G*= ...      SBN   2  A1+FRH   
  10 7G*G ...      SBX   2  JOBDATASIZE  
  11 7G*Q ...      LDN   4  JLUSER+A1+FRH(2) 
   9 7GB2 ...      LDN   5  A1+1(3)  
  15 7GB= ...      MOVE  4  3                   [ USERNAME   
   4 7GC= ...[   
  11 7GC? ...      SETMODE  6,GENERAL,QUERY  
   9 7GC# ...      OPEND    XBR1,6   
  12 7GC* ...[CHECK REPLY WORD FOR DIRECTORY OPEN
   9 7GCB ...      TESTREP2 OK,SFLOC 
   9 7GCC ...      BRN      WRONGSORT
   4 7GCD ...[   
  13 7GCF ...[     CHECK WHETHER FILE OF THIS NAME   
  12 7GCG ...[     (IE JOBNAME) ALTREADY EXISTS  
   5 7GCH ...SFLOC   
  11 7GCJ ...      SETNCORE 8,3,FILE,FLOCNB  
  11 7GCK ...      MHUNTW   2,JWELL,COPYSYS  
  12 7GCL ...      LDN   4  JLJOBNAME+A1+FRH(2)  
   8 7GCM ...      LDN   5  A1(3)
   7 7GCN ...      MOVE  4  3
   4 7GCP ...[   
   9 7GCQ ...      STOZ     A1+3(3)  
  18 7GCS ...      STOZ     A1+4(3)           [TO FIND LATEST GENERATION 
  17 7GCW ...      LDX   0  MFLANG(1)                [LANG CODE B1B0 
   9 7GCX ...      STO   0  A1+5(3)  
   9 7GCY ...      STOZ     A1+6(3)  
   9 7GC_ ...      STOZ     A1+7(3)  
   4 7GD2 ...[   
  11 7GD3 ...[     GET LATEST GEN OF THE FILE
   7 7GD4 ...      GETDIR   3
  10 7GD5 ...      MHUNTW   3,FILE,FLOCNB
   7 7GD6 ...      LDN   4  1
  16 7GD7 ...      TESTREP2 OK,OMF10                 [FILE EXISTS
  21 7GD8 ...      TESTREP2 NOFILE,OMF,VRYWRONG,NAMEINUSE      [FILE DOESN'T EXIST   
  19 7GD9 ...      CALL  7  SEB                      [BREAK FOR OTHER REPLIES
   4 7GD= ...[   
  13 7GD? ...[     NO MON FILE OF THIS NAME EXISTS   
   4 7GD# ...OMF 
  16 7GD* ...      STO   4  A1+4(3)                  [SET FGN=1  
   8 7GDB ...      BRN      OMF20
   4 7GDC ...[   
  12 7GDD ...[     A MON FILE OF THIS NAME EXISTS
   5 7GDF ...OMF10   
  17 7GDG ...      LDX   5  A1+4(3)             [PICK UP FGN OF FILE 
  14 7GDH ...      SLL   5  1                   [CLEAR B0
   7 7GDJ ...      SRL   5  1
   8 7GDK ...      LDN   6  4095 
  19 7GDL ...      TXL   5  6                   [MAKE SURE FGN NOT TOO BIG   
  18 7GDM ...      BCC      NAMEINUSE           [REPORT ERROR IF IT IS   
  15 7GDN ...      ADX   5  4                   [ADD 1 TO FGN
   9 7GDP ...      STO   5  A1+4(3)  
   4 7GDQ ...[   
  10 7GDR ...[     CREATE MONITORING FILE
   5 7GDS ...OMF20   
   7 7GDT ...      CREATEB  2
   9 7GDW ...      LDX   4  WTEMP(1) 
  16 7GDX ...      STO   4  CEINF2(2)                [TEMP FILE  
   4 7GDY ...[   
  15 7GD_ ...      SETMODE  6,CREATE,APPEND,SYSTCOMM,REPLY   
   9 7GF2 ...      OPENCREX XBR1,6   
   9 7GF3 ...      TESTREP2 OK,OMF50 
  19 7GF4 ...      CALL  7  SEB                      [BBEAK FOR REPLY NOT OK 
   4 7GF5 ...[   
  13 7GF6 ...[     MONITORING FILE HAS BEEN OPENED   
   5 7GF7 ...OMF50   
  10 7GF8 ...      MFREE    FILE,FLOCNB  
  10 7GF? ...      MFREE     FILE,FABSNB 
   4 7GFG ...[   
   4 7GFP ...[   
   5 7GG2 ...OJDFS   
   9 7GGQ ... SEGENTRY    K90DEWELL  
  10 7GH2 ...      BRN      XK91DEWELL   
   7 7GH6 ...#SKI  G3UGSI<1$1
   8 7GH= ...      INHIBITB XBR3 
   6 7GHB ...XK91DEWELL  
   9 7GHG ... SEGENTRY    K91DEWELL  
  20 7GHK ...#SKI  G3UGSI                       [USER GROUP SECURITY INTERFACE   
   8 7GHN ...      INHIBITB XBR3 
   8 7GHS ...      CALL  0  WELL3
  18 7GJG ...      STOZ     EXT+3(2)            [ FOR COMMAND PROCESSOR  
  15 7GJQ ...      JBS      NOJDF,3,JLBMOP      [ J IF NO JDF
   4 7GK2 ...[   
   7 7GK= ...[     OPEN JDF  
   4 7GKG ...[   
   4 7GKH ...OJDF
   9 7GKJ ...      LDN   4  SIZEABS  
   9 7GKK ...      CALL  7  SFABSNB  
  10 7GKL ...      MHUNTW   2,JWELL,AJDF 
  16 7GKM ...      LDX   0  JDFGEN(2)           [ GENERATION NO. 
   9 7GKN ...      STO   0  A1+8(3)  
  16 7GKP ...      LDX   0  JDFLANG(2)          [ LANGUAGE CODE  
   9 7GKQ ...      STO   0  A1+9(3)  
  15 7GKR ...      LDN   4  JDFDIR(2)           [ DIRECTORY  
   9 7GKS ...      LDN   5  A1+1(3)  
   7 7GKT ...      MOVE  4  3
  15 7GKW ...      LDN   4  JDFNAME(2)          [ FILENAME   
   9 7GKX ...      LDN   5  A1+4(3)  
   7 7GKY ...      MOVE  4  3
   7 7GK_ ...      LDN   0  1
   9 7GL2 ...      STO   0  A1+7(3)  
  15 7GL3 ...      SETMODE  6,READ,ERASING,REOPEN,LEAVE,REPLY
   9 7GLQ ...XCORD OPEND    XBR3,6   
  19 7GM2 ...      TESTREP2 OK,SFINE,LOST,XLOST,CANT,XCOOR3,TEMPLOST,TLOST   
   6 7GM4 ...#UNS B7535  
  11 7GM6 ...      TESTREP2 CLOSEAB,REMOVED  
  10 7GM= ...      GEOERR   1,JDFOPEN?   
   5 7GMG ...XCOOR3  
   9 7GML ...      MFREE    FILE,ENT 
   7 7GMQ ...      COOR3    6
   8 7GN2 ...      BRN      XCORD
   5 7GN= ...SFINE   
   4 7GNG ...[   
   4 7GNQ ...[   
  12 7GP2 ...[     SET BITS FOR COMMAND PROCESSOR
   4 7GP= ...[   
   7 7GPG ...      LDN   6  0
  16 7GPQ ...      MHUNT    3,FILE,ENT          [ FROM JDF OPEN  
  15 7GQ2 ...      LDX   0  ETM(3)              [ TYPE/MODE  
  14 7GQ= ...      ANDN  0  #77                 [ MODE   
   8 7GQG ...      ERN   0  #12  
  16 7GQQ ...      BNZ   0  MEXT                [ J IF SHIFT FILE
  16 7GR2 ...      ORX   6  GSIGN               [ B0 FOR GRAPHIC 
  17 7GR= ...MEXT  MFREE    FILE,ENT            [ NOT WANTED ANY MORE
   8 7GR# ...      CALL  0  WELL3
  10 7GS2 ...      JBC      MRJ,3,JLBJB  
  16 7GS= ...      LDCT  0  #200                [ BIT 1 FOR JB   
   7 7GSG ...      ORX   6  0
  15 7GSQ ...MRJ   JBC      MSTO,3,JLBRJ        [ J IF NOT RJ
  16 7GT2 ...      LDCT  0  #100                [ BIT 2 FOR RJ   
   7 7GT= ...      ORX   6  0
  17 7GTG ...MSTO  STO   6  EXT+3(2)            [ STORE IN NEW CPAT  
   4 7GTQ ...[   
  16 7GTW ...      STOZ     AWORK3(2)         [INITIALISE AWORK3 
   7 7GW2 ...      LDN   0  0
  10 7GW= ...      TXU   0  JLASSOCS(3)  
  20 7GWG ...      LDN   4  0                          [ COUNT OF ASSOCIATIONS   
  10 7GWQ ...      TXU   0  JLASSOCS+1(3)
   7 7GX2 ...      ADN   4  0
  10 7GX= ...      TXU   0  JLASSOCS+2(3)
   7 7GXG ...      ADN   4  0
  15 7GXQ ...      BZE   4  NOPROPS             [ J IF NONE  
  10 7GY2 ...      MPY   4  SOCSLEN(1)   
   7 7GY= ...      ADN   5  1
  18 7GYG ...      OPENSYS  ,PROPERTY,READ      [ OPEN :SYSTEM.PROPERTY  
  16 7GYQ ...      SETUPCOR 5,3,APERI,ASSOCS    [ CREATE BLOCK   
   8 7G_2 ...      STOZ     A1(3)
   8 7G_4 ...      CALL  0  WELL3
  10 7G_Q ...      LDN   3  JLASSOCS(3)  
   9 7H22 ...      LDN   4  AWORK2(2)
  19 7H2= ...      MOVE  3  3                   [ SAVE PROP. NOS IN AWORK2-4 
   7 7H2G ...      LDN   7  3
   7 7H2Q ...      LDN   6  0
   4 7H32 ...[   
   7 7H3= ...XR    SMO      6
   4 7H3G ...[   
   7 7H3Q ...[     PROPERTY  
   4 7H42 ...[   
   9 7H4= ...      LDX   5  AWORK2(2)
   8 7H4G ...      BZE   5  XNUN 
  10 7H4Q ...      APROPNAM 5,0,PROPNOT  
  11 7H52 ...      MHUNTW   3,APERI,ASSOCS   
   4 7H5= ...[   
  12 7H5G ...[     RETAIN START OF BLOCK ADDRESS 
   4 7H5Q ...[   
   9 7H62 ...      LDN   1  A1+1(3)  
   4 7H6= ...[   
  10 7H6G ...[     FIRST ENTRY ADDRESS   
   4 7H6Q ...[   
   8 7H72 ...      LDX   4  A1(3)
   4 7H7= ...[   
   8 7H7G ...[     NO OF ENTRIES 
   4 7H7Q ...[   
   8 7H82 ...      BZE   4  RNEW 
   8 7H8= ...RNDL  TXU   5  1(1) 
   8 7H8G ...      BCC      RSAME
   9 7H8Q ...      ADN   1  ASSOCSLEN
   8 7H92 ...      BCT   4  RNDL 
   4 7H9= ...[   
   8 7H9G ...RNEW  STO   5  1(1) 
  10 7H9Q ...      LDN   4  ACOMMUNE1(2) 
   8 7H=2 ...      LDN   5  2(1) 
   4 7H== ...[   
   9 7H=G ...[     COPY PROPERTY NAME
   4 7H=Q ...[   
   7 7H?2 ...      MOVE  4  3
   7 7H?= ...      LDN   0  1
   8 7H?G ...      ADS   0  A1(3)
   8 7H?Q ...      LDX   0  GSIGN
   8 7H#2 ...      STO   0  0(1) 
   4 7H#= ...[   
  17 7H#G ...[     HAVE SET LF BIT AD INCREASED NO OF ASSOCS IN BLOCK
   4 7H#Q ...[   
   8 7H*2 ...RSAME LDX   5  FX1  
   7 7H*= ...      ADX   5  6
   4 7H*G ...[   
  15 7H*Q ...[     GET PERIPHERAL TYPE BIT FOR ASSOCS ENTRY  
   4 7HB2 ...[   
   7 7HB= ...      SMO      5
   8 7HBG ...      LDX   0  XTAB 
   8 7HBQ ...      ORS   0  0(1) 
   7 7HC2 ...XNUN  ADN   6  1
   8 7HC= ...      LDX   2  FX2  
   8 7HCG ...      BCT   7  XR   
   4 7HCQ ...[   
  19 7HCW ...      STOZ     AWORK3(2)         [INDICATES PROPERTY FOUND OK   
   7 7HD2 ...      CLOSETOP  
  11 7HD= ...      MHUNTW   3,APERI,ASSOCS   
   8 7HDG ...      LDX   4  A1(3)
  10 7HDQ ...      MPY   4  SOCSLEN(1)   
   7 7HF2 ...      ADN   5  1
   7 7HF= ...      SLL   5  2
   8 7HFG ...      ADX   5  A1(3)
   8 7HFQ ...      BRN      STENL
   4 7HG2 ...[   
  11 7HG= ...RHUNT MHUNTW   2,APERI,ASSOCS   
   7 7HGG ...      EXIT  1  0
   4 7HGQ ...[   
   4 7HH2 ...[   
   9 7HH= ...STENL ALTLENG  3,5,RHUNT
   9 7HH? ...      BRN      NOPROPS  
   4 7HH# ...[   
   5 7HH* ...PROPNOT 
  15 7HHB ...[     NOT FOUND IN :SYSTEM.PROPERTY, SO ASSUME  
  11 7HHC ...[     THAT ONLY ^CENTRAL^ EXISTS
   7 7HHD ...      CLOSETOP  
  21 7HHF ...      MFREEW   APERI,ASSOCS        [CLOSE :SYSTEM.PROPERTY & FREE BLOCK 
  18 7HHG ...      STOZ     JSOURCE3(2)         [ZEROISE JSOURCE3 IN CPAT
  21 7HHH ...      JLASLOST                     [ZEROISE JLASSOCS WORDS IN JOBLIST   
   7 7HHJ ...      LDN   4  1
  19 7HHK ...      STO   4  AWORK3(2)          [INDICATES PROPERTY NOT FOUND 
   4 7HHL ...[   
   4 7HHM ...[   
   5 7HHQ ...NOPROPS 
   5 7HJ2 ...NOJDF   
   4 7HJ= ...[   
  13 7HJG ...[     FREE FABSNB USED FOR OPENING FILES
  15 7HJQ ...[     AND THEN SET ABOUT CREATING THE JOBBLOCK  
   4 7HK2 ...[   
  10 7HK7 ...      VFREE    FILE,FABSNB  
   8 7HK# ...      CALL  0  WELL3
  10 7HL2 ...      JBS   NCPR,3,JLBMOP   
  10 7HL= ...      MHUNTW   1,JWELL,AJDF 
  10 7HLG ...      LDN   4  JOBCURDIR(1) 
  10 7HLQ ...      LDN   5  CPREFIX(2)   
  17 7HM2 ...      MOVE  4  3                   [ CURRENT DIRECTORY  
   8 7HM= ...      LDX   1  FX1  
   4 7HMG ...NCPR
   4 7HPG    [   
  16 7J96          LDX   4  IMOPTRACE           [ DEFAULT TRACE  
  15 7JNQ          JBS      SMIN,3,JLBMOP       [ J IF MOP   
   9 7K8B          LDX   4  JOBTRACE 
   9 7KN2    SMIN  ORX   4  IMINTRACE
  19 7L7L          STO   4  AMFMSK(2)           [ INITIAL MONFILE CATEGORIES 
  11 7LCD          JBC      SNOTM,3,JLBMOP   
  10 7LM=          MHUNTW   3,CPB,CALAS  
  21 7M6W          STO   4  CMFMSK(3)                                  [ MASK FOR THI
   4 7MLG    [   
   9 7N66    SNOTM LDX   0  AWORK2(2)
   8 7NKQ          BNG   0  SMON 
   9 7P5B          LOBSFILE XMONE(1) 
   7 7PK2    SMON  LOBSFILE  
  10 7Q4L          TESTREP2 LOBSALR,WMONF
   9 7QJ=          INCRECUB FORCED   
   4 7R3W    [   
  13 7RHG    [     CREATE OUPUT IN MONITORING FILE   
   4 7S36    [   
   5 7SGQ    WMONF   
   6 7SGY ...#UNS G400   
   4 7SH6 ...(   
  17 7SH# ...      SEGENTRY K60DEWELL   [RESTORE TIME DODEWELLHOOK   
  18 7SHG ...      BRN      XDSK1       [BRN IF DEWELL HOOK NOT IN USE   
   9 7SHN ...      LDX   7  XTYPE(1) 
  14 7SHW ...      BZE   7  XDSK1       [NOTHING HOOKED  
   7 7SJ4 ...      ERN   7  3
  14 7SJ= ...      BZE   7  XDSK5       [HOOK ON ALL JOBS
   8 7SJD ...      CALL  0  WELL3
  13 7SJL ...      JBS      XDSK4,3,JLBMOP  [J IF MOP
   7 7SJS ...      SRC   7  1
  15 7SK2 ...      BNG   7  XDSK1       [J IF BACK NOT HOOKED
   8 7SK8 ...      BRN      XDSK5
   7 7SKB ...XDSK4 SRC   7  2
  15 7SKJ ...      BNG   7  XDSK1       [J IF MOP NOT HOOKED 
  15 7SKQ ...XDSK5 LDX   4  EXT+3(2)    [SAVE RUNJOB MARKER  
   9 7SKY ...      DOWN     DOMONOP,3
  14 7SL6 ...      BRN      XDSK2       [BREAK-IN RETURN 
  14 7SL# ...      BRN      XDSK1A      [DEWELL HOOK OK  
  10 7SLG ...#              EXIT ABANDON 
  16 7SLN ...      CALL  7  MONER       [ERASE MONITORING FILE   
  14 7SLW ...      CALL  7  XDSK3       [CLEAR MON MASK  
   7 7SM4 ...      LDN   5  1
  18 7SM= ...      ACROSS   ENWELLB,60  [GET ABANDON FINISHED BY ENDCOM  
   8 7SMD ...XDSK2 CALL  7  XDSK3
   8 7SML ...      BRN      XBR3 
   5 7SMS ...XDSK1A  
   9 7SN2 ...      STO   4  EXT+3(2) 
   5 7SN8 ...XDSK1   
   4 7SNB ...)   
   8 7SNJ ...      CALL  0  WELL3
   9 7SNK ...      FSHCODE  A,XENDA1 
   4 7SNL ...(   
   4 7SNM ...#   
  20 7SNN ...#     FOR JOB'S THAT ARE TO RUN ON 'B', WE MUST GO TO 'B' AT THIS   
  11 7SNP ...#     POINT IN THE PROCEEDINGS  
   4 7SNQ ...#   
  18 7SNR ...      JBC      XENDA1,3,JLBRUNB    [JIF JOB WILL RUN IN 'A' 
  17 7SNS ...      ACROSS   JWELLTWO,3          [ELSE GO TO 'B' NOW!!
   4 7SNT ...#   
   5 7SNW ...XENDA1  
   4 7SNX ...)   
  19 7SP# ...      JMBAC    WM1,3,JLBGENRES,JLBBREAK,JLBABANDON,JLBPARTRES   
   9 7SSD ...      DOWN     PNTLAST,8
   4 7SWB ...WM1 
  11 7T2B          MHUNTW   3,JWELL,JPARAMS  
   8 7TG2          LDEX  4  A1(3)
   8 7T_L ...      ADN   4  10   
   4 7WF=    [   
  14 7WYW    [     NOW CALCULATING GMON/ASET BLOCK SIZE  
   4 7XDG    [   
   8 7XJC ...      CALL  0  WELL3
  10 7YXB          LDX   0  JLRESTPTR(3) 
   8 7_C2          BZE   0  XOTB 
  10 7_LS ...      SBX   0  JOBDATASIZE  
   7 7_WL          SMO      0
   8 823# ...      LDX   0  0(3) 
   7 824W ...      LDX   7  0
   9 826D ...      ANDX  0  HALFTOP  
   8 8282 ...      BNZ   0  XOTB 
   7 829J ...      ADX   4  7
   7 82TW    XOTB  OUTBLOCK 4
  15 83*G          OUTPAR   USERNAME,JOBNAME,DATENOW,TIMENOW 
   8 83FC ...      CALL  0  WELL3
   7 84SB          LDN   4  1
  10 85#2          LDN   5  JPDJOBTYPE   
   9 85RL          LDN   6  ZERO(1)  
  15 86?=          JBS      ONL,3,JLBMOP        [ J IF LN/DC 
   7 86QW          ADN   6  1
  16 87=G    ONL   JBC      XCENTRAL,3,JLBCLUS  [ J IF CENTRAL   
   7 87Q6          ADN   6  2
   9 889Q          CALL  7  XOUTPACK 
  10 88PB          LDN   6  JSOURCE3(2)  
   9 8992          LDN   5  JPDPROPS 
   7 89DS ...      LDN   4  1
   9 89NL          CALL  7  XOUTPACK 
   9 8=8=          BRN      PARAMOUT 
   4 8=D4 ...[   
   5 8=MW    XCENTRAL
   9 8?7G          CALL  7  XOUTPACK 
   9 8?#N ...      CALL  7  OUTSKIP  
   5 8#6Q    PARAMOUT
  11 8#LB          MHUNTW   3,JWELL,JPARAMS  
   9 8*62          LDX   0  A1+1(3)  
   7 8*KL          SLL   0  1
  15 8B5=          BNG   0  POUT                [ J IF PARAM 
   9 8B=D ...      CALL  7  OUTSKIP  
  11 8C4G          MHUNTW   3,JWELL,JPARAMS  
   8 8CJ6    POUT  LDEX  4  A1(3)
   7 8D3Q          SBN   4  2
  15 8DHB          BZE   4  UNACS               [ NO PARAM OR
  17 8F32          LDN   6  A1+2(3)             [ ADDRESS OF PARAMS  
  18 8FGL          CALL  7  VOUTPACK            [ OUTPUT PARAM OR PARAMS 
   8 8FLH ...UNACS CALL  0  WELL3
  10 8G_G          LDX   6  JLRESTPTR(3) 
  19 8HF6          BZE   6  MOUT                [ SEE IF A REMAINDER FEILD   
  10 8HRG ...      SBX   3  JOBDATASIZE  
   7 8HYQ          SMO      6
  14 8JDB          LDX   4  0(3)                [ COUNT  
   7 8JHM ...      LDX   0  4
   9 8JJQ ...      ANDX  0  HALFTOP  
   8 8JKT ...      BNZ   0  MOUT 
   7 8JN8 ...      SBN   4  1
   7 8JY2          ADX   6  3
   7 8KCL          ADN   6  1
   9 8KX=          CALL  7  VOUTPACK 
   8 8K_5 ...      BRN      MOUT2
   9 8L2Y ...MOUT  CALL  7  OUTSKIP  
  10 8L4R ...MOUT2 LDX   0  CONTEXT(2)   
   8 8L6L ...      ANDN  0  #10  
  15 8L8F ...      BZE   0  MOUT3               [J IF NOT MOP
   8 8L=# ...      CALL  7  MOPID
   9 8L#7 ...      CALL  7  VOUTPACK 
   8 8L#D ...      BRN      MOUT4
   4 8L#J ...[   
   7 8L#P ...MOUT3 OUTNULL   
   8 8L*2 ...MOUT4 FJOCA    3,2  
  18 8L*? ...      TESTNAMX 3,JUSER(3),CPREFIX(2),NOTID   [J IF NO MATCH 
  16 8L*J ...      CALL  7  OUTSKIP              [OUTPUT NO PAR  
   8 8L*T ...      BRN     MOUT5 
   4 8L*_ ...[   
  10 8LB6 ...NOTID LDN   5  JPDUSERNAME  
   8 8LBC ...      LDN   4   3   
  10 8LBN ...      LDN   6  CPREFIX(2)   
  15 8LB_ ...      CALL  7  XOUTPACK       [OUTPUT CURR DIR  
   9 8LC= ...MOUT5 MONOUT   ASTART   
   4 8LC? ...[   
  17 8LC# ...[     SEE WHETHER AN ASSOCIATED PROPERTY HAS BEEN LOST  
   8 8LC* ...      CALL  0  WELL3
  15 8LCB ...      JBS      TELLHLS,3,JLBMOP        [J IF MOP
   9 8LCC ...      LDX   4  AWORK3(2)
  19 8LCD ...      BZE   4  TELLHLS                   [J IF AWORK3 IS CLEAR  
   4 8LCF ...[   
  18 8LCG ...[     AWORK3 SET MEANS USER MUST BE TOLD ABOUT LOST PROPERTY
   9 8LCH ...      MONOUT   CENTONLY 
   9 8LCJ ...      STOZ     AWORK3(2)
   4 8LCK ...[   
   5 8LCL ...TELLHLS 
  12 8LDS ...      HLSINFORM XHLS,DEWELL,JOBNO(2)
   4 8LFR ...XHLS
   8 8LFX ...      FSHCODE  AORB 
   4 8LG3 ...(   
  19 8LG7 ...#     FOR SHARED FILESTORE, WE SEND A MESSAGE TO THE MONITORING 
  17 8LG? ...#     FILE TO SAY WHICH MACHINE THE JOB IS RUNNING IN!! 
   4 8LGC ...#   
  21 8LGH ...      DOWN     JWELLTWO,7          [CODE TO OUTPUT MESSAGE IS ELSEWHERE 
   4 8LGM ...)   
   8 8LGR ...      CALL  0  WELL3
  18 8MTQ          JBS      PCLB,3,JLBMOP       [ NO ADATA/CREADL IF MOP 
  18 8N*B          LDN   4  CPDATA-A1+3         [ BASIC SIZE - JDF ONLY  
  11 8NT2          MHUNTW   3,JWELL,JPARAMS  
   8 8P#L          LDEX  5  A1(3)
   7 8PS=          SBN   5  2
  17 8Q?W          LDX   6  A1+1(3)             [ ACTUAL CHAR SIZE   
   8 8QRG          ANDN  6  #7777
  16 8R?6          BZE   5  NP                  [ J IF NO PARAMS 
  15 8RQQ          ADX   4  5                   [ NEW SIZE   
   7 8S=B          ADN   4  1
  16 8SQ2    NP    SETUPCOR 4,3,ADATA,CREADL    [ CREATE BLOCK   
   7 8T9L          SBX   4  5
   9 8TP=          SBN   4  CPDATA-A1
   7 8W8W          SLL   4  2
  16 8WNG          ADX   4  6                   [ NEW CHAR SIZE  
  15 8X86          STO   4  A1(3)               [ CHAR SIZE  
   8 8XMQ          BZE   5  NOPS 
   8 8Y7B          LDX   0  ACES 
  20 8YM2          STO   0  CPDATA+3(3)         [ SPACES BETWEEN JDF AND PARAMS  
  11 8_6L          MHUNTW   2,JWELL,JPARAMS  
  16 8_L=          LDN   6  A1+2(2)             [ INPUT PARAMS   
  10 925W          LDN   7  CPDATA+4(3)  
   7 92KG          SMO      5
   7 9356          MOVE  6  0
  15 93JQ    NOPS  MHUNTW   2,JWELL,AJDF        [ JDF NAME   
   9 944B          LDN   7  CPDATA(3)
  10 94J2          LDN   6  JDFNAME(2)   
  17 953L          MOVE  6  3                   [ MOVE IN JDF NAME   
   4 95H=    [   
  10 962W    PCLB  MHUNTW   3,JWELL,ACLB 
  15 96GG          LDEX  5  A1(3)               [ RECORD SIZE
  16 9726          SBN   5  2                   [ MINUS RED TAPE 
  15 97FQ          SLL   5  2                   [ INTO CHARS 
  19 97_B          OUTEXES  5,A1+2,JWELL,ACLB   [ OUTPUT A COPY OF COMMAND   
   9 98F2          OUTPAR   TIMENOW  
   7 98YL          LDN   4  0
   8 994H ...      CALL  0  WELL3
  16 9=CG          JBC      MNOUT,3,JLBMOP      [ SKIP IF NOT MOP
   8 9=X6          LDN   4  #1000
   9 9?BQ          ANDX  4  AMXMSK(2)
  17 9?WB          ERS   4  AMXMSK(2)           [ ENSURE CM BIT CLEAR
  17 9#B2    MNOUT MONOUT   ACOMJT              [ O/P COPY OF COMMAND
   8 9#FX ...      CALL  0  WELL3
  10 9*SW          JBC      NMP,3,JLBMOP 
   9 9B#G          ORS   4  AMXMSK(2)
   7 9BS6    NMP   LDN   4  0
  11 9C?Q          JBC      ZFREE,3,JLBMOP   
  15 9CRB          LDN   4  1                   [ X4=1 IF MOP
  20 9D?2    ZFREE MFREE    JWELL,COPYSYS       [ JWELL BLOCKS NOW FINISHED WITH 
  10 9DQL          MFREE    JWELL,ACLB   
  10 9DYW ...      MHUNT    1,JWELL,AJDF 
  10 9F76 ...      LDX   0  JDFWFNAME(1) 
  10 9F*B ...      STO   0  AWFNAME(2)   
   7 9FHL ...      FREECORE 1
  10 9FPW          MFREE    JWELL,JPARAMS
  11 9G9G          TRACE    JOBNO(2),DEWELLED
   9 9GBM ... SEGENTRY    K92DEWELL  
  10 9GDB ...      BRN      XK93DEWELL   
   9 9GG5 ...      DOWN     PNTLAST,5
   6 9GGY ...XK93DEWELL  
   9 9GHS ... SEGENTRY    K93DEWELL  
   7 9GJM ...#SKI  G3UGSI<1$1
   7 9GKH ...      PERMITBRK 
  10 9GMS ...      LDX   0  CONTEXT(2)   
   8 9GN2 ...      ANDN  0  #4000
  17 9GN5 ...      BNZ   0  TFS                 [J IF FULLY STARTED  
  15 9GN8 ...      CALL  7  SJOB                [X3->JOBBLOCK
   9 9GN? ...      LDEX  0  JURGE(3) 
  16 9GNB ...      BZE   0  NOTFS1              [J IF NO URGENCY 
  10 9GNF ...TFS   DOWN     HLSEVENT,2   
   4 9GNG ...[   
   5 9GNJ ...NOTFS1  
  17 9GNK ...[     NEXT SECTION IS COMPILED IF G3 USER GROUP SECURITY
  14 9GNL ...[     INTERFACE ENHANCEMENT IS SWITCHED ON  
   6 9GNM ...#SKI  G3UGSI
   4 9GNN ...(   
  12 9GNP ...[     SINMAC4 NEEDS B-IN INHIBITED  
   7 9GNQ ...      SINMAC4   
   7 9GNR ...      PERMITBRK 
  15 9GNS ...      BZE   4  SUE                 [J IF NOT MOP
   5 9GNT ...XENDC   
   6 9GNW ...      ENDCOM
   4 9GNX ...SUE 
   7 9GNY ...      ISSUCOMJ  
   4 9GN_ ...)   
   7 9GP2 ...#SKI  G3UGSI<1$1
   4 9GP3 ...(   
  19 9GP6          BZE   4  SUE                 [ GO AND ISSUCOMJ IF NOT MOP 
   8 9GSF ...      SINMAC4  MOP  
  13 9GXS ...XENDC ENDCOM                       [ MOP
   4 9H37 ...[   
   8 9HQJ ...SUE   SINMAC4  BACK 
  15 9H_= ...      ISSUCOMJ                     [ BACKGROUND 
   4 9J4L ...)   
   4 9J82    [   
   4 9K?7 ...SEB 
   9 9KC4 ...      GEOERR   1,MONFILE
   4 9KG_ ...[   
   4 9KLW    XBR3
   4 9L6G    [   
  10 9LL6    [     BREAKIN ON JDF OPEN   
  17 9M5Q    [     SO ERASE MONFILE AND TAKE NORMAL BREAKIN PATH...  
   4 9MKB    [   
   8 9N52          CALL  7  MONER
   4 9NJL    [   
   4 9P4= ...[   
   4 9PHW ...XBR1
   9 9Q3G ...      ACROSS ENWELLB,5  
   4 9QH6 ...[   
   4 9R2Q ...[   
   6 9R8# ...#UNS B7535  
   4 9R*W ...(   
   5 9RHD ...REMOVED 
  15 9RP2 ...[     JDF REMOVED, PROBABLY DUE TO QUITTED EDIT 
   9 9RWJ ...      OUTMESS  JWJDFGONE
  15 9S46 ...      CLOSE                        [CLOSE JDF   
   8 9S9N ...      CALL  7  MONER
  15 9SC= ...[     SET INDICATOR FOR ERROR ACTION IN ENWELLB 
   7 9SJS ...      LDN   4  1
  10 9SQB ...      STO   4  ACOMMUNE1(2) 
   9 9SXY ...      ACROSS   ENWELLB,4
   4 9T5G ...)   
   4 9T?4 ...[   
   6 9TDW    WRONGSORT   
   4 9WD6    [   
  19 9WXQ          OUTMESS  JWMONF              [ CANT OPEN MONITORING FILE  
   9 9XCB          BRN      XCOMMON  
   4 9XCY ...[   
   6 9XDG ...NAMEINUSE   
  19 9XF4 ...[     THERE IS A FILE IN USER S DIRECTORY WITH SAME NAME AS JOB,
  14 9XFL ...[     BUT WRONG SORT FOR A MONITORING FILE  
  10 9XG8 ...      OUTMESS  JWNAMEUSED   
  16 9XGQ ...      CLOSE                        [CLOSE DIRECTORY 
   9 9XH# ...      BRN      XCOMMON  
   4 9XHW ...[   
   9 9XJJ ...TLOST OUTMESS  JWJDFTEMP
   9 9XPQ ...      BRN      XLOST4   
  15 9XX2    XLOST OUTMESS  JWJDF               [ JDF LOST   
   5 9Y6S ...XLOST4  
  18 9YBL          CALL  7  MONER               [ ERASE MONITORING FILE  
   5 9YW=    XCOMMON 
  11 9_3D ...[     SET INDICATOR FOR ENWELLB 
  10 9_8L ...      STOZ     ACOMMUNE1(2) 
   9 9_*W ...      ACROSS ENWELLB,4  
   4 9_TG ...[   
   4 ==LQ    [   
   8 =?6B    MONER SBX   7  FX1  
   7 =?L2          GETFNAME  
   7 =#5L          CLOSETOP  
  16 =#K=          ERASE                        [ ERASE MONFILE  
   8 =*4W          ADX   7  FX1  
   7 =*JG          EXIT  7  0
   4 =B46    [   
   6 =B5H ...#UNS G400   
   4 =B6Y ...(   
  16 =B8* ...XDSK3 STOZ     AMFMSK(2)   [ZEROISE MONITORING MASK 
  18 =B9Q ...      HUNT     2,CPB,CALAS [TO STOP OUTPUT TO NON-EXISTANT  
  14 =B?7 ...      BNG   2  XDSKBJ      [MONITORING FILE 
  12 =B#J ...      STOZ     CMFMSK(2)   [HERE TO 
   5 =B*_ ...XDSKBJ  
   8 =BCB ...      LDX   2  FX2  
   7 =BDR ...      EXIT  7  0
   4 =BG8 ...)   
   4 =BHQ    #END
   8 ____ ...67412554000400000000
