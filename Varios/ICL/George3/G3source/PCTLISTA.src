  15 22FL          SEG      PCTLISTA,,GEORGE CONSTANTINIDES. 
  16 22PD ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD, 1982   
  11 22_=    #LIS  K0PCTLISTA>K0PCT>K0ALLGEO 
   7 23YG    # ENTRY POINTS  
  12 24D6          SEGENTRY K1PCTLISTA,Z1PCTLISTA
  12 24S= ...      SEGENTRY K2PCTLISTA,Z2PCTLISTA
  12 258B ...      SEGENTRY K3PCTLISTA,Z3PCTLISTA
  12 25PQ ...      SEGENTRY K4PCTLISTA,Z4PCTLISTA
   4 25X2    [   
  21 26BL    [THIS SEGMENT OUTPUTS HEADINGS TO A REMOTE LP OR TP WHERE THE 7020 IS   
  19 26W=    [ATTACHED VIA A MULTIPLEXOR/UNIPLEXOR/SCANNER -I.E. NOT A 7900. 
  16 27*W    [IT IS EQUIVALENT TO LISTHEAD/LISTINIT OR IPBLISTA. 
  20 27TG    [IT IS ENTERED  AT K1 FROM'LISTFIND' AFTER THE FILE TO BE LISTED HAS
  16 28*6    [BEEN OPENED, AND THE SETUP FILE IF THERE IS ONE.   
  11 29S2    [CURRENT ACTIVITY IS AN ACTLF.  
   4 2=?L    [   
   6 2=R=    [ON ENTRY K1
  17 2?=W    [AN ADATA/CREADL WITH '#LISTING OF USERNAME.FILENAME '  
  14 2?QG    [A FILE/ENT BLOCK OF THE FILE TO BE LISTED. 
   8 2#=6 ...[A GOUT/INFO BLOCK  
   4 2*P2    [   
   5 2B8L    [DURING 
  13 2BN=    [AWORK1 HAS B0 SET IF OUTPUT IS TO TP.  
   4 2BNC ...[   
   8 2BNP ...#DEF  QUAL=IWORK5   
   4 2BNW ...[   
  14 2BQN ...TWEMS          #76007631,#60763576,#04762300
   6 2BS# ...VLIP  FLIP  
   8 2BT6 ...      LDX   1  FX1  
   8 2BTY ...      LDX   2  FX2  
   7 2BWQ ...      EXIT  7  0
   4 2C7W    [   
   9 2CB6 ...ZZCL49         #75713777
   9 2CMG    YLIST          #76663232
  21 2D76    STAR           #32323232           [KEEP YLIST,STAR,ZLIST IN THIS ORDER 
   9 2DBY ...ZLIST          #32327666
   9 2DWJ ...               #76667634
  14 2F6B    NLIST          #76347666,#76327632,#76357634
  12 2FL2    TAPE           #76327620,#76207620  
  14 2G5L ...THEAD          28H    PRODUCED ON         AT
  14 2GK=    SHEAD          24H#OUTPUT BY LISTFILE IN '  
   9 2H4W    ONAT           8H' ON AT
   9 2H#N ...ZUSING         8H USING 
  10 2HJG    SDOC           12HDOCUMENT  
   9 2J46    TPUNCH         #40100000
   8 2JHQ    SPACE          #20  
   9 2K3B    STX            #76222020
   9 2K?8 ...XETX           #23000000
   9 2KH2    QDC3           #76037623
   4 2L2L    #   
  18 2LG=    [TABLE OF LEGIBLE CHARACTERS FOR VISIBLE HEADINGS ON THE TP 
   4 2L_W    [   
  14 2MFG    TABLE          #07566161,#56404040     [0   
  14 2M_6                   #05766740,#40404040     [1   
  14 2NDQ                   #07536165,#51404040     [2   
  14 2NYB                   #07526165,#56404040     [3   
  14 2PD2                   #10465262,#47424040     [4   
  14 2PXL                   #07727161,#66404040     [5   
  14 2QC=                   #07566565,#42404040     [6   
  14 2QWW                   #07606164,#70404040     [7   
  14 2RBG                   #07526565,#52404040     [8   
  14 2RW6                   #07516565,#56404040     [9   
  14 2S*Q                   #04454040,#40404040     [:   
  14 2STB                   #05414640,#40404040     [;   
  14 2T*2                   #06445261,#40404040     [<   
  14 2TSL                   #07464646,#46404040     [=   
  14 2W#=                   #06615244,#40404040     [>   
  14 2WRW                   #07506364,#50404040     [?   
  15 2X?G                   #10404040,#40404040     [SPACE   
  14 2XR6                   #05766540,#40404040     [!   
  14 2Y=Q                   #05707040,#40404040     [^   
  14 2YQB                   #12527667,#52766752     [#   
  14 2_=2                   #07455765,#61404040     [/   
  14 2_PL                   #11416244,#40516040     [%   
  14 329=                   #07536562,#45404040     [&   
  14 32NW                   #04704040,#40404040     ['   
  14 338G                   #05566140,#40404040     [(   
  14 33N6                   #05615640,#40404040     [)   
  14 347Q                   #06474747,#40404040     [*   
  14 34MB                   #10444457,#44444040     [+   
  14 3572                   #05414240,#40404040     [,   
  14 35LL                   #07444444,#44404040     [-   
  14 366=                   #04414040,#40404040     [.   
  14 36KW                   #11414244,#40506040     [/   
  14 375G                   #11506265,#67615640     [@   
  14 37K6                   #10576464,#64574040     [A   
  14 384Q                   #12766765,#65655442     [B   
  14 38JB                   #10566161,#61614040     [C   
  14 3942                   #11766761,#61615640     [D   
  14 39HL                   #10766765,#65614040     [E   
  14 3=3=                   #10766764,#64604040     [F   
  14 3=GW                   #11566161,#63634240     [G   
  14 3?2G                   #12766744,#44447667     [H   
  14 3?G6                   #07617667,#61404040     [I   
  14 3?_Q                   #12426161,#76666060     [J   
  14 3#FB                   #10766744,#52614040     [K   
  14 3#_2                   #10766741,#41414040     [L   
  14 3*DL                   #12766750,#44507667     [M   
  14 3*Y=                   #12766750,#44427667     [N   
  14 3BCW                   #10566565,#65564040     [O   
  14 3BXG                   #10766764,#64504040     [P   
  14 3CC6                   #11566161,#65564140     [Q   
  14 3CWQ                   #11766764,#64524140     [R   
  14 3DBB                   #10425165,#65424040     [S   
  14 3DW2                   #11606076,#67606040     [T   
  14 3F*L                   #12766641,#41417666     [U   
  14 3FT=                   #10704441,#44704040     [V   
  14 3G#W                   #12766742,#44427667     [W   
  14 3GSG                   #11615244,#44526140     [X   
  14 3H#6                   #10605047,#50604040     [Y   
  14 3HRQ                   #11416365,#61716140     [Z   
  14 3J?B                   #07766761,#61404040     [[   
  14 3JR2                   #11425176,#67654240     [$   
  14 3K=L                   #07616176,#67404040     []   
  14 3KQ=                   #07507667,#50404040     [_   
  14 3L9W                   #10424742,#42424040     [_   
  10 3LPG    VFROM          12H FROM JOB 
   9 3M96    VNULL          #40404040
   4 3MNQ    #   
  21 3N8B    [     END OF LINE IN HEADINGS : NL + RUNOUT FOR TAPE, NL * 2 FOR PRINTER
   4 3NN2    [   
  20 3P7L    ZLINE SMO      FX2                 [GET OUTPUT PERIPHERAL SWITCH.   
   9 3PM=          LDX   6  AWORK1   
  20 3Q6W          LDN   2  4                   [SET UP FOR PRINTER : #76327632. 
  10 3QLG          LDN   4  NLIST+1(1)   
  15 3R66          BZE   6  ZMOVE               [ZERO IF LP. 
  21 3RKQ          LDN   2  8                   [SET U7 FOR PUNCH : #7632762076207620
   9 3S5B          LDN   4  TAPE(1)  
  18 3SK2    ZMOVE MVCH  4  0(2)                [INSERT LINE TERMINATOR. 
   7 3SSS ...      EXIT  7  0
   4 3T4L    [   
  20 3TJ=    [     APPEND ETB TO THE OUTPUT BUFFER AND UPDATE THE BUFFER POINTER.
   4 3W3W    #   
   4 3W4L ...SEND
  18 3W_W ...      LDX   4  GSIGN               [APPEND ETB TO THE OUTPUT
   8 3X36          LDN   0  #7607
   7 3XGQ          MVCH  4  2
  19 3Y2B          WAKEUP   LF                  [FPUT THE AMOP IF NECCESSARY.
  17 3YG2          LDX   4  INTLK3(3)           [OLD BUFFER NUMBER   
  10 3Y_L          SBN   4  ALFBUFFNUM-1 
   8 3_F=          BZE   4  TWAS 
  10 3_YW          ADN   4  ALFBUFFNUM   
  17 42DG    TWAS  STO   4  INTLK3(3)           [NEW BUFFER NUMBER   
   8 42Y6          LDX   0  GSIGN
   7 43CQ          SMO   4   
  17 43XB          ORS   0  INTLK(3)            [INDICATE BUFFER USED
  21 44C2          SMO      4                   [SET EOM MARKER (MUST BE UNSET AGAIN 
  21 44WL          STO   0  INTLK2(3)           [BY THE CALLING ROUTINE IF NOT WANTED
   7 45B=          EXIT  7  0
   4 45TW    #   
  16 46*G    # DO SLEEP MACRO, TAKE CANT-DO ACTION IF ANY ERROR. 
   4 46T6    #   
   8 47#Q    SLEEP SBX   5  FX1  
  10 47SB          SLEEP    LF,XCD,XCD   
  18 48#2          MHUNTW   3,A7020,ALIST       [RE-LOCATE BUFFER BLOCK. 
  21 48RL          LDCT  0  #103                [J. OUT IF ERROR, MOP OFF OR INOP. LI
   9 49?=          ANDX  0  EWAIT(3) 
   8 49QW          BNZ   0  XCD  
   7 4==G          ADX   5  1
   7 4=Q6          EXIT  5  0
  19 4?9Q    #     IF INOP., MOP OFF, ^TE^ OR ^CD^ DURING OUTPUT OF HEADINGS.
   8 4?PB    XCD   LDX   2  FX2  
  10 4?SG ...      MHUNTW   3,A7020,ALIST
   8 4?XL ...      LDCT  0  #100 
   9 4#2Q ...      ANDX  0  EWAIT(3) 
   9 4#5W ...      ERS   0  EWAIT(3) 
  20 4*8=          VFREEW   ADATA,CREADL        [FREE THE VARIOUS DATA BLOCKS.   
   9 4*MW          VFREEW   FILE,ENT 
  21 4BM6          NGS   1  IWORK1(2)           [SET AS -VE TO SHOW NOT STARTED LISTI
  15 4C6Q          LFEND    CD                  [CANTDF EXIT 
   4 4CLB    #   
  20 4D62    #     GET ADDRESS OF NEXT BUFFER ; WAIT IF IT IS STILL BEING USED   
   4 4DKL    #   
  10 4F5=    NEW   MHUNTW   3,A7020,ALIST
  21 4FJW          LDCT  0  #103                [J. OUT IF ERROR, MOP OFF OR INOP. LI
   9 4G4G          ANDX  0  EWAIT(3) 
   8 4GJ6          BNZ   0  XCD  
  17 4H3Q          LDX   4  INTLK3(3)           [OLD BUFFER NUMBER   
  10 4HHB          SBN   4  ALFBUFFNUM-1 
   8 4J32          BZE   4  TWOAL
  10 4JGL          ADN   4  ALFBUFFNUM   
   7 4K2=    SEG   SMO   4   
   9 4KFW    TWOAL LDX   5  INTLK(3) 
  19 4K_G          BPZ   5  XFREE               [NEXT BUFFER NOT BEING USED  
   7 4LF6          SBX   7  1
  19 4LYQ          CALL  5  SLEEP               [WAIT FOR THE NEXT BUFFER.   
   7 4MDB          ADX   7  1
   8 4MY2          BRN      SEG  
   9 4NCL    XFREE ANDX  5  BITS22LS 
  19 4NX=          ADN   5  ACTBUFF(3)          [ADDRESS FOR DEPOSITING IN X5
   9 4PBW          LDN   4  STX(1)   
  15 4PWG          MVCH  4  2                   [#7622 = STX 
   7 4QB6          EXIT  7  0
   4 4QTQ    #   
  20 4R*B    # SET OUTPUT FINISHED MARKER AND WAIT FOR TRANSMISSION ENDED REPLY. 
   4 4RT2    #   
   7 4S33 ...WFIN1 LDXC  7  7
   8 4S3W ...      BRN      WFMHW
   8 4S4R ...SPLWF ORX   7  GSIGN
   7 4S7B ...      ADN   7  1
  10 4S8M ...WFMHW MHUNTW   3,A7020,ALIST
  21 4SS=          LDCT  0  #103                [J. OUT IF ERROR, MOP OFF OR INOP. LI
   9 4T?W          ANDX  0  EWAIT(3) 
   8 4TRG          BNZ   0  XCD  
  19 4W?6          LDCT  6  #20                 [SET OUTPUT COMPLETE MARKER. 
   7 4WDY ...      LDXC  7  7
   8 4WGW ...      BCC      NSPN2
   7 4WJS ...      ORN   6  8
   5 4WLQ ...NSPN2   
   8 4WQQ          SBX   7  FX1  
   9 4X=B    WFIN2 ORS   6  EWAIT(3) 
  13 4XQ2          CALL  5  SLEEP               [WAIT
  21 4Y9L          LDCT  0  #10                 [HAS THE TRANSMISSION FINISHED YET.  
   9 4YP=          ANDX  0  EWAIT(3) 
   8 4_8W          BZE   0  WFIN2
   7 4_NG          ADX   7  1
  19 5286          ERS   0  EWAIT(3)            [CLEAR THE ^FINISHED^ MARKER.
   7 52BR ...      LDN   0  8
   9 52F2 ...      ANDX  0  EWAIT(3) 
   9 52H9 ...      ERS   0  EWAIT(3) 
   7 52MQ          EXIT  7  0
   4 537B    #   
  20 53M2    #     STORE USER NAME AND JOB NAME , OMITTING NON-SIGNIFICANT SPACES
   4 546L    #   
  10 54L= ...UFLEN MHUNTW   1,GOUT,INFO  
  17 555W          FLENME1  GUSER,1,3           [X2,X4,X5 NOT CHANGED
   7 55KG          EXIT  4  0
   4 5656    #   
  21 56JQ    #     CONVERT TAPE HEADINGS INTO VISIBLE FORM ; CHAR COUNT IN X6 ; CHARS
  19 574B    #     TO BE CONVERTED START AT IWORK1 OF ACTIVITY ; CALL ON X7  
  17 57J2    #     ON ENTRY X5 CONTAINS ROOM LEFT IN TRANSFER COUNT  
   4 583L    #   
   8 58H=    VISIB SBX   7  FX1  
   8 592W          LDX   2  FX2  
  16 59GG          STO   7  IWORK15(2)          [PRESERVE LINK.  
   8 5=26          SMO      FX1  
  15 5=FQ          LDX   0  VNULL               [#40404040   
  10 5=_B          STO   0  IWORK14(2)   
   9 5?F2          ADN   2  IWORK1   
  17 5?YL    VLOOP LDCH  3  0(2)                [CHAR TO BE CONVERTED
   9 5#D=          ADN   3  TABLE(3) 
   8 5#XW          ADX   3  FX1  
   8 5*CG          LDX   1  FX2  
  10 5*X6          LDN   4  IWORK12(1)   
  21 5BBQ          MOVE  3  2                   [STORE REPRESENTATION FOR LATER MVCH 
   7 5BWB          LDX   3  4
  15 5CB2          LDX   4  IWORK11(1)          [DCH POINTER.
  19 5CTL          LDCH  1  0(3)                [NO. OF CHARS IN VISIBLE FORM
   7 5D*=          BCHX  3  /
   7 5DSW          TXL   5  1
  19 5F#G          BCC      VROOM               [ENOUGH ROOM IN THIS BLOCK   
  18 5FS6          BZE   5  VFULL               [NOT EVEN ROOM FOR 1 CHAR
   7 5G?Q          SMO   5   
  16 5GRB          MVCH  3  0                   [FILL UP BLOCK   
   8 5H?2    VFULL SBX   2  FX2  
   8 5HQL          SMO      FX2  
  18 5J==          STO   2  IWORK18             [PRESERVE CHAR  POINTER  
   8 5JPW          LDX   2  FX2  
   7 5K9G          SBX   3  2
  21 5KP6          STO   3  IWORK17(2)          [PRESERVE START ADDR:SS FOR NEXT MVCH
   7 5L8Q          SBX   1  5
  20 5LNB          STO   1  IWORK16(2)          [PRESERVE COUNT FOR NEXT MVCH.   
  19 5M82          MHUNTW   3,A7020,ALIST       [RESET THE BLOCK POINTER AND 
   7 5MML          LDX   5  4
  15 5N7=          CALL  7  SEND                [APPEND ETB  
  18 5NLW          SMO      4                   [UNSET THE EOM MARKER.   
   9 5P6G          STOZ     INTLK2(3)
   8 5PL6          LDX   1  FX1  
  16 5Q5Q          CALL  7  NEW                 [GET NEXT BUFFER 
  19 5QKB          LDX   4  5                   [START ADDRESS FOR DEPOSITING
  15 5R52          LDN   5  76                  [CHAR COUNT  
   8 5RJL          LDX   2  FX2  
  17 5S4=          LDX   1  IWORK16(2)          [COUNT FOR NEXT MVCH 
  10 5SHW          LDX   3  IWORK17(2)   
   7 5T3G          ADX   3  2
  21 5TH6          ADX   2  IWORK18(2)          [POINTER TO CHAR. BEING CONVERTED.   
   8 5W2Q    VROOM MVCH  3  0(1) 
   7 5WGB          SBX   5  1
   8 5X22          SMO      FX2  
   9 5XFL          STO   4  IWORK11  
   7 5X_=          BCHX  2  /
   8 5YDW          BCT   6  VLOOP
  17 5YYG          LDX   2  FX2                 [END OF CONVERSION   
  10 5_D6          LDX   7  IWORK15(2)   
   8 5_XQ          ADX   7  FX1  
   7 62CB          EXIT  7  0
  14 62X2    PDC3  BNZ   6  XY1TP              [J IF TP  
   9 63BL          LDX   7  EWAIT(3) 
   7 63W=          ANDN  7  1
  16 64*W          BZE   7  XY1TP                [J IF NOT 1901A 
   7 64TG          SMO      4
   9 65*6          STOZ     INTLK2(3)
   7 65SQ    XY1TP EXIT  0  0
   4 66#B    #   
   4 66S2    #   
   4 67?L    #   
   4 67R=    #   
  20 68=W    #     ENTRY POINT FROM LISTFIND TO OUTPUT HEADINGS AND PREPARE FOR  
  21 68QG    #     OUTPUT TO A BULK DEVICE ON A 7020 VIA A MULTIPLEXOR OR UNIPLEXOR  
   4 69=6    #   
   6 69PQ    Z1PCTLISTA  
  10 69Q= ...      MHUNTW   3,GOUT,INFO  
   9 69QQ ...      LDX   0  GMODE(3) 
  20 69R= ...      STO   0  AWORK2(2)            [STORE GMODE - CENTRAL LF USED  
  21 69RQ ...                                   [TO DO IT FOR US BUT DONT ANY MORE   
   8 69S* ...      LDCT  0  #200 
   9 69WY ...      STO   0  IWORK2(2)
   9 69_J ...      STOZ     AWORK3(2)
  17 6=9B          STOZ     IWORK10(2)          [MARK ENTRY AT K1.   
   9 6=P2          LDX   0  AWORK2(2)
   7 6?8L          SLL   0  2
   8 6?N=          ANDX  0  GSIGN
   9 6#7W          STO   0  AWORK4(2)
  10 6#M4 ...START MHUNTW   3,A7020,ALIST
   8 6#M5 ...      SMO      FX1  
   9 6#M7 ...      LDX   0  ZZCL49   
   9 6#M= ...      ANDS  0  EWAIT(3) 
   9 6#MB ...      LDX   0  BITS22LS 
   9 6#MN ...      ANDS  0  INTLK(3) 
  10 6#N2 ...      ANDS  0  INTLK+1(3)   
  10 6#N# ...      ANDS  0  INTLK+2(3)   
   8 6#NB ...      LDN   0  #2000
  21 6#ND ...      ORS   0  EWAIT(3)            [TELL  PCTLPTP BLOCK IS BEING RESET  
  18 6#NG ...      STOZ     INTLK1(3)           [RESET LAST OUTPUT NUMBER
  20 6#NJ ...      STOZ     INTLK3(3)           [AND LAST FILLED BUFFER NUMBER   
  17 6#NQ ...      LDX   6  APCTID(3)           [GET INTEFACE DATA   
   9 6#QD ...      LDN   1  FPTPCT(3)
   9 6#S6 ...SCINF LDX   1  FPCT(1)  
  11 6#TS ...      LDXC  0  APCTID-FPTPCT(1) 
  18 6#XG ...      BCC      SCINF               [A7020-INFORM HAS BO SET 
   7 6#_8 ...      LDEX  5  6
  18 6*2W ...      ADX   1  5                   [FIND CORRECT INTERFACE  
   9 6*4J ...      ANDX  6  HALFTOP  
   7 6*56 ...      NGN   0  7
   9 6*5N ...      ANDS  0  EWAIT(3) 
   7 6*6= ...      LDX   0  6
  12 6*7Y ...      ANDX  0  APCTSTAT-FPTPCT(1)   
   8 6*9L ...      BZE   0  NSPL 
   8 6*=8 ...      LDX   0  GSIGN
   9 6*=Q ...      STO   0  AWORK3(2)
   7 6*?# ...      LDN   0  4
   7 6**2 ...      SRC   6  6
  12 6*BN ...      ANDX  6  APCTSTAT-FPTPCT(1)   
   8 6*DB ...      BZE   6  STBTS
   7 6*G4 ...      ADN   0  2
   9 6*HQ ...STBTS ORS   0  EWAIT(3) 
   4 6*KD ...NSPL
  10 6*M6 ...      LDCH  6  JSOURCE1(2)  
   9 6*NS ...      LDX   5  EWAIT(3) 
  18 6*QG ...      CALL  7  VLIP                [FLIP BIT FOR POLL ACTY  
   7 6*S3 ...      LDN   7  4
   7 6*TJ ...      ANDX  7  5
   8 6*X5 ...      BZE   7  NSLPR
   9 6*YL ...      SPLPROPS  (GEOERR)
   5 6B27 ...NSLPR   
   7 6B3R ...      SRC   5  3
  18 6B*4 ...      ANDX  5  GSIGN               [-VE IF B21 IN EWAIT SET 
   8 6BHS ...STARA LDX   1  FX1  
   8 6BL2          LDX   2  FX2  
  18 6C5L          ANDN  6  1                   [#400:0 IF TP, 0 IF LP.  
   7 6CK=          SRC   6  1
  21 6D4W          STO   6  AWORK1(2)           [REMEMBER PERIPHERAL TYPE INDICATOR  
  21 6DJG          ADNC  6  4                   [TAPE REQUIRES 4 BUFFERS OF RUNOUT.  
   8 6F46          BCS      TRUN 
   4 6FHQ    #   
  17 6G3B ...      LDN   6  0                   [0 = START OF LISTING
   7 6HMW ...      ORX   6  5
  18 6HQN ...      CALL  7  NEW                 [GET BUFFER ADDRESS IN X5
  16 6J36 ...      BPZ   6  NSPLE               [ IF NOT SPOOLING
   9 6J5Y ...      LDN   4  TWEMS(1) 
   7 6J8Q ...      MVCH  4  7
   5 6J?J ...NSPLE   
   9 6J_6          LDN   4  NLIST(1) 
  14 6KDQ ...      MVCH  4  4                   [FF + _  
   9 6KYB ...      LDN   4  STAR(1)  
  14 6LD2 ...      MVCH  4  6                   [******  
   7 6LXL ...      LDX   4  5
   7 6MC= ...      SBN   4  1
  17 6N4N ...      BPZ   6  NSPLA               [J IF NOT SPOOLING   
   7 6N6L ...      NGN   7  7
   7 6N8J ...      SMO      7
   5 6N=G ...NSPLA   
  10 6NBG          SEGENTRY K50PCTLISTA  
  19 6NW6 ...      MVCH  4  A7021WIDTH-66       [ASSUMES 79<A7021WIDTH<133   
  15 6P*Q          CALL  7  SEND                [APPEND ETB. 
  18 6PTB          SMO      4                   [CLEAR THE EOM MARKER.   
   9 6Q*2          STOZ     INTLK2(3)
  16 6QSL          CALL  7  NEW                 [GET NEXT BUFFER 
   9 6R#=          LDN   4  STAR(1)  
  14 6RRW          MVCH  4  6                   [******  
   7 6S?G          LDX   4  5
   7 6SR6          SBN   4  1
  17 6T=N ...      BPZ   6  NSPLB               [J IF NOT SPOOLING   
   7 6TCW ...      MVCH  4  7
  16 6TP* ...NSPLB MVCH  4  50                  [MOVE 50 STARS   
  13 6Y55 ...      LDN   4  ZLIST(1)            [**_ 
   7 6YBJ ...      MVCH  4  4
  18 6YN6          CALL  7  SEND                [ADD CR & ETB AND OUTPUT.
   9 6_7Q          LDX   5  EWAIT(3) 
   7 6_MB          ANDN  5  1
  15 75JB          BZE   5  YN01A             [J IF NOT 1901A
   7 7642          SMO      4
   9 76HL          STOZ     INTLK2(3)
   8 773=    YN01A CALL  7  NEW  
  15 77GW          LDN   4  NLIST+1(1)          [2 NEWLINES  
   7 782G          MVCH  4  4
   8 78G6          BRN      WHEAD
   4 78_Q    #   
  21 79FB    #     SEPARATE TAPE ROUTINE : OUTPUT TWO FEET OF RUNOUT BEFORE HEADINGS 
   4 79_2    #   
  18 7=DL    TAG   SMO      4                   [CLEAR THE EOM MARKER.   
   9 7=Y=          STOZ     INTLK2(3)
   8 7?#= ...      BRN      TRUNA
   7 7?D= ...TRUN  ORX   6  5
   8 7?J= ...TRUNA CALL  7  NEW  
   7 7##= ...      LDXC  6  6
   8 7#D= ...      BCC      NSPLJ
   9 7#J= ...      LDN   4  TWEMS(1) 
   7 7#N= ...      MVCH  4  7
   5 7#S= ...NSPLJ   
   9 7*4= ...      LDN   4  TAPE(1)  
   8 7*8= ...      ORX   4  GSIGN
   7 7*FT ...      MVCH  4  6
   7 7*HD ...      LDX   4  5
   7 7*K3 ...      SBN   4  1
   8 7*LL ...      MVCH  4  108  
   5 7B4= ...NENYE   
   8 7C#W          CALL  7  SEND 
   8 7CSG          BCT   6  TAG  
   9 7D#6          LDX   5  EWAIT(3) 
   7 7DRQ          ANDN  5  1
  16 7GQ=          BZE   5  XN1901A             [J IF NOT 1901A  
   7 7H9W          SMO      4
  16 7HPG          STOZ     INTLK2(3)              [DONT SEND DC3
   5 7J96    XN1901A 
   4 7JNQ    #   
  19 7K8B    #     OUTPUT VISIBLE TAPE HEADINGS WITH FILENAME AND USERNAME   
   4 7KN2    #   
  11 7L7L          MHUNTW   3,ADATA,CREADL   
  20 7LM=          LDEX  6  A1+FRH(3)           [NO OF CHARS. IN ^#LISTING.....^ 
  17 7M6W          ADN   3  CPDATA+3            [ADDRESS OF FILENAME.
   8 7MLG          LDX   2  FX2  
   8 7N66          SBN   6  12   
   9 7NKQ          LDN   4  IWORK1(2)
   7 7P5B          SMO   6   
  20 7PK2          MVCH  3  0                   [SAVE FILENAME FOR CONVERSION.   
   8 7Q4L          CALL  7  NEW  
  19 7QJ=          STO   5  IWORK11(2)          [SET INITIAL OUTPUT POINTER. 
  15 7R3W          LDN   5  76                  [CHAR. COUNT 
  16 7RHG          CALL  7  VISIB               [CONVERT FILENAME
   8 7S36          LDX   1  FX1  
   8 7SGQ          ADN   1  VFROM
   9 7T2B          ADN   2  IWORK1   
   7 7TG2          MOVE  1  3
   8 7T_L          LDN   6  10   
  17 7WF=          CALL  7  VISIB               [CONVERT ' FROM JOB '
   9 7WYW          LDN   7  IWORK1(2)
  19 7XDG          CALL  4  UFLEN               [FLENME USER NAME & JOB NAME 
   9 7XY6          SBN   7  IWORK1(2)
  21 7YCQ          SLC   7  2                   [NO. OF CHARS IN :USERNAME.JOBNAME   
   7 7YXB          LDX   6  7
  20 7_C2          CALL  7  VISIB               [CONVERT USER NAME AND JOB NAME  
   9 7_LS ...      STOZ     IWORK5(2)
   4 7_WL    #   
   8 82B=          LDX   1  FX1  
  10 83*G          MHUNTW   3,A7020,ALIST
  17 83T6          LDX   5  4                   [DCH POINTER IN IOUT 
   8 84#Q          CALL  7  SEND 
   8 84SB          CALL  7  NEW  
  16 85#2          LDN   4  TAPE(1)             [#762076207620   
  18 85RL          NGN   6  1                   [SET MARKER TO SHOW TP.  
   8 86?=          ORX   4  GSIGN
   7 86QW          MVCH  4  6
   7 87=G          LDX   4  5
   7 87Q6          SBN   4  1
  14 889Q          MOVE  4  10                  [RUNOUT  
  17 88PB          ADN   5  9                   [SET POINTER TO END. 
   8 8992          CALL  7  SEND 
   8 89NL          CALL  7  NEW  
   4 8=8=    #   
  21 8=MW    #     IDENTIFY LISTING TO OUTPUT DEVICE WITH FILENAME , DATE AND TIME   
   4 8?7G    #   
  20 8?M6    WHEAD MHUNTW   3,ADATA,CREADL      [BLOCK CONTAINING #LISTING ETC.  
  18 8#6Q          LDN   4  CPDATA(3)           [START ADDRESS OF DATA   
  20 8#LB          SMO      A1+FRH(3)           [MOVE THE CONTENTS OF THE BLOCK  
  16 8*62          MVCH  4  0                   [INTO THE BUFFER.
  17 8*FP ...      FREECORE 3                   [ADATA/CREADL BLOCK  
  10 8C9C ...      MHUNTW   3,A7020,ALIST
  17 8C=6 ...      LDN   4  THEAD(1)            [^    PRODUCED ON ^  
   8 8C?H ...      MVCH  4  16   
  17 8C#Y ...      CALL  7  SEND                [ADD ETB AND OUTPUT  
   7 8CB* ...      SMO      4
  18 8CCQ ...      STOZ     INTLK2(3)           [MARK ^NOT CLEAN FINISH^ 
  16 8CF7 ...      CALL  7  NEW                 [GET NEW BUFFER  
  10 8CJ6          MHUNTW   3,FILE,ENT   
  19 8D3Q          LDX   4  EWRITDAY(3)         [DATE AND TIME THIS FILE WAS 
  16 8DHB          LDX   7  EWRITTIME(3)        [LAST WRITTEN TO 
  21 8F32          BZE   6  PRFIL               [X6 = 0 IF ENTERED FROM PRINTER CODE 
  20 8FGL          STOZ     IWORK16(2)          [CLEAR THE MODE INDICATOR WORD.  
  10 8G2=          STOZ     IWORK17(2)   
  10 8GFW    PRFIL MHUNTW   2,A7020,ALIST
  21 8G_G          SBN   5  3(2)                [RELATIVE POINTER TO 1ST. CH. OF DATE
  16 8HF6          FREECORE 3                   [FILE/ENT BLOCK. 
   7 8HYQ          TIMECON  7
  21 8JDB          LDX   6  ACOMMUNE1(2)        [SAVE THE CONVERTED TIME IN SAFE WORD
  10 8JY2          LDX   7  ACOMMUNE2(2) 
   7 8KCL          DATECON  4
  10 8KX=          MHUNTW   3,A7020,ALIST
  21 8LBW          ADX   5  3                   [POINTER TO 1ST. CHAR. OF DATE AREA. 
  17 8MGJ ...      STO   6  ACOMMUNE3(2)        [SAVE CONVERTED TIME 
  10 8MJR ...      STO   7  ACOMMUNE4(2) 
   7 8MRD ...      ADN   5  3
  10 8MTM ...      LDN   4  ACOMMUNE1(2) 
  17 8MXW ...      MVCH  4  7                   [PUT DATE IN BUFFER  
   9 8N4# ...      LDX   6  AWORK1(2)
  14 8N6H ...      BNZ   6  XNTP                [J IF TP 
  10 8N8Q ...      SEGENTRY K54PCTLISTA  
  10 8N=9 ...      LDN   6  A7021WIDTH   
   8 8N?N ...      SBN   6  90   
  20 8N*8 ...      BNG   6  XENDL               [J IF NOT ENOUGH ROOM FOR TIME   
   4 8NCC ...XNTP
   9 8P#= ...      LDN   4  ONAT+1(1)
  16 8PBF ...      MVCH  4  5                   [INSERT ^ AT  ^  
  10 8PDN ...      LDN   4  ACOMMUNE3(2) 
  15 8PGX ...      MVCH  4  8                   [INSERT TIME 
  17 8PM* ...XENDL CALL  7  ZLINE               [INSERT NEWLINE ETC  
  14 8PPJ ...      CALL  7  SEND                [ADD ETB 
   8 8PS=          CALL  0  PDC3 
   4 8Q?W    #   
  16 8QRG          CALL  7  NEW                 [GET NEW BUFFER  
   9 8R?6          LDN   4  SHEAD(1) 
  19 8RQQ          MVCH  4  24                  [^#OUTPUT BY LISTFILE IN '^  
   7 8S=B          LDX   7  5
  18 8SQ2          LDX   5  3                   [ADDRESS OF A7020/ALIST  
  19 8T9L          CALL  4  UFLEN               [STORE USER NAME & JOB NAME  
  17 8TP=          SBX   7  5                   [IN CASE ALIST MOVES 
   8 8W8W          DATECON  EDATE
  18 8WNG          LDX   5  7                   [CURRENT OUTPUT POINTER. 
  21 8X86          LDX   6  ACOMMUNE1(2)        [SAVE THE CONVERTED DATE IN SAFE WORD
  10 8XMQ          LDX   7  ACOMMUNE2(2) 
   9 8Y7B          TIMECON  CLEANCT  
  19 8YM2          MHUNTW   3,A7020,ALIST       [RELOCATE AFTER COORDINATION 
   7 8_6L          ADX   5  3
   9 8_L=          LDN   4  ONAT(1)  
  13 925W          MVCH  4  5                   [' ON
   7 92KG          LDN   4  6
  15 9356          MVCH  4  7                   [CURRENT DATE
   9 93JQ          LDN   4  ONAT+1(1)
  13 944B          MVCH  4  4                   [AT  
  10 94J2          LDN   4  ACOMMUNE1(2) 
  15 953L          MVCH  4  8                   [CURRENT TIME
   8 9559 ...      SMO      FX2  
   9 955Q ...      LDX   7  AWORK1   
  14 956? ...      BNZ   7  USNTP                [J IF TP
  10 956Y ...      SEGENTRY K55PCTLISTA  
  10 957B ...      LDN   6  A7021WIDTH   
   8 957S ...      SBN   6  90   
  19 958= ...      BNG   6  USNSM                [J IF NO ROOM FOR UNIT NAME 
   5 958Y ...USNTP   
  15 959F ...      CALL  7  SEND                [SEND BUFFER 
   7 95=2 ...      SMO      4
  18 95=H ...      STOZ     INTLK2(3)           [MARK ^NOT CLEAN FINISH^ 
  16 95?4 ...      CALL  7  NEW                 [GET NEW BUFFER  
  17 95?K ...      SBX   5  3                   [SAVE PLACE IN ALIST 
   8 95#6 ...      LDX   2  FX2  
  10 95#M ...      LDN   0  JSOURCE1(2)  
  19 95*8 ...      LDN   1  ACOMMUNE1(2)        [SET UP ACOMM WDS FOR OUTPERA
   7 95*P ...      MOVE  0  2
  21 95B= ...      OUTPERA                      [GET LP UNIT DESCRIPT. IN ACOMM WDS  
  10 95BR ...      MHUNTW   3,A7020,ALIST
  18 95C# ...      ADX   5  3                   [RESET X5 AS DCH POINTER 
  17 95CT ...      LDN   4  ZUSING(1)           [MOVE IN ^ USING ^   
   7 95DB ...      MVCH  4  7
  17 95DX ...      LDN   4  ACOMMUNE2(2)        [AND LP DESCRIPTION  
   7 95FD ...      MVCH  4  8
   5 95F_ ...USNSM   
  17 95H=          CALL  7  ZLINE               [INSERT NEWLINE ETC. 
   8 95R4 ...      CALL  7  SEND 
   8 962W          CALL  0  PDC3 
   4 96GG    #   
  16 9726          CALL  7  NEW                 [GET NEW BUFFER  
   9 97FQ          LDN   4  SDOC(1)  
  16 97_B          MVCH  4  12                  [^DOCUMENT    ^  
  10 98F2 ...      MHUNTW   2,GOUT,INFO  
   9 98YL          LDN   4  GHEAD(2) 
   9 99D=          LDX   2  GSUSE(2) 
  19 99XW          MVCH  4  0(2)                [COPY ACROSS USER'S HEADER   
   8 9=CG          SMO      FX2  
   9 9=X6          LDXC  0  AWORK4   
  20 9?BQ          BCC      NOFF                [J NO SETUP FILE SO NO FF REQD   
  20 9?WB          ADN   1  1                   [S/R ZLINE WILL PICK UP #7635734 
   9 9#B2    NOFF  CALL   7  ZLINE   
  15 9#TL          BNZ   6  XY2TP                 [J IF TP   
   9 9**=          LDX   0  EWAIT(3) 
   7 9*SW          ANDN  0  1
  16 9B#G          BZE   0  PRIN               [J IF NOT 1901A   
  15 9D?2          LDN   4  QDC3(1)            [PUT DC3ETX   
   7 9DKP ...      MVCH  4  2
  16 9DYD ...      CALL  7  SEND                [ADD ETB & OUTPUT
   7 9F?7 ...      SLC   5  2
   7 9FKW ...      SBN   5  1
   7 9FYK ...      SRC   5  2
  17 9G?# ...      LDN   4  XETX(1)              [CHANGE ETB TO ETX  
   7 9GL3 ...      MVCH  4  1
   8 9GYQ ...      BRN      PRIN1
   8 9H?F ...XY2TP BPZ   6  PRIN 
   7 9HL8 ...      LDN   0  8
  19 9HYX ...ROUT  LDN   4  TAPE+1(1)           [EXTRA RUNOUT FOR TAPE PUNCH 
   7 9J?L ...      MVCH  4  4
   8 9JL* ...      BCT   0  ROUT 
   8 9J_4 ...PRIN  CALL  7  SEND 
   5 9K?R ...PRIN1   
  10 9KLW ...      MHUNTW   3,GOUT,INFO  
   9 9L6G ...      LDXC  0  GFLI(3)  
  20 9LL6 ...      BCS      XFRTOERR            [J IF ERROR MESSAGE TO BE SENT...
  19 9M5Q ...[                                   ...INSTEAD OF LISTING FILE  
  20 9NJL          CALL  7  WFIN1               [WAIT FOR TRANSMISSION TO END.   
   4 9P4=    #   
  21 9PHW    #     STORE INFORMATION ABOUT FILE AND TYPE OF LISTING REQUIRED IN CA2-4
   4 9Q3G    #   
  10 9QH6 ...      MHUNTW   3,GOUT,INFO  
  21 9R2Q          LDX   4  GFLI(3)             [START RECORD NO. = START LINE NO. +1
   7 9RGB          ADN   4  1
  18 9S22          LDX   6  AWORK1(2)           [>0 IF PRINT ; <0 IF TAPE
  18 9SFL          STO   4  IWORK1(2)           [SET THE START RECORD NO.
  18 9S_=          STO   4  IWORK14(2)          [RESTART RECORD NUMBER.  
  19 9TDW          LDX   0  GNLI(3)             [NO. OF LINES TO BE OUTPUT   
   8 9TYG          BNZ   0  NEND 
  19 9WD6          NGN   0  1                   [INDICATE ALL LINES REQUIRED 
   9 9WXQ    NEND  STO   0  IWORK3(2)
   7 9XCB          LDCT  0  3
  18 9XX2          ANDX  0  GMODE(3)            [TYPE OF RESTART REQUIRED
   7 9YBL          SLL   0  7
   7 9YK? ...      LDX   4  0
   9 9YMG ...      ORX   0  AWORK3(2)
   7 9YPP ...      LDXC  4  4
   9 9YW=          STO   0  IWORK2(2)
   9 9Y_S ...      LDXC  4  AWORK3(2)
   8 9_32 ...      BCC      NOSPL
   8 9_48 ...      LDN   4  32   
   8 9_5B ...      LDX   7  IPAGE
   7 9_6J ...      TXL   4  7
   8 9_7Q ...      BCC      YSSPL
   7 9_8Y ...      LDX   4  7
   8 9_=6 ...      BRN      YSSPL
   5 9_?# ...NOSPL   
  16 9_*W          BPZ   0  NOPRE               [NOT PAGE RESTART
  18 9_TG          LDX   4  IPAGE               [CURRENT PAGES PARAMETER 
   5 =27W ...YSSPL   
   7 =2*6          ADN   4  2
   7 =2SQ          PHOTO    7
  21 =3#B          SETUPCORE 4,1,ADATA,IPTEMP   [BLOCK TO HOLD 1ST REC NO PER PAGE   
  17 =3S2          LDX   0  IWORK14(2)          [INITIAL RESTART NO. 
  17 =4?L          STOC  0  A1+1(1)             [CAREFUL : SETS C.   
  19 =4R=          STOZ     A1(1)               [INITIALISE THE PAGE COUNT.  
   9 =5=W          TESTMOVE 7,NOPRE  
  10 =5QG ...      MHUNTW   3,GOUT,INFO  
   7 =6=6    NOPRE LDN   4  0
  18 =6PQ          LDX   0  GFTY(3)             [J. IF AMORPHOUS FILE.   
   7 =79B          ANDN  0  1
  10 =7P2          BZE   0  XBASICMAIN   
  17 =88L          BNG   6  PUNCH1              [J. IF TAPE PUNCH.   
  18 =8N=          LDX   0  GMODE(3)            [J. IF SPECIAL LISTING.  
   7 =97W          ANDN  0  1
   9 =9MG          BNZ   0  NOBITS   
  19 ==76          LDX   0  GFTY(3)             [J. IF TAPE PUNCH TYPE FILE  
   8 ==LQ          ANDN  0  #10  
  10 =?6B          BNZ   0  TPTYPEMAIN   
  21 =?L2          LDCT  4  #100                [MARK TO SHOW PFCCS VALID IN REC.HDDR
   6 =#5L    TPTYPEMAIN  
  17 =#K=          LDX   0  GFTY(3)             [J. IF GRAPHIC FILE. 
   7 =*4W          ANDN  0  6
   9 =*JG          BZE   0  NOBITS   
  20 =B46          ORX   4  GSIGN               [MARK TO SHOW TRANSFORM REQUIRED.
  17 =BHQ          ANDN  0  2                   [J. IF ALLCHARS FILE.
   9 =C3B          BZE   0  NOBITS   
  19 =CH2          BCHX  4  NOBITS              [SET NORMAL FILE MARK & J.   
   5 =D2L    PUNCH1  
  21 =DG=          LDX   0  GFTY(3)             [SET IWORK17 NON ZERO IF TP TYPE FILE
   8 =D_W          ANDN  0  #10  
  10 =FFG          STO   0  IWORK17(2)   
   9 =F_6          BRN      PUNCH2   
   6 =GDQ    XBASICMAIN  
  18 =GYB          LDN   4  #1001               [MARK AS AMORPHOUS FILE  
  17 =HD2          BNG   6  PUNCH2              [J. IF TAPE PUNCH.   
   5 =HXL    NOBITS  
  18 =JC=          LDX   0  GMFMSK(3)           [CALEGORY MASK IF MONFILE
   9 =JWW          STO   0  IWORK4(2)
  18 =KBG          LDX   0  GMODE(3)            [SEQUENCING, MONFILE AND 
  17 =KW6          ANDN  0  7                   [SPECIAL LISTING BITS
   4 =L*Q    TAFF
   7 =LTB          ORX   4  0
   5 =M*2    PUNCH2  
   7 =MSL          LDN   0  4
   7 =N#=          ANDX  0  4
   8 =NRW          BZE   0  STOR 
  20 =P?G          NGN   0  2                   [ENSURE THE SPECIAL BIT IS NOT   
  18 =PR6          ANDX  4  0                   [SET FOR MONITORING FILES
   4 =Q=Q    STOR
   9 =QQB         STO  4  IWORK5(2)  
   9 =R=2          LDXC  0  AWORK4(2)
  16 =RPL          BCC      NOTSETUP            [J NO SETUX FILE 
  19 =S9=          LDN   0  IWORK1(2)           [SAVE MAIN FILE PARAMETERS   
   9 =SNW          LDN   1  IWORK6(2)
   7 =T8G          MOVE  0  5
   8 =TN6          LDCT  0  #200 
   9 =W2G ...      ORX   0  AWORK3(2)
  16 =W7Q          STO   0  IWORK2(2)           [SET ALL RESTART 
   9 =WMB          NGS   1  IWORK3(2)
   7 =X72          LDN   0  1
   9 =XLL          STO   0  IWORK1(2)
  18 =Y6=          LDX   0  GFTY(3)             [J. IF AMORPHOUS FILE.   
   8 =YKW          ANDN  0  #360 
  10 =_5G          BZE   0  XBASICSETUP  
  19 =_K6          ANDN  0  #200                [J. IF TAPE PUNCH TYPE FILE. 
   7 ?24Q          LDN   4  0
  10 ?2JB          BNZ   0  TPTYPESETUP  
  21 ?342          LDCT  4  #100                [MARK TO SHOW PFCCS VALID IN REC.HDDR
   6 ?3HL    TPTYPESETUP 
  17 ?43=          LDX   0  GFTY(3)             [J. IF GRAPHIC FILE. 
   8 ?4GW          ANDN  0  #140 
  10 ?52G          BZE   0  NOBITSSETUP  
  20 ?5G6          ORX   4  GSIGN               [MARK TO SHOW TRANSFORM REQUIRED.
  17 ?5_Q          ANDN  0  #40                 [J. IF ALLCHARS FILE.
  10 ?6FB          BZE   0  NOBITSSETUP  
  18 ?6_2          BCHX  4  NOBITSSETUP         [MARK AS NORMAL FILE & J.
   6 ?7DL    XBASICSETUP 
  18 ?7Y=          LDN   4  #1001               [MARK AS AMORPHOUS CILE  
   6 ?8CW    NOBITSSETUP 
   9 ?8XG          STO   4  IWORK5(2)
   5 ?9C6    NOTSETUP
   9 ?9G= ...      LDX   0  AWORK3(2)
   8 ?9KB ...      BPZ   0  NZPL 
   7 ?9NG ...      SRL   0  1
  10 ?9RL ...      ORS   0  IWORK17(2)   
  11 ?9RY ...      MHUNTW   1,ADATA,IPTEMP   
   9 ?9S= ...      LDX   0  IWORK1(2)
   9 ?9SJ ...      STO   0  A1+1(1)  
   4 ?9SW ...NZPL
   8 ?=3X ...      LDCT  0  #300 
  15 ?=5L ...      ANDX  0  AWORK2(2)           [SETUP AND   
  21 ?=6F ...      ORN   0  1                   [SET ^MAINFILE ALREADY POSITIONED^   
  10 ?=7* ...      STO   0  IWORK20(2)   
   8 ?=94 ...      LDX   0  GSIGN
   9 ?==R ...      ANDX  0  AWORK4(2)
  10 ?=#G ...      ORS   0  IWORK20(2)   
  17 ?=B9 ...      LDXC  0  AWORK1(2)           [J. IF TAPE PUNCH.   
   8 ?=CY ...      BCS      TP1  
  10 ?=FM ...      SEGENTRY K80PCTLISTA  
  20 ?=HB ...      LDN   6  A7021PAGE-HEADINGS-1[SET NO. OF LINES LEFT ON PAGE   
  10 ?=K5 ...      SEGENTRY K85PCTLISTA  
  17 ?=LS ...      LDN   5  A7021WIDTH+1        [SET PRINTER WIDTH   
  10 ?=NH ...      STO   5  IWORK12(2)   
  10 ?=Q= ...      STO   6  IWORK16(2)   
   6 ?=R_ ...Z4PCTLISTA  
   9 ?=TN ...      LDXC  0  QUAL(2)  
   9 ?=XC ...      BCC      ONCEMORE 
  11 ?=_6 ...      SETNCORE 40,3,ADATA,ACONV 
   5 ??2T ...ONCEMORE
  17 ??32 ...      STOZ     IWORK17(2)          [CLEAR CONTINUATION  
  10 ??37 ...      LDCH  0  IWORK20(2)   
   8 ??3# ...      ANDN  0  #54  
   8 ??3F ...      ERN   0  #50  
  17 ??3L ...      BZE   0  SETLI               [J TO LIST SETUP FILE
   7 ??3R ...      LDN   0  1
  10 ??3Y ...      ANDX  0  IWORK20(2)   
  21 ??45 ...      ERS   0  IWORK20(2)          [UNSET ^MAINFILE ALREADY POSITIONED^ 
  17 ??4= ...      BNZ   0  ZMPOS               [AND J IF IT WAS SET 
   5 ??4C ...SETLI   
   9 ??6? ...      ACROSS   RMTLF,1  
  17 ??6N ...TP1   LDN   6  1                   [SET SO RMTLF FLIPS  
  15 ??75 ...ZMPOS LDX   4  IWORK1(2)            [LINE NUMBER
  10 ??7G ...      STO   4  IWORK14(2)   
  19 ??7X ...                                    [SAVE LINE NUM FOR RESTARTS 
  10 ??8# ...      STO   4  IWORK19(2)   
  10 ??8P ...      MHUNTW   3,A7020,ALIST
  21 ??96 ...      STO   4  INTLK4(3)            [SO RESTARTS DONT GO BACK TOO FAR   
   7 ??9H ...      LDN   4  1
  21 ??9Y ...                                    [SHOW RMTLF POSITION AT CORRECT LINE
  20 ??=* ...      LDN   5  1                    [AND CLEAN FINISH ON LAST BUFFER
  16 ??=Q ...      ACROSS   RMTLF,2              [GO TO LIST FILE
   5 ??T=    XFRTOERR
   9 ??_5 ...      LDX   7  GMODE(3) 
   8 ?#2B ...      ANDX  7  BIT11
   8 ?#3M ...      BZE   7  NDRQ 
  17 ?#4Y ...[                                J IF FILE WAS CORRUPT  
  10 ?#69 ...      MHUNTW   3,A7020,ALIST
   9 ?#7G ...      ORS   7  EWAIT(3) 
   9 ?#8R ...      RENGAGE  5,XCD,XCD
   4 ?#=4 ...NDRQ
   8 ?##5 ...      LDX   1  FX1  
   8 ?##W          CALL  7  NEW  
  11 ?#SG ...      MHUNTW   1,ADATA,HEADING  
  19 ?*#6 ...      LDN   4  HEADPFCC(1)         [MESSAGE PRECEDED BY 2 NLS   
  17 ?*RQ ...      SMO      HEADCNT(1)          [CHARACTER CT INC NLS
  16 ?B?B ...      MVCH  4  0                   [MOVE INTO BUFFER
  17 ?BR2 ...      FREECORE 1                   [FREE HEADINGS BLOCK 
   8 ?C=L ...      CALL  7  ZLINE
   8 ?CGD ...      CALL  7  SEND 
   8 ?CQ= ...      CALL  7  WFIN1
  10 ?F#= ...      MHUNTW   3,GOUT,INFO  
  11 ?FCB ...      JBC      NCB2,3,BLFSETUP  
  17 ?FGG ...[                                   J IF NO SETUP FILE  
  19 ?FKL ...      FILEMOVE 1,0                 [PUT MAINFILE AT TOP LEVEL   
  10 ?G5= ...NCB2  ACROSS   PCTLISTB,2   
   4 ?Y*G    #   
   4 ?YT6    #   
  17 ?_#Q    SET   GEOERR   1,LONGSET           [LONGSET GIVEN TWICE 
   4 #2#2    #   
   6 #2C6 ...Z2PCTLISTA  
  10 #2G= ...      ACROSS   PCTLISTB,1   
   6 #2KB ...Z3PCTLISTA  
  10 #2NG ...      ACROSS   PCTLISTB,2   
   4 #2RL    #END
   8 ____ ...57150704000100000000
