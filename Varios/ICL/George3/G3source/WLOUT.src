  10 22FL    #OPT           K0WLOUT = 0  
   9 22_=    #LIS           K0WLOUT  
   8 23DW    #SEG           WLOUT
   7 23YG          8HWLOUT   
   4 24D6    [   
  21 24XQ       SEGENTRY K1WLOUT,WLOUTK1        [WRITES A 'FULL' ENTRY TO O/P FILE   
   4 25CB    [   
   4 25X2    [   
  19 26BL    [************************************************************   
   4 26W=    [   
   7 27*W    [ PRESET DATA   
   4 27TG    [   
  19 28*6    [************************************************************   
   4 28SQ    [   
   4 29#B    [   
  18 29S2       SEGENTRY    K90WLOUT            [CHANGED BY 'WLOUTLIM'   
   4 2=?L    [   
  21 2=R=    MAXFULL        100                 [MAX 'FULL' ENTRIES TO ANY OP'S CONS 
   4 2?=W    [   
   4 2?QG    [   
  13 2#=6    #DEF           WGOUTKEY1 = GOUTKEY1 - A1
  13 2#PQ    #DEF           WGOUTKEY2 = GOUTKEY2 - A1
  12 2*9B    #DEF           WGMODE = GMODE - A1  
  12 2*P2    #DEF           WGHEAD = GHEAD - A1  
  12 2B8L    #DEF           WGSUSE = GSUSE - A1  
  12 2BN=    #DEF           WGUSER = GUSER - A1  
  12 2C7W    #DEF           WGJOBS = GJOBS - A1  
  12 2CMG    #DEF           WGJOBN = GJOBN - A1  
  12 2D76    #DEF           WGOPTY = GOPTY - A1  
  13 2DLQ    #DEF           WCPROPNO = CPROPNO - A1  
  13 2F6B    #DEF           WBPROPCNT = BPROPCNT - A1
  12 2FL2    #DEF           WBPROPS = BPROPS - A1
   4 2G5L    [   
  20 2GK=    #DEF           WJOBSIZE = 12       [INCLUSIVE WORDCOUNT OF NARROW   
  20 2H4W                                       [FORMAT ^JOB^ MESSAGE (40 CHAR   
  21 2HJG                                       [MESSAGE + CATEGORY WD + COUNT WD)   
  12 2J46                                       [
  21 2JHQ    PDEVDISP       8.1                 [DISPLACEMENT OF 33 CHARS (I.E. TO   
  21 2K3B                                       [DEVICE FIELD IN NARROW FORMAT ^JOB^ 
  14 2KH2                                       [MESSAGE 
  10 2L2L    PVAR           +JPDVARCHAR  
   8 2LG=    PIMPLE         4H(I)
   8 2L_W    PUNAV          4H(U)
   6 2M74 ...#UNS AWLSZ  
  10 2M#= ...PFSEL           12HSELECTIVE
  12 2MFG                                       [
  15 2M_6    PNEWLINE       +JWLBLANK           [ MESSAGES:  
  19 2NDQ    PJOB           +JWLJOB             [ NARROW FORMAT MESSAGE MUST 
  21 2NYB    PMAINMESS      +BWLIST             [    IMMEDIATELY FOLLOW ITS STANDARD 
  20 2PD2                   +BWLIST1            [    EQUIVALENT (IF THERE IS ONE)
   9 2PXL    PROPMESS       +JWLPROPS
  10 2QC=                   +JWLPROPS1   
   9 2QWW    PSETUPMESS     +JWLSETUP
  10 2RBG                   +JWLSETUP1   
   9 2RW6    PTERMINATE     +AWLTERM 
   6 2RY* ...#UNS AWLSZ  
   4 2S2J ...(   
  13 2S4R ...PFSZE           +JWLFSZE     [FILE SIZE 
  14 2S72 ...PFNYK           +JWLFNYK     [NOT YET KNOWN 
  14 2S99 ...PFNTC           +JWLFNTC     [NOT CALCULATED
   4 2S?D ...)   
   4 2S*Q    [   
   4 2STB    [   
  15 2T*2    [       *******  USE OF AWORK WORDS  ********   
   4 2TSL    [   
  19 2W#=    [ AWORK1 - FORMAT OF ENTRY - 1(NARROW) OR 0(STANDARD) - SET BY  
  10 2WRW    [          CALLING CHAPTER  
  14 2X?G    [ AWORK2 - GMODE OF REQUEST - SET ON ENTRY  
  12 2XR6    [ AWORK3 - USED WITHIN 'SWRITEPROPS'
  12 2Y=Q    [ AWORK4 -  ........  ^  .........  
   4 2YQB    [   
   4 2_=2    [   
   4 2_PL    [   
   4 2_QM ...[   
  21 2_RN ...[***********************************************************************
   4 2_SP ...[   
   6 2_TQ ...XSTEPAGAIN  
   4 2_WR ...[   
  21 2_XS ...[***********************************************************************
   4 2_YT ...[   
  21 2__W ...[ THE STEPAGAIN MACRO IN A SUBROUTINE AS IT IS CALLED A NUMBER OF TIMES 
   4 322X ...[   
   8 323Y ...      SBX   7  FX1  
   7 324_ ...      STEPAGAIN 
   8 3262 ...      ADX   7  FX1  
   7 3273 ...      EXIT  7  0
   4 3284 ...[   
  19 329=    [************************************************************   
   4 32NW    [   
   5 338G    SWRITE  
   4 33N6    [   
  19 347Q    [************************************************************   
   4 34MB    [   
  12 3572    [ WRITES A LINE TO THE OUTPUT FILE  
   4 35LL    [   
  13 366=    [ LINK X5 MUST BE PRESERVED THROUGHOUT  
   4 36KW    [   
   9 375G    [ ENVIRONMENT REQUIRED:-
  10 37K6    [    X4=MESSAGE IDENTIFIER  
   4 384Q    [   
   9 38JB    [ ENVIRONMENT CHANGES:- 
  10 3942    [    X0, X3, X4 DESTROYED   
   6 39HL    [    X1=FX1 
   6 3=3=    [    X2=FX2 
   4 3=GW    [   
   7 3?2G       SBX   5  FX1 
  18 3?G6       ASSMESS  4                   [INSERT PARAMS IN MESSAGE   
   9 3?_Q       MHUNT    3,GMON,AOUT 
   9 3#FB       NAME     3,FILE,FAPB 
   9 3#_2       LDEX  0  A1+FRH(3)   
   7 3*DL       ADN   0  11  
   7 3*Y=       SRL   0  2   
  18 3BCW       STO   0  A1+FRH(3)           [CONVERT MESS LENGTH TO WDS 
   9 3BXG       APPEND   1,ZABANDON  
   9 3CC6       MFREE    FILE,FAPB   
   7 3CWQ       ADX   5  FX1 
   7 3DBB       EXIT  5  0   
   4 3DW2    [   
   4 3F*L    [   
  19 3FT=    [************************************************************   
   4 3G#W    [   
   6 3GSG    SWRITEPROPS 
   4 3H#6    [   
  19 3HRQ    [************************************************************   
   4 3J?B    [   
  17 3JR2    [ WRITES THE PROPERTIES LINE OF A FULL WL/SL/CH OUTPUT  
   4 3K=L    [   
  13 3KQ=    [ LINK X6 MUST BE PRESERVED THROUGHOUT  
   4 3L9W    [   
   9 3LPG    [ ENVIRONMENT REQUIRED:-
  15 3M96    [    FILE/FRB IN APERI/APROPNOS FORMAT EXISTS   
   4 3MNQ    [   
   9 3N8B    [ ENVIRONMENT CHANGES:- 
   8 3NN2    [    FILE/FRB FREED 
  11 3P7L    [    X0, X3, X4, X5 DESTROYED   
   6 3PM=    [    X1=FX1 
   6 3Q6W    [    X2=FX2 
   4 3QLG    [   
   7 3R66       SBX   6  FX1 
  19 3RKQ    [ A MESSAGE BLOCK TO HOLD DETAILS OF 'ATTMAX' 12-CHAR PROPERTY  
  19 3S5B    [ NAMES SPLIT INTO PERMANENT AND TEMPORARY STRINGS REQUIRES :-  
  13 3SK2    [    2 WORDS FOR THE PARAMETER HEADERS  
  18 3T4L    [    ATTMAX*13-1+3/4 FOR A SINGLE STRING <PROP>&<PROP>...   
  17 3TJ=    [    +1 IF THE TEMP/PERM SPLIT ISN'T ON A WORD BOUNDARY 
  10 3W3W       OUTBLOCN ATTMAX*13+2/4+3 
   9 3WHG       MHUNT    3,FILE,FRB  
  10 3X36       IF       +CHPERM(3),NZ   
   5 3XGQ       THEN 
  18 3Y2B          STO   0  AWORK3(2)           [LENGTH (CHARS) IN AWORK3
   9 3YG2          LDX   0  ADPERM(3)
  19 3Y_L          STO   0  AWORK4(2)           [START ADDR REL X3 IN AWORK4 
   9 3_F=          CALL  5  SPRFORMAT
   5 3_YW       ELSE 
   4 42?? ...XAA 
  10 42KN ...      OUTPACK  5,1,SKIP,XAA 
   5 42Y6       FI   
   9 43CQ       MHUNT    3,FILE,FRB  
  10 43XB       IF       +CHTEMP(3),NZ   
   5 44C2       THEN 
   9 44WL          STO   0  AWORK3(2)
   9 45B=          LDX   0  ADTEMP(3)
   9 45TW          STO   0  AWORK4(2)
   9 46*G          CALL  5  SPRFORMAT
   5 46T6       FI   
   8 47#Q       MFREE    FILE,FRB
   9 47SB       SMO      AWORK1(2)   
   9 48#2       LDX   4  PROPMESS(1) 
   8 48RL       CALL  5  SWRITE  
   7 49?=       ADX   6  FX1 
   7 49QW       EXIT  6  0   
   6 4==G    SPRFORMAT   
   7 4=Q6       SBX   5  FX1 
   9 4?9Q       LDX   4  AWORK3(2)   
   7 4?PB       ANDN  4  #3  
   7 4#92       IF       4,NZ
   5 4#NL       THEN 
   7 4*8=          NGX   4  4
  19 4*MW          ADN   4  4                   [X4 := REDUNDANT CHARS AT END
   9 4B7G          LDX   1  AWORK4(2)
   7 4BM6          ADX   1  3
   9 4C6Q          LDX   0  AWORK3(2)
   7 4CLB          SRC   0  2
  18 4D62          ADX   1  0                   [X1 -> FIRST UNUSED CHAR 
   8 4DKL          LDN   0  #20  
   5 4F5=          DO
   9 4FJW             DCH   0  0(1)  
   8 4G4G             BCHX  1  / 
  17 4GJ6          REPEAT   CT 4                [UNUSED CHARS NULL   
   5 4H3Q       FI   
   4 4H8Y ...XAB 
   9 4HB6 ...   MHUNT      3,FILE,FRB
   9 4HHB       LDX   4  AWORK3(2)   
   7 4J32       ADN   4  3   
   7 4JGL       SRL   4  2   
   9 4K2=       ADX   3  AWORK4(2)   
   7 4KFW       LDX   1  FX1 
  10 4K_G ...   OUTPACKX 3,4,PVAR(1),XAB 
   7 4LF6       ADX   5  FX1 
   7 4LYQ       EXIT  5  0   
   4 4MDB    [   
   4 4MY2    [   
   8 4NCL    [ WW   WW       W   
   8 4NX=    [ WW  WW       WW   
   8 4PBW    [ WW WW       WWW   
   8 4PWG    [ WWWW         WW   
   8 4QB6    [ WWWWW        WW   
   8 4QTQ    [ WW  WW       WW   
   8 4R*B    [ WW   WW      WW   
   8 4RT2    [ WW    WW     WW   
   4 4S#L    [   
   4 4SS=    [   
   5 4T?W    WLOUTK1 
   4 4TRG    [   
  19 4W?6    [ APPENDS INFORMATION ABOUT A SYSOUT REQUEST TO THE OUTPUT FILE 
   4 4WQQ    [   
   9 4X=B    [ ENVIRONMENT REQUIRED:-
  17 4XQ2    [    FILE PTRS SET TO 1ST RECORD OF ENTRY TO BE WRITTEN 
   4 4Y9L    [   
   9 4YP=    [ ENVIRONMENT CHANGES:- 
  13 4_8W    [    AWLCOUNT INCREMENTED IN /AWHATLIST 
  17 4_NG    [    FILE PTRS SET TO GIVE NEXT SYSOUT ENTRY ON 'STEP'  
  11 5286    [    INFO APPENDED TO O/P FILE  
  10 52MQ    [    AWORK2 = GMODE OF ENTRY
   4 537B    [   
  19 53M2       LDN   7  0                   [SET X7 TO 1 IF NARROW FORMAT   
  11 546L       MHUNTW   3,ADATA,AWHATLIST   
  10 54L=       IF       BS,3,AWLBNARROW 
   5 555W       THEN 
   7 55KG          LDN   7  1
   5 5656       FI   
  18 56JQ       STO   7  AWORK1(2)           [SEE 'USE OF AWORKS' ABOVE  
  17 574B       IF       7,NZ                [IF NARROW FORMAT THEN  
   5 57J2       THEN 
  17 583L          LDX   4  PNEWLINE(1)         [WRITE SEPARATOR LINE
   9 58H=          CALL  5  SWRITE   
  12 592W       FI                           [FI 
  16 59GG       OUTBLOCN 24                  [SET UP PARAM BLOCK 
   9 5=26 ...   CALL  7  XSTEPAGAIN  
   9 5=FQ       LDX   0  WGMODE(3)   
  21 5=_B       STO   0  AWORK2(2)           [ SAVE GMODE FOR TESTING FOR OTHER RECOR
   5 5?98 ...WLOUT2  
  13 5?F2 ...   OUTPACK  WGUSER(3),3,USERNAME,WLOUT1 
   5 5?NS ...WLOUT4  
  13 5?YL ...   OUTPACK  WGJOBN(3),3,JOBNAME,WLOUT3  
   5 5#8D ...WLOUT6  
   9 5#D=       LDCH  6  WGOPTY(3)   
  11 5#XW ...   OUTPACK  6,1,PERITYPE,WLOUT5 
  19 5*CG                                    [SET UP USER, JOB, PERIPH TYPE  
   9 5*X6       LDCH  0  WGOUTKEY1(3)
   7 5BBQ       LDX   6  ACES
   7 5BWB       LDN   2  0   
  18 5CB2       DCH   0  6(2)                [ASSUME WILL OUTPUT URGENCY 
   5 5CKS ...WLOUT8  
  16 5CTL       IF       BS,3,BLFRNAV        [IF UNAVAILABLE THEN
   5 5D*=       THEN 
  15 5DSW          LDX   6  PUNAV(1)            [OUTPUT (U)  
  19 5F#G       ELSF     MBS,3,BLFRIMPLEM,BLFRPARTIMP [ELSF IMPLEMENTING THEN
   5 5FS6       THEN 
  15 5G?Q          LDX   6  PIMPLE(1)           [OUTPUT (I)  
  12 5GRB       FI                           [FI 
  11 5H?2 ...   OUTPACK  6,1,VARCHAR,WLOUT7  
  17 5HQL       IF       +AWORK1(2),NZ       [IF NARROW FORMAT THEN  
   5 5J==       THEN 
  17 5JPW          ASSMESS  PJOB(1)             [SET UP ^JOB^ LINE   
  10 5K9G          MHUNT    3,GMON,AOUT  
  10 5KP6          NAME     3,FILE,FAPB  
  20 5L8Q                                       [UNLESS JOBNAME AND USERNAME ARE 
  20 5LNB                                       [BOTH ONLY ONE LETTER, THERE IS  
  20 5M82                                       [TOO MUCH SPACE BETWEEN END OF   
  19 5MML                                       [JOBNAME AND START OF DEVICE.
   9 5N7=          LDN   2  A1+2(3)  
  20 5NLW          ADX   2  PDEVDISP(1)         [X2 -> WHERE THE ^*^ SHOULD BE   
   7 5P6G          LDX   1  2
   8 5PL6          WHILE    TRUE 
   9 5Q5Q             LDCH  0  0(1)  
   9 5QKB             SBN   0  #32   
   8 5R52          AND      0,NZ 
   5 5RJL          DO
  20 5S4=          REPEAT   CHX 1               [X1 -> ^*^ IN OVER-LONG MESSAGE  
  21 5SHW          MVCH  1  7                   [MOVE THE DEVICE AND URGENCY FORWARD 
   4 5T3G    [   
   9 5TH6          LDN   0  WJOBSIZE 
  17 5W2Q          STO   0  A1+FRH(3)           [SET RECORD WORDCOUNT
  10 5WGB          SHORTEN  3,WJOBSIZE   
  19 5X22          APPEND   1,ZABANDON          [APPEND MSG AND FREE BLOCK   
   9 5XFL          MFREE    FILE,FAPB
  18 5X_=          OUTBLOCN 15                  [SET UP BLOCK FOR ^FILE^ 
  10 5YDW ...      CALL   7  XSTEPAGAIN  
  12 5YYG       FI                           [FI 
  11 5_D6       MHUNTW   2,ADATA,AWHATLIST   
  19 5_XQ       IF       BC,2,AWLBWKFILE     [IF (DOCUMENT NOT SELECTED ...  
   9 62CB          LDCH  0  WGHEAD(3)
   8 62X2          SBN   0  #21  
  18 63BL       AND      EITHER,0,NZ         [  ... AND NOT ! DOCUMENT)  
  17 63W=       OR       BS,2,AWLBFILE       [OR FILE SELECTED THEN  
   5 64*W       THEN 
  17 64TG          IF       BS,3,BLFRPROPREC    [IF PROPERTIES THEN  
   6 65*6          THEN  
  15 65SQ             READ                         [READ THEM
  17 66#B             DOWN     WLD,3               [SET UP FILENAME  
  10 66S2             SMO      AWORK1(2) 
  11 67?L             LDX   4  PMAINMESS(1)  
  17 67R=             CALL  5  SWRITE              [WRITE THE LINE   
  17 68=W             CALL  6  SWRITEPROPS         [WRITE PROPERTIES 
  16 68QG          ELSE                         [ELSE (NO PROPS) 
  17 69=6             DOWN     WLD,3               [SET UP FILENAME  
  10 69PQ             SMO      AWORK1(2) 
  11 6=9B             LDX   4  PMAINMESS(1)  
  17 6=P2             CALL  5  SWRITE              [WRITE THE LINE   
  16 6?8L          FI                           [FI PROPERTIES   
  15 6?N=       ELSE                         [ELSE (WORKFILE)
   5 6#7W    RETRY1  
   7 6#MG          STEPAGAIN 
   9 6*76          LDN   6  WGHEAD(3)
   9 6*LQ          LDX   5  WGSUSE(3)
   7 6B6B          ADN   5  3
  19 6BL2          SRL   5  2                   [X5 := WD CT OF DOCUMENT NAME
  10 6C5L          LDN   4  JPDENTRANT   
  17 6CK=          OUTPACKX 6,5,4,RETRY1        [SET UP DOCUMENT NAME
   9 6D4W          SMO      AWORK1(2)
  10 6DJG          LDX   4  PMAINMESS(1) 
  16 6F46          CALL  5  SWRITE              [WRITE MAIN LINE 
  10 6FHQ          BSXD     0,BLFPROPREC 
   9 6G3B          ANDX  0  AWORK2(2)
  17 6GH2          IF       0,NZ                [IF PROPERTIES THEN  
   6 6H2L          THEN  
   7 6HG=             READ   
  16 6H_W             CALL  6  SWRITEPROPS         [WRITE THEM   
  16 6JFG          FI                           [FI PROPERTIES   
   6 6J_6          STEP  
  15 6KDQ       FI                           [FI FILE/WKFILE 
   6 6KF2 ...#UNS AWLSZ  
   4 6KF= ...(   
   9 6KFC ...      SEGENTRY K50WLOUT 
   8 6KFJ ...      BRN      XSTOP
  11 6KFQ ...[  CODE OBEYED WHEN WLSIZE ON   
   4 6KG2 ...[   
  14 6KG= ...[ OUTPUTS ONE OF THREE MESSAGES - FILE SIZE 
  12 6KGG ...[             - FILE SIZE NOT KNOWN 
  13 6KGQ ...[             - FILE SIZE NOT CALCULATED
   4 6KH2 ...[   
   4 6KH= ...[   
   7 6KHG ...      BACKSPACE 
  10 6KHQ ...      BSXD     0,BLFPROPREC 
   9 6KJ2 ...      ANDX  0  AWORK2(2)
   8 6KJ= ...      BZE   0  XPAST
   7 6KJG ...      BACKSPACE 
   5 6KJQ ...XPAST   
   9 6KK2 ...      CALL  7 XSTEPAGAIN
  11 6KK= ...      JBC     WOFF,3,BLFRWLSIZE 
   7 6KKG ...      LFWLINFO  
   7 6KKQ ...      SMO      2
   8 6KL2 ...      LDX   6  0(3) 
   8 6KL= ...      BPZ   6  XOUT2
  10 6KLG ...[ FILE SIZE NOT YET KNOWN   
   9 6KLQ ...      LDX   4  PFNYK(1) 
   8 6KM2 ...      BRN      XENDP
  12 6KM= ...[ FILE SIZE : N BLOCKS (SELECTIVE)  
   4 6KMG ...[   
   7 6KMQ ...XOUT2 OUTBLOCN 6
   5 6KMY ...XOUT2B  
  12 6KN6 ...      OUTPACK  6,1,BLOCKCT,XOUT2B   
  10 6KN# ...      CALL  7  XSTEPAGAIN   
  10 6KNG ...      LDX   0  GFLI-A1(3)   
  10 6KNQ ...      ORX   0  GNLI-A1(3)   
  15 6KP2 ...      BZE   0  XOUT3              [NOT SELECTIVE
   8 6KP= ...      BZE   6  XOUT3
   5 6KPB ...XOUT2A  
   9 6KPG ...      LDX   5  PFSEL(1) 
  10 6KPQ ...      LDX   6  PFSEL+1(1)   
  10 6KQ2 ...      LDX   7  PFSEL+2(1)   
  12 6KQ= ...      OUTPACK  5,3,VARCHAR,XOUT2A   
   9 6KQG ...XOUT3 LDX   4  PFSZE(1) 
   8 6KQQ ...      BRN      XENDP
   4 6KR2 ...[   
  18 6KR= ...[ FILE SIZE NOT CALCULATED - WLSIZE OFF WHEN ENTRY PUT IN   
   7 6KRG ...[ :SYSTEM.OUTPUT
   4 6KRQ ...[   
   9 6KS2 ...WOFF  LDX   4  PFNTC(1) 
   5 6KS= ...XENDP   
   9 6KSG ...      CALL  5  SWRITE   
   6 6KSQ ...      STEP  
  10 6KT2 ...      BSXD     0,BLFPROPREC 
   9 6KT= ...      ANDX  0  AWORK2(2)
   8 6KTG ...      BZE   0  XSTOP
   6 6KTQ ...      STEP  
   4 6KW2 ...[   
   9 6KW= ...XSTOP SEGENTRY K51WLOUT 
   4 6KWG ...[   
   4 6KWQ ...)   
   7 6KYB       LDX   2  FX2 
   9 6LD2       BSXD     0,BLFSETUP  
   9 6LXL       ANDX  0  AWORK2(2)   
  15 6MC=       IF       0,NZ                [IF SETUP THEN  
   5 6MWW       THEN 
   8 6NBG          OUTBLOCN 15   
  10 6NW6          BSXD     0,BLFSETNAV  
   9 6P*Q          ANDX  0  AWORK2(2)
  17 6PTB          IF       0,NZ                [IF UNAVAILABLE THEN 
   6 6Q*2          THEN  
   5 6QJS ...WLOUTC  
   9 6QSL             SMO      FX1   
   9 6R#=             LDX   6  PUNAV 
  15 6RRW ...         OUTPACK  6,1,VARCHAR,WLOUTC [MARK IT   
  13 6S?G          FI                           [FI  
  16 6SR6          DOWN     WLD,3               [OUTPACK FILENAME
   9 6T=Q          SMO      AWORK1(2)
  10 6TQB          LDX   4  PSETUPMESS(1)
  17 6W=2          CALL  5  SWRITE              [WRITE THE SETUP LINE
  14 6WPL       FI                           [FI SETUP   
  11 6X9=       MHUNTW   2,ADATA,AWHATLIST   
   9 6XNW       LDX   0  AWLCOUNT(2) 
  10 6Y8G       ADX   0  AWLCOUNTUN(2)   
   7 6YN6       LDX   1  FX1 
  17 6_7Q       IF       0,E,MAXFULL(1)      [IF MAX ENTRIES OUTPUT  
  19 6_MB       AND      MBAS,2,AWLBHERE,AWLBOPER [AND HERE, OPERATOR THEN   
   5 7272       THEN 
  10 72LL          BC       2,AWLBFULL   
  10 736=          LDX   4  PTERMINATE(1)
  18 73KW          CALL  5  SWRITE              [INFORM OF TERMINATION   
  12 745G       FI                           [FI 
   4 74K6    [   
   7 754Q       LDX   2  FX2 
   9 75JB       LDX   0  AWORK2(2)   
   9 7642       STO   0  ACOMMUNE2(2)
   7 76HL       UPPLUS   1   
   4 76JF ...[   
   5 76K# ...WLOUT1  
  10 76L7 ...      CALL  7  XSTEPAGAIN   
   9 76M2 ...      BRN      WLOUT2   
   4 76MT ...[   
   5 76NN ...WLOUT3  
  10 76PH ...      CALL  7  XSTEPAGAIN   
   9 76QB ...      BRN      WLOUT4   
   4 76R9 ...[   
   5 76S4 ...WLOUT5  
  10 76SX ...      CALL  7  XSTEPAGAIN   
   9 76TQ ...      BRN      WLOUT6   
   4 76WK ...[   
   5 76XD ...WLOUT7  
  10 76Y? ...      CALL  7  XSTEPAGAIN   
   9 76_6 ...      BRN      WLOUT8   
   4 76__ ...[   
   4 773=    [   
   4 77GW    [   
  19 782G    [************************************************************   
   4 78G6    [   
   7 78_Q    [ BREAKIN LABELS
   4 79FB    [   
  19 79_2    [************************************************************   
   4 7=DL    [   
   4 7=Y=    [   
  13 7?CW    ZABANDON           [ OUTPUT FILE FULL   
   4 7?XG    [   
   5 7#C6       UP   
   4 7#WQ    [   
   4 7*BB    [   
  19 7*W2    [************************************************************   
   4 7B*L    [   
   6 7BT=    [ GEOERRS   
   4 7C#W    [   
  19 7CSG    [************************************************************   
   4 7D#6    [   
   4 7DRQ    [   
   4 7H9W    #END
   6 ____ ...644731560003
