  16 22FL    #SEG  CIOUT8                       [G CONSTANTINIDES
   9 22_=    #OPT           K0CIOUT=0
   9 23DW    #OPT           K6CIOUT=0
  14 23YG    #LIS           K0CIOUT>K0ALLGEO>K0COMMAND   
   9 24D6                   8HCIOUT8 
   7 24XQ    # ENTRY POINTS  
  11 25CB          SEGENTRY K1CIOUT,Z1CIOUT  
  10 25X2          MENDAREA 50,K100CIOUT 
  21 26BL    #                                  THIS  SEGMENT  IS  USED  TO  TRANS-  
  21 26W=    #              FER  MESSAGES  FROM  THE  COMMAND  PROCESSOR  TO  CIMOP  
   4 27*W    #   
  20 27TG    WAKEY          [THIS SUBR WAKES UP THE OBJECT PROG USING CI CHANNEL 
  20 28*6                   [AND IF CPA IS BEING ABANDONED,ALSO WAKES UP THE JOB 
  16 28SQ                   [WHICH IS DRIVING THE OBJECT PROGRAM 
   8 29#B          SBX   7  FX1  
  18 29S2          LONGON1  CIREL,CIJOBNUM(3)   [ELSE ACTIVATE PARENT CPA
  16 2=?L          LDX   2  CIFPTR(3)           [PTR TO THE OLPA 
   9 2=R=          SBN   2  BWORKRING
  19 2?=W          LDX   0  ATYPE(2)            [RENAMED GENERAL IF RELEASED 
   8 2?QG          SRL   0  12   
   9 2#=6          SBN   0  GENERAL  
   8 2#PQ          BNZ   0  WKY  
  21 2*9B          LDX   1  FX1                 [DONOT TICKLE SINCE IS BEING RELEASED
   8 2*P2          LDX   2  FX2  
   8 2B8L          BRN      WKY1 
  21 2BN=    WKY   TICKLE   CIJOBNUM(3),CIMEMBER(3)  [WAKE UP THE MEMBER THAT ISSUED 
  20 2C7W                                       [THE LAST PERI TO THE CI CHANNEL 
   4 2CMG    WKY1
   7 2D76          ADX   7  1
   7 2DLQ          EXIT  7  0
   4 2F6B    #   
   4 2FL2    #   
   5 2G5L    Z1CIOUT 
  10 2GK=          LDX   0  ACOMMUNE7(2) 
   9 2H4W          STO   0  AWORK1(2)
   7 2HJG          READYOFF  
  10 2J46    Z60   MHUNTW  3,AMXOR,CILCB 
  10 2JHQ          LDX   5  CIREADY(3)   
   7 2K3B          SLL   5  1
  19 2KH2          BPZ   5  Z10                 [J IF INTERLOCK BIT 1 UNSET  
   8 2L2L          LDCT  5  #100 
  18 2LG=          ORS   5  CIREADY(3)          [ SET  INTERLOCK  BIT2   
   8 2L_W          CALL  7  WAKEY
   6 2MFG          COOR1 
  20 2M_6          BRN      Z60                 [ALWAYS LOOK FOR BBC BLOCKS FIRST
   4 2NDQ    Z10 
  10 2NYB          HUNTW    1,AMXOR,BBC  
  17 2PD2          BNG   1  Z30                 [J IF NO BBC BLOCK   
  16 2PXL          LDN   7  0                   [MARK BBC FOUND  
  16 2QC=          LDX   4  A1(1)               [CHAR CT OF MSGE 
  20 2QWW          ADN   4  11                  [ROUND UP TO INTEGRAL WDS + 2 FOR
  20 2RBG          SRL   4  2                   [COMPATIBILITY WITH AOUT BLOCK   
  14 2RW6          STO   4  6                   [SAVE IT 
   8 2S*Q          BRN      Z30A 
  10 2STB    Z30   MHUNTW  1,GMON,AOUT   
  18 2T*2          STO   1  7                   [MARK AOUT BLOCK FOUND   
  18 2TSL          LDX   4  A1(1)               [NO OF WDS IN MSGE + 2   
  10 2W#=    Z30A  LDX   5  ALOGLEN(3)   
   9 2WRW          SBN   5  CIMESS-A1
  20 2X?G    Z30B  BXL   4  5,Z30C              [J IF LCB LARGE ENOUGH FOR MSGE  
   9 2XR6          ADN   4  CIMESS-A1
  17 2Y=Q          ALTLEN   3,4,AMXOR,CILCB     [ALTER LENGTH OF LCB 
  10 2YQB          MHUNTW   3,AMXOR,CILCB
  15 2_=2          BNZ   7  Z50                 [J IF NOT BBC
  16 2_PL          LDX   4  6                   [RESTORE WD CT   
  10 329=          MHUNTW   1,AMXOR,BBC  
   8 32NW          BRN      Z40  
  21 338G    Z30C  SBN   5  10                  [LCB IS LARGE ENOUGH,SO NOW SEE IF IT
  21 33N6          BNG   5  Z40                 [IS TOO LARGE. IF SO I0ERATE UNTIL   
  20 347Q          BRN      Z30B                [NOT MORE THAN 10 WDS TOO BIG.   
  10 34MB    Z50   MHUNTW  1,GMON,AOUT   
  16 3572          LDX   4  A1(1)               [RESTORE WD CT   
  18 35LL    Z40   LDN   5  A1+2(1)             [START OF MSGE IN BLOCK  
   9 366=          LDN   6  CIMESS(3)
  17 36KW          SBN   4  2                   [NO OF WDS IN MSGE   
  18 375G          BZE   4  Z20                 [J IF NO CHARS TO MOVE   
   7 37K6          SMO      4
   7 384Q          MOVE  5  0
   4 38JB    Z20 
  15 3942          BNZ   7  Z21                 [J IF NOT BBC
  17 39HL          LDX   5  A1(1)               [CHAR CT OF MSGE(BBC)
   8 3=3=          BRN      Z21A 
   4 3=GW    Z21 
  18 3?2G          LDX   5  AWORK1(2)           [CHAR CT OF MSGE (AOUT)  
  17 3?G6    Z21A  DSA   5  CIREADY(3)          [SET CHAR CT IN LCB. 
   8 3?_Q          LDCT  5  #200 
  17 3#FB          ORS   5  CIREADY(3)          [SET INTERLOCK BIT 1 
  15 3#_2          BNZ   7  Z150                [J IF NOT BBC
  16 3*DL          LDN   6  #2000               [BBC CATEGORY BIT
  13 3*Y=          OUTMON   A1(1),A1+2,6,AMXOR,BBC   
   9 3BCW          MFREEW   AMXOR,BBC
  10 3BXG          HUNTW    1,AMXOR,BBC  
  19 3CC6          BPZ   1  Z60                 [J IF ANOTHER BBC BLOCK FOUND
  18 3CWQ          HUNTW   3,GMON,AOUT          [BBC CANT BE A 'READY'.  
  18 3DBB          BNG   3  Z80B                [J IF ONLY BBC TO OUTPUT 
   9 3DW2          LDX   0  AWORK1(2)
  17 3F*L          BNG   0  Z80B                [J IF BROADCAST ONLY 
  20 3FT=          BRN      Z60                 [ELSE J TO SET B2 AND WAIT UNTIL 
  15 3G#W                                       [BUFFER FREE 
   4 3GSG    Z150
  21 3H#6          NGN   0  1                   [IN CASE WE FIND A BBC BLOCK AFTER   
  20 3HRQ          STO   0  AWORK1(2)           [WE OUTPUT THE GMON/AOUT BLOCK   
  19 3J?B          LDX   6  A1+1(1)             [ PICK  UP  CATEGORY  WORD   
   7 3JR2          SLC   6  1
  16 3K=L          BPZ   6  Z70                 [J IF NOT READY  
  10 3KQ=          LDX   6  CIREADY(3)   
   7 3L9W          SLC   6  5
  21 3LPG          BPZ   6  Z90                 [J IF NOT AWAITING BROKEN-IN MSGE.   
  19 3M96          READYON                      [SET BREAKIN MARKER IN CPA.  
  20 3MNQ          ERS   5  CIREADY(3)          [UNSET INTERLOCK BIT 1 IN CILCB  
  21 3N8B          BRN      Z90A                [LOOK FOR BBC BLOCKS BEFORE GOING UP 
   8 3NN2    Z90   LDCT  5  #400 
  16 3P7L          ORS   5  CIREADY(3)          [SET READY IN LCB
   7 3PM=          LDN   5  0
   8 3Q6W    Z80   CALL  7  WAKEY
  20 3QLG          BNZ   5  Z90A                [J IF NOT 'READY' TO MISS COOR   
   6 3R66          COOR1Y
  16 3RKQ    Z90A  HUNTW    1,AMXOR,BBC         [J TO OUTPUT BBC 
   8 3S5B          BPZ   1  Z60  
   8 3SK2          LDCT  0  #10  
  20 3T4L          ANDX  0  CLONG1(2)           [IN CASE BBC DONE WHILE WE WERE  
  14 3TJ=          ERS   0  CLONG1(2)           [IN CIOUT
   5 3W3W          UP
   7 3WHG    Z70   SLC   6  4
  20 3X36          BPZ   6  Z80                 [J IF THIS IS NOT BROKEN-IN MSGE.
  20 3XGQ          LDCT  6  #10                 [OTHERWISE CLEAR 'AWAITING' BIT. 
  10 3Y2B          ANDX  6  CIREADY(3)   
  10 3YG2          ERS   6  CIREADY(3)   
   8 3Y_L          BRN      Z80  
  10 3_F=    Z80B  MHUNTW   3,AMXOR,CILCB
   8 3_YW          BRN      Z80  
   4 42DG    #END
   8 ____ ...20071740000200000000
