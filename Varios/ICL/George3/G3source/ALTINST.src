  15 22FL    #SEG  ALTINST60                    [E WILLIAMS  
   9 22_=                   8HALTINST
  11 23DW          SEGENTRY K1ALTINST,XENT1  
   8 23YG    #OPT  K0ALTINST=0   
  12 24D6    #OPT  K6ALTINST=K6ALLGEO>K6GREATGEO 
  12 24XQ    #LIS  K0ALTINST>K0ALLGEO>K0GREATGEO 
  10 25CB    #     THE ALTINST COMMAND   
  19 25X2    #     THIS ALLOWS AN OBJECT PROGRAM LOCATION TO BE ALTERED TO   
  19 26BL    #     THE INSTRUCTION SPECIFIED AS THE SECOND COMMAND PARAMETER 
   4 26W=    #   
   4 27*W    #   
  17 27TG    MSK1           #777777             [TO LOSE TOP CHAR.   
  15 28*6    MSK2           #37777              [14-BIT MASK 
  16 28SQ    TWB            +4096               [TWELVE BITS+1   
  16 29#B    FIFB           +32768              [FIFTEEN BITS+1  
  15 29S2    TENB           +1024               [TEN BITS+1  
   9 2=?L    MIN8192        #77760000
   9 2=R=    XBIT9          #00040000
  11 2?=W    XACCESS        +GWRITE+CHNSHARED
   9 2?QG    XACCESS1       +CHWRITE 
   4 2#=6    #   
  21 2#PQ    PERTAB                             [PERIPHERAL FOR SUSSB,REL,DIS ETC.   
  13 2*9B          #00006462                    [TR  
  13 2*P2          #00016460                    [TP  
  13 2B8L          #00025460                    [LP  
  13 2BN=          #00034362                    [CR  
  13 2C7W          #00044360                    [CP  
  13 2CMG          #00055564                    [MT  
  13 2D76          #00064544                    [ED  
  13 2DLQ          #00075543                    [MC  
  13 2F6B          #00114462                    [DR  
  13 2FL2          #00125164                    [IT  
  13 2G5L          #00135570                    [MX  
  13 2GK=          #00144364                    [CT  
  13 2H4W          #00154644                    [FD  
  13 2HJG          #00244760                    [GP  
  15 2J46          #00000000                    [END OF TABLE
   4 2JHQ    #   
   4 2K3B    #   
   5 2KH2    PARGET  
   8 2L2L          SBX   6  FX1  
   7 2LG=          PARAPASS 5
  10 2L_W          MHUNT    3,CPB,CUNI   
   8 2MFG          ADX   6  FX1  
   7 2M_6          EXIT  6  0
   4 2NDQ    #   
   4 2NYB    #   
   4 2PD2    XGET
   8 2PXL          SBX   7  FX1  
  18 2QC=          LDN   5  1                   [GET ACCUMULATOR FIELD   
   9 2QWW          CALL  6  PARGET   
   9 2RBG          LDCH  5  APARA(3) 
   8 2RW6          ADX   7  FX1  
   9 2S*Q          LDX   6  ANUM(3)  
   8 2STB          ANDN  6  #7777
   9 2T*2          LDX   0  APARA(3) 
   8 2TSL          SRL   0  12   
  17 2W#=          BCT   6  Q2                  [J IF NOT JUST 1 CHAR
   8 2WRW          ANDN  0  #7000
  19 2X?G          BNZ   0  XERR5               [ERROR IF CHAR NOT IN RANGE  
  16 2XR6          EXIT  7  1                   [EXIT FOR 1 CHAR 
  19 2Y=Q    Q2    BCT   6  XERR5               [ERROR IF NOT 1 OR 2 CHARS   
   8 2YQB          ANDN  0  #7070
  19 2_=2          BNZ   0  XERR5               [ERROR IF CHARS NOT IN RANGE 
  16 2_PL          EXIT  7  0                   [EXIT FOR 2 CHARS
   4 329=    #   
   4 32NW    MOCH
   8 338G          SBX   7  FX1  
   7 33N6          LDN   5  1
   9 347Q          CALL  6  PARGET   
   8 34MB          ADX   7  FX1  
   9 3572          NGX   5  ANUM(3)  
   8 35LL          BPZ   5  XFR2 
   7 366=          NGX   5  5
   8 36KW          SRL   5  12   
   7 375G          SBN   5  3
   7 37K6          EXIT  7  1
   4 384Q    XFR2
   7 38JB          EXIT  7  0
   4 3942    #   
   4 39HL    MGET
   8 3=3=          SBX   7  FX1  
   9 3=GW          LDX   5  AWORK2(2)
   9 3?2G          ANDX  5  BITS22LS 
   7 3?G6          LDN   6  1
  15 3?_Q          PARANEXT  ,6,5               [DPLIT PARAM.
  10 3#FB          TESTREP  UNPAIR,XERR9 
   7 3#LJ ...#UNS ANSTOOMANY 
  10 3#RQ ...      TESTREP2 TOOMANY,XERRA
   7 3#_2          LDN   5  2
   9 3*DL          CALL  6  PARGET   
  15 3*Y=          LDX   5  ANUM(3)             [GET M-FIELD 
  16 3BCW          BZE   5  XERR9               [ERROR IF NULL   
   7 3BXG          LDN   6  4
   7 3CC6          BCHX  3  /
  16 3CWQ          LDCH  5  APARA(3)            [GET MOD. CHAR.  
   9 3DBB          ANDX  3  BITS22LS 
  16 3DW2          TXL   5  6                   [ILLEGAL IF M>3  
   8 3F*L          BCC      XERR9
  16 3FT=          BZE   5  XERR9               [ILLEGAL IF M=0  
   8 3G#W          SLL   5  12   
  15 3GSG          ORX   4  5                   [SET M-FIELD 
   7 3H#6          FREECORE 3
  17 3HRQ          MHUNT    3,CPB,CUNI          [GET N-FIELD BLOCK   
   8 3J?B          ADX   7  FX1  
   7 3JR2          EXIT  7  0
   4 3K=L    #   
   4 3KQ=    NGET
   8 3L9W          SBX   7  FX1  
  10 3LPG          MHUNT    3,CPB,CUNI   
   9 3M96          NGX   5  ANUM(3)  
  17 3MNQ          BPZ   5  XERR6               [ERROR IF NO PARAM.  
   8 3N8B          CALL  6  XCON 
   8 3NN2          ADX   7  FX1  
  10 3P7L          LDX   5  ACOMMUNE1(2) 
   4 3PM=    NCHL
   9 3Q6W          LDX   6  AWORK1(2)
   9 3QLG          LDX   0  AWORK2(2)
   7 3R66          SLL   0  1
  21 3RKQ          BPZ   0  NFC                 [J AS MNEMONIC PARAMETER FOR SHIFT   
  20 3S5B          ERN   6  #140                [SET 12-BIT N-RANGE AS FN. CODE  
  16 3SK2    NFC   ANDN  6  #160                [[MASK NQRANGE   
   8 3T4L          LDN   0  #40  
   7 3TJ=          TXU   0  6
  19 3W3W          BCS      N12                 [BRANCH IF NOT 12-BIT LIMIT  
   9 3WHG          TXL   5  TWB(1)   
  18 3X36          BCC      XERR7               [BRANCH IF NOT IN RANGE  
   8 3XGQ          BRN      PQR5 
   8 3Y2B    N12   LDN   0  #20  
   7 3YG2          TXU   0  6
  19 3Y_L          BCS      N15                 [BRANCH IF NOT 15-BIT LIMIT  
   9 3_F=          TXL   5  FIFB(1)  
  18 3_YW          BCC      XERR7               [BRANCH IF NOT IN RANGE  
   8 42DG          BRN      PQR5 
   9 42Y6    N15   TXL   5  TENB(1)  
  17 43CQ          BCC      XERR7               [J IF NOT IN RANGE   
   7 43XB          EXIT  7  0
   4 44C2    #   
   4 44WL    #   
   4 45B=    XCON
   8 45TW          SBX   6  FX1  
   6 46*G    #SKI  ADELO 
   9 46T6          CHNUMCO1 1,ZBRKN  
   7 47#Q    #SKI  ADELO<1$1 
   7 47SB          CHNUMCO1 1
  11 48#2          TESTREP  CHNUMERR,XERR7   
   8 48RL          ADX   6  FX1  
   7 49?=          EXIT  6  0
   4 49QW    #   
  20 4==G    XDUB           [CHECKS IF 2* LENGTH ACC FIELD LEGAL & CORRECT FORMAT
   9 4=Q6          LDX   0  AWORK1(2)
   8 4?9Q          ANDN  0  #10  
  21 4?PB          BZE   0  XERR5               [ERROR IF 2* LENGTH ACC FIELD ILLEGAL
   9 4#92          LDX   0  APARA(3) 
   8 4#NL          SRL   0  12   
   7 4*8=          SBN   0  1
   7 4*MW          ANDN  0  7
  19 4B7G          BXU   0  5,XERR5             [ERROR IF NOT 01,12 ETC OR 70
  21 4BM6          ORX   4  XBIT9(1)            [INSERT '2* LENGTH' BIT IN INSR WORD 
   7 4C6Q          EXIT  7  0
   4 4CLB    #   
   5 4D62    XENT1   
   7 4DKL          LDN   5  1
  18 4F5=          PARANUMB 7,5                 [NO. OF PARAMS. INTO X7  
   9 4FJW          ORS   7  AWORK2(2)
   7 4G4G          SMO      6
  20 4GJ6          BRN      ROUTS               [BRANCH TO APPROPRIATE ROUTINE   
  16 4H3Q    ROUTS BRN      ROUT0               [NORMAL ROUTINE  
  16 4HHB          BRN      ROUT1               [BRANCH ROUTINE  
  19 4J32          BRN      ROUT2               [DEFINED ACCUMULATOR ROUTINE 
  16 4JGL          BRN      ROUT3               [SHIFT ROUTINE   
  17 4K2=          BRN      ROUT4               [PERIPHERAL ROUTINE  
  16 4KFW          BRN      ROUT5               [SPECIAL ROUTINE 
  19 4K_G          BRN      ROUT6               [ACCUMULATOR BRANCH ROUTINE  
   4 4LF6    #   
   4 4LYQ    OUT 
   9 4MDB          MFREE    CPB,CUNI 
  16 4MY2          LDX   5  AWORK4(2)           [GET CONVERSION  
   5 4NCL    #SKI  G4
   4 4NX=    (   
   7 4PBW          LDN   3  0
   8 4PWG          PARANOT  1,3  
  10 4QB6          MHUNT    3,CPB,CUNI   
   9 4QTQ          LDCH  0  APARA(3) 
   8 4R*B          SBN   0  #60  
   8 4RT2          BNZ   0  NPURE
  10 4S#L          LDX   3  XACCESS(1)   
   8 4SS=          BRN      PURE 
   5 4T?W    NPURE   
   4 4TRG    )   
  10 4W?6          LDX   3  XACCESS1(1)  
   4 4WQQ    PURE
   7 4X=B    #SKI  ADELO<1$1 
   8 4XQ2          GETWORDX 5,6,3
   6 4Y9L    #SKI  ADELO 
   4 4YP=    (   
  10 4_8W          GETWORDX 5,6,3,,,ZBRKN
  10 4_NG          TESTREP  LOADERR,XERR2
   4 5286    )   
  10 52MQ          TESTREPNOT NOCORE,XNEX
   9 537B          ERROR    GWERNO   
   8 53M2          BRN      XUP  
  10 546L    XNEX  TESTREPNOT RESVIOL,XOK
   9 54L=          ERROR    GWERRES  
   8 555W          BRN      XWF  
   7 55KG    XOK   SMO      6
  15 5656          STO   4  0                   [CHANGE WORD 
  17 56JQ    XWF   WORDFIN                      [TIDY UP OBJ. PROG.  
   6 574B    XUP   ENDCOM
   4 57J2    #   
   4 583L    #   
  15 58H=    #     ERROR IF NUMBER FORMAT ERROR OR RES. VIOC.
   7 592W    XERR8 WORDFIN   
   6 59GG    XERR2 ENDCOM
   4 5=26    #   
  14 5=_B    #     ERROR FOR INVALID ACCUMULATOR FIELD   
   5 5?F2    XERR5   
   9 5?YL          COMERR   JINVACC  
  11 5#D=    #     INSTRUCTION FORMAT ERROR  
   5 5#XW    XERR6   
   9 5*CG          COMERR   JINSFERR 
  13 5*X6    #     ERROR FOR INVALID OPERAND FIELDS  
   5 5BBQ    XERR7   
   9 5BWB          COMERR   JINVOP   
  13 5CB2    #     ERROR FOR INVALID MODIFIER FIELD  
   5 5CTL    XERR9   
   9 5D*=          COMERR   JINVMOD  
   7 5DCT ...#UNS ANSTOOMANY 
   4 5DGD ...(   
   5 5DK3 ...XERRA   
   9 5DML ...      COMERR   JMAXPAR  
   4 5DQ9 ...)   
   6 5DSW    #SKI  ADELO 
   7 5F#G ...ZBRKN COMBRKIN  
   4 5FS6    #   
   4 5G?Q    #   
   5 5GRB    ROUT0   
   7 5H?2          SBN   7  4
  17 5HQL          BPZ   7  XERR6               [ERROR IF 4 PARAMS.  
   7 5J==          ADN   7  4
   9 5JPW          LDX   6  AWORK1(2)
   7 5K9G          ANDN  6  5
   7 5KP6          SBN   6  5
  17 5L8Q          BZE   6  PQR1                [J IF 3 FIELDS REQD. 
   7 5LNB          SBN   7  3
  17 5M82          BZE   7  XERR6               [J IF THREE FIELDS   
   7 5MML          ADN   6  4
  17 5N7=          BNZ   6  PQR8                [J IF OMLY N-FIELD   
  19 5NLW          ADN   7  4                   [DROP THROUGH AS ONLY X-FIELD
   7 5P6G    PQR1  SBN   7  3
  17 5PL6          BNZ   7  XERR6               [J IF ONLY 2 FIELDS  
   8 5Q5Q          CALL  7  XGET 
  19 5QKB          CALL  7  XDUB                [2* LENGTH OK FOR NORM INSTR 
  16 5R52    PQR2  SRC   5  3                   [PLACE X-FIELD   
  16 5RJL          ORX   4  5                   [PLACE X-FIELD   
   7 5S4=    PQR6  FREECORE 3
   8 5SHW    PQR8  CALL  7  MOCH 
   8 5T3G          BRN      PQR3 
   7 5TH6          ADN   5  1
  20 5W2Q          BZE   5  XERR7               [ERROR IF M-FIELD WITHOUT N-FIELD
  17 5WGB          BNG   5  PQR4                [BRANCH IF NO M-FIELD
   9 5X22    PQR7  LDX   7  AWORK1(2)
   7 5XFL          ANDN  7  2
  18 5X_=          BZE   7  XERR6               [J IF NO M-FIELD ALLOWED 
   8 5YDW          CALL  7  MGET 
   9 5YYG    PQR3  LDX   2  AWORK1(2)
   7 5_D6          ANDN  2  4
  18 5_XQ          BNZ   2  PQR4                [J IF N-FIELD NECESSARY  
   7 62CB          STOZ     5
  17 62X2          BRN      PQR5                [BRANCH AS NO N-FIELD
   8 63BL    PQR4  CALL  7  NGET 
   9 63W=    PQR5  MFREE    CPB,CUNI 
  15 64*W          ORX   4  5                   [SET N-FIELD 
  17 64TG          BRN      OUT                 [EXIT TO MAIN ROUTINE
   4 65*6    #   
   5 65SQ    ROUT1   
   7 66#B          SBN   7  3
  18 66S2          BNZ   7  XERR6               [ERROR IF NOT 3 FIELDS   
   8 67?L          CALL  7  XGET 
  17 67R=          BRN      XERR5               [ERROR IF >1 CHAR.   
   7 68=W          SRC   5  3
  15 68QG          ORX   4  5                   [SET X-FIELD 
   7 69=6    RST6  FREECORE 3
   8 69PQ    RST4  CALL  7  MOCH 
   8 6=9B          BRN      XERR6
   8 6=P2          BZE   5  XERR6
   7 6?8L          ADN   5  1
  17 6?N=          BNZ   5  RST1                [J AS NOT REPLACED   
   9 6#7W    RST5  LDX   0  AWORK1(2)
   8 6#MG          ANDN  0  #200 
  21 6*76          BZE   0  XERR7               [ERROR IF REPLACED BRANCH NOT ALLOWED
  21 6*LQ          CALL  6  XCON                [REPLACED JUMP OPERAND STILL VALID   
  21 6B6B                                       [ FOR CHNUMCO1 THOUGH ENCLOSED IN () 
  10 6BL2          LDX   5  ACOMMUNE1(2) 
   7 6C5L          LDCT  6  1
   7 6CK=          SRL   6  1
  16 6D4W          ORX   5  6                   [SET REPLACED BIT
   8 6DJG          BRN      RST3 
   9 6F46    RST1  LDCH  7  APARA(3) 
   8 6FHQ          SBN   7  #32  
  19 6G3B          BNZ   7  RST2                [IF 1ST CHAR NOT A *,BRANCH  
  19 6GH2          LDX   0  MSK1(1)             [ OTHERWISE OVERWRITE BY 0   
   9 6H2L          ANDS  0  APARA(3) 
  16 6HG=    RST2  CALL  6  XCON                [CONVERTED NUMBER
  14 6H_W          LDX   5  ACOMMUNE1(2)        [ TO X5  
   9 6JFG          MFREE    CPB,CUNI 
  19 6J_6          BNZ   7  RST10               [IF 1ST CHAR NOT A *,BRANCH  
  21 6KDQ          ADX   5  AWORK4(2)           [ OTHERWISE MAKE RELATIVE BRANCH ABS.
   8 6KYB    RST10 BPZ   5  RST3 
  20 6LD2          BXL   5  MIN8192(1),XERR7    [REL BRN OF OVER -8192 ILLEGAL   
   9 6LXL          ANDX  5  MSK2(1)  
   8 6MC=    RST3  CALL  7  NCHL 
  13 6MWW          BRN      OUT                 [EXIT
   4 6NBG    #   
   5 6NW6    ROUT2   
  16 6P*Q          LDN   0  PQR8                [SET EXIT ADDRESS
   9 6PTB    OPQ2  LDX   6  AWORK2(2)
  17 6Q*2          STO   0  AWORK3(2)           [[STORE EXIT POINT   
  18 6QSL          BPZ   6  XMN                 [J IF MNEMONIC PARAMETER 
  19 6R#=          CALL  7  XGET                [OTHERWISE ASSUME FN. CODE   
  18 6RRW          BRN      XERR5               [J IF ILLEGAL ACCUMULATOR
   7 6S?G          SRC   5  3
  18 6SR6          ORX   4  5                   [SET ACCUMULATOR FIELD   
   7 6T=Q          FREECORE 3
   8 6TQB          BRN      OPQ1 
   7 6W=2    XMN   SBN   7  2
  20 6WPL          BNZ   7  XERR6               [ERROR IF F,X, AND N SPECIFIED   
   9 6X9=          LDX   6  AWORK1(2)
  18 6XNW          LDCT  7  #700                [MASK ACCUMULATOR FIELD  
   7 6Y8G          ANDS  7  6
  15 6YN6          ORX   4  6                   [SET XQFIELD 
  16 6_7Q    OPQ1  LDX   7  AWORK3(2)           [GET EXIT ADDRESS
   8 6_MB          ADX   7  FX1  
   7 7272          EXIT  7  0
   4 72LL    #   
   5 736=    ROUT3   
   7 73KW          SBN   7  3
  21 745G          BNZ   7  XERR6               [ERROR IF NOT X,F,N FIELDS PRESENT   
   8 74K6          LDX   2  FX2  
   9 754Q          LDX   7  AWORK2(2)
  18 75JB          BNG   7  UVW4                [J IF FN. CODE PARAMETER 
  16 7642          LDX   6  AWORK1(2)           [GET BIT-PATTERN 
   8 76HL          SRL   6  11   
  18 773=          ANDN  6  #6000               [MASK OUT UNWANTED BITS  
  20 77GW          ORX   4  6                   [SET IN SHIFT DEFINITION FIELD   
   8 782G    UVW4  CALL  7  XGET 
  19 78G6          CALL  7  XDUB                [CHECK FOR 2* LENGTH SHIFT   
  16 78_Q          SRC   5  3                   [SHIFT X-FIELD   
  15 79FB          ORX   4  5                   [SET IN WORD 
   9 79_2          LDX   6  AWORK2(2)
  19 7=DL          BPZ   6  UVW3                [J IF NOT FN. CODE PARAMETER 
   8 7=Y=          LDCT  6  #200 
  20 7?CW          ORS   6  AWORK2(2)           [SET BIT TO SHOW FN. CODE SHIFT  
  16 7?XG    UVW3  BRN      PQR6                [J TO SET N-FIELD
   4 7#C6    #   
   5 7#WQ    ROUT4   
   7 7*BB          LDX   0  7
   7 7*W2          SBN   0  3
   8 7B*L          BZE   0  ROUT0
   7 7BT=          LDN   5  1
  16 7C#W          CALL  6  PARGET              [GET FIRST PARAM 
   9 7CSG          LDCH  6  APARA(3) 
   8 7D#6          SBN   6  #41  
  19 7DRQ          BNG   6  XERR6               [ERROR IF NOT PERIPH. TYPE   
   9 7F?B          LDX   5  APARA(3) 
  15 7FR2          SRL   5  12                  [TYPE IN X5  
  16 7G=L          LDN   1  PERTAB(1)           [START OF TABLE  
   8 7GQ=    XYZ4  LDX   6  0(1) 
  18 7H9W          BZE   6  XERR6               [ERROR IF END OF TABLE   
  15 7HPG          ANDN  6  #7777               [TYPE IN X6  
   7 7J96          SBX   6  5
  16 7JNQ          BZE   6  XYZ3                [J IF TYPE FOUND 
   7 7K8B          ADN   1  1
  13 7KN2          BRN      XYZ4                [LOOP
  16 7L7L    XYZ3  LDX   5  0(1)                [GET PERIPH TYPE 
   8 7LM=          SRL   5  12   
  15 7M6W          ORX   4  5                   [SET IN X4   
   9 7MLG          LDX   6  ANUM(3)  
   8 7N66          ANDN  6  #77  
   7 7NKQ          SBN   6  3
  18 7P5B          BNZ   6  XERR6               [ERROR IF NOT 3 CHARS.   
  15 7PK2          LDX   6  APARA(3)            [GET PARAM.  
   7 7Q4L          SRL   6  6
   8 7QJ=          ANDN  6  #77  
   7 7R3W          SBN   6  8
   8 7RHG          BPZ   6  XERR7
   7 7S36          ADN   6  8
  17 7SGQ          SRC   6  3                   [POSITION UNIT NO.   
  16 7T2B          ORX   4  6                   [SET UNIT NUMBER 
   7 7TG2          FREECORE 3
  13 7T_L          BRN      OUT                 [EXIT
   4 7WF=    #   
   5 7WYW    ROUT5   
   7 7XDG          SBN   7  1
  14 7XY6          BZE   7  SINP                [J IF ONE
   7 7YCQ          SBN   7  1
  14 7YXB          BZE   7  TWOP                [J IF TWO
   7 7_C2          SBN   7  1
  14 7_WL          BNZ   7  XERR6               [J IF >3 
  15 82B=          CALL  7  XGET                [GET X-FIELD 
   8 82TW ...      BRN      XERR5
   7 83*G          LDX   6  4
   8 83T6          SRC   6  12   
  16 84#Q          LDN   7  PQR6                [SET EXIT LOCN.  
   8 84SB          SBN   6  #714 
  16 85#2          BNZ   6  VOK1                [J IF NOT AUTO   
   7 85RL    XAUT  SBN   5  4
   8 86?=          BNG   5  VOK1 
  15 86QW          BRN      XERR5               [ERROR IF X>4
   9 87=G    VOK1  LDCH  5  APARA(3) 
   7 87Q6          SRC   5  3
   7 889Q          ORX   4  5
   8 88PB          ADX   7  FX1  
  17 8992          EXIT  7  0                   [J TO NEXT ROUTINE   
   4 89NL    SINP
   7 8=8=          LDX   6  4
   8 8=MW          SRC   6  15   
   8 8?7G          SBN   6  #57  
  17 8?M6          BNZ   6  XERR6               [ERROR IF NOT LFPZ   
   8 8#6Q          LDCT  6  #100 
   7 8#LB          ORX   4  6
   8 8*62          BRN      PQR8 
   4 8*KL    TWOP
   7 8B5=          LDX   6  4
   8 8BJW          SRC   6  12   
   8 8C4G          SBN   6  #474 
   8 8CJ6          BZE   6  VOX  
   8 8D3Q          SBN   6  #100 
  18 8DHB          BNZ   6  XERR6               [ERROR IF NOT SMO OR SFPZ
   8 8F32    VOX   BRN      PQR8 
   4 8FGL    #   
   4 8G2=    #   
   5 8GFW    ROUT6   
  16 8G_G          LDN   0  RST4                [SET ECIT POINT  
   8 8HF6          BRN      OPQ2 
   4 8HYQ    #   
   4 8JDB    #   
  11 8JY2 ...      MENDAREA 30,K100ALTINST   
   4 8KX=    #END
   8 ____ ...12321524000400000000
