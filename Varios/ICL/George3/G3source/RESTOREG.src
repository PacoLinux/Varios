  15 22FL ...      SEG   RESTOREG,864,PETER CAMPBELL,RESTORE 
   4 22_=    #   
  12 23DW          SEGENTRY K1RESTOREG,RESTOREG  
  12 23L4 ...      SEGENTRY K2RESTOREG,RESTOREG2 
   4 23YG    #   
  21 24D6    #   THE PURPOSE OF THIS SEGMENT IS TO RESTORE THE FILESTORE COMPLETELY  
  12 24XQ    #   FROM AN INCREMENTAL DUMP TAPE.  
  18 25CB    #     ENTRY    ENTERED IN EARLY MORNING START FROM RESTOREA 
  13 25X2    #              A TAPE HAS BEEN ENGAGED  
  19 26BL    #     METHOD   SEGMENT USES RESTLIMB AND RESTMERG AND RESTEND   
  21 26W=    #              IT FIRST CHECKS THAT THE CORRECT INCREMENT DUMP TAPE HAS 
  10 27*W    #              BEEN ENGAGED 
  13 27TG    #              IT SETS UP FILE BLOCKS   
  15 28*6    #                  FWB USED BY RESTOREG,RESTLIMB
  10 28SQ    #                  FLOCNB   
  16 29#B    #                  FAPB USED BY RESTLIMB,RESTMERG   
  14 29S2    #                  FRESTOR USED BY RESTMERG 
  20 2=?L    #           RESTLIMB IS USED TO RESTORE ALL FILES UNDER MASTER AND  
  21 2=R=    #              LEAVES SKELETON DIRECTORIES FOR DICTIONARY,MANAGER AND   
  10 2?=W    #              SYSTEM FILES 
  19 2?QG    #           RESTMERG IS USED TO MERGE SKELETON ENTRIES SETUP BY 
  18 2#=6    #              RESTLIMB WITH THE FILE OF MASTER ON THE TAPE.
  19 2#PQ    #              THE SEGMENT THEN CREATES MASTERS DIRECTORY ENTRY 
  21 2*9B    #           RESTEND IS USED TO INSERT THE TAPE SERIAL NUMBER INTO FILES 
  14 2*P2    #              INCINDEX,SERIAL AND DUMPER.  
  18 2B8L    #     EXIT     UP      NORMAL RETURN WHEN RESTORE COMPLETED 
   4 2BN=    #   
  11 2CMG    SFS            12HGEORGE3DUMP   
   9 2D76    MAST           4HMAST   
  20 2DLQ    SLINK                              [ENTRY LINK FOR REWIND ACTIVITY  
   9 2F6B          PAIR     LIBLOAD,1
  11 2LG=    TDUMP          12HGEO3DUMPTAPE  
   9 2L_W    XTHOU          +1000000 
  21 2MFG ...#     ******************************************************************
  15 2M_6 ...#                                  SFRESTUSER2  
  15 2NDQ ...#                                  ***********  
   8 2NYB ...#     ( LINK IN X1 )
  16 2PD2 ...#     SUBROUTINE TO GET POINTER TO FRESTURER IN X2  
   6 2PXL ...SFRESTUSER2 
  20 2QC= ...      HUNTMISB   2,FI,FRESTUSER    [ SEARCH MISC CHAIN FOR FRESTUSER
  17 2QWW ...      BNG   2  (GEOERR)            [ BREAK IF NOT THERE 
  14 2RBG ...      EXIT  1  0                   [AND BACK
   4 2RW6 ...#   
  21 2S*Q ...#     ******************************************************************
  15 2STB ...#                                  SFRESTUSER3  
  15 2T*2 ...#                                  ***********  
   8 2TSL ...#     (LINK IN X1 ) 
  16 2W#= ...#     SUBROUTINE TO GET POINTER TO FRESTUSER IN X3  
   4 2WRW ...#   
   6 2X?G ...SFRESTUSER3 
  12 2XR6 ...      HUNTMISB   3,FI,FRESTUSER    [
  12 2Y=Q ...      BNG   3  (GEOERR)            [
  15 2YQB ...      EXIT  1  0                   [ AND BACK   
   4 2_=2 ...#   
  21 2_PL ...#     ******************************************************************
   3 329= ...
  21 32NW ...#     ******************************************************************
   4 338G ...#   
  14 33N6 ...#                                  NEXTINC  
  14 347Q ...#                                  *******  
   9 34MB ...#     USES X6 AS LINK   
   4 3572 ...#   
  18 35LL ...#     THIS SUBROUTINE READS DOWN THE TAPE UNTIL IT FINDS THE
  13 366= ...#     NEXT START OF INCREMENT SENTINEL  
  14 36KW ...#     IT LEAVES THE INCREMENT NUMBER IN X5  
  21 375G ...#     ******************************************************************
   5 37K6 ...NEXTINC 
  12 384Q ...      SBX   6  FX1                 [
   5 38JB ...NEXTINC2
  17 3942 ...      CALL  5  TM                  [ SKIP TO TAPE MARK  
  16 39HL ...      CALL  5  READP               [ READ SENTINEL  
  12 3=3= ...      LDX   0  ESENTYP(3)          [
  12 3=GW ...      SBN   0  7                   [
  17 3?2G ...      BZE   0  NOTINC              [ JUMP IF END OF TAPE
  12 3?G6 ...      ADN   0  1                   [
  19 3?_Q ...      BNZ   0  NEXTINC2            [ JUMP UNLESS START SENTINEL 
  15 3#FB ...      TESTNAMX   3,SFS(1),ESENLOC1(3),NEXTINC2  
  18 3#_2 ...                                   [ JUMP IF NOT GEORGE3DUMP
  21 3*DL ...      BBUS     1,ESENB(3),NEXTINC2   [ JUMP IF NOT START OF INCREMENT   
  18 3*Y= ...      LDX   5  ESENFG(3)           [ X5 IS INCREMENT NUMBER 
  12 3BCW ...      ADX   6  FX1                 [
  15 3BXG ...      EXIT  6  0                   [ AND AWAY   
   4 3CC6 ...#   
  21 3CWQ ...[***********************************************************************
  14 3DBB ...#                                  READHDDR 
  14 3DW2 ...#                                  ******** 
   7 3F*L ...#     LINK = X5 
   9 3FT= ...#     LEAVES TSN IN X6  
  19 3G#W ...#     THIS SUBROUTINE REWINDS THE TAPE,READS THE HEADER LABEL   
  20 3GSG ...#     CHECKS THAT THE NAME IS GEO3DUMPTAPE AND THAT TSN IS NON-ZERO 
   4 3H#6 ...#   
  13 3HRQ ...#     X6 WILL CONTAIN THE TSN ON EXOT   
   5 3J?B ...READHDDR
  12 3JR2 ...      SBX   5  FX1                 [
  12 3K=L ...      LDN   0  7                   [
  16 3KQ= ...      DCA      ,STO,0,MODE         [REWIND THE TAPE 
  12 3L9W ...      CALL  4  PERI                [
  17 3LPG ...      LDN   6  0                   [ NO TAPE READ YET   
  12 3M96 ...      SETNCORE   20,3,FILE,FINTER6  
  18 3MNQ ...      STOZ     A1(3)               [ SET UP EMPTY FINTER6   
  17 3N8B ...      SETNCORE 20,3,FILE,FWB       [SET UP AND LOCK FWB 
  12 3NN2 ...      LOCKC    3,XLOCK             [
  10 3P7L ...      MHUNTW   3,FILE,FWB   
  16 3PM= ...      LDN   0  A1(3)               [ START ADDRESS  
  18 3Q6W ...      DCA      ,STO,0,CTSA         [ SET DESTINATION ADDRESS
  17 3QLG ...      LDN   0  20                  [ LENGTH OF TRANSFER 
  12 3R66 ...      DCA      ,STO,0,COUNT        [
  15 3RKQ ...      DCA      ,STOZ,,MODE         [ READ MODE  
  16 3S5B ...      CALL  4  PERI                [ FORE OF REPI   
  15 3SK2 ...      UNLOCK   3                   [UNLOCK FWB  
  12 3T4L ...      LDCT  0  #440                [
  17 3TJ= ...      ANDX  0  CPRW1(2)            [ CHECK REPLY WORD   
  19 3W3W ...      BNZ   0  XER19               [ JUMP IF BIT 0 OR BIT 3 SET 
  15 3WHG ...      LDX   6  A1+1(3)             [ PICK UP TSN
  14 3X36 ...      TESTNAMX   3,TDUMP(1),A1+2(3),NOTDTAPE
  19 3XGQ ...                                   [ JUMP IF NOT GEO3DUMPTAPE   
  17 3Y2B ...      BZE   6  XNOTSN              [ JUMP IF ZORO TSN   
   8 3YG2 ...      ADX   5  FX1  
  15 3Y_L ...      EXIT  5  0                   [ AND AWAY   
  21 3_F= ...#     ******************************************************************
  14 3_YW ...#                                  XNOTSN   
  14 42DG ...#                                  ******   
  20 42Y6 ...#     THIS SECTION IS ENTERED IN THE UNLIKELY EVENT OF A TAPE WITH  
  11 43CQ ...#     A ZERO TSN BEING LOADED   
   4 43XB ...#   
   5 44C2 ...XNOTSN  
  17 44WL ...      MONOUT   GRZEROTSN           [ TAPE HAS A ZERO TSN
   8 45B= ...      BRN      XER30
   4 45TW ...#   
  21 46*G ...#     ******************************************************************
  14 46T6 ...#                                  NOTDTAPE 
  14 47#Q ...#                                  ******** 
  21 47SB ...#     THIS SECTION IS ENTERED IF THE LOADED TAPE IS NOT CALLED ^GEO3DUMP
   5 48#2 ...NOTDTAPE
  12 48RL ...      OUTPACK  6,1,TSN             [
  18 49?= ...      MONOUT   GRNOTDT             [%A IS NOT A DUMP TAPE   
   8 49QW ...      BRN      XER30
   4 4==G ...#   
  21 4=Q6 ...#     ******************************************************************
  14 4?9Q ...#                                  NOTINC   
  14 4?PB ...#                                  ******   
  21 4#92 ...#     THIS SECTION IS ENTERED WHEN THE REQUIRED INC IS NOT ON THE TAPE L
   5 4#NL ...NOTINC  
   4 4*8= ...#   
   6 4*?B ...#UNS AMTGR  
  11 4*BG ...      JBC     RONGTAPE,,BGENRES 
   6 4*FL ...#UNS AMTGR  
   4 4*JQ ...#SKI
  12 4*MW ...      BWZ      JINCLAST,RONGTAPE2   
  11 4B7G ...#     JUMP IF PARTIAL RESTORE   
  11 4BM6 ...      OUTPACK  JINCLAST,1,INCNUM
  10 4C6Q ...      OUTPACK  GINCTSN,1,TSN
  19 4CLB ...      MONOUT   GRWRONGDT           [ INCREMENT %A IS NOT ON %B  
   8 4D62 ...      BRN      XER30
   4 4DKL ...#   
  21 4F5= ...#     ******************************************************************
  14 4FJW ...#                                  NOTSUIT  
  14 4G4G ...#                                  *******  
  20 4GJ6 ...#     THIS SECTION IS ENTERED IF AN INCREMENT IS UNSUITABLE FOR A   
  18 4H3Q ...#     GENERAL RESTORE. - B0 OF WORD 19 IS SET IF IT SUITABLE
   4 4HHB ...#   
   5 4J32 ...NOTSUIT 
  12 4JGL ...      OUTPACK  GINCTSN,1,TSN       [
  21 4K2= ...      MONOUT   GRNOTSUIT           [ MT %A IS NOT SUITABLE FOR A GENERAL
   8 4KFW ...      BRN      XER30
   3 4K_G ...
   4 4LF6 ...#   
  21 4LYQ ...#     ******************************************************************
  13 4MDB ...#                                  PERI 
  13 4MY2 ...#                                  **** 
  15 4NCL ...#     THIS SUBROUTINE LOCKS THE ACTIVITY BLOCK  
  21 4NX= ...#     DOES A GPERI , COORDINATES UNLOCKS THE ACTIVITY BLOCK AND EXITS   
  13 4PBW ...#     IT USES X4 AS THE LINK ACCUMULATOT
   4 4PWG ...#   
   4 4QB6 ...PERI
  12 4QTQ ...      SBX   4  FX1                 [
  17 4R*B ...      LOCKC    2,/                 [LOCK THE ACTIVITY   
  17 4RT2 ...      GPERI    ,2                  [ FIRE OFF THE PERI  
  12 4S#L ...      COOR1                        [
  16 4SS= ...      UNLOCK   2                   [ UNLOCK IT AGAIN
  12 4T?W ...      ADX   4  FX1                 [
  12 4TRG ...      EXIT  4  0                   [
   4 4W?6 ...#   
  21 4WQQ ...#     ******************************************************************
  14 4X=B ...#                                  SFINTER  
  14 4XQ2 ...#                                  *******  
  15 4Y9L ...#     SUBROUTINE FOR USE BY ALTLENG ( LINK X1 ) 
   5 4YP= ...SFINTER 
  12 4_8W ...      MHUNTW   2,FILE,FINTER       [
  12 4_NG ...      EXIT  1  0                   [
   4 5286 ...#   
  21 52MQ ...#     ******************************************************************
   4 5H?L ...#   
  19 5H?N ...#                                  RESTOREG   (GENERAL RESTORE) 
  14 5H?Q ...#                                  ******** 
   4 5H?S ...#   
  19 5H?W ...#     THE FOLLOWING SECTION OF GENERAL RESTORE READS THE HEADDER
  18 5H?Y ...#     OF THE TAPE JUST LOADED ANDS WINDS DOWN UNTIL IT FINDS
  10 5H#2 ...#     THE REQUIRED INCREMENT
   4 5H#4 ...#   
   4 5H#6 ...#   
   5 5H#8 ...RESTOREG
  21 5H#= ...      CALL  5  READHDDR            [ REWIND AND READ THE HEADER LABEL   
  21 5H## ...      STO   6  GINCTSN             [ STORE THE TSN OF TAPE BEING USED   
  15 5H#B ...      OUTPACK   6,1,TSN            [OUTPUT TSN  
  18 5H#D ...      MONOUT   GRTAPENGD           [ DUMP TAPE %A IS ENGAGED
  20 5H#G ...      SETNCORE   0,3,FILE,FINTER   [ BLOCK FOR INCREMENTS ON TAPE   
  15 5H#J ...      SETNCORE   AMAGS-A1FINT7,3,FILE,FINTER7   
  19 5H#L ...                                   [ BLOCK FOR ODD INFORMATION  
  15 5H#N ...#     A FEW LINES OF CODE FOR PARALLEL DUMPER   
  15 5H#Q ...      STOZ     GRPINCSIZE(3)       [ CLEAR SIZE 
  21 5H#S ...      STOZ     GRPINCFILES(3)      [ AND NUMBER OF FILES IN PARALLEL INC
  12 5H#W ...      LDX   0  JINCLAST            [
  18 5H#Y ...      SBN   0  1                   [ FINAL INCREMENT MUST BE
  17 5H*2 ...                                   [ > = JINCLAST - 1   
  12 5H*4 ...      STO   0  GRPINCNUM(3)        [
  15 5H*6 ...#     END OF FEW LINES OF PARALLEL DUMPER CODE  
  19 5H*8 ...      STO   6  CURRTSN(3)          [ STORE TSN OF RESTORED TAPR 
  12 5H*= ...      MHUNTW   2,FILE,FWB          [
  16 5H*# ...      LDX   0 A1+5(2)              [ HEADER LABEL   
  17 5H*B ...      STO   0  BMEREEL(3)          [ STORE REEL NUMBER  
  17 5H*D ...      LDX   0  A1+6(2)             [ STORE GEN NUMBER   
   9 5H*G ...      STO   0  BMEGEN(3)
  21 5H*J ...#     ******************************************************************
  20 5H*L ...#     WE NOW READ DOWN THW TAPE UNTIL WE FIND THE REQUIRED INCREMENT
   5 5H*N ...NEXTINCR
   9 5H*Q ...      CALL  6  NEXTINC  
  12 5H*S ...      MHUNTW   3,FILE,FINTER       [
  18 5H*W ...      LDX   4  ALOGLEN(3)          [ LENGTH BLOCK BY 1 WORD 
  12 5H*Y ...      ADN   4  1                   [
  12 5HB2 ...      ALTLENG  3,4,SFINTER         [
  16 5HB4 ...      CALL  1  SFINTER             [ RE-HUNT FINTER 
  12 5HB6 ...      SMO      4                   [
  18 5HB8 ...      STO   5  FIRSTINC-1(2)       [ ADD INCREMENT TO LIST  
  20 5HB= ...      BXL   5  JINCLAST,NEXTINCR   [ JUMP IF NOT YET REACHED INCR   
  18 5HB# ...      BXU   5  JINCLAST,NOTINC     [JUMP IF PAST INCREMENT  
   3 5HBB ...
  21 5HBD ...#     ******************************************************************
  15 5HBG ...#     WE HAVE NOW FOUND THE CORRECT INCREMENT   
  20 5HBJ ...[     CLEAR :MASTERS DIRENT IN CASE OF TAPEFAILS IN GENERAL RESTORE 
  12 5HBL ...      SETNCORE GSBS,3,BSTB,BWRITE   
   9 5HBN ...      LDN   0  BTHREE   
   9 5HBQ ...      STO   0  BACK1(3) 
   9 5HBS ...      LDN   0  ADMASTDIR
   9 5HBW ...      STO   0  BACK2(3) 
   8 5HBY ...      STOZ     A1(3)
   8 5HC2 ...      LDN   1  A1(3)
   9 5HC4 ...      LDN   2  A1+1(3)  
   9 5HC6 ...      MOVE  1  GSBS-1   
   9 5HC8 ...      LDX   0  GVERSION 
  10 5HC= ...      STO   0  EVERSION(3)  
   7 5HC# ...      LDCT  0  1
   9 5HCB ...      STO   0  EINF1(3) 
  10 5HCD ...      BACKWRITE  FREE,BSBS  
  18 5HCG ...      CLEARFAIL                    [CLEAR FAILED BLOCKS LIST
   8 5HCJ ...      LDX   3  BFILE
   9 5HCL ...      LDN   0  FBLKS-A1 
  10 5HCN ...      STO   0  ALOGLEN(3)   
   9 5HCQ ...      STO   0  FUSEBL(3)
   9 5HCS ...      STO   0  FBLMOD(3)
   9 5HCW ...      STOZ     BSPRE(3) 
   6 5HCX ...#UNS AMTGR  
   4 5HCY ...(   
  11 5HC_ ...      JBCC XLEAVBSGR,,BFREEBS   
   9 5HD2 ...       DOWN BACKROOM,11 
   6 5HD3 ...XLEAVBSGR   
   4 5HD4 ...)   
   6 5HD5 ...#UNS AMTGR  
   4 5HD6 ...#SKI
   9 5HD7 ...      DOWN  BACKROOM,11 
   3 5HD8 ...
  14 5HD9 ...#     NOW PLUNGE INTO GENERAL RESTORE ITSELF
  17 5HD= ...#   THE TAPE IS NOW POSITIONED BEFORE THE SFS FOR MASTER
  20 5HD? ...#   'OPEN MASTER BY ALTERING LENGTH OF FCB AND SETTING UP IN CORRECT
  14 5HD# ...#    FORMAT AND SETTING UP FILE CONTROL AREA
  10 5HD* ...      MHUNTW   3,FILE,FWB   
  17 5HDB ...      LDX   7  ESEN10(3)            [ KEEP GMT OF DUMP  
  20 5HDD ...      LDX   0  ESENRL(3)            [ KEEP FILESTORE MARK VERSION   
   8 5HDG ...      LDX   2  FX2  
   9 5HDJ ...      STO   0  AWORK3(2)
  11 5HDL ...      ALTLEN   3,BSBS,FILE,FWB  
  21 5HDN ...      SETNCORE GSBS,3,FILE,FAPB    [SET UP BLOCKS ONE OF WHICH WILL BE  
  17 5HDQ ...      SETNCORE GSBS,3,FILE,FRESTOR [NEEDED THROUGHOUT   
  11 5HDS ...      SETNCORE 6,3,FILE,FLOCNB  
  15 5HDW ...      CALL  5  TM                  [SKIP PAST TM
  14 5HDY ...      CALL  5  READP               [READ QB 
  21 5HF2 ...#  ASSUME :MASTER MAY BE ALLOCATED TO A SPECIAL RESIDENCE . UP TO & INCL
  21 5HF4 ...#  MARK 7 THIS IS NOT POSSIBLE(INDEED :MASTER IS PUT ON RESIDENCE 3).   
   9 5HF6 ...      LDX   6  ESENAL(3)
   8 5HF8 ...      ANDN  6  #77  
   7 5HF= ...      SBN   6  3
  18 5HF# ...      BPZ   6  YAL                 [J IF :MASTER ALLOCATED  
  18 5HFB ...Y3    LDN   6  BTHREE              [PUT IT ON RESIDENCE 3   
   4 5HFD ...YGET
  21 5HFG ...      GETBACK  6                   [GET 1 BLOCK ON REQUIRED RESIDENCE   
   5 5HFJP...#SKI IFS
   7 5HFLP...      MASFCB   3
   6 5HFNP...#SKI IFS<1$1
  17 5HFQ ...      LDX   3  BFILE               [X3-> MASTER'_ FCB   
  10 5HFS ...      LDN   4  FBLKS-A1+1   
  21 5HFW ...      ALTLENG  3,4,XALT            [ALTER LENGTH OF MASTER'S FCB TO CONT
   5 5HFYP...#SKI IFS
   7 5HG2P...      MASFCB   3
   6 5HG4P...#SKI IFS<1$1
  19 5HG6 ...      LDX   3  BFILE               [                   1 BLOCK  
   9 5HG8 ...      LDN   0  FBLKS-A1 
  18 5HG= ...      STO   0  FBLMOD(3)           [SET UP   BLOCK POINTERS 
   9 5HG# ...      STO   4  FUSEBL(3)
  18 5HGB ...      STO   6  BSPRE(3)            [      & RESIDENCE NO.   
  10 5HGD ...      LDX   0  ACOMMUNE7(2) 
  17 5HGG ...      STO   0  FBLKS(3)            [      & BLOCK NO.   
   8 5HGJ ...      LDCT  0  #400 
  17 5HGL ...      STO   0  CTOPEN(3)           [OPEN IN GENERAL MODE
  15 5HGN ...      STO   0  CMOD(3)             [CMOD -IVE   
  11 5HGQ ...      MBSXD    0,BFDIR,BFHALF   
  20 5HGS ...      STO   0  COMM(3)               [ SET APPROPRIATE BITS IN COMM 
  16 5HGW ...      STOZ     FWAITCOUNT(3)       [NO ONE WAITING  
   9 5HGY ...      LDN   0  FILESIZE 
  16 5HH2 ...      STO   0  FSIZE(3)            [MAX. FILE SIZE  
  13 5HH4 ...      STO   7  FGMTBS(3)           [GMT 
  20 5HH6 ...      STO   7  FLASTJAM(3)         [RESET GMT OF LAST JAM TO TIME OF
   5 5HH8P...#SKI IFS
   4 5HH=P...(   
  20 5HH#P...      LDX   1  FPTR(3)             [ X1 - MASTER'S  FSTACK SET UP BY
  14 5HHBP...[                                  BACKMAP  
  17 5HHDP...      SETFCAGR  GENERAL,1          [ RING IN NEW ELEMENT
   4 5HHGP...)   
   6 5HHJP...#SKI IFS<1$1
  19 5HHL ...      RESTOREN GENERAL             [SET UP & RING FSTACK BLOCK  
  19 5HHN ...      STOZ     AWORK1(2)           [AWK1 AND ACOM2 = NO.OF FILES
   9 5HHQ ...      STOZ     AWORK2(2)
  21 5HHS ...      STOZ     ACOMMUNE2(2)        [AWK2 AND ACOM3 = ACCUMULATED SIZE   
  10 5HHW ...      STOZ     ACOMMUNE3(2) 
   4 5HHY ...SFS0
  10 5HJ2 ...      LDX   0  ACOMMUNE2(2) 
  21 5HJ4 ...      ADS   0  AWORK1(2)           [NO. OF FILES IN THIS INCR. ACCUMULAT
  16 5HJ6 ...                                   [       IN AWORK1
  10 5HJ8 ...      LDX   0  ACOMMUNE3(2) 
  21 5HJ= ...      ADS   0  AWORK2(2)           [SIZE OF INCREMENT ACCUMULATED IN AWO
   8 5HJ# ...      CALL  5  TM   
  14 5HJB ...SFS1  CALL  5  READP               [READ OB 
   7 5HJD ...      LDN   5  6
  17 5HJG ...      BXE   5  ESENTYP(3),SFS2     [J IF START SENTINEL 
   8 5HJJ ...      LDCT  5  #400 
  17 5HJL ...      BXE   5  ESENTYP(3),SKEL     [J IF END SENTINEL   
   9 5HJN ...      ACROSS   RESTERR,1
   4 5HJQ ...YAL 
   7 5HJS ...      ADN   6  3
  21 5HJW ...      RESQUERY 6,Y3                [J IF SPECIAL RESIDENCE NOT AVAILABLE
   8 5HJY ...      BRN      YGET 
  18 5HK2 ...XALT                               [SUBROUTINE FOR ALTLEN   
   5 5HK4P...#SKI IFS
   7 5HK6P...      MASFCB   2
   6 5HK8P...#SKI IFS<1$1
   8 5HK= ...      LDX   2  BFILE
   7 5HK# ...      EXIT  1  0
   4 5HKB ...SFS2
  20 5HKD ...      MHUNTW   1,FILE,FINTER6      [STORE S/F SENTINEL IN FINTER6   
   8 5HKG ...      LDN   0  A1(3)
   8 5HKJ ...      ADN   1  A1   
   8 5HKL ...      MOVE  0  20   
  21 5HKN ...      DOWN     RESTLIMB,1          [RESTORE ALL FILES BENEATH THIS ONE  
   8 5HKQ ...      BRN      SFS0 
  20 5HKS ...      BRN      UPPLUS              [ERROR HAS OCCURRED-RETURNING TO 
  19 5HKW ...                                   [   RESTOREA TO CLEAR B/STORE
  15 5HKY ...#   ALL FILES UNDER MASTER HAVE BEEN RESTORED.  
  20 5HL2 ...#   TAPE IS POSITIONED AFTER MASTER SENTINEL ON TAPE AND RESTMERG   
  21 5HL4 ...#   WILL MERGE MASTER FILE ON TAPE WITH THE SKELETON ENTRIES SET UP BY  
   7 5HL6 ...#   RESTLIMB.   
   8 5HL8 ...SKEL  CALL  5  TM   
  17 5HL= ...      CALL  5  READP               [READ NEXT SENTINEL  
   8 5HL# ...      LDN   0  #2000
  18 5HLB ...      SMO      FILERING(2)         [SET B13 [1ST OPEN REEL] 
   9 5HLD ...      ORS   0  FGENERAL1
   9 5HLG ...      LDX   0  A1+2(3)  
  10 5HLJ ...      BXE   0  MAST(1),TBM  
   9 5HLL ...      ACROSS   RESTERR,2
  10 5HLN ...TBM   DOWN      RESTMERG,1  
   8 5HLQ ...      BRN      TOK  
   5 5HLS ...UPPLUS  
  21 5HLW ...      UPPLUS 1                     [ERROR HAS OCCURRED - RETURNING TO   
  19 5HLY ...                                   [  RESTOREA TO CLEAR B/STORE 
   4 5HM2 ...TOK 
  20 5HM4 ...      CALL  5  TM                  [THIS SECTION CHECKS THAT A DUMP 
  20 5HM6 ...      CALL  5  READP               [TAPE IS SUITABLE FOR A GENERAL  
  11 5HM8 ...      HUNT     1,FILE,FINTER7   
   9 5HM= ...      LDX   5  AWORK1(2)
   7 5HM# ...      ADN   5  1
  10 5HMB ...      STO   5  BMINTO1(1)   
  20 5HMD ...      LDX   5  AWORK2(2)           [ STORE THE SIZE OF THE DUMP IN  
   7 5HMG ...      ADN   5  1
  17 5HMJ ...      STO   5  BMINTO2(1)          [          BMINTO2   
  21 5HML ...      BBUS  0,ESENGR(3),NOTSUIT    [ERROR IF INCR. NOT GENERAL RESTOREAB
  19 5HNL ...      DOWN     RESTOREA,4          [ DOWN TO RESTOREA TO SET UP 
  18 5HPL ...                                   [ ACTIVITY TO UNLOAD TAPE
  20 5HQL    #   THIS SECTION OF PROGRAM CREATES A DIRECTORY ENTRY FOR MASTER AND
  19 5J==    #   STORES IT IN THE SECTION OF BACKING STORE RESERVED FOR IT   
  13 5JPW    #   UNIT NO. BTHREE,BLOCK NO ADMASTDIR. 
   4 5K9G    #   
   7 5KP6          GRINDEX M 
   7 5L8Q          CLOSEFRBS 
   9 5LNB          LDX   0  AWORK3(2)
  10 5M82          STO   0  ACOMMUNE1(2) 
  19 5MML    #   AFTER MASTER HAS BEEN CLOSED VARIOUS ROUTINES ARE ENTERED   
  11 5N7=    #   TO TIDY UP THE FILESTORE.   
  21 5NLW    #              RESTEND      INSERTS THE TAPE NO. OF THE TAPE USED INTO  
  17 5P6G    #                          INCINDEX,SERIAL AND DUMPER   
  21 5PL6    #              INCEND      CLEARS UNRELIABLE INCREMENTS FROM INCINDEX.  
   3 5PYY ...
   3 5Q?Q ...
   5 5QLJ ...MFREE   
   3 5Q_B ...
  10 5R#8 ...      MFREE    FILE,FLOCNB  
  10 5RM2 ...      MFREE    FILE,FRESTOR 
   9 5R_S ...      MFREE    FILE,FAPB
   9 5S#L ...      MFREE    FILE,FWB 
   5 5SMD ...      UP
   4 5T2= ...#   
   4 5T*4 ...#   
  21 5TMW ...#   THIS SUBROUTINE READS A BLOCK FROM TAPE (UNIT IN X4) AND TESTS ITS  
   6 5W2Q    #   REPLY   
   5 5WGB    READP   
   8 5X22          SBX   5  FX1  
  10 5XFL          MHUNTW   3,FILE,FWB   
  14 5X_=          LOCKC 3,XLOCK                [LOCK FWB
  10 5YDW          MHUNTW   3,FILE,FWB   
   8 5YYG          LDN   0  A1(3)
  18 5_D6          DCA   ,STO,0,CTSA            [SET DESTINATION ADDRESS 
   8 5_XQ          LDN   0  20   
  16 62CB          DCA ,STO,0,COUNT             [ TRANSFER LENGTH
  16 62X2          DCA   ,STOZ,,MODE            [SET 'READ& MODE 
  17 63BL          CALL  4  PERI                [READ PERI (FROM MT) 
   7 63W=          UNLOCK   3
   8 64*W          LDCT  0  #640 
   9 64TG          ANDX  0  CPRW1(2) 
  21 65*6          BNZ   0  XER19               [J IF B0,B1 OR B3 SET IN REPLY WORD  
   8 65SQ          ADX   5  FX1  
   7 66#B          EXIT  5  0
  21 66S2    XLOCK                              [SUBROUTINE ENTERED IF FWB MOVED WHIL
  15 67?L          MHUNTW   2,FILE,FWB          [BEING LOCKED
   7 67R=          EXIT  1  0
   4 68=W    #   
  19 68QG    #   THIS SUBROUTINE POSITIONS THE TAPE AFTER THE NEXT TAPEMARK  
   7 69=6    #   (UNIT IN X4)
  17 69PQ    TM    SBX   5  FX1                 [FOR CO-ORDINATION   
   8 6=9B          LDX   2  FX2  
  18 6=P2          LDN   0  4                   [SKIP TO TAPE-MARK MODE  
   9 6?8L          DCA ,STO,0,MODE   
  17 6?N=          CALL  4  PERI                [SKIP TO TAPE-MARK   
   8 6#7W          ADX   5  FX1  
   7 6#MG          EXIT  5  0
   4 6*76    #   
   4 6*D? ...#   
  21 6*PD ...#     ******************************************************************
   4 6B2K ...#   
  14 6B?Q ...#                                  RESTOREG2
  14 6BJX ...#                                  *********
   4 6BW4 ...#   
  15 6C79 ...#     THIS THE ENTRY POINT FOR PARTIAL RESTORE  
   4 6CDB ...#   
   6 6CPH ...RESTOREG2   
  13 6D2N ...#     SET UP BLOCKS NEEDE FOR RESTORE   
   4 6D?T ...#   
  12 6DK2 ...      SETNCORE   GSBS,3,FILE,FAPB   
  12 6DW7 ...      SETNCORE   GSBS,3,FILE,FRESTOR
  11 6F7# ...      SETNCORE   6,3,FILE,FLOCNB
  10 6FDF ...      SETNCORE 20,3,FILE,FWB
  10 6FPL ...      CALL  1  SFRESTUSER3  
   4 6G2R ...#   
   4 6G?Y ...#   
  18 6GK5 ...      SMO      FRNXT(3)            [ POINTER TO NEXT LIMB   
  19 6GW= ...      LDX   0  FRINC(3)            [ INCREMENT NUMBER OF NEXT   
  17 6H7C ...                                   [ LIMB TO BE RESTORED
  18 6HDJ ...      BXE   0  FRLST(3),RIGHTINC   [ JUMP IF ALREADY ON INC 
   4 6HPP ...#   
  16 6J2W ...#     EITHER THE TAPE HAS JUST BEEN LOADED OR IT IS 
  16 6J#3 ...#     POSITIONED ON A LATER INCREMENT ON THE TAPE   
  19 6JK8 ...#     IN EITHER CASE WE REWIND THE TAPE AND CHECK THE HEADDED   
   4 6JW* ...#   
  16 6K7G ...      CALL  5  READHDDR            [ REWIND AND READ
   9 6KDM ...      MFREE    FILE,FWB 
  12 6KPS ...      MHUNTW   2,FILE,APROC        [
  19 6L2_ ...      LDX   0  AMTS(2)             [ COUNT OF NUMBER OF TAPES   
  12 6L#6 ...NEXTAPE                            [
  20 6LK? ...      BXE   6  AMAG(2),RIGHTTAPE   [ JUMP IF A SUITABLE TAPE IS UP  
  17 6LWD ...      ADN   2  INCMAGLEN           [ OTHERWISE COMPARE  
  18 6M7K ...      BCT   0  NEXTAPE             [WITH NEXT TAPE IN RECORD
  12 6MDQ ...RONGTAPE                           [
  16 6MPX ...#     THE TAPE LOADED DOES NOT CONTAIN THE INCREMENT
  15 6N34 ...      OUTPACK  6,1,TSN             [ OUTPUT TSB 
  21 6N#9 ...      MONOUT   GRPRNOTSUIT           [MT %A NOT SUITABLE FOT THIS PARTIA
  14 6NKB ...                                   [ RESTORE
  17 6NWH ...      DOWN     RESTOREA,4          [ DOWN TO UNLOAD TAPE
  19 6P7N ...      CALL  1  SFRESTUSER3         [ POINTER TO FRESTUSER IN X3 
  18 6PDT ...      STOZ     FRTSN(3)            [ CLEAR TSN LOADED WORD  
  19 6PQ2 ...      STOZ     FRLST(3)            [ CLEAR INCREMENT NUMBER AND 
  21 6Q37 ...      STOZ     FRLST+1(3)          [ FILE NUMBER OF LIMB LAST RESTOERED 
  21 6Q## ...      BRN      XER30               [ ACCROSS TO RESTERR TO FREE BLOSCKS 
   4 6QKF ...#   
   6 6QWL ...RONGTAPE2   
  10 6R7R ...      CALL  1  SFRESTUSER3  
   9 6RDY ...      LDX   6  FRTSN(3) 
   9 6RQ5 ...      BRN      RONGTAPE 
  11 6S3= ...#      SET UP TSN FOR MESSAGE   
   4 6S#C ...#   
  21 6SKJ ...#     ******************************************************************
   4 6SWP ...#   
  15 6T7W ...#     THE RIGHT TAPE IS NOW LOADED - TSN IN X6  
  12 6TF3 ...RIGHTTAPE                          [
  10 6TQ8 ...      CALL  1  SFRESTUSER3  
  20 6W3* ...      BXE   6  FRTSN(3),OLREADYUP  [ J IF THIS TAPE USED LAST TIME  
  19 6W#G ...      STO   6  FRTSN(3)            [ OTHERWISE UPDATE TSN WORD  
  18 6WKM ...      OUTPACK  6,1,TSN             [ DUMPTAPE %A IS ENGAGED 
  12 6WWS ...      MONOUT   GRTAPENGD           [
   6 6X7_ ...OLREADYUP   
  21 6XF6 ...#     ******************************************************************
  14 6XQ? ...#                                  OLREADYUP
  14 6Y3D ...#                                  *********
  21 6Y#K ...      CALL  6  NEXTINC             [SKIP TO NEXT START OF INCREMENT SENT
  12 6YKQ ...      CALL  1  SFRESTUSER3         [
  17 6YWX ...      SMO      FRNXT(3)            [ POINTER TO NEXT INC
  12 6_84 ...      LDX   4  FRINC(3)            [
   9 6_F9 ...      STO   5  FRLST(3) 
  21 6_QB ...      BXL   5  4,OLREADYUP         [ JUMP IF NOT YET REACHED INCREMENT  
  16 723H ...      BXE   5  4,RIGHTINC          [ JUMP IF  INC   
  14 72#N ...#     WE APPEAR TO HAVE MISSED THE INCREMENT
  12 72KT ...      OUTPACK  4,1,INCNUM          [
  10 72X2 ...      CALL  1  SFRESTUSER3  
  12 7387 ...      OUTPACK  FRTSN(3),1,TSN      [
  18 73F# ...      MONOUT   GRWRONGDT           [ INCREMENT %A NOT ON %B 
  16 73QF ...      BRN      RONGTAPE            [JUMP TO OUTPUT  
  16 743L ...#     MT %A NOT SUITABLE FOR THIS PARTIAL RESTORE   
  21 74#R ...#     ******************************************************************
  16 74KY ...#     WE ARE NOW POSITIONED AT THE CORRECT INCREMENT
  17 74X5 ...#     WE NOW HAVE TO READ TO FIND THE CORRECT SUBFILE   
  14 758= ...#                                  RIGHTINC 
  14 75FC ...#                                  ******** 
  12 75QJ ...RIGHTINC                           [
  16 763P ...      CALL  5  TM                  [ SKIP TO NEXT TM
  15 76#W ...      CALL  5  READP               [AND READ QB 
  12 76L3 ...      LDN   0  7                   [
  16 76X8 ...      BXE   0  ESENTYP(3),RONGTAPE2  [ JUMP IF EOT  
  12 778* ...      LDN   0  6                   [
  19 77FG ...      BXU   0  ESENTYP(3),RIGHTINC   [ JUMP IF NOT START SENT   
  14 77QM ...#     IS THIS THE CORRECT START OF SUBFILE ?
  19 783S ...      CALL  1  SFRESTUSER2        [ POINTER TO FRESTUSER IN X2  
  18 78#_ ...      ADX   2  FRNXT(2)            [ POINTER TO NEXT USER   
  15 78L6 ...      TESTNAMX   3,FRUSE(2),ESENUSE1(3),RIGHTINC
  16 78X? ...#     JUMP IF THIS NOT THE TO BE RESTORED SUBFILE   
   4 798D ...#   
  21 79FK ...#     ******************************************************************
   4 79QQ ...#   
  13 7=3X ...#     WE HAVE FOUND THE REQUIRED LIMB   
  21 7=*4 ...#     THE TAPE IS POSITIONED JUST AFTER THE START OF SUBFILE SENTINEL   
  17 7=L9 ...      LDX   6  ESENFN(3)           [ PICK UP FILE NUMBER
   4 7=XB ...#   
   3 7?8H ...
   4 7?FN ...#   
  12 7?QT ...      MHUNT    3,FILE,FWB          [
  12 7#42 ...      ALTLEN   3,BSBS,FILE,FWB     [
  21 7#*7 ...#     ******************************************************************
  17 7#L# ...#     SET DOING PARTIAL RESTORE BIT IN MASTER'S DIR ENT 
   9 7#XF ...      LDN   1  BTHREE   
   9 7*8L ...      LDN   2  ADMASTDIR
  18 7*FR ...      BACKREAD ,BSBS,,1            [ READ MASTER'S DIR ENT  
  10 7*QY ...      MHUNTW   3,BSTB,BREAD 
  21 7B45 ...      BS       3,BEPARTRES         [ INDICATE PR'S INITIATED AND BS FREE
  12 7B*= ...      LDCT  0  #200                [
  16 7BLC ...      ERS   0  EREC(3)             [ INVERT SWITCH  
   8 7BXJ ...      LDN   4  A1(3)
  19 7C8P ...      LDN   5  GSBS/2+A1(3)        [ COPY FIRST HALF INTO SECOND
  12 7CFW ...      MOVE  4  GSBS/2              [
  20 7CR3 ...      BS       BFILE,BFPARTRES     [ SET PR'S BIT IN MASTER'S FCB   
  16 7D48 ...      BACKWRIT FREE,BSBS           [ REWRITE DIRENT 
   4 7D** ...#   
   6 7DB* ...#UNS AMTGR  
   4 7DC* ...(   
  10 7DD* ...     JBCC XLEAVBSPR,,BFREEBS
  10 7DF* ...      DOWN    BACKROOM,11   
   6 7DG* ...XLEAVBSPR   
   4 7DH* ...)   
   6 7DJ* ...#UNS AMTGR  
   4 7DK* ...#SKI
  15 7DLG ...      DOWN     BACKROOM,11         [ CLEAR BS   
  19 7DXM ...      DOWN     RESTLIMB,1           [ DOWN TO RESTORE THE LIMB  
  19 7F8S ...      BRN      RESTORED                     [ J IF SUCCESSFUL   
  18 7FF_ ...      UPPLUS   1                      [ NO GOOD TRY AGAIN   
   5 7FR6 ...RESTORED
  21 7G4? ...#     ******************************************************************
  14 7G*D ...#                                  RESTORED 
  14 7GLK ...#                                  ******** 
   4 7GXQ ...#   
  12 7H8X ...#     THE LIMB HAS NOW BEEN RESTORED
   3 7HG4 ...
  18 7HR9 ...      REINDEX                      [ INDEX THE TOP FILE OPEN
  21 7J4B ...                                   [ AND SET UP A FI FINFEXF FOR IT IN A
  10 7J*H ...      TESTREPN  OK,(GEOERR) 
  21 7JLN ...      TOPFCA   3                   [ FIND PLACE FOR FINDEXF IN FILE CHAI
  12 7JXT ...      ADX   3  FBACKPOINT(3)       [
  12 7K92 ...      MHUNT    2,FI,FINDEXF        [
  18 7KG7 ...      CHAIN    2,3                 [ CHIAN IN THE FILE CHAIN
  15 7KR# ...      SETREP   OK                  [SET OK REPLY
  21 7L4F ...      BRN      MFREE                 [ JUMP TO FREE BLOCKS AND UP TO BAC
  18 7L*L ...UP                                 [ AND BACK UP TO BACKMAP 
   5 7LLR ...      UP
  10 7LXY ...#     AND BACK TO BACKMAP   
  21 7M95 ...#     ******************************************************************
   4 7MG= ...#   
   4 7MRC ...#   
   5 7N66    XER19   
  21 7NKQ          ACROSS   RESTERR,19          [B0, B1 OR B3 SET IN REPLY WORD ON RE
   5 7P5B    XER30   
  21 7PK2          ACROSS   RESTERR,30          [GO TO CLOSE FILES OPEN & FREE BLOCKS
   4 7Q4L    #   
  11 7QJ=          MENDAREA 30,K99RESTOREG   
   4 7R3W    #   
   4 7RHG    #END
   8 ____ ...26613111000100000000
