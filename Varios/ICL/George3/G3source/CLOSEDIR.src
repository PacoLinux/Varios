  14 22FL ...      SEG   CLOSEDIR,863,SECTION FILE,CLOSE 
   4 2Y=Q    #   
  15 2YGJ ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   4 2YQB    #   
   7 2_=2    #     USES OF X7
   7 2_*Y ...#     ----------
  15 2_FW ...      BITDEFS  7,0,XBSHORT         [CLOSESHORT  
  17 2_KS ...      BITDEFS  7,1,XBTOP           [CLOSETOP/CLOSEDIR   
  15 2_PQ ...      BITDEFS  7,2,XBFULL          [CLOSE (FULL)
  15 2_TN ...      BITDEFS  7,3,XBFRBS          [CLOSEFRBS   
  19 329G ...      BITDEFS  7,10,XBFRITEALL     [FRITE WITH 'ALL' PARAMETER  
  21 32#6 ...      BITDEFS  7,11,XBCORUNJWR     [ADATA/CORUNJAM BUFFER BEING (FIXED) 
  19 32BQ ...                                   [ WRITTEN BY CORE UNJAMMER   
  15 32FB ...      BITDEFS  7,12,XBTHROW        [CLOSETHROW  
  15 32M? ...      BITDEFS  7,14,XBABAND        [CLOSEABANDON
  20 32XS ...      BITDEFS  7,15,XBNONAUTOWR    [NON-AUTONOMOUS WRITING OF WRITE 
  18 338* ...                                   [ BUFFER BLOCKS REQUIRED 
  18 33DW ...      BITDEFS  7,20,XBCOPY         [CLOSING FROM COPY MODE  
  15 33NQ ...      BITDEFS  7,22,XBAUTO         [AUTONOMOUS  
  14 33SN ...      BITDEFS  7,23,XBSET          [CLOSESET
   4 33YL ...#   
   4 344J ...#   
   8 348G ...#     ENTRY POINTS. 
   4 34#D ...#   
  14 34DB ...      SEGENTRY K1CLOSEDIR,XENT1    [CLOSE %A
  14 34J# ...      SEGENTRY K2CLOSEDIR,XENT2    [CLOSETOP
  20 34M= ...      SEGENTRY K4CLOSEDIR          [ENTRY IN AUTOCLOSE FROM CLOSEALT
  21 34Q8 ...                                   [ AFTER DIRECTORY HAS BEEN UNCLEANLY 
  21 34T6 ...                                   [ UPDATED (IT IS SPECIAL FULL CLOSE) 
  11 34Y6 ...        MBSXD  7,XBFULL,XBAUTO  
   9 3544 ...        BRN    SMERGE   
  21 356# ...      SEGENTRY K9CLOSEDIR          [ENTRY FROM COPYFILE WHICH HAS JUST  
  21 358J ...                                   [ WRITTEN THE FILE'S DIRECTORY ENTRY 
  21 35=S ...                                   [ AND CLOSED THE DIRECTORY. IF FILE  
  21 35*4 ...                                   [ IS DIRECTORY, WILL CONVERT TO FULL 
  21 35C# ...                                   [ CLOSE SINCE BFDIRUPDATE SET (UNLESS
  19 35FJ ...                                   [ DIR. NOT ACTUALLY COPIED). 
  21 35HS ...                                   [ OTHERWISE CONVERTS TO FULL CLOSE   
  18 35L4 ...                                   [ TYPE IF FILE ALTERED.  
  12 35NC ...        MBSXD  7,XBTOP,XBCOPY,XBAUTO
   9 35RN ...        BRN    SMERGE   
  16 35XL ...      SEGENTRY K12CLOSEDIR         [CLOSEABANDON.   
  20 363W ...        MBSXD  7,XBABAND,XBFULL    [NEED MAIN MODE (FAIRLY ARBITRARY
  16 3686 ...        BRN    SMERGE              [ WHAT IT IS !!) 
  14 36#C ...      SEGENTRY K21CLOSEDIR,XENT21  [CLOSE   
  15 36F# ...      SEGENTRY K23CLOSEDIR,XENT23  [CLOSESHORT  
  14 36L9 ...      SEGENTRY K16CLOSEDIR,XENT16  [CLOSESET
   6 36R8 ...        NULL
  21 36TS ...      SEGENTRY K27CLOSEDIR         [CLOSEFRBS (A CLOSESHORT WHICH FREES 
  18 36YD ...                                   [ SPARE BS NON-CAREFULLY)
   9 3734 ...        BSXD   7,XBFRBS 
   9 3772 ...        BRN    XENT27   
   6 37=Y ...        NULL
   6 37BW ...        NULL
   6 37GS ...        NULL
   6 37LQ ...        NULL
  16 37QN ...      SEGENTRY K88CLOSEDIR,XENT88  [AUTONOMOUS FRITE
  18 37WL ...      SEGENTRY K89CLOSEDIR,XENT89  [NON-AUTONOMOUS FRITE.   
  14 382J ...      SEGENTRY K5CLOSEDIR,XENT5    [CLOSEDIR
  21 386G ...      SEGENTRY K95CLOSEDIR         [CLOSETHROW (A CLOSETOP WHICH DOES   
  21 38=D ...                                   [ NOT RETAIN DIR. IN HALF-OPEN CHAIN)
  11 38BB ...        MBSXD  7,XBTOP,XBTHROW  
   9 38G# ...        BRN    XENT95   
   4 38L= ...#   
   4 38Q8 ...#   
  21 38W6 ...MCOMMUNE       #57777777           [TO REMOVE COMMUNE BIT FROM CTOPEN   
   5 39FS ...SADATA  
  20 39KQ ...#HAL           ADATA+CORUNJAM,0    [CORE UNJAMMER BLOCK--DON'T TOUCH
   5 39PN ...SEXTRA  
  10 39TL ...#HAL           FILE+FEXTRA,0
   5 3=9D ...SFINDEXF
  10 3=*B ...#HAL           FI+FINDEXF,0 
   4 3=GW    #   
   4 3=L2 ...#   
   8 3=P6 ...#     SUBROUTINES   
   8 3=S= ...#      ----------   
   4 3=XB ...#   
   4 3?2G    #   
  20 3?G6          FILETRAN                     [SUBROUTINE MACRO FOR FILEAUTW   
   4 3?_Q    #   
   6 3CWQ    RINGFIRST   
   9 3DBB          ADN   2  FILERING 
   8 3DW2          RERING   3,2  
   7 3F*L          EXIT  5  0
   4 3FKD ...#   
   5 3FT=    SFSTACK 
  21 3G#W ...      SFSTACK  AWORK1(2),3         [X3-> FCA OF FILE AT DEPTH IN AWORK1 
   7 3GSG          EXIT  4  0
   4 3H4# ...#   
   4 3H#6    TAB 
  19 3HRQ          TOPFCAB  3,2                 [X2->FCB X3->FCA AT LEVEL 0  
   7 3J?B          EXIT  6  0
   4 3JH8 ...#   
   4 3JR2    TB  
  17 3K=L ...      TOPFCB   2                   [X2-> FCB AT LEVEL 0 
   7 3KQ=          EXIT  6  0
   4 3PM=    #   
   4 3PMY ...#   
  21 3PNG ...#     THIS SUBROUTINE WRITES A 'WRITE BUFFER' TO BS USING FILEAUTW (FOR 
  20 3PP4 ...#      SERIAL FILE BUFFER) OR CONFURWB (FOR DA OR MT FILE BUFFER).  
  21 3PPL ...#     ON ENTRY, X2->BLOCK TO BE WRITTEN BACK, X4= BLOCK TYPE FOR FUWB OR
  19 3PQ8 ...#               FURWB, AWORK1= DEPTH OF FILE, XBNONAUTOWR SET IF
  20 3PQQ ...#               NON-AUTONOMOUS WRITE REQUIRED.    X6 IS S/R LINK.   
  19 3PRB ...#     ON EXIT, X1=FX1, X2= FX2.   OVERWRITES X0, X3, X4 & X5.   
   4 3PS4 ...#   
   5 3PSQ ...WBWRITE 
   8 3PTD ...      SBX   6  FX1  
   8 3PW6 ...      SMO      FX2  
  15 3PWS ...      LDX   5  AWORK1              [X5= DEPTH   
  17 3PXG ...      BXU   4  FFSFUWB,RANDWR      [J IF RANDOM WRITE   
  17 3PY8 ...#SKI  K6FREEBUFF>199-199           [FREEBUFF DIAGNOSTIC 
   4 3PYW ...(   
  17 3P_J ...      JBC      NALL1,,XBFRITEALL   [J IF NOT 'FRITE ALL'
   7 3Q2= ...      LDN   0  1
  21 3Q2N ...      ADS   0  INSTPARAM+1         [UPDATE COUNT OF FUWBS WRITTEN (IN   
  16 3Q36 ...                                   [ IP MANAGER1)   
   5 3Q3L ...NALL1   
   4 3Q4# ...)   
   8 3Q52 ...      CHAIN    2,FX2
  21 3Q5N ...      BLAM     5                   [X1= FREADBLOCK TYPE MODIFIER FOR BLK
   7 3Q6B ...      LDX   4  1
  11 3Q74 ...      FILEAUTW 5,FREE+FAIL,,4   
  12 3Q85 ...      JBC      WREXIT,,XBNONAUTOWR  
   7 3Q96 ...      BACKWAIT  
   5 3Q9S ...WREXIT  
   8 3Q=G ...      ADX   6  FX1  
   7 3Q?8 ...      EXIT  6  0
   5 3Q?W ...RANDWR  
  17 3Q#J ...#SKI  K6FREEBUFF>199-199           [FREEBUFF DIAGNOSTIC 
   4 3Q*= ...(   
  17 3Q*Y ...      JBC      NALL2,,XBFRITEALL   [J IF NOT 'FRITE ALL'
   7 3QBL ...      LDN   0  1
  21 3QC4 ...      ADS   0  INSTPARAM+2         [UPDATE COUNT OF FURWBS WRITTEN (IN  
  16 3QCG ...                                   [ IP MANAGER2)   
   5 3QD2 ...NALL2   
   4 3QDN ...)   
  18 3QFB ...      LBW      ,XBNONAUTOWR,4      [X4 NON-ZERO IF NON-AUTO 
  21 3QFS ...      CONFURWB 2,5,4               [FOR DA & MT BUFFERS, DECIDE HOW MUCH
  17 3QG= ...                                   [ TO WRITE & WRITE IT
   9 3QGQ ...      BRN      WREXIT   
   4 3QHD ...#   
   4 3QJ6 ...#   
   7 3QJ* ...#     MAIN CODE 
   7 3QJJ ...#     ---- ---- 
   4 3QJS ...#   
   4 3QKG ...#   
  20 3QLG    #     CLOSE ENTRY POINTS.  X7 IS SET TO INDICATE THE TYPE OF CLOSE  
  20 3R66    #     IF THE FILE IS AT NON-ZERO DEPTH IT IS RERINGED AT DEPTH 0.   
   4 3RKQ    #   
   4 3Y_L    #   
   6 3_F=    # CLOSESET  
  21 3_YW    #     A CLOSETOP IN WHICH THE CLOSING ACTIVITY IS MADE TO WAIT UNTIL THE
  13 42DG    #     FILE HAS BEEN CAREFULLY UPDATED   
   4 42Y6    #   
  14 43CQ ...XENT16                             [CLOSESET
   8 44WL          CALL  6  TB   
  21 45B= ...      JBC      XENT2,2,BFCARE      [J IF NON-CAREFUL FILE (DO CLOSETOP) 
  10 45TW ...      MBSXD    7,XBTOP,XBSET
   9 48#2          BRN      SMERGE   
   4 48RL    #   
   5 49?=    # CLOSE 
  18 49QW ...#     UPDATES THE DIRECTORY, EXCEPT CLOSES ON DIRECTORIES   
  12 4==G ...#     WHICH CONVERT TO CLOSETOPS.   
   4 4=Q6    #   
  14 4=WJ ...XENT1                              [CLOSE %A
  17 4?32 ...      LDX   0  ACOMMUNE8(2)        [PICK UP FILE DEPTH  
   8 4?7D ...      SLL   0  12   
  19 4??W ...      SRA   0  12                  [RESTORE B0-11 IF -IVE DEPTH 
  15 4?D# ...      STO   0  AWORK1(2)           [STORE DEPTH 
  21 4?JQ ...      CALL  4  SFSTACK             [X3-> FCA FOR FILE AT DEPTH IN AWORK1
  19 4?P8 ...      CALL  5  RINGFIRST           [RERING FILE TO BE AT DEPTH 0
  14 4?TL ...XENT21                             [CLOSE   
   8 4#24 ...#UNS   CLOSESTATS   
   4 4#6G ...(   
   7 4#=Y ...      LDN   0  1
  17 4#CB ...      ADS   0  CLSTAT4             [NO. OF CLOSE USES   
   4 4#HS ...)   
   8 4#NL          CALL  6  TB   
  20 4*8= ...      JBS      XENT2,2,BFDIR       [CONVERT TO CLOSETOP IF DIRECTORY
   4 4*MW ...#   
   5 4B7G ...XFULL   
   9 4CLB ...      BSXD     7,XBFULL 
   9 4DKL          BRN      SMERGE   
   4 4F5=    #   
   6 4FJW    # CLOSETOP  
  21 4G4G    #     CLOSES THE FILE AT LEVEL 0. IT WONT UPDATE THE DIRECTORY UNLESS   
  21 4GJ6    #     NECESSARY (E.G. BLOCKS ALTERED), WHEN IT CONVERTS TO A FULL CLOSE 
   4 4H3Q    #   
  14 4HHB ...XENT2                              [CLOSETOP
   9 4J32 ...      BSXD     7,XBTOP  
   5 4J#5 ...XENT95  
   7 4JK9 ...#UNS  CLOSESTATS
   4 4JMS ...(   
   7 4JQC ...      LDN   0  1
  18 4JT2 ...      ADS   0  CLSTAT6             [NO. OF USES OF CLOSETOP 
   4 4JXK ...)   
   8 4K2=          CALL  6  TB   
  17 4KFW ...      JBS      XFULL,2,BFERASE     [IF ERASE, FULL CLOSE
   9 4K_G          BRN      SMERGE   
   4 4LF6    #   
   6 4LYQ    # CLOSEDIR  
  17 4MDB    #     CLOSES THE FILE AT LEVEL 1. OTHERWISE AS CLOSETOP 
   4 4MY2    #   
  14 4N95 ...XENT5                              [CLOSEDIR
   7 4NG9 ...#UNS  CLOSESTATS
   4 4NJS ...(   
   7 4NMC ...      LDN   0  1
  20 4NQ2 ...      ADS   0  CLSTAT7             [NO. OF CLOSEDIR MACROS OBEYED   
   4 4NSK ...)   
  10 4NX=          LDX   3  FILERING(2)  
   9 4PBW          LDX   3  FPTRF(3) 
  20 4PP? ...      CALL  5  RINGFIRST           [RERING FILE AT DEPTH 1 TO BE AT 
  14 4Q3N ...                                   [ DEPTH 0
   8 4QB6 ...      BRN      XENT2
   4 4QTQ    #   
   6 4QWR ...# CLOSEFRBS 
  21 4QXS ...#     CHECK THAT NO ONE ELSE IS USING OR WAITING FOR FILE, SINCE THIS IS
  12 4QYT ...#     A HIGHLY SPECIALISED MACRO.   
   4 4Q_W ...#   
   5 4R2X ...XENT27  
  14 4R3Y ...      CALL  6  TB                  [X2-> FCB
  10 4R4_ ...      LDX   0  FWAITCOUNT(2)
  17 4R62 ...      BNZ   0  XERRFRBS            [GEOERR IF WAITERS   
  15 4R73 ...      LDX   2  FPTR(2)             [X2-> FSTACK 
  10 4R84 ...      LDEX  0  ARINGNO(2)   
   7 4R95 ...      SBN   0  1
   9 4R=6 ...      BZE   0  SMERGE   
  21 4R=S ...      BRN      XERRFRBS            [GEOERR IF OTHER ACTS. HAVE FILE OPEN
  18 4R?G ...                                   [ (OR AUTOCLOSE GOING)   
   4 4R#8 ...#   
   6 4R*B    # CLOSESHORT
  21 4RT2    #     CLOSES THE FILE AT LEVEL 0, BUT NEVER UPDATES THE DIRECTORY, WHICH
  11 4S#L    #     MUST BE OPEN AT LEVEL 1.  
   4 4SS=    #   
  15 4T?W ...XENT23                             [CLOSESHORT  
   9 4WQQ ...      BSXD     7,XBSHORT
   4 4XQ2    #   
   4 5X_=    #   
   4 5YDW ...#   
   9 5YYG ...#     START OF MAIN PATH
   9 5_D6 ...#     ----- -- ---- ----
   4 5_XQ ...#   
   4 62CB ...#   
   5 65*6    SMERGE  
   7 65SQ ...#UNS  CLOSESTATS
   4 66#B ...(   
   7 66S2 ...      LDN   0  1
  21 67?L ...      ADS   0  CLSTAT3             [NO. OF ENTRIES TO CLOSEDIR SEGMENT  
   4 67R= ...)   
   8 6?N=          LDX   2  FX2  
   4 6#MG    #   
  18 6*76    #     BREAK IF THERE'S NO FILE OPEN TO THE CURRENT ACTIVITY 
  10 6B6B          LDN   0  FILERING(2)  
  18 6BL2 ...      BXE   0  FILERING(2),NOFILE  [GEOERR IF NO FILE OPEN  
   4 6DJG    #   
   8 6NW6          CALL  6  TAB  
  20 6Q*2 ...      JMBAC    NEMPTIER,2,BFREV,BFEMPTY,BFINDEXALT [THESE CONDITIONS
  17 6QSL ...                                   [ IMPLY FULL UPDATE  
  19 6_MB ...      JBC      NEMPTIER,,XBTOP     [J IF NOT CLOSETOP/CLOSEDIR  
  19 7272 ...      ANDX  7  BSP16               [PRESERVE L.S. 15 BITS OF X7 
  18 72LL ...      BS       ,XBFULL             [CONVERT TO FULL CLOSE   
   5 754Q    NEMPTIER
   4 7=Y=    #   
  20 7?CW    # IF FILE IS DESTCOMM OR ACCESS IS GDR & FILE WAS OPEN IN READ MODE 
  21 7?XG    # IT IS NECESSARY TO TIDY UP THE READ POINTERS AT THIS STAGE AND ALSO   
  16 7#C6    # TO SORT OUT WHICH BLOCKS ARE ALLOCATED TO THE FILE
   4 7#WQ    #   
  13 7*BB          JMBAC    NODESTCOMM,2,BFGDR,BFDCF 
  12 7*W2          JBC      NODESTCOMM,3,BAMREAD 
  10 7DRQ          DOWN     CLOSTIDY,1   
   8 7F?B          CALL  6  TAB  
   6 7G=L    NODESTCOMM  
  10 7GQ=          NGS   3  FREADWORD(3) 
  10 7H9W          NGS   3  FREADBLOCK(3)
   4 7HFN ...#   
  16 7HPG ...#     IF NOT A WF, ADJUST THE CURRENT-USED-ONLINE-BS
   4 7HTC ...#   
  18 7J3= ...      JMBS     NOSUBCUBS,2,BFWORK,BFERALLWF  [J IF WORKFILE 
   9 7J96 ...      LDX   0  FBLMOD(2)
   9 7JDY ...      SBN   0  FBLKS-A1 
   9 7JNQ ...      SUBCUBS  3,0,JOB  
  17 7JYJ ...      CALL  6  TAB                 [X2-> FCB,  X3-> FCA 
   6 7K8B ...NOSUBCUBS   
  17 7KD8 ...      TRACEIF  K6CLOSEDIR,99,299,7,CLOSE  [TYPE OF CLOSE
   4 7KJ5 ...#   
  21 7KN2    #     CHECK THE MODE AND SET X4 TO THE APPROPRIATE DECREMENT FOR CTOPEN 
   4 7L7L    #   
  17 7LM= ...      LDN   4  1                   [SET UP READ COUNT   
  13 7M6W ...      JMBS     SUBT,3,BAMREAD,BAMREADR  
  17 7MLG ...      LDCT  4  #400                [SET UP WRITE COUNT  
  15 7N66 ...      JMBS     SUBT,3,BAMWRITE,BAMGEN,BAMCLEAN  
  17 7NKQ ...      LDX   4  BIT11               [SET UP APPEND COUNT 
  10 7P5B ...      JBS      SUBT,3,BAMAPP
  21 7PK2 ...      LDN   4  0                   [NOTHING IN CTOPEN TO INDICATE COPY  
  17 7Q4L ...      JBC      NOPEN,3,BAMCOPY     [GEOERR IF NOT COPY  
   4 7WYW    SUBT
   4 7X== ...#   
  13 7XHL ...#     UPDATE FCOMMCT IF COMMUNAL ACCESS.
   4 7XT2 ...#   
  21 7Y6B ...      JMBAC    RIDBUFF,3,BACOMM,BACLEAN [J IF NOT COMMUNE OR CLEAN DA   
  17 7YCQ          LDX   6  BIT11               [COMMUNE FIELD LS BIT
  11 7YXB          JBS      SUBCT,3,BACOMM   
  17 7_C2          LDN   6  1                   [CLEAN FIELD LS BIT  
   5 7_WL    SUBCT   
  21 82B=          SBS   6  FCOMMCT(2)          [REDUCE COUNT OF COMM/CLEAN OPENERS. 
  10 82TW          LDX   6  FCOMMCT(2)   
  16 83*G          BNZ   6  NOTLASTCOM          [STILL COMMUNERS 
  10 83T6          BC       2,BFSYSCOMM  
   6 84#Q    NOTLASTCOM  
  16 84SB ...      TRACEIF  K6CLOSEDIR,99,299,FLOC1(2),CLOSCMCT  
   4 8MB6    #   
  19 8N*B    #     CLEANS UP THE FILE CHAIN IF NOBODY ELSE HAS THE FILE OPEN 
  18 8NK8 ...#     UPDATES DATE/TIME LAST WRITTEN FOR ALTERED ! FILES.   
  17 8NT2    #     FREES READ BLOCKS AND WRITES AWAY THE WRITE BLOCKS
  21 8P#L    #     CHECKS FOR STRANGE BLOCKS AND THAT FULLB'S ARE IN THE RIGHT PLACE 
  11 8PS=    #     WAIT IF THERE'S A CORE JAM
  21 8Q?W    #     BACKWAIT FOR TRANSFERS TO FINISH AND THEN GO ACROSS TO CLOSEND TO 
  10 8QRG    #           CLOSE THE FILE  
   4 8R?6    #   
   5 8RMB ...RIDBUFF 
   8 8S3M ...      SMO      FX2  
  19 8S#J ...      STO   4  AWORK2              [REMEMBER CTOPEN DECREMENT   
  20 8SFX ...RIDBL                              [LOOP BACK HERE IF COORDINATION  
  19 8SM= ...                                   [ DURING FILE CHAIN SEARCH   
  21 8SSK ...      BC       ,XBCORUNJWR         [CLEAR 'BUFFER BEING WRITTEN BY CORE 
  16 8S_Y ...                                   [ UNJAMMER' BIT  
  14 8T7? ...      CALL  6  TB                  [X2-> FCB
  15 8TD8 ...      LDX   3  FPTR(2)             [X3-> FSTACK 
   9 8TRN ...      LDX   0  CTOPEN(2)
  10 8T_D ...      ANDX  0  MCOMMUNE(1)  
   8 8W78 ...      SMO      FX2  
   9 8W#Y ...      SBX   0  AWORK2   
  21 8WCS ...      BZE   0  TWORK               [J IF NO OTHER OPENERS (IGNORE ANY   
  15 8WGN ...                                   [ COPIERS)   
  12 8WKJ ...      JBS      STILLOPEN1,,XBCOPY   
   8 8WND ...      SMO      FX2  
   9 8WW8 ...      LDX   0  AWORK2   
   8 8X3Y ...      ANDN  0  #7777
  21 8X7Q ...      BNZ   0  STILLOPEN1          [J IF OTHER OPENERS & CLOSING FROM   
  14 8X?J ...                                   [ READING
  20 8XCD ...      LDX   0  CTOPEN(2)           [MUST BE CLOSING FROM APPENDING  
   9 8XK8 ...      ANDX  0  COMUNI   
   8 8XQY ...      SMO      FX2  
   9 8XYN ...      SBX   0  AWORK2   
  18 8Y2# ...      BZE   0  TWORK               [J IF NO OTHER APPENDERS 
   6 8Y3Y ...STILLOPEN1  
   9 8Y=D ...      BRN      STILLOPEN
   5 8Y#8 ...TWORK   
  20 8YFY ...      JMBC     TRYMORE,2,BFWORK,BFALTR [J IF NOT AN ALTERED ! FILE  
   9 8YMN ...      LDX   0  SEXTRA(1)
   4 8YTD ...NOEX
  17 8_38 ...      LDX   2  FPTR(2)             [FIND THE FILE/FEXTRA
  10 8_8Y ...      BXU   0  ATYPE(2),NOEX
   9 8_BN ...      LDX   0  CLEANCT  
  20 8_JD ...      STO   0  FWRITTIME(2)        [UPDATE DATE/TIME LAST WRITTEN   
   8 8_Q8 ...      LDX   0  EDATE
  10 8_XY ...      STO   0  FWRITDAY(2)  
  21 9276 ...TRYMORE                            [LOOP BACK HERE IF NO COORDINATION   
  19 92B# ...                                   [ DURING FILE CHAIN SEARCH   
  16 92KG ...      LDX   2  3                   [X2-> FILE/FSTACK
   4 9356    TRYX
  20 93CH ...      LDX   2  FPTR(2)             [X2-> NEXT BLOCK IN FILE CHAIN OR
  16 93PY ...                                   [ WORKFILE RING  
  18 944B ...      BXE   2  CXFI,QUARRYON       [J IF END OF FILE CHAIN  
   8 94J2 ...      SMO      FX2  
   9 953L ...      LDN   0  BWORKRING
  19 95H= ...      BXE   2  0,QUARRYON          [J IF END OF WORK FILE RING  
   9 962W ...      LDX   4  ATYPE(2) 
  18 96GG ...      ANDX  4  HALFTOP             [X4= TYPE OF NEXT BLOCK  
  15 9726 ...      BXE   4  FILEPLUSFCB,QUARRYON [J IF FCB   
  15 97FQ ...      BXE   4  FFSFURB,XFREE       [J IF FURB   
  15 97_B ...      BXE   4  FFSFURRB,XFREE      [J IF FURRB  
  15 98F2 ...      BXE   4  FFSFUWB,XWRITE      [J IF FUWB   
  15 98YL ...      BXE   4  FFSFURWB,XWRITE     [J IF FURWB  
  21 99D= ...      BXE   4  SADATA(1),XWRITECOR [J IF ADATA/CORUNJAM (BEING WRITTEN  
  17 99XW ...                                   [ BY CORE UNJAMMER)  
  16 9=CG ...      BXE   4  SFINDEXF(1),TRYX    [J IF FI/FINDEXF 
  16 9=X6 ...      BXE   4  FFSFMAPP,XFMAPP     [J IF FILE/FMAPP 
  19 9?BQ ...      BXU   4  SEXTRA(1),ODDBLOK   [GEOERR IF NOT FILE/FEXTRA   
   5 9?WB ...QUARRYON
  21 9#8R ...      JBS      WAITWRCOR,,XBCORUNJWR [J IF ADATA/CORUNJAM FOUND (BEING  
  19 9#H8 ...                                     [ WRITTEN BY CORE UNJAMMER)
   6 9#TL ...STILLOPEN   
  14 9**= ...      CALL  6  TB                  [X2->FCB 
   4 9*SW ...#   
  21 9B#G ...#     ON 'B', WE HAVE AN ADDITIONAL INTERLOCK TO SIMPLIFY THINGS A BIT -
  21 9BS6 ...#     IT'S A PITY IT ISN'T IN STANDARD GEORGE TOO!!  NOONE IS ALLOWED TO
  21 9C?Q ...#     START CLOSING THE FILE IF SOMEONE IS IN THE PROCESS OF OPENING IT,
  20 9CRB ...#     WHICH IS INDICATED BY B0-6 OF FGENERAL1 OF AN FCA BEING ZERO. 
  20 9D?2 ...#     (THE OPPOSITE IS ALSO TRUE.)   IF FSTACK BEING ALTLENED, WE   
  20 9DQL ...#     MUST ALSO WAIT SINCE THIS INDICATES SOMEONE IS JUST STARTING  
   7 9F== ...#     OPENING.  
   4 9P4= ...#   
  19 9PHW ...      POP      RIDBL,2,BFTIDY      [STOP HERE IF SOMEONE ELSE IS
  17 =*4W ...                                   [CROSSING TO CLOSEND 
  21 =*#N ...      BACKWAIT                     [ENSURE ALL BLOCKS OF FILE WRITTEN TO
  13 =*JG ...                                   [ BS 
  20 =*S# ...      LDX   4  AWORK2(2)           [RESTORE CTOPEN DECREMENT TO X4  
   9 =B46          ACROSS   CLOSEND,1
   4 =B49 ...#   
   4 =B6? ...#   
   5 =B6J ...XFREE   
  17 =B6_ ...      FREECORE 2                   [FREE BUFFER BLOCK   
   9 =B7B ...      BRN      TRYMORE  
   4 =B7R ...#   
   5 =B88 ...XWRITE  
   8 =B92 ...      SMO      FX2  
  15 =B9C ...      STOZ     AWORK1              [ZERO DEPTH  
  18 =B9S ...      CALL  6  WBWRITE             [WRITE AWAY FUWB OR FURWB
   8 =B=9 ...      BRN      RIDBL
   4 =B=L ...#   
   6 =B?3 ...XWRITECOR   
  20 =B?? ...      BS       ,XBCORUNJWR         [SET 'CORE UNJAMMER WRITE BUFFER 
  16 =B?H ...                                   [ PRESENT' MARKER
   8 =B?T ...      BRN      TRYX 
   4 =B#= ...#   
   5 =B#M ...XFMAPP  
  16 =B*4 ...      LDX   2  FPTR(2)             [X2-> NEXT BLOCK 
  21 =B*# ...      BXE   2  CXFI,QUARRYON       [J IF END OF FILE CHAIN (CAN'T BE WF 
  20 =B*J ...                                   [ RING SINCE WF NEVER CAREFUL)   
   9 =B*W ...      LDX   4  ATYPE(2) 
  16 =BB? ...      BXE   4  FILEPLUSFCB,QUARRYON [J IF NEXT FCB  
  16 =BBN ...      BXE   4  FFSFULLB,XFMAPP     [J IF BSTB/FULLB 
   5 =BC5 ...ODDBLOK 
  21 =BCB ...      GEOERR   BRIEFPM,ODD BLOK    [BLOCK OF IMPERMISSIBLE TYPE IN FILE 
  21 =BCM ...                                   [ CHAIN (OR BLOCK OTHER THAN FULLB   
  16 =BCY ...                                   [ FOLLOWS FMAPP) 
   4 =BD# ...#   
   6 =BDP ...WAITWRCOR   
  20 =BF6 ...      COOR3    #55                 [WAIT FOR CORE UNJAMMER TO FINISH
  21 =BFH ...                                   [ (FIXED) WRITING AWAY ADATA/CORUNJAM
  14 =BFY ...                                   [ BLOCK  
  21 =BG* ...      BRN      RIDBL               [LOOK AGAIN FOR BUFFER BLOCKS (MIGHT 
  21 =BGQ ...                                   [ BE >1 CORUNJAM BLOCK ANYWAY, ALL   
  21 =BH7 ...                                   [ACTS. WAITING IN STYLE #55 RELEASED)
   4 =BHQ    #   
   4 =GYB    #   
  19 =HXL    #     K88 AND K89 CLOSEDIR ARE THE ENTRIES FROM THE FRITE MACRO.
  21 =JC=    #     K88 FORCES AN AUTONOMOUS, K89 A NON-AUTONOMOUS WRITE ON ANY FUWB'S
  20 =JWW    #           OR FURWB'S AFTER THE FILE WHOSE DEPTH IS IN ACOMMUNE7   
  21 =K4G ...#           (ACOMMUNE7=#40000000 IS SPECIAL CASE MEANING 'WRITE BUFFERS 
  21 =K=6 ...#           FOR ALL FILES OPEN TO THIS ACT.' (BUT EXCLUDE FILES OPEN TO 
   9 =KCQ ...#           OTHERS)).   
   5 =KKD ...XENT88  
  21 =KW6 ...      LDN   7  0                   [AUTONOMOUS FRITE (CLEAR MARKER WORD)
   9 =L*Q          BRN      WRMERGE  
   5 =LTB ...XENT89  
  17 =M*2 ...      BSXD     7,XBNONAUTOWR       [NON-AUTONOMOUS FRITE
   5 =MSL    WRMERGE 
  10 =N8* ...      LDX   0  ACOMMUNE7(2) 
  20 =NJ4 ...      BXE   0  GSIGN,WALL          [J IF 'ALL' PARAMETER SPECIFIED  
   5 =NPX ...WRNEXT3 
  16 =NXR ...      STO   0  AWORK1(2)           [REMEMBER DEPTH  
   5 =P?G    WRNEXT2 
  18 =PH# ...      CALL  4  SFSTACK             [X3->FCA AT DEPTH AWORK1 
  15 =PR6 ...      PSTAC 3,3                    [X3-> FSTACK 
  14 =Q2Y ...      BFCBX 2,3                    [X2-> FCB
  16 =Q=Q ...      JBC      NOTALL,,XBFRITEALL  [J IF NOT 'ALL'  
  21 =QCY ...                                   [DON'T WRITE BUFFERS IF OTHERS ARE   
  15 =QK6 ...                                   [ USING FILE 
  10 =QQB ...      LDEX  0  ARINGNO(3)   
   7 =R28 ...      SBN   0  1
  20 =R=2 ...      BNZ   0  WALL2               [J IF >1 ACT. RINGED INTO FILE   
   5 =RFS ...NOTALL  
  20 =RJC ...      JBC      WRNEXT1,,XBNONAUTOWR [J IF AUTONOMOUS(FILEAUTOW DEALS
  19 =RM2 ...                                    [ WITH FILE BEING COPIED)   
  13 =RPL          JMBAC    WRNEXT1,2,BFFCB,BFMCOP   
  19 =S9=    [              OPEN IN COPY, DON'T REMOVE FCB FROM FILE CHAIN?  
  10 =SNW          BS       2,BFFRITEW   
  16 =T8G ...      TRACEIF  K6CLOSEDIR,99,299,FLOC1(2),FRITE WT  
  20 =TN6 ...      COOR3    FRITEWAIT           [WAIT FOR THE COPIER TO FINISH   
   9 =X72          BRN      WRNEXT2  
   5 =XLL    WRNEXT1 
   9 =Y6=          LDX   2  FPTR(2)  
  18 =YKW ...      BXE   2  CXFI,WREND          [J IF END OF FILE CHAIN  
   8 =_5G ...      SMO      FX2  
   9 =_K6 ...      LDN   0  BWORKRING
  19 ?24Q ...      BXE   2  0,WREND             [J IF END OF WORK FILE RING  
   9 ?2JB ...      LDX   4  ATYPE(2) 
  17 ?342 ...      ANDX  4  HALFTOP             [X4= TYPE OF BLOCK   
  14 ?3HL ...      BXE   4  FILEPLUSFCB,WREND   [J IF FCB
  15 ?43= ...      BXE   4  FFSFUWB,WRITE       [J IF FUWB   
  15 ?47N ...      BXE   4  FFSFURWB,WRITE      [J IF FURWB  
  18 ?4#6 ...      BXU   4  SADATA(1),WRNEXT1   [J IF NOT ADATA/CORUNJAM 
  18 ?4DJ ...      JBC      WRNEXT1,,XBNONAUTOWR [J IF AUTONOMOUS FRITE  
  20 ?4K2 ...      COOR3    #55                 [WAIT FOR CORE UNJAMMER TO FINISH
  19 ?4PD ...                                   [  FIXED CORE WRITE OF BUFFER
   9 ?4TW ...      BRN      WRNEXT2  
   5 ?52G ...WRITE   
  18 ?5G6 ...      CALL  6  WBWRITE             [WRITE AWAY FUWB OR FURWB
   9 ?5_Q ...      BRN      WRNEXT2  
   5 ?6FB ...WREND   
  16 ?6_2 ...      JBC      UP,,XBFRITEALL      [J IF NOT 'ALL'  
   5 ?7DL ...WALL2   
   8 ?7Y= ...      LDX   2  FX2  
  19 ?8CW ...      LDX   0  AWORK1(2)           [X0= DEPTH OF CURRENT FILE   
   5 ?9C6 ...WALL1   
  17 ?9WQ ...      SBN   0  1                   [REDUCE DEPTH BY 1   
  19 ?=BB ...      BPZ   0  WRNEXT3             [J IF MORE FILES TO LOOK AT  
   4 ??T= ...UP  
   5 ?##W ...      UP
   4 ?#SG ...WALL
  20 ?*#6 ...      BS       ,XBFRITEALL         [MARKER TO SAY 'THIS IS FRITE ALL
  14 ?*RQ ...                                   [ CASE'  
  21 ?B?B ...      FILENUMB 0                   [X0= NO. OF FILES OPEN TO THIS ACT.  
   8 ?BR2 ...      BRN      WALL1
   4 ?C=L ...#   
   4 ?XS6 ...#   
   5 ?Y34 ...NOFILE  
  20 ?Y=2 ...      GEOERR   BRIEFPM,NO FILE     [NO FILE OPEN TO CURRENT ACTIVITY
   5 ?YDY ...NOPEN   
  21 ?YMW ...      GEOERR   BRIEFPM,CL NOPEN    [FILE NOT OPEN IN PERMISSIBLE MODE   
   5 ?YWS ...XERRFRBS
  21 ?_3D ...      GEOERR   BRIEFPM,CLOSFRBS    [CLOSEFRBS MACRO & OTHERS RINGED INTO
  17 ?_84 ...                                   [ OR WAITING FOR FILE
   4 ?_#Q    #END
   8 ____ ...46176321000100000000
