  12 22*P ...      SEG      SYSPROP,,LEN WAGSTAFF
  15 22HJ ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983
   8 22PD ...#OPT  K0SYSPROP=0   
  15 2357 ...#LIS  K0PROPERTY>K0PROPSYS>K0ALLGEO>K0SYSPROP   
   4 23DW    #   
  12 23YG          SEGENTRY K1SYSPROP,SYSPROPK1  
   8 24D6    # ENTERED AT E.M.S. 
   4 24XQ    #   
  12 25CB          SEGENTRY K2SYSPROP,SYSPROPK2  
  13 25X2    # ENTRY POINT TO OPEN :SYSTEM.PROPERTY  
   4 26BL    #   
  12 26W=          SEGENTRY K3SYSPROP,SYSPROPK3  
  13 27*W    # ENTRY POINT TO IMPLEMENT PROPAE MACRO 
   4 27TG    #   
  12 28*6          SEGENTRY K4SYSPROP,SYSPROPK4  
  13 28SQ    # ENTRY POINT TO IMPLEMENT PROPCAE MACRO
   4 29#B    #   
   4 29BQ ...#   
  12 29F6 ...#   SHARED FILESTORE ENTRY POINTS   
   4 29HG ...#   
  15 29KW ...      FSHENTRY K5SYSPROP,SYSPROPK5,,SYSPROPK5   
  21 29N= ...# ENTRY POINT TO COLLECT PROPERTY INFORMATION FOR SWITCHABLE PERIPHERALS
   4 29QL ...#   
  15 29T2 ...      FSHENTRY K7SYSPROP,SYSPROPK7,,SYSPROPK7   
  21 29XB ...# ENTRY POINT FOR UNANTICIPATED CLOSEDOWN TO FREE IDFANDPROP SEMAPHORE  
   4 29_Q ...#   
  15 2=46 ...      FSHENTRY K8SYSPROP,SYSPROPK8,,SYSPROPK8   
  16 2=6G ...# NORMAL ENTRY POINT TO FREE IDFANDPROP SEMAPHORE   
   4 2=8W ...#   
   4 2=?L    #   
  10 2=R=    PCENTRAL       12HCENTRAL   
   9 2?=W    PCENTNO        #20000001
   9 2?QG    TWOBITS        #17777777
   8 2#=6    PTAPE          +1   
   8 2#PQ    PCARDS         +4   
   8 2*9B    PRINTER        +2   
   8 2*P2    PDRUM          +9   
   8 2B8L    PHDRUM         +25  
   8 2BN=    PIPC           +28  
   9 2C7W    ONEBIT         #37777777
   9 2CMG    ONE            #40000000
   8 2D76    TWELVE         #7777
   4 2NDQ    #   
  14 2NYB    #     SUBRNUTINE TO OPEN :SYSTEM.PROPERTY   
   5 2PD2    OPENSYS 
   8 2PXL          SBX   7  FX1  
  21 2QC= ...      OPENSYS  ,PROPERTY,GENERAL,CAREFUL [GEOERR IF BREAK-IN OR ERROR   
   8 2Y=Q          ADX   7  FX1  
   7 2YQB          EXIT  7  0
   4 2_=2    #   
  17 2_PL    #     SUBROUTINE TO FINDPROPERTY RECORD-EXIT POINTING TO
  20 329= ...# SECOND WORD OF PAIR FOR SPECIFIC ASSOCIATION.AWORK1-COMMAND WORD  
  13 32NW ...# AWORK2-PROP.NO.,AWORK3-UNIT GEOG.NO.  
   4 338G    #   
   4 33N6    UNIT
   8 347Q          SBX   7  FX1  
   9 34MB          LDX   4  AWORK2(2)
  10 3572          ANDX  4  TWOBITS(1)   
   5 35LL    STEPU   
   6 366=          STEP  
   8 36KW          BZE   3  OUT  
  10 375G          LDX   5  APROPNO(3)   
  10 37K6          ANDX  5  TWOBITS(1)   
   7 384Q          TXU   5  4
   8 38JB          BCS      STEPU
   9 3942          LDX   4  AWORK3(2)
   9 39HL          ORX   4  ONE(1)   
  10 3=3=          LDX   6  ATTCOUNT(3)  
   8 3=GW          BZE   6  U4   
   4 3?2G    U1  
  10 3?G6          TXU   4  APROPATT(3)  
   8 3?_Q          BCC      U2   
   7 3#FB          ADN   3  1
   8 3#_2          BCT   6  U1   
  15 3*DL          BRN      U4                  [NO UNIT AE'D
   4 3*Y=    U2  
   7 3BCW          ADN   3  1
  10 3BXG          LDX   5  APROPATT(3)  
   9 3CC6          ERX   5  AWORK1(2)
   8 3CWQ          SRL   5  12   
   8 3DBB          BZE   5  U3   
   7 3DW2          ADN   3  1
   7 3F*L          SBN   6  1
   8 3FT=          BCT   6  U1   
  14 3G#W    U4    ADX   7  FX1                 [NOT AE'D
   7 3GSG          EXIT  7  1
  19 3H#6    U3    ADX   7  FX1                 [COMMAND WORS B0-B11 EQUAL   
   7 3HRQ          EXIT  7  2
  16 3J?B    OUT   ADX   7  FX1                 [PROP.NO. UNKNOWN
   7 3JR2          EXIT  7  0
   4 3K=L    #   
  17 3KQ=    #     SUBROUTINE TO DO THE STEPAGAIN,STEPWRITE&RESET X3 
   4 3L9W    #   
   5 3LPG    WRITE   
   8 3M96          SBX   7  FX1  
   7 3MNQ          STEPAGAIN 
   7 3N8B          STEPWRITE 
  10 3NN2    W1    TXU   4  APROPATT(3)  
   8 3P7L          BCC      W2   
   7 3PM=          ADN   3  1
   8 3Q6W          BRN      W1   
   7 3QLG    W2    ADN   3  1
   8 3R66          ADX   7  FX1  
   7 3RKQ          EXIT  7  0
   4 3S5B    #   
   4 3SK2    #   
  12 3T4L    # S/R TO PUT CENTRAL PROPERTY RECORD
  13 3TJ=    # IN EACH APERI/APIA OF TYPE GIVEN IN X5
  21 3TPD ...# IF SYSPROP IS NOT OK THEN ALL OTHER PROPERTY ATTRIBUTIONS ARE REMOVED 
  21 3TWL ...# IF TYPE IS TR OR CR THEN ANY PROPERTY ASSOCIATIONS ARE REMOVED INSTEAD
   4 3W3W    #   
   5 3WHG    PCENTSTO
   8 3X36          SBX   7  FX1  
  15 3XGQ ...      FINDPEREC 1,APFOTY,5,RETURN,,PSWOK,PSWOK  
   8 3Y2B ...PSWOK BNG   1  PEXIT
   7 3YG2          LADDP 1,2 
  15 3YJK ...      BZE   4  PSTOR               [SYSPROP OK  
   7 3YM8 ...      LDX   0  5
  21 3YPR ...      BZE   0  PBIP                [TR:CLEAR ANY PROPERTY ASSOCIATIONS  
   7 3YSB ...      SBN   0  3
  21 3YW_ ...      BZE   0  PBIP                [CR:CLEAR ANY PROPERTY ASSOCIATIONS  
  21 3Y_J ...PSTOR STO   6  APERPROP(2)         [STORE ATTRIBUTION FOR PROPERTY CENTR
  15 3_47 ...      BZE   4  RETURN              [SYSPROP OK  
  21 3_6Q ...      LDN   0  ATTMAX-1            [CLEAR ANYOTHER PROPERTY ATTRIBUTIONS
   7 3_9* ...PSTOZ SMO      0
  10 3_?Y ...      STOZ     APERPROP(2)  
   8 3_BH ...      BCT   0  PSTOZ
   9 3_F=          BRN      RETURN   
   9 3_HT ...PBIP  LDN   0  ASSMAX*2 
   7 3_LD ...PCASS SMO      0
  10 3_P3 ...      STOZ     APERPROP-1(2)
   8 3_RL ...      BCT   0  PCASS
   9 3_W9 ...      BRN      RETURN   
   8 3_YW    PEXIT LDX   1  FX1  
   8 42DG          ADX   7  FX1  
   7 42Y6          EXIT  7  0
   4 43CQ    #   
   4 43XB    #   
   9 44C2    #     #   #           # 
   9 44WL    #     #  #            # 
   9 45B=    #     # #             # 
   9 45TW    #     ##              # 
   9 46*G    #     # #             # 
   9 46T6    #     #  #            # 
   9 47#Q    #     #   #           # 
   4 47SB    #   
   4 48#2    #   
  17 48RL    # THIS SECTION CHECKS THE FORMAT OF :SYSTEM.PROPERTY AT 
  17 49?=    # RESTART TIME.IF THE PROPERTY'CENTRAL' DOES NOT EXIST, 
  18 49QW ...# OR THE RECORDS ARE NOT CORRECT FORMAT,A MESSAGE IS OUTPUT 
  18 4==G    # TO THE OPERATOR AND ALL RECORDS ARE DELETED AND A RECORD  
  17 4=Q6    # FOR 'CENTRAL' IS SET UP.ATTRIBUTIONS AND ASSOCIATIONS 
  17 4?9Q    # OF ALL 'TEMPORARY' PROPERTIES ARE CANCELLED;THOSE OF  
  18 4?PB ...# PERMANENT PROPERTIES ARE STORED IN THE APIA.'CENTRAL' IS  
  17 4#NL    # ATTRIBUTED TO ALL SLOW OUTPUT DEVICES,DRUMS AND IPC   
   5 4*8=    # LINKS.
  19 4*=T ...# WE ALSO CHECK PROP.NOS NOT > 32K-1 (IE 15 BITS ONLY ALLOWED   
  19 4**D ...# FROM NOW ON); IF ANY FOUND >32K-1, ALL RECORDS EXCEPT 'CENTRAL
  20 4*D3 ...# ARE DELETED AND OPERATOR INFORMED. RECORDS ARE ALSO SORTED INTO   
  20 4*GL ...# ASCENDING ORDER OF PROP.NO. SINCE PROPERTY WILL FROM NOW ON TAKE  
  17 4*K9 ...# THE LOWEST UNUSED NO. WHEN DEFINING A NEW PROPERTY.   
   4 4*MW    #   
   6 4B7G    SYSPROPK1   
  17 4BM6 ...      OPENSYS  ,PROPERTY,GENERAL,NOWAIT,QUERY,CAREFUL   
   9 4G4G          TESTREP2 OK,OPENED
  21 4GJ6                                       [IF DON'T HAVE TO RESTORE THE FILE -O
  21 4H3Q                                       [OTHERWISE SET THE MODES TO EMPTY IT 
  10 4HHB          TRACE EXEC3(2),NOKPROP
  13 4J32          SETMODE 4,GENERAL,EMPTY,CAREFUL   
  19 4JGL                                       [IF FILE EXISTS GO & OPEN IT 
  21 4K2=                                       [OTHERWISE SET THE MODES TO CREATE IT
  10 4KFW          TESTREP2 MAG,OPENOW   
  13 4K_G          SETMODE 4,GENERAL,CREATE,CAREFUL  
  16 4LF6                                       [OPEN THE FILE   
   5 4LYQ    OPENOW  
  10 4MDB ...      OPENSYSX ,PROPERTY,4  
   5 4PBW    OPENED  
  21 4PWG ...      TOPFCB2  1                   [CAUSE USAGE BLKS TO BE LEFT IN CORE 
   9 4QB6 ...      BS       1,BFCORE 
  17 4QTQ          STEP                         [STEP TO FIRST RECORD
  19 4R*B          BZE   3  NOCENT              [NO RECORDS IN SYSPROP-BRANCH
  16 4RT2          TESTNAMX 3,APROPNAME(3),PCENTRAL(1),NOTCENT   
  21 4S#L                                       [IF'CENTRAL'NOT FIRST RECORD BRANCH  
  10 4SS=          LDX   4  PCENTNO(1)   
  10 4T?W          TXU   4  APROPNO(3)   
  18 4TRG          BCS      NOTCENT             [IF NUMBER NOT 1 BRANCH  
  17 4W?6          ANDX  4  TWOBITS(1)          [UNSET FIRST TWO BITS
  15 4WQQ          STO   4  AWORK1(2)           [STORE NUMBER
   4 4WXW ...#   
  18 4X52 ...# SORTING ALGORITHM. IF :SYS.PROP IN THIS FILESTORE HAS NOT 
  18 4X=6 ...# PREVIOUSLY BEEN SORTED, PROP NOS ARE LIKELY TO BE (A) WELL
  19 4XC= ...# JUMBLED (B)ALL LOW NUMBERS. THE ALGORITHM WAS DESIGNED WITH   
  17 4XJB ...# THIS IN MIND. CONSIDER EXAMPLE:-  1 2 4 5 3 8 13 10 9 
  19 4XPG ...# WE STEP ON FROM 1ST RECORD (CENTRAL), REMEMBERING (IN X5) THAT
  19 4XWL ...# 2 IS THE NEXT POSSIBLE PROPERTY NO. LO & BEHOLD THE PROP NO IS
  20 4Y3Q ...# 2 SO WE UPDATE OUR COUNT OF FINALLY SORTED RECORDS (IN X6) TO 2   
  20 4Y8W ...# WE STEP ON LOOKING FOR 3, BUT FIND 4, IN THIS CASE, WE STEP ALL   
  20 4YB2 ...# THE WAY ALONG AND IF WE FIND 3, WE REMOVE IT, AND REPOSITION IT   
  17 4YH6 ...# AFTER OUR SET OF 'FINALLY SORTED' RECORDS.  NOW HAVE: 
  19 4YN= ...#    1 2 3 4 5 8 13 10 9       WE CARRY ON LIKE THIS, BUT DONT  
  19 4YTB ...# FIND 6 AT ALL. IN THIS CASE WE UPDATE 'NEXT POSSIBLE NO' BY   
  20 4_2G ...# ONE (IN X5), AND REPOSITION OURSELVES AFTER THE SET OF 'FINALLY   
  14 4_7L ...# SORTED' AGAIN TO LOOK FOR 7. ETC ETC,  IE:
  15 4_#Q ...# 1 2 3 4 5 8 9 13 10      1 2 3 4 5 8 9 10 13  
  19 4_FW ...# X7=1 WHEN SEARCHING FOR A PROPNO NOT SO FAR FOUND, 0 OTHERWISE
  20 4_H7 ...      STOZ     AWORK4(2)           [SET NOT -VE TO ASSUME SYSPROP   
  17 4_JD ...                                   [PREVIOUSLY SORTED   
   5 4_KP ...START   
  20 4_M2 ...      LDN   5  1                   [INITIAL VALUES-CURRENT PROPNO   
  19 4_S6 ...      LDN   6  0                   [ - NO OF FINALLY SORTED RECS
  13 4__= ...      LDN   7  0                   [FLAG
   4 526B ...#   
   6 52?G ...OUTERLOOP   
  19 52DL ...      ADN   6  1                   [ANOTHER FINALLY SORTED REC  
   5 52KQ ...NEXTNUM 
  19 52QW ...      ADN   5  1                   [UPDATE NEXT POSSIBLE PROPNO 
   4 52Y2 ...#   
   5 5356 ...SMALLOOP
   6 53== ...      STEP  
  19 53CB ...      BNZ   3  RECGOT              [J ENDOFFILE NOT YET REACHED 
  20 53JG ...      BZE   7  PLIST               [J END REACHED & SORT COMPLETED  
  20 53PL ...      LDN   7  0                   [UNSET FLAG(SINCE PROPNO NOT FND 
  20 53WQ ...      REWIND                       [THIS NUMBER MISSING- GET BACK   
  17 543W ...      LDX   4  6                   [TO WHERE WE WERE... 
   6 5492 ...STEPX STEP  
   8 54B6 ...      BCT   4  STEPX
  18 54H= ...      BRN      NEXTNUM             [...AND TRY FOR NEXT NO. 
   4 54NB ...#   
   5 54TG ...RECGOT  
   9 552L ...      LDX   4  FRH(3)   
   7 557Q ...      SBN   4  7
  19 55#W ...      BNG   4  NOTFORM             [J IF RECORD HAS WRONG FORMAT
  10 55G2 ...      LDX   4  APROPNO(3)   
   9 55M6 ...      ANDX  4  BITS22LS 
   8 55S= ...      LDCT  0  #777 
   7 55_B ...      ANDX  0  4
  17 566G ...      BNZ   0  NOTFORM             [PROP.NO TOO LARGE   
   9 566W ...      LDX   0  AWORK4(2)
  17 567= ...      BNG   0  SORTING             [SYSPROP BEING SORTED
  21 567L ...      LDX   0  4                   [NO SORT REQ IF PROP NO. IS ASCENDING
   7 5682 ...      SBX   0  5
  19 568B ...      STO   4  5                   [UPDATE X5 WITH THIS PROP NO.
  19 568Q ...      BPZ   0  OK1                 [THIS PROP NO. IS ASCENDING  
  20 5696 ...      STO   0  AWORK4(2)           [INDICATE SYSPROP BEING SORTED   
  19 569G ...      REWIND                       [RESTART BUT SORT THIS TIME  
  20 569W ...      STEP                         [(REPOSITION ON PROPERTY CENTRAL)
  21 56== ...      LDCT  4  #400                [(INDICATE SYSPROP NOK+SORT REQUIRED)
  21 56=L ...      BRN      RESETAPIA           [AFTER CLEARING APIA OF AES AND AUS  
   5 56?2 ...SORTING 
   7 56?L ...      LDX   0  5
   7 56DQ ...      SBX   0  4
  18 56KW ...      BPZ   0  THIS1               [J IF THIS IS ONE WE WANT
  20 56R2 ...      LDN   7  1                   [ELSE J BACK TO KEEP SEARCHING   
   9 56Y6 ...      BRN      SMALLOOP 
  20 577S ...THIS1 BNZ   0  NOTFORM             [NO. LOWER THAN CURRENT ONE FOUND
   4 57CG ...#   
  10 57JL ...OK1   LDX   4  APROPNO(3)   
   7 57PQ ...      SLL   4  1
  17 57WW ...      BPZ   4  TEMPPROP            [J IF TEMPORARY PROP 
   4 5842 ...#   
   9 5896 ...      LDX   0  FRH(3)   
   9 58B= ...      STO   0  AWORK1(2)
  20 58HB ...      LDX   1  ATTCOUNT(3)         [IF NO ATTRIBS OR ASSOCS, DONT.. 
  17 58NG ...      BZE   1  XFNAP               [PUT ANYTHING IN APIA
  10 58TL ...      LDX   0  APROPNO(3)   
   9 592Q ...      STO   0  AWORK3(2)
  10 597W ...      LDX   0  APROPGROUP(3)
   9 59*2 ...      STO   0  AWORK2(2)
   5 59GG    RECORD  
  17 5=26          LDX   4  APROPATT(3)         [GET NEXT UNIT NUMBER
  18 5=FQ          BNG   4  PASSOC              [IF ASSOCIATION BRANCH   
  19 5=N7 ...      FINDPEREC 2,APGEOG,4,,,PSWOKB         [FIND DEVICE LIST   
   5 5=WJ ...PSWOKB  
  17 5?53 ...      BNG   2  NOPUT               [IFNOT FOUND BRANCH  
   8 5?8N ...      LTYPE    2,4  
   7 5?#* ...      SBN   4  1
  13 5?D2 ...      BZE   4  OKTY1               [TP  
   7 5?HM ...      SBN   4  1
  13 5?M# ...      BZE   4  OKTY1               [LP  
   7 5?Q_ ...      SBN   4  2
  13 5?WL ...      BZE   4  OKTY1               [CP  
   7 5#2? ...      SBN   4  5
  13 5#5Y ...      BZE   4  OKTY1               [DR  
   8 5#9K ...      SBN   4  16   
  13 5#*= ...      BZE   4  OKTY1               [DR  
   7 5#DX ...      SBN   4  3
  13 5#JJ ...      BNZ   4  NOPUT               [IPC 
   7 5#N9 ...OKTY1 LADDP 2,2 
  20 5#RW ...PP    ADN   2  1                   [ATTRIBUTION- START AT APERPROP+1
   5 5#XW    PERPROP 
  18 5*CG          LDX   4  APERPROP(2)         [FIND APERPROP WORD NOT  
  13 5*TS ...      BNZ   4  PP                  [USED
   8 5B#6 ...      SMO      FX2  
   9 5BQD ...      LDX   0  AWORK3   
   5 5C8Q ...PLISTOK 
  17 5CM4 ...      STO   0  APERPROP(2)         [SET UP APIA ENTRY   
   7 5D5B ...NOPUT ADN   3  1
   9 5DHN ...      BCT   1  RECORD   
  19 5F22 ...      BRN      XFNAP               [FINISHED WITH THIS RECORD   
   5 5FD# ...PASSOC  
   8 5FWL ...      SMO      FX1  
  18 5G#Y ...      ANDX  4  ONEBIT              [ZEROISE ASSOCIATION BIT 
  19 5GH_ ...      FINDPEREC 2,APGEOG,4,,,PSWOKC         [FIND DEVICE LIST   
   5 5GR2 ...PSWOKC  
  17 5H24 ...      BNG   2  NOPUT               [IF NOT FOUND BRANCH 
   8 5H8Q ...      LTYPE    2,4  
  13 5HCD ...      BZE   4  OKTY2               [TR  
   7 5HL6 ...      SBN   4  3
  13 5HSS ...      BNZ   4  NOPUT               [CR  
   7 5J3G ...OKTY2 LADDP 2,2 
   5 5J==    PERPSOC 
  18 5JPW          LDX   4  APERPROP(2)         [FIND APERPROP WORD NOT  
  13 5K9G          BZE   4  POKSOC              [USED
   7 5KP6          ADN   2  1
   9 5L8Q          BRN      PERPSOC  
   5 5LNB    POKSOC  
  10 5M82          LDX   4  APROPATT+1(3)
  17 5MML          STO   4  APERPROP(2)         [STORE COMMAND WORD  
   8 5MTW ...      SMO      FX2  
   9 5N46 ...      LDX   0  AWORK3   
   8 5N=B ...      SMO      FX1  
  16 5NDL ...      ANDX  0  ONEBIT              [ZEROISE EXCL BIT
   8 5NLW          SMO      FX2  
  16 5P6G          LDX   4  AWORK2              [LOAD APROPGROUP 
   9 5PL6          BPZ   4  NOCONSOC 
   8 5PYH ...      SMO      FX1  
  16 5Q=Y ...      ERX   0  ONE                 [SET CONSOLE BIT 
   5 5QKB    NOCONSOC
   7 5R52          ADN   2  1
   7 5RJL          ADN   3  1
   7 5S4= ...      SBN   1  1
   9 5SHW          BRN      PLISTOK  
   4 5SW6 ...#   
  20 5T8B ...XFNAP BZE   7  OUTERLOOP           [J IF RECORD IS IN CORRECT POSN  
   8 5TGL ...      SMO      FX2  
  16 5TSW ...      LDX   4  AWORK1              [RECORD LENGTH   
   9 5W76 ...      BRN      NEWREC   
   4 5WFB ...#   
   5 5WRL ...TEMPPROP
  10 5X5W ...      LDX   0  ATTCOUNT(3)  
  19 5XD6 ...      BNZ   0  SOMAT               [J IF ANY ATTRIBS OR ASSOCS  
  20 5XQB ...      BZE   7  OUTERLOOP           [J IF RECORD IS IN CORRECT POSN  
  19 5Y4L ...SOMAT LDN   4  7                   [SINCE TEMP,RECORDLENGTH=7   
   5 5YBW ...NEWREC  
  19 5YP6 ...      SETUPCORE 4,3,FILE,FWB       [RECONSTRUCT RECORD IN FWB   
   7 5_3B ...      STEPAGAIN 
   7 5_*L ...      LDX   2  3
  10 5_MW ...      MHUNTW   3,FILE,FWB   
   8 6226 ...      ADN   3  A1   
   7 62#B ...      SMO      4
   7 62LL ...      MOVE  2  0
   7 62YW ...      SBN   4  7
   8 63?6 ...      BNZ   4  ZCTOK
   7 63KB ...      LDN   4  7
   9 63XL ...      STO   4  FRH(3)   
  15 649W ...      STOZ     ATTCOUNT(3)          [TO BE SURE 
   6 64J6 ...ZCTOK DELETE
  19 64WB ...      REWIND                       [REPOSITION TO CORRECT PLACE 
  19 658L ...      LDX   4  6                   [TO INSERT REPLACED RECORD   
   7 65GW ...      ADN   4  1
   6 65T6 ...RESET STEP  
   8 667B ...      BCT   4  RESET
   6 66FL ...      INSERT
  10 66RW ...      MHUNTW   1,FILE,FWB   
   7 6766 ...      FREECORE 1
  19 67DB ...      BACKSPACE                    [JUST SO NEXT 'STEP' WORKS OK
  17 67QL ...      LDN   7  0                   [RE-INITIALISE FLAG  
   9 684W ...      BRN      OUTERLOOP
   4 68C6 ...#   
   5 68GX ...PLIST   
  21 68LN ...      LDN   4  0                   [INDICATE TO ^PCENTSTO^ SYSPROP OK   
   5 68QG    PLIST1  
  16 69=6          LDX   6  PCENTNO(1)          [CENTRAL NUMBER  
  15 6=9B          LDX   5  PTAPE(1)            [TAPE PUNCH  
   9 6=P2          CALL  7  PCENTSTO 
  15 6?8L          LDX   5  PCARDS(1)           [CARD PUNCH  
   9 6?N=          CALL  7  PCENTSTO 
  15 6#7W          LDX   5  PRINTER(1)          [LINE PRINTER
   9 6#MG          CALL  7  PCENTSTO 
  13 6*76          LDX   5  PDRUM(1)            [DRUM
   9 6*LQ          CALL  7  PCENTSTO 
  14 6B6B          LDX   5  PHDRUM(1)           [HS DRUM 
   9 6BL2          CALL  7  PCENTSTO 
  13 6C5L          LDX   5  PIPC(1)             [IPC 
   9 6CK=          CALL  7  PCENTSTO 
  17 6CT4 ...      BNG   4  START               [SYSPROP TO BE SORTED
   8 6D4W          BRN      PEND 
   4 6DJG    #   
   5 6F46    NOCENT  
  17 6FHQ          SETNCORE 7,3,FILE,FAPB       [SET UP APPEND BLOCK 
  10 6G3B          LDN   4  PCENTRAL(1)  
  17 6GH2          LDN   5  APROPNAME+A1(3)     [STORE'CENTRAL'NAME  
   7 6H2L          MOVE  4  3
   7 6HG=          LDN   4  7
  17 6H_W          STO   4  FRH+A1(3)           [STORE RECORD HEADER 
  10 6JFG          LDX   4  PCENTNO(1)   
  15 6J_6          STO   4  APROPNO+A1(3)       [STORE NUMBER
   7 6KDQ          LDN   4  0
  17 6KYB          STO   4  APROPGROUP+A1(3)    [ZEROIZE APROPGROUP  
  19 6LD2          STO   4  ATTCOUNT+A1(3)      [SET COUNT OF ATTRIBUTIONS=0 
  16 6LXL          APPEND                       [APPEND RECORD   
   8 6MC=          BRN      PMESS
   5 6MWW    NOTFORM 
  20 6NBG          REWIND                       [GO BACK TO BEGINING OF SYSPROP  
  18 6NW6          STEP                         [STEP PAST'CENTRAL'RECORD
   4 6P*Q    NEXT
  16 6PTB          STEP                         [GET NEXT RECORD 
  17 6Q*2          BZE   3  PMESS               [END OF FILE -BRANCH 
  16 6QSL          DELETE                       [DELETE RECORD   
  15 6R#=          BRN      NEXT                [TRY AGAIN   
   5 6RRW    NEXT1   
  16 6S?G          STEP                         [GET NEXT RECORD 
  18 6SR6          BZE   3  NOCENT              [NON-EXISTENT-SO BRANCH  
   5 6T=Q    NOTCENT 
  16 6TQB          DELETE                       [DELETE RECORD   
  15 6W=2          BRN      NEXT1               [TRY AGAIN   
   4 6WPL    #   
   9 6X9= ...PMESS MONOUT JPROPMESS  
  21 6XNW ...      LDN   4  1                   [INDICATE TO ^PCENTSTO^ SYSPROP NOK  
   6 6Y8G ...RESETAPIA   
  15 6YN6 ...      LDN   5  0                   [TAPE READER 
  20 6_7Q ...      CALL  7  PCENTSTO            [CLEAR TR APIA ENTRIES OF ASSOCS 
  15 6_MB ...      LDN   5  3                   [CARD READER 
  20 7272 ...      CALL  7  PCENTSTO            [CLEAR CR APIA ENTRIES OF ASSOCS 
   9 745G          BRN      PLIST1   
   4 74K6    PEND
   7 74Q# ...      TOPFCB   1
   9 74XG ...      BC       1,BFCORE 
   6 754Q          CLOSE 
   5 75JB          UP
   4 7642    #   
   4 76HL    #   
   4 773=    #   
   9 77GW    #     #   #          ###
  10 782G    #     #  #          #   #   
  10 78G6    #     # #               #   
   9 78_Q    #     ##               #
   9 79FB    #     # #             # 
   9 79_2    #     #  #           #  
  10 7=DL    #     #   #         #####   
   4 7=Y=    #   
   4 7?CW    #   
   9 7?XG    # OPEN :SYSTEM.PROPERTY 
   4 7#C6    #   
   6 7#WQ    SYSPROPK2   
   9 7*BB          CALL  7  OPENSYS  
   5 7*W2          UP
   4 7B*L    #   
   4 7BT=    #   
   4 7C#W    #   
   9 7CSG    #     #   #          ###
  10 7D#6    #     #  #          #   #   
  10 7DRQ    #     # #               #   
   9 7F?B    #     ##              ##
  10 7FR2    #     # #               #   
  10 7G=L    #     #  #          #   #   
   9 7GQ=    #     #   #          ###
   4 7H9W    #   
   4 7HPG    #   
  16 7J96    # IMPLEMENT PROPAE MACRO WHICH SETS UP ASSOCIATIONS 
   4 7JNQ    #   
   6 7K8B    SYSPROPK3   
  10 7KN2          LDX   7  ACOMMUNE1(2) 
   9 7L7L          STO   7  AWORK1(2)
  10 7LM=          LDX   7  ACOMMUNE2(2) 
   9 7M6W          STO   7  AWORK2(2)
  10 7MLG          LDX   7  ACOMMUNE3(2) 
   9 7N66          STO   7  AWORK3(2)
   9 7NKQ          CALL  7  OPENSYS  
   8 7P5B          CALL  7  UNIT 
   8 7PK2          BRN      PENDA
   8 7Q4L          BRN      NEW  
   8 7QJ=          CALL  7  WRITE
   9 7R3W          LDX   5  AWORK1(2)
  10 7RHG          ORS   5  APROPATT(3)  
   9 7S36          BRN      PCLOSE   
   4 7SGQ    NEW 
   7 7T2B          STEPAGAIN 
  10 7TG2          LDX   4  ATTCOUNT(3)  
   7 7T_L          ADN   4  9
  10 7W9D ...      SETUPCORE 4,2,FILE,FWB
   7 7WF= ...      STEPAGAIN 
  10 7WP4 ...      MHUNTW   2,FILE,FWB   
  10 7WYW          LDX   1  ATTCOUNT(3)  
   7 7XDG          ADN   1  6
  10 7XY6          LDN   4  APROPNO(3)   
  10 7YCQ          LDN   5  APROPNO+A1(2)
   8 7YXB          MOVE  4  0(1) 
   7 7_C2          ADN   1  3
   9 7_WL          STO   1  FRH+A1(2)
  11 82B=          ADX   2  ATTCOUNT+A1(2)   
   8 82TW          SMO      FX2  
   9 83*G          LDX   4  AWORK3   
   8 83T6          LDX   1  FX1  
   9 84#Q          ORX   4  ONE(1)   
  11 84SB          STO   4  APROPATT+A1(2)   
   7 85#2          ADN   2  1
   8 85RL          SMO      FX2  
   9 86?=          LDX   4  AWORK1   
  11 86QW          STO   4  APROPATT+A1(2)   
  10 87=G          SBX   2  ATTCOUNT(3)  
   7 87Q6          SBN   2  1
   7 889Q          LDN   4  2
  11 88PB          ADS   4  ATTCOUNT+A1(2)   
   8 8992          BRN      PDEL 
   4 89NL    #   
   4 8=8=    #   
   9 8=MW    #     #   #            #
   9 8?7G    #     #  #            ##
   9 8?M6    #     # #            # #
   9 8#6Q    #     ##            #  #
  10 8#LB    #     # #          #######  
   9 8*62    #     #  #             #
   9 8*KL    #     #   #            #
   4 8B5=    #   
   4 8BJW    #   
  16 8C4G    # IMPLEMENT PROPCAE MACRO WHICH CANCELS ASSOCIATIONS
   4 8CJ6    #   
   6 8D3Q    SYSPROPK4   
  10 8DHB          LDX   7  ACOMMUNE1(2) 
   9 8F32          STO   7  AWORK1(2)
  10 8FGL          LDX   7  ACOMMUNE2(2) 
   9 8G2=          STO   7  AWORK2(2)
  10 8GFW          LDX   7  ACOMMUNE3(2) 
   9 8G_G          STO   7  AWORK3(2)
   9 8HF6          CALL  7  OPENSYS  
   8 8HYQ          CALL  7  UNIT 
   8 8JDB          BRN      PENDA
   8 8JY2          BRN      PENDA
   9 8KCL          LDX   5  AWORK1(2)
  10 8KX=          ERX   5  APROPATT(3)  
   9 8LBW          BZE   5  PBLOCK   
   8 8LWG          CALL  7  WRITE
   9 8MB6          LDX   5  AWORK1(2)
   9 8MTQ          ANDX  5  TWELVE(1)
  10 8N*B          ERS   5  APROPATT(3)  
   9 8NT2          BRN      PCLOSE   
   5 8P#L    PBLOCK  
   7 8PS=          STEPAGAIN 
  10 8Q?W          LDX   6  ATTCOUNT(3)  
   7 8QRG          ADN   6  5
  10 8R3# ...      SETUPCORE 6,2,FILE,FWB
   7 8R?6 ...      STEPAGAIN 
  10 8RGY ...      MHUNTW   2,FILE,FWB   
  10 8RQQ          LDN   4  APROPNO(3)   
  10 8S=B          LDN   5  APROPNO+A1(2)
   7 8SQ2          MOVE  4  6
   9 8T9L          STO   6  FRH+A1(2)
   7 8TP=          SBN   6  7
  11 8W8W          STO   6  ATTCOUNT+A1(2)   
   8 8WNG          BZE   6  PDEL 
   8 8X86          SMO      FX2  
   9 8XMQ          LDX   4  AWORK3   
   9 8Y7B          ORX   4  ONE(1)   
   5 8YM2    XPRATT  
  10 8_6L          TXU   4  APROPATT(3)  
   9 8_L=          BCC      UNITATT  
  10 925W          LDX   5  APROPATT(3)  
  11 92KG          STO   5  APROPATT+A1(2)   
   7 9356          ADN   3  1
   7 93JQ          ADN   2  1
   9 944B          BCT   6  XPRATT   
   8 94J2          BRN      PDEL 
   5 953L    UNITATT 
   7 95H=          LDX   1  6
   8 962W          BZE   1  PDEL 
   7 96GG          ADN   3  2
  10 9726          LDN   4  APROPATT(3)  
  11 97FQ          LDN   5  APROPATT+A1(2)   
   8 97_B          MOVE  4  0(1) 
   4 98F2    PDEL
   7 98YL ...      REPLACE   
   5 99XW    PCLOSE  
   6 9=CG          CLOSE 
   7 9=X6          UPPLUS 1  
   5 9?BQ    PENDA   
   5 9?WB          UP
   4 9#B2    #END
   8 ____ ...33551046000100000000
