  14 22FL          SEG      LIBCOM,6,N.R.BOULT,LIBRARY   
   4 22LS ...[   
  14 22S2 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 
   4 22_=    #   
  12 23DW          SEGENTRY K1LIBCOM,QK1LIBCOM   
  12 23YG          SEGENTRY K2LIBCOM,QK2LIBCOM   
  12 24D6          SEGENTRY K3LIBCOM,QK3LIBCOM   
  12 24XQ          SEGENTRY K4LIBCOM,QK4LIBCOM   
  12 25CB          SEGENTRY K5LIBCOM,QK5LIBCOM   
  12 25X2          SEGENTRY K7LIBCOM,QK7LIBCOM   
  12 26BL          SEGENTRY K8LIBCOM,QK8LIBCOM   
  14 26LD ...      FSHENTRY K9LIBCOM,QK9LIBCOM,,QK9LIBCOM
  21 26W=    #              THIS SEGMENT DEALS WITH MOST OF THE ERROR REPORTING ON TH
  20 27*W    #           PARAMETERS OF THE NEW, GET, RETURN, DEAD, TAPEWRING &   
  21 27TG    #           TAPERIGHT COMMANDS WHICH ENTER THE SEGMENT AT K1, K2, K3, K4
  19 28*6    #           K7 & K8 RESPECTIVELY. THE ERRORS REPORTED ON ARE:-  
  10 28SQ    #              NO PARAMETER 
  13 29#B    #              ERROR IN FORMAT OF NAME  
  16 29S2    #              DEVICE TYPE QUALIFIER MISSING TO NAME
  15 2=?L    #              DEVICE TYPE QUALIFIER NOT (*MT)  
  19 2=R=    #              NAME GIVEN (NEW,DEAD, TAPEWRING & TAPERIGHT ONLY)
  18 2?=W    #              TAPE SERIAL NO. MISSING (AS FOR NAME GIVEN)  
  20 2?QG    #              PROPER USER DOES NOT OWN DIRECTORY (IN USER CONTEXT) 
  21 2#=6    #           IF THERE IS NO ERROR THE SEGMENT OPENS :SYSTEM.SERIAL (EXCEP
  20 2#PQ    #           GET, TAPEWRONG & TAPERIGHT) BEFORE GOING ACROSS TO THE  
  20 2*9B    #           RELEVANT COMMAND. NEW, TAPEWRONG & TAPERIGHT ARE TREATED
  19 2*P2    #           SPECIALLY AS THEY MAY HAVE MORE THEN ONE PARAMETER. 
   8 2B8L    ZNEW           +1   
   8 2BN=    ZGET           +2   
   8 2C7W    ZRETURN        +3   
   8 2CMG    ZDEAD          +4   
   8 2D76    ZREND          +5   
   8 2DLQ    ZTAPEWR        +7   
   8 2F6B    ZTAPERI        +8   
   9 2FL2    WORK           #67202020
   9 2G5L    POOL           #60202020
   7 2G89 ...#UNS  JDUMPTW   
   4 2G=S ...(   
   9 2G*C ...XDUMP          4HDUMP   
   8 2GD2 ...XNDMP          +4   
   4 2GGK ...)   
   4 2GK=    #   
   4 2H4W    #   
   4 2HJG    #   
   6 2J46    QK1LIBCOM   
   9 2JHQ          LDX   7  ZNEW(1)  
   8 2K3B          BRN      TCONA
   6 2KH2    QK2LIBCOM   
   9 2L2L          LDX   7  ZGET(1)  
   8 2LG=          BRN      SUF  
   6 2L_W    QK3LIBCOM   
  10 2MFG          LDX   7  ZRETURN(1)   
   8 2M_6          BRN      SUF  
   6 2NDQ    QK4LIBCOM   
   9 2NYB          LDX   7  ZDEAD(1) 
   5 2PD2    TCONA   
  17 2PXL          LDN   0  #12                 [MOP AND NOUSER BITS 
  10 2QC=          ANDX  0  CONTEXT(2)   
   8 2QWW          BZE   0  SUG  
   8 2RBG          ERN   0  #12  
   8 2RW6          BNZ   0  SUG  
  21 2S*Q          COMERR   JNOMANCON           [COMMAND IN MOP AND NOUSER CONTEXT   
   6 2STB    QK5LIBCOM   
   9 2T*2          LDX   7  ZREND(1) 
   8 2TSL          BRN      SUF  
   6 2W#=    QK7LIBCOM   
  10 2WRW          LDX   7  ZTAPEWR(1)   
   7 2WS? ...#UNS  JDUMPTW   
   4 2WSN ...(   
   9 2WT5 ...      STOZ     AWORK1(2)
   8 2WTG ...      LDN   0  #20  
  10 2WTX ...      ANDX  0  CONTEXT(2)   
   8 2WW# ...      BZE   0  TCON 
  19 2WWP ...[ OPERATOR CONTEXT - IF TWDUMP ON THEN A DUMP PARAMETER MAY HAVE
   7 2WX6 ...[ BEEN SPECIFIED
   9 2WXH ...      SEGENTRY K40LIBCOM
   8 2WXY ...      BRN      SUG  
   7 2WY* ...      PARANOT  1
  10 2WYQ ...      MHUNT    3,CPB,CUNI   
   9 2W_7 ...      LDX   4  ANUM(3)  
   9 2W_J ...      LDX   5  APARA(3) 
   7 2W__ ...      FREECORE 3
   8 2X2B ...      BZE   4  SUG  
   8 2X2R ...      BNG   4  MISS 
   9 2X38 ...      TXU   4  XNDMP(1) 
   8 2X3K ...      BCS      SUG  
   9 2X42 ...      TXU   5  XDUMP(1) 
   8 2X4C ...      BCS      SUG  
  16 2X4S ...[ DUMP PARAMETER SPECIFIED - SET MARKER IN AWORK1   
  16 2X59 ...[ AND UPDATE PARAPASS POINTER PAST FIRST PARAMETER  
   7 2X5L ...      LDN   0  1
   9 2X63 ...      STO   0  AWORK1(2)
   7 2X6D ...      PARAPASS  
   9 2X6T ...      MFREE    CPB,CUNI 
   7 2X7= ...      LDN   4  0
   8 2X7M ...      PARANUMB 4,4  
   7 2X84 ...      SBN   4  1
   8 2X8F ...      BZE   4  MISS 
   7 2X8W ...      ADN   4  1
   8 2X9? ...      BRN      SUGA 
   4 2X9N ...)   
   7 2X=5 ...#UNS  JDUMPTW   
   4 2X=G ...#SKI
   8 2X?G          BRN      TCON 
   6 2XR6    QK8LIBCOM   
  10 2Y=Q          LDX   7  ZTAPERI(1)   
   4 2YQB    TCON
   8 2_=2          LDN   0  #21  
  10 2_PL          ANDX  0  CONTEXT(2)   
   8 329=          BNZ   0  SUG  
   9 32NW          COMERR   JOPUCON  
   7 32W4 ...#UNS  JDUMPTW   
   9 333= ...      SEGENTRY K41LIBCOM
   4 338G    SUG 
   7 33N6          LDN   4  0
  18 347Q          PARANUMB 4,4                 [GET NO. OF PARAMETERS   
   8 34CJ ...      BZE   4  MISS 
   7 34MB          ADN   4  1
   7 34SJ ...#UNS  JDUMPTW   
   4 34_Q ...SUGA
  19 3572          SETUPCORE 4,3,AONBS,GLIST    [SET UP AN AONBS/GLIST BLOCK 
   7 35LL          LDX   2  4
   4 366=    SUG1
   7 36KW          SMO      3
  15 375G          STOZ     A1-1(2)             [AND EMPTY IT
   8 37K6          BCT   2  SUG1 
   4 384Q    SUGL
   8 38JB          CALL  5  PARAM
   9 3942          BRN      NOMORE   
   8 39HL          BRN      SUGL 
  15 3=3=          BRN      SUGL                [TRY AGAIN   
  10 3=GW          MHUNT    1,AONBS,GLIST
  10 3?2G          MHUNT    2,CPB,CUNI   
  10 3?G6          MHUNT    3,FILE,FABSNB
  10 3?_Q          LDX   4  JPARNUM(2)   
   7 3#2Y ...#UNS  JDUMPTW   
   4 3#46 ...(   
   8 3#5# ...      SMO      FX1  
   9 3#6G ...      TXU   7  ZTAPEWR  
   8 3#7N ...      BCS      SUGL2
   8 3#8W ...      SMO      FX2  
   9 3#=4 ...      LDX   0  AWORK1   
   8 3#?= ...      BZE   0  SUGL2
  19 3##D ...[ IF TW DUMP USE ONE LESS THAN JPARNUM FOR PLACING TSN IN GLIST 
   7 3#*L ...      SBN   4  1
   5 3#BS ...SUGL2   
   4 3#D2 ...)   
   8 3#FB          SMO      A1(3)
  17 3#_2          LDX   5  A1+1(3)             [TAPE SERIAL NUMBER  
   5 3*DL    SUG1A   
   7 3*Y=          SBN   4  1
   8 3BCW          BZE   4  SUG1B
   7 3BXG          SMO      4
  10 3CC6          BXU   5  A1(1),SUG1A  
  17 3CWQ          ERROR    ERALGIV             [TSN ALREADY GIVEN   
  10 3DBB          MHUNT    3,FILE,FABSNB
   8 3DW2          BRN      SUG2 
   5 3F*L    SUG1B   
   7 3FFH ...#UNSET   FTS1   
   5 3FKD ...#SKIP   
   4 3FP* ...(   
   8 3FT=          SMO      FX1  
  17 3G#W          BXU   7  ZNEW,SUG1C          [BRANCH IF NOT NEW   
   4 3GJN ...)   
   8 3GSG          SMO      FX2  
   9 3H#6          LDX   0  CONTEXT  
   7 3HRQ          ANDN  0  1
  19 3J?B          BZE   0  SUG1B1              [BRANCH UNLESS USER CONTEXT  
  18 3JR2          CALL  6  OWNCHECK            [PROPER USER OWNS DIR?   
  13 3K=L          BRN      SUGL                [NO  
  10 3KQ=          MHUNT    1,AONBS,GLIST
  10 3L9W          MHUNT    2,CPB,CUNI   
  10 3LPG          MHUNT    3,FILE,FABSNB
   8 3M96          SMO      A1(3)
   9 3MNQ          LDX   5  A1+1(3)  
   8 3N8B          BRN      SUG1C
   5 3NN2    SUG1B1  
   7 3NQK ...#UNSET   FTS1   
   4 3NT8 ...(   
  18 3NXR ...      SMO      FX1                 [NO USER CONTEXT - J IF  
  18 3P2B ...      BXU   7  ZNEW,SUG1C          [ANYTHING OTHER THAN NEW 
   4 3P4_ ...)   
   8 3P7L          SMO   A1(3)   
  14 3PM=          LDCH  0  A1-6(3)             [P OR W ?
   8 3Q6W          SBN   0  #60  
   8 3QLG          BNZ   0  SUG1C
  17 3R66          ORX   5  GSIGN               [B0 SET IF POOL TAPE 
   5 3RKQ    SUG1C   
   7 3RLM ...#UNS  JDUMPTW   
   4 3RMJ ...(   
  10 3RNF ...      LDX   4  JPARNUM(2)   
   8 3RPB ...      SMO      FX1  
   9 3RQ? ...      TXU   7  ZTAPEWR  
   8 3RR8 ...      BCS      SUG2C
   8 3RS5 ...      SMO      FX2  
   9 3RT2 ...      LDX   0  AWORK1   
   8 3RTX ...      BZE   0  SUG2C
  19 3RWS ...[ IF TW DUMP USE ONE LESS THAN JPARNUM FOR PLACING TSN IN GLIST 
   7 3RXP ...      SBN   4  1
   5 3RYL ...SUG2C   
   7 3R_H ...      SMO      4
   4 3S2D ...)   
   7 3S3* ...#UNS  JDUMPTW   
   4 3S4= ...#SKI
  10 3S5B          SMO      JPARNUM(2)   
  15 3SK2          STO   5  A1(1)               [STORE TSN   
   7 3T4L          LDN   0  1
  18 3TJ=          ADS   0  A1(1)               [ADD 1 TO NO. OF TAPES   
   8 3W3W          SMO      FX1  
   9 3WHG          BXE   7  ZNEW,SUG3
   4 3X36    SUG2
   7 3XGQ          FREECORE 3
   4 3Y2B    SUG3
  10 3Y=8 ...      VFREEW   FILE,ADJUNCTS
   9 3YG2          MFREE    CPB,CUNI 
   8 3Y_L          BRN      SUGL 
   5 3_F=    NOMORE  
  10 3_YW          MHUNT    1,AONBS,GLIST
   8 42DG          LDX   0  A1(1)
   8 42Y6          BZE   0  NULL 
   4 432P ...NOM1
  20 435# ...      HUNTW    1,FI,FUTILITY       [ALL ADJUNCTS BLOCKS HAVE BEEN   
  20 437X ...      BNG   1  XIT                 [DISGUISED IN ORDER NOT TO UPSET 
  20 43=G ...      NAME     1,FILE,ADJUNCTS     [FNORM - THEY MUST NOW BE RENAMED
  14 43*5 ...      BRN      NOM1                [ADJUNCTS
   4 43CQ    XIT 
   8 43XB          LDX   1  FX1  
  11 44C2          BXE   7  ZTAPEWR(1),XIT5  
  11 44WL          BXE   7  ZTAPERI(1),XIT5  
  10 45B=          BXE   7  ZNEW(1),XIT1 
  10 45TW          BXE   7  ZGET(1),XIT2 
  10 46*G          BXE   7  ZDEAD(1),XIT4
   6 46R4 ...QK9LIBCOM   
  12 46T6          OPENSYS  ZBRKIN,SERIAL,GENERAL
   9 47#Q          TESTREP  OK,XIT3  
   9 47SB          GEOERR   1,GLIBREP
   4 48#2    XIT1
   4 48CX ...[   
   8 48HS ...      SMO      FX2  
   9 48MP ...      LDX   0  CONTEXT  
   7 48RL ...      ANDN  0  1
  18 48XH ...      BZE   0  XIT1A                [J IF OPERATOR CONTEXT  
   9 493D ...      ACROSS   NEWOWN,1 
   9 497* ...XIT1A ACROSS   NEWPW,1  
   4 49?=    XIT2
   8 49QW          ACROSS   GET,1
   4 4==G    XIT3
  11 4=Q6          BXU   7  ZRETURN(1),XIT4A 
   9 4?9Q          ACROSS   RETURN,1 
   4 4?PB    XIT4
   9 4#92          ACROSS   DEAD,1   
   5 4#NL    XIT4A   
  10 4*8=          ACROSS   RENAMDIR,1   
   4 4*MW    XIT5
  10 4B7G          ACROSS   TAPERING,1   
   5 4BM6    ZBRKIN  
   7 4C6Q          COMBRKIN  
   4 4CLB    SUF 
  20 4D62          CALL  5  PARAM               [GET NEXT ACCEPTABLE PARAMETER   
   8 4DKL          BRN      MISS 
   8 4F5=          BRN      NULL 
   8 4FJW          BRN      MISS 
   9 4G4G          CALL  6  OWNCHECK 
   8 4GJ6          BRN      NULL 
   8 4H3Q          BRN      XIT  
   4 4HHB    MISS
   9 4J32          COMERR   JPARMIS  
   4 4JGL    NULL
   6 4K2=          ENDCOM
  21 4KFW    #     THIS SUBROUTINE PASSES THE NEXT PARAMETER IN A FILE/FABSNB BLOCK. 
  19 4K_G    #     ON ENTRY X5 IS LINK AND X7 HOLDS ENTRY POINT IDENTIFIER.  
  19 4LF6    #     EXIT AT LINK ADDRESS+1 IF PARAMETER NULL OR UNSUITABLE.   
  21 4LYQ    #     EXIT AT LINK ADD.+2 IF PARAMETER PASSED (FILE/FABSNB & CPB/CUNI). 
  15 4MDB    #     EXIT AT LINK ADD. IF NO MORE PARAMETERS.  
   5 4MY2    PARAM   
   8 4NCL          SBX   5  FX1  
  17 4NX=          SPARAPAS                     [GET NEXT PARAMETER  
  10 4PBW          MHUNT    3,CPB,CUNI   
   9 4PWG          LDX   0  ANUM(3)  
  18 4QB6          BZE   0  PNULL               [BRANCH IF NULL PARAMETER
  17 4QTQ          BNG   0  PEND                [BRANCH IF NO MORE   
  10 4R*B          NAMETOP  3,FILE,FNAME 
   7 4RT2          FNORM    7
  10 4S#L          MHUNT    3,FILE,FNAME 
  10 4SS=          NAMETOP  3,CPB,CUNI   
  18 4T6M ...      TESTREP  NAMEFORM,PNAMEF     [BRANCH IF FORMAT ERROR  
   7 4TF4 ...      PICKREP  6
  10 4TRG          LDX   4  JPARNUM(3)   
  11 4W?6          BXE   7  ZGET(1),PARAM0B  
   5 4WQQ    PARAM0  
  10 4X=B          HUNT     3,FILE,FTRAP 
   9 4XQ2          BNG   3  PARAM0B  
  11 4Y9L          BXU   7  ZNEW(1),PARAM0A  
  19 4YP=          NAMETOP  3,AONBS,GROT        [SAVE FOR APPROPRIATE MESSAGE
   9 4_8W          DSA   4  ATYPE(3) 
   9 4_NG          BRN      PARAM0   
   5 5286    PARAM0A 
   7 52MQ          FREECORE 3
   9 537B          BRN      PARAM0   
   5 53M2    PARAM0B 
   7 542P ...      PUTREP   6
  11 54BD ...      TESTREPN ADJUNCTS,PERR1   
  11 54Q7 ...      MHUNTW   3,FILE,ADJUNCTS  
   9 555W          LDX   0  A1+2(3)  
   8 55KG          SRL   0  15   
  21 5656          BZE   0  PERR1               [BRANCH IF NO DEVICE TYPE QUALIFIER  
   7 56JQ          SBN   0  5
  17 574B          BNZ   0  PERR2               [BRANCH IF NOT *MT   
   9 57J2          LDX   0  A1+2(3)  
   7 583L          ANDN  0  2
  18 58H=          BZE   0  PARAM0C             [BRANCH IF NO PROPERTIES 
  11 592W          BXE   7  ZNEW(1),PARAM0D  
  11 59GG          BXE   7  ZGET(1),PARAM0D  
  11 5=26          BXE   7  ZREND(1),PARAM0D 
   9 5=FQ          BRN      PARAM0E  
   5 5=_B    PARAM0D 
  19 5?F2          MTEXPROP TAPE                [EXTRACT SPECIAL PROPERTIES  
  14 5?YL          TESTREP  ILLCOMB,PERR10,ILLMODE,PERR11
  10 5#D=          LDX   3  ACOMMUNE1(2) 
  10 5#XW          SETNCORE 1,2,FLIB,FSPD
   9 5*CG          DSA   4  ATYPE(2) 
  17 5*X6          STO   3  A1(2)               [STORE CHARACTERISTIC
   9 5BBQ    #SKI  K6LIBCOM>199-199  
   9 5BWB          SWITRACE 3,LBCMSPD
   5 5CB2    PARAM0E 
  10 5CTL          MFREE    FILE,FPROPS  
   5 5D*=    PARAM0C 
   7 5D*N ...#UNSET   FTS1   
   4 5DB6 ...(   
   8 5DBJ ...      SMO      FX2  
   9 5DC2 ...      LDX   2  CONTEXT  
  17 5DCD ...      ANDN  2  1                   [B23 = USER CONTEXT  
  18 5DCW ...      BZE   2  PARAMNU             [J IF NOT USER CONTEXT   
  10 5DD# ...      MHUNT    2,FILE,FABSNB
   9 5DDQ ...      LDX   4  ATYPE(2) 
   8 5DF8 ...      ANDN  4  #200 
  16 5DFL ...      BNZ   4  PARAMNU             [J IF WORKTAPE   
  18 5DG4 ...      LDX   3  ALOGLEN(2)          [SET X3=LENGTH OF FABSNB 
  18 5DGG ...      SETUPCORE 3,2,FILE,FABSNB    [CREATE DUPLICATE FABSNB 
  11 5DGY ...      HUNT2    1,FILE,FABSNB,2  
   9 5DHB ...      LDX   4  ATYPE(1) 
   9 5DHS ...      STO   4  ATYPE(2) 
   8 5DJ= ...      LDN   3  A1(1)
   8 5DJN ...      LDN   4  A1(2)
  10 5DK6 ...      SMO      ALOGLEN(2)   
   7 5DKJ ...      MOVE  3  0
  18 5DL2 ...      LDN   4  6                   [ADJUST RECORD HEADERIN  
  18 5DLD ...      SBS   4  A1(2)               [FILE-FABSNB FOR OPENING 
  18 5DLW ...      OPEN     ZBRKIN,READ,QUERY   [OF LOCAL USER DIRECTORY 
   6 5DM2 ...XFSHNBOPEN  
  18 5DM# ...      MFREE    FILE,FABSNB         [FREE THE EXTENDED FABSNB
  14 5DMQ ...      TESTREP  OK,PARAMFZ          [OPEN OK?
   7 5DN8 ...      REPALLERR 
  10 5DNC ...      VFREEW   FILE,ADJUNCTS
   8 5DNL ...      BRN      PNEXT
   5 5DP4 ...PARAMFZ 
  16 5DPG ...      TOPFCB   2                   [X2 POINT TO FCB 
  19 5DPY ...      JFREZFCB 2,PERR12            [CHECK FOR FROZEN QUALIFIER  
  16 5DQB ...      CLOSETOP                     [CLOSE DIRECTORY 
   4 5DQS ...)   
   7 5DR= ...#UNSET   FTS1   
   5 5DRN ...#SKIP   
  10 5DSW          MFREE    FILE,ADJUNCTS
   5 5F24 ...PARAMNU 
  15 5F7= ...      LDX   2  FX2                 [RESTORE X2  
  10 5F#G          MHUNT    3,FILE,FABSNB
   8 5FS6          NGN   0  #101 
  19 5G?Q          ANDS  0  ATYPE(3)            [MAKE SURE LANGUAGE NOT GIVEN
   9 5GRB          LDX   4  ATYPE(3) 
  14 5H?2          ANDN  4  #2000               [TSN BIT 
  16 5HQL          BZE   4  PARAM1              [BRANCH IF NO TSN
   8 5J==          SMO      A1(3)
   9 5JPW          LDX   0  A1+1(3)  
  17 5K9G          BNG   0  PERR5               [BRANCH IF XENOTAPE  
   5 5KP6    PARAM1  
  16 5L8Q          BXE   7  ZRETURN(1),PARAM2B  [BRANCH IF RETURN
  19 5LNB          BXU   7  ZGET(1),PARAM2      [BRANCH UNLESS GET COMMAND   
  16 5M82          BNZ   4  PERR6               [BRANCH IF TSN   
  18 5M?X ...      LDX   0  ACES                [OVERWRITE LANGUAGE CODE 
  15 5MCS ...      SMO      HDREC(3)            [WITH SPACES 
   9 5MHP ...      STO   0  A1-1(3)  
   9 5MML          BRN      PARAM2B  
   5 5N7=    PARAM2  
  16 5NLW          BZE   4  PERR3               [BRANCH IF NO TSN
   9 5P6G          LDX   0  ATYPE(3) 
   8 5PL6          ANDN  0  #200 
  17 5Q5Q          BNZ   0  PERR8               [BRANCH IF WORKFILE  
   5 5QKB    PARAM2B 
   9 5R52          LDX   0  ATYPE(3) 
   8 5RJL          ANDN  0  #4000
  16 5S4=          BNZ   0  PERR8               [BRANCH IF STREAM
  11 5SHW          BXE   7  ZGET(1),PARAM4   
  11 5T3G          BXE   7  ZRETURN(1),PARAM4
  11 5TH6          BXE   7  ZNEW(1),PARAM2A  
  11 5W2Q          BXE   7  ZREND(1),PARAM3  
   8 5WGB          LDX   0  A1(3)
   8 5X22          SBN   0  10   
  17 5XFL          BNZ   0  PERR4               [BRANCH IF NAME GIVEN
   9 5X_=          LDX   0  A1+4(3)  
  17 5YDW          BNZ   0  PERR4               [BRANCH IF NAME GIVEN
   9 5YYG          BRN      PARAM4   
   5 5_D6    PARAM2A 
  10 5_XQ          LDX   0  CONTEXT(2)   
   7 62CB          ANDN  0  1
  18 62X2          BNZ   0  PARAM4              [BRANCH IF USER CONTEXT  
   8 63BL          LDX   0  A1(3)
   8 63W=          SBN   0  10   
   8 64*W          BNZ   0  PERR9
   9 64TG          LDX   0  ATYPE(3) 
   8 65*6          ANDN  0  #4700
   8 65SQ          BNZ   0  PERR9
   9 66#B          LDX   0  A1+4(3)  
  15 66S2          BZE   0  PARAM4              [CODE OMITTED
   9 67?L          LDX   4  ATYPE(3) 
   8 67R=          ANDN  4  #1000
   8 68=W          BZE   4  PERR9
  11 68QG          BXE   0  POOL(1),PARAM4   
  11 69=6          BXE   0  WORK(1),PARAM4   
   8 69PQ          BRN      PERR9
   5 6=9B    PARAM3  
   9 6=P2          LDX   0  ATYPE(3) 
   8 6?8L          ANDN  0  #1000
  20 6?N=          BZE   0  PERR7               [BRANCH IF NOT GENERAL LOCAL NAME
   5 6#7W    PARAM4  
   8 6#MG          ADX   5  FX1  
   7 6*76          EXIT  5  3
  10 6*LQ    #     OUTPUT ERROR MESSAGE  
  20 6B6B    PERR1                              [DEVICE TYPE QUALIFIER MISSING   
   9 6BL2          LDX   3  ZAMISS(1)
  10 6C5L          LDX   4  ZATYPEQ(1)   
   8 6CK= ...      BRN      PERX 
  21 6D4W    PERR2                              [DEVICE TYPE QUALIFIER IMPERMISSIBLE 
   9 6DJG          LDX   3  ZIMPER(1)
  10 6F46          LDX   4  ZATYPEQ(1)   
   8 6FHQ          BRN      PERR 
  19 6G3B    PERR3                              [TAPE SERIAL NUMBER MISSING  
   9 6GH2          LDX   3  ZAMISS(1)
   9 6H2L          LDX   4  ZERNOP(1)
   8 6HG=          BRN      PERR 
  17 6H_W    PERR4                              [SHOULD BE TSN ONLY  
  10 6JFG          LDX   3  ZAMTDESC(1)  
   9 6J_6          LDX   4  ZJTSN(1) 
   8 6KDQ          BRN      PERR 
   5 6KYB    PERR5   
   9 6LD2          LDX   3  ZERXE(1) 
   7 6LXL          NGN   4  1
   8 6MC=          BRN      PERR 
  19 6MWW    PERR6                              [TSN GIVEN FOR GET COMMAND   
  10 6NBG          LDX   3  ZAMTDESC(1)  
   9 6NW6          LDX   4  ZJNTSN(1)
   8 6P*Q          BRN      PERR 
   5 6PTB    PERR7   
  10 6Q*2          LDX   3  ZAMTDESC(1)  
   9 6QSL          LDX   4  ZJLOCN(1)
   8 6R#=          BRN      PERR 
   5 6RRW    PERR8   
  21 6S?G          LDX   3  ZERNAM(1)           [FORMAT ERROR  (+ OR ! IN MT DESC)   
   7 6SR6          NGN   4  1
   8 6T=Q          BRN      PERR 
   5 6TQB    PERR9   
  10 6W=2          LDX   3  ZCODEWR(1)   
   7 6WPL          NGN   4  1
   8 6X9=          BRN      PERR 
   5 6XNW    PERR10  
   9 6Y8G          LDX   3  ZPILL(1) 
   7 6YN6          NGN   4  1
   8 6_7Q          BRN      PERR 
   5 6_MB    PERR11  
   9 7272          LDX   3  ZPMOD(1) 
   7 72LL          NGN   4  1
   7 72N* ...#UNSET   FTS1   
   4 72Q4 ...(   
   8 72RR ...      BRN      PERR 
   5 72TG ...PERR12  
  18 72X9 ...      CLOSETOP                             [CLOSE DIRECTORY 
  10 72Y4 ...      MFREEW   FILE,ADJUNCTS
  18 72YY ...      ERRTEST  3,PNEXT,FROZEN,NOFPRIV      [ERROR MESSAGE   
  10 732M ...      GEOERR   1,LIBFRREP   
   4 734B ...)   
   4 736=    PERR
  10 73?D ...      MFREEW   FILE,ADJUNCTS
   4 73DL ...PERX
  20 73KW          ERRORX   3,4                 [OUTPUT APPROPRIATE ERROR MESSAGE
   5 745G    PNEXT   
  10 74K6          MFREE    FILE,FABSNB  
  10 754Q          MHUNT    3,CPB,CUNI   
   5 75JB    PNEXT1  
   7 7642          ADN   5  1
   4 76HL    PEND
   7 773=          FREECORE 3
   8 77GW          ADX   5  FX1  
   7 782G          EXIT  5  0
   5 78G6    PNULL   
   7 78_Q          ADN   5  1
   9 79FB          BRN      PNEXT1   
   5 79K? ...PNAMEF  
  19 79P8 ...      VFREEW   FILE,ADJUNCTS       [FNORM MAY LEAVE AN ADJUNCTS 
  19 79T5 ...      BRN      PNEXT1              [  EVEN THOUGH IT SHOULDN'T  
  11 79_2    #     ERROR MESSAGE IDENTIFIERS 
   9 7=DL    ZAMISS         +AMISS   
   9 7=Y=    ZATYPEQ        +ATYPEQ  
   9 7?CW    ZIMPER         +IMPER   
   9 7?XG    ZAMTDESC       +AMTDESC 
   8 7#C6    ZJTSN          +JTSN
   9 7#WQ    ZERNOP         +ERNOP   
  10 7*BB    ZERXE          +ERXENOSER   
   9 7*W2    ZJNTSN         +JNTSN   
   9 7B*L    ZJLOCN         +JLOCN   
  10 7BT=    ZERNAM         +ERNAMEFORM  
   9 7C#W    ZCODEWR        +ERCODEWR
  10 7CSG    ZPILL          +ERILLCOMB   
  10 7D#6    ZPMOD          +ERILLMODE   
  20 7DRQ    #              SUBROUTINE TO CHECK WHETHER PROPER USER OWNS ENTRANT 
  20 7F?B    #     EXIT AT LINK ADDRESS IF TAPE DOES NOT BELONG TO PROPER USER.  
  18 7FR2    #     EXIT AT LINK ADDRESS+1 IF TAPE BELONGS TO PROPER USER.
  14 7G=L    #     X6 IS THE LINK ACC. AND X7 IS NOT USED
   5 7GQ=    OWNCHECK
   8 7H9W          SBX   6  FX1  
  10 7HPG          MHUNT    2,FILE,FABSNB
   9 7J96          LDX   0  ATYPE(2) 
   8 7JNQ          ANDN  0  #200 
  18 7K8B          BNZ   0  OWN                 [BRANCH IF NAMED WORKTAPE
   8 7KN2          LDX   0  A1(2)
   8 7L7L          SBN   0  10   
  21 7LM=          BNZ   0  OWN1                [BRANCH IF LOCAL NAME OF DIR GIVEN   
  17 7M6W          LDX   3  A1+1(2)             [X3,4,5=NAME OF DIR  
   9 7MLG          LDX   4  A1+2(2)  
   9 7N66          LDX   5  A1+3(2)  
  16 7NKQ          CJOBLOCK 2                   [FIND JOB BLOCK  
   9 7P5B          TXU   3  JUSER(2) 
  10 7PK2          TXU   4  JUSER+1(2)   
  10 7Q4L          TXU   5  JUSER+2(2)   
  17 7QJ=          BCC      OWN                 [BRANCH IF PROPER DIR
  11 7R3W          SETNCORE 4,2,FILE,FABSNB  
   7 7RHG          LDN   0  4
   8 7S36          STO   0  A1(2)
   9 7SGQ          STO   3  A1+1(2)  
   9 7T2B          STO   4  A1+2(2)  
   9 7TG2          STO   5  A1+3(2)  
   7 7T_L          CHECKOWNER
   4 7WF=    OWN2
   9 7WYW          TESTREP  OK,OWN   
  10 7XDG          TESTREPN NOFILE,OWN2A 
   8 7XY6          SETREP   NAME 
   5 7YCQ    OWN2A   
  11 7YXB          TESTREPN NOTOWNER,OWN2C   
  11 7_C2          BXE   7  ZTAPEWR(1),OWN2B 
  11 7_WL          BXE   7  ZTAPERI(1),OWN2B 
  11 82B=          BXU   7  ZREND(1),OWN2C   
   5 82TW    OWN2B   
   9 83*G          ERROR    ERNOTOWN 
   8 83T6          BRN      OWN3 
   5 84#Q    OWN2C   
  21 84M7 ...      ERRTEST  3,OWN3,LOST,TEMPLOST,NAME,NOUSER,VRYWRONG,NOTOWNER,WRONG-
  12 84_J ...               ,NOINFACC,NOALLACC   
   9 85#2          GEOERR   1,LIBREP 
   4 85RL    OWN3
   9 86?=          MFREE    CPB,CUNI 
  10 86QW          MFREE    FILE,FABSNB  
   7 86W2 ...#UNSET   FTS1   
   4 86_6 ...(   
  10 874= ...      MFREEW   FILE,ADJUNCTS
   4 877B ...)   
   8 87=G          ADX   6  FX1  
  20 87Q6          EXIT  6  0                   [PROPER USER DOES NOT OWN ENTRANT
   4 889Q    OWN 
   7 88#W ...#UNSET   FTS1   
   4 88D2 ...(   
   8 88DD ...      LDX   1  FX1  
  20 88FQ ...      BXE   7  ZRETURN(1),OWNYF    [NO ADJUNCTS NEEDED FOR RETURN   
  15 88G8 ...      BXU   7  ZREND(1),OWNNF      [OR RENAMDIR 
   5 88GL ...OWNYF   
  10 88H4 ...      MFREEW   FILE,ADJUNCTS
   9 88HG ...      BRN      OWNEND   
   5 88HY ...OWNNF   
  11 88JB ...      BXE   7  ZGET(1),OWNEND   
  20 88JS ...      MHUNTW   1,FILE,ADJUNCTS     [WHERE MULTIPLE PARAMS ALLOWED   
  21 88K= ...      NAME     1,FI,FUTILITY       [ADJUNCTS NEEDS TEMPORARY DISGUISE   
   5 88KN ...OWNEND  
   4 88L= ...)   
   8 88PB          ADX   6  FX1  
  18 8992          EXIT  6  1                   [PROPER USER OWNS ENTRANT
   4 89NL    OWN1
   7 8=8=          LDN   0  6
   8 8=MW          SBS   0  A1(2)
  13 8?7G          OPEN     ZBRKIN,READ,LEAVE,QUERY  
  10 8?M6          MHUNT    3,FILE,FABSNB
   7 8#6Q          LDN   0  6
   8 8#LB          ADS   0  A1(3)
   9 8*62          TESTREPN OK,OWN2  
  10 8*KL          MHUNT    1,FILE,ENT   
  10 8B5=          MHUNT    2,FILE,FABSNB
   8 8BJW          ADN   1  EUSE1
   8 8C4G          LDX   0  A1(2)
   8 8CJ6          LDN   3  10   
  15 8D3Q          STO   3  A1(2)               [NEW HEADER  
   8 8DHB          ADN   2  A1+1 
  15 8F32          MOVE  1  3                   [USER NAME   
   8 8FGL          LDN   3  3(2) 
   7 8G2=          SBN   2  7
   7 8GFW          ADX   2  0
   7 8G_G          MOVE  2  8
   8 8HF6          SBN   1  EUSE1
   7 8HYQ          FREECORE 1
  10 8JDB          MHUNT    2,FILE,FABSNB
  11 8JY2          ALTLEND  2,12,FILE,FABSNB 
   7 8KCL          OWNERCHEC 
   7 8KX=          PICKREP  3
   7 8LBW          CLOSETOP  
   7 8LWG          PUTREP   3
   8 8MB6          BRN      OWN2 
  10 8MTQ          MENDAREA 50,K99LIBCOM 
   4 8N*B    #END
   8 ____ ...06517471000200000000
