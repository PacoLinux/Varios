  13 22FL          SEG      MAKEVCC,,R.WEYAND COMM   
  15 22PD ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  19 22_=          SEGENTRY K1MAKEVCC,Z1MAKEVCC [IMPLEMENTS MAKEVCC COMMAND  
  21 23DW          SEGENTRY K2MAKEVCC,Z2MAKEVCC [IMPLEMENTS CANCEL MAKEVCC COMMAND   
   4 23YG    #   
  10 24D6    #              CONVENTIONS :
  16 24XQ    #              PRESET LABELS ARE PREFIXED WITH 'P'  
  17 25CB    #              SUBROUTINE LABELS ARE PREFIXED WITH 'S'  
  21 25X2    #              PROGRAM LABELS ARE PREFIXED WITH 'V'(FOR VIRTUAL 7900)   
  16 26BL    #              ERROR LABELS ARE PREFIXED WITH 'X'   
   4 26W=    #   
   4 27*W    [   
   8 27TG    [     PRESET DATA   
   4 28*6    [   
  11 28SQ    #     REMOTE DEVICE TYPE NUMBERS
   7 29#B    #DEF  PTTDT = 0 
   7 29S2    #DEF  PLPDT = 3 
   7 2=?L    #DEF  PCRDT = 4 
   7 2=R=    #DEF  PTWDT = 5 
   7 2?=W    #DEF  PTRDT = 6 
   7 2?QG    #DEF  PTPDT = 7 
   7 2#=6    #DEF  PVDDT = 10
   4 2#PQ    #   
   8 2*9B    #DEF  PTYPE7900 = 24
  15 2*P2    PUNITRANGE     256,512             [512 = MAX+1 
   9 2B8L    PCPREFIX       12HSYSTEM
   8 2BN=    PBASICFILE     #400 
   9 2C7W    PFILETERM      4H****   
   7 2CMG    PMINREC        4
   8 2D76    PSPACE         #20  
   9 2DLQ    PIDENPREFIX    4H000I   
  15 2F6B    PIDENMAX       4096                [4096 = MAX+1
   9 2FL2    PDTTTW         4HTTW0   
   9 2G5L    PDTLP          4HLP00   
   9 2GK=    PDTCR          4HCR00   
   9 2H4W    PDTTW          4HTW00   
   9 2HJG    PDTTR          4HTR00   
   9 2J46    PDTTP          4HTP00   
   9 2JHQ    PDTVDU         4HVDU0   
  17 2K3B    PST7502        4                   [LENGTH OF SUBTYPE   
  21 2KH2                   #00010000           [BIT 11 : 7502 SOFT FORMAT LOOP FLAG 
  16 2L2L                   4H7502              [SUBTYPE MNEMONIC
   7 2LG=    PSTSPOOLING    8
  19 2L_W                   #00020000           [BIT 10 : SPOOLING CAPABILITY
  10 2MFG                   8HSPOOLING   
  19 2M_6 ...PAV7900        +AV7900E            [VIRTUAL 7900 %A NOW EXISTS  
  20 2NDQ    PAV7900X       +AV7900X            [VIRTUAL 7900 %A NOW NON-EXISTENT
   4 2NYB    [   
   8 2PD2    [     SUBROUTINES   
   4 2PXL    [   
   4 2QC=    #   
  19 2QWW    #     SUBROUTINE TO SEARCH A STRING OF CHARACTERS FOR THE FIRST 
  10 2RBG    #     NON-SPACE CHARACTER . 
  21 2RW6    #     ON ENTRY X3 -> START OF STRING ; X6 = LENGTH OF STRING IN CHARS   
  15 2S*Q    #     IF A NON-SPACE CHAR NOT FOUND,EXITS +0 .  
  20 2STB    #     IF A NON-SPACE CHAR FOUND,EXITS +1 WITH X3 -> NON-SPACE CHAR ;
  19 2T*2    #     X6 = LENGTH OF STRING REMAINING INCLUDING THE NON-SPACE ; 
  11 2TSL    #     X0 = THE NON-SPACE CHAR . 
   8 2W#=    #     LINK IN X7 .  
   6 2WRW    SNONSPACE   
   4 2X?G    #   
  21 2XR6          BZE   6  (7)                 [NON-SPACE CHAR NOT FOUND : EXIT +0  
   8 2Y=Q          LDCH  0  0(3) 
  12 2YQB          BXE   0  PSPACE(1),SNEXTCHAR  
  21 2_=2          EXIT  7  1                   [NON-SPACE CHARACTER FOUND : EXIT +1 
   6 2_PL    SNEXTCHAR   
   7 329=          SBN   6  1
   9 32NW          BCHX  3  SNONSPACE
   4 338G    #   
   4 33N6    #   
  21 347Q    #     SUBROUTINE TO INCREMENT A CHARACTER INDEX WORD AND TO DECREMENT A 
  21 34MB    #     COUNT WORD BY A SPECIFIED NUMBER. XJCONFERR IF COUNT GOES -VE .   
  18 3572    #     ON ENTRY X3 = CHARACTER INDEX WORD TO BE INCREMENTED ;
  17 35LL    #     X6 = COUNTER TO BE DECREMENTED ; X0 = THE NUMBER .
   8 366=    #     LINK IN X7 .  
   6 36KW    SUPDPTRCT   
   7 375G          SLC   3  2
   7 37K6          ADX   3  0
   7 384Q          SRC   3  2
   7 38JB          SBX   6  0
   9 3942          BNG   6  XJCONFERR
   7 39HL          EXIT  7  0
   4 3=3=    #   
   4 3=GW    #   
  21 3?2G    #     SUBROUTINE TO CONVERT A DECIMAL CHAR STRING IN RANGE 1 TO 4095 TO 
  20 3?G6    #     BINARY FORM. XJCONFERR IF DECIMAL INTEGER IS NOT IN RANGE .   
  16 3?_Q    #     ON ENTRY X3 -> START OF DECIMAL CHAR STRING ; 
  14 3#FB    #     X6 >= LENGTH OF DECIMAL CHAR STRING . 
  21 3#_2    #     ON EXIT X0 = THE BINARY INTEGER ; X3 -> FIRST NON-NUMERIC CHAR ;  
  13 3*DL    #     X6 = LENGTH OF STRING REMAINING . 
   8 3*Y=    #     LINK IN X7 .  
   4 3BCW    #   
   4 3BXG    SCDB
   8 3CC6          STO   4  GEN4 
   8 3CWQ          STO   5  GEN5 
   7 3DBB          LDN   4  0
   7 3DW2          LDN   5  0
   5 3F*L    SCDBCHAR
  16 3FT=          BZE   6  SCDBTEST            [STRING EXHAUSTED
   8 3G#W          CDB   4  0(3) 
  21 3GSG          BCS      SCDBTEST            [STRING TERMINATED BY A NON-NUMERIC  
   7 3H#6          SBN   6  1
  17 3HRQ          BCHX  3  SCDBCHAR            [CONVERT NEXT CHAR   
   5 3J?B    SCDBTEST
  20 3JR2          BNZ   4  XJCONFERR           [BINARY NUMBER IS DOUBLE-LENGTH  
  18 3K=L          BZE   5  XJCONFERR           [BINARY NUMBER IS ZERO   
  18 3KQ=          BXGE  5  PIDENMAX(1),XJCONFERR[BINARY NUMBER IS > 4095
   7 3L9W          STO   5  0
   8 3LPG          LDX   4  GEN4 
   8 3M96          LDX   5  GEN5 
   7 3MNQ          EXIT  7  0
   4 3N8B    #   
   4 3NN2    #   
  19 3P7L    #     SUBROUTINE TO TEST IF A STRING MATCHES A SPECIFIED SUBTYPE
  16 3PM=    #     AND IF TRUE SETS A SPECIFIED FLAG IN AWORK2 . 
   4 3Q6W    #   
  21 3QLG    #     ON ENTRY X3 -> START OF STRING ; X6 = LENGTH OF STRING IN CHARS ; 
  17 3R66    #     X1 -> SUBTYPE DETAILS OF THE SUBTYPE TO TEST FOR ;
  20 3RKQ    #     SUBTYPE DETAILS : WORD 0 = LENGTH OF SUBTYPE, WORD 1 = SUBTYPE
  18 3S5B    #     FLAG TO BE SET IN AWORK2, WORD 2 = SUBTYPE MNEMONIC . 
  18 3SK2    #     EXITS +0 IF FALSE WITH X3 AND X6 UNCHANGED, X1 =FX1 . 
  21 3T4L    #     EXITS +1 IF TRUE WITH X3 -> FIRST CHAR FOLLOWING THE SUBTYPE FIELD
  16 3TJ=    #     AND X6 = LENGTH OF FIELD REMAINING, X1 = FX1 .
   8 3W3W    #     LINK IN X7 .  
   4 3WHG    #   
   6 3X36    SMATCHSTYPE 
   8 3XGQ          STO   3  GEN3 
   8 3Y2B          STO   4  GEN4 
   8 3YG2          STO   5  GEN5 
   8 3Y_L          STO   6  GEN6 
   8 3_F=          STO   7  GEN0 
  18 3_YW          LDX   7  1(1)                [SUBTYPE FLAG TO BE SET  
  17 42DG          LDX   4  0(1)                [LENGTH OF SUBTYPE   
  21 42Y6          SBX   6  4                   [DECREMENT STRING LENGTH FOR SUBTYPE 
  19 43CQ          BNG   6  SMATFAIL            [SUBTYPE LONGER THAN STRING  
   5 43XB    SMATTEST
  15 44C2          LDCH  0  0(3)                [STRING CHAR 
  15 44WL          LDCH  5  2(1)                [SUBTYPE CHAR
  14 45B=          BXU   0  5,SMATFAIL          [NO MATCH
   7 45TW          BCHX  3  /
   7 46*G          BCHX  1  /
  16 46T6          BCT   4  SMATTEST            [TEST NEXT CHAR  
  16 47#Q          ORS   7  AWORK2(2)           [SET SUBTYPE FLAG
  14 47SB          LDN   7  1                   [EXIT +1 
   8 48#2          ADS   7  GEN0 
   5 48RL    SMATEXIT
   8 49?=          LDX   1  FX1  
   8 49QW          LDX   4  GEN4 
   8 4==G          LDX   5  GEN5 
   8 4=Q6          LDX   7  GEN0 
   7 4?9Q          EXIT  7  0
   5 4?PB    SMATFAIL
  17 4#92          LDX   6  GEN6                [RESET STRING LENGTH 
  17 4#NL          LDX   3  GEN3                [RESET STRING POINTER
  14 4*8=          BRN      SMATEXIT            [EXIT +0 
   4 4*MW    #   
   4 4B7G    [   
   7 4BM6    [     MAIN PATHS
   4 4C6Q    [   
   4 4CLB    #   
  16 4D62    Z1MAKEVCC                          [MAKEVCC COMMAND 
   4 4DKL    #   
  12 4F5=    #              AWORK WORDS USAGE :  
  18 4FJW    #              AWORK1 = VIRTUAL 7900 OPERATOR UNIT NUMBER   
  21 4G4G    #              AWORK2 = CURRENT REMOTE DEVICE SPECIFICATION WORD (SEE   
  20 4GJ6    #                       PSD 14.32.6,SECTION 5.3.3.(J).REMOTE DEVICE)
  21 4H3Q    #              AWORK3 = CURRENT CONFIGURATION FILE RECORD NUMBER (=/> 0)
   4 4HHB    #   
  12 4K_G    #     VALIDATE THE TWO PARAMETERS   
  18 4LF6          PARANUMB 5                   [GET COUNT OF PARAMETERS 
  20 4LYQ          SBN   5  3                   [TEST NO MORE THAN 2 SPECIFIED   
   9 4MDB          BPZ   5  XJTOOMANY
  17 4MY2    #              TEST FOR THE UNIT NUMBER PARAMETER : U^N^
   7 4NCL          LDN   5  1
  21 4NX=          LDCT  6  #650                [LOOK FOR PARAM BEGINNING WITH ^U^   
   8 4PBW          PARABEG  1,5  
  10 4PWG          MHUNT    3,CPB,CUNI   
   9 4QB6          LDX   0  ANUM(3)  
  16 4QTQ          BNG   0  XJPARMIS1           [^U^ NON-EXISTENT
  16 4R*B          BZE   0  XAPFERR             [^N^ NON-EXISTENT
   8 4RT2          CHNUMCOD ,3   
  20 4S#L          TESTREP  CHNUMERR,VENDCOM    [^N^ NOT A VALID NUMBER FORMAT   
  21 4SS=          LDX   0  ACOMMUNE1(2)        [CHECK ^N^ IS IN THE RANGE 256 TO 511
  12 4T?W          BXL   0  PUNITRANGE(1),XERANGE
  13 4TRG          BXGE  0  PUNITRANGE+1(1),XERANGE  
  16 4W?6          STO   0  AWORK1(2)           [SAVE UNIT NUMBER
  17 4WQQ    #              TEST FOR THE ABSOLUTE FILE NAME PARAMETER
   7 4X=B          SPARUNAC  
  10 4XQ2          MHUNT    3,CPB,CUNI   
   9 4Y9L          NGX   0  ANUM(3)  
  19 4YP=          BPZ   0  XJPARMIS2           [FILE NAME PARAMETER MISSING 
  20 4_8W          NAMETOP  3,FILE,FNAME        [CONVERT FILE NAME TO AN ABSOLUTE
  17 4_NG ...      FNORM    128                   [FILE NAME BLOCK   
  19 5286          TESTREP2 NAMEFORM,VENDCOM    [ERROR IN FILE NAME REPORTED 
  19 52MQ          MHUNT    3,FILE,FNAME        [RENAME FNAME FOR USEROPEN   
  10 537B          NAMETOP  3,CPB,CUNI   
  21 53M2          MHUNT    3,FILE,FABSNB       [CHECK SYNTACTIC FORM IS AN ABSOLUTE 
  17 546L                                       [NAME BY DEDUCTION   
  17 54L=          JMBS     XAPFERR,3,BFABTSN,BFABLOCONLY,BFABWORK   
  12 555W    #     OPEN THE CONFIGURATION FILE   
  21 55KG          LDN   5  PCPREFIX(1)         [PRETEND TO BE IN USER CONTEXT UNDER 
  20 5656          LDN   6  CPREFIX(2)          [:SYSTEM FOR USEROPEN MACRO BY   
  17 56JQ          MOVE  5  3                   [SETTING UP CPREFIX  
  15 574B          USEROPEN (GEOERR),READ,LEAVE,NOWAIT,REPLY 
  14 57J2          REPERR2  VOPENED             [REPLY OK
   9 583L          BRN      VENDCOM  
   5 58H=    VOPENED 
  20 592W          MHUNTW   3,FILE,ENT          [SET UP BY USEROPEN (MODE LEAVE) 
  18 59GG          LDX   0  ETM(3)              [CHECK FILE TYPE IS BASIC
   8 5=26          SRL   0  15   
  13 5=FQ          BXU   0  PBASICFILE(1),XERWRONG   
   7 5=_B    #     TIDY UP   
   9 5?F2          MFREEW   FILE,ENT 
  10 5?YL          MFREE    FILE,FABSNB  
  21 5#D=    # CONVERT EACH CONFIGURATION FILE RECORD TO A DCP'S CONFIGURATION REPORT
  21 5#XW    # REMOTE DEVICE SPECIFICATION WORD FORMAT AND STORE IN AN IDF/IREPORT   
   6 5*CG    # BLOCK .   
  20 5*X6          SETNCORE 65,4,IDF,IREPORT    [ALLOW AN ARBITRARY 64 ENTRIES   
  20 5BBQ          STOZ     AWORK2(2)           [INITIALISE CURRENT REMOTE DEVICE
  17 5BWB                                       [SPECIFICATION WORD .
  20 5CB2          STOZ     AWORK3(2)           [INITIALISE CURRENT CONFIGURATION
  17 5CTL                                       [FILE RECORD NUMBER .
   6 5D*=    VNEXTRECORD 
  20 5DSW          STEP                         [READ A CONFIGURATION FILE RECORD
  19 5F#G          BZE   3  XJCONFERR           [END OF FILE : NO TERMINATOR 
   8 5FS6          LDX   6  0(3) 
  21 5G?Q          SBN   6  2                   [RECORD LENGTH IN WORDS MINUS HEADER 
  20 5GRB          SLC   6  2                   [RECORD LENGTH IN CHARS PLUS ANY 
  20 5H?2                                       [UNUSED SPACE CHARS IN LAST WORD 
  21 5HQL    #              TEST FOR THE CONFIGURATION FILE TERMINATOR RECORD IN THE 
  11 5J==    #              FIRST FOUR CHARS 
  21 5JPW          BXL   6  PMINREC(1),XJCONFERR[RECORD LENGTH IS BELOW MINIMUM (4)  
  21 5K9G          ADN   3  2                   [SET POINTER TO START OF RECORD DATA 
   8 5KP6          LDX   0  0(3) 
  21 5L8Q          BXE   0  PFILETERM(1),VTERMINATED  [CONFIG. FILE TERMINATOR FOUND 
  21 5LNB    #              CONVERT THE IDENTIFIER FIELD TO AN IDENTIFIER NUMBER IN  
  11 5M82    #              B12-23 OF AWORK2 
  20 5MML          CALL  7  SNONSPACE           [SEARCH FOR THE IDENTIFIER FIELD 
  19 5N7=          BRN      XJCONFERR           [IDENTIFIER FIELD NOT FOUND  
  21 5NLW          BXU   0  PIDENPREFIX(1),XJCONFERR[IDEN FIELD NOT PREFIXED WITH ^I^
  20 5P6G          LDN   0  1                   [STEP TO NEXT CHAR (IDENTIFIER)  
   9 5PL6          CALL  7  SUPDPTRCT
  21 5Q5Q          CALL  7  SCDB                [CONVERT IDENTIFIER TO BINARY(IN X0) 
   9 5QKB          LDX   5  AWORK2(2)
  19 5R52          ANDN  5  #7777               [PREVIOUS IDENTIFIER NUMBER  
  19 5RJL          STO   0  AWORK2(2)           [STORE NEW IDENTIFIER NUMBER 
  20 5S4=          BXGE  5  0,XJCONFERR         [IDENTIFIER NUMBER NOT ASCENDING 
  21 5SHW    #              CONVERT THE DEVICE TYPE FIELD TO A DEVICE TYPE NUMBER IN 
  11 5T3G    #              B5-8 OF AWORK2   
  20 5TH6          CALL  7  SNONSPACE           [SEARCH FOR THE DEVICE TYPE FIELD
  19 5W2Q          BRN      XJCONFERR           [DEVICE TYPE FIELD NOT FOUND 
  21 5WGB          SBN   6  2                   [DECREMENT RECORD LENGTH FOR DEV TYPE
  19 5X22          BNG   6  XJCONFERR           [DEVICE TYPE FIELD NOT FOUND 
   7 5XFL          LDN   0  0
  21 5X_=          LDN   4  0                   [OBTAIN THE 2 CHAR DEVICE TYPE IN X0 
  21 5YDW          MVCH  3  2                   [AND STEP PAST THE DEVICE TYPE CHARS 
  21 5YYG    #                                   MATCH WITH RECOGNISED DEVICE TYPE   
  15 5_D6    #                                   MNEMONICS   
  11 5_XQ          BXE   0  PDTLP(1),VDTLP   
  11 62CB          BXE   0  PDTCR(1),VDTCR   
  11 62X2          BXE   0  PDTTW(1),VDTTW   
  11 63BL          BXE   0  PDTTR(1),VDTTR   
  11 63W=          BXE   0  PDTTP(1),VDTTP   
  21 65*6          MVCH  3  1                   [NOT A 2 CHAR DEVICE TYPE:TEST FOR 3 
  21 65SQ          SBN   6  1                   [DECREMENT RECORD LENGTH FOR DEV TYPE
  11 66#B          BXE   0  PDTTTW(1),VDTTT  
  11 66S2          BXE   0  PDTVDU(1),VDTVD  
  19 67R=          BRN      XJCONFERR           [DEVICE TYPE FIELD INVALID   
  21 68=W    #                                   PICK UP CORRESPONDING DEVICE TYPE   
  14 68QG    #                                   NUMBER  
   8 69=6    VDTTT LDN   0  PTTDT
   9 69PQ          BRN      VDTPACK  
   8 6=9B    VDTLP LDN   0  PLPDT
   9 6=P2          BRN      VDTPACK  
   8 6?8L    VDTCR LDN   0  PCRDT
   9 6?N=          BRN      VDTPACK  
   8 6#7W    VDTTW LDN   0  PTWDT
   9 6#MG          BRN      VDTPACK  
   8 6*76    VDTTR LDN   0  PTRDT
   9 6*LQ          BRN      VDTPACK  
   8 6B6B    VDTTP LDN   0  PTPDT
   9 6BL2          BRN      VDTPACK  
   8 6C5L    VDTVD LDN   0  PVDDT
   5 6CK=    VDTPACK 
   7 6D4W          SRC   0  9
   9 6DJG          ORS   0  AWORK2(2)
  19 6F46    #              CONVERT ANY SUBTYPE FIELDS TO SPECIAL FLAGS IN   
  11 6FHQ    #              B9-11 OF AWORK2  
   6 6G3B    VNEXTSTYPE  
  19 6GH2          CALL  7  SNONSPACE           [SEARCH FOR A SUBTYPE FIELD  
  16 6H2L          BRN      VRECORDOK           [NO MORE SUBTYPES
  18 6HG=          LDN   1  PST7502(1)          [TEST FOR SUBTYPE 7502   
  10 6H_W          CALL  7  SMATCHSTYPE  
  14 6JFG          BRN      VSTSPOOLING         [NO MATCH
  18 6J_6          LDX   0  AWORK2(2)           [CHECK DEVICE TYPE IS LP 
   8 6KDQ          SRL   0  15   
   8 6KYB          SBN   0  PLPDT
  17 6LD2          BNZ   0  XJCONFERR           [DEVICE TYPE NOT LP  
  17 6LXL          BRN      VNEXTSTYPE          [ANYMORE SUBTYPES ?  
   6 6MC=    VSTSPOOLING 
  19 6MWW          LDN   1  PSTSPOOLING(1)      [TEST FOR SUBTYPE SPOOLING   
  10 6NBG          CALL  7  SMATCHSTYPE  
  21 6NW6          BRN      XJCONFERR           [NO MATCH : SUBTYPE NOT RECOGNISED   
  17 6P*Q          BRN      VNEXTSTYPE          [ANYMORE SUBTYPES ?  
  21 6PTB    #              STORE THE REMOTE DEVICE SPECIFICATION WORD (AWORK2) IN   
  12 6Q*2    #              THE IDF/IREPORT BLOCK
   6 6QSL    VRECORDOK   
  10 6R#=          MHUNTW   3,IDF,IREPORT
  20 6RRW          LDX   4  ALOGLEN(3)          [TEST IF ROOM EXISTS FOR THE WORD
  19 6S?G          SBN   4  2                   [(NUMBER OF THE LAST ENTRY)  
  14 6SR6          BXL   4  AWORK3(2),VEXTEND   [NO ROOM 
  16 6T=Q          LDX   0  AWORK2(2)           [STORE NEW ENTRY 
   9 6TQB          SMO      AWORK3(2)
   9 6W=2          STO   0  A1+1(3)  
  21 6WPL          LDN   0  1                   [UPDATE RECORD NUMBER FOR NEXT RECORD
   9 6X9=          ADS   0  AWORK3(2)
  18 6XNW          BRN      VNEXTRECORD         [PROCESS THE NEXT RECORD 
  20 6Y8G    VEXTEND                            [EXTEND THE IREPORT BLOCK BY AN  
  20 6YN6                                       [ARBITRARY 64 WORDS AND RETURN   
   8 6_7Q          ADN   4  2+64 
  11 6_MB          ALTLEN   3,4,IDF,IREPORT  
   9 7272          BRN      VRECORDOK
   6 72LL    VTERMINATED 
   9 736=          LDX   4  AWORK3(2)
  21 73KW          BZE   4  XJCONFERR           [CURRENT CONFIGURATION FILE RECORD   
  20 745G                                       [NUMBER = 0  : NULL CONFIGURATION
  18 74K6          CLOSE                        [THE CONFIGURATION FILE  
  20 754Q          ADN   4  1                   [SET UP IDF/IREPORT COUNT WORD   
  10 75JB          MHUNTW   3,IDF,IREPORT
   8 7642          STO   4  A1(3)
  20 76HL          ALTLENG  3,4                 [SHORTEN IDF/IREPORT IF NOT FULL 
  16 773=    #     CREATE THE VIRTUAL 7900 IN THE IDF AND CORE   
  21 77GW          LDX   0  AWORK1(2)           [SET THE VIRTUAL 7900'S OPS UNIT NO. 
  19 782G          STO   0  ACOMMUNE1(2)        [IN ACOMMUNE1 FOR K3SETIDF   
   9 78G6 ...      DOWN     IDFCHK,3 
  20 78_Q          LDX   3  ACOMMUNE1(2)        [VIRTUAL 7900 DEVICE LIST POINTER
  18 79FB                                       [(RETURNED BY K3SETIDF)  
  21 79_2          BZE   3  XERALREX            [VIRTUAL 7900 UNIT NO. ALREADY EXISTS
  20 7=DL    #     INSERT THE IDENTIFIER CONFIGURATION(IDF/IREPORT) IN THE IDF   
  10 7=Y=          IDFTPUPD (GEOERR),3   
   7 7?CW          LDN   0  0
  20 7?XG          DSA   0  ATYPE(3)            [NOW THAT THE VIRTUAL 7900 HAS A 
  21 7#C6                                       [CONFIGURATION INDICATE THAT IT IS NO
  21 7#WQ                                       [LONGER BEING CREATED BY ZEROISING   
  21 7*BB                                       [APERI/AV7900.ATYPE.B12-23(WHICH WAS 
  16 7*W2                                       [SET BY K3SETIDF)
  14 7B*L    #     INFORM THE CENTRAL OPERATOR AND ENDCOM
  10 7BT=          LDX   6  PAV7900(1)   
   5 7C#W    VINFORMX
  20 7CSG          GEOPACK  5,AWORK1(2),UNIT    [OUTPUT UNIT NO. PARAMETER : U'N'
  10 7D#6          OUTPACK  5,1,GEOPER   
  16 7DRQ          INFORMX  ,6,1                [CENTRAL OPERATOR
   5 7F?B    VENDCOM 
   6 7FR2          ENDCOM
   4 7G=L    #   
   6 7GQ=    Z2MAKEVCC   
   4 7H9W    #   
  13 7KN2    #     VALIDATE THE UNIT NUMBER PARAMETER
   9 7L7L          PARSORT  (GEOERR) 
  10 7LM=          HUNTW    3,CPAR,JSCE  
  21 7M6W          BNG   3  VENDCOM             [UNIT NO. PARAMETER MISSING/INVALID  
  17 7MLG          SAWCEJX  JSCEB(3),VLOCALUNIT,XJUNITERR,XJUNITERR  
   6 7N66    VLOCALUNIT  
   9 7NKQ          LDCH  0  JSCEA(3) 
   9 7P5B          SBN   0  PTYPE7900
  16 7PK2          BNZ   0  XJUNITERR           [UNIT NOT A 7900 
  12 7Q4L          JV7900   JSCEP(3),VIRTUAL7900 
  18 7QJ=          BRN      XJUNITERR           [UNIT NOT A VIRTUAL 7900 
  20 7R3W    #     TEST VIRTUAL 7900 IS MOPPED OFF AND IS NOT BEING MOPPED ON NOR
  13 7RHG    #     ACCESSED (WITHOUT COORDINATING)   
   6 7S36    VIRTUAL7900 
  15 7SGQ          JALLOC   JSCEP(3),XJRES1     [MOPPED ON   
  21 7T2B          LAMOP    JSCEP(3),4          [SET X4 -> V7900 AMOP/IPBIN ACTIVITY 
   8 7TG2          LDCT  0  #10  
   7 7T_L          SMO      4
   8 7WF=          ANDX  0  IPBSW
  16 7WYW          BNZ   0  XJRES1              [BEING MOPPED ON 
  20 7XDG    #              NOTE THE V7900 AMOP/IPBIN IS NOW IMPLICITLY ASLEEP   
  21 7XY6          SMO      JSCEP(3)            [TEST IF V7900 IS BEING ACCESSED BY  
  10 7Y9G ...      LDN   0  AVOLPARING   
   9 7YGW ...      SMO      JSCEP(3) 
  10 7YS= ...      TXU   0  AVOLPARING   
  19 7_5L ...      BCS      XJRES1             [J IF BEING ACCESSED BY A PROG
  21 7_C2          LGEOG    JSCEP(3),0          [SET THE V7900 OPS UNIT NO FOR INFORM
   9 7_WL          STO   0  AWORK1(2)
  14 82B=    #     REMOVE THE VIRTUAL 7900 FROM CORE :   
  21 82TW    #     REMOVE THE DEVICE LIST FROM THE MISCELLANEOUS CHAIN AND THE AMOP/ 
  16 83*G    #     IPBIN FROM THE IPB LIST (WITHOUT COORDINATING)
  13 83T6    #     FINALLY MURDER THE AMOP/IPBIN .   
  21 84#Q    #              REMOVE THE V7900 DEVICE LIST FROM THE MISCELLANEOUS CHAIN
  15 84SB    #              AS IT CAN NOT BE DESTROYED YET   
  18 85#2          CHAIN    JSCEP(3),2          [CHAIN IT TO THIS CPAT   
  19 85RL    #              REMOVE THE V7900 AMOP/IPBIN FROM THE IPB LIST :: 
  21 86?=    #              SEARCH THE IPB LIST FOR ITS POINTER TO THE V7900 AMOP/   
  21 86QW    #              IPBIN ACTIVITY AND REPLACE IT WITH THE V7900 AMOP/IPBIN'S
  16 87=G    #              POINTER TO THE NEXT AMOP/IPBIN(IPBL) 
  19 87Q6          LDN   3  IPBADDR-IPBL        [BEGIN SEARCH WITH IPBADDR   
   6 889Q    VPTRSEARCH  
   9 88PB          LDX   0  IPBL(3)  
  16 8992          BXE   0  4,VPTRFOUND         [POINTER FOUND   
  19 89NL          STO   0  3                   [CHECK NEXT AMOP/IPBIN'S IPBL
  10 8=8=          BRN      VPTRSEARCH   
   6 8=MW    VPTRFOUND   
   7 8?7G          SMO      4
  21 8?M6          LDX   0  IPBL                [V7900 AMOP/IPBIN'S PTR TO NEXT AMOP 
  20 8#6Q          STO   0  IPBL(3)             [OVERWRITE PTR TO THE V7900 AMOP 
  14 8#LB    #              MURDER THE V7900 AMOP/IPBIN  
  19 8*62          MURDER   4                   [AMOP/IPBIN IS ALWAYS LOCKED 
  14 8*KL    #     REMOVE THE VIRTUAL 7900 FROM THE IDF  
  16 8B5=          MHUNTW   3,APERI,AV7900      [REMAINS LOCKED  
  21 8BJW          IDFTPUPD (GEOERR),3          [THE WHOLE UNIT WILL BE REMOVED AS   
  20 8C4G                                       [NO IDF/IREPORT BLOCK IS PRESENT 
  14 8CJ6    #     INFORM THE CENTRAL OPERATOR AND ENDCOM
  15 8D3Q    #     LET ENDCOM DESTROY THE V7900 DEVICE LIST  
  10 8DHB          LDX   6  PAV7900X(1)  
   9 8F32          BRN      VINFORMX 
   4 8FGL    #   
   4 8G2=    [   
   9 8GFW    [     EXCEPTION PATHS   
   4 8G_G    [   
   6 8HF6    XJPARMIS1   
  20 8HYQ          COMERR   JPARMIS,JUNNO       [UNIT NUMBER PARAMETER MISSING   
  15 8JDB          ZERRORY                      [^COMERRX^   
  18 8JY2    XJCOMDEV                           [K1MAKEVCC & K2MAKEVCC   
  21 8KCL          ZERRORX  JCOMDEV             [THIS COMMAND IS ONLY ALLOWED IN A   
  16 8KX=                                       [DME ENVIRONMENT 
   6 8LBW    XJTOOMANY   
  17 8LWG          ZERRORX  JTOOMANY            [TOO MANY PARAMETERS 
   5 8MB6    XAPFERR 
  18 8MTQ          ZERRORX  APFERR              [PARAMETER FORMAT ERROR  
   6 8N*B    XJPARMIS2   
  19 8NT2          COMERR   JPARMIS,JFNAME      [FILE NAME PARAMETER MISSING 
   5 8P#L    XERANGE 
  17 8PS=          ZERRORX  ERANGE              [%C IS OUT OF RANGE  
   5 8Q?W    XERWRONG
   6 8QRG          CLOSE 
  21 8R?6          ZERRORX  ERWRONG             [ENTRANT %C IS OF THE WRONG TYPE FOR 
  16 8RQQ                                       [THIS OPERATION  
   6 8S=B    XJCONFERR   
   8 8SQ2    #SKI  K6VIPB>599-599
   4 8T9L    (   
   8 8TP=          STO   0  GEN0 
  21 8W8W          LDX   0  ASWITCH1            [IF MOP TR IS ^ON^, GEOERR INSTEAD . 
   7 8WNG          SLL   0  7
   9 8X86          BNG   0  (GEOERR) 
   4 8XMQ    )   
  18 8Y7B          CLOSE                        [THE CONFIGURATION FILE  
  21 8YM2          OUTNUM   AWORK3(2),0         [CONVERT THE CURRENT CONFIGURATION   
  21 8_6L                                       [FILE RECORD NO. TO A MESSAGE PARAM. 
  20 8_L=          ZERRORX  JCONFERR            [CONFIGURATION RECORD %A INVALID 
   5 925W    XERALREX
  20 92KG    #              ACCESS THE COMPLETE UNIT NUMBER PARAMETER FOR THIS   
  10 9356    #              ERROR MESSAGE
  16 93JQ          MFREE    CPB,CUNI            [FILE NAME CUNI  
  16 944B          MHUNT    3,CPB,CUNI          [UNIT NUMBER CUNI
  10 94J2          PARANOTX JPARNUM(3)   
   4 953L    #   
  17 95H=          ZERRORX  ERALREX             [%C ALREADY EXISTS   
  15 962W    XJUNITERR                          [K2MAKEVCC   
   7 96GG          LDN   4  0
   9 9726          BRN      XPERIDESC
  15 97FQ    XJRES1                             [K2MAKEVCC   
   7 97_B          LDN   4  1
   6 98F2    XPERIDESC   
  17 98YL          LDX   5  JSCEQ(3)            [GET PARAMETER LENGTH
   8 99D=          ANDN  5  #7777
  21 99XW          OUTPARAM 5,JSCER,CPAR,JSCE   [OUTPUT PERIPHERAL DESCRIPTION PARAM 
  15 9=CG          BNZ   4  XZJRES1             [XJRES1 ENTRY
  20 9=X6          ZERRORX  JUNITERR            [UNIT %A IS NOT A SUITABLE DEVICE
   5 9?BQ    XZJRES1 
  20 9?WB          ZERRORX  JRES1               [UNIT %A NOT IN REQUIRED STATE   
   4 9#B2    #END
   6 ____ ...206543110001
