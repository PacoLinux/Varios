   4 22FL    #   
  16 22_=    #SEG  SIMULPTR60                   [LADISLAV KUTILEK
   8 23DW    #OPT  K0SIMULPTR=0  
  15 23YG    #LIS  K0SIMULPTR>K0ALLGEO>K0GREATGEO>K0FILESTORE
  10 24D6                   8HSIMULPTR   
  12 24XQ          SEGENTRY K1SIMULPTR,Z1SIMULPTR
   4 25CB    #   
  21 25X2    #           THIS SEGMENT IMPLEMENTS OFFLINE *TR AND *CR (TYPE 0 AND 3), 
  18 26BL    #           IF ASSIGNED TO AN ^OLD^ TP LIKE FILE(PRE MARK 7)
   4 26W=    #   
  10 27*W    #           N=COUNT IN PERI,
  10 27TG    #           R=SIZE OF RECORD
  17 28*6    #           C=CHARACTER POINTER TO NEXT CHAR.IN BUFFER, 
  17 28SQ    #           D=CHARACTER POINTER TO NEXT CHAR.IN BUFFER, 
  13 29#B    #           S=CURRENT SHIFT IN RECORD,  
  15 29S2    #           E=CURRENT SHIFT ASSUMED BY READER.  
  13 2=?L    #              AWORK1=LAST LOADED CHAR  
  14 2=R=    #              AWORK2=B23=1 CR SIMULATION   
  14 2?=W    #                     B0 =1 CR,CP,LP FILE   
   4 2?QG    #   
   9 2#=6    MASK1          #3770000 
   9 2#PQ    MASK2          #37777777
   9 2*9B    MASK3          #47777777
   9 2*P2    MASK4          #60000077
   9 2B8L    MASK5          #74007777
   9 2BN=    MASK6          #57777777
   9 2C7W    MASK7          #20000000
   9 2CMG    MASK8          #17777777
   9 2D76    MASK9          #40000000
   9 2DLQ    MASK10         #3777777 
   9 2F6B    MASK11         #10000   
   9 2FL2    MASK12         #17770000
   9 2G5L    MASK13         #77770000
   9 2GK=    MASK14         #30000   
  14 2H4W    RNO1           #02                 [MODES   
   8 2HJG    RNO2           #04  
   8 2J46    RNO3           #06  
   8 2JHQ    RAC1           #20  
   8 2K3B    RAC2           #22  
   8 2KH2    RAC3           #24  
   8 2L2L    RAC4           #26  
   8 2LG=    RGS1           #10  
   8 2L_W    RGS2           #12  
  15 2MFG    TW             #2                  [GRAPHIC SET 
  15 2M_6    TH             #3                  [PUNCH BLANKS
  13 2NDQ    ST             #64                 [T   
  13 2NYB    SU             #65                 [U   
  13 2PD2    SV             #66                 [V   
  13 2PXL    SW             #67                 [W   
  13 2QC=    SX             #70                 [X   
  15 2QWW    SALP           #74                 [ALPHA SHIFT 
  15 2RBG    SBET           #75                 [BETA SHIFT  
  15 2RW6    SDEL           #76                 [DELTA SHIFT 
  13 2S*Q    SUNA           #77                 [_   
  14 2STB    SDLT           #73                 [DELETE  
  14 2T*2    SROT           #20                 [RUN-OUT 
  13 2TSL    SSTP1          #21                 [TC1 
  13 2W#=    SSTP2          #40                 [@   
  15 2WRW    SSTP3          #32                 [FE2-NEWLINE 
  15 2X?G    SSTP4          #36                 [SHIFT OUT   
   8 2XR6    MAX            +129 
   8 2Y=Q    MAXC           +81  
   8 2YQB    MAXCD          +2037
   5 2_=2    PLINK1  
   9 2_PL          PAIR  SIMULPTR,1  
   6 329=    Z1SIMULPTR  
  15 32NW          LDX   3  APETADDR(2)         [PTR TO APET 
   9 338G          LOCKCA   3,ACAP(2)
  16 33N6          LDX   1  ACAP(2)             [CA IN OBJ.PROG. 
  15 347Q          LDN   2  ACA1(2)             [CA IN OLPA  
   7 34MB          LDN   7  4
  16 3572          FOUTMOVE 3,1,2,7,HLOCK1      [MOVE CA TO OLPA 
   8 35LL          LDX   1  FX1  
   8 366=          LDX   2  FX2  
  17 36KW          STOZ     ACA2(2)             [ZEROISE REPLY WORD  
  15 375G          LDX   3  ACA3(2)             [PERI COUNT  
   8 37K6          BZE   3  WC   
   9 384Q          LDEX  0  AWORK2(2)
  16 38JB          BNZ   0  RCARD               [J SIMULATE CR   
  16 3942    Z5    BXGE  3  MAX(1),WC           [CHECK COUNT<129 
   8 39HL          SLC   3  12   
   9 3=3=          ANDX  5  MASK5(1) 
  15 3=GW          ORX   5  3                   [NEW N IN X5 
  13 3?2G          ANDX  6  MASK5(1)            [D=0 
  15 3?G6          LDX   3  ACA1(2)             [PERI MODE   
   8 3?_Q          ANDN  3  #7777
  14 3#FB          BZE   3  SHIFT               [J IF #00
  14 3#_2          BXE   3  RNO1(1),SHIFT       [     #02
  14 3*DL          BXE   3  RNO2(1),RO1         [     #04
  14 3*Y=          BXE   3  RNO3(1),RO2         [     #06
  14 3BCW          BXE   3  RAC1(1),RC1         [     #20
  14 3BXG          BXE   3  RAC2(1),RC2         [     #22
  14 3CC6          BXE   3  RAC3(1),RC3         [     #24
  14 3CWQ          BXE   3  RAC4(1),RC4         [     #26
  14 3DBB          BXE   3  RGS1(1),SHIFT       [     #10
  14 3DW2          BXE   3  RGS2(1),SHIFT       [     #12
   8 3F*L          BRN      MOPER
  16 3FT=    RCARD BXGE  3  MAXCD(1),WC         [CHECK COUNT<2037
   8 3G#W          SLC   3  12   
  16 3GSG          ANDN  5  #7777               [ZEROISE N IN X5 
  17 3H#6          ORX   5  3                   [STORE NEW N IN X5   
  13 3HRQ          ANDN  6  #7777               [D=0 
   8 3J?B          BRN      SHCR 
   7 3JR2    RO1   LDN   3  1
   8 3K=L          BRN      SHIFT
   7 3KQ=    RO2   LDN   3  3
   8 3L9W          BRN      SHIFT
   7 3LPG    RC1   LDN   3  4
   8 3M96          BRN      SHIFT
   7 3MNQ    RC2   LDN   3  6
   8 3N8B          BRN      SHIFT
   7 3NN2    RC3   LDN   3  5
   8 3P7L          BRN      SHIFT
   7 3PM=    RC4   LDN   3  7
   7 3Q6W    SHIFT SRC   3  4
  17 3QLG          ANDX  6  MASK10(1)           [ERASE PREVIOUS MODE 
  16 3R66          ORX   6  3                   [STORE NEW MODE  
  15 3RKQ    SHCR  LDX   3  ACA3(2)             [PERI COUNT  
  16 3S5B          LDX   1  ACA4(2)             [BUFFER ADDRESS  
   7 3SK2          ADN   3  3
   7 3T4L          SRL   3  2
  15 3TJ=          CHECKA   1,3,RESV,APETADDR(2),W,Z4,HLOCK2 
  17 3W3W    Z4    LDX   0  ALIMEY(2)           [CHECK IF FIRST PERI 
  17 3WHG          LDX   4  AWORK1(2)           [LAST LOADED CHAR.   
  15 3X36          BZE   0  PEFI                [J IF IT IS  
   7 3XGQ          LDX   1  5
  15 3Y2B          ANDN  1  #7777               [PICK UP R   
   7 3YG2          LDX   2  6
  15 3Y_L          ANDN  2  #7777               [PICK UP C   
  17 3_F=          BXE   1  2,SGE               [J IF END OF RECORD  
  15 3_YW          BACKSPACE                    [SET POINTER 
  15 42DG          STEP                         [TO RECORD   
   7 42Y6    SGE   LDX   1  5
  18 43CQ          SLC   1  2                   [PICK UP MODE OF RECORD  
  18 43XB          BPZ   1  TESMO               [J IF NORMAL OR ALCHAR   
   7 44C2          SLC   1  1
  15 44WL          BNG   1  PG                  [J IF BLANKS 
  15 45B=          BRN      PH                  [J IF GRAPHIC
   8 45TW    TESMO LDX   1  FX1  
  18 46*G          BNG   6  PD                  [J IF READ IN GRAPHIC SET
  16 46T6          SLC   6  3                   [PICK UP SHIFT   
  19 47#Q          BNG   6  PRSH                [J IF READ IN PREVIOUS SHIFT 
  20 47SB          SRC   6  3                   [BITS OF X6 INTO NORMAL POSITION 
  14 48#2          ANDX  5  MASK2(1)            [E=ALPHA 
  18 48RL          BXE   4  SDEL(1),SONE        [J IF LOADED CHAR.=DELTA 
  18 49?=          BXE   4  SALP(1),SONE        [                 =ALPHA 
  18 49QW          BXU   4  SBET(1),PA          [                 =BETA  
  16 4==G    SONE  ADN   6  1                   [C=C+1,READER /2 
   8 4=Q6          BRN      PA   
  20 4?9Q    PRSH  SRC   6  3                   [BITS OF X6 INTO NORMAL POSITION 
  18 4?PB          BXE   4  SDEL(1),PB          [J IF LOADED CHAR.=DELTA 
   8 4#92          BRN      PA   
   7 4#NL    PEFI  LDN   0  1
  18 4*8=          STO   0  ALIMEY(2)           [SWITCH FOR FIRST PERI   
   8 4*MW          LDX   1  FX1  
  16 4B7G          ANDX  5  MASK12(1)           [E=S=ALPHA,R=0   
  13 4BM6          ANDX  6  MASK13(1)           [C=0 
  19 4C6Q          BNG   6  PD                  [J IF READ GRAPHIC SET MODE  
   4 4CLB    #   
  13 4D62    #              RECORD IN A SHIFT MODE.  
   4 4DKL    #   
   4 4F5=    PA  
  17 4FJW          CALL  1  SRGNC               [GET FIRST CHARACTER 
   8 4G4G          LDX   1  FX1  
  21 4GJ6          BXE   4  SALP(1),SA          [TEST LOADED CHAR.,J IF IT IS ALPHA  
  16 4H3Q          BXE   4  SBET(1),SB          [J IF IT IS BETA 
  16 4HHB          BXE   4  SDEL(1),SD          [J IF IT IS DELTA
   7 4J32          LDX   0  5
  17 4JGL          SLC   0  1                   [PICK UP S FROM X5   
   8 4K2=          BNG   5  TEST 
  17 4KFW          BNG   0  UNEQ                [J IF E=ALPHA,S=BETA 
  16 4K_G          BRN      SAME                [J IF E=S=ALPHA  
  16 4LF6    TEST  BNG   0  SAME                [J IF E=S=BETA   
  18 4LYQ          ANDX  5  MASK2(1)            [E=BETA,S=ALPHA,MAKE E=S 
   8 4MDB          SMO      FX2  
  17 4MY2          STO   4  AWORK1              [STORE LOADED CHAR   
  15 4NCL          LDX   4  SALP(1)             [LOAD ALPHA  
   8 4NX=          BRN      TCH  
  14 4PBW    UNEQ  ORX   5  MASK9(1)            [MAKE E=S
   8 4PWG          SMO      FX2  
  17 4QB6          STO   4  AWORK1              [STORE LOADED CHAR   
  15 4QTQ          LDX   4  SBET(1)             [LOAD BETA   
  18 4R*B    TCH   CALL  0  SRTCH               [TRANSFER SHIFT CHARACTER
   8 4RT2          SMO      FX2  
  15 4S#L          LDX   4  AWORK1              [RELOAD CHAR 
  17 4SS=    SAME  CALL  0  SRTCH               [TRANSFER CHARACTER  
   8 4T?W          BRN      PA   
  14 4TRG    SA    ANDX  5  MASK6(1)            [S=ALPHA 
   8 4W?6          BRN      PA   
  14 4WQQ    SB    ORX   5  MASK7(1)            [S=BETA  
   8 4X=B          BRN      PA   
   7 4XQ2    SD    LDX   2  6
  19 4Y9L          SLC   2  1                   [TEST IF READ ALL CHARACTERS 
  15 4YP=          BNG   2  YES                 [J IF IT IS  
  15 4_8W          LDX   2  6                   [PICK UP C   
   8 4_NG          ANDN  2  #7777
  20 5286          SRC   2  2                   [X2 AS CHARACTER COUNTER-MODIFIER
   7 52MQ          SMO      3
  17 537B          LDCH  0  2(2)                [LOAD NEXT CHARACTER 
   8 53M2          LDX   1  FX1  
  17 546L          BXE   0  SDLT(1),SIG         [TEST IF IT IS DELETE
  15 54L=          BXE   0  SROT(1),SIG         [OR RUN-OUT  
  17 555W    YES   CALL  0  SRTCH               [TRANSFER DELTA SHIFT
  17 55KG    PB    CALL  1  SRGNC               [GET NEXT CHARACTER  
  17 5656          CALL  0  SRTCH               [TRANSFER CHARACTER  
   7 56JQ          LDX   0  6
  20 574B          SLC   0  2                   [TEST IF READ TO STOP CHARACTER  
  15 57J2          BNG   0  TOST                [J IF IT IS  
   8 583L          BRN      PA   
  14 58H=    SIG   ADN   6  1                   [C=C+1   
   8 592W          BRN      PA   
  18 59GG    TOST  BXE   4  RAC3(1),QUIT        [J IF LOADED CHAR.=TC4   
  17 5=26          BXL   4  SSTP3(1),PA         [J IF NOT STOP CHAR. 
  17 5=FQ          BXGE  4  SSTP4(1),PA         [J IF NOT STOP CHAR. 
   8 5=_B    QUIT  LDX   1  FX1  
   8 5?F2          SMO      FX2  
   9 5?YL          LDEX  2  AWORK2   
  14 5#D=          BNZ   2  SETCR               [J IF *CR
   7 5#XW          LDX   3  5
  15 5*CG          ANDX  3  MASK1(1)            [PICK UP N   
   7 5*X6          LDX   2  6
  15 5BBQ          ANDX  2  MASK1(1)            [PICK UP D   
   7 5BWB          SBX   3  2
  16 5CB2          SRL   3  12                  [REMAINDER COUNT 
   8 5CTL          BRN      REPWO
   5 5D*=    SETCR   
   7 5DSW    SET2  LDX   2  6
  17 5F#G          SRL   2  14                  [POINTER IN BUFFER   
   8 5FS6          SMO      FX2  
  16 5G?Q          LDX   3  ACA4                [BUFFER ADDRESS  
  20 5GRB          ADX   3  2                   [ADDRESS OF LAST TRANSFERRED WORD
   8 5H?2    REPWO LDX   2  FX2  
  17 5HQL          STO   4  AWORK1(2)           [STORE LOADED CHAR   
   5 5J==    OLPREP  
  15 5JPW          LDX   1  APETADDR(2)         [PTR TO APET 
  16 5K9G          LDX   2  ACAP(2)             [PTR TO PERI CA  
   7 5KP6          ADN   2  1
  10 5L8Q          FADDRESS 1,2,HLOCK1   
  15 5LNB          STO   3  0(2)                [REPLY WORD  
   4 5M82    OEND
   8 5MML          LDX   2  FX2  
  15 5N7=          JNOTFDRM XNDRM               [J IF NOT DRM
   8 5NLW          TICKLES  2,3  
  10 5P6G    XNDRM LDX   2  APETADDR(2)  
   7 5PL6          OLPFIN   2
  10 5Q5Q          BRN      K1SIMULPTR   
   4 5QKB    #   
  16 5R52    #              PERI IN GRAPHIC SET MODES(#10,#12),  
  13 5RJL    #              RECORD IN A SHIFT MODE.  
   4 5S4=    #   
   4 5SHW    PD  
  17 5T3G          CALL  1  SRGNC               [GET NEXT CHARACTER  
   8 5TH6          LDX   1  FX1  
   8 5W2Q          SMO      FX2  
   9 5WGB          LDEX  0  AWORK2   
  16 5X22          BNZ   0  READC               [J SIMULATE CR   
  14 5XFL          ANDX  5  MASK2(1)            [E=ALPHA 
  19 5X_=          BXE   4  SALP(1),SAL         [J IF LOADED CHAR.IS ALPHA   
  18 5YDW          BXE   4  SBET(1),SBE         [J IF LOADED CHAR.IS BETA
  19 5YYG    XSDE  BXE   4  SDEL(1),SDE         [J IF LOADED CHAR.IS DELTA   
  17 5_D6          CALL  0  SRTCH               [TRANSFER CHARACTER  
   8 5_XQ          BRN      PD   
  18 62CB    READC BXE   4  SALP(1),PD          [J IF LOAD.CHAR.IS ALPHA 
  18 62X2          BXE   4  SBET(1),PD          [J IF LOAD.CHAR.IS BETA  
   8 63BL          BRN      XSDE 
  14 63W=    SAL   ANDX  5  MASK6(1)            [S=ALPHA 
   8 64*W          BRN      PD   
   9 64TG    SBE   LDX   0  MASK9(1) 
  13 65*6          SRC   0  1                   [B1=1
  14 65SQ          ORX   5  0                   [S=BETA  
   8 66#B          BRN      PD   
  16 66S2    SDE   CALL  1  SRGNC               [NEXT CHARACTER  
   8 67?L          LDX   1  FX1  
  19 67R=          BXL   4  ST(1),STOGR         [J,IT COULD BE A STOP CHAR   
   7 68=W          ADN   4  4
  17 68QG          BXGE  4  SALP(1),NATCH       [J IF A NATIONAL CHAR
   7 69=6          ADN   4  4
  16 69PQ    TRNAT CALL  0  SRTCH               [TRANSFER CHAR   
   8 6=9B          BRN      PD   
  18 6=P2    NATCH BXGE  4  SUNA(1),PD          [JIF NOT A NATIONAL CHAR 
   8 6?8L          BRN      TRNAT
   8 6?N=    STOGR SBN   4  #21  
  16 6#7W          BNG   4  PD                  [J IF NOT A STOP 
   8 6#MG          SBN   4  #17  
  17 6*76          BPZ   4  PD                  [IGNORE IF NOT A STOP
  15 6*LQ          ADN   4  #40                 [RESTORE CHAR
   8 6B6B          SMO      FX2  
   9 6BL2          LDEX  0  AWORK2   
  16 6C5L         BNZ    0  TCOUN               [SPACEFILL IF CR 
   8 6CK=          BRN      QUIT 
   4 6D4W    #   
  13 6DJG    #              RECORD IN GRAPHIC MODE.  
   4 6F46    #   
   4 6FHQ    PH  
   8 6G3B          SMO      FX1  
  15 6GH2          ANDX  5  MASK8               [E=S=ALPHA   
  17 6H2L          BNG   6  RGM                 [J IF READ IN GRAPHIC
  16 6HG=    PE    CALL  1  SRGNC               [GET CHARACTER   
   5 6H_W    SREXIT  
   8 6JFG          LDX   1  FX1  
  19 6J_6          BXL   4  SALP(1),TRACH       [J IF IT IS NOT A FUNY CHAR. 
   8 6KDQ          SMO      FX2  
  15 6KYB          STO   4  AWORK1              [STORE SHIFT 
  14 6LD2          LDN   4  #76                 [DELTA   
  16 6LXL          CALL  0  SRTCH               [TRANSFER DELTA  
   8 6MC=          SMO      FX2  
  16 6MWW          LDX   4  AWORK1              [RESTORE SHIFT   
  16 6NBG          SBN   4  #10                 [CONVERT SHIFT   
  17 6NW6    TRACH CALL  0  SRTCH               [TRANSFER CHARACTER  
   8 6P*Q          BRN      PE   
   8 6PTB    RGM   CALL  1  SRGNC
  16 6Q*2          CALL  0  SRTCH               [TRANSFER CHAR   
   8 6QSL          BRN      RGM  
   4 6R#=    #   
  18 6RRW    #              TP FILE WRITTEN IN MODE #20(PUNCH BLANKS).   
   4 6S?G    #   
   4 6SR6    PG  
  17 6T=Q          SLC   6  1                   [TEST MODE OF PERI   
  17 6TQB          BNG   6  MACH                [J IF READ ALL CHARS.
  20 6W=2          SRC   6  1                   [BITS OF X6 INTO NORMAL POSITIONS
   8 6WPL          LDX   1  FX1  
  13 6X9=          ANDX  5  MASK13(1)           [R=0 
  13 6XNW          ANDX  6  MASK13(1)           [C=0 
  18 6Y8G    NEXRE CALL  1  SRGNC               [BRING DOWN NEXT RECORD  
   8 6YN6          BRN      PG   
  20 6_7Q    MACH  SRC   6  1                   [BITS OF X6 INTO NORMAL POSITION 
   8 6_MB          SMO      FX1  
  15 7272          ANDX  5  MASK8               [E=S=ALPHA   
   7 72LL    PI    LDX   0  5
  13 736=          ANDN  0  #7777               [R   
   7 73KW          LDX   1  6
  13 745G          ANDN  1  #7777               [C   
  14 74K6          BXE   1  0,NEXRE             [J IF R=C
  13 754Q          LDN   4  #76                 [_   
  14 75JB          ADN   6  1                   [C=C+1   
  15 7642          CALL  0  SRTCH               [TRANSFER _  
   7 76HL          LDX   0  5
  13 773=          ANDN  0  #7777               [R   
   7 77GW          LDX   1  6
  13 782G          ANDN  1  #7777               [C   
  14 78G6          BXE   1  0,NEXRE             [J IF R=C
  14 78_Q          LDN   4  #20                 [SPACE   
  14 79FB          ADN   6  1                   [C=C+1   
  16 79_2          CALL  0  SRTCH               [TRANSFER SPACE  
   8 7=DL          BRN      PI   
   4 7=Y=    #   
   8 7?CW    MOPER CALL  4  SRER 
  20 7?XG          ILLEGAL  MODE,,3             [PRINT MESSAGE AND MEMBER NUMBER 
   4 7#C6    RESV
   8 7#WQ          CALL  4  SRER 
  20 7*BB          ILLEGAL  RESVIOL,,3          [PRINT MESSAGE AND MEMBER NUMBER 
   4 7*W2    WC  
   8 7B*L          CALL  4  SRER 
  20 7BT=          ILLEGAL  COUNT,,3            [PRINT MESSAGE AND MEMBER NUMBER 
   4 7C#W    NORE
   8 7CSG          COMPQU   NORE1
  10 7D#6          BRN      K1SIMULPTR   
   5 7DRQ    NORE1   
   8 7F?B          LDX   2  FX2  
  15 7FR2          LDX   0  ACA1(2)             [TYPE/MODE   
  17 7G=L          SRL   0  6                   [IGNORE A MAID MODE  
   8 7GQ=          ANDN  0  #777 
  20 7H9W          SBN   0  #300                [TEST IF MONITORING MODE #30000  
  14 7HPG          BNZ   0  XBACK               [J IF NOT
  15 7J96          LDX   0  5                   [PERI COUNT  
  21 7JNQ          SBX   0  6                   [SUBTRACT NO. OF TRANSFERED CHARS.   
   8 7K8B          SRL   0  12   
  16 7KN2          ANDN  0  #377                [REMAINDER COUNT 
   8 7L7L          LDCT  7  #50  
  15 7LM=          ORX   7  0                   [SET B3 & B5 
  10 7M6W          LDX   1  APETADDR(2)  
  16 7MLG          LDX   3  ACAP(2)             [PTR TO PERI CA  
   7 7N66          ADN   3  1
  10 7NKQ          FADDRESS 1,3,HLOCK1   
  15 7P5B          STO   7  0(3)                [REPLY WORD  
   7 7PK2          BACKSPACE 
   8 7Q4L          BRN      OEND 
   7 7QJ=    XBACK BACKSPACE 
  17 7R3W          LDX   7  FTYPNO(2)           [PERIPHERAL TYPE/NO  
   8 7RHG          CALL  4  SRER 
  10 7S36          OUTPACK  7,1,PERINAME 
  20 7SGQ          ILLEGAL  FILE,,3             [PRINT MESSAGE AND MEMBER NUMBER 
   4 7T2B    SRER
  18 7TG2          SBX   4  FX1                 [SUBTRACT FX1 FROM LINK  
   8 7T_L          LDX   2  FX2  
  15 7WF=          LDX   3  AMEMNO(2)           [MEMBER NO.  
  16 7WYW    TOPCA CALL  0  SRPCA               [CHANGE TO PCA   
  21 7XDG          UNPLUG                       [UNPLUGS OBJECT PROGRAM FROM EXECUTIV
  17 7XY6          ADX   4  1                   [ADD NEW FX1 TO LINK 
   7 7YCQ          EXIT  4  0
   4 7YXB    WAIT
   7 7_C2          LDN   3  1
   5 7_WL    WAIT2   
  15 82B=          FDRMTEST STEPWAIT,NODRM      [TEST FOR DRM
   8 82TW          BNZ   3  WAIT3
  17 83*G          LDCT  3  #410                [REJECTED INOPERABLE 
   9 83T6          BRN      OLPREP   
   5 84#Q    WAIT3   
  15 84SB          LDCT  3  #420                [REPLY WORD  
   9 85#2          BRN      OLPREP   
   5 85RL    NODRM   
  16 86?=          BZE   3  NORE                [J IF NO RECORDS 
  16 86QW          CALL  0  SRPCA               [CHANGG TO PCA   
   9 87=G          ACROSS   OLPODD,3 
   4 87Q6    #   
  21 889Q    SRPCA                              [ROUTINE TO DUMP X3-X7 & CHANGE TO PC
  10 88PB          LDX   1  APETADDR(2)  
  17 8992          STO   0  AWORK1(1)           [STORE LINK IN PCA   
   7 89NL          LDN   1  3
   8 8=8=          ADN   2  ACC3 
  15 8=MW          MOVE  1  5                   [DUMP X3-X7  
   8 8?7G          LDX   1  FX1  
  16 8?M6          TOPCA1   PLINK1(1)           [CHANGE TO PCA   
  15 8#6Q          LDX   0  AWORK1(2)           [RESTORE LINK
   7 8#LB          EXIT  0  0
   4 8*62    #   
  13 8*KL    #     INSERT N/L IF RECORD IN GRAPHIC   
   4 8B5=    #   
   5 8BJW    NLINSERT
   7 8C4G          LDX   0  5
  13 8CJ6          ANDN  0  #7777               [R   
  13 8D3Q          BZE   0  NEWRC               [R=0 
   8 8DHB          SMO      FX1  
  13 8F32          ANDX  5  MASK13              [R=0 
   8 8FGL    NLINS SMO      FX2  
   9 8G2=          LDEX  2  AWORK2   
  14 8GFW          BNZ   2  TCOUN               [J IF CR 
  17 8G_G          BNG   6  TESTO               [J IF PERI IN GRAPHIC
  13 8HF6          LDN   4  #76                 [_   
   8 8HYQ          CALL  0  SRTCH
  13 8JDB          LDN   4  #32                 [*   
   8 8JY2          CALL  0  SRTCH
  13 8KCL          LDN   7  SREXIT(1)           [LINK
   7 8KX=    TESTO LDX   0  6
   7 8LBW          SLL   0  2
  17 8LWG          BNG   0  QUIT                [J IF READ TO STOP   
  18 8MB6          LDX   1  7                   [RESTORE LINK FOR SRGNC  
  17 8MTQ          BRN      NEWRC               [J IF READ N CHARS   
   4 8N*B    #   
  13 8NT2    #     SPACE-FILLING IF SIMULATION OF CR 
   4 8P#L    #   
   6 8PS=    SPACEFILL   
   8 8Q?W          LDN   4  #20  
   8 8QRG    PUTSP CALL  0  SRTCH
   8 8R?6          BRN      PUTSP
   4 8RQQ    #   
   7 8S=B    TCOUN LDX   2  5
  15 8SQ2          SRL   2  12                  [PERI COUNT  
   8 8T9L          LDX   1  FX1  
  13 8TP=          ANDX  5  MASK13(1)           [R=0 
  17 8W8W          BXL   2  MAXC(1),SPACEFILL   [J IF PERI COUNT<80  
   7 8WNG          LDX   0  6
  13 8X86          SRL   0  12                  [D   
   8 8XMQ          LDN   2  80   
  17 8Y7B          BXGE  0  2,TWORD             [J IF PERI COUNT>80  
  13 8YM2          LDCT  5  #12                 [N=80
   9 8_6L          BRN      SPACEFILL
   7 8_L=    TWORD LDX   2  6
  21 925W          ANDX  2  MASK14(1)           [TEST IF WHOLE LAST WORD HAS BEEN TRA
  14 92KG          BZE   2  SET2                [J IF YES
  15 9356          LDX   4  #20                 [LOAD SPACE  
  16 93JQ          CALL  0  SRTCH               [TRANSFER SPACE  
  15 944B          BRN      TWORD               [J TO TEST   
   4 94J2    #   
  12 953L    #     SUBROUTINE: GET NEXT CHARACTER
   4 95H=    #   
   9 962W    #           X0 UNDEFINED
   8 96GG    #           X1 LINK 
   9 9726    #           X2 UNDEFINED
  11 97FQ    #           X3 POINTER TO RECORD
  11 97_B    #           X4 LOADED CHARACTER 
  15 98F2    #           X5 B0    =E=CURRENT SHIFT IN RECORD 
  17 98YL    #              B1    =S=CURRENT SHIFT ASSUMED BY READER 
  14 99D=    #              B2-3  =RECD.MODE=0=SHIFT MODE
  14 99XW    #                               2=GRAPHIC   
  15 9=CG    #                               3=PUNCH BLANKS  
  13 9=X6    #              B4-11 =N=COUNT IN PERI   
  13 9?BQ    #              B12-23=R=SIZE OF RECORD  
  12 9?WB    #           X6 B0-3  =MODE OF PERI  
  18 9#B2    #              B4-11 =D=CHAR.POINTER TO NEXT CHAR.IN BUFFER 
  18 9#TL    #              B12-23=C=CHAR.POINTER TO NEXT CHAR.IN RECORD 
   4 9**=    #   
   5 9*SW    SRGNC   
  15 9B#G          LDX   0  5                   [PICK UP R   
   8 9BS6          ANDN  0  #7777
  15 9C?Q          LDX   2  6                   [PICK UP C   
   8 9CRB          ANDN  2  #7777
  15 9D?2          STO   1  7                   [STORE LINK  
  14 9DQL          BXL   2  0,R6                [J IF C<R
   8 9F==          SMO      FX2  
   9 9FPW          LDEX  2  AWORK2   
  15 9G9G          BZE   2  TESTG               [J IF NOT CR 
  19 9GP6          BNZ   0  TCOUN               [J IF CR TO CHECK PERI COUNT 
  17 9H8Q          BRN      NEWRC               [BRING DOWN RECORD   
   7 9HNB    TESTG LDX   2  5
  15 9J82          SLL   2  2                   [PICK UP MODE
  18 9JML          BPZ   2  NEWRC               [J IF NORMAL OR ALLCHAR  
   7 9K7=          SLL   2  1
  15 9KLW          BPZ   2  NLINSERT            [J IF GRAPHIC
  18 9L6G    NEWRC SBX   1  FX1                 [SUBTRACT FX1 FROM LINK  
   7 9LL6          LDX   4  1
  21 9M5Q          STEP                         [SET POINTER TO HEADER OF NEXT RECORD
  19 9MKB          TESTREP  STEPWAIT,WAIT       [J IF END COMMUNICATION FILE 
  15 9N52          BZE   3  WAIT2               [END OF FILE 
  16 9NJL          LDX   2  FX1                 [PICK UP NEW FX1 
  16 9P4=          ADX   4  2                   [MAKE NEW LINK   
  15 9PHW          STO   4  7                   [STORE IN X7 
  13 9Q3G          ANDX  6  MASK13(2)           [L=0 
  21 9QH6          LDX   0  0(3)                [PICK UP NUMBER OF WORDS IN RECORD   
  17 9R2Q          SBN   0  2                   [SUBTRACT RED TAPE   
  21 9RGB          SLL   0  2                   [MULTIPLY BY 4=NUMBER OF CHARACTERS  
  20 9S22          LDX   1  1(3)                [PICK UP SECOND WORD OF RED TAPE 
  21 9SFL          SLC   1  2                   [PICK UP NUMBER OF USED CHARACTERS   
  15 9S_=          ANDN  1  #3                  [IN LAST WORD
   8 9TDW          BZE   1  OK   
  18 9TYG          SBN   0  4                   [SUBTRACT FOUR CHARACTERS
  21 9WD6          ADX   0  1                   [AND ADD NUMBER OF CHAR.IN LAST WORD 
  19 9WXQ    OK    DSA   0  5                   [PUT SIZE OF RECORD INTO X5  
  15 9XCB          LDX   0  5                   [PICK UP MODE
  17 9XX2          SLC   0  4                   [OF PREVIOUS RECORD  
   8 9YBL          ANDN  0  #3   
   8 9YW=          SMO      FX2  
   9 9_*W          LDEX  1  AWORK2   
  17 9_TG          BNZ   1  RDCRD               [J IF CR SIMULATION  
  19 =2*6          LDX   1  1(3)                [PICK UP MODE OF NEW RECORD  
   7 =2SQ          SRL   1  6
   8 =3#B          ANDN  1  #77  
  14 =3S2          BZE   1  R11                 [J IF #00
  14 =4?L          BXE   1  RNO1(2),R12         [J IF #02
  14 =4R=          BXE   1  RNO2(2),R1          [J IF #04
  14 =5=W          BXE   1  RAC2(2),R12         [J IF #22
  14 =5QG          BXE   1  RGS1(2),R2          [J IF #10
  14 =6=6          BXE   1  RGS2(2),R2          [J IF #12
  14 =6PQ          BXE   1  RAC1(2),R3          [J IF #20
  18 =79B          BRN      R1                  [J IF NEW TYPE OF FILE   
  19 =7P2    RDCRD LDX   1  1(3)                [PICK UP MODE OF NEW RECORD  
   7 =88L          SRL   1  6
   8 =8N=          ANDN  1  #77  
  18 =97W          BZE   1  XC                  [J TO TEST TYPE OF FILE  
  18 =9MG          BXE   1  RGS1(2),RGM         [J IF RECORD MODE IS #10 
  18 ==76          BXE   1  RGS2(2),RGM         [J IF RECORD MODE IS #12 
  16 ==LQ          BRN      PD                  [J IF OTHER MODES
   8 =?6B    XC    SMO      FX2  
   9 =?L2          LDX   2  AWORK2   
  18 =#5L          BNG   1  RGM                 [J IF CR,CP OR LP FILE   
   8 =#K=          BRN      PD   
   8 =*4W    R11   SMO      FX2  
  21 =*JG          LDX   1  AWORK2              [PICK UP INFORMATION ABOUT TYPE OF FI
  18 =B46          BNG   1  R2                  [J IF CR,CP.OR LP FILE   
  14 =BHQ    R12   ANDX  5  MASK6(2)            [S=ALPHA 
  15 =C3B    R1    LDN   1  0                   [A SHIFT MODE
   8 =CH2          BRN      R4   
  14 =D2L    R2    LDN   1  2                   [GRAPHIC 
  14 =DG=          ANDX  5  MASK6(2)            [S=ALPHA 
   8 =D_W          BRN      R4   
   7 =FFG    R3    LDX   1  5
   7 =F_6          SLL   1  1
  14 =GDQ          DSA   1  5                   [R=R.2   
   7 =GYB          LDN   1  3
  21 =HD2    R4    BXE   1  0,R5                [J IF EQUAL TO MODE OF PREVIOUS RECOR
  16 =HXL          SLC   5  2                   [MODE INTO B0-1  
  21 =JC=          SLL   5  2                   [ERASE MODE,B23-24 FILLED WITH ZEROES
  17 =JWW          ORX   5  1                   [PUT NEW MODE INTO X5
  21 =KBG          SRC   5  4                   [SHIFT BITS OF X5 INTO NORMAL POSITIO
  14 =KW6          BXE   1  TH(2),PG            [J IF #20
  14 =L*Q          BXE   1  TW(2),PH            [J IF #10
  19 =LTB          BNG   6  PD                  [J IF READ IN GRAPHIC MODE   
  14 =M*2          BRN      PA                  [J IF NOT
  19 =MSL    R5    BXE   1  TH(2),PG            [J IF NEW MODE IS #20 AGAIN  
  18 =N#=          LDX   0  6                   [PICK UP MODE OF READING 
   7 =NRW          SLC   0  3
  18 =P?G          BNG   0  R7                  [J IF IN PREVIOUS SHIFT  
   7 =PR6          LDX   0  6
   8 =Q=Q          SRL   0  12   
  15 =QQB          ANDN  0  #377                [PICK UP D   
  18 =R=2          BNZ   0  R7                  [J TO READ IN PREVIOUS   
  19 =RPL                                       [IF PREV.PERI IS NOT FINISHED
  20 =S9=          ANDX  5  MASK6(2)            [S=ALPHA,IF READ IN ALPHA SHIFT  
   7 =SNW    R7    LDX   0  5
  13 =T8G          ANDN  0  #7777               [R   
  13 =TN6          BZE   0  NLINS               [R=0 
   7 =W7Q          LDX   2  6
  15 =WMB          ANDN  2  #7777               [PICK UP C   
  20 =X72    R6    SRC   2  2                   [X2 AS CHARACTER COUNTER-MODIFIER
   7 =XLL          SMO      3
  17 =Y6=          LDCH  4  2(2)                [LOAD NEXT CHARACTER 
  14 =YKW          ADN   6  1                   [C=C+1   
  15 =_5G          LDX   1  7                   [RESTORE LINK
   7 =_K6          EXIT  1  0
   4 ?24Q    #   
  12 ?2JB    #     SUBROUTINE: TRANSFER CHARACTER
   4 ?342    #   
   9 ?3HL    #           X0    LINK  
  10 ?43=    #           X1-X2 UNDEFINED 
  10 ?4GW    #           X3-X7 SEE SRGNC 
   4 ?52G    #   
   5 ?5G6    SRTCH   
  15 ?5_Q          STO   0  7                   [STORE LINK  
  15 ?6FB          LDX   2  6                   [PICK POINTER
  19 ?6_2          SRC   2  12                  [TO NEXT CHARACTER JN BUFFER 
   8 ?7DL          ANDN  2  #377 
   8 ?7Y=          SMO      FX2  
  16 ?8CW          LDX   0  ACA4                [BUFFER ADDRESS  
   7 ?8XG          SLC   0  2
  20 ?9C6          ADX   2  0                   [AND ADD TO POINTER TO NEXT CHAR.
  20 ?9WQ          SRC   2  2                   [X2 AS CHARACTER COUNTER-MODIFIER
   8 ?=BB          SMO      FX2  
   9 ?=W2          LDX   1  APETADDR 
  17 ??*L          FADDRESS 1,2,HLOCK2          [CONVERT BUFFER ADD. 
  20 ??T=          DCH   4  0(2)                [MOVE CHAR.INTO OBJ.PROGRAM AREA 
   8 ?##W          LDX   1  FX1  
  14 ?#SG          ADX   6  MASK11(1)           [D=D+1   
   8 ?*#6          SMO      FX2  
   9 ?*RQ          LDEX  2  AWORK2   
  17 ?B?B          BNZ   2  TCR                 [J IF CR SIMULATION  
   9 ?BR2          LDX   0  MASK1(1) 
   7 ?C=L          LDX   2  0
  15 ?CQ=    TETRA ANDX  0  6                   [PICK UP D   
  15 ?D9W          ANDX  2  5                   [PICK UP N   
  14 ?DPG          BXE   2  0,QUIT              [J IF N=D
   8 ?F96          BRN      RELIN
   9 ?FNQ    TCR   LDX   0  MASK13(1)
   7 ?G8B          LDX   2  0
  13 ?GN2          ANDX  0  6                   [D   
  13 ?H7L          ANDX  2  5                   [N   
   9 ?HM=          BXU   2  0,RELIN  
  13 ?J6W          ANDX  5  MASK13(1)           [R=0 
   8 ?JLG          BRN      TWORD
  15 ?K66    RELIN LDX   0  7                   [RESTORE LINK
   7 ?KKQ          EXIT  0  0
  12 ?L5B          MENDAREA   AOLPGAP,K99SIMULPTR
   4 ?LK2    #END
   8 ____ ...02131607000200000000
