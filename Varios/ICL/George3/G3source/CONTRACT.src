  16 22FL ...#SEG  CONTRACT70                   [FRANK BLUNDELL  
   7 22_=    #LIS  K0CONTRACT
   7 23DW          8HCONTRACT
   4 23YG    #   
  18 24D6    #     THIS  SEGMENT DEALS WITH CONTRACT FOR PERMANENT FILES 
  13 24XQ    #     ON ENTRY CURRENT ACTIVITY IS CPA  
  20 25CB    #     THE CONTROL AREA HAS BEEN CHECKED AND A GEXD BLOCK IS CHAINED 
  19 25X2    #     AFTER THE CURRENT ACTIVITY CONTAINING DETAILS OF THE PERI 
   4 26BL    #   
   4 26W=    #   
   4 27*W    #   
   4 27TG    #   
   4 28*6    #   
  10 28SQ          SEGENTRY K1CONTRACT   
  10 29#B          MHUNTW   3,EWDAS,GEXD 
  16 29S2          STOZ     AWORK1(2)           [COUNT OF BUCKETS
  16 2=?L          STOZ     AWORK2(2)           [COUNT OF AREAS  
  10 2=R=          MFINDEXOF 1,CEFN(3)   
   9 2?=W          NGX   5  BREQ(3)  
   9 2?QG          LDX   4  FIP+8(1) 
   8 2#=6          ANDN  4  #77  
  18 2#PQ          DVS   4  4                   [X5=BUCKETS TO BE REMOVED
   9 2*9B    #SKI  K6CONTRACT>199-199
   4 2*P2    (   
  11 2B8L          TRACE    CEFN(3),CONTRACT 
   8 2BN=          TRACE    5,   
   8 2C7W          BZE   4  XA   
  10 2CMG    XB    GEOERR   1,CONTRACT   
   4 2D76    XA  
   4 2DLQ    )   
  16 2F6B          LDEX  0  FIP+5(1)            [NUMBER OF AREAS 
   7 2FL2          SBN   0  1
   7 2G5L          SLL   0  1
   7 2GK=          LDX   4  0
   7 2H4W          SLL   0  1
  18 2HJG          ADX   4  0                   [X4=POINTER TO LAST CELL 
   9 2J46          STO   4  AWORK3(2)
   9 2JHQ          LDEX  7  FIP+5(1) 
   9 2K3B    #SKI  K6CONTRACT>199-199
  20 2KH2          BZE   7  XB                  [ERROR IF NO AREAS-SHOULD BE DONE
  15 2L2L          LDX   6  EOPMODE(3)          [BY EXTEND   
   8 2LG=          SBN   6  #400 
   8 2L_W    XD    BNZ   6  XCB  
   7 2MFG          SMO      4
  10 2M_6          LDX   0  FDCELLS-1(1) 
   9 2NDQ          TXL   0  FIP+10(1)
   8 2NYB          BCS      XC   
   7 2PD2    XCB   SMO      4
  10 2PXL          LDX   0  FDCELLS+5(1) 
   7 2QC=          SMO      4
  17 2QWW          SBX   0  FDCELLS-1(1)        [BUCKETS IN THIS AREA
  17 2RBG          ADX   0  AWORK1(2)           [ADD IN TOTAL SO FAR 
  12 2RW6          TXL   5  0                   [
  20 2S*Q          BCS      XC                  [JUMP IF THIS WOULD BE TOO MUCH  
  17 2STB          STO   0  AWORK1(2)           [RESET TOTAL SO FAR  
   7 2T*2          LDN   0  1
  21 2TSL          ADS   0  AWORK2(2)           [ADD 1 TO COUNT OF AREAS TO REMOVE   
   9 2W#=          TXU   5  AWORK1(2)
  19 2WRW          BCC      XC                  [JUMP IF TOTAL EXACTLY ENOUGH
  17 2X?G          SBN   4  6                   [UPDATE CELL POINTER 
  17 2XR6          BCT   7  XD                  [JUMP IF MORE AREAS  
   4 2Y=Q    #   
  17 2YQB    #     AWORK2 NOW CONTAINS A COUNT OF AREAS TO BE REMOVED
  16 2_=2    #     AWORK3 NOW CONTAINS A POINTER TO THE LAST AREA
   4 2_PL    #   
   9 329=    XC    LDX   0  ASUSN(1) 
  19 32NW          STO   0  HMUNIT(3)           ['HOME' UNIT SERIAL NUMBER   
  20 338G          STO   0  ARUNIT(3)           [TEMPORARY-AREA UNIT=HOME UNIT   
  20 33N6          STOZ     AWORK1(2)           [WILL CONTAIN A COUNT OF BUCKETS 
  14 347Q                                       [REMOVED 
   4 34MB    #   
  17 3572          SUSEST   ASUSN(1),XE         [GET HOME UNIT ONLINE
  10 35LL    XG    MHUNTW   3,EWDAS,GEXD 
  10 366=          MFINDEXOF 1,CEFN(3)   
   8 36KW          LDCT  0  #20  
   9 375G          ANDX  0  EXMARK(1)
  20 37K6          BZE   0  XF                  [JUMP IF NOT SYSTEM FILE OPEN FOR
  14 384Q                                       [WRITING 
   9 38JB          LDX   0  HMUNIT(3)
  18 3942          STO   0  ESERL(3)            [STORE SUSN FOR EXOPEN   
  17 39HL          LDX   5  BACK2(1)            [SAVE EXOFILE NUMBER 
  17 3=3=          LDX   4  ALOGLEN(1)          [GET LOGICAL LENGTH  
   9 3=GW          LDX   7  EMARK(3) 
  16 3#FB          PLOCKSU  HMUNIT(3),SMA,SNA   [LOCK HOME UNIT  
  16 3#_2          SETUPCORE 4,2,EWDAS,EXOF     [GET ANOTHER EXOF
   8 3*DL          MFINDEXOF 1,5 
   9 3*Y=          STO   5  BACK2(2) 
   8 3BCW          ADN   1  A1   
   8 3BXG          ADN   2  A1   
  15 3CC6 ...      MOVLONG  1,2,4               [COPY EXOF   
  13 3DBB          LDN   0  BOPEN+BSPEC+BSERL+BLOCK  
  10 3DW2          STO   0  EXMARK-A1(2) 
  17 3F*L          DOWN     EXOPEN,1            [GO TO REFIND FILE   
  15 3FT=          TESTREP  OK,XH               [JUMP IF OK  
   9 3G#W          TESTREP  EXFAIL,XJ
   9 3GSG          GEOERR   1,EXOREP 
  12 3H#6    XH    MHUNTW   2,EWDAS,EXOF        [
   8 3HRQ          MFINDEXOF 1,5 
  15 3J?B          LDX   0  HOME(2)             [RESET HOME  
   9 3JR2          STO   0  HOME(1)  
   6 3JY8 ...#UNS  IPBD  
   4 3K5B ...#SKI
  17 3K=L          LDX   4  1                   [SAVE EXOF POINTER   
  16 3KQ=          FREECORE 2                   [FREE SPARE EXOF 
  10 3L9W          MHUNTW   3,EWDAS,GEXD 
   9 3LPG          STO   7  EMARK(3) 
  16 3M96          UNLOCKSU HMUNIT(3)           [UNLOCK HOME UNIT
   6 3MB# ...#UNS  IPBD  
   4 3MHG ...#SKI
  17 3MNQ          LDX   1  4                   [RESET EXOF POINTER  
   6 3MR* ...#UNS  IPBD  
   4 3MTY ...(   
  10 3MYH ...      MHUNTW   3,EWDAS,GEXD 
   8 3N36 ...      MFINDEXOF 1,5 
   4 3N5P ...)   
   4 3N8B    #   
   4 3NN2    #   
   8 3P7L    XF    LDX   2  FX2  
  21 3PM=          LDX   7  AWORK2(2)           [X7 HOLDS COUNT OF AREAS TO REMOVE   
  12 3Q6W    #SKI  K6CONTRACT>199-199           [
   4 3QLG    (   
  20 3R66          BNZ   7  XFA                 [ERROR IF NO AREAS TO BE REMOVED-
  20 3RKQ          GEOERR   1,ZEROAREA          [SHOULD BE DEALT WITH BY EXTEND  
   4 3S5B    XFA 
   4 3SK2    )   
  19 3T4L          LDX   6  AWORK3(2)           [X6=POINTER TO CURRENT FDCELL
   4 3TJ=    #   
   8 3W3W    #     CONTRACT LOOP 
   4 3WHG    #   
   7 3X36    ZB    LDX   4  1
   6 3X8# ...#UNS  IPBD  
   4 3X*G ...#SKI
   7 3XGQ          PHOTO    5
   7 3Y2B          SMO      6
  10 3YG2          LDX   0  FDCELLS(1)   
  16 3Y_L          STO   0  ARUNIT(3)           [GET AREA SUSN   
   9 3_F=          TXU   0  HMUNIT(3)
  19 3_YW          BCC      XL                  [JUMP IF SAME AS HOME UNIT   
  18 42DG          SUSEST   ARUNIT(3),XM        [OTHERWISE GET IT ONLINE 
   6 42G9 ...#UNS  IPBD  
   4 42HY ...(   
  10 42KM ...XN    MHUNTW   3,EWDAS,GEXD 
  20 42MB ...XL    LOCKM    HMUNIT(3),ARUNIT(3),SAX,SBX [GET LOCK OUTS IN ORDER  
   4 42P5 ...)   
   6 42QS ...#UNS  IPBD  
   4 42SH ...#SKI
   4 42W= ...(   
   8 42Y6    XN    TESTMOVE 5,XL 
  10 43CQ          MHUNTW   3,EWDAS,GEXD 
  16 45B=    XL    PLOCKSU  HMUNIT(3),SAA,SBA   [LOCK HOME MNIT  
   7 45TW          LDX   1  4
   8 46*G          TESTMOVE 5,XP 
   4 46K# ...)   
  10 46T6          MHUNTW   3,EWDAS,GEXD 
  10 47#Q          MFINDEXOF 1,CEFN(3)   
   7 47SB    XP    SMO      6
  10 48#2          LDX   5  FDCELLS+5(1) 
   7 48RL          SMO      6
  18 49?=          SBX   5  FDCELLS-1(1)        [X5=BUCKETS IN THIS AREA 
  10 49QW          STO   5  CELLA+5(3)   
  18 4==G          ADS   5  AWORK1(2)           [ADD TO CUMULATIVE TOTAL 
   7 4=Q6          SMO      6
  10 4?9Q          LDX   0  FDCELLS-1(1) 
   7 4?PB          SMO      6
  19 4#92          STO   0  FDCELLS+5(1)        [MAKE LAST AREA LOOK EMPTY   
   7 4#NL          LDX   4  1
   9 4*8=          OPENST   HMUNIT(3)
   7 4*MW          LDX   1  4
  17 4B7G          LDX   5  HOME(1)             [FIP CELL BS ADDRESS 
  18 4BM6          LDN   4  133                 [CONTROL HEADER ADDRESS  
  17 4C6Q          READSAVE 4,XQ                [READ AND SAVE C/H   
   8 4CLB          TESTREP  OK,XR
   9 4D62    XV    TESTREP  EXFAIL,R1
   9 4DKL    XS    GEOERR   1,TRANS1 
   9 4F5=    XR    LDX   1  ECOR(3)  
  17 4FJW    XQ    STO   1  AWORK2(2)           [SAVE ADDRESS OF C/H 
  17 4G4G          READSAVE 5,XT                [READ AND SAVE FIP   
   9 4GJ6          TESTREPNOT OK,XV  
   9 4H3Q          LDX   1  ECOR(3)  
  17 4HHB    XT    STO   1  AWORK3(2)           [SAVE ADDRESS OF FIP 
  18 4J32          LDX   4  1(1)                [POINTER TO FIRST FDCELL 
  18 4JGL          BZE   4  R2                  [JUMP IF NO MORE CELLS   
  17 4K2=          READSAVE 4,XW                [READ AND SAVE CELL  
   9 4KFW          TESTREPNOT OK,XV  
   9 4K_G          LDX   1  ECOR(3)  
  17 4LF6    XW    STO   1  AWORK4(2)           [SAVE ADDRESS OF CELL
   9 4LYQ          LDN   2  CELLA(3) 
  17 4MDB          MOVE  1  5                   [SAVE COPY OF CELL   
   8 4MY2          LDX   0  0(1) 
   8 4NCL          SMO      FX2  
   9 4NX=          LDX   2  AWORK3   
  19 4PBW          STO   0  1(2)                [STORE POINTER TO NEXT AREA  
   8 4PWG          LDEX  0  7(2) 
  19 4QB6          SBN   0  1                   [REDUCE COUNT OF FILE AREAS  
   8 4QTQ          DEX   0  7(2) 
  18 4R*B          CHUCKWR  C,2,XY              [CHUCK ANDWZITE FIP CELL 
   8 4RT2          TESTREP  OK,XY
   9 4S#L    YD    TESTREP  EXFAIL,R3
   8 4SS=          BRN      XS   
   9 4T?W    XY    LDX   0  HMUNIT(3)
   9 4TRG          TXU   0  ARUNIT(3)
  19 4W?6          BCC      YA                  [JUMP IF AREA ON HOME UNIT   
  20 4WQQ          LDX   2  FX2                 [OTHERWISE CELL IS CHAINED INTO  
  15 4X=B                                       [VACANT CHAIN
   9 4XQ2          SMO      AWORK2(2)
  19 4Y9L          LDN   1  7                   [X1=POINTER TO VACANT CHAIN  
   8 4YP=    YC    LDX   0  0(1) 
   7 4_8W          TXL   0  4
  21 4_NG          BCC      YB                  [JUMP IF NEXT CELL HAS HIGHER ADDRESS
  18 5286          BZE   0  YB                  [OR IF THIS IS LAST CELL 
  16 52MQ          READST   0(1),YC             [READ NEXT CELL  
   9 537B          TESTREPNOT OK,YD  
   9 53M2          LDX   1  ECOR(3)  
   8 546L          BRN      YC   
   9 54L=    YB    SMO      AWORK4(2)
  21 555W          STO   0  0                   [STO ADDRESS OF NEXT CELL IN WORD 0  
  17 55KG          SAVEST   C,1                 [OF CELL BEING FREED 
  21 5656          LDX   5  1                   [SAVE VACANT CELL AND CORE POINTER   
  20 56JQ          CHUCKWR  C,AWORK4(2),YE      [CHUCK AND WRITE CELL BEING FREED
   9 574B          TESTREPNOT OK,YD  
   7 57J2    YE    SMO      5
  21 583L          STO   4  0                   [STORE ADDRESS OF CELL BEING FREED   
  19 58H=          CLOSEWR  C,5                 [WRITE CELL AND CLOSE UNIT   
   9 592W          TESTREPNOT OK,YD  
  10 59GG          MHUNTW   3,EWDAS,GEXD 
   6 59LC ...#UNS  IPBD  
   4 59Q# ...#SKI
   4 59W9 ...(   
   9 5=26          UNLOCKSU HMUNIT(3)
  11 5?YL    YHA   PLOCKSU  ARUNIT(3),YHA,R4A
  10 5#D=          MHUNTW   3,EWDAS,GEXD 
   4 5#N4 ...)   
   4 5#XW    #   
  16 5*CG          OPENST   ARUNIT(3)           [OPEN AREA UNIT  
   8 5*X6          LDN   4  133  
  17 5BBQ          READSAVE 4,YF                [READ AND SAVE C/H   
   8 5BWB          TESTREP  OK,YG
   9 5CB2    YH    TESTREP  EXFAIL,R4
   8 5CTL          BRN      XS   
   9 5D*=    YG    LDX   1  ECOR(3)  
  18 5DSW    YF    STO   1  AWORK2(2)           [SAVE CORE ADDRESS C/H   
  21 5F#G          ADN   1  6                   [ADDRESS OF START OF FOREIGN CHAIN   
   7 5FS6          LDN   4  0
   8 5G?Q    YKB   LDX   1  0(1) 
  21 5GRB          BZE   1  R5                  [JUMP IF END OF CHAIN-SCA CORRUPT-   
  17 5H?2                                       [FOREIGN CELL MISSING
  17 5HQL          READSAVE 1,YJ                [READ AND SAVE CELL  
   9 5J==          TESTREPNOT OK,YH  
   9 5JPW          LDX   1  ECOR(3)  
   8 5K9G    YJ    LDX   0  1(1) 
  10 5KP6          TXU   0  CELLA+1(3)   
  16 5L8Q          BCS      YK                  [STARTING BLOCK  
   8 5LNB          LDX   0  2(1) 
  15 5M82          TXU   0  CELLA+2(3)          [ENDING BLOCK
   8 5MML          BCS      YK   
   8 5N7=          LDX   0  3(1) 
   8 5NLW          BZE   0  YK   
   9 5P6G          TXU   0  HMUNIT(3)
   8 5PL6          BCC      YL   
  18 5Q5Q    YK    BZE   4  YKA                 [JUMP IF NO CELL SAVED   
  17 5QKB          CHUCK    C,4                 [OTHERWISE CHUCK IT  
  18 5R52    YKA   LDX   4  1                   [REMEMBER CURRENT CELL   
   8 5RJL          BRN      YKB  
  15 5S4=    YL                                 [CELL FOUND  
  18 5SHW          BNZ   4  YLA                 [JUMP IF A1PREVIOUS CELL 
   9 5T3G          SMO      AWORK2(2)
  18 5TH6          LDN   4  6                   [OTHERWISE USE C/H BASE  
   8 5W2Q          SAVEST   C,4  
  20 5WGB    YLA   STO   1  AWORK4(2)           [SAVE ADDRESS OF CELL BEING FREED
   8 5X22          LDX   0  0(1) 
   7 5XFL          SMO      4
  17 5X_=          STO   0  0                   [STORE  NEW POINTER  
  20 5YDW          CHUCKWR  C,4,YAA             [CHUCK AND WRITE PREVIOUS CELL   
   9 5YYG          TESTREPNOT OK,YH  
  10 5_D6          MHUNTW   3,EWDAS,GEXD 
   9 5_XQ    YAA   LDX   0  ARUNIT(3)
   9 62CB          SMO      AWORK4(2)
  19 62X2          STO   0  3                   [RESET SERIAL NUMBER IN CELL 
   4 63BL    #   
  20 63W=    #     THE NEXT PIECE OF CODE CHAINS THE AREA INTO THE UNALLOCATED   
  12 64*W    #     CHAIN ON THE APPROPRIATE UNIT 
  18 64TG    #     THIS IS THE AREA UNIT IF AREA UNIT IS NOT HOME UNIT   
  15 65*6    #     OR THE HOME UNIT IF HOME UNIT=AREA UNIT   
   9 65SQ    #     IN BOTH CASES:-   
  18 66#B    #                    AWORK2=CORE ADDRESS OF CONTROL HEADER  
  20 66S2    #                    AWORK4=CORE ADDRESS OF CELL BEING FREED -THIS  
  19 67?L    #     MAYBE EITHER THE ORIGINAL FILE AREA CELL(HOME UNIT) OR THE
  14 67R=    #     CELL FROM FOREIGN CHAIN (AREA UNIT)   
   4 68=W    #   
   4 68QG    #   
   4 69=6    #   
   4 69PQ    #   
   8 6=9B    YA    LDX   2  FX2  
   9 6=P2          SMO      AWORK2(2)
  19 6?8L          LDN   1  5                   [ADDRESS OF UNALLOCATED CHAIN
   7 6?N=          LDN   4  0
  17 6#7W    YR    LDX   1  0(1)                [ADDRESS OF NEXT CELL
   8 6#MG          BZE   1  YPB  
  17 6*76          READSAVE 1,YN                [READ AND SAVE CELL  
   9 6*LQ          TESTREPNOT OK,YH  
   9 6B6B          LDX   1  ECOR(3)  
  10 6BL2    YN    LDX   0  CELLA+1(3)   
  12 6C5L          TXL   0  1(1)                [
  21 6CK=          BCS      YP                  [JUMP IF THIS BLOCK NO. > OUR BLOCK  
  18 6D4W          BZE   4  YQ                  [JUMP IF CELL NOT FOUND  
  17 6DJG          CHUCK    C,4                 [CHUCK PREVIOUS CELL 
  19 6F46    YQ    LDX   4  1                   [SAVE AEERESS OF CURRENT CELL
   8 6FHQ          BRN      YR   
  17 6G3B    YP    CHUCK    C,1                 [CHUCK CURRENT CELL  
   8 6GH2    YPB   BNZ   4  YPA  
   9 6H2L          SMO      AWORK2(2)
  21 6HG=          LDN   4  5                   [SET X4 TO START OF UNALLOCATED CHAIN
   4 6H_W    YPA 
   7 6JFG          LDX   1  4
   9 6J_6          LDX   2  AWORK4(2)
  18 6KDQ          WORDST   2                   [GET BS ADDRESS OF CELL  
   9 6KYB          LDX   5  EWK(3)   
   8 6LD2          LDX   0  0(1) 
  20 6LXL          STO   0  0(2)                [SET POINTER IN CELL BEING FREED 
  18 6MC=          CHUCKWR  C,2,YS              [AND CHUCK AND WRITE IT  
   9 6MWW          TESTREPNOT OK,YH  
   7 6NBG          LDX   1  4
  21 6NW6    YS    STO   5  0(1)                [STORE ADDRESS OF CELL BEING FREED   
  21 6P*Q          CLOSEWR  C,1                 [IN CURRENT CELL AND CLOSE AND WRITE 
   9 6PTB          TESTREPNOT OK,YH  
  10 6Q*2          MHUNTW   3,EWDAS,GEXD 
  10 6RRW          MFINDEXOF 2,CEFN(3)   
  14 6S?G    XYB   FIPPER   HOME(2),ASUSN(2),XYB,XYC,NOC 
  10 6SR6    XYC   MHUNTW   3,EWDAS,GEXD 
   4 6T=Q    NOC 
  12 6W=2          LDX   0  HMUNIT(3)           [
  12 6WPL          TXU   0  ARUNIT(3)           [
  18 6X9=          BCC      ZA                  [JUMP IF AREA=HOME UNIT  
   9 6XNW          UNLOCKSU ARUNIT(3)
   6 6XW4 ...#UNS  IPBD  
  10 6Y3= ...      MHUNTW   3,EWDAS,GEXD 
  19 6Y8G          RELST    ARUNIT(3)           [UNLOCK AND RELEASE AREA UNIT
   6 6Y#C ...#UNS  IPBD  
   4 6YD# ...#SKI
   4 6YJ9 ...(   
  10 6YN6          MHUNTW   3,EWDAS,GEXD 
  16 6_7Q          LOCKSU   HMUNIT(3)           [LOCK HOME UNIT  
   4 6_CJ ...)   
  10 6_MB    ZA    MHUNTW   3,EWDAS,GEXD 
  15 7272          MFINDEXOF 3,CEFN(3)          [FIND EXOF   
  10 72LL          LDX   4  ALOGLEN(3)   
   7 736=          SBN   4  6
  20 73KW          LDEX  0  FIP+5(3)            [SUBTRACT 1 FROM COUNT OF AREAS  
   7 745G          SBN   0  1
   9 74K6          DEX   0  FIP+5(3) 
  17 754Q          ALTLEN   3,4                 [ALTER LENGTH OF EXOF
  17 75JB          SBN   6  6                   [UPDATE AREA POINTER 
  10 7642          MHUNTW   3,EWDAS,GEXD 
  16 76HL          UNLOCKSU HMUNIT(3)           [UNLOCK HOME UNIT
   6 76NS ...#UNS  IPBD  
  10 76W2 ...      MHUNTW   3,EWDAS,GEXD 
  10 773=          MFINDEXOF 1,CEFN(3)   
  17 77GW          BCT   7  ZB                  [BACK FOR NEXT AREA  
  17 782G          ACROSS   CONTEND,1           [GO AND SET REPLIES  
   4 78G6    #   
   4 78_Q    #   
   4 79FB    #   
   4 79_2    #   
   4 7=DL    #   
  19 7=Y=    #     AREA UNIT NOT ONLINE -CHECK FOR MODE #4000 AND SET REPLIES
  14 7?CW    #     IF PRESENT OTHERWISE GET UNIT ONLINE  
   4 7?XG    #   
  10 7#C6 ...XM    MHUNTW   3,EWDAS,GEXD 
   8 7*BB          LDCT  0  #10  
   9 7*W2          ANDX  0  EMARK(3) 
  18 7B*L ...      BNZ   0  SCA1                [JUMP IF MODE #4000 SET  
  11 7BT= ...      SMUSEST  ARUNIT(3),SB,SA  
  16 7C#W          BRN      XN                  [JUMP IF ONLINE  
  17 7CSG                                       [CANTDO HAS OCCURED  
   6 7D6X ...#UNS  IPBD  
   4 7DF# ...#SKI
   4 7DRQ    (   
  10 7F?B    SBA   MHUNTW   3,EWDAS,GEXD 
   9 7FR2          RELST    HMUNIT(3)
   4 7G=L    )   
   9 7GQ=    SB    LDX   0  AWORK1(2)
  21 7H9W ...      BNZ   0  SCA1                [JUMP IF CONTRACT HAS BEEN STARTED   
  16 7HPG          LDCT  7  #400                [OTHERWISE FAIL  
   8 7J96          BRN      SCA  
   6 7JHH ...#UNS  IPBD  
   4 7JTY ...#SKI
   4 7K8B    (   
  10 7KN2    SAA   MHUNTW   3,EWDAS,GEXD 
   9 7L7L          RELST    HMUNIT(3)
   4 7LM=    )   
  14 7M6W    SA    LDN   7  1                   [BREAKIN 
   9 7MLG          LDX   0  AWORK1(2)
  19 7N66 ...      BZE   0  SMA                 [JUMP IFNO AREAS REMOVED YET 
  18 7NKQ    SCA   ACROSS   CONTEND,2           [GO AND SET REPLIES ETC  
   7 7NQY ...SCA1  LDN   7  0
   8 7NY6 ...      BRN      SCA  
   4 7P5B    #   
  15 7PK2    #     HOME UNIT NOT ONLINE-CHECK FOR MODE #4000 
  19 7Q4L    #     AND GET UNIT ONLINE IF NOT PRESENT -OTHERWISE SET REPLIES 
   4 7QJ=    #   
  10 7R3W    XE    MHUNTW   3,EWDAS,GEXD 
   8 7RHG          LDCT  0  #10  
   9 7S36          ANDX  0  EMARK(3) 
  18 7SGQ          BNZ   0  SL                  [JUMP IF MODE #4000 SET  
   6 7SQJ ...#UNS  IPBD  
   7 7T2B ...      LDN   7  0
  11 7T=8 ...      SMUSEST  HMUNIT(3),SN,SM  
  17 7TG2          BRN      XG                  [JUMP IF ONLINE OK   
   6 7TTP ...#UNS  IPBD  
   4 7W9D ...#SKI
   4 7WK7 ...(   
  10 7WYW    SMA   MHUNTW   3,EWDAS,GEXD 
   9 7XDG          RELST    HMUNIT(3)
  10 7YCQ    SM    MHUNTW   3,EWDAS,GEXD 
  18 7YXB          FREECORE 3                   [CONTRACT HAS NOT BEEN   
  20 7_C2          STEPBACK                     [STARTED SO PROGRAM CAN REPEAT   
  14 7_WL          PROGBRKIN                    [THE PERI
   4 7_X9 ...)   
   6 7_XS ...#UNS  IPBD  
   4 7_YC ...(   
   4 7__2 ...#   
  15 7__K ...#     IPB INOP WHEN GETTING HOME UNIT LOCK-OUT  
   4 8228 ...#   
   7 822R ...SNA   NGNC  7  1
   4 823B ...#   
  20 823_ ...#     BREAKIN WHEN WAITING FOR IPB WHEN GETTING HOME UNIT LOCK OUT  
   4 824J ...#   
   7 8257 ...SMA   LDN   7  0
  10 825Q ...      MHUNTW   3,EWDAS,GEXD 
   9 826* ...      RELST    HMUNIT(3)
   4 826Y ...#   
  17 827H ...#     BREAKIN  WHEN WAITING FOR HOME UNIT TO COME ONLINE
   4 8286 ...#   
  10 828P ...SM    MHUNTW   3,EWDAS,GEXD 
   9 829# ...      LDX   6  HMUNIT(3)
   7 829X ...      FREECORE 3
  16 82=G ...      STEPBACK                     [TAKE ONE ORF ONR
  15 82?5 ...      BZE   7  NIPB                [J IF BREAKIN
  15 82?N ...      TWAIT    6,NIPB              [WAIT FOR IPB
  15 82#? ...      RUNPROG                      [TRY AGAIN   
   7 82#W ...NIPB  PROGBRKIN 
   4 82*F ...)   
   4 82B=    #   
   4 82TW    #   
   4 83*G    #   
   6 83MX ...#UNS  IPBD  
   4 842# ...#SKI
   4 84#Q    (   
  10 84SB    SNA   MHUNTW   3,EWDAS,GEXD 
   9 85#2          RELST    HMUNIT(3)
   4 85RL    )   
  14 86?=    SN    ACROSS   CONTEND,9           [CANTDO  
   4 86QW    #   
   4 87=G    #   
  18 87Q6    SL    ACROSS   CONTEND,3           [CONTRACT NOT YET STARTED
  15 889Q    #                                  [SET REPLY 6 
   4 88PB    #   
   4 8992    #   
   4 89NL    #   
   4 8=8=    #   
   4 8=MW    #   
   4 8?7G    #   
  10 8?M6    #     AREA UNIT SCA CORRUPT 
   4 8#6Q    #   
   4 8#LB    #   
  10 8*62    R5    ACROSS   CONTEND,10   
   4 8*KL    #   
  10 8B5=    #     FAILURE  DURING EXOPEN
   4 8BJW    #   
   9 8C4G    XJ    ACROSS   CONTEND,4
   4 8CJ6    #   
  11 8D3Q    #     SCA CORRUPT ON HOME UNIT  
   9 8DHB    R2    ACROSS   CONTEND,5
   4 8F32    #   
   4 8FGL    #   
  16 8G2=    #     ERROR WHILST WRITING TO AREA UNIT OR FIP CELL 
   4 8GFW    #   
   9 8G_G    R3    ACROSS   CONTEND,7
   4 8HF6    #   
   4 8HYQ    #   
  18 8JDB    #     FAIL  BEFORE DOING ANY WRITING - EXOF MUST BE RESET   
   4 8JY2    #   
   9 8KCL    R1    ACROSS   CONTEND,8
   4 8KX=    #   
   4 8LBW    #   
   6 8LJ4 ...#UNS IPBD   
  10 8LP= ...R4    ACROSS   CONTEND,11   
   6 8LWD ...#UNS  IPBD  
   4 8M3L ...#SKI
   4 8M8S ...(   
   7 8MB6    R4A   NGNC  7  1
   7 8MTQ    R4    LDN   7  0
  17 8N*B    R4B   ACROSS   CONTEND,11          [FAIL ON AREA UNIT   
   4 8N*L ...)   
   4 8N*W ...#   
   4 8NB6 ...#   
   6 8NBB ...#UNS  IPBD  
   4 8NBL ...(   
   4 8NBW ...#   
  19 8NC6 ...#     BREAKIN WHEN GETTING LOCK-OUT FOR AREA AND OR HOME UNITS  
   4 8NCB ...#   
  10 8NCL ...SAX   MHUNTW   3,EWDAS,GEXD 
   9 8NCW ...      LDX   5  HMUNIT(3)
   9 8ND6 ...      TXU   5  ARUNIT(3)
  16 8NDB ...      BCC      SAAC                [J IF AREA=HOME  
  17 8NDL ...      RELST    ARUNIT(3)           [RELEASE AREA UNIT   
  16 8NDW ...SAAC  BRN      SA                  [REPORT BREAKIN  
   4 8NF6 ...#   
   9 8NFB ...#     IPB DOWN ON LOCKM 
   4 8NFL ...#   
  21 8NFW ...SBX   LDX   4  ACOMMUNE9(2)        [SUSN WE WERE TRYING FOR WHEN IPB FAI
  10 8NG6 ...      MHUNTW   3,EWDAS,GEXD 
   9 8NGB ...      LDX   5  HMUNIT(3)
   9 8NGL ...      TXU   5  ARUNIT(3)
  16 8NGW ...      BCC      SAAB                [J IF HOME=AREA  
  15 8NH6 ...      RELST    ARUNIT(3)           [RELEASE AREA
  16 8NHB ...SAAB  RELST    5                   [AND HOME UNIT   
  15 8NHL ...      TWAIT    4,SAF               [WAIT FOR IPB
   4 8NHW ...#   
  18 8NJ6 ...#     AT THIS POINT WE COULD DO A STEPBACK-RUNPROG BUT WE   
  19 8NJB ...#     MAY HAVE ALREADY REMOVED SOME AREAS,IN WHICH CASE THE PROG
  21 8NJL ...#     MAY CONTRACT MORE THAN IT WISHED.INSTEAD THE DISCS ARE GOT AGAIN  
  20 8NJW ...#     AND THE CONTRACT LOOP RE-ENTERED. OF COURSE THERE IS A CHANCE 
  21 8NK6 ...#     THAT THE FILE COULD HAVE BEEN CONTRACTED IN THE OTHER PROCESSOR   
  20 8NKB ...#     IN WHICH CASE A ERROR WOULD BE REPORTED AS THE COUNT OF AREAS 
  17 8NKL ...#     COMPARED TO THE NUMBER WE HAVE WOULD NOT TALLY.   
   4 8NKW ...#   
  19 8NL6 ...      SUSEST   5,XXE               [GET HOME UNIT ONLINE AGAIN  
  17 8NLB ...SAAD  MHUNTW   3,EWDAS,GEXD        [SET UP CONDITIONS   
  16 8NLL ...      MFINDEXOF 1,CEFN(3)          [..TO RE-ENTER   
  16 8NLW ...      BRN      ZB                  [..CONTRACT LOOP 
  10 8NM6 ...#     NOT ONLINE-DEMAND IT  
  10 8NMB ...XXE   MHUNTW   3,EWDAS,GEXD 
   8 8NML ...      LDCT  0  #10  
   9 8NMW ...      ANDX  0  EMARK(3) 
  18 8NN6 ...      BNZ   0  SCE                 [J IF ADDITIVE MODE #4000
  17 8NNB ...      SMUSEST  5,SCD,SAF           [GET HOME UNIT ONLINE
  17 8NNL ...      BRN      SAAD                [JOIN CONTRACT LOOP  
   7 8NNW ...#     BREAKIN   
   7 8NP6 ...SAF   LDN   7  0
   9 8NPB ...      LDX   0  AWORK1(2)
  19 8NPL ...      BZE   0  SM                  [J IF NO AREAS REMOVED YET   
   7 8NPW ...      LDN   7  1
  10 8NQ6 ...SCAF  ACROSS   CONTEND,12   
   7 8NQB ...SCE   LDN   7  0
   8 8NQL ...      BRN      SCAF 
   6 8NQW ...#     CANTDO
   9 8NR6 ...SCD   LDX   0  AWORK1(2)
  17 8NRB ...      BNZ   0  SCE                 [J IF AREAS REMOVED  
   8 8NRL ...      LDCT  7  #400 
   8 8NRW ...      BRN      SCAF 
   4 8NS6 ...)   
   4 8NT2    #   
   4 8P#L    #   
   4 8PS=    #   
  11 8Q?W          MENDAREA 30,K99CONTRACT   
   4 8QRG    #END
   8 ____ ...77342544000100000000
