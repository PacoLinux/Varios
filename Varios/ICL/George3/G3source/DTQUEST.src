  13 22FL ...  SEG DTQUEST,860,C.R.JOHNSON,SCHEDULER 
   4 22_=    [   
  21 23DW    [     THIS SEGMENT IS ENTERED BY THE INCDUMP COMMAND WITH PARAM QUEST   
  16 23YG    [     TO CHECK AVAILABILITY OF WRONGED INCREMENTS   
   4 24D6    [   
  12 25CB          SEGENTRY K1DTQUEST,X1DTQUEST  
   4 25M8 ...[   
  18 25X2    QA             44HQN 1,CAN YOU TAPERIGHT ANY OF THESE TAPES?
   9 26BL    WAITCOM        8HWAIT 2 
   4 26W=    [   
  21 27*W ...[   ******************************************************************  
   4 27TG ...[   
  18 28*6 ...[     THIS SUBROUTINE DOES A NON-COORDINATING SEARCH OF THE 
  18 28SQ ...[     OLC, CLEARING BIT 'BINCUNVERR' IN ALL RELEVENT HOST   
   9 29#B ...[     REQUEST BLOCKS.   
  11 29S2 ...[               X4     -  LINK  
  14 2=?L ...[               X5     -  INCREMENT NUMBER  
  15 2=R= ...[               X6,X7  -  MUST NOT BE CORRUPTED 
   4 2?=W ...[   
  12 2?QG ...XSTATER                            [
  17 2#=6 ...      LDX   2  BOLC                [X2 => BASE OF OLC   
  17 2#PQ ...      LDN   3  0                   [X3 => HOST FILESTORE
  12 2*9B ...X1STATER                           [
  16 2*P2 ...      LDX   2  FPTR(2)             [X2 -> NEXT BLOCK
  17 2B8L ...      BXE   2  CXOL,X2STATER          [J IF END OF CHAIN
  19 2BN= ...      BXL   5  FDTINCNUM(2),X2STATER  [J IF INCREMENT NOT THERE 
  20 2C7W ...      BXU   5  FDTINCNUM(2),X1STATER  [J IF INCREMENT NOT REACHED   
  20 2CMG ...      BXL   3  FDTFILSTR(2),X2STATER  [J IF HOST FILESTORE NOT THERE
  21 2D76 ...      BXU   3  FDTFILSTR(2),X1STATER  [J IF FILESTORE NOT YET REACHED   
  20 2DLQ ...      BC       2,BINCUNVERR        [CLEAR 'UNVERIFIED INCREMENT' BIT
  15 2F6B ...      BRN      X1STATER            [NEXT BLOCK  
  14 2FL2 ...X2STATER                           [ALL DONE
  12 2G5L ...      EXIT  4  0                   [
   4 2NDQ    [   
  21 2NKY ...[   ******************************************************************  
   4 2NR6 ...[   
   6 2NYB    X1DTQUEST   
  11 2PD2          SETNCORE 20,3,FLIB,FLINC  
   7 2PXL          LDN   1  1
   8 2QC=          STO   1  A1(3)
   9 2QWW          STOZ     A1+1(3)  
   5 2RBG    OPENII  
  14 2RW6          OPENINC  ,GENERAL            [INCINDEX
  19 2S*Q          LDN   7  0                   [COUNT OF UNAVAILABLE INCS   
  18 2STB          LDN   6  0                   [MAX.NO.OF WRONGED TAPES 
   6 2T*2    STEP  STEP  
   9 2TSL          BZE   3  READEND  
  21 2W2W ...      JMBS     STEP,3,BIAVESTR,BIUNFINR   [J IF NOT A REC FOR DTQUEST   
  15 2WRW          LDX   4  AMTS-A1(3)          [COUNT OF MTS
   7 2X?G          LDX   2  3
  14 2XR6          LDX   5  AINCNO-A1(3)        [INC.NO. 
   6 2Y=Q    TESTWRONG   
  19 2YQB ...      JMBAC    NOTWRONG,2,BMUSWRR,BMOPWRR [J IF TAPE NOT WRONGED
   9 2_=2          ADN   2  INCMAGLEN
  16 2_PL          BCT   4  TESTWRONG           [LOOK AT NEXT MT 
  10 329=          MHUNTW   2,FLIB,FLINC 
   8 32NW          LDX   4  A1(2)
   7 338G          SBN   4  1
  10 33N6          BZE   4  NOVERIFYET   
   6 347Q    NEXTVERIF   
   7 34MB          ADN   2  1
  20 3572          BXE   5  A1+1(2),NOTWRONG    [J IF INC VERIFIED AS UNAVAILABLE
   9 35LL          BCT   4  NEXTVERIF
   6 366=    NOVERIFYET  
  19 36KW          READAGAIN                    [GET INCINDEX RECORD INTO FRB
  19 375G          ADN   7  1                   [UPDATE COUNT OF INCREMENTS  
  10 37K6          MHUNTW   3,FILE,FRB   
  18 384Q          ADX   6  AMTS(3)             [UPDATE COUNT OF TAPES   
   8 38JB          BRN      STEP 
   4 3942    [   
  21 39HL    [     INCREMENT NOW VERIFIED(MAY OR MAY NOT BE AVAILABLE)WAKE UP WAITERS
   4 3=3=    [   
   5 3=GW    NOTWRONG
  18 3?2G ...      BS       3,BIAVESTR          [SET 'INC VERIFIED' BIT  
   8 3?G6          LDX   2  FX2  
   8 3?_Q          STEPREWRITE   
  21 3#FB ...      CALL  4  XSTATER             [CLEAR 'BINCUNVERR' BIT IN RELEVENT F
   8 3CC6          BRN      STEP 
   5 3CWQ    READEND 
   7 3DBB          CLOSETOP  
  10 3DW2          MHUNTW   3,FLIB,FLINC 
   7 3F*L          LDN   1  1
  21 3FT=          STO   1  A1(3)               [CLEAR LIST OF NEWLY VERIFIED INCS   
  19 3G#W          BNZ   7  STATEMENT           [J IF SOME STILL UNVERIFIED  
   7 3GSG          FREECORE 3
  16 3H#6          STARTACT RET                 [KICK SCHEDULER  
   6 3HRQ          ENDCOM
   4 3J?B    [   
  19 3JR2    [     MAKE STATEMENT ABOUT WRONGED TAPES AND UNAVAILABLE INCS.  
   4 3K=L    [   
  16 3KQ=    [     MT%A %B %C WRONGED,MAKING INC%D %E UNAVAILABLE
   4 3L9W    [   
   6 3LPG    STATEMENT   
   7 3M96          ADN   7  2
  10 3MNQ          MHUNTW   3,FLIB,FLINC 
  10 3N8B          LDX   4  ALOGLEN(3)   
  10 3NN2          BXGE  4  7,RIGHTSIZE  
  11 3P7L          ALTLEN   3,7,FLIB,FLINC   
  10 3PM=          MHUNTW   3,FLIB,FLINC 
   6 3Q6W    RIGHTSIZE   
  18 3QLG          SBN   7  1                   [ DPDATE RECORD HEADER   
   8 3R66          STO   7  A1(3)
   9 3RKQ          STOZ     A1+1(3)  
  18 3S5B          SBN   7  1                   [ X7= NO OF INCREMENTS   
   4 3SK2    [   
  20 3T4L    [     AND APROC TO CONTAIN UP TO X6 TAPES-INCMAGLEN WORDS PER TAPE  
   4 3TJ=    [   
   7 3W3W          LDX   5  6
   9 3WHG          LDN   0  INCMAGLEN
   7 3X36          MPY   5  0
   9 3XGQ          ADN   6  AMTS-A1+1
  11 3Y2B          SETUPCORE 6,3,FILE,APROC  
   8 3YG2          STO   6  A1(3)
   9 3Y_L          STOZ     AMTS(3)  
   4 3_F=    [   
  17 3_YW    [     INSERT INC.NOS INTO FLINC AND TAPE NOS INTO APROC 
   4 42DG    [   
   5 42Y6    NEXTFRB 
  10 43CQ          MHUNTW   3,FILE,FRB   
  18 43XB          LDX   4  AINCNO(3)           [NEXT INC NOT VERIFIED   
  10 44C2          MHUNTW   2,FLIB,FLINC 
   7 44WL          SMO      7
  20 45B=          STO   4  A1+1(2)             [INCS WILL BE IN DESCENDING ORDER
  17 45TW          LDX   6  AMTS(3)             [COUNT OF MTS IN FRB 
  10 46*G          MHUNTW   2,FILE,APROC 
   7 46T6          LDX   0  2
   5 47#Q    TSNGET  
   9 47SB          LDX   5  AMTS(2)  
   9 48#2          LDX   4  AMAG(3)  
   9 48RL          BZE   5  ZEROCT   
   5 49?=    TRYNEXT 
  10 49QW          BXE   4  AMAG(2),NOWIN
   9 4==G          ADN   2  INCMAGLEN
   9 4=Q6          BCT   5  TRYNEXT  
   5 4?9Q    ZEROCT  
  16 4?PB          STO   4  AMAG(2)             [NEW TSN IN LIST 
   7 4#92          LDN   1  1
   7 4#NL          LDX   2  0
  20 4*8=          ADS   1  AMTS(2)             [INCREASE COUNT OF TSNS IN APROC 
   9 4*MW    NOWIN ADN   3  INCMAGLEN
  19 4B7G          LDX   2  0                   [RESET APROC POINTER AGAIN   
   9 4BM6          BCT   6  TSNGET   
   9 4C6Q          MFREEW   FILE,FRB 
   9 4CLB          BCT   7  NEXTFRB  
  16 4D62          DOWN     DTSUBS,4            [OUTPUT STATEMENT
  10 4DKL          MFREEW   FILE,APROC   
   4 4F5=    [   
  18 4FJW    [     NOW ASK QUESTION-CAN YOU TAPERIGHT ANY OF THESE TAPES?
   4 4G4G    [   
  11 4GJ6    QUEST SETNCORE 20,2,ADATA,CREADL
   8 4H3Q          LDN   6  42   
  15 4HHB          STO   6  A1(2)               [CHAR COUNT  
   8 4J32          ADN   1  QA   
   9 4JGL          ADN   2  CPDATA   
   8 4K2=          MOVE  1  11   
   9 4KFW          ISSUCOM  QUESTERR 
  11 4K_G          MHUNTW   2,APED,ADISPLAY  
  18 4LF6          LDCH  5  APEDDISM+1(2)       [X5=FIRST CHAR OF REPLY  
   8 4LYQ          SBN   5  #71  
   9 4MDB          BZE   5  REPYES   
   9 4MY2          ADN   5  #71-#56  
   8 4NCL          BZE   5  REPNO
   4 4NX=    [   
   8 4PBW    [     INVALID ANSWER
   4 4PWG    [   
  16 4QB6          OUTPARAM APEDDISM(2),APEDDISM+1,APED,ADISPLAY 
   9 4QTQ          MONOUT   ANOTVAL  
  10 4R*B          MFREEW   APED,ADISPLAY
   8 4RT2          BRN      QUEST
   4 4S#L    [   
  21 4SS=    [     SOME OF THE TAPES CAN BE MADE AVAILABLE-WAIT 2 MINS THEN TRY AGAIN
   4 4T?W    [   
   5 4TRG    REPYES  
  17 4W?6          MONOUT   HDOTR               [DO TAPERIGHTS,PLEASE
  11 4WQQ          SETNCORE 10,2,ADATA,CREADL
   7 4X=B          LDN   6  6
   8 4XQ2          STO   6  A1(2)
   9 4Y9L          ADN   1  WAITCOM  
   9 4YP=          ADN   2  CPDATA   
   7 4_8W          MOVE  1  2
  20 4_NG          ISSUCOM  QUESTERR            [WAIT 2MINS FOR OPS TO DO THIS   
  10 5286          MHUNTW   3,FLIB,FLINC 
   7 52MQ          LDN   1  1
   8 537B          STO   1  A1(3)
   9 53M2          STOZ     A1+1(3)  
   4 546L    [   
  17 54L=    [     NONE OF INCS IN FLINC BLOCK CAN BE MADE AVAILABLE 
   4 555W    [   
  10 55KG    REPNO MFREEW   APED,ADISPLAY
   9 5656          BRN      OPENII   
   5 56JQ    QUESTERR
  10 574B          GEOERR   1,DTCHCOMM   
  10 57J2          MENDAREA 40,K99DTQUEST
   4 583L    #END
   8 ____ ...25364750000100000000
