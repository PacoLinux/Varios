  10 22FL ...      SEG      CHINTEG,867  
   4 22_= ...#   
  15 23DW ...#   COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982
   4 23YG ...#   
   4 24D6    #   
  12 24XQ          SEGENTRY K1CHINTEG,QK1CHINTEG 
   4 25CB    #   
   4 28SQ    #   
  20 29#B ...#              THIS SECTION CHECKS THAT A FILE WHICH HAS BEEN FOUND 
  19 29S2 ...#              CAN BE OPENED,CHECKING INTEGRITY CODE,RETENTION  
  20 2=?L    #              PERIOD,ETC.JUMPS TO Z--- FOR ERROR ---,ELSE GOES TO  
   9 2=R=    #              CONNECT. 
   4 2?=W    #   
   4 2?QG    #   
  13 2#=6    QOLAY          #100                [   )
  13 2#PQ    QREAD          #200                [   )
  17 2*9B    QWRITE         #300                [   )OPENING MODES   
  13 2*P2    QEOF           #400                [   )
   9 2BH2 ...XEWINS         +EWINS   
   4 2BPY ...#   
   6 2BYW ...#DEF  XA1=A1
   4 2C7W    #   
   4 2CMG    #   
   6 2D76    QK1CHINTEG  
   4 2DLQ    #   
   9 2G5L    #SKI  K6CHINTEG>199-199 
   4 2GK=    (   
   8 2H4W          TESTREP  OK,OK
   9 2HJG          GEOERR   1,EXOREP 
   4 2J46    OK  
   4 2JHQ    )   
  16 2K3B          MHUNTW   2,EWDAS,EXOF        [FIND DATA BLOCKS
  10 2KH2          MHUNTW   3,EWDAS,GEXD 
  20 2L2L          STOZ     COPEN(2)            [CLEAR COPEN AND BACK2 IN CASE   
  15 2LG=          STOZ     BACK2(2)            [OF REENTRY, 
   8 2L_W          LDN   0  #7777
  17 2MFG          ANDS  0  EXMARK(2)           [ALSO B0-11 OF EXMARK
  10 2MP# ...      LDX   7  EOPMODE(3)   
  17 2M_6 ...      FPCACA   1,FX2               [GET POINTER TO PCA  
  12 2Y=Q          LDN   6  0                   [
  17 2YQB          TRUSTED  1,NOTS,S            [J IF NOT S-TRUSTED, 
  16 2_=2          LDCT  6  #400                [ELSE SET X6 NEG 
   8 2_PL ...      BRN      NOTQ 
  17 366=    NOTS  TRUSTED  1,NOTQ,Q            [J IF NOT Q-TRUSTED, 
  17 36KW          ORN   6  1                   [ELSE SET X6 NON-ZERO
   8 375G    NOTQ  LDX   1  FX1  
   8 37K6          LDCT  0  #100 
   9 384Q          ANDX  0  FIP+5(2) 
  17 38JB          BZE   0  NSYS                [J UNLESS SYSTEM FILE
  18 38KY ...      BPZ   6  NUND                [DONT CHECK FOR DEDICATED
  16 38MG ...      MFINDSU  1,ASUSN(2)          [IF NOT S-TRUSTED
  16 38P4 ...      LADDP    HUNIT(1),1          [POINTER TO APIA 
  17 38QL ...      JDED     1,NUND              [J IF S-TRUSTED - OK 
  21 38S8 ...      LDN   6  8                   [ERROR - S-TRUSTED ON NON-DED UNIT   
   7 38TQ ...      LDN   7  2
   9 38X# ...      BRN      ZERROR   
   4 38YW ...NUND
   8 392D ...      LDX   1  FX1  
   8 3942          LDCT  0  #20  
  18 39HL          ORS   0  EXMARK(2)           [SET SYFIL BIT IN EXMARK 
   9 3=3=          TXU   7  QEOF(1)  
  17 3=GW          BCC      ZSYS                [ERROR IF MODE *400, 
  20 3?2G          BZE   6  ZSYS                [OR PROGRAM NOT TRUSTED AT ALL   
   9 3?G6          TXU   7  QREAD(1) 
  19 3?_Q          BCC      XINT                [J IF MODE *200,CHECK PASSED 
  19 3#FB          BNG   6  STR                 [ELSE J IF PROGRAM S-TRUSTED 
  20 3#_2    ZSYS  LDN   6  3                   [ERROR,NOT SUFFICIENTLY TRUSTED, 
  17 3*DL          LDN   7  2                   [SET MESS AND REPLY  
  17 3*Y=          BRN      ZERROR              [AND DEAL WITH ERROR 
  19 3BCW    STR   LDEX  4  FIP+5(2)            [CHECK UNITS ARE NOT IN USE  
  18 3BXG          ADN   4  1                   [COUNT OF FILE AREAS+1   
  16 3CC6          LDX   0  ASUSN(2)            [SERIAL NR OF FIP
  21 3CWQ    NSER  LDN   1  BCCB                [SEARCH FOR REFERENCE TO SUSN [X0];  
  21 3DBB    NEXF  LDX   1  BPTR(1)             [!NOTE! COPEN IS USED AS A TEMPORARY 
  18 3DW2          TXU   1  CXCC                [ COUNT DURING THIS CHECK
  18 3F*L          BCC      YEND                [J IF END OF CCB CHAIN   
   9 3FT=          LDX   7  BACK2(1) 
  18 3G#W          BNZ   7  NEND                [J IF THIS IS AN EXOF,   
  19 3GSG    YEND  SMO   COPEN(2)               [ELSE ,THIS SUSN NOT FOUND   
  18 3H#6          LDX   0  FDCELLS(2)          [LOOK FOR NEXT;SUSN IN X0
  21 3HRQ          LDN   7  6                   [UPDATE POINTER TO CURRENT FD CELL   
   9 3J?B          ADS   7  COPEN(2) 
  21 3JR2          BCT   4  NSER                [J IF MORE CELLS TO CHECK NEXT SUSN, 
  17 3K=L          STOZ     COPEN(2)            [ELSE CHECK PASSED   
   7 3KQ=          LDCT  0  4
  17 3L9W          ORS   0  EXMARK(2)           [SET S-TRUSTED BIT   
  15 3LPG          LDX   7  EOPMODE(3)          [RESTORE MODE
  14 3M96          LDX   1  FX1                 [& FX1   
  15 3MNQ          BRN      NSYS                [AND CONTINUE
   9 3N8B    NEND  TXU   0  ASUSN(1) 
  17 3NN2          BCC      ZS                  [J IF FIP SUSN SAME  
  17 3P7L          LDEX  7  FIP+5(1)            [GET COUNT FIO AREAS 
  18 3PM=          LDN   5  0                   [POINTER TO FIRST AREA   
  21 3Q6W    NEXS  BZE   7  NEXF                [J IF NO MORE AREAS,LOOK AT NEXT EXOF
   7 3QLG          SBN   7  1
  20 3R66          ADN   5  6                   [REDUCE COUNT AND UPDATE POINTER 
   7 3RKQ          SMO      5
  10 3S5B          TXU   0  FDCELLS-6(1) 
  20 3SK2          BCS      NEXS                [J IF THIS AREA OF DIFFERENT SU  
  21 3T4L    ZS    LDN   6  4                   [ELSE ERROR:-S-TRUSTED CANNOT BE OPEN
  19 3TJ=          LDN   7  7                   [ED BECAUSE UNIT(S) IN USE   
  15 3W3W          BRN      ZERROR              [J FOR ERROR 
   9 3WHG    NSYS  TXU   7  QOLAY(1) 
  20 3X36 ...      BCC      OVER                [J IF MODE *100 TO CHECK TRUSTED 
   9 3XGQ          TXU   7  QWRITE(1)
  21 3Y2B          BCS      XINT                [J IF MODE NOT *300 TO SKIP RP CHECK 
  21 3YG2 ...OVER  BZE   6  RPCH                [J TO CHECK RP IF PROGRAM NOT TRUSTED
  13 3Y_L          LDX   6  ENEWRP(3)           [OR  
  18 3_F=          BPZ   6  XINT                [IF NEW RP IS NEGITIVE   
  14 3_YW    RPCH  LDX   0  FIP+7(2)            [CHECK RP
   8 42DG          SRL   0  12   
  15 42Y6          ADX   0  FIP+6(2)            [PURGE DATE  
   8 43CQ          LDX   6  EDATE
   7 43XB          TXL   6  0
  18 44C2          BCC      XINT                [J IF PURGE DATE EXCEEDED
  21 44WL ...      LDN   6  6                   [ELSE ERROR,FILE NOT TO BE WRITTEN   
   7 45B=          LDN   7  4
   9 45TW          BRN      ZERROR   
  19 46*G    XINT  LDX   5  ASUSN(2)            [CHECK IF FILE ALREADY OPEN: 
  21 46T6          LDX   6  HOME(2)             [LOOK FOR EXOF WITH SAME ASUSN,HOME  
  19 47#Q          LDN   4  0                   [MARK IF ANY S-TRUSTEDS OPEN 
   8 47SB          LDN   1  BCCB 
   4 48#2    XINA
   9 48RL          LDX   1  BPTR(1)  
   8 49?=          TXU   1  CXCC 
  18 49QW          BCC      NOPE                [J IF END OF CCB CHAIN,OR
  19 4==G          LDX   0  BACK2(1)            [BLOCK IS NOT AN EXOF;FILE IS
  16 4=Q6          BZE   0  NOPE                [NOT ALREADY OPEN
  17 4?9Q          ORX   4  EXMARK(1)           [ELSE REMEMBER S-BIT 
   9 4?PB          TXU   5  ASUSN(1) 
   9 4#92          TXU   6  HOME(1)  
  19 4#NL          BCS      XINA                [J IF DIFFERENT,LOOK AT NEXT 
   9 4*8=    #SKI  K6CHINTEG>199-199 
   4 4*MW    (   
  20 4B7G          LDX   4  FIP(2)              [(CHECK THAT IDENTITY MATCHES)   
   9 4BM6          LDX   5  FIP+1(2) 
   9 4C6Q          LDX   6  FIP+2(2) 
   9 4CLB          TXU   4  FIP(1)   
   9 4D62          TXU   5  FIP+1(1) 
   9 4DKL          TXU   6  FIP+2(1) 
   8 4F5=          BCS      XINC 
   9 4FJW          LDX   4  FIP+4(2) 
   9 4G4G          ERX   4  FIP+4(1) 
   8 4GJ6          SRL   4  12   
   8 4H3Q          BNZ   4  XINC 
  20 4HHB          LDCT  4  4                   [(AND THAT WE ARE NOT S-TRUSTED) 
   9 4J32          ANDX  4  EXMARK(2)
   8 4JGL          BZE   4  XIND 
   9 4K2=          GEOERR   1,S-OPEN 
  10 4KFW    XINC  GEOERR   1,NAMEHOME   
   4 4K_G    XIND
   4 4LF6    )   
   8 4LYQ          LDCT  0  #100 
   9 4MDB          ANDX  0  EXMARK(1)
  18 4MY2          BZE   0  NGRG                [J UNLESS OPEN TO GEORGE 
   7 4NCL          LDN   6  5
  15 4NX=          LDN   7  1                   [ELSE ERROR  
   9 4PBW          BRN      ZERROR   
  18 4PWG ...NGRG  LDCH  0  FIP+5(2)            [CHECK INTEGRITY CODE:-  
   7 5286          SRL   0  4
   7 52MQ          SBN   0  1
  19 537B          BZE   0  ZINTEG              [FAIL IF IC=1 (1 USER ONLY)  
   7 53M2          LDX   6  7
   8 546L          SBN   6  #200 
   9 54L=          LDX   4  EXMARK(1)
  20 555W          BPZ   4  READ                [J IF CURRENTLY OPEN FOR READING 
   7 55KG          SBN   0  2
  21 5656          BZE   0  ZINTEG              [FAIL IF IC=3  (1 WRITER OR N READERS
  18 56JQ          BNZ   6  ZINTEG              [OR IF WE ARE A WRITER   
  17 574B          BRN      PASS                [ELSE CHECK PASSED   
  17 57J2    READ  SBN   0  1                   [OPEN FOR READING,   
   8 59GG          BZE   0  PASSA
  20 5=26          BNZ   6  ZINTEG              [FAIL IF IC=3 AND WE ARE A WRITER
  16 5=FQ    PASS  LDX   4  BACK2(1)            [IC CHECK PASSED 
  17 5=_B          STO   4  BACK2(2)            [SAVE EXOFILE NUMBER 
  12 5?F2          STO   4  CEFN(3)             [
  16 5?YL          BRN      UPD                 [GO TO FIP UPDATE
   4 5#D=    #   
  19 5*X6    PASSA BZE   6  PASS                [NOT NEED TO CHECK IF READING
   9 5BBQ          LDX   4  BACK2(1) 
  17 5BWB          CHFILE   HOME(2),ASUSN(2),7,TBREAK,ZIF,XIPB,XNOC  
  10 5CB2          MHUNTW   2,EWDAS,EXOF 
  10 5CTL          MHUNTW   3,EWDAS,GEXD 
   4 5D*=    XNOC
   8 5DSW          MFINDEXOF  1,4
   7 5F#G          BRN   PASS
  18 5J==    NOPE  SLL   4  6                   [FILE IS NOT ALREADY OPEN
  18 5JPW          BPZ   4  NOST                [J IF NO S-TRUSTEDS OPEN 
  21 5K9G          LDN   4  0                   [ELSE CHECK THAT NONE OF OUR AREAS   
  20 5KP6          LDEX  5  FIP+5(2)            [ARE ON UNIT IN USE BY S-TRUSTED 
   9 5L8Q          LDX   0  ASUSN(2) 
   8 5LNB          BRN      NIC1 
  16 5M82    SAME  BZE   5  NARE                [J IF NO AREAS   
  19 5MML          SBN   5  1                   [UPDATE COUNT OF AREAS AND   
  16 5N7=          ADN   4  6                   [FD CELL POINTER 
   7 5NLW          SMO      4
  17 5P6G          LDX   0  FDCELLS-6(2)        [CHECK THIS FDCELL   
   9 5PL6          TXU   0  ASUSN(2) 
  21 5Q5Q          BCC      SAME                [J IF ON HOME UNIT,NO NEED TO CHECK  
  17 5QKB    NIC1  LDN   1  BCCB                [FIND S-TRUSTED EXOFS
   9 5R52    NIC   LDX   1  BPTR(1)  
   8 5RJL          TXU   1  CXCC 
  17 5S4=          BCC      SAME                [J END OF CCB CHAIN  
   9 5SHW          LDX   6  BACK2(1) 
  19 5T3G          BZE   6  SAME                [OR IF THIS IS NOT AN EXOF,  
   7 5TH6          LDCT  6  4
   9 5W2Q          ANDX  6  EXMARK(1)
  17 5WGB          BZE   6  NIC                 [OR IF NOT S-TRUSTED 
   9 5X22          TXU   0  ASUSN(1) 
  21 5XFL          BCC      ZSS                 [ERROR IF SAME HOME UNIT AS OUR AREA 
  19 5X_=          LDEX  6  FIP+5(1)            [CHECK FD CELLS IN EXOF FOUND
   7 5YDW          LDN   7  0
  15 5YYG    NFA   BZE   6  NIC                 [J NO AREAS  
   7 5_D6          SBN   6  1
  15 5_XQ          ADN   7  6                   [UPDATE...   
   7 62CB          SMO      7
  10 62X2          TXU   0  FDCELLS-6(1) 
  21 63BL          BCS      NFA                 [J THIS AREA NOT SAME UNIT AS OURS   
  20 63W=    ZSS   LDN   6  4                   [ELSE ERROR,FILE CANNOT BE OPENED
  21 64*W          LDN   7  1                   [BECAUSE A UNIT IT REQUIRES IS IN USE
  16 64TG          BRN      ZERROR              [BY AN S-TRUSTED 
  15 65*6    NARE  LDX   7  EOPMODE(3)          [RESTORE MODE
  17 67R=    NOST  CHFILE   HOME(2),ASUSN(2),7,TBREAK,ZIF,XIPB,XNOC1 
  10 68=W          MHUNTW   3,EWDAS,GEXD 
  10 68QG          MHUNTW   2,EWDAS,EXOF 
   7 69=6    XNOC1 LDX   6  2
  15 6=9B          LDX   5  ALOGLEN(2)          [SAVE LOGLEN 
  21 6=P2          EXOFILE  6                   [CHAIN IN EXOF AND ALLOCATE IT AN EFN
   7 6?8L          SMO      6
   8 6?N=          LDX   4  BACK2
  14 6#7W          STO   4  CEFN(3)             [SAVE EFN
   7 6#MG          PHOTO    7
  21 6*76          SETUPCORE 5,2,EWDAS,EXOF     [CREATE NEW EXOF IN ACTIVITY CHAIN   
  21 6*LQ          LDX   1  6                   [X1 POINTS TO EXOF IN CCB CHAIN...   
   8 6B6B          TESTMOVE 7,MEX
  10 6BL2          MHUNTW   3,EWDAS,GEXD 
  14 6C5L          MFINDEXOF 1,4                [EVEN IF 
  17 6CK=    MEX   STO   4  BACK2(2)            [PUT EFN IN NEW EXOF 
  13 6D4W          LDN   6  XA1(1)              [COPY
  14 6DJG          LDN   7  XA1(2)              [OLD EXOF
  17 6F46 ...      MOVLONG  6,7,5               [TO NEW,FOR UPDATING 
  15 6G3B          LDX   7  EOPMODE(3)          [RESTORE MODE
   4 6GH2    [   
   4 6GHX ...UPD 
   8 6GLL ...      LDX   1  FX1  
   9 6GMH ...      TXU   7  QREAD(1) 
  16 6GND ...      BCC      SETRD               [J IF MODE #200  
   9 6GP* ...      TXU   7  QEOF(1)  
  16 6GQ= ...      BCC      SETAP               [J IF MODE #400  
  18 6GR7 ...      LDN   5  CWRITE              [MODE IS #100 OR #300 SO 
   9 6GS4 ...      TXU   7  QWRITE(1)
  21 6GS_ ...      BCS      XOFLE               [J IF MODE #100 NEEDING WRITE TRAPS  
   9 6GT= ...      LDXC  1  ENEWG(3) 
  20 6GTH ...      BCS      XOFLE               [J IF GEN NO NOT TO BE UPDATED   
   9 6GTW ...      LDX   0  FIP+4(2) 
  15 6GWR ...      SRL   0  12                  [OLD GEN NO  
  15 6GXN ...      TXU   0  1                   [NEW GEN NO  
  16 6GYK ...      BCC      XOFLE               [J IF SAME ELSE  
  20 6G_G ...      ORN   5  CERASET             [ERASE TRAPS ALSO NEEDED FOR #300
   8 6H2C ...      BRN      XOFLE
   5 6H3# ...SETRD   
  19 6H49 ...      LDN   5  CREAD               [NEED TO TEST FOR READ TRAPS 
   8 6H56 ...      BRN      XOFLE
   5 6H63 ...SETAP   
  20 6H6Y ...      LDN   5  CAPPEND             [NEED TO TEST FOR APPEND TRAPS   
   5 6H7T ...XOFLE   
  20 6H8Q ...      EXCHOFE  EXOF,,5,XOCNT       [EXOFILE LIBRARIAN TO CHECK TRAPS
  17 6H94 ...      TESTREP2 OK,XCHOK            [J IF TRAPS ARE OK   
  21 6H=J ...      TESTREPNOT NOFILE,XOERR      [NO FILE - NO CHECKS,ELSE ERROR CASE 
   8 6H=Y ...      LDX   0  EDSQ 
  19 6H?4 ...      BNG   0  XOERR               [J IF FULL SECURITY IN USE   
   8 6H?5 ...      BRN      XOCNT
   5 6H?6 ...XCHOK   
   8 6H?7 ...      SMO      FX1  
   8 6H?8 ...      TXU   7  QREAD
  18 6H?9 ...      BCS      XCHOFW              [J IF OPEN MODE NOT READ 
  20 6H?= ...      EXCHOFE  EXOF,,,XOCNT,A      [D&T ACCESSED FOR EXOFILE ENTRY  
   8 6H?? ...      BRN      XOCNT
   5 6H?# ...XCHOFW  
  21 6H?* ...      EXCHOFE  EXOF,,,XOCNT,AW     [D&T WRITTEN+ACCESSED FOR NEW EXOF   
   5 6H?F ...XOCNT   
  16 6H#B ...      MFINDEXOF 1,4                [RE-HUNT BLOCKS  
  10 6H*? ...      MHUNTW   2,EWDAS,EXOF 
  10 6HB8 ...      MHUNTW   3,EWDAS,GEXD 
   8 6HDX ...      SBN   7  #200 
  16 6HG=          BZE   7  XEND                [J IF READ MODE  
   8 6H_W          SBN   7  #100 
  16 6JFG          BZE   7  WRT                 [J IF OUTPUT MODE
   9 6J_6          LDX   0  FIP+4(2) 
   8 6KDQ          ANDN  0  #7777
  18 6KYB          ADN   0  1                   [ELSE ADD 1 TO VERSION   
   9 6LD2          DSA   0  FIP+4(2) 
   8 6LXL          ADN   7  #200 
  20 6MC=          BZE   7  PDAT                [J IF OLAY TO INSERT DATE,ELSE   
  19 6MWW          BRN      REW                 [IF EOF MODE,J TO REWRITE FIP
  15 6NBG    WRT   LDX   0  ENEWG(3)            [OUTPUT MODE,
  20 6NW6 ...      BNG   0  NUPG                [J IF GEN NO NOT TO BE UPDATED,  
   8 6P*Q          SLL   0  12   
   9 6PTB          LDX   4  FIP+4(2) 
   7 6Q*2          DSA   4  0
   7 6QSL          TXU   4  0
  19 6R#=          BCC      NUPG                [OR IF OLD GEN SAME AS NEW   
  21 6RRW          STO   0  FIP+4(2)            [PUT NEW GEN IF EXOF (IN ACTCHAIN)   
   9 6RTB ...      LDX   4  CEFN(3)  
  21 6RTW ...      LDX   6  EXOTRAP(3)          [-VE FOR NON-OK REPLY FROM PREVIOUS  
  20 6RWB ...      LDX   7  EXMARK(2)           [EXCHOFE & X7 PRESERVES EXMARK   
  18 6RWW ...      LDX   5  HOME(2)             [PRESERVE THE FILE'S HOME
  13 6RXB ...      LDN   0  BOPEN+BSERL+BSPEC+BLOCK  
  17 6RXW ...      STO   0  EXMARK(2)           [SEE IF NEW EXOFILE  
  16 6RYB ...      DOWN     EXOPEN,1            [ALREADY EXISTS  
  16 6RYW ...      TESTREP2 OK,XOERR            [J IF SO - ERROR 
  10 6R_B ...      MHUNTW   2,EWDAS,EXOF 
  10 6R_W ...      MHUNTW   3,EWDAS,GEXD 
  18 6S2B ...      STO   5  HOME(2)             [REINSTATE FILE'S HOME   
   9 6S2W ...      LDN   0  ANFOUND  
  17 6S3B ...      ANDX  0  EMARK(3)            [IS REPLY 'NOT FOUND'
  19 6S3W ...      BZE   0  ZFAIL               [J IF NOT - TRANSFER ERROR   
  21 6S4B ...      LDN   5  CWRITE              [EXOFILE(REVGEN) MUST HAVE EXOFILE   
  18 6S4W ...      EXCHOFE  EXOF,,5,WRIT1       [ENTRY WITH WRITE TRAPS  
  13 6S54 ...      TESTREP2 NOFILE,XCHNF,NOTRAP,XOERR
  21 6S5= ...      EXCHOFE  EXOF,,,WRIT1,AW     [D&T WRITTEN+ACCESSED FOR NEW EXOF   
   8 6S5D ...      BRN      WRIT1
   5 6S5L ...XCHNF   
  20 6S5W ...      LDX   0  EDSQ                [NOFILE REPLY SO TEST FOR 'FULL' 
  16 6S6B ...      BNG   0  XOERR               [J IF FULL IN USE
  21 6S6W ...      BNG   6  WRIT1               [J IF PREVIOUS EXCHOFE ALSO HAD NOFIL
   8 6S7B ...      SMO      FX1  
   9 6S7W ...      LDX   6  XEWINS   
  20 6S8B ...      DOWN     CHINERR,7           [O/P THE NO LONGER SECURE MESSAGE
   5 6S8W ...WRIT1   
  10 6S9B ...      MHUNTW   2,EWDAS,EXOF 
  16 6S9W ...      STO   7  EXMARK(2)           [REINSTATE EXMARK
  10 6S=B ...      MHUNTW   3,EWDAS,GEXD 
   7 6S?G    NUPG  LDN   0  0
  16 6SR6          DSA   0  FIP+4(2)            [ZEROISE VERSION 
   7 6T=Q          LDN   0  1
  14 6TQB          STO   0  FIP+10(2)           [EOF=1   
   9 6W=2          LDX   0  ENEWRP(3)
   7 6WPL          SLL   0  1
  21 6X9=          BNG   0  PDAT                [DO NOT UPDATE RP IF NEW RP HAS B1=1 
   8 6XNW          SLL   0  11   
   9 6Y8G          LDX   6  FIP+7(2) 
  17 6YN6          STO   0  FIP+7(2)            [INSERT NEW DATA RP  
   9 6_7Q          DSA   6  FIP+7(2) 
  15 6_MB    PDAT  LDX   0  EDATE               [REWRITE DATE
   9 7272          STO   0  FIP+6(2) 
  16 72LL    REW   LDX   4  CEFN(3)             [WRITE FIP BACK  
  17 736=          LDX   5  2                   [SAVE POINTERS,ETC   
   7 73KW          PHOTO    6
  18 745G          OPENST   ASUSN(2)            [SET UP TRANSFER SYSTEM  
  10 74K6          READST   HOME(2),NKOR 
   9 754Q          TESTREP  OK,NKM1  
  10 75JB          TESTREP  EXFAIL,ZFAIL 
   9 7642    NKM3  GEOERR   1,CHINREP
   7 76HL    NKM1  LDX   2  5
   8 773=          TESTMOVE 6,NKM
  10 77GW          MHUNTW   2,EWDAS,EXOF 
   7 782G          LDX   5  2
  19 78G6    NKM   LDX   1  ECOR(3)             [AND GET POINTER TO AREA READ
   9 78_Q    NKOR  LDN   0  FIP(2)   
   7 79FB          ADN   1  2
  17 79_2          MOVE  0  15                  [MOVE FI&P TO BUFFER 
   7 7=DL          PHOTO    6
   7 7=Y=          LDX   2  5
   9 7?CW          CLOSEWR  W,HOME(2)
   9 7?XG          TESTREP  OK,NKM2  
  10 7#C6          TESTREP  EXFAIL,ZFAIL 
   8 7#WQ          BRN      NKM3 
   7 7*BB    NKM2  LDX   2  5
   8 7*W2          TESTMOVE 6,NK2
  17 7B*L          MHUNTW   3,EWDAS,GEXD        [REHUNT IF NECESSARY 
  10 7BT=          MHUNTW   2,EWDAS,EXOF 
  20 7C#W    NK2   MFINDEXOF 1,4                [FIND COPY OF EXOF IN CCB CHAIN  
   9 7CSG          LDN   5  FIP(2)   
   9 7D#6          LDN   6  FIP(1)   
  18 7DRQ          MOVE  5  15                  [COPY UPDATED FIP TO IT  
  16 7G=L          FIPPER   HOME(2),ASUSN(2),XBREAK,XFAIL,NOCS   
  10 7GQ=    XFAIL MHUNTW   3,EWDAS,GEXD 
  10 7H9W          MHUNTW   2,EWDAS,EXOF 
   8 7HPG    NOCS  MFINDEXOF 1,4 
   8 7JNQ          LDCT  0  #400 
  19 7K8B          ORS   0  EXMARK(1)           [SET OPEN-FOR-WRITING MARKER 
   4 7KN2    [   
   4 7L7L    [   
   7 7LM=    XEND  LDN   0  0
   9 7M6W          DSA   0  EXMARK(1)
   7 7MLG          LDN   0  1
   9 7N66          ADS   0  COPEN(1) 
   9 7NKQ          LDN   4  FIP(1)   
   9 7P5B          LDN   5  FNAMER(3)
   7 7PK2          MOVE  4  3
   9 7Q4L          LDX   0  FIP+4(1) 
  10 7QJ=          STO   0  FNAMER+3(3)  
   9 7R3W          LDX   6  ASUSN(1) 
   7 7RHG          FREECORE 2
   4 7S36    [   
   4 7S7J ...REL 
   7 7S#2 ...#UNS  CAFSINT   
   4 7SDD ...(   
  19 7SJW ...#     IF THE FILE IS BEING OPENED FOR WRITING, WE HAVE TO INFORM
  13 7SP# ...#     THE CAFS SUBSYSTEM OF THE EVENT   
   4 7STQ ...#   
  17 7T28 ...      LDX   0  EOPMODE(3)          [GET THE OPENING MODE
  18 7T6L ...      BXE   0  QREAD(1),NOTOFW     [& JIF OPENING TO READ   
  19 7T?4 ...      CAFSINFO OPENFILE,CEFN(3)    [ELSE NOTIFY EVENT TO CAFS   
   4 7TCG ...#   
   5 7THY ...NOTOFW  
   4 7TNB ...)   
   6 7TSS ...      EXMESS
  15 7T_L          ACROSS   ANTIC,1             [CONNECT FILE
   4 7WF=    [   
   4 7WYW    [   
   5 7X2B ...XOERR   
  20 7X2J ...      LDX   7  EXEC1(2)            [LENGTH OF FABSNB USED IN EXCHOFE
   7 7X2L ...      SBN   7  4
  15 7X2W ...      PICKREP  6                   [SAVE REPLY  
  20 7X3B ...      MFINDEXOF 3,4                [RE-HUNT EWDAS-EXOF IN CCB CHAIN 
   9 7X3W ...      LDX   4  COPEN(3) 
  21 7X4B ...      BNZ   4  XREP                [J IF EXOFILE OPEN TO SOMEONE ELSE   
   7 7X4W ...      FREECORE 3
   4 7X5B ...XREP
  18 7X5W ...      PUTREP   6                   [PUT REPLY BACK IN EXEC3 
  15 7X8= ...      TESTREP  NOTRAP,XITER,OK,XOK,NOFILE,XTEMP 
  10 7X8B ...      GEOERR   1,XOFREPER   
   4 7X8F ...XOK 
  19 7X8G ...      LDN   6  12                  [EXOFILE ALREADY EXISTS MESS 
   7 7X8H ...      LDN   7  2
  19 7X8J ...      ORX   7  GSIGN               [-VE TO INDUCE OL *DA FAIL   
   9 7X8K ...      BRN      ZERROR   
   5 7X8W ...XTEMP   
  19 7X94 ...      BZE   7  XSUNOP              [J IF SYSFLE WAS WEXCHOFED   
  20 7X96 ...      LDN   6  11                  [EXOFILE ENTRY DOESNT EXIST MESS 
   9 7X98 ...      BRN      XINTREP  
   5 7X9= ...XSUNOP  
   8 7X9# ...      LDN   6  13   
   9 7X9B ...      BRN      XINTREP  
   5 7X*B ...XITER   
   9 7X*N ...      BZE   7  XSUNOP   
  17 7X*W ...      LDN   6  10                  [REPLIES FOR CHINERR 
   5 7XB8 ...XINTREP 
   7 7XBB ...      LDN   7  2
   9 7XBW ...      BRN      ZERROR   
   4 7XDG    [   
   4 7XY6    [   
   4 7YCQ    #   
   4 84NF ...XIPB
  10 84SB          MHUNTW   3,EWDAS,GEXD 
   9 852L ...      LDN   0  APLINKF  
   9 85RL          STO   0  EMARK(3) 
   9 86?=          UNLOCKSU ESERL(3) 
  10 86KL ...      MHUNTW   3,EWDAS,GEXD 
   9 86QW          RELST    ESERL(3) 
   5 88PB    ZFAIL   
  19 8992          ACROSS   CHINERR,2           [TRANSFER FAIL ON FIP REWRITE
   5 89NL    ZERROR  
  16 8=8=          ACROSS   CHINERR,3           [GENERAL ERROR   
   5 8=MW    ZINTEG  
  17 8?7G          LDX   7  BACK2(1)            [SAVE EXOFILE NUMBER 
  17 8?M6          ACROSS   CHINERR,4           [INTEGRITY CODE FAIL 
   9 8#LB    ZIF   ACROSS   CHINERR,5
   4 8*62    #   
   4 8*KL    #   
  10 8B5=          MENDAREA 30,K99CHINTEG
   4 8BJW    #END
   8 ____ ...16052013000200000000
