  16 22FL ...#SEG           ANTIC70             [FRANK BLUNDELL  
   4 22KH ...#   
  16 22PD ...#     COPYRIGHT INTERNATIONAL COMPUTERS LTD   1982  
   4 22T* ...#   
   6 22_=    #DEF  XA1=A1
   7 23DW    #LIS  K0ANTIC   
   7 23YG          8HANTIC   
   4 24D6    #   
  15 24XQ    #     THIS  ROUTINE CONNECTS A FILE TO A PROGRAM
  16 25CB    #     ON ENTRY A GEXD BLOCK IS CHAINED AFTER THE CPA
   4 25X2    #   
   7 26BL    #DEF  XCA=EVENT2
   7 26W=    #DEF  T1=EWORK1 
   7 27*W    #DEF  T2=T1+1   
   7 27TG    #DEF  T3=T2+1   
   7 28*6    #DEF  T4=T3+1   
   7 28SQ    #DEF  T5=T4+1   
   7 29#B    #DEF  T6=T5+1   
   7 29S2    #DEF  T7=T6+1   
   7 2=?L    #DEF  T8=T7+1   
   7 2=R=    #DEF  T9=T8+1   
   4 2?=W    #   
   4 2?QG    #   
   4 2#=6    #   
  11 2#PQ          SEGENTRY K1ANTIC,QK1ANTIC 
  11 2*9B          SEGENTRY K2ANTIC,QK2ANTIC 
  11 2*P2          SEGENTRY K3ANTIC,QK3ANTIC 
  11 2B8L          SEGENTRY K4ANTIC,QK4ANTIC 
  12 2BH2 ...      SEGENTRY K77ANTIC,QK77ANTIC   
   4 2BN=    #   
   9 2C7W    ZLINK PAIR     PERION,1 
   8 2C*N ...WDA            4HDA 
  10 2CCL ...SCA            12HICLSCAFILE
  20 2CFJ ...XEWRARED        +EWRARED            [DIRECTORY FOR SU DOESNT EXIST  
  17 2CHG ...XEWSTUC         +EWSTUC             [SU Z TRAPS CLOSED  
   4 2CMG    #   
   5 2D76    QK1ANTIC
  14 2DLQ          LDCT  4  AOLPT/8             [TYPE,   
  17 2F6B          LDN   5  EXAOLPT             [LENGTH FOR GETACTY  
  19 2FL2          GETACTX  4,5,1               [GET AOLPT ACTIVITY AND START
  16 2G5L          LDX   1  BPTR(2)             [SETTING IT UP   
  10 2GK=          MHUNTW   3,EWDAS,GEXD 
   9 2H4W          LDX   0  JOBNO(2) 
  15 2HJG          STO   0  JOBNO(1)            [JOB NUMBER  
   9 2J46          LDX   0  EXCHNL(3)
  16 2JHQ          STO   0  EWCHAN(1)           [CHANNEL NUMBER  
   7 2K3B          LDN   0  1
  15 2KH2          STO   0  ATMARK(1)           [OLPA TYPE   
   9 2L2L          LDX   0  GIVEX(3) 
   7 2LG=          ANDN  0  1
   7 2L_W          SRC   0  1
   9 2MFG          ORS  0  GIVE(1)   
  18 2MP? ...      LDX   0  EXOTRAP(3)          [TRANSFER EXOFILE ENTRY  
  18 2MRW ...      STO   0  EXTRAPS(1)          [TRAPS FROM GEXD TO AOLPT
   8 2M_6          LDX   2  FX1  
  10 2NDQ          LINKSET  1,ZLINK(2)   
   9 2NYB          LDX   0  CEFN(3)  
  16 2PD2          STO   0  EXNUM(1)            [EXOFILE NUMBER  
  10 2PXL          LDX   4  EOPMODE(3)   
   9 2QC=          STO   4  EXMODE(1)
   8 2QWW          SBN   4  #600 
  10 2RBG          MFINDEXOF 2,EXNUM(1)  
   8 2RW6          LDCT  0  #400 
  10 2S*Q          STO   0  FRECHADD(1)  
  10 2STB          STOZ     CPRW1(1)  [3 
  11 2T*2          STOZ     CPRW2(1)  [REPLY 
  11 2TSL          STOZ     CPRSW(1)  [WORDS 
  14 2W#=          LDX   7  1         [SAVE PTR TO AOLPT 
  15 2WRW          LDX   5  ASUSN(2)  [SERIAL NO FROM EXOF   
  12 2X?G          MFINDSU 1,5        [X1-> ASTUC
  14 2XR6 ...      LDX   6  HUNIT(1)  [PTR TO DEVICE LIST
  14 2Y=Q          LDX   1  7         [RESTORE AOLPT PTR 
   9 2YGJ ...      BZE   5  ZEROSN   
  14 2YQB ...      STO   6  CPPTR(1)  [PTR TO DEVICE LIST
   5 2_28 ...ZEROSN  
   8 2_=2          BZE   4  SCRA 
  19 2_#K ...      UNLOCKSU 5                   [UNLOCK + RELST HERE AS DISC 
  20 2_C8 ...      RELST    5                   [..MAY GO OFFLINE + MFINDSU FAILS
  16 2_FR ...      LDX   1  BPTR(2)             [AOLPT PTR IN X1 
  16 2_JB ...      MHUNTW   3,EWDAS,GEXD        [GEXD PTR IN X3  
  16 2_L_ ...      MFINDEXOF 2,EXNUM(1)         [EXOF PTR IN X2  
  16 2_PL          LDCT  4  4                   [MASK FOR BIT 6  
   9 329=          ANDX  4  EXMARK(2)
  14 32NW          SLL   4  5                   [TO BIT 1
  10 338G          ORS   4  EXMAYBE(1)   
  16 33N6          LDX   0  ASUSN(2)            [MOVE IN REPLIES 
  16 347Q          STO   0  T2(1)               [SERIAL NUMBER   
   8 34MB          ADN   1  T3   
  13 3572          LDN   0  FNAMER(3)           [NAME
   7 35LL          MOVE  0  3
   8 366=          SBN   1  T3   
  17 36KW          LDX   0  FIP+3(2)            [2ND LEVEL OVERFLOW  
   8 375G          STO   0  T6(1)
  10 37K6          LDX   0  FNAMER+3(3)  
  17 384Q          STO   0  T7(1)               [GENERATION/VERSION  
   9 38JB          LDN   0  FIP+8(2) 
  17 3942          ADN   1  T8                  [REST OF THE REPILES 
   7 39HL          MOVE  0  2
   8 3=3=    NSCR  LDX   2  FX2  
  14 3=GW          FPCACA   2                   [FIND PCA
   8 3?2G          LDCT  0  #6   
   9 3?G6          LDX   4  EXCHNL(3)
  13 3?JG ...      ORX   4  0           [TYPE/UNIT NO
  11 3?RL ...      SETNCORE 3,1,ADATA,ASUP   
   7 3?W2 ...      LDN   0  6
  13 3?YB ...      STO   0  A1(1)               [*DA 
   8 3#2Q ...      BZE   6  NOGEO
   8 3#56 ...      LGEOG    6,6  
   5 3#7G ...NOGEO   
  15 3#9W ...      STO   6  A1+1(1)             [GEOG UNIT NO
  13 3##= ...      STO   5  A1+2(1)             [SUSN
  13 3#FB          LDCT  1  #400        [OFF LINE BIT
   8 3*DL ...      PUTPERENT 4,1 
  16 3*Y=          ROLPA                        [RECHAIN AOLPT   
  10 3BXG          MHUNTW   3,EWDAS,GEXD 
   9 3CC6    NMOV  LDX   0  GIVEX(3) 
  19 3CWQ          BPZ   0  NCOM                [JUMP IF NOT ONLINE COMMAND  
  15 3DBB          FREECORE 3                   [FREE GEXD   
   6 3DW2          ENDCOM
   4 3F*L    #   
   9 3FT=    NCOM  LDX   5  CNR(3)   
  15 3G#W          ADN   5  1                   [REPLY WORD  
  19 3GSG          LDN   7  1                   [ONE WORD ONLY IF MODE #1200 
   7 3H#6          SMO      6
   9 3HRQ          LDX   0  EXMODE   
   8 3J?B          SBN   0  #600 
  16 3JR2          BZE   0  XSCR                [JUMP IF SCRATCH 
   7 3K=L          SMO      6
   8 3KQ=          LDN   2  T2   
   8 3L9W          ADN   3  XA1  
   8 3LPG          SBN   0  #400 
   8 3M96          BZE   0  NCRE 
  18 3MNQ          LDN   7  8                   [8 WORDS IF NOT CREATE   
   7 3N8B    NCRE  SMO      7
  16 3NN2          MOVE  2  0                   [MOVE IN REPLIES 
   8 3NT8          LDN   4  XA1  
  20 3P2B    XADDR GETWORD  5,5,WRITE,6,7       [GEORGE ADDRESS OF OBJ PROG CA.  
   9 3P7J          TESTREPNOT  OK,XRV
  10 3P#Q          MHUNT  3,EWDAS,GEXD   
  19 3PFY          ADX   4  3                   [ADDRESS OF REPLIES IN GEXD  
  20 3PM6          TXL   6  7                   [ X6=NO OF WORDS WE CAN ACCESS   
  20 3PS#          BCC      XMOV                [J IF MOVE CAN BE DONE IN ONE GO 
   7 3P_G          SMO      6
  18 3Q6N          MOVE  4  0                   [MOVE AS MANY AS ALLOWED 
   9 3Q?W          LDX   5  CNR(3)   
   7 3QF4          ADN   5  1
  20 3QL=          ADX   5  6                   [UPDATE REQUIRED OBJ PROG ADDRESS
  20 3QRD ...      SBX   7  6                   [NO OF WORDS STILL TO BE MOVED   
   7 3QYL          SMO      6
  21 3R5S          LDN   4  XA1                 [ADDRESS IN GEXD FROM WHICH TO MOVE  
  17 3R?2          BRN      XADDR               [J TO DO REST OF MOVE
   7 3RD8    XMOV  SMO      7
  16 3RKQ          MOVE  4  0                   [MOVE IN REPLIES 
   7 3S5B          FREECORE 3
   7 3SK2          WORDFIN   
  16 3T4L          RUNPROG                      [RESTART PROGRAM 
   4 3TJ=    #   
   4 3W3W    #   
  17 3WHG    SCRA                               [SET SCRATCH REPLIES 
   9 3X36          LDEX  0  FIP+5(2) 
   8 3XGQ          BZE   0  SCRB 
  10 3Y2B          LDX   0  FDCELLS+5(2) 
   7 3YG2          SBN   0  1
  14 3Y_L    SCRB  STO   0  T6(1)               [LAST LBN
   9 3_F=          LDX   0  BKSIZE(3)
  15 3_YW          STO   0  T7(1)               [BUCKET SIZE 
   9 42DG          LDX   0  BREQ(3)  
  16 42Y6          STO   0  T8(1)               [BLOCKS REQUESTED
   9 434S ...      LDX   5  ARUNIT(3)
  16 439G ...      STO   5  T9(1)               [SERIAL NUMBER   
  18 43MJ ...      LDN   6  0                   [SUSN OF ZERO FOR SCRATCH
   8 43PX ...      BZE   5  XOUT 
   9 43S= ...      FINDSU   2,5,XOUT 
  18 43YY ...      LDX   6  HUNIT(2)            [X6= DEVICE LIST POINTER 
   4 445L ...XOUT
   8 44C2          LDCT  0  #20  
   9 44WL          ANDX  0  EMARK(3) 
   7 45B=          SLL   0  4
  16 45TW          STO   0  EXMAYBE(1)          [SET MAYBE MARKER
   8 46*G          BRN      NSCR 
   4 46T6    #   
   4 47#Q    #   
   7 47SB    XSCR  LDN   7  4
   7 48#2          SMO      6
   8 48RL          LDN   2  T6   
   8 49?=          ADN   3  XA1  
   8 49QW          BRN      NCRE 
   4 4==G    #   
   4 4=Q6    #   
   4 4?9Q    #   
  16 4?PB    #                                  ANTICIPATED OPENS
  20 4#92    ZREPLY         #40000001           [REPLY FOR DIFFERENT BUCKET SIZE 
   5 4#NL    QK4ANTIC
  16 4*8=          GEOERR   1,CREATE?           [ILLEGAL ENTRY   
   5 4*MW    QK2ANTIC
  17 4B7G          LDN   7  9                   [9 WORD CONTROL AREA 
  17 4BM6          CHECKEXT XCA(2),7,ZRES,APETADDR(2),W,ZJ1,HLOCK1   
  20 4C6Q    ZJ1                                [CHECK PROGRAM C/A RESERVATIONS  
   9 4CLB    #SKIP K6ANTIC>199-199   
   4 4D62    (   
   9 4DKL          LDX   4  EXMODE(2)
   8 4F5=          CALL  0  ZMODE
   4 4FJW    )   
  18 4G4G          LDX   3  XCA(2)              [ADDRESS OF CONTROL AREA 
  16 4GJ6          LDX   1  APETADDR(2)         [ADDRESS OF PCA  
  19 4H3Q          FADDRESS 1,3,HLOCK1          [GET ABSOLUTE GEORGE ADDRESS 
  18 4HHB          LDX   4  0(3)                [LOAD TYPE/MODE WORD AND 
  19 4J32          STO   4  T1(2)               [STORE WITH ONLINE REPLIES   
   9 4JGL    #SKIP K6ANTIC>199-199   
   4 4K2=    (   
   8 4KFW          ANDN  4  #1777
   8 4K_G          CALL  0  ZMODE
   4 4LF6    )   
   9 4LYQ          LDX   4  EXMODE(2)
  20 4MDB          DLA   4  T1(2)               [RESET MODE FROM ONLINE COMMAND  
  16 4MY2          LDX   4  XCA(2)              [ADDRESS OF C/A  
   8 4NCL          LDN   3  T1(2)
  19 4NX=          FINMOVE  1,4,3,7,HLOCK1      [MOVE C/A INTO OBJECT PROGRAM
   8 4PBW    XJ6   LDCT  4  256  
  15 4PWG          ORS  4  GIVE(2)        [SET GIVE MARKER   
   8 4QB6          LDX   1  FX1  
   9 4QTQ          OLPFIN1  ZLINK(1) 
   9 4R*B    #SKIP K6ANTIC>199-199   
   4 4RT2    (   
   7 4S#L    ZMODE LDN   5  4
  20 4SS=    ZJ3   SBN   4  #100                [ENSURE MODES #100,#200,#300,#400
   8 4T?W          BZE   4  ZJ2  
   8 4TRG          BCT   5  ZJ3  
  10 4W?6          GEOERR   1,WRNGMODE   
   7 4WQQ    ZJ2   EXIT  0  0
   4 4X=B    )   
  16 4XQ2    ZRES  TOPCA1   ZLINK(1)            [AOLPT TO SLEEP  
  18 4Y9L          UNPLUG                       [UNPLUG PROGRAM BEFORE...
  16 4YP=          ILLEGAL  NOCA                [...FAILING IT   
   4 4_8W    #   
   5 4_NG    QK3ANTIC
  17 5286          LDN   7  5                   [5 WORD CONTROL AREA 
  17 52MQ          CHECKEXT XCA(2),7,ZRES,APETADDR(2),W,ZJ1A,HLOCK1  
  16 537B    ZJ1A  LDX   1  APETADDR(2)         [ADDRESS OF PCA  
   8 53M2          LDN   4  T1(2)
  16 546L          LDX   3  XCA(2)              [ADDRESS OF C/A  
  19 54L=          FOUTMOVE 1,3,4,7,HLOCK1      [MOVE C/A INTO ACOMMUNE WORDS
  10 555W          LDX   4  EXMAYBE(2)   
  20 55KG          BPZ   4  XJ3                 [BRANCH IF NO CHECKING REQUIRED  
   8 5656          LDX   4  T3(2)
   8 56JQ          TXU   4  T7(2)
  17 574B          BCC      XJ7                 [SAME BUCKET SIZE?   
   8 57J2    XJ4   SMO      FX1  
  21 583L          LDX   4  ZREPLY              [REPLY INDICATING DIFFERENT BUCKET.. 
  13 58H=    XJ5   LDX   3  XCA(2)              [SIZE
  21 592W          ADN   3  1                   [ADDRESS OF OBJECT PROG.REPLY WORD   
  19 59GG          FADDRESS 1,3,HLOCK1          [INTO ABSOLUTE GEORGE ADDRESS
  17 5=26          STO   4  0(3)                [REPLY INTO OBJ PROG 
   8 5=FQ          LDX   1  FX1  
  20 5=_B          OLPFIN1  ZLINK(1)            [GO ASLEEP WITH LINK TO PERION   
  17 5?F2    XJ7   LDX   4  T8(2)               [FILE SIZE IN BLOCKS 
   8 5?YL          TXL   4  T4(2)
  20 5#D=          BCC      XJ3                 [BRANCH IF FILE SIZE BIG ENOUGH  
  18 5#XW          BZE   4  XJ4                 [BRANCH IF ZERO BLOCKS   
  17 5*CG          ORX   4  GSIGN               [BRANCH IF TOO SMALL 
   8 5*X6          BRN      XJ5  
   8 5BBQ    XJ3   LDX   4  T1(2)
  19 5BWB          STO   4  T5(2)               [TYPE/MODE TO ONLINE REPLIES 
   8 5CB2          LDN   3  T5(2)
   9 5CTL          LDX   4  XCA(2)   
  20 5D*=          FINMOVE  1,4,3,7,HLOCK1      [OVERWRITE OBJ PROG C/A WITH...  
  17 5DSW          BRN      XJ6                 [...ONLINE REPLIES   
   4 5F#G    #   
   6 5F#R ...QK77ANTIC   
   4 5F#W ...#   
  14 5F#_ ...#              ENTRY POINT FOR EXCHOFE MACRO
   4 5F*4 ...#   
  10 5F*7 ...      LDX   0  ACOMMUNE9(2) 
  19 5F*= ...      STO   0  AWORK1(2)           [PRESERVE REQD ACCESS MODES  
  21 5F*? ...      LDX   0  ACOMMUNE6(2)        [=0 FOR TRAP CHECKING,=1 FOR UPDATING
  21 5F*# ...      STO   0  AWORK3(2)           [D&T LAST ACCESSED & -VE FOR D&T LAST
  18 5F** ...      SETNCORE 10,3,FILE,FABSNB    [SET UP 10 WORD FABSNB   
  18 5F*D ...      LDX   7  ACOMMUNE7(2)        [X7 - 0=EXOF,1=DIR,2=ICL 
   9 5F*H ...      LDX   6  WDA(1)   
  17 5F*L ...      STO   6  A1+1(3)             [FILL IN CHARS ^DA^  
  16 5F*P ...      LDX   6  ACES                [AND SPACES IN   
  16 5F*S ...      STO   6  A1+3(3)             [DIRECTORY NAME  
   9 5F*X ...      STO   6  A1+2(3)  
  17 5FB2 ...      LDX   6  ACOMMUNE8(2)        [SUSN IS PARAMETER   
  10 5FB5 ...      MHUNTW   2,EWDAS,EXOF 
  20 5FB8 ...      BZE   6  NEXOF               [ZERO SUSN - GO GET IT FROM EXOF 
   8 5FB? ...      BRN      SNCON
   5 5FBB ...NEXOF   
  16 5FBF ...      LDX   6  ASUSN(2)            [EWDAS-EXOF BLOCK
   5 5FBJ ...SNCON   
  18 5FBM ...      SLL   6  6                   [PRESERVE L.S. 6 DIGITS  
  14 5FBQ ...      LDN   4  6                   [COUNT   
   9 5FBT ...      LDN   1  A1+1(3)  
  14 5FBY ...      ORX   1  GSIGN               [A1+1.2  
   7 5FC3 ...      LDN   5  0
   5 5FC6 ...SNZER   
   7 5FC9 ...      SLL   56 3
   8 5FC# ...      BNZ   5  STREM
   8 5FCC ...      BCT   4  SNZER
   8 5FCG ...      BRN      STFIN
   5 5FCK ...SNMOV   
   7 5FCN ...      LDN   5  0
  16 5FCR ...      SLL   56 3                   [GET NEXT DIGIT  
   5 5FCW ...STREM   
  18 5FC_ ...      DCH   5  0(1)                [STORE IN DIRECTORY NAME 
   7 5FD4 ...      BCHX  1  /
   8 5FD7 ...      BCT   4  SNMOV
   5 5FD= ...STFIN   
   8 5FD* ...      LDX   1  FX1  
  16 5FDD ...      BNZ   7  MISSY               [J IF NOT EXOF   
   8 5FDH ...      LDCT  0  #100 
   9 5FDL ...      ANDX  0  FIP+5(2) 
  18 5FDP ...      BZE   0  STNAM               [J IF NOT A SYSTEM FILE  
   9 5FDS ...      LDX   0  FIP(2)   
  17 5FDX ...      TXU   0  SCA(1)              [SYFLE OR ICLSCAFILE 
  16 5FF2 ...      BCC      STNAM               [J IF ICLSCAFILE 
   5 5FF5 ...SHRTN   
  17 5FF8 ...      LDN   0  4                   [SHORTEN FILE-FABSNB 
  18 5FF? ...      STO   0  A1(3)               [IN ORDEN TO OPEN ENTRY  
   8 5FFB ...      SMO      FX2  
   9 5FFF ...      STO   0  AWORK2   
  16 5FFJ ...      BRN      TOPEN               [FOR DIRECTORY   
   5 5FFM ...MISSY   
  15 5FFQ ...      SBN   7  1                   [DIR OR ICL  
  14 5FFT ...      BNZ   7  STNAM               [J IF ICL
  18 5FFY ...      ADN   7  1                   [RESTORE PARAM INDICATOR 
  15 5FG3 ...      BRN      SHRTN               [J FOR DIR   
   5 5FG6 ...STNAM   
   8 5FG9 ...      LDN   0  10   
  14 5FG# ...      STO   0  A1(3)               [SET HDDR
   8 5FGC ...      SMO      FX2  
   9 5FGG ...      STO   0  AWORK2   
   7 5FGK ...      LDN   0  0
   9 5FGN ...      STO   0  A1+7(3)  
   9 5FGR ...      STO   0  A1+8(3)  
   9 5FGW ...      STO   0  A1+9(3)  
   9 5FG_ ...      LDN   6  A1+4(3)  
  19 5FH4 ...      BNZ   7  STICL               [X7=0 FOR EXOF,=1 FOR ICL NOW
   9 5FH7 ...      LDX   4  FIP+4(2) 
   8 5FH= ...      SRL   4  12   
  18 5FH* ...      BNZ   4  STGEN               [J IF GEN NUM NOT ZERO   
  17 5FHD ...      LDCT  4  #100                [SET BIT2 FOR OPENDIR
   5 5FHH ...STGEN   
  17 5FHL ...      STO   4  A1+8(3)             [STORE GENERATION NO 
  20 5FHP ...      LDN   5  FIP(2)              [GET LOCAL NAME FROM EWDAS-EXOF  
   8 5FHS ...      BRN      STLOC
   5 5FHX ...STICL   
  19 5FJ2 ...      LDN   5  SCA(1)              [SET LOCAL NAME TO ICLSCAFILE
   5 5FJ5 ...STLOC   
  17 5FJ8 ...      MOVE  5  3                   [MOVE IN LOCAL NAME  
  21 5FJ? ...      BS       3,BFABEXO           [INDICATE EXOFILE ENTRANT REQUIRED   
   5 5FJB ...TOPEN   
  17 5FJF ...      OPENDIR  OBRK,GENERAL,QUERY  [OPEN REQUIRED ENTRY 
  17 5FJG ...      LDX   5  AWORK3(2)           [FUNCTION INDICATOR  
   9 5FJJ ...      TESTREP  OK,OPOK  
   8 5FJM ...      BRN      NOFE 
   4 5FJQ ...OBRK
   9 5FJT ...      GEOERR   1,BREAKIN
   4 5FJY ...NOFE
  18 5FJ_ ...      BNZ   5  OKTDY               [J IF NOT TRAP CHECKING  
   9 5FK3 ...      LDX   0  AWORK2(2)
  19 5FK6 ...      SBN   0  4                   [IF ORIGINAL FABSNB LENGTH   
  20 5FK9 ...      BNZ   0  NOFE1               [NOT 4 THEN DIRECTORY NOT OPENED 
  10 5FK# ...      MHUNTW   3,EWDAS,GEXD 
  18 5FKC ...      OPTMESS  XEWRARED(1),ESERL(3) [DIRECTORY DOESNT EXIST 
   5 5FKG ...NOFE1   
  19 5FKK ...      SETREP   NOFILE              [NO ENTRY - NO TRAP CHECKS   
   8 5FKN ...      LDX   4  GSIGN
   8 5FKR ...      BRN      OKFIN
   5 5FKW ...NOTRAP  
   7 5FKX ...#UNS  ILOGACC   
   4 5FKY ...#SKI
   9 5FK_ ...      MFREE    FILE,ENT 
  17 5FL2 ...      LDX   0  AWORK2(2)           [IF ORIGINAL LENGTH  
  17 5FL4 ...      SBN   0  4                   [OF FABSNB WAS NOT 4 
  18 5FL7 ...      BNZ   0  NOTRAP1             [THEN NOT DIRECTORY SO J 
  10 5FL= ...      MHUNTW   3,EWDAS,GEXD 
  16 5FL* ...      OPTMESS  XEWSTUC(1),ESERL(3)  [SU TRAPS CLOSED
   5 5FLD ...NOTRAP1 
   9 5FLH ...      KEEPREP  5,NOTRAP 
   8 5FLL ...      LDX   4  GSIGN
   8 5FLP ...      BRN      OKCL 
   4 5FLS ...OPOK
  21 5FLT ...      BNZ   5  NOCHKTRP            [J IF EXCHOFE IS ONLY UPDATING D&T   
  19 5FLX ...      CHECKTRAPS                   [PUTS ACCESS MODES IN EXEC1  
   9 5FM2 ...      LDX   0  EXEC1(2) 
  16 5FM5 ...      ANDX  0  AWORK1(2)           [MODES REQUIRED  
   9 5FM8 ...      SBX   0  AWORK1(2)
  18 5FM? ...      BNZ   0  NOTRAP              [J FOR FORBIDDEN ACCESS  
  17 5FMB ...      LDX   4  EXEC1(2)            [ACCESS MODES ALLOWED
   8 5FMH ...      KEEPREP  5,OK 
   8 5FMN ...      BRN      OKCL 
   5 5FMT ...NOCHKTRP
  10 5FN2 ...      MHUNTW   3,FILE,ENT   
  21 5FN7 ...      BBUS     23,ECOPS(3),TANDD   [J IF NOT DIRECTORY FOR D&T UPDATE   
  21 5FN# ...      BACKSPACE                    [BACKSPACE BY NO OF TRAPS RECORDS ETC
  10 5FNG ...      MHUNTW   3,FILE,ENT   
   5 5FNH ...TANDD   
  13 5FNK ...      LDX   0  EDATE               [DATE
  13 5FNN ...      LDX   1  CLEANCT             [TIME
  19 5FNW ...      BPZ   5  TDLA                [J IF ONLY D&T LAST ACCESSED 
  16 5FP4 ...      STO   0  EWRITDAY(3)         [UPDATE DATE &   
  17 5FP7 ...      STO   1  EWRITTIME(3)        [TIME LAST WRITTEN   
   4 5FP= ...TDLA
  14 5FP* ...      STO   0  EDLA(3)             [DATE &  
  17 5FPD ...      STO   1  ETLA(3)             [TIME LAST ACCESSED  
   9 5FPH ...      LDX   0  GMTNOW   
  20 5FPL ...      STO   0  EGMTLA(3)           [TIMESINCE LAST ACCESS - DOWNTIME
   5 5FPM ...WRITBACK
  10 5FPP ...      NAME     3,FILE,FWB   
  16 5FPS ...      REWRITE                      [REWRITE RECORD  
  10 5FPX ...      MFREE      FILE,FWB   
   7 5FPY ...#UNS  ILOGACC   
   4 5FP_ ...(   
   9 5FQ2 ...      BRN      XCLOSE1  
   4 5FQ3 ...OKCL
  16 5FQ4 ...      BNZ   5  XFRUST          [J IF ACCESS NOT OK  
  17 5FQ5 ...      LOGACCESS 2,AWORK1(2)    [LOG ACCESS OF CONTENTS  
   9 5FQ6 ...      BRN      XCLOSE   
   5 5FQ7 ...XFRUST  
  17 5FQ8 ...      LOGACCESS 7,AWORK1(2)    [LOG FRUSTRATED ACCESS   
   5 5FQ9 ...XCLOSE  
   9 5FQ= ...      MFREE    FILE,ENT 
   5 5FQ? ...XCLOSE1 
   4 5FQ# ...)   
   7 5FQ* ...#UNS  ILOGACC   
   4 5FQB ...#SKI
   4 5FQC ...OKCL
   7 5FQD ...      CLOSETOP  
   7 5FQF ...      PUTREP2  5
   5 5FQG ...OKFIN   
   9 5FQH ...      LDX   0  AWORK3(2)
  18 5FQJ ...      BNZ   0  OKTDY               [J IF NOT TRAP CHECKING  
  17 5FQK ...      BNZ   7  OKTDY               [J IF PARAM NOT EXOF 
  10 5FQL ...      MHUNTW   3,EWDAS,GEXD 
  18 5FQM ...      STO   4  EXOTRAP(3)          [PRESERVE TRAPS RECORD   
   9 5FQN ...      LDX   0  AWORK2(2)
   9 5FQQ ...      STO   0  EXEC1(2) 
   5 5FQT ...OKTDY   
  10 5FQY ...      MHUNT    3,FILE,FABSNB
  16 5FR3 ...      FREECORE 3                   [FREE FILE-FABSNB
   5 5FR6 ...      UP
   9 5FS6    XRV   GEOERR   1,CONNECT
   4 5G?Q    #   
   4 5GRB    #   
  10 5H?2          MENDAREA 30,K99ANTIC  
   4 5HQL    #END
   8 ____ ...72555322000100000000
