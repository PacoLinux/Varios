  15 22FL    #LIS  K0WHATPERA>K0ALLGEO>K0GREATGEO>K0COMMAND  
  14 22LS ... SEG  WHATPERA,,,,G203           [N.R.BOULT 
   4 22S2 ...[   
  15 22_8 ...[ (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1982
  13 236B ...[     THIS EXCLUDES CODE UNDER #SKI G203
   4 23?J ...[   
   7 23DQ ...#OPT G203 = 0   
   6 23KY ...#SKI G203&1 
  17 23R6 ...# WITH UGUG EDIT M203 (ALLOW WHATPER FOR REMOTE DEVICES)
  13 23YG          SEGENTRY K1WHATPERA,QK1WHATPERA   
  20 24D6    # THIS SEG CONTINUES THE IMPLEMENTATION OF THE WHATPER COMMAND. FOR 
  21 24XQ    # EACH RECORD IN THE BLOCK SET UP BY SEG WHATPER, IT ANALYSES THE INFO  
  21 25CB    # AND OUTPUTS THE APPROPRIATE MESSAGE(S), READING ANY FILES (SYS.DOC OR 
  10 25X2    # SYS.SERIAL) IF RELEVANT.  
   4 26BL    #   
  15 26W=    #     THE FILES REFERENCED ARE OPEN AS FOLLOWS:-
  11 27*W    #     DOCUMENT :        LEVEL -1
  19 27TG    #     SERIAL   :        LEVEL -2 (ONLY OPEN IF DOCUMENT OPEN)   
  11 28*6    #     PROPERTY :        LEVEL  0
   4 28SQ    #   
   9 29#B    TEMP           4HTEMP   
   9 29S2                   4HPERM   
   9 2=?L    XINCL          4HINCL   
   9 2=R=                   4HEXCL   
   5 2?=W    XCR   +3
   5 2?QG    MTNO  +5
   5 2#=6    SIX   +6
   9 2BN=    XCAT           #10000   
   9 2C7W    XQEND          +FLOWQ   
   5 2C=R    XMES1   
   9 2C*N    XFUSSY         +FUSSY   
   9 2CDK    XFLOUNUS       +FLOUNUS 
   9 2CHG    XFRESUNEN      +FRESUNEN
   9 2CLC    XFRONG         +FWRONG  
   9 2CP#    XFIDLE         +FIDLE   
   9 2CS9    XFONLINE       +FONLINE 
   5 2CX6    XMES2   
   9 2D23    XFUSSYA        +FUSSYA  
   9 2D4Y    XFLOUNUSA      +FLOUNUSA
  10 2D7T    XFRESUNENA     +FRESUNENA   
   9 2D=Q    XFRONGA        +FWRONGA 
   9 2D*M    XFIDLEA        +FIDLEA  
   9 2DDJ    XFONLINEA      +FONLINEA
   9 2DHF    XFOLJOB        +FOLJOB  
   8 2DLQ    XFDOC       +FDOC   
   8 2H4W    XFTINS      +FINS   
   8 2HJG    XFFAIL      +FFAIL  
   9 2J46    XFTWRONG       +FTWRONG 
   9 2J?Y    XFSWO          +JUNITSWO
   9 2JHQ    XFUSEWT        +FUSEWTXX
   8 2K3B    XFTNO          +FTNO
   8 2KH2    XFNFD          +FNFD
   8 2L2L    XFENG          +FENG
   6 2LG=    #SKI  IPCV1 
   4 2L_W    (   
   9 2MFG    XFUNOP         +FUNOP   
   9 2M_6    XFUSIN         +FUSIN   
   9 2NDQ    XFLINK         +FLINKUS 
   4 2NYB    )   
   9 2PD2    XHDDR          4HHDDR   
   9 2PXL    YFILE          +HFILE   
   9 2QC=    XBIT           #37777777
  14 2QWW    #     S/R   TO CLOSE UP USER AND LOCAL NAMES
   9 2RBG    #     X3   -> USER NAME 
   7 2RW6    #     X4    LINK
   5 2S*Q    UPCLOSE 
   8 2STB          SBX   4  FX1  
  10 2T*2          LDN   7  ACOMMUNE1(2) 
   8 2TSL          FLENME1  0,3,5
  10 2W#=          SBN   7  ACOMMUNE1(2) 
   7 2WRW          SLC   7  2
   8 2X?G          LDN   0  24   
   7 2XR6          SBX   0  7
  16 2Y=Q          BPZ   0  NOT25               [J IF < 25 CHARS 
  11 2YQB          HUNTW    3,ADATA,CSTORE   
   9 2_=2          BPZ   3  PHOUND1  
  11 2_PL          SETNCORE 7,3,ADATA,CSTORE 
   5 329=    PHOUND1 
  17 32NW          ADN   2  ACOMMUNE1           [THIS NECESSARY AS   
   8 338G          ADN   3  A1   
  18 33N6          MVCH  2  26                  [OUTPARAM USES ACOMMUNE7 
  11 347Q          OUTPARAM 7,A1,ADATA,CSTORE
   8 34MB          BRN      XIT  
   5 3572    NOT25   
  10 35LL          OUTPARAM 7,ACOMMUNE1  
   4 366=    XIT 
   8 36KW          ADX   4  FX1  
   7 375G          EXIT  4  0
   4 3768    #   
   5 376W    XOUTBLK 
   8 377J          SBX   7  FX1  
   7 378=          OUTBLOCK 0
   8 378Y          ADX   7  FX1  
   7 379L          EXIT  7  0
   4 37=#    #   
   5 37?2    XSTEP   
   8 37?*          SBX   7  FX1  
   8 37?N          STEP     0(3) 
   8 37#3          ADX   7  FX1  
   7 37#B          EXIT  7  0
   4 37*4    #   
   5 37*Q    XOUTPRM 
   8 37BD          SBX   7  FX1  
  11 37C6          OUTPARAM 0,A1,ADATA,AWDOC 
   8 37CS          ADX   7  FX1  
   7 37DG          EXIT  7  0
   4 37F8    #   
   5 37FW    XHTAWP  
  10 37GJ          MHUNTW   3,ADATA,AWPER
   7 37H=          EXIT  7  0
   4 37HY    #   
   6 37K6    QK1WHATPERA 
  16 384Q          STO   5  AWORK2(2)           [SAVE PER TYPE   
   9 38JB          CALL  7  XHTAWP   
  14 3942          LDX   2  3                   [SAVE X3 
   8 39HL          LDN   4  A1+5 
  16 3=3=          STO   4  A1(3)               [INTERNAL POINTER
  19 3=GW          ADN   3  A1+5                [INT PTR TO FIRST REC HDDR   
   8 3?2G          BRN      ST1  
   8 3?G6    UPD   LDX   2  FX2  
   9 3?_Q          LDX   3  AWORK1(2)
   7 3#FB    REWX  SBN   3  1
  15 3#_2          BNG   3  NO                  [J IF NO MORE
  18 3*DL          REWIND   0(3)                [REWIND ANY FILES OPEN   
   8 3*Y=          BRN      REWX 
  16 3BCW    NO    LDX   5  AWORK2(2)           [RESTORE PER TYPE
   9 3BXG          CALL  7  XHTAWP   
  14 3CC6          LDX   2  3                   [SAVE X3 
   9 3CWQ          LDX   4  A1+1(3)  
   7 3DBB          SBN   4  1
  18 3DW2          BZE   4  NOUGH               [J IF NO MORE RECS IN BLK
  17 3F*L          STO   4  A1+1(3)             [UPDATE COUNT OF RECS
  18 3FT=          LDN   4  K54-K50+14          [LENGTH OF THIS RECORD   
  19 3G#W          ADS   4  A1(3)               [POINTER SET TO NEXT RECORD  
  20 3GSG          ADX   3  A1(3)               [ABSOLUTE POINTER TO NEXT RECORD 
   4 3H#6    ST1 
  17 3HHY          JSWOUT   3,SWITOUT           [J IF SWITCHED OUT   
  15 3HRQ          LGEOG    3,0                 [GET GEOG NO 
   8 3J?B          SMO      FX2  
   9 3JR2          STO   0  AWORK3   
   7 3K=L          LDX   0  5
   8 3KQ=          SBN   0  28   
  16 3Q6W          BZE   0  PB                  [J IF IPB TYPE 28
  15 3QLG          JWRONG   3,WRONGED           [J IF WRONGED
  15 3R66          JONL     3,ONLINE            [J IF ONLINE 
   7 3RKQ          LDX   0  5
   7 3S5B          SBN   0  9
   8 3SK2          BZE   0  TABLA
   8 3T4L          SBN   0  16   
   8 3YG2          BZE   0  TABLA
   9 3Y_L          JALLOC   3,OWNED  
   7 3_F=          LDX   0  5
   7 3_YW          SBN   0  5
  17 42DG          BNZ   0  XIDLE               [J IF NOT MAG TAPE   
  10 42Y6          LDX   0  K54-K50+8(3) 
   8 43CQ          ANDN  0  #10  
  17 43XB          BZE   0  XIDLE               [J IF TAPE NOT LOADED
   9 44C2          CALL  7  OUTNUM   
   7 44WL          LDN   0  1
  18 45B=          BRN      MTINFO              [BRN TO OUTPUT MT INFO   
   6 66S2    #SKI  IPCV1 
   4 67?L    (   
  10 67R=    PB    LDX   6  K54-K50+8(3) 
  16 68=W          BNZ   6  PBAL                [J IF ANY LINKS  
   9 68QG          JENG     3,UNOP   
   8 69=6          BRN      XIDLE
   9 69PQ    PBAL  JDISENG  3,USIN   
   9 6=9B          CALL  7  OUTNUM   
   7 6=P2          LDX   0  6
   9 6?8L          CALL  7  OUTNUM2  
  17 6?N=          LDX   7  XFLINK(1)           [ALLOCATED & OPERABLE
   9 6#7W          BRN      MONOUTX  
   9 6#MG    UNOP  CALL  7  OUTNUM   
  19 6*76          LDX   7  XFUNOP(1)           [NOT ALLOCATED BUT OPERABLE  
   9 6*LQ          BRN      MONOUTX  
   9 6B6B    USIN  CALL  7  OUTNUM   
   7 6BL2          LDX   0  6
   9 6C5L          CALL  7  OUTNUM2  
  18 6CK=          LDX   7  XFUSIN(1)           [ALLOCATED BUT INOPERABLE
   9 6D4W          BRN      MONOUTX  
   4 6DJG    )   
   5 6F46    TABLA   
   9 6FHQ          JFREE    3,XIDLE  
   9 6G3B          CALL  7  OUTNUM   
   9 6GH2          LDX   7  XFUSSY(1)
   9 6H2L          BRN      MONOUTX  
   5 6H5Q    SWITOUT 
   9 6H8W          CALL  7  OUTNUM   
   9 6H#2          LDX   7  XFSWO(1) 
   9 6HC6          BRN      MONOUTX  
   5 6HG=    WRONGED 
  17 6H_W          CALL  7  OUTNUM              [OUTPUT OPERATORS NO.
   7 6JFG          LDN   0  3
   9 6J_6          BRN      XMONTX   
   5 6KDQ    XIDLE   
  17 6KYB          CALL  7  OUTNUM              [OUTPUT OPERATORS NO.
   7 6LD2          LDN   0  4
   9 6LXL          BRN      XMONTX   
   5 6MC=    OWNED   
   7 6MWW          LDX   0  5
   7 6NBG          SBN   0  5
   8 6NW6          BZE   0  OWNTP
  20 6P*Q          JENG     3,USSY              [J IF NOT ^RESERVED & UNENGAGED^ 
   8 6PTB          BRN      RESNE
  10 6Q*2    OWNTP LDX   0  K54-K50+8(3) 
   8 6QSL          ANDN  0  #10  
  16 6R#=          BNZ   0  USSY                [J IF TAPE LOADED
   5 6RCT    RESNE   
  17 6RHD          CALL  7  OUTNUM              [OUTPUT OPERATORS NO.
   7 6RM3          LDN   0  2
   5 6RQL    XMONTX  
   7 6RW9          LDX   4  5
   7 6R_S          SBN   4  5
   9 6S5C          BZE   4  XMONMT   
   7 6S92          SMO      0
   9 6S#K          LDX   7  XMES1(1) 
   9 6SD8          BRN      MONOUTX  
   5 6SHR    XMONMT  
   8 6SMB          CALL  4  XMODE
   9 6SR6          BRN      MONOUTX  
   4 6T=Q    #   
  15 6TQB    UNMT  LGEOG    3,6                 [OPS NO TO X6
   7 6W4R          LDN   0  9
   9 6WC8          CALL  7  XOUTBLK  
   7 6WPL          LDX   0  6
  16 6X9=          CALL  7  OUTNUM2             [O/P UNIT NUMBER 
   9 6XNW          CALL  7  XHTAWP   
   8 6Y8G          ADX   3  A1(3)
   9 6YN6          JJOBLMT  3,XJOBL  
   8 6_7Q    STEP3 LDX   2  FX2  
  21 6_MB          LDX   3  AWORK1(2)           [TO REF :SYS.DOC =LOWEST FILE OPENED 
  15 7272          SBN   3  1                   [BY COMMAND  
   8 72LL          CALL  7  XSTEP
  16 736=          BZE   3  XJOBL               [J IF END OF FILE
   8 73KW          LDX   2  1(3) 
  16 745G          TXU   2  XHDDR(1)            [J IF MT RECORD  
   8 74K6          BCC      STEP3
  17 754Q          BZE   2  STEP3               [J IF NONS-TAPE REC  
  20 75JB          SRL   2  12                  [POINTER TO 1ST PERIPHERAL WORD  
  16 7642          ADX   2  3                   [DATUMISE POINTER
   8 76HL          SBN   2  A1   
  17 773=          LDX   4  1(2)                [2ND PERIPHERAL WORD 
   8 77GW          ANDN  4  #777 
   7 782G          TXU   4  6
   8 78G6          BCS      STEP3
  14 78_Q          LDX   1  2(3)                [CH COUNT
   7 79FB          ADN   3  3
  10 79_2          HUNT     2,ADATA,AWDOC
   8 7=DL          LDN   4  A1(2)
  17 7=Y=          MVCH  3  0(1)                [COPY DOCUMENT NAME  
   7 7?=M          LDX   0  1
   9 7?K4          CALL  7  XOUTPRM  
   9 7?XG          LDX   7  XFDOC(1) 
   9 7#C6          BRN      MONOUTX  
  13 7#WQ    #     SLOW  INPUT PER : JOBLIMIT REACHED
  21 7*BB    #     THIS SECTION SEARCHES THE QUEUE FOR ACTS WAITING IN STYLES #6 OR  
  14 7*W2    #     #44 AS THESE HAVE DIFFERENT MESSAGES  
   8 7B*L    XJOBL LDN   7  FLOWQ
   7 7BT=    XLP   LDX   3  7
   9 7C#W          LDX   3  FPTR(3)  
   9 7CSG          TXU   3  XQEND(1) 
   9 7D#6          BCC      XJOBL1   
   5 7DHY    NOTEND  
   7 7DRQ          LDX   7  3
   9 7F?B          SBN   3  CHAINADD 
   7 7HPG          LDN   4  1
  15 7J96          LDEX  0  ATYPE(3)            [CHECK FOR   
  16 7JNQ          SBN   0  IWTDEST             [WAITING STYLE   
  14 7K8B          BZE   0  XDRED               [IWTDEST 
   9 7KN2          LDEX  0  ATYPE(3) 
   7 7L7L          SBN   0  6
   8 7LM=          BZE   0  XYZ  
   8 7M6W          SBN   0  #36  
   8 7MLG          BNZ   0  XLP  
   7 7N66          LDN   4  0
   4 7N=J    XYZ 
  14 7NC2          LDX   0  JSOURCE1(3)         [CHECK   
  13 7NHD          ANDN  0  #777                [FOR 
  15 7NMW          TXU   6  0                   [GEOGRAPHIC  
  14 7NS#          BCS      XLP                 [NUMBER  
   8 7NYQ          BNZ   4  XFILE
   9 7P5B          CALL  7  OUTMESS  
   7 7PK2          +FJOBLIM  
   5 7Q4L    XJOBL1  
   9 7QJ=          LDX   7  XFUSSY(1)
   9 7R3W          BRN      MONOUTX  
   5 7R8#    XDRED   
  14 7R#Q          LDX   0  JSOURCE1(3)         [CHECK   
  13 7RF8          ANDN  0  #777                [FOR 
  15 7RKL          TXU   6  0                   [GEOGRAPHIC  
  14 7RQ4          BCS      XLP                 [NUMBER  
   9 7RWG          CALL  7  OUTMESS  
   6 7S36          +FDRED
   9 7SGQ          BRN      XJOBL1   
  20 7T2B    #     THIS SECTION SEARCHES FILE CHAIN FOR FILE FOR WHICH ACT WAITS.
   5 7TG2    XFILE   
   8 7T_L          FINDLINK  3,3 
  19 7WF=    #     THIS CASE OF WAITING STYLE 6: X5 & X6 HAVE BACK1 & BACK2  
   9 7WYW          LDX   6  ACC5(3)  
  14 7XDG          LDX   7  ACC6(3)      [SEARCH FOR FCB 
   8 7XY6          GFCBC 7,3,ZNOT
  17 83*G          ADN   3  FUSE1               [SET X3 -> USER NAME 
   9 83T6          CALL  4  UPCLOSE  
   9 84#Q    ZNOT  LDX   7  YFILE(1) 
   9 84SB          BRN      MONOUTX  
  13 85#2    #     PERIPHERAL BEING USED BY SYSTEM   
   4 85RL    USSY
  15 86?=          BZE   5  UNMT                [BRN IF TR   
   9 86QW          TXU   5  XCR(1)   
  15 87=G          BCC      UNMT                [BRN IF C.R. 
  15 87Q6          CALL  7  OUTNUM              [O/P GEOG NO 
   9 889Q          LDX   7  XFUSSY(1)
   9 88PB          TXU   5  MTNO(1)  
   9 8992          BCS      MONOUTX  
   7 89DS          LDN   0  0
   9 89NL          BRN      MTINFO   
   4 8=8=    #   
  10 8=MW    #     DEVICE ONLINE TO A JOB
   4 8?7G    #   
   5 8?M6    ONLINE  
   9 8?NC          CALL  7  OUTNUM   
   7 8?PN          LDX   0  5
   7 8?Q_          SBN   0  5
  14 8?S=          BZE   0  XOLA                [J IF MT 
  10 8?TH          LDX   7  XFONLINE(1)  
   8 8?WS          BRN      XOLB 
   4 8?Y5    XOLA
   7 8?_B          LDN   0  5
  16 8#2M          CALL  4  XMODE               [OUTPUT TAPE MODE
   4 8#3Y    XOLB
   7 8#59          MONOUTX  7
   9 8#5P          CALL  7  XHTAWP   
   8 8#69          ADX   3  A1(3)
   8 8#6Q          LJOBNO   3,4  
   7 8*KL          LDX   0  4
  16 8B5=          CALL  7  OUTNUM2             [OUTPUT JOB NO.  
   9 8BJW          CALL  7  XHTAWP   
   8 8C4G          SMO      A1(3)
  10 8CJ6          LDN   3  K54-K50+1(3) 
   9 8D3Q          CALL  4  UPCLOSE  
   5 8DHB    OUTOL   
   9 8F32          CALL  7  XHTAWP   
  10 8FGL          LDX   7  XFOLJOB(1)   
  18 8G2=          ADX   3  A1(3)               [X3 IS ABSOLUTE POINTER  
   9 8GFW          JFAIL    3,PHAIL  
   9 8G_G          BRN      NOTFAIL  
   5 8HF6    PHAIL   
   7 8HYQ          MONOUTX  7
   9 8JDB          LDX   7  XFFAIL(1)
   5 8JY2    NOTFAIL 
   9 8KCL          LDX   5  AWORK2(2)
   9 8KX=          TXU   5  MTNO(1)  
  15 8LBW          BCS      MONOUTX             [J IF NOT MT 
   9 8LLN          BRN      MTINFX   
   4 8LWG    #   
  19 8MB6    #     ON ENTRY TO THIS SECTION X7 CONTAINS MESSAGE FOR OUTPUT   
   4 8MTQ    #   
   5 8N*B    MTINFO  
   8 8NGJ          CALL  4  XMODE
   5 8NMQ    MTINFX  
  17 8NT2          MONOUTX  7                   [OUTPUT MESSAGE IN X7
   9 8P#L          CALL  7  XHTAWP   
   8 8PS=          ADX   3  A1(3)
  14 8Q?W          LDX   6  K54-K50+7(3)        [GET TSN 
  10 8QRG          LDX   5  K54-K50+8(3) 
   7 8R?6          ANDN  5  1
   8 8RQQ          BZE   5  WTEST
  18 8S=B          NGN   5  1                   [SET X5=-1 IF NONS TAPE  
   9 8SQ2    WTEST JWPR     3,WPR1   
  17 8T9L          LDN   4  1                   [SET X4=1 IF NO WPR  
   8 8TP=          BRN      TNONS
  16 8W8W    WPR1  STOZ     4                   [SET X4=0 IF WPR 
  16 8WNG    TNONS BPZ   5  NOTNS               [J IF STANDARD   
   9 8X86          CALL  7  MONOUT   
  21 8XMQ          +FNONS                       [OUTPUT MESSAGE-NO TSN FOR NON STAN  
   8 8Y7B          BRN      STEPX
  20 8YM2    NOTNS BZE   6  NFD                 [J IF NO STN=INTERMEDIATE STATE  
   8 8Y_C          LDN   0  12   
   9 8_?S          CALL  7  XOUTBLK  
  21 8_L=          LDX   5  6                   [THIS WILL UNSET BIT 0 AS XENO TAPE  
  16 925W          ANDX  6  XBIT(1)             [MARKER IS THIS  
  15 92KG          CALL  7  OUTOCT              [CONVERT TSN 
   7 9356          LDX   6  5
   7 93JQ          LDN   5  0
  14 944B          BZE   4  WPR                 [J IF WPR
   7 94J2          OUTNULL   
   8 953L          BRN      NOWPR
   9 95H=    WPR   CALL  7  OUTMESS  
  18 962W          +FWPR                        [OUTPUT WPR INFORMATION  
   5 96*?    NOWPR   
   9 96MN          CALL  7  XHTAWP   
   8 9726          ADX   3  A1(3)
   8 97FQ          JONL     3,ONL
   7 97_B          STOZ     4
   8 98F2          BRN      NONL 
   7 98YL    ONL   LDN   4  1
   4 99D=    NONL
  10 99XW          LDX   7  K54-K50+8(3) 
   7 9=CG          ANDN  7  2
  16 9=X6          BNZ   7  UWT                 [J IF WORKTAPE   
   4 9?BQ    #   
  16 9?WB    #     THIS SECTION INVOLVES A SEARCH THRO :SYS.SER  
  15 9#B2    #     IN CASE THE TAPE SHOULD BE A USED WORKTAPE
   4 9#TL    #   
   9 9**=    SERCH STOZ     AWORK4(2)
   9 9*SW          LDX   3  AWORK1(2)
   7 9B#G          SBN   3  2
   8 9BS6          CALL  7  XSTEP
  16 9C?Q          BZE   3  NOTWT               [J IF END OF FILE
   8 9CRB          TXU   6  1(3) 
  17 9D?2          BCS      SERCH               [J IF NO TSN MATCH   
   8 9DQL          LDX   7  5(3) 
   7 9F==          SLL   7  1
  17 9FPW          BPZ   7  NOTWT               [J IF NOT WORKTAPE   
   7 9G9G          LDN   7  1
  17 9GP6          STO   7  AWORK4(2)           [SET WORKTAPE MARKER 
   8 9H8Q          LDX   7  2(3) 
  17 9HNB          BZE   7  UWT                 [J IF UNUSED WORKTAPE
  18 9J82          BNZ   4  UWT                 [J IF O/L TO A PROGRAM   
   4 9JML    #   
  17 9K7=    #     FOR A WORKTAPE IN USE BY BUT NOT O/L TO A PROGRAM 
  20 9KLW    #     THIS ROUTINE TRUNCATES TRAILING SPACES FROM USER NAME FOR O/P 
   4 9L6G    #   
   7 9LL6          LDX   4  3
   7 9M5Q          SLC   3  2
   8 9MKB          ADN   3  11   
  21 9N52          SRC   3  2                   [X3-> END OF USERNAME IN :SYS.SER REC
   8 9NJL    RND   LDCH  0  2(3) 
   8 9P4=          SBN   0  #20  
   8 9PHW          BNZ   0  OUT  
   7 9Q3G          SLC   3  2
   7 9QH6          SBN   3  1
   7 9R2Q          SRC   3  2
   8 9RGB          BRN      RND  
   7 9S22    OUT   SLC   3  2
   7 9SFL          SLC   4  2
   7 9S_=          SBX   3  4
  20 9TDW          ADN   3  1                   [X3 CONTAINS NO OF CHARS FOR O/P 
   7 9TYG          LDX   7  3
   7 9WD6          SRC   4  2
  19 9WXQ          LDX   3  4                   [RESTORE :SYS.SER REC POINTER
  19 9XCB          MHUNTW   2,ADATA,AWDOC       [FIND /AWDOC BLK FOR USERNAME
   8 9XX2          LDN   4  2(3) 
   8 9YBL          LDN   5  A1(2)
   7 9YW=          SMO      7
  21 9_*W          MVCH  4  0                   [MOVE USER NAME INTO STORAGE BLOCK   
   7 9_N?          LDX   0  7
   9 =22N          CALL  7  XOUTPRM  
   9 =2*6          CALL  7  MONOUT   
   7 =2SQ          +FUSEWTXX 
  17 =3#B          LDN   5  1                   [SET STAN TAPE MARBER
   8 =3S2          BRN      STEPX
   9 =4?L    UWT   CALL  7  OUTMESS  
   7 =4R=          +FWTAPE   
  17 =5=W          LDN   5  1                   [SET STAN TAPE MARKER
  17 =5QG          STO   5  AWORK4(2)           [SET WORKTAPE MARKER 
   8 =6=6          BRN      STEP1
   5 =6PQ    NOTWT   
  17 =79B          BPZ   6  NOTX                [J IF NOT A XENOTAPE 
   9 =7P2          CALL  7  OUTMESS  
   6 =88L          +FXEN 
  16 =8N=          LDN   5  1                   [SET XEN MARKER  
   8 =97W          BRN      STEP1
   4 =9G?    NOTX
   9 =9SN          CALL  7  XHTAWP   
   8 ==76          ADX   3  A1(3)
  10 ==LQ          LDX   3  K54-K50+8(3) 
   7 =?6B          ANDN  3  4
  17 =?L2          BZE   3  STEP1               [J IF NOT POOL TAPE  
  15 =#5L          CALL  7  OUTMESS             [OUTPUT MESS 
   7 =#K=          +FPTAPE   
   5 =*4W    STEP1   
  16 =*JG          CALL  7  MONOUT              [OUTPUT TSN ETC  
   6 =B46          +FTNO 
   9 =BHQ    STEPX LDX   3  AWORK1(2)
   8 =C3B          BZE   3  UPD  
   7 =CH2          SBN   3  1
   8 =D2L          CALL  7  XSTEP
  16 =DG=          BZE   3  NFD                 [J IF END OF FILE
   8 =D_W          LDX   0  1(3) 
  16 =FFG          BZE   0  NONST               [J IF NONS TAPE  
   9 =F_6          TXU   0  XHDDR(1) 
  17 =GDQ          BCS      STEPX               [J IF NOT MT RECORD  
   8 =GYB    NONST LDX   0  23(3)
   8 =HD2          ANDN  0  #777 
   9 =HXL          TXU   0  AWORK3(2)
   8 =JC=          BCS      STEPX
   8 =JWW          LDX   7  21(3)
  15 =KBG          STO   7  AWORK3(2)           [SAVE WORD 21
  10 =KW6         MHUNTW 2,ADATA,AWDOC   
   8 =L*Q          LDN   4  A1(2)
   7 =LTB          ADN   3  3
  19 =M*2          MOVE  3  6                   [MOVE IN NAME AND REEL INFO  
  17 =MSL          BPZ   5  STAN                [J IF NOT NONSTANDARD
  13 =N#=    NSTAN OUTMOND  12,A1,XCAT(1),ADATA,AWDOC
  16 =NRW          BZE   6  UPD                 [J IF NONS TAPE  
  16 =P?G          BNG   6  UPD                 [J IF XENOTAPE   
  16 =PR6          BRN      TSTIN               [OUTPUT TAPE NAME
   4 =Q=Q    STAN
  10 =QQB          MHUNTW   2,ADATA,AWDOC
  13 =R=2          LDX   7  A1+3(2)             [REEL
  13 =RPL          LDX   4  A1+4(2)             [FGN 
  15 =S9=          LDX   3  A1+5(2)             [RET PERIOD  
   7 =SNW          LDX   0  7
   8 =T8G          ANDN  3  #7777
   7 =TN6          ADX   0  3
   8 =W7Q          ANDN  4  #7777
   7 =WMB          ADX   0  4
  16 =X72          BZE   0  NSTAN               [J IF ALL ZERO   
   7 =XCX          LDX   6  7
   8 =XNS          LDN   0  10   
   9 =X_P          CALL  7  XOUTBLK  
   8 =Y=L          LDN   0  12   
   9 =YHH          CALL  7  XOUTPRM  
   7 =YSD          LDX   0  6
   9 =_5G          CALL  7  OUTNUM2  
   7 =_K6          LDX   0  4
   9 ?24Q          CALL  7  OUTNUM2  
   7 ?2JB          LDX   0  3
   9 ?342          CALL  7  OUTNUM2  
   9 ?3HL          CALL  7  MONOUT   
  15 ?43=          +FMTI                        [OUTPUT MESS 
   8 ?4GW    TSTIN LDCT  7  #200 
   9 ?52G          ANDX  7  AWORK3(2)
  17 ?5G6          BNZ   7  TINS                [J IF INSECURE TAPE  
   9 ?5_Q    NOXEN LDX   7  AWORK4(2)
  16 ?6FB          BNZ   7  PROP                [J IF WORKTAPE   
   9 ?6_2          LDX   3  AWORK1(2)
   7 ?7DL          SBN   3  2
   8 ?7Y=          STEPAGAIN 0(3)
   8 ?8CW          BZE   3  NFD  
  19 ?8XG          LDX   4  5(3)                [SET UP X4 IN CASE NOT OWNED 
   8 ?9C6          LDX   0  2(3) 
  16 ?9WQ          BZE   0  XDEAD               [J IF NOT OWNED  
  10 ?=BB          MHUNTW   2,ADATA,AWDOC
   8 ?=W2          LDN   4  A1(2)
   7 ??*L          ADN   3  2
   7 ??T=          MOVE  3  4
  16 ?##W          LDX   4  3(3)                [WORD 5 OF RECORD
   8 ?#M?          LDN   0  12   
   9 ?#_N          CALL  7  XOUTPRM  
  18 ?*#6          BPZ   4  NULL                [J IF NOT TO BE RETURNED 
   9 ?*RQ          CALL  7  OUTMESS  
   7 ?B?B          +FGOING   
   9 ?BR2    NULL  CALL  7  MONOUT   
   7 ?C=L          +FLTAPE   
  17 ?CQ=          ANDN  4  #7777               [CT OF USERS WAITING 
  19 ?D9W          BZE   4  XDEAD               [J IF NO ONE USING OR WAITING
   7 ?DPG          SBN   4  1
  18 ?F96          BZE   4  XDEAD               [J IF NO USERS WAITING   
   7 ?FNQ          LDX   0  4
   9 ?G8B          CALL  7  OUTNUM2  
   9 ?GN2          CALL  7  MONOUT   
   6 ?H7L          +FNUS 
   8 ?HM=    XDEAD LDCT  0  #100 
   7 ?J6W          ANDX  0  4
  18 ?JLG          BZE   0  XLIVE               [J IF NOT TO BE DEADED   
   9 ?K66          CALL  7  MONOUT   
   6 ?KKQ          +FDEAD
   8 ?L5B    XLIVE LDCT  0  #20  
   7 ?LK2          ANDX  0  4
  18 ?M4L          BZE   0  XOP                 [J IF NOT WRONGED BY USER
   9 ?MJ=          CALL  7  OUTMESS  
   6 ?N3W          +JCOM 
   8 ?NHG          BRN      RIGHT
   8 ?P36    XOP   LDCT  0  #10  
   7 ?PGQ          ANDX  0  4
  18 ?Q2B          BZE   0  PROP                [J IF NOT WRONGED BY OPS 
   9 ?QG2          CALL  7  OUTMESS  
   7 ?Q_L          +JOPCON   
  10 ?RF=    RIGHT LDX   7  XFTWRONG(1)  
   9 ?RYW          BRN      MONOUTX  
   9 ?SDG    TINS  LDX   7  XFTINS(1)
   5 ?SY6    MONOUTX 
   7 ?TCQ          MONOUTX  7
  17 ?TXB    #     THIS SECTION ANALYSES AND O/P INFOR ON PROPERTIES 
  14 ?WC2    PROP  LDN   6  1                   [MARKER  
   9 ?WWL          LDN   7  ATTMAX   
  10 ?XB=    ROUN  MHUNTW   3,ADATA,AWPER
  18 ?XTW          ADX   3  A1(3)               [X3->RECORD BEING PROC   
   7 ?Y*G    ROUND SMO      7
  10 ?YT6          LDX   4  K54-K50+8(3) 
  18 ?_#Q          BNZ   4  PHOUND              [FOUND A PROP ATTRIBUTED 
   8 ?_SB          BCT   7  ROUND
   8 #2#2          BRN      UPD  
   5 #2RL    PHOUND  
  18 #3?=          BZE   6  OPDON               [J IF NOT 1ST PROP FOUND 
   9 #3QW          MONOUT   HPROPS   
   5 #3Y4    OPDON   
   6 #45=    #UNS HWATIDY
   8 #4=D          APROPNAME 4,0 
   6 #4CL    #UNS HWATIDY
   4 #4JS    #SKI
  17 #4Q6          PROPNAME 4                   [GET PROPERTY NAME   
  18 #59Q          OUTBLOCN 8                   [SET UP O/P PARAM BLOCK  
   4 #5PB    #   
  11 #692    #     ACOMMUNE4 IS SET THUS:-   
   9 #6NL    #     B0=1     => EXCL  
   9 #78=    #     B1=1     => PERM  
  10 #7MW          OUTPARN  12,ACOMMUNE1 
  10 #8M6          LDX   6  XINCL+1(1)   
  10 #96Q          LDXC  4  ACOMMUNE4(2) 
  16 #9LB          BCS      PERM                [J IF EXCLUSIVE  
   9 #=62          LDX   6  XINCL(1) 
  10 #=KL    PERM  STO   6  ACOMMUNE2(2) 
   9 #?5=          LDX   6  TEMP+1(1)
  18 #?JW          ADXC  4  4                   [THIS SETS C IF B1 IS SET
  15 ##4G          BCS      NOTTP               [J IF PERM   
   9 ##J6          LDX   6  TEMP(1)  
  10 #*3Q    NOTTP STO   6  ACOMMUNE1(2) 
  10 #*HB          OUTPARN  4,ACOMMUNE1  
  10 #B32          OUTPARN  4,ACOMMUNE2  
   9 #BGL          MONOUT   HPROPS1  
   7 #C2=          LDN   6  0
   8 #CFW          BCT   7  ROUN 
   8 #C_G          BRN      UPD  
   8 #DF6    NOUGH LDX   2  FX2  
  16 #DYQ          LDX   3  AWORK1(2)           [NO OF FILES OPEN
   8 #FDB          BZE   3  RONG1
  16 #FY2    WC1   CLOSE                        [CLOSE ALL FILES 
   8 #GCL    WC2   BCT   3  WC1  
   5 #GH5    RONG1   
  17 #H?B          OFF      AWHATBIT            [GIVE THE ALL CLEAR  
   6 #H#= ...#SKI G203&1 
   9 #H*6 ...      ACROSS   WHATPER,7
   7 #HB2 ...#SKI G203&1$1   
   8 #HBW          FON      #114 
   5 #HSL    TOENDCOM
   6 #HWG          ENDCOM
   9 #JB6    NFD   LDX   7  XFNFD(1) 
   9 #JTQ          BRN      MONOUTX  
  17 #K*B    #     SUBROUTINES TO OUTPUT MESS IN LOCN FOLLOWING CALL 
   5 #KT2    MONOUT  
   7 #L#L          LDX   1  7
   8 #LS=          SBX   7  FX1  
   8 #M?W          MONOUTX  0(1) 
   8 #MRG          ADX   7  FX1  
   7 #N?6          EXIT  7  1
   5 *4FQ    OUTMESS 
   7 *4_B          LDX   1  7
   8 *5F2          SBX   7  FX1  
   8 *5YL          OUTMESSX 0(1) 
   8 *6D=          ADX   7  FX1  
   7 *6XW          EXIT  7  1
  16 *7CG    #           SUBROUTINE TO OUTPUT DECIMAL NO IN X0   
   5 *7X6    OUTNUM  
  15 *8BQ          LGEOG    3,0                 [GET OPS NO  
   5 *8WB    OUTNUM2 
   8 *9B2          SBX   7  FX1  
   8 *9TL          OUTNUM   0,0  
   8 *=*=          ADX   7  FX1  
   7 *=SW          EXIT  7  0
   4 *?#G    #   
  16 *?S6    #           SUBROUTINE TO OUTPUT AN OCTAL NO. IN X6 
   4 *#?Q    #   
   5 *#RB    OUTOCT  
   8 **?2          SBX   7  FX1  
  18 **QL          CONSERNO 6                   [CONVERT SERIAL NUMBER   
  10 *B==          MHUNTW   2,FLIB,FTSN  
  12 *BPW          OUTPARAM A1(2),A1+1,FLIB,FTSN 
   8 *C9G          ADX   7  FX1  
   7 *CP6          EXIT  7  0
   4 *CPF    #   
   4 *CPS    #   
  13 *CQ7    # SUBROUTINE TO OUTPUT RECORDING MODE   
   4 *CQG    #   
   5 *CQT    XMODE   
  15 *CR8          STO   0  AWORK4(2)           [SAVE MESSAGE
   8 *CRH          SBX   4  FX1  
   9 *CRL          CALL  7  XHTAWP   
   8 *CRP          ADX   3  A1(3)
   8 *CRW          LPROP    3,0  
   7 *CS9          SRC   0  8
  15 *CSJ          BNG   0  XMD9                [J IF 9TRACK 
   8 *CSX          LMODEQ   3,0  
   9 *CT=          CALL  7  OUTNUM2  
   9 *CTK          BRN      XMDEND   
   4 *CTY    XMD9
   7 *CW?          SLC   0  5
  17 *CWL          BPZ   0  XMDENDA             [J IF NOT SWITCHABLE 
   7 *CW_          SLL   0  1
   8 *CX#          BNG   0  XMD1 
   9 *CXM          CALL  7  OUTMESS  
   6 *CY2          +FNRZI
   9 *CY*          BRN      XMDEND   
   4 *CYN    XMD1
   7 *C_3          SLL   0  1
   8 *C_B          BNG   0  XMD2 
  15 *C_P          CALL  7  OUTMESS             [JMODE IS PE 
   6 *D24          +FPE  
   9 *D2C          BRN      XMDEND   
   4 *D2Q    XMD2
  15 *D35          CALL  7  OUTMESS             [MODE IS GCR 
   6 *D3D          +FGCR 
   5 *D3R    XMDEND  
   9 *D46          LDX   0  AWORK4(2)
   7 *D4F          SMO      0
   9 *D4S          LDX   7  XMES2(1) 
   9 *D57          BRN      XMDENDB  
   5 *D5G    XMDENDA 
   9 *D5T          LDX   0  AWORK4(2)
   7 *D68          SMO      0
   9 *D6H          LDX   7  XMES1(1) 
   5 *D6W    XMDENDB 
   8 *D79          ADX   4  FX1  
   7 *D7J          EXIT  4  0
   4 *D7X    #   
   4 *D8=    #   
  11 *D8Q          MENDAREA 50,K99WHATPERA   
   4 *DNB    #END
   8 ____ ...05453342000100000000
