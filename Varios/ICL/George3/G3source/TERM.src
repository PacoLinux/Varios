  15 22FL    #SEG  TERM                         [LEN WAGSTAFF
  16 22PD ...# (C) COPYRIGHT INTERNATIONAL COMPUTERS LTD 1983.   
   8 22_=    #OPT  K6TERM=K6PERI 
   7 23DW    #OPT  K0TERM=0  
  13 23YG    #LIS  K0TERM>K0PERI>K0COMMAND>K0ALLGEO  
   9 24D6                   8HTERM   
   4 24XQ    #   
  20 25CB    #              THIS COMMAND TERMINATES THE ACTIVITY WAITING FOR A   
  13 25X2    #              PERIPHERAL TO BE ENGAGED 
  18 26BL    #              IF ONLINE TO AN OBJ PROG A CANTDO IS ISSUED  
  18 26W=    #              OTHERWISE THE WAIT IS TERMINATED USING TERMON
   4 27*W    #   
   4 27TG    #   
   4 28*6    #   
  10 28SQ          SEGENTRY K1TERM,X1TERM
   4 29#B    #   
   4 29S2    #   
  11 2=?L    #              ERROR MESSAGES   
  18 2=R=    XJRES          +JRES               [ NOT IN REQUIRED STATE  
  19 2?=W    XJMULT         +JMULT              [ NOT A LEGITIMATE PERIPHERAL
  17 2?QG    XINODEV        +JNOPER             [NO SUCH PERIPHERAL  
  16 2#2#    XJERAT         +JERAT              [DEVICE ATTACHED 
   8 2#=6    THOUSAND       +1000
   4 2#PQ    #   
   5 2*9B    THUNT   
  15 2*P2    #     SUBROUTINE HUNTS FOR CPAR,JSCE BLOCK ON X2
   7 2B8L    #     LINK IN X6
  10 2BN=          HUNTW    2,CPAR,JSCE  
   8 2C7W          BRN      (7)  
   4 2CMG    #   
   5 2D76    X1TERM  
   4 2DLQ    #   
  17 2F6B          PARSORT  XBREAKIN,ALL        [ANALYSE PARAMETERS  
   5 2FL2    TREPT   
  15 2G5L          LDN   6  0                   [NOT ONLINE  
  18 2GK=          CALL  7  THUNT               [LOCATE PARAMETER BCOCK  
  18 2H4W          BNG   2  NOPARS              [GIVE UP - NOTHING TO DO 
  17 2HJG          LDCH  0  JSCEA(2)            [ACCESS DEVICE TYPE  
   7 2J46          SBN   0  5
  19 2JHQ          BPZ   0  TWTYP               [ERROR IF NOT SLOW PERIPHERAL
  17 2K3B          LDX   3  JSCEP(2)            [DEVICE LIST POINTER 
  13 2KH2          SAWCEJX  JSCEB(2),T1,REMOTE,REMOTE
   4 2L2L    #   
   4 2LG=    T1  
  17 2L_W          JFREE    3,TWST              [J. IF FREE TO TWST. 
  16 2MFG          JENG     3,TWST              [J. IF ENGAGED.  
   8 2M_6          LGEOG    3,7  
  15 2NDQ          JONL     3,ONLINE            [BR IF ONLINE
   8 2NYB          BRN      TOFL 
   5 2PD2    ONLINE  
  20 2PXL          LJOBNO   3,6                 [X6 = JOBNO IF ONLINE, ELSE ZERO 
   4 2QC=    TOFL
  17 2QWW    #              SEARCH ACTIVITIES FOR THE ONE REQUIRED   
   8 2RBG          LDX   1  FX2  
   5 2RW6    TNXTAC  
  10 2S*Q          LDX   1  ACTRING(1)   
   9 2STB          SBN   1  ACTRING  
  20 2T*2          BXE   1  FX2,TWST            [ERROR IF BACK TO START OF SEARCH
   9 2TSL          LDCH  0  ATYPE(1) 
   9 2W#=          SBN   0  ACTLF/64 
  15 2WRW          BZE   0  TRY                 [BR IF ACTLF 
  10 2X?G          SBN   0  CPAT-ACTLF/64
  16 2XR6          BNZ   0  TNXTAC              [BR IF NOT CPA   
   4 2Y=Q    TRY 
  17 2YQB          BNZ   6  ONLINT              [BR IF DEVICE ONLINE 
  20 2_=2          TXU   3  CPPTR(1)            [BR IF THIS ACTIVITY DOES NOT OWN
  18 2_PL          BCS      TNXTAC              [THE REQUIRED PERIPHERAL 
   8 329=          BRN      TSTWT
   5 32NW    ONLINT  
  16 338G          TXU   6  JOBNO(1)            [COMPARE JOBNO'S 
  17 33N6          BCS      TNXTAC              [& BR IF WRONG ONE   
   4 347Q    XLOC
  12 34MB    #              ACT FOUND,ADDS IN X1 
   8 3572          BRN      TSTWT
   4 35LL    #   
  20 366=    REMOTE                             [REMOTE DEVICE TO BE TERMINATED  
  14 36KW          LDEX  0  JSCEB(2)            [J. IF CI
   8 375G          BZE   0  TCI  
   9 37K6          JFREE    3,TWST   
   9 384Q          JONL     3,TWST   
  20 38JB          LAMOP    3,3                 [LOAD ADDRESS OF RELEVANT AMOP   
  13 3942          SAWCEJX  JSCEB(2),TREPT,X7900,PCT 
  18 39HL    #                                   BR TO PCT IF PCT DEVICE 
  16 3=3=    TCI   ANDX  3  BITS22LS            [PROG. PROP. NO. 
  15 3=GW          ORX   3  GSIGN               [PLUS BIT 0  
  17 3?2G          FMOPG    1,3,IPB             [FIND RELEVANT AMOP  
  17 3?G6          BNG   1  TWST                [ERROR IF NOT THERE  
  18 3?_Q          LDX   3  1                   [CONTINUE DOWN IPB PATH  
   4 3#FB    #   
  15 3#_2    X7900                              [7900 DEVICE 
  21 3*DL    TP3                                [SEARCH FOR ACT FOR IDE TO BE TERMD  
   9 3*Y=          LDX   3  FPTR(3)  
   9 3BCW          LDX   0  ATYPE(3) 
  18 3BXG          BXGE  0  CACT,TWST           [ERROR IF NO ADEVS BLOCK 
   8 3CC6          SRL   0  12   
  10 3CWQ          SBN   0  AMXOR+ADEVS  
  18 3DBB          BNZ   0  TP3                 [BR IF ADEVS NOT FOUND   
   9 3DW2          LDX   0  JSCEA(2) 
  19 3F*L          ANDN  0  #7777               [ISOLATE IDENTIFIER NUMBER   
  11 3FT=          SBX   0  A1+FOURTHWD(3)   
   7 3G#W          SLC   0  2
  20 3GSG          ADX   3  0                   [POINT TO RING ENTRY FOR THIS IDE
  11 3H#6          LDX   0  A1+FOURTHWD(3)   
  19 3HRQ          BPZ   0  TWST                [ERROR IF IDE NOT UNAVAILABLE
   8 3HWW          LDN   4  #2001
   8 3J22          SLL   4  12   
   7 3J56          ANDX  0  4
  17 3J8=          BNZ   0  TAT                 [ERROR IF ATTACHED   
  10 3J?B          LDX   1  A1+BPTR(3)   
  17 3JR2          BZE   1  TWST                [ERROR IF NOT RINGED 
  19 3K=L          SBN   1  A1+FPTR             [ELSE POINT TO START OF BLOCK
  18 3KQ=    TP4                                [& SEARCH BACK FOR ACT   
   9 3L9W          LDX   1  BPTR(1)  
   9 3LPG          LDX   0  ATYPE(1) 
   9 3M96          ANDX  0  HALFTOP  
   9 3MNQ          BXL   0  CACT,TP4 
  18 3N8B          LDN   4  0                   [PERF WILL LOOK OFFLINE  
   8 3NN2          SRL   0  18   
   9 3P7L          SBN   0  ACTLF/64 
  15 3PM=          BZE   0  TSTWTA              [BR IF ACTLF 
  10 3Q6W          SBN   0  CPAT-ACTLF/64
  16 3QLG          BNZ   0  TWTYP               [BR IF NOT CPAT  
  18 3R66          BRN      TSTWTA              [BR TO TERMINATE ACTIVITY
   4 3RKQ    #   
  17 3S5B    T7020                              [SEARCH FOR 7020 ACT 
  19 3SK2    PCT                                [PCT DEVICE TO BE TERMINATED 
   9 3SQ8          LDX   0  JSCEA(2) 
  17 3SXB          BZE   0  TWST                [ERROR IF A MOP LINE 
   5 3T4L    THNTD   
   9 3TJ=          LDX   3  FPTR(3)  
   9 3W3W          LDX   0  ATYPE(3) 
  17 3WHG          BXGE  0  CACT,TWST           [ERROR IF NO ALINES  
   8 3X36          SRL   0  12   
  10 3XGQ          SBN   0  AMXOR+ALINES 
   8 3Y2B          BNZ   0  THNTD
  14 3YG2    #                  ADEVS FOUND,NOW FIND LCB 
  18 3Y_L          LDX   0  JSCEB(2)            [TO ISOLATE LINE NUMBER  
   7 3_F=          SRL   0  7
  17 3_YW          ANDN  0  #374                [X0 = LINE NUMBER X 4
   7 42*6          ADX   3  0
  10 42PC          LDX   1  A1+BPTR(3)   
  18 432#          BZE   1  TWST                [WRONG STATE IF NO LCB   
  11 43?9          LDX   0  A1+FOURTHWD(3)   
   7 43J6          SLL   0  1
   8 43T3          BNG   0  TAT  
   9 445Y          SBN   1  A1+FPTR  
   7 44=F          LDX   3  1
  18 44C2    #                  LCB FOUND,NOW HUNT BACK FOR A7020/INFORM 
   5 44WL    THNTE   
   9 45B=          LDX   3  BPTR(3)  
   9 45TW          LDX   0  ATYPE(3) 
   8 46*G          SRL   0  12   
  10 46T6          SBN   0  A7020+INFORM 
   8 47#Q          BNZ   0  THNTE
  20 47SB          LDX   1  3                   [INFORM BLOCK,PRESERVE ADDRESS   
   9 48#2          LDX   7  JSCEA(2) 
   7 48RL          SRL   7  6
  19 49?=          ANDN  7  #7777               [ISOLATE TERMINAL IDENTIFIER 
  18 49QW          LDX   5  APCTNUM(3)          [NO OF PCT'S ON THIS LINE
   7 4==G          LDN   4  0
   4 4=Q6    NEXT
   7 4?9Q          SMO      4
  19 4?PB          LDX   0  IDUNIQUE(3)         [LOCATE ENTRY FOR THIS PCT   
   8 4#92          SRL   0  12   
   7 4#NL          SBX   0  7
   9 4*8=          BZE   0  THISPCT  
  17 4*MW          ADN   4  APCTRLEN            [ADD RECORD LENGTH   
  17 4B7G          BCT   5  NEXT                [BR TO TRY NEXT ONE  
  20 4BM6          BRN      TNDV                [ERROR IF TERMINAL DISAPPEARED!  
   5 4C6Q    THISPCT 
   9 4CLB          LDX   7  JSCEA(2) 
  18 4D62          ANDN  7  #77                 [ISOLATE INTERFACE NUMBER
   7 4DKL          LDN   0  6
  19 4DTD          ADX   3  4                   [ELIMATE NECESSITY FOR SMO 4 
   5 4F5=    TBCKB   
  10 4FJW          LDX   5  APCTDEV(3)   
   8 4G4G          LDCT  2  #200 
   7 4GJ6          SMO      3
  20 4H3Q          LDCH  6  APCTDEV+1(2)        [GET INTERFACE NO FOR THIS ENTRY 
  20 4HHB          BXE   6  7,TGOTIT            [BR IF SAME AS INT TO BE TERMD   
   7 4J32          ADN   3  2
   8 4JGL          BCT   0  TBCKB
   8 4K2=          BRN      TNDV 
   5 4KFW    TGOTIT  
  16 4K_G          LDCT  2  #400                [LOOK AT CHAR2   
   7 4LF6          SMO      3
  10 4LYQ          LDCH  6  APCTDEV+1(2) 
  20 4MDB          BZE   6  TWST                [WRONG STATE IF NO PLEASE ENGAGE 
  17 4MY2    #                                  [MESSAGE OUTSTANDING 
  16 4NCL          LDCT  2  #200                [LOOK AT CHAR1   
   7 4NX=          SMO      3
  10 4PBW          LDCH  0  APCTDEV(2)   
  18 4PWG          SLL   0  12                  [INT BIT POSN TO CHAR 1  
  19 4QB6          ADX   4  0                   [X4 NOW LOOKS LIKE [APCTID]  
  15 4QTQ    #                                  [DATA BLOCK  
  17 4R*B    #              NOW SEARCH 7020 RING FOR RELEVANT BLOCK. 
   9 4RK8          LDN   6  FPTPCT(1)
   4 4RT2    TP1 
   9 4S#L          LDX   1  FPTPCT(1)
  18 4SJD          BXE   6  1,TWST              [BR IF BLOCK NOT FOUND   
   9 4SS=          SBN   1  FPTPCT   
   9 4T?W          LDX   0  APCTID(1)
   8 4TRG          BXU   0  4,TP1
  16 4W?6    #              WHEN BLOCK FOUND SEARCH BACK FOR ACT 
   4 4WQQ    TP2 
   9 4X=B          LDX   1  BPTR(1)  
   9 4XQ2          LDX   0  ATYPE(1) 
   9 4Y9L          BXL   0  CACT,TP2 
  18 4YP=          LDN   4  0                   [PERF WILL LOOK OFFLINE  
  14 4_8W    #              CHECK ACT IS CPAT OR ACTLF   
   8 4_NG          SRL   0  18   
   9 5286          SBN   0  ACTLF/64 
  15 52MQ          BZE   0  TSTWTB              [BR IF ACTLF 
  14 537B          SBN   0  CPAT-ACTLF/64       [OR CPAT 
   8 53M2          BNZ   0  TWTYP
   9 546L          BRN      TSTWTB   
   4 54L=    #   
  19 555W    TSTWT                              [NOW LOOK AT WAITING STYLE   
  16 55KG    #              ADDS ACT IN X1,X4 -VE IF PERF ONLINE 
  17 5656          JONL     3,TONLN             [BR IF DEVICE ONLINE 
   5 56JQ    TSTWTA  
   5 574B    TSTWTB  
  18 57J2          LDEX  0  CLONG1(1)           [LOOK AT WAITING STYLE   
   8 583L          SBN   0  #36  
  19 58H=          BZE   0  X36                 [BR IF #36 (PLEASE LINE UP)  
   8 592W          SBN   0  #41  
  20 59GG          BNZ   0  TWST                [BR IF NOT #77 (PLEASE ENGAGE)   
   4 5=26    X36 
  20 5=FQ          LDEX  0  ATYPE(1)            [FIND CURRENT STATE OF ACTIVITY  
   8 5=_B          SBN   0  #36  
  16 5?F2          BZE   0  TMON36              [TERMINATE IF #36
  14 5?YL          BPZ   0  TMON77              [OR #77  
   4 5#D=    WAIT
  20 5#XW          COOR3    #41                 [OTHERWISE WAIT K9 MILLISECONDS  
   8 5*CG          BRN      TREPT
   5 5*X6    TONLN   
   7 5BBQ          LDX   2  1
   9 5BWB          LDEX  0  ATYPE(2) 
   8 5CB2          SBN   0  #77  
   8 5CTL          BNZ   0  TWST 
  18 5D*=          WAITSTOP WAIT                [CANTDO THE PLEASE ENGAGE
   8 5DSW          BRN      TENDA
   4 5F#G    #   
   5 5FS6    TMON36  
   8 5G?Q          TERMON   1,#36
   8 5GRB          BRN      TENDA
   5 5H?2    TMON77  
  17 5HQL          TERMON   1,#77               [TERMINATE THE WAIT  
   5 5J==    TENDA   
  18 5JPW          CALL  7  THUNT               [RELOCATE CPAR,JSCE BLOCK
  16 5K9G          GEOPACK  5,JSCEA(2)          [PACK GEOPER PDC 
   7 5KP6          FREECORE 2
  16 5L8Q          OUTPACK  5,1,GEOPER          [OUTPUT PARAMETER
  19 5LNB          INFORM   0,JTERMINATE,1      [OUTPUTS TERMINATED MESSAGE  
   5 5M82    TENDC   
  18 5MML          BRN      TREPT               [BR TO DO NEXT PARAMETER 
   5 5N7=    NOPARS  
   6 5NLW          ENDCOM
   4 5P6G    #   
   4 5PL6    #   
  10 5Q5Q    #              ERROR REPLIES
   9 5Q=Y    TAT   LDN   1  XJERAT   
   9 5QD6          BRN      TERROR   
   9 5QKB    TWTYP LDN   1  XJMULT   
   9 5R52          BRN      TERROR   
   8 5RJL    TWST  LDN   1  XJRES
   9 5S4=          BRN      TERROR   
   9 5SHW    TNDV  LDN   1  XINODEV  
   5 5T3G    TERROR  
   8 5TH6          ADX   1  FX1  
   8 5W2Q          LDX   6  0(1) 
  18 5WGB          CALL  7  THUNT               [LOCATE CPAR,JSCE BLOCK  
  15 5X22          LDX   4  JSCEQ(2)            [PARAMETER NO
  17 5XFL          FREECORE 2                   [FREE CPAR,JSCE BLOCK
   8 5X_=          SRL   4  12   
  16 5YDW          LDN   5  0                   [ANALYSIS LEVEL 0
  17 5YYG          SPARANOX 4,5                 [REACCESS PARAMETER  
  17 5_D6          ERRORX   6                   [OUTPUT ERROR MESSAGE
  17 5_XQ          UNIFREE                      [FREE CPB,CUNI BLOCK 
   8 62CB          BRN      TENDC
  17 62X2    XBREAKIN                           [BREAK-IN IN PARSORT 
  10 63BL          GEOERR   1,TEABREAK   
   5 63W=    #PAGE   
  10 64*W          MENDAREA 30,K100TERM  
   4 64TG    #END
   8 ____ ...40132640000100000000
