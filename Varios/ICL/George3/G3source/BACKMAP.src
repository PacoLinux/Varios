  14 22FL ...      SEG      BACKMAP,864,EDWARD MOON,BMAP 
   7 25H? ...#OPT  BMTRACE=0 
  12 25X2          SEGENTRY K1BACKMAP,Z1BACKMAP  
  12 26BL          SEGENTRY K2BACKMAP,NEEDFILE   
  11 26W= ...      SEGENTRY K3BACKMAP,RESTY  
  10 27*W ...      SEGENTRY K4BACKMAP,NR 
   4 27TG ...[   
  21 28*6 ...[***********************************************************************
  18 28SQ ...[     THIS IS THE FIRST BACKMAP SEGMENT. IT DOES THE INITIAL
  19 29#B ...[     CHECKING THAT A SUITABLE FILESTORE HAS BEEN PROVIDED, AND 
  18 29S2 ...[     SETS UP VARIOUS BLOCKS REQUIRED BY BACKMAP. IT ALSO   
  19 2=?L ...[     PERFORMS SOME RUDIMENTARY CHECKS ON THE AVAILABILITY OF   
  19 2=R= ...[     'ESSENTIAL' SYSTEM FILES. THE OPERATORS ARE ASKED IF ANY  
  19 2?=W ...[     RESTORES ARE REQUIRED AND WE CHECK TO SEE IF A GEN. RES.  
   9 2?QG ...[     MUST BE ENFORCED. 
   4 2#=6 ...[   
   8 2*F8 ...#SKI  K6BACKMAP-150 
   4 2*P2 ...#SKI
   9 2*YS ...UNITNUMASK     #17770000
   6 2B8L    SLAVETYPE   
   8 2BN=    #HAL  +BSA+AUTOMAP,0
   5 2BW8 ...XSERLINK
  10 2C46 ...      LINK     BACKROOM,4   
   6 2C=4 ...XDICTLINK   
  10 2CD2 ...      LINK     BACKROOM,2   
  10 2CKY ...NOTAVAIL       +BMNOTAVAIL  
  10 2CMX ...      SEGENTRY K66BACKMAP   
   8 2CPW ...SLAVEK         +2   
   4 2CRW ...[   
  21 2C_S ...[***********************************************************************
  17 2D7Q ...[           ROUTINE TO CHECK IF THE RESIDENCE NO. OF THE
  13 2D*N ...[                TOP FILE OPEN EXISTS   
   4 2DHL ...[   
   5 2DPJ ...XCHECKLF
   7 2DXG ...      TOPFCB2  3
  19 2F5D ...      LDX   4  BSPRE(3)                    [ PICK UP RES. NO.   
   5 2F?B ...NXTFBSIB
  21 2FF# ...      HUNT2J   2,BSTB,FBSIB,,NEEDFILE      [ HUNT NEXT MAPPING BLOCK AND
  21 2FM= ...      LDEX  0  ATYPE(2)                    [ BRANCH TO ASK FOR RES. IF  
  17 2FT8 ...      SBX   0  4                           [ NONE LEFT. 
  21 2G36 ...      BNZ   0  NXTFBSIB                    [ COMPARE RESIDENCE NOS. & J.
  18 2G94 ...      LDX   2  FX2                         [ IF NOT THE SAME
  19 2GC2 ...      EXIT  7  0                           [ ELSE FOUND, SO EXIT
   9 2GK=    MASWRECK       +BMNOTFS 
   9 2H4W    MARKFS         +BMMKFS  
  10 2HJG    RESTART        +BMRESTART   
  10 2J46    RESTORE        +BMRESTORE   
   9 2LG=    NMASK          #300000  
   9 2M_6    NMAST          12HMASTER
  15 2NDQ    MAST           12H????????????              [   
  19 2NYB ...               12HMASTER                    [ PRESETS FOR FCB   
  15 2PD2 ...               12HMASTER                    [   
   4 2QWW    [   
   5 2RBG    MONOUT  
   8 2RW6          SBX   7  FX1  
   7 2S*Q          MONOUTX  3
   7 2STB          ADX   7  1
   7 2T*2          EXIT  7  0
   4 2TSL    [   
   5 2W#=    OUTPAR  
   8 2WRW          SBX   7  FX1  
   8 2X?G ...      OUTBLOCN 13   
  11 2XR6          OUTPAR   DATENOW,TIMENOW  
   8 2Y=Q          ADX   7  FX1  
   7 2YQB          EXIT  7  0
   4 2_=2    [   
   4 2_PL    [   
   6 329=    WRITEBACK   
   8 32NW          SBX   7  FX1  
   9 338G          BACKWRIT FREE,BSBS
   7 33N6          ADX   7  1
   7 347Q          EXIT  7  0
   4 347_ ...[   
  21 3488 ...[***********************************************************************
  20 348C ...[     ROUTINE TO GET POINTER IN X3 TO NEXT RECORD IN FILE (SAME AS  
  21 348L ...[     STEP). IF WE NEED TO READ A BLOCK FROM BACKING STORE, WE DO THIS  
  20 348T ...[     OURSELVES, SKIPPING ANY BLOCKS WHICH FAIL. X3 = 0 AT E.O.F.   
   4 3494 ...[   
   5 349? ...QSTEPC  
  21 349G ...      QSTEPC   ,XBACKREAD        [ GET PTR. TO NEXT RECORD, BRANCHING   
  18 349P ...XIT                              [ IF WE NEED TO READ BLOCK 
  18 349Y ...      EXIT  7  0                 [ ELSE EXIT WITH PTR. IN X3
   6 34=7 ...XBACKREAD   
  20 34=B ...      STOZ    AWORK1(2)           [ INITIALISE TRANSFER REPEAT COUNT
   7 34=K ...      TOPFCA2  3
   6 34=S ...TRYNXTBLK   
  19 34?3 ...      LDX   3  FREADBLOCK(3)      [ POINTER TO BLOCK TO BE READ 
   7 34?= ...      TOPFCB2  1
   9 34?F ...      LDX   2  FUSEBL(1)
  18 34?N ...      ADN   2  A1                 [ POINTER TO END OF FCB   
   7 34?X ...      SBX   2  3
  17 34#6 ...      BNZ   2  NOTEOF             [ J. IF NOT AT E.O.F. 
   8 34#9 ...      LDX   2  FX2  
  16 34#* ...      STOZ     3                  [ ELSE SET X3 = 0 
  14 34#J ...      BRN      XIT                [ AND EXIT
   5 34#R ...NOTEOF  
  18 34*2 ...      SMO      3                  [ NOT AT E.O.F. SO PICK   
  15 34*9 ...      LDX   3  0(1)               [ UP BLOCK NO.
  17 34*D ...      LDX   2  BSPRE(1)           [ AND RESIDENCE NO.   
   8 34*M ...      SBX   7  FX1  
  17 34*W ...      BACKREAD ,BSBS,READFAIL,2    [ READ REQUIRED BLOCK
  17 34B2 ...      MHUNTW   2,BSTB,BREAD        [ FIND THE BLOCK READ
  17 34B6 ...      BNG   2  READFAIL            [ READFAIL IF ABSENT 
   7 34B= ...      LDX   3  2
   8 34BB ...      LDX   2  FX2  
   8 34BG ...      ADX   7  FX1  
  19 34BQ ...      TOPFCB2  1                   [ ELSECHECK RECORD HEADERS   
  17 34B_ ...      CHECKBL   1,3,NOTCORUPT      [ J. TO %C IF O.K.   
  19 34C8 ...      LDX   2  FX2                 [ ELSE RECORD HEADERS CORRUPT
   9 34C# ...      LDX   0  BACK1(3) 
  19 34CD ...      STO   0  ACOMMUNE2(2)        [ SAVE RES. NO. AND BLOCK NO.
   9 34CL ...      LDX   0  BACK2(3) 
  19 34D4 ...      STO   0  ACOMMUNE3(2)        [ FOR LATER USE BY READFAIL  
   7 34D? ...      TOPFCA2  3
  19 34DG ...      LDX   0  AWORK1(2)           [ NO. OF REPEATS DONE SO FAR 
  19 34DP ...      SBN   0  5                   [ IF WE HAVE DONE 5 GIVE UP  
  17 34DY ...      BZE   0  TRYNXT              [ AND TRY NEXT BLOCK 
  14 34F7 ...      MFREE    BSTB,BREAD          [ ELSE   
  19 34FB ...      LDN   0  1                   [ INCREASE COUNT OF REPEATS  
  19 34FK ...      ADS   0  AWORK1(2)           [ AND GO BACK TO TRY AGAIN   
   9 34FS ...      BRN      TRYNXTBLK
   6 34G3 ...NOTCORUPT   
  19 34G= ...      NAME     3,FILE,FURB         [ BLOCK IS O.K. SO RENAME IT 
   7 34GF ...      TOPFCA   2
  10 34GN ...      ADX   2  FBACKPOINT(2)
  21 34GX ...      CHAIN    3,2                 [ AND CHAIN AFTER THE FSTACK BLOCK   
  17 34H6 ...      BRN      QSTEPC              [ J. TO REPEAT QSTEPC
   5 34H* ...READFAIL
   8 34HB ...      ADX   7  FX1  
   7 34HC ...      TOPFCB2  1
  10 34HD ...      STO   3  ACOMMUNE3(2) 
   9 34HF ...      LDX   0  BSPRE(1) 
  20 34HG ...      STO   0  ACOMMUNE2(2)       [STORE RES + BLK NOS FOR READFAIL 
  19 34HJ ...      DOWN     READFAIL,4          [ TO OUTPUT READFAIL MESSAGE 
   7 34HR ...      TOPFCA   3
  19 34J2 ...      BRN      TOTRYNXT            [ GIVE UP & TRY NEXT BLOCK   
   5 34J9 ...TRYNXT  
  18 34JD ...      DOWN     READFAIL,3          [ OUTPUT CORRUPT MESSAGE 
  13 34JM ...      MFREE    BSTB,BREAD          [ AND
   5 34JW ...TOTRYNXT
  19 34K5 ...      LDX   0  FREADBLOCK(3)       [ INCREMENT BLOCK NO. PTR.   
   7 34K# ...      ADN   0  1
  10 34KH ...      STO   0  FREADBLOCK(3)
  20 34KQ ...      STOZ     AWORK1(2)           [ REINITIALISE COUNT OF REPEATS  
  16 34K_ ...      BRN      TRYNXTBLK           [ TRY NEXT BLOCK 
   4 34MB    [   
  21 35LL    [***********************************************************************
  20 366=    [   THIS ROUTINE SETS UP A GMON ASET BLOCK FOR MESSAGE PARAMETERS   
  12 375G    [     ON ENTRY  X5=> SIZE OF BLOCK  
  10 37K6    [               X7 =>LINK   
   4 384Q    [   
   5 38JB    OUTBLOCK
   8 3942          SBX   7  FX1  
   7 39HL          OUTBLOCK 5
   7 3=3=          STEPAGAIN 
   8 3=GW          ADX   7  FX1  
   7 3?2G          EXIT  7  0
   4 3?G6    [   
  21 3?_Q    [***********************************************************************
  17 3#FB ...[ THIS ROUTINE PACKS A PARAMETER INTO A GMON ASET BLOCK 
  15 3#_2    [   ON ENTRY   X2 = ADDRESS OF PARAMATER DATA   
  12 3*DL    [              X5 = LENGTH OF DATA  
  12 3*Y=    [              X6 = LPDC CHARACTER  
   9 3BCW    [              X7 = LINK
   5 3BXG    OUTPACK 
   8 3CC6          SBX   7  FX1  
   8 3CWQ          OUTPACKX 2,5,6
   8 3DBB          ADX   7  FX1  
   7 3DW2          EXIT  7  0
   4 3DXP ...[   
  21 3D_D ...[***********************************************************************
  20 3F37 ...[     ROUTINE TO SEARCH FOR THE DIRECTORY ENTRY OF THE FILE WHOSE   
  19 3F4W ...[     LOCALNAME IS SPECIFIED BY 3 WORDS AFTER THE LINK ADDRESS. 
  19 3F6K ...[     BRANCHES TO 'XFILEBENT' IF IT IS NOT FOUND OR IF FORMAT IS
  19 3F8# ...[     WRONG I.E. BLOCKS & INDEX RECORDS DISAGREE WITH ECOPS OR  
  16 3F=3 ...[     THERE ARE FEWER TRAPS RECORDS THAN ENUSE ETC. 
   4 3F?Q ...[   
   6 3F*L    SERCHFFILE  
  16 3FT=          LDX   3  7                 [ SAVE LINK IN X7  
  17 3G#W          LDX   4  0(3)              [ PUT NAME OF FILE THAT
  18 3GSG          LDX   5  1(3)              [ WE ARE SEARCHING FOR INTO
  13 3H#6          LDX   6  2(3)              [ X4-X6
  15 3HF# ...      SBX   7  FX1               [ DATUMISE LINK
  15 3HLG ...      STO   7  AWORK2(2)         [ AND SAVE IT  
   6 3HRQ    NEXTRECORD  
  17 3J?B ...      CALL  7  QSTEPC            [ PTR. TO NEXT RECORD  
  17 3JR2          BZE   3  XFILEBENT         [ NOT FOUND IF E.O.F.  
  10 3K=L          LDX   1  EDESCRIBE(3) 
  19 3KQ=          BNZ   1  NEXTRECORD        [TRY AGAIN IF NOT NAMERECORD   
  14 3L9W          TESTNAMX 3,4,ELOC1N(3),NEXTRECORD,1   
  21 3LPG          TOPFCA2  1                 [ WE HAVE FOUND NAME REC. OF OUR FILE  
  17 3M96          LDX   4  FREADBLOCK(1)     [ STORE BLOCK  POINTER 
  12 3MNQ ...      SLL   4  12                [  
  14 3N8B ...      ADX   4  FREADWORD(1)      [ TO AVOID 
  20 3NN2          STO   4  AWORK3(2)         [ LATER BACKSPACING TO NAME REC.   
  12 3P7L          LDX   5  ECOPSN(3)         [  
  20 3PM=          SRL   5  15                [ NUMBER OF BLOCKS FOR LASER CHECK 
  20 3Q6W          LDX   6  ENUSEN(3)         [ NUMBER OF TRAPS FOR LATER CHECK  
   8 3QLG          LDN   0  #1000
   9 3R66          ANDX  0  EINF2N(3)
  17 3RKQ          STO   0  AWORK4(2)         [ NON ZERO IF INDEXED  
  17 3S5B ...      CALL  7  QSTEPC            [ ON TO BLOCKS RECORD  
  16 3SK2          BZE   3  XFILEBENT         [ EROR IF E.O.F.   
   9 3T4L          LDX   4  FRH(3)   
   7 3TJ=          SBN   4  2
  18 3W3W          SBX   4  5                 [ COMPARE LENGTH WITH ECOPS
  21 3WHG          BNZ   4  XFILEBENT         [ IF BLOCKS REC. IS THE WRONG LENGTH   
   9 3X36          LDX   0  AWORK4(2)
  15 3XGQ          BZE   0  NOINDEXG          [ IF UNINDEXED 
  17 3Y2B ...      CALL  7  QSTEPC            [ ON TO INDEX RECORD   
  16 3YG2          BZE   3  XFILEBENT         [ ERROR IF E.O.F.  
   8 3Y_L          LDCT  4  #200 
  10 3_F=          ANDX  4  EDESCRIBE(3) 
  19 3_YW          BZE   4  XFILEBENT         [ ERROR IF NOT AN INDEX RECORD 
   5 42DG    NOINDEXG
  18 42Y6          BZE   6  NOTRAPS           [ J. IF NO TRAPS EXPECTED  
   5 43CQ    NEXTTRAP
  17 43XB ...      CALL  7  QSTEPC            [ TO NEXT TRAPS RECORD 
  16 44C2          BZE   3  XFILEBENT         [ ERROR IF E.O.F.  
   9 44WL          LDX   0  FRH(3)   
  17 45B=          SBN   0  5                 [ CHECK RECORD HEADER  
  20 45TW          BNZ   0  XFILEBENT         [ ERROR IF NOT 5 WORD TRAPS RECORD 
  20 46*G          LDX   0  ETRUSER1N(3)      [ CHECK USERNAME STARTS WITH ALPHA 
  19 46T6          BPZ   0  XFILEBENT         [ J. IF NOT A VALID USERNAME   
  20 47#Q          BCT   6  NEXTTRAP          [ LOOP ROUND FOR NEXT TRAPS RECORD 
   5 47SB    NOTRAPS 
   7 48#2          TOPFCA2  1
  18 48RL ...      LDSA     4,AWORK3(2)       [ RECOVER READING POINTERS 
  13 49?= ...      STO   4  FREADWORD(1)      [ FOR  
  17 49QW          LDX   4  AWORK3(2)         [ NAMERECORD AND PUT   
  13 49_6 ...      SRL   4  12                [ INTO 
  14 4=7B ...      STO   4  FREADBLOCK(1)     [ THE FCA  
   9 4=*L ...      LDX   7  AWORK2(2)
   8 4=HW ...      ADX   7  FX1  
  19 4=Q6          EXIT  7  3                 [ EXIT PAST LOCALNAME PROVIDED 
   6 4?PB    Z1BACKMAP   
   4 4?T? ...[   
  21 4?_8 ...[************************************************************ Z1BACKMAP 
   4 4#55 ...[   
  17 4#92 ...[         MAIN ENTRY POINT DOWN FROM SETUP TO PERFORM   
  16 4##X ...[             THE INITIALISATION PHASE OF BACKMAP   
   4 4#DS ...[   
  18 4#JP ...      LOCKC    2,/                  [ LOCK BSA/CHAPTER ACT. 
  19 4#NL ...      LDX   2  FX2                  [ AND STORE PTR. TO IT SO   
  18 4#SH ...      STO   2  BSACHAPTR            [ SLAVES CAN FIND IT.   
   7 4#YD ...      LDN   0  0
  19 4*4* ...      BMSTATS  0                    [ SEND STATISTICS TO JOURNAL
   6 4*6* ...#UNS AMTGR  
  20 4*8* ...      BS       ,BFREEBS           [ SHOW BACKING SORE TO BE FREED   
  20 4*=* ...      SETNCORE FCBLEN,3,FILE,FCB,,1    [ SET UP AN FCB FOR MASTER   
  10 4*** ...      LDN   0  FCBRING(3)   
  15 4*D* ...      STO   0  FCBRING(3)          [SET EMPTY   
  10 4*H* ...      STO   0  FCBRING+1(3) 
  15 4*L* ...      LDCT  0  #400         [MASTERS FCB NUMBER 
   9 4*P* ...      STO   0  BACK2(3) 
   9 4*S* ...      STOZ     BACK1(3) 
   9 4*X* ...      STOZ     FBLMOD(3)
   9 4B2* ...      LDN   1  FBLMOD(3)
  10 4B5* ...      LDN   2  FBLMOD+1(3)  
  16 4B8* ...      MOVE  1  BSPRE-FBLMOD           [ ZEROISE FCB 
   8 4B?C ...      LDX   1  FX1  
   8 4BC# ...      ADN   1  MAST 
   9 4BH9 ...      LDN   2  FUSER1(3)
  21 4BM6 ...      MOVE  1  9                  [ MOVE IN SUPERIOR USER + USER NAMES  
  12 4BR3 ...      LDN   0  FBLKS-A1           [ 
  16 4BWY ...      STO   0  FBLMOD(3)          [ START OFF WITH  
  15 4C2T ...      STO   0  FUSEBL(3)          [ EMPTY FILE  
   9 4C6Q ...      LDN   0  FILESIZE 
  17 4C=M ...      STO   0  FSIZE(3)           [ STORE MAX. FILESIZE 
  18 4CBJ ...      NGS   0  CMOD(3)            [ MAKE APPEND PTR. -VE.   
  17 4CGF ...      MBS      3,BFDIR,BFHALF,BFSER  [ SET RELEVANT BITS
  18 4CLB          CHAIND   3,BFILE               [ CHAIN IN FILE CHAIN  
  18 4D62          SETNCORE FDTLEN+1,3,FILE,FDT   [ FOR RETRIEVAL SYSTEM 
  14 4DKL          STOZ     A1(3)                 [ AND  
  19 4F5=          CHAIND   3,BOLC                [ CHAIN IN OFFLINE CHAIN   
  18 4FJW          DOWN     BACKROOM,1            [ SET UP MHPPING BLOCKS
  19 4FL? ...      SETNCORE 14,3,FI,FRESTUSER     [ SET UP FRESTUSER FOR ANY 
  18 4FMN ...      STOZ     A1(3)                 [ PARTIAL RESTORES AND 
  16 4FP5 ...      LDN   1  A1(3)                 [ INITIALISE IT
   9 4FQG ...      LDN   2  FRCNT(3) 
   7 4FRX ...      MOVE  1  5
   8 4FT# ...      LDX   0  ACES 
   9 4FWP ...      STO   0  FRUSE(3) 
  10 4FY6 ...      STO   0  FRUSE+1(3)   
  10 4F_H ...      STO   0  FRUSE+2(3)   
  20 4G2Y ...      CHAIN    3,BMISC+1           [ CHAIN IN MISCELLANEOUS CHAIN   
   9 4G4G          CALL  7  OUTPAR   
  14 4GJ6          LDN   4  G3MARK              [ MARK   
   7 4H3Q          LDN   2  4
   7 4HHB          LDN   5  1
   9 4J32          LDN   6  JPDNUMA  
   9 4JGL          CALL  7  OUTPACK  
   7 4K2=          LDN   2  4
  14 4KFW          LDEX  4  GVERSION            [ VERSION
   7 4K_G          LDN   5  1
   9 4LF6          LDN   6  JPDNUMB  
   9 4LYQ          CALL  7  OUTPACK  
   7 4MDB          LDN   2  4
   7 4MY2          LDN   4  0
   8 4NCL          BNSJ     NSJ  
   7 4NX=          LDN   4  1
   4 4PBW    NSJ 
   9 4PWG          LDN   6  JPDSWITCH
   9 4QB6          CALL  7  OUTPACK  
   9 4QF= ...      LDN   2  EXECNAME 
   7 4QJB ...      LDN   5  2
  10 4QMG ...      LDN   6  JPDVARCHAR   
   9 4QQL ...      CALL  7  OUTPACK  
  10 4QTQ          LDX   3  RESTART(1)   
  17 4R*B          CALL  7  MONOUT              [SJ RESTARTED MESSAGE
  12 4RT2    [CHECK IF GENERAL RESTORE REQUIRED  
   9 4S#L          STOZ     AWORK1(2)
   9 4SS= ...      ACROSS   BMQUEST,1
   5 5D*=    RESTY   
   5 5R52    RESTA2  
  10 5RJL          DOWN     RESTOREA,2   
  10 5SHW          LDX   7  ACOMMUNE1(2) 
   9 5T3G          STO   7  FSMARK(2)
   8 5T65 ...      LOCKC    2,/  
   8 5T8N ...      LDX   2  FX2  
   9 5T?? ...      STO   2  BSACHAPTR
   7 5T*W ...      LDN   0  1
   7 5TDF ...      BMSTATS  0
   9 5TH6 ...      CALL  7  OUTPAR   
   9 5W2Q ...      LDN   2  JINCLAST 
   7 5WGB          LDN   5  1
   9 5X22 ...      LDN   6  JPDINCNUM
  16 5XFL          CALL  7  OUTPACK             [INCREMENT NUMBER
  10 5X_=          LDX   3  RESTORE(1)   
  19 5YDW          CALL  7  MONOUT              [RESTORED MESSAGE TO JOURNAL 
  10 5YNN ...      MFREEALL BSTB,FBSIB   
   9 5YYG          LDX   7  FSMARK(2)
   4 5_D6    NR  
   9 5_XQ          STOZ     JOBNUMBER
  13 62CB    [READ & CHECK :MASTERS DIRECTORY ENTRY  
   9 62X2          LDN   1  BTHREE   
   9 63BL          LDN   2  ADMASTDIR
   9 63W=          BACKREAD ,BSBS,,1 
  10 64*W          MHUNTW   3,BSTB,BREAD 
   9 64TG          LDEX  0  EREC(3)  
   8 65*6          SBN   0  FRDE 
   8 65SQ          BNZ   0  MASA 
  14 66#B          TESTNAMX 3,NMAST(1),EUSE1(3),MASA,6   
  11 66S2          BWNZ     GINCTSN,VERSION  
  10 67?L          LDX   7  EVERSION(3)  
   5 67R=    VERSION 
  10 68=W          STO   7  EVERSION(3)  
  18 6#MG          BWZ      GINCTSN,NOTREST     [J IF NO GENERAL RESTORE 
  21 6*76          BS       3,BEBMUWB           [REMEMBER RESTORE IF SUBSEQUENT RESTA
  21 6*LQ          LDCT  0  #200                [INVERT SWITCH & COPY ENTRY TO 2ND 1/
   9 6B6B          ERS   0  EREC(3)  
   8 6BL2          LDN   4  A1(3)
  10 6C5L          LDN   5  GSBS/2+A1(3) 
   9 6CK=          MOVE  4  GSBS/2   
   8 6D4W          BRN      WRITE
   5 6DJG    NOTREST 
   9 6FHQ          LDX   7  EREC(3)  
  11 6G3B          ERX   7  GSBS/2+EREC(3)   
  18 6GH2          BZE   7  NMOVE               [BOTH THE SAME-NO CHANGE 
  10 6H2L          LDN   4  GSBS/2+A1(3) 
   8 6HG=          LDN   5  A1(3)
  16 6H_W          MOVE  4  GSBS/2              [USE 2ND ENTRY   
   5 6JFG    NMOVE   
   8 6J_6          LDCT  0  #200 
   9 6KDQ          ERS   0  EREC(3)  
  11 6KYB          ERS   0  GSBS/2+EREC(3)   
  18 6LD2          BWNZ     AWORK1(2),WASAST    [J IF 'RESTORE?' ASKED   
  18 6LXL          JBC      WASAST,3,BEBM       [OR IF NO PREVIOUS BREAK 
   4 6MC=    ZONK
  16 6MWW          FREECORE 3                   [ IF ANSWER NO   
   9 6NBG ...      ACROSS   BMQUEST,9
   5 6NW6    WASAST  
  21 6P*Q          JBC      MASOK,3,BEBMUWB     [J UNLESS PREVIOUS BREAK DURING RESTO
   9 6PKJ ...      MONOUT   BMFORCEGR
   5 6PTB    REPREST 
   7 6Q*2          LDN   5  0
  21 6QSL ...      ACROSS   BMQUEST,10          [TO RESET FRESTUSER & REPEAT RESTORE?
   5 6R#=    MASOK   
  13 6RCB ...      JBC      NOTPRFAILED,3,BEPARTRES  
   8 6RGG ...      LDX   2  BFILE
  10 6RKL ...      BS       2,BFPARTRES  
   6 6RNQ ...NOTPRFAILED 
  10 6RRW          LDX   6  EREC+FRDE(3) 
   8 6S?G          BZE   6  MASA 
   7 6SR6          SMO      6
  10 6T=Q          LDX   5  EREC+FRDE(3) 
   8 6TQB          BZE   5  MASA 
  11 6W=2          SETUPCOR 5,1,FI,FINDEXF   
  10 6WPL          MHUNTW   3,BSTB,BREAD 
   8 6X9=          ADN   1  EREC 
   7 6XNW          SMO      6
  10 6Y8G          LDN   0  EREC+FRDE(3) 
   7 6YN6          SMO      5
  19 6_7Q          MOVE  0  0                   [MOVE INDEX RECORD INTO BLOCK
   9 6_MB          ADN   6  FCBLEN-2 
  17 7272          ALTLEN   BFILE,6             [FOR BLOCK NUMBERS   
  10 72LL          MHUNTW   3,BSTB,BREAD 
   8 736=          LDX   1  BFILE
   9 73KW          LDN   0  INDEXDIR 
   9 745G          ORS   0  FINFC(1) 
   9 74K6          STO   6  FBLMOD(1)
   9 754Q          STO   6  FUSEBL(1)
  11 75JB          LDN   0  EREC+FRDE+1(3)   
   8 7642          ADN   1  BSPRE
   7 76HL          SMO      6
  10 773=          MOVE  0  512-FCBLEN+1 
  10 77GW          MHUNTW   1,FI,FINDEXF 
   8 782G          LDX   2  BFILE
  21 78G6          CHAIN    1,2                 [CHAIN INDEX BLOCK AFTER MASTER'S FCB
   5 78_Q    WRITE   
   8 79FB          LDX   1  BFILE
  10 79_2          LDX   0  ELASTJAM(3)  
  10 7=DL          STO   0  FLASTJAM(1)  
   9 7=Y=          LDX   0  EGMTBS(3)
   9 7?CW          STO   0  FGMTBS(1)
  17 7?XG          BS       3,BEBM              [BACKMAP IN PROGRESS 
  11 7#C6          ORS   0  GSBS/2+EINF1(3)  
   8 7#WQ          CHAIN    3,2  
   9 7*BB          CALL  7  WRITEBACK
  19 7*W2          SETNCORE 6,1,FILE,FLOCNB     [FOR USE THROUGHOUT BACKMAP  
  10 7B*L          OPENMAS  XBR,GENERAL  
  10 7BT=          BWZ      GINCTSN,NRSET
  18 7C#W          DOWN     BACKROOM,1          [RECREATE MAPPING BLOCKS 
   5 7CSG    NRSET   
   9 7D#6 ...      CALL  7  XCHECKLF 
  20 7DRQ          CALL  7  SERCHFFILE          [TO FIND AND CHECK DICTIONARIES  
  16 7F?B                   12HDICTIONARY       [ENTRY IN MASTER 
   7 7FR2          READAGAIN 
  19 7G=L          MHUNTW   2,FILE,FLOCNB       [SET UP FLOCNB FOR OPENRELN  
  10 7GQ=          MHUNTW   3,FILE,FRB   
  10 7H9W          NAME     3,FILE,ENT   
   8 7HPG          ADN   2  A1   
   9 7J96          LDN   1  ELOC1(3) 
   7 7JNQ          MOVE  1  5
   9 7K8B          LDX   4  ELAN(3)  
   8 7KN2          STO   4  5(2) 
  15 7L7L          OPENRELN XBR,GENERAL,DIROPEN [DICTIONARY  
  10 7LM=          TESTRPN2 OK,XOPENFAIL 
   9 7LPT ...      CALL  7  XCHECKLF 
  10 7LSD ...      STOZ     AMAPDEPTH(2) 
   5 7LX3 ...SETLOCK 
   8 7L_L ...      LDX   2  FX2  
  12 7M49 ...      POP      SETLOCK,2,FTABWAIT   
  10 7M6S ...      GETACT   BSA,AUTOMAP  
   9 7M9C ...      LDX   3  BPTR(2)  
  11 7M#2 ...      LINKSET  3,XDICTLINK(1)   
   7 7MBK ...      LOCK     3
   7 7MF8 ...      CHANGEFI 3
   9 7MHR ...      LDX   3  BPTR(2)  
   7 7MLB ...      UNLOCK   3
   7 7MN_ ...      LDX   2  3
   6 7MRJ ...      FPUT  
   8 7MW7 ...      LDX   2  FX2  
   7 7MYQ ...      LDN   0  1
  10 7N3* ...      ADS   0  AMAPDEPTH(2) 
  14 7N66          REWIND                       [MASTER  
  10 7NKQ          CALL  7  SERCHFFILE   
   9 7P5B                   12HSYSTEM
  17 7PK2          READAGAIN                    [SYSTEM NAME RECORD  
  10 7Q4L          MHUNTW   2,FILE,FLOCNB
  10 7QJ=          MHUNTW   3,FILE,FRB   
  10 7R3W          NAME     3,FILE,ENT   
   8 7RHG          ADN   2  A1   
   9 7S36          LDN   1  ELOC1(3) 
   7 7SGQ          MOVE  1  5
   9 7T2B          LDX   4  ELAN(3)  
   8 7TG2          STO   4  5(2) 
  11 7T_L          OPENRELN XBR,READ,DIROPEN 
  16 7WF=          TESTRPN2 OK,XOPENFAIL        [IF NOT OPENED   
   9 7WP4 ...      CALL  7  XCHECKLF 
  10 7WYW          CALL  7  SERCHFFILE   
   9 7XDG                   12HSERIAL
  17 7XY6          READAGAIN                    [SERIAL NAME RECORD  
  19 7YCQ          MHUNTW   2,FILE,FLOCNB       [SET UP FLOCNB FOR OPENRELN  
  10 7YXB          MHUNTW   3,FILE,FRB   
  10 7_C2          NAME     3,FILE,ENT   
   8 7_WL          ADN   2  A1   
   9 82B=          LDN   1  ELOC1(3) 
   7 82TW          MOVE  1  5
   9 83*G          LDX   4  ELAN(3)  
   8 83T6          STO   4  5(2) 
  12 83XC ...      OPENRELN XBR,GENERAL,DIROPEN  
   9 83_N ...      CALL  7  XCHECKLF 
  10 843_ ...      TESTRPN2 OK,XOPENFAIL 
  10 846= ...      GETACT   BSA,AUTOMAP  
   9 848H ...      LDX   3  BPTR(2)  
  10 84=S ...      LINKSET  3,XSERLINK(1)
   7 84*5 ...      LOCK     3
   7 84CB ...      CHANGEFI 3
   9 84FM ...      LDX   3  BPTR(2)  
   7 84HY ...      UNLOCK   3
   7 84L9 ...      LDX   2  3
   6 84NG ...      FPUT  
   8 84QR ...      LDX   2  FX2  
   7 84T4 ...      LDN   0  1
  10 84X* ...      ADS   0  AMAPDEPTH(2) 
  10 84_L ...      CALL  7  SERCHFFILE   
  10 853X ...               12HINCINDEX  
   7 8568 ...      READAGAIN 
  10 858F ...      MHUNTW   2,FILE,FLOCNB
  10 85=Q ...      MHUNTW   3,FILE,FRB   
  10 85*3 ...      NAME     3,FILE,ENT   
   8 85C# ...      ADN   2  A1   
   9 85FK ...      LDN   1  ELOC1(3) 
   7 85HW ...      MOVE  1  5
   9 85L7 ...      LDX   4  ELAN(3)  
   8 85ND ...      STO   4  5(2) 
  12 85QP ...      OPENRELN XBR,GENERAL,DIROPEN  
  10 85T2 ...      TESTRPN2 OK,XOPENFAIL 
   9 85X? ...      CALL  7  XCHECKLF 
   8 85_J ...      FILEMOVE 0,-1 
  14 863T ...      CLOSEFRBS                     [SYSTEM 
  10 8666 ...      VOP      2,FTABWAIT   
   6 868C ...      REWIND
  18 86?=          SETNCORE 150,1,FI,FLIST      [SET UP BLOCK FOR SFTS   
   8 86QW          STOZ     A1(1)
   8 872N ...      LDX   6  CFREE
   8 87=G ...      ADX   6  ACHAP
   8 87G# ...      SRL   6  10   
   8 87Q6 ...      SBN   6  10   
   8 87_Y ...      SMO      FX1  
   9 889Q ...      DVS   5  SLAVEK   
   9 88FJ ...      STO   6  A1+2(1)  
   9 88PB          STOZ     A1+1(1)  
   9 88XL ...      CHAIN    1,BMISC+1
  10 894# ...      OFF      BMBLKTWICE   
  10 895W ...      SETNCORE 8,3,FI,FONCE 
   8 897D ...      STOZ     A1(3)
   9 8992 ...      CHAIN    3,BMISC+1
   7 89=P ...      LDN   0  2
   7 89#D ...      BMSTATS  0
   6 89B7 ...WAITFORBLOX 
  10 89CW ...      LDX   0  AMAPDEPTH(2) 
   8 89FK ...      BZE   0  READY
   9 89H# ...      COOR3    BMAPACT  
  10 89K3 ...      BRN      WAITFORBLOX  
   5 89LQ ...READY   
   9 89NL          ACROSS   BMAPONE,1
   4 8=8=    [   
  15 8=MW ...[       ASK FOR REQUIRED RESIDENCE TO BE LOADED.
   5 944B    NEEDFILE
   9 94J2 ...      ACROSS   BMQUEST,2
   5 95H= ...OUTNUM  
   7 962W ...      LDN   2  4
   7 96GG          LDN   5  1
   9 9726          LDN   6  JPDNUMA  
   9 97FQ          CALL  7  OUTPACK  
   9 97_B          BRN      OUTMON   
   4 98F2    MASA
  20 98YL ...      JBSS     WANTREST,3,BEBM     [J IF ALREADY FAILED IN BACKMAP  
  11 99D=          ORS   0  GSBS/2+EINF1(3)  
   9 99XW          CALL  7  WRITEBACK
   9 9=CG    #SKI  K6BACKMAP>199-199 
  10 9=X6          GEOERR   1,MASWRECK   
   9 9?BQ    #SKI  K6BACKMAP<200-200 
  10 9?WB          LDX   3  MASWRECK(1)  
   5 9#B2    OUTMON  
   9 9#TL          CALL  7  MONOUT   
   7 9*5D ...      EMSCONWT  
   7 9**=          GRELOAD   
   5 9*SW ...WANTREST
  10 9B#G ...      LDX   3  MASWRECK(1)  
   9 9BS6 ...      CALL  7  MONOUT   
  10 9C?Q ...      ACROSS   BMQUEST,10   
   4 9J82    [   
   6 9JML ...XFILEBENT   
   9 9K7= ...      LDX   2  AWORK2(2)
   8 9KLW ...      ADX   2  FX1  
   7 9L6G ...      LDN   5  3
  10 9LL6 ...      LDN   6  JPDLOCNAME   
   9 9M5Q ...      CALL  7  OUTPACK  
  10 9MKB ...      LDX   3  NOTAVAIL(1)  
   9 9N52 ...      CALL  7  MONOUT   
   7 9N#S ...      EMSCONWT  
  10 9NJL ...      GEOERR   1,SYSFILE?   
   4 =GYB    XBR 
   9 =HD2          GEOERR   1,BACKMAP
   6 =HXL    XOPENFAIL   
  10 =JC=          GEOERR   1,OPENFAIL   
   4 =JWW    [   
  12 =KBG          MENDAREA BMAPGAP,K99BACKMAP   
   4 =KW6    #END
   6 ____ ...071254750004
