/*M* RATFOR_SI64 */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X* DMR,PLM=5,IND=5,CTI=5,SDI=5,MCL=10,CSI=0,ECI=0 */
 
%SET LISTSUB='1'B;
 
GETID: PROC (MOP,IACN,IDEP,ID,IFACCN);
/*
************************************************************************
************************************************************************
*
*  TITLE: GETID
*
*  PURPOSE:
*      GETID obtains the mode of operation, logon account name,
*      department number (user name), job/online identification
*      number (sysid), and current file management directory
*      name from the user's JIT and returns the information to
*      the user
*
*  CALLING PROCEDURE:
*      GETID (MOP, IACN, IDEP, ID, IFACCN)
*
*  INPUTS:
*      Nothing
*
*  OUTPUTS:
*
*      MOP      - Mode of operation.
*                 Integer - 1 if batch
*                           2 if online.
*
*      IACN     - Logon account name of the current user.  Integer
*                 array dimensioned 2, returned in 2A4 format.
*
*      IDEP     - Department number (user name) of the current
*                 user.  Integer array dimensioned 3, returned
*                 in 3A4 format.
*
*      ID       - Job/online identification number (sysid).
*                 If 'MOP' = 1, 'ID' is the batch job number
*                 associated with the job.  If 'MOP' = 2, 'ID'
*                 is the online identification number displayed
*                 on the terminal at the beginning of an online
*                 session.  Integer array dimensioned 2, returned
*                 in A4, A2 format.
*
*      IFACCN   - Current file directory name of the current user
*                 Array dimensioned 2, returned in 2A4 format.
*
*  NOTES:
*      This routine is intended to be called with from 1 to 5
*      arguments.  Calls with less than five arguments will cause
*      a 'LINK' error message about argument/parameter mismatch.
*      The program will execute properly despite the error.
*
************************************************************************
************************************************************************
*
*   Basic Approach:
*      The data stored into all of the arguments is obtained from
*      the user's JIT.  The definition of the JIT layout is in the
*      'INCLUDE' file, 'B$JIT'.  (see System Support Ref., CE41,
*      A-5ff)
*
*      If the mode of operation is not batch, the mode of operation
*      returned is that of online (MOP = 2).
*
*      The comparisons to 'ADDR(NIL)' are to determine if the
*      argument is present.  If argument(s) is(are) not used
*      in the call to 'GETID', the address(es) 'GETID' receives
*      are set to 'NIL'.
*
*   Major Program Variables:
*      B$JIT.MODE  - Mode of operation from JIT; UBIN(4) UNAL
*      B$JIT.ACCN  - Account name from JIT; CHAR(8)
*      B$JIT.UNAME - User name from JIT; CHAR(12)
*      B$JIT.SYSID - Sysid from JIT; UBIN HALF UNAL
*      B$JIT.FACCN -File management directory from JIT; CHAR(8)
*      See also 'Outputs:' above for other program variables
*
*   Routines Called:
*      None
*
*   Date Written:  9-23-81
*
*   Author:  Dave Moebs
*
*   Restrictions:
*      None
*
*   Environment:
*      Language - PL6
*      Machine - DPS8/70C
*      Operating System - CP-6
*      Programming Priorities - Readability, maintainablity,
*                               usability
*      Compile Instructions - PL6 GETID_S over *G
*      Load Instructions - LEMUR, COPY *G INTO L.U
*
*   History/References:
*      GETID was a METASYMBOL routine in CP-V which only returned
*      the first four argumants above.  The arguments are identical
*      in format and content between the old and new except 'ID'.
*      Sysid grew from 16 to 18 bits and went from hex to decimal
*      external representation. It was expanded from a single element
*      to a two element array to accomodate the decimal, 6 position
*      format.
*
*   Special Instructions:
*      None
*********************************************************************
*********************************************************************
*
*   Record of Changes:
*   06-08-83 DSM Added code to return MOP = 3 when called from a
*                ghost environment.  This feature will not be
*                documented in users' info since it was added for
*                use in EPDA ghost and system type applications.
*
*********************************************************************
*********************************************************************
*/
  %INCLUDE B$JIT;
DCL B$JIT$ PTR SYMREF;
DCL MOP    UBIN;
DCL IACN   CHAR(8);
DCL IDEP   CHAR(12);
DCL ID     CHAR(6);
DCL IFACCN CHAR(8);
 
P10_GET_MOP:
     IF ADDR (MOP) = ADDR (NIL) THEN
/* NEXT PARAGRAPH */ ;
     ELSE
          IF B$JIT.MODE = 1 THEN
               MOP = 1;                 /* BATCH */
          ELSE
               IF B$JIT.MODE = 3 THEN
                    MOP = 2;            /* ONLINE */
               ELSE
                    MOP = 3;            /* GHOST */
 
P20_GET_IACN:
     IF ADDR (IACN) = ADDR (NIL) THEN
/* NEXT PARAGRAPH */ ;
     ELSE
          IACN = B$JIT.ACCN;
 
P30_GET_IDEP:
     IF ADDR (IDEP) = ADDR (NIL) THEN
/* NEXT PARAGRAPH */ ;
     ELSE
          IDEP = B$JIT.UNAME;
 
P40_GET_ID:
     IF ADDR (ID) = ADDR (NIL) THEN
/* NEXT PARAGRAPH */ ;
     ELSE
          CALL BINCHAR (ID, B$JIT.SYSID);
 
P50_GET_FACCN:
     IF ADDR (IFACCN) = ADDR (NIL) THEN
/* NEXT PARAGRAPH */ ;
     ELSE
          IFACCN = B$JIT.FACCN;
 
P90_RETURN:
     RETURN;
 
END GETID;
