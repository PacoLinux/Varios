/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
LOD:PROC MAIN;
%INCLUDE CP_6;
%INCLUDE XU_MACRO_C;
%INCLUDE XUD_UTS_M;
%XUD$UTS_ENTRIES;
%XUD_UTS_EQU;
%EQU N=10;
DCL 1 TABLE(0:%N) STATIC SYMDEF,
 2 DCBN UBIN INIT(0*0),
 2 BUF$ PTR;
DCL 1 CN REDEF TABLE,
 2 T UBIN,
 2 L UBIN,
 2 *(0:%(2*N-1)) UBIN;
%FPT_GETDCB;
%FPT_OPEN (ORG=FPRG, RES='FE00', FPRG=VLP_FPRG, FUN=CREATE);
%VLP_FPRG (NAME='LINKSPEED_FP',ACCT=' ');
%FPT_EVENT (STCLASS=CONSTANT, UENTRY=LOD_EVENT);
DCL LOD_EVENT ENTRY ASYNC;
DCL M$SI DCB;
DCL M$LO DCB;
DCL M$LM DCB;
%F$DCB;
%FPT_GDP (PAGES=1);
%FPT_READ (STCLASS=CONSTANT, FPTN=READUC, DCB=M$UC, BUF=BUF, WAIT=NO);
%FPT_EOM (STCLASS=CONSTANT, EOMTABLE=ZEROS);
%VLP_EOMTABLE (FPTN=ZEROS, STCLASS=CONSTANT, VALUES=0*16);
DCL BUF CHAR(1) STATIC INIT('+');
%FPT_WAIT (STCLASS=CONSTANT, UNITS=1);
%FPT_CLOSE;
%FPT_READ (BUF=TABLE,WAIT=NO);
%FPT_WRITE (BUF=TABLE, BP=YES);
DCL I SBIN;
DCL BYTES SBIN STATIC INIT(0);
DCL PBYTES SBIN STATIC INIT(0);
DCL B1 SBIN STATIC INIT(0);
DCL B2 SBIN STATIC INIT(0);
DCL B3 SBIN STATIC INIT(0);
DCL B4 SBIN STATIC INIT(0);
DCL B5 SBIN STATIC INIT(0);
DCL T0 SBIN;
DCL T1 SBIN;
DCL T2 SBIN;
DCL T3 SBIN;
DCL T4 SBIN;
DCL T5 SBIN;
DCL UTS_DIFF SBIN;
DCL TIME SBIN STATIC;
%FPT_TIME (STCLASS=CONSTANT, TSTAMP=TIME, DEST=UTS);
%FPT_WRITE (FPTN=WRITEUC, DCB=M$UC, BUF=MSG, VFC=YES);
DCL MSG CHAR(0) STATIC INIT(
%CONCAT('`NN Fprgs 1024 Bytes 000000 Cps 0000.0 kBaud  ',BINASC(8),BINASC(0)));
 
%P_PCB (W=WORK, R=LOD_OPTS, WSZ=100);
%PARSE$OUT (NAME=POUT, STCLASS="BASED(P_PCB.OUT$)");
%PARSE$SYM (NAME=PSYM, STCLASS=BASED);
DCL WORK(0:99) UBIN STATIC;
DCL LOD_OPTS UBIN SYMREF;
DCL X$PARSE ENTRY(1) ALTRET;
DCL B$JIT$ PTR SYMREF;
%INCLUDE B$JIT;
DCL O(0:3) UBIN STATIC INIT(0,0,1024,0);
DCL 1 OPT REDEF O,
   2 RCVONLY BIT(36),
   2 TIMED SBIN,
   2 SIZE SBIN,
   2 FPRGS SBIN;
%EJECT;
   VLP_FPRG.ACCT# = DCBADDR(DCBNUM(M$LM))->F$DCB.ACCT#;
   VLP_FPRG.PSN# = DCBADDR(DCBNUM(M$LM))->F$DCB.PSN#;
   P_PCB.NCHARS = B$JIT.CCARS-B$JIT.CCDISP;
   P_PCB.TEXT$ = ADDR(SUBSTR(B$JIT.CCBUF,B$JIT.CCDISP));
   IF P_PCB.NCHARS~=0 THEN CALL X$PARSE(P_PCB) WHENRETURN DO;
      DO I=0 TO POUT.NSUBLKS-1;
         IF POUT.SUBLK$(I)->PSYM.CODE>1
         THEN CALL CHARBIN (O(POUT.SUBLK$(I)->PSYM.CODE-1),
           POUT.SUBLK$(I+1)->PSYM.TEXT);
         ELSE IF POUT.SUBLK$(I)->PSYM.CODE=1
         THEN O(0) = 2;
      END;
   END;
   WHENALTRETURN DO; CALL M$ERR; END;
   IF OPT.SIZE>4096 THEN OPT.SIZE=4096;
   CALL M$EOM (FPT_EOM) WHENRETURN DO; END;
   CALL M$EVENT (FPT_EVENT);
   CALL M$TIME (FPT_TIME);
   T0 = TIME;
   T1 = TIME;
   T2 = TIME;
   T3 = TIME;
   T4 = TIME;
   T5 = TIME;
   TABLE.BUF$(0) = DCBADDR(DCBNUM(M$SI));
   IF SUBSTR(TABLE.BUF$(0)->F$DCB.NAME#.C,0,2)='FE'
   THEN FPT_OPEN.V.RES# = TABLE.BUF$(0)->F$DCB.NAME#.C;
   ELSE FPT_OPEN.V.RES# = TABLE.BUF$(0)->F$DCB.RES#;
 
   CALL BINCHAR (SUBSTR(MSG,10,4), OPT.SIZE);
   CN.L = OPT.SIZE-1;
   IF OPT.TIMED~=0 THEN DO;
      WRITEUC.BUF_.BOUND = SIZEV(MSG)-2;
      WRITEUC.V.DCB# = DCBNUM(M$LO);
      SUBSTR(MSG,0,1) = BITASC('036'O);
      CALL XUD$UTS_ADJ_25TH (OPT.TIMED, TIME, OPT.TIMED*%UTS_25TH_SEC#);
   END;
NEXT: DO SELECT (BUF);
   SELECT '+',%BINASC(ASCBIN('+')+128);
      OPT.FPRGS=OPT.FPRGS-1;
      IF CN.T=%N THEN EXIT;
      CN.T = CN.T+1;
      CALL BINCHAR (SUBSTR(MSG,1,2), CN.T);
      IF TABLE.DCBN(CN.T)=0 THEN DO;
         FPT_GDP.RESULTS_ = VECTOR (TABLE(CN.T));
         CALL M$GDP (FPT_GDP);
         FPT_GETDCB.DCBNUM_ = VECTOR (TABLE.DCBN(CN.T));
         CALL M$GETDCB (FPT_GETDCB);
      END;
      FPT_OPEN.V.DCB# = TABLE.DCBN(CN.T);
      TABLE.DCBN(CN.T) = TABLE.DCBN(CN.T)+262144;
      BYTES = BYTES-2*CN.L-2; /* First one doesn't count. */
      CALL M$OPEN (FPT_OPEN);
      IF OPT.RCVONLY THEN DO;
         FPT_WRITE.V.DCB# = FPT_OPEN.V.DCB#;
         FPT_WRITE.BUF_.BUF$ = PINCRC(ADDR(MSG),10);
         FPT_WRITE.BUF_.BOUND = 3;
         CALL M$WRITE (FPT_WRITE);
      END;
      IF OPT.FPRGS>0 THEN GOTO NEXT;
   SELECT '-',%BINASC(ASCBIN('-')+128);
      IF CN.T=1 THEN EXIT;
      FPT_CLOSE.V.DCB# = TABLE.DCBN(CN.T);
      CN.T = CN.T-1;
      CALL BINCHAR (SUBSTR(MSG,1,2), CN.T);
      CALL M$CLOSE (FPT_CLOSE);
   SELECT '0',%BINASC(ASCBIN('0')+128),
          'q',%BINASC(ASCBIN('q')+128),
          'Q',%BINASC(ASCBIN('Q')+128);
DONE: T1 = T0; B1 = 0;
      CALL DOMSG;
      CALL M$EXIT;
   SELECT '<',',';
      IF CN.L>16 AND NOT OPT.RCVONLY THEN CN.L = CN.L/2;
      GOTO PUTSZ;
   SELECT '>','.';
      IF CN.L<2048 AND NOT OPT.RCVONLY THEN CN.L = CN.L*2+1;
PUTSZ: CALL BINCHAR (SUBSTR(MSG,10,4), CN.L+1);
   SELECT %BINASC(7);
      SUBSTR(MSG,47,1) = BINASC(7-ASCBIN(SUBSTR(MSG,47,1)));
   SELECT %BINASC(256);
      DO I=1 TO CN.T;
         IF TABLE.DCBN(I)>262144 THEN DO INHIBIT;
            FPT_WRITE.V.DCB# = TABLE.DCBN(I);
            FPT_WRITE.BUF_.BUF$ = TABLE.BUF$(I);
            FPT_WRITE.BUF_.BOUND = CN.L;
            IF NOT OPT.RCVONLY THEN CALL M$WRITE (FPT_WRITE);
            FPT_READ.V.DCB# = TABLE.DCBN(I);
            FPT_READ.BUF_ = FPT_WRITE.BUF_;
            FPT_READ.V.EVENT# = I;
            TABLE.DCBN(I) = TABLE.DCBN(I)-262144;
            CALL M$READ (FPT_READ);
            BYTES = BYTES+2*CN.L+2;
         END;
      END;
      IF BYTES>PBYTES THEN DO;
         PBYTES = BYTES;
         CALL M$TIME (FPT_TIME);
         CALL XUD$UTS_DIFF (UTS_DIFF, TIME, T5);
         IF UTS_DIFF<%UTS_CSEC_SEC# THEN EXIT;
         IF OPT.TIMED=0 THEN CALL DOMSG;
         ELSE IF OPT.TIMED<=TIME THEN GOTO DONE;
         B1 = B2; B2 = B3; B3 = B4; B4 = B5; B5 = BYTES;
         T1 = T2; T2 = T3; T3 = T4; T4 = T5; T5 = TIME;
      END;
      CALL M$WAIT (FPT_WAIT);
      GOTO NEXT;
   END;
   BUF = %BINASC(256);
   IF OPT.TIMED=0 THEN CALL M$READ (READUC);
   GOTO NEXT;
DOMSG: PROC;
   CALL XUD$UTS_DIFF (UTS_DIFF, TIME, T1);
   I = (BYTES-B1)*100/UTS_DIFF;
   IF OPT.RCVONLY THEN I=I/2;
   CALL BINCHAR (SUBSTR(MSG,21,6), I);
   I = (8*I+100)/200;
   IF OPT.RCVONLY THEN I = I*2;
   CALL BINCHAR (SUBSTR(MSG,32,4), I/10);
   SUBSTR(MSG,37,1) = BINASC(MOD(I,10)+48);
   CALL M$WRITE (WRITEUC);
END;
END;
%EOD;
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
LOD_EVENT:PROC ASYNC;
%INCLUDE CP_6;
%EQU N=10;
DCL 1 TABLE(0:%N) SYMREF,
 2 DCBN UBIN,
 2 BUF$ PTR;
DCL 1 CN REDEF TABLE,
 2 T UBIN,
 2 L UBIN,
 2 *(0:%(N+N-1)) UBIN;
DCL I UBIN;
%B$NWIO (STCLASS="BASED(B$STK$)");
DCL B$STK$ PTR;
%B$TCB (STCLASS="BASED(B$TCB$)");
DCL B$TCB$ PTR SYMREF;
   B$STK$ = B$TCB.STK$;
   I = B$NWIO.EVID;
   IF I<=CN.T AND I>0 THEN TABLE.DCBN(I) = TABLE.DCBN(I)+262144;
END;
