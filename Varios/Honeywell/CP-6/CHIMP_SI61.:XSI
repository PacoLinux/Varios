/*M* CHIMP - Add imp to profile */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
/*X* DMR,PLM=5,IND=5,CTI=5,SDI=5,MCL=10,CSI=0,ECI=0 */
CHIMP: PROC MAIN;
 
%INCLUDE B$JIT;
%INCLUDE CP_6;
%INCLUDE KL_SUPER_C;
 
DCL M$SI DCB;
DCL M$PRO DCB;
 
%F$DCB;
 
DCL B$JIT$ PTR SYMREF;
%FPT_OPEN (FPTN=OPEN_PRO,NAME=PRONAME,ACCT=SYSACCT,ASN=FILE,SHARE=ALL,FUN=UPDATE,DCB=M$PRO);
%FPT_OPEN (FPTN=OPEN_PRO_AMR, ASN=FILE, SHARE=ALL, FUN=UPDATE, DCB=M$PRO);
%VLP_NAME(FPTN=PRONAME,NAME=':PRO');
%VLP_ACCT(FPTN=SYSACCT,ACCT=':SYS',STCLASS="STATIC SYMDEF");
%FPT_READ (FPTN=READ_PRO,KEY=PROKEY,KEYS=YES,BUF=PROBUF,DCB=M$PRO);
%FPT_WRITE (FPTN=WRITE_PRO,KEY=PROKEY,BUF=PROBUF,DCB=M$PRO);
%FPT_WRITE (FPTN=WRITE_PROIMP,KEY=PROKEY,BUF=IMPBUF,DCB=M$PRO);
%FPT_READ (FPTN=READ_SI,BUF=IMPBUF,DCB=M$SI,STRPVFC=YES);
%FPT_DELREC (FPTN=DELIMP,KEY=PROKEY,DCB=M$PRO);
 
DCL NOERROR BIT(1);
 
DCL 1 PROKEY STATIC,
         2 C UBIN BYTE CALIGNED,
         2 PRONAME CHAR(11) CALIGNED,
         2 IMP# UBIN BYTE CALIGNED;
%KL_PHDR(NAME=PROBUF,LAST=",");
         2 * CHAR(4096);
DCL IMPBUF CHAR(1024) STATIC;
 
     IF DCBADDR(DCBNUM(M$PRO))->F$DCB.AMR# THEN DO;
          CALL M$OPEN ( OPEN_PRO_AMR );
        END; ELSE DO;
          CALL M$OPEN ( OPEN_PRO );
        END;
 
     PROKEY.PRONAME = SUBSTR(B$JIT.CCBUF,B$JIT.CCDISP+1,B$JIT.CCARS-B$JIT.CCDISP-2);
     PROKEY.C = B$JIT.CCARS-B$JIT.CCDISP-2;
     CALL M$READ(READ_PRO);
     PROBUF.IMP# = 0;
     WRITE_PRO.BUF_.BOUND = DCBADDR(DCBNUM(M$PRO))->F$DCB.ARS# - 1;
     CALL M$WRITE (WRITE_PRO);
 
     PROKEY.C = 12;
     PROKEY.IMP# = 1;
 
     NOERROR = '1'B;
 
     DO WHILE (NOERROR);
          CALL M$DELREC (DELIMP)
          WHENALTRETURN DO;
               NOERROR='0'B;
             END;
          PROKEY.IMP# = PROKEY.IMP# + 1;
        END;
 
     NOERROR = '1'B;
     PROKEY.IMP# = 0;
 
     DO WHILE(NOERROR);
          CALL M$READ (READ_SI)
          WHENRETURN DO;
               PROKEY.IMP# = PROKEY.IMP# + 1;
               WRITE_PROIMP.BUF_.BOUND = DCBADDR(DCBNUM(M$SI))->F$DCB.ARS# - 1;
               CALL M$WRITE (WRITE_PROIMP);
             END;
          WHENALTRETURN DO;
               NOERROR = '0'B;
             END;
        END;
 
     PROKEY.C = B$JIT.CCARS - B$JIT.CCDISP - 2;
     CALL M$READ (READ_PRO);
     PROBUF.IMP# = PROKEY.IMP#;
     WRITE_PRO.BUF_.BOUND = DCBADDR(DCBNUM(M$PRO))->F$DCB.ARS# - 1;
     CALL M$WRITE (WRITE_PRO);
 
 
END CHIMP;
