/*M* Copy files - deleting schema perhaps. */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X*     PLM=3,SQB=Y,AND=Y,ENI=0,SDI=3,MCL=10,CSI=0,ECI=0,IND=0,IAD=3 */
MPUR:PROC MAIN;
%INCLUDE CP_6;
%INCLUDE B_ERRORS_C;
%INCLUDE XU_MACRO_C;
%FPT_OPEN(FPTN=OPNSI,THISF=YES,DCB=M$SI,FPARAM=NAMES);
%FPT_OPEN(FPTN=OPNEO,DCB=M$OU,ACS=DIRECT,IFPARAM=NAMES,SCRUB=YES);
%FPT_READ(FPTN=RPRE, BUF=NAMES,DCB=M$UI);
%FPT_READ(FPTN=READ,KEYR=YES,KEY=KBUF,DCB=M$SI,FULL=YES);
%FPT_READ(FPTN=RNAM,BUF=NAMES,KEYS=YES,KEY=NAMKEY,DCB=X$NM);
%FPT_WRITE(FPTN=WNAM,BUF=NAMES,KEY=NAMKEY,DCB=X$NM);
%FPT_WRITE(FPTN=WNIL,KEY=KBUF,DCB=M$OU);
%FPT_WRITE(FPTN=WREC,KEY=KBUF,FULL=YES,DCB=M$OU);
%FPT_WRITE(FPTN=WSL,DCB=M$LO);
%FPT_GDS(FPTN=GDS,RESULTS=READ.BUF_,SEGSIZE=1024);
%FPT_CLOSE(FPTN=CLSS,DISP=SAVE,RELG=YES);
%FPT_PRECORD(FPTN=PREC,DCB=M$SI,N=-1);
%FPT_INT(FPTN=INT,UENTRY=MPURBK);
/**/
DCL M$SI DCB;
DCL M$OU DCB;
DCL M$LO DCB;
DCL M$UI DCB;
%F$DCB(DCBN=F$DCB,BASED="BASED(SIDCB$)");
DCL B$TCB$ PTR SYMREF;
%B$TCB(STCLASS="BASED(B$TCB$)");
%B$ALT;
%FPT_PROMPT(FPTN=PRFPT,PROMPT=PREF);
%M$DCB(DCBN=X$NM,FUN=CREATE,ORG=KEYED,DISP=SCRATCH,ASN=FILE,ACS=DIRECT);
DCL SIDCB$ PTR;
DCL I UBIN;
DCL P$ PTR;
DCL NAM# SBIN;
DCL PREF CONSTANT CHAR(0)INIT('PREFIX?');
DCL 1 KBUF STATIC,
       2 T,
          3 L UBIN(9)UNAL,
          3 C CHAR(1),
       2 UTS UBIN UNAL,
       2 NAM REDEF UTS,
          3 S UBIN(18)UNAL,
          3 N UBIN(18)UNAL,
       2 TYPE UBIN(9)UNAL,
       2 * CHAR(249);
DCL 1 NAM BASED(P$),
       2 * BIT(9),
       2 L UBIN(9)UNAL,
       2 NAM CHAR(Q);
DCL Q UBIN;
DCL 1 NAMES STATIC,
       2 SEL(0:507)UBIN,
       2 UTS(0:507)UBIN;
DCL 1 HREC BASED(READ.BUF_.BUF$) ALIGNED,
       2 UTS UBIN,
       2 *(0:4)BIT(18),
       2 DBUG UBIN(18)UNAL;
DCL 1 NAMKEY STATIC,
       2 * BIT(9)INIT('003'O),
       2 S UBIN(9)UNAL,
       2 N UBIN(18)UNAL;
DCL SELCNT UBIN STATIC INIT(0);
DCL 1 SELS(0:40)STATIC,
       2 L UBIN(9)UNAL INIT(3),
       2 NAM CHAR(3)INIT('B_D');
/**/
%P_PCB(T=NAMES,WSZ=1000,R=SELCMDS);
%PARSE$OUT(NAME=OUT,STCLASS="BASED(P_PCB.OUT$)");
%PARSE$SYM(NAME=OUTSYM,STCLASS="BASED(P$)");
DCL X$PARSE ENTRY(1)ALTRET;
DCL SELCMDS UBIN SYMREF;
/**/
DCL MPURBK ENTRY ASYNC;
DCL BREAK SBIN STATIC SYMDEF;
%EJECT;
   SIDCB$=DCBADDR(DCBNUM(M$SI));
   IF F$DCB.NAME#.C=' ' THEN OPNSI.V.OPER.NXTF#='1'B;
   P$=DCBADDR(DCBNUM(M$OU));
   IF P$->F$DCB.NAME#.C=' ' OR P$->F$DCB.NAME#.C='*G'
   THEN OPNEO.NAME_=VECTOR(F$DCB.NAME#);
   ELSE IF OPNSI.V.OPER.NXTF# THEN DO;
      OPNSI.NAME_=VECTOR(P$->F$DCB.NAME#);
      OPNEO.NAME_=VECTOR(F$DCB.NAME#);
   END;
   IF NOT P$->F$DCB.AMR# THEN OPNEO.ACCT_=VECTOR(F$DCB.ACCT#);
   ELSE OPNEO.V.INITZ.REASSIGN#='1'B;
   CALL M$INT(INT);
   CALL M$GDS(GDS);
   P_PCB.WORK$=READ.BUF_.BUF$;
   WREC.BUF_=READ.BUF_;
   CALL M$PROMPT(PRFPT);
CONT:CALL M$READ(RPRE)ALTRET(NOPRE);
   P_PCB.NCHARS=DCBADDR(DCBNUM(M$UI))->F$DCB.ARS#;
   CALL X$PARSE(P_PCB)ALTRET(RETALT);
   IF P_PCB.OUT$~=ADDR(NIL) THEN DO I=0 TO OUT.NSUBLKS-1;
      IF OUT.NSUBLKS=1 THEN P$=P_PCB.OUT$;
      ELSE P$=OUT.SUBLK$(I);
      IF OUTSYM.CODE~=0 THEN GOTO CONT;
      IF SELCNT=40 THEN GOTO RETALT;
      SELCNT=SELCNT+1;
      IF SUBSTR(OUTSYM.TEXT,OUTSYM.COUNT-1,1) = '?'
      THEN OUTSYM.COUNT = OUTSYM.COUNT-1;
      SELS.L(SELCNT)=OUTSYM.COUNT;
      SELS.NAM(SELCNT)=OUTSYM.TEXT;
   END;
NOPRE:;
   IF SELCNT=0 AND DCBADDR(DCBNUM(M$UI))->F$DCB.ASN# = 1 THEN DO;
      SELCNT=1;
      SELS(1) = DCBADDR(DCBNUM(M$UI))->F$DCB.NAME#;
      IF SELS.L(1)>3 THEN SELCNT=0;
      IF SUBSTR(SELS.NAM(1),SELS.L(1)-1,1) = '?'
      THEN SELS.L(1)=SELS.L(1)-1;
   END;
%EJECT;                                 /*GET THE NEXT FILE TO DO*/
NXTF: CALL M$OPEN(OPNSI)ALTRET(RETALT);
   IF F$DCB.ORG#=2 AND F$DCB.IXTNSIZE#>10 THEN DO;
      OPNEO.V.IXTNSIZE#=10;OPNEO.V.XTNSIZE#=10;END;
   ELSE DO;OPNEO.V.IXTNSIZE#=0;OPNEO.V.XTNSIZE#=0;END;
   CALL M$OPEN(OPNEO)ALTRET(RETALT);
   P$=PINCRC(ADDR(F$DCB.NAME#),-1);
   IF OPNSI.V.OPER.NXTF# THEN CALL WSEL;
   BREAK=0;
   NAMKEY.N=0;
   NAM#=-1;
   IF F$DCB.ARS#~=0 THEN
   DO WHILE(READ.BUF_.BOUND+1~=F$DCB.ARS#);
      IF F$DCB.ARS#>READ.BUF_.BOUND+1 THEN CALL M$GDS(GDS);
      ELSE CALL M$FDS(GDS);
   END;
   CALL M$READ(READ)ALTRET(RETALT);
   IF KBUF.T.C='"' OR SUBSTR(F$DCB.TYPE#,0,1)='O' THEN GOTO OBJECT;
   IF F$DCB.ORG#~=2 THEN GOTO COPCON;
   IF KBUF.T~='005001'O AND F$DCB.TYPE#~='R' THEN GOTO COPCON;
%EJECT;                                 /*RUN UNIT: BULD THE NAME FILE*/
   DO WHILE(KBUF.T<'005001'O);
      CALL WRITE1 ALTRET(RETALT);
      CALL M$READ(READ) ALTRET(RETALT);
   END;
   DO WHILE(KBUF.T='005001'O);
      NAMKEY.S=KBUF.NAM.S;
      NAMKEY.N=0;
      WNAM.BUF_.BOUND=SIZEV(NAMES);
      WNAM.BUF_.BUF$=READ.BUF_.BUF$;
      I=SIZEC(NAMES);
      DO WHILE(I<F$DCB.ARS#);
         CALL M$WRITE(WNAM);
         NAMKEY.N=NAMKEY.N+1;
         WNAM.BUF_.BUF$=PINCRW(WNAM.BUF_.BUF$,SIZEW(NAMES));
         I=I+SIZEC(NAMES);
      END;
      WNAM.BUF_.BOUND=F$DCB.ARS#-I+SIZEV(NAMES);
      CALL M$WRITE(WNAM);
      CALL WRITE1 ALTRET(RETALT);
      CALL M$READ(READ)ALTRET(RETALT);
   END;
%EJECT;                                 /* NOW COPY THE RECORDS*/
   NAMKEY.S=511;
WRITE:CALL WRITE1 ALTRET(RETALT);
RREC:CALL M$READ(READ)ALTRET(RETALT);
   IF KBUF.T='007777'O THEN DO;
      IF NAMKEY.N~=KBUF.NAM.N/SIZEW(NAMES) OR NAMKEY.S~=KBUF.NAM.S THEN DO;
         NAMKEY.S=KBUF.NAM.S;
         NAMKEY.N=KBUF.NAM.N/SIZEW(NAMES);
         CALL M$READ(RNAM);
      END;
      P$=PINCRW(RNAM.BUF_.BUF$,MOD(KBUF.NAM.N,SIZEW(NAMES)));
      DO I=0 TO SELCNT;
         Q=SELS.L(I);
         IF SUBSTR(SELS.NAM(I),0,Q)=NAM.NAM THEN DO;
            IF NAM#~=KBUF.NAM.N THEN DO;
               NAM#=KBUF.NAM.N;
               CALL WSEL;
            END;
            GOTO WRITE;
         END;
      END;
      IF BREAK~=0 THEN GOTO ENDFILE;
      GOTO RREC;
   END;
   GOTO WRITE;
/**/
%EJECT;                                 /*OTHER FILES*/
COPCON: CALL WRITE1 ALTRET(RETALT);
   CALL M$READ(READ)ALTRET(RETALT);
   GOTO COPCON;
/**/
%EJECT;                                 /*OBJECT UNIT: GET THE OUNAMES*/
OBJECT:;
   DO WHILE(KBUF.T.C='"');
      NAM#=NAM#+1;
      NAMES.UTS(NAM#)=HREC.UTS;
      NAMES.SEL(NAM#)=0;
      P$=ADDR(KBUF);
      DO I=0 TO SELCNT;
         Q=SELS.L(I);
         IF SUBSTR(SELS.NAM(I),0,Q)=NAM.NAM THEN DO;
            NAMES.SEL(NAM#)=1;
            P$=PINCRC(P$,-1);
            CALL WSEL;
         END;
      END;
      IF NAMES.SEL(NAM#)=0 THEN HREC.DBUG=0;
      CALL WRITE1 ALTRET(RETALT);
      CALL M$READ(READ)ALTRET(RETALT);
   END;
   DO WHILE(KBUF.T.C~=BITASC('777'O));
      CALL WRITE1 ALTRET(RETALT);
      CALL M$READ(READ) ALTRET(RETALT);
   END;
   DO WHILE('1'B);
      DO I=0 TO NAM#;
         IF KBUF.UTS=NAMES.UTS(I) THEN
         IF KBUF.TYPE<11 OR KBUF.TYPE=23 OR NAMES.SEL(I)~=0 THEN
         CALL WRITE1 ALTRET(RETALT);
      END;
      CALL M$READ(READ)ALTRET(RETALT);
   END;
/**/
%EJECT;                                 /*NOW THE REST*/
WRITE1:PROC(D#) ALTRET;
DCL D# UBIN;
   IF F$DCB.ARS#=0 THEN CALL M$WRITE(WNIL)ALTRET(RETALT);
   ELSE DO;
      WREC.BUF_.BOUND=F$DCB.ARS#-1;
      CALL M$WRITE(WREC)ALTRET(RETALT);
   END;
   RETURN;
RETALT:ALTRETURN;
/**/
WSEL: ENTRY ALTRET;
   WSL.BUF_.BOUND=NAM.L-1;
   WSL.BUF_.BUF$=PINCRC(P$,2);
   CALL M$WRITE(WSL);
   RETURN;
/**/
CLOSE:ENTRY(D#)ALTRET;
   CLSS.V.DCB#=D#;
   CALL M$CLOSE(CLSS)ALTRET(RETALT);
END;
/**/
RETALT:;
   IF B$TCB.ALT$->B$ALT.ERR.CODE=%E$LD THEN DO;
      CALL M$GDS(GDS)ALTRET(RETALT);
      CALL M$PRECORD(PREC)ALTRET(RETALT);
      CALL M$RETRY;
   END;
   IF B$TCB.ALT$->B$ALT.ERR.CODE=%E$DI AND F$DCB.IORG=3 THEN CALL M$RETRY;
   IF B$TCB.ALT$->B$ALT.ERR.CODE=%E$EOF THEN DO;
ENDFILE:;
      CALL CLOSE(DCBNUM(M$OU))ALTRET(RETALT);
      CALL CLOSE(DCBNUM(M$SI))ALTRET(RETALT);
      CALL CLOSE(DCBNUM(M$LO))ALTRET(RETALT);
      OPNSI.V.OPER.THISF#='0'B;
      IF BREAK=0 AND OPNSI.V.OPER.NXTF# THEN GOTO NXTF;
      CALL M$EXIT;
   END;
   CALL M$MERC;
END;
%EOD;
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
MPURBK: PROC ASYNC;
DCL BREAK SBIN SYMREF;
   BREAK=BREAK+1;
END;
