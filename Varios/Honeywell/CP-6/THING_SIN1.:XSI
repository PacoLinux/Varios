/*M*  THING_SIP   Option nodes for THING processor */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
           
%INCLUDE THING_C61;

/*
      Command-line options for THING
*/
 
THING_OPTS (DEF,OUT) = '(' < ',', THING_OPTION > [')'] .END
 
THING_OPTION = {SYSID_OPTION | INTERVAL_OPTION | ALERT_OPTION | ;
                 MESSAGE_FORMAT_OPTION | QUIT_OPTION | ;
                 BELL_SELECTION | MAIL_SELECTION | CALENDAR_SELECTION | ;
                 OPTION_OPTION | ;
                 SALUTATION_SELECTION | AUTOSCHED_SELECTION }
 
SYSID_OPTION (%SYSID_OPTION) = 'SY/SID' '=' .DECIMAL
 
INTERVAL_OPTION (%INTERVAL_OPTION) = 'I/NTERVAL' '=' .DECIMAL
 
ALERT_OPTION (%ALERT_OPTION) = 'A/LERT' '=' ALERT_VALUES
 
ALERT_VALUES = ( .DECIMAL | '(' < ',', .DECIMAL > ')' )
 
MESSAGE_FORMAT_OPTION = (LONGFORM_OPTION | SHORTFORM_OPTION | ;
                         MESSAGELINE_OPTION)
 
LONGFORM_OPTION (%LONGFORM_OPTION) = ( 'LO/NG' | 'FU/LL' )
 
SHORTFORM_OPTION (%SHORTFORM_OPTION) = ( 'SH/ORT' | 'BR/IEF' )
 
MESSAGELINE_OPTION (%MESSAGELINE_OPTION) = 'ME/SSAGE' ['LINE']
 
QUIT_OPTION (%QUIT_OPTION) = ( 'Q/UIT' | 'ST/OP' )
 
BELL_SELECTION = { BELL_OPTION | NOBELL_OPTION }
 
BELL_OPTION (%BELL_OPTION) = 'BE/LL'
 
NOBELL_OPTION (%NOBELL_OPTION) = 'NOBE/LL'
 
MAIL_SELECTION = { MAIL_OPTION | NOMAIL_OPTION }
 
MAIL_OPTION (%MAIL_OPTION) = 'MA/IL' [ '=' MAIL_FOLDER ]

MAIL_FOLDER (NOOUT) = (MAIL1 | MAIL2 | MAIL3 | MAIL4)
MAIL1 (NOOUT) = NAME [FOLDER] ['@' SITE]
MAIL2 (NOOUT) = FOLDER ['.' [ACCOUNT]] ['@' SITE]
MAIL3 (NOOUT) = '.'[ACCOUNT]['@' SITE]
MAIL4 (NOOUT) = '@' SITE
 
NAME (%NAME ) = (.ALPHA+'0123456789$-_# ' | .QUOTE)
FOLDER (%FOLDER) = '/' .ALPHA25+'0123456789$-_:'
ACCOUNT (%ACCOUNT) = .ALPHA8+'0123456789$-_:'
SITE (%SITE) = (.ALPHA+'0123456789$-_:' | .QUOTE)
 
NOMAIL_OPTION (%NOMAIL_OPTION) = 'NOMA/IL'
 
CALENDAR_SELECTION = { CALENDAR_OPTION | NOCALENDAR_OPTION }
 
CALENDAR_OPTION (%CALENDAR_OPTION) = 'CA/LENDAR'
 
NOCALENDAR_OPTION (%NOCALENDAR_OPTION) = 'NOCA/LENDAR'
 
SALUTATION_SELECTION = { SALUTATION_OPTION | NOSALUTATION_OPTION }
 
SALUTATION_OPTION (%SALUTATION_OPTION) = 'SA/LUTATION'
 
NOSALUTATION_OPTION (%NOSALUTATION_OPTION) = 'NOSA/LUTATION'
 
AUTOSCHED_SELECTION = { AUTOSCHED_OPTION | NOAUTOSCHED_OPTION }
 
AUTOSCHED_OPTION (%AUTOSCHED_OPTION) = 'AU/TOSCHED'
 
NOAUTOSCHED_OPTION (%NOAUTOSCHED_OPTION) = 'NOAU/TOSCHED'
 
OPTION_OPTION (%OPTION_OPTION) = 'OPT/IONS'
 
/*
      Date and time formats for THING
*/
 
DATE_STRING (D, OUT) = '[' DATE_ITEMS ']' .END
 
DATE_ITEMS = < DELIM, { DAY | DATE | TIME | PLACE } >
 
/*
   Define delimiters permissible between scheduling items
*/
 
DELIM = (',' | ' ' | .BLANK)
 
/*
   Define what a day-of-the-week name looks like
*/
 
DAY = (MONDAY | TUESDAY | WEDNESDAY | THURSDAY | FRIDAY | SATURDAY | SUNDAY | ;
       TODAY | TOMORROW)
 
MONDAY    (%MONDAY)    = 'MON/DAY'- REST_OF_NAME
TUESDAY   (%TUESDAY)   = 'TUE/SDAY'- REST_OF_NAME
WEDNESDAY (%WEDNESDAY) = 'WED/NESDAY'- REST_OF_NAME
THURSDAY  (%THURSDAY)  = 'THU/RSDAY'- REST_OF_NAME
FRIDAY    (%FRIDAY)    = 'FRI/DAY'- REST_OF_NAME
SATURDAY  (%SATURDAY)  = 'SAT/URDAY'- REST_OF_NAME
SUNDAY    (%SUNDAY)    = 'SUN/DAY'- REST_OF_NAME
TODAY     (%TODAY)     = 'TOD/AY'- REST_OF_NAME
TOMORROW  (%TOMORROW)  = 'TOM/MORROW'- REST_OF_NAME
 
REST_OF_NAME(NO) = .ASYM+' ,'
 
/*
   Define what a date looks like
*/
 
DATE = ( NAMED_MONTH_AND_DAY | NUMBERED_M_D | YYMMDD )
 
/*
   Define a named month & day combination
*/
 
NAMED_MONTH_AND_DAY  = MONTH_NAME DELIM DAY_NUMBER ;
                       [('ND/.'|'TH/.'|'RD/.'|'ST/.')] [ DELIM YEAR_NUMBER ]
 
MONTH_NAME = (JANUARY | FEBRUARY | MARCH | APRIL | MAY | JUNE | ;
              JULY | AUGUST | SEPTEMBER | OCTOBER | NOVEMBER | DECEMBER)
 
JANUARY   (%JANUARY)   = 'JAN/UARY'- REST_OF_NAME
FEBRUARY  (%FEBRUARY)  = 'FEB/RUARY'- REST_OF_NAME
MARCH     (%MARCH)     = 'MAR/CH'- REST_OF_NAME
APRIL     (%APRIL)     = 'APR/IL'- REST_OF_NAME
MAY       (%MAY)       = 'MAY'- REST_OF_NAME
JUNE      (%JUNE)      = 'JUN/E'- REST_OF_NAME
JULY      (%JULY)      = 'JUL/Y'- REST_OF_NAME
AUGUST    (%AUGUST)    = 'AUG/UST'- REST_OF_NAME
SEPTEMBER (%SEPTEMBER) = 'SEP/TEMBER'- REST_OF_NAME
OCTOBER   (%OCTOBER)   = 'OCT/OBER'- REST_OF_NAME
NOVEMBER  (%NOVEMBER)  = 'NOV/EMBER'- REST_OF_NAME
DECEMBER  (%DECEMBER)  = 'DEC/EMBER'- REST_OF_NAME
 
/*
   Define a numbered month/day or month/day/year
*/
 
NUMBERED_M_D = ( MONTH_NUMBER '/' DAY_NUMBER [ '/' YEAR_NUMBER ] | ;
                 YEAR_NUMBER  '.' MONTH_NUMBER '.' DAY_NUMBER       )
 
MONTH_NUMBER (%MONTH_NUMBER)       = .DEC2
DAY_NUMBER   (%DAY_NUMBER)         = .DEC2
YEAR_NUMBER  (%YEAR_NUMBER, SUCC)  = .DEC4
 
/*
   Define a yymmdd-style date
*/
 
YYMMDD (%YYMMDD, SUCC) = .DEC6
 
/*
   Define a time
*/
 
TIME               =  ( ( HOUR [':' [MINUTES]] | HHMM) [ (AM | PM | NOON) ] | ;
                                                                    NOON      )
 
HOUR    (%HOUR)    = .DEC2
MINUTES (%MINUTES) = .DEC2
 
HHMM    (%HHMM)    = .DEC4
 
AM      (%AM)      = 'A/.' 'M/.'
PM      (%PM)      = 'P/.' 'M/.'
NOON    (%NOON)    = 'NOON/.'
 
/*
   Define what a meeting-place looks like
*/
 
PLACE   (%PLACE)   = .ASYM+'],'
