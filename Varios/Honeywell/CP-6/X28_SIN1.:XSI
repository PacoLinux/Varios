/*M*   X28_SIN1 - X28 PARSE NODES
                                                                              */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
%INCLUDE X28_C61;
       %SET LISTEXP='1'B;
 
       XDIGITS = .DECIMAL +'ABCDEF'
       XSTRING (%X28_XSTRING) = '.' *XDIGITS*
       PREP(%X28_PREP) = ( ON | TO | OVER | INTO )
       ON(%X28_ON) = 'ON'
       TO(%X28_TO) = 'TO'
       OVER(%X28_OVER) = 'OVER'
       INTO(%X28_INTO) = 'INTO'
/* */
/*
       COMMANDS
                                                                              */
       COMMAND = ( CON | CLR | RST | PAR | SET | RSM | END | QUIT | DIS ;
                 | DEF | DEL | USE | RNAME | TRACE ) .END
/*
       CONNECT COMMAND
                                                                              */
       CON(%X28_CON) = 'C/ONNECT' .B ( .DECIMAL (.B|.END)| .AN8 ) ;
                       [ .B <',', CALL_OPT_LIST>] .END
 
       CALL_OPT_LIST(%X28_CALL_OPT_LIST) = ( REVERSE | FAST | DATA | RNAMOPT )
 
       REVERSE(%X28_REVERSE) = 'R' '=' ( YES | NO )
 
       YES(%X28_YES) = 'Y/ES'
 
       NO(%X28_NO) = 'N/O'
 
       FAST(%X28_FAST) = 'FS' '=' ( NO | RESTRICT | UNRESTRICT )
 
       RESTRICT(%X28_RESTRICT) = 'R/ESTRICT'
 
       UNRESTRICT(%X28_UNRESTRICT) = 'U/NRESTRICT'
 
       DATA(%X28_DATA) = 'D' '=' .HEX
 
       RNAMOPT(%X28_RN) = '@' (.AN6 | .END)
 
/*
       CLEAR COMMAND
                                                                              */
       CLR(%X28_CLR) = ( 'CLR' | 'CLEAR' ) .END
/*
       RESET COMMAND
                                                                              */
       RST(%X28_RST) = ( 'RST' | 'RESET' ) .END
/*
       PARameter command; read individual or all x.3 parameter(s)
       current session assumed.
*/
       PAR(%X28_PAR) = 'PAR' (.DECIMAL | .END )
/*
       SET command; set individual x.3 parameter
       current session assumed.
*/
       SET(%X28_SET) = 'SET' .B .DECIMAL '=' .DECIMAL .END
/*
       ReSuMe command.
*/
       RSM(%X28_RSM) = ( 'RSM' | 'RESUME' ) .END
/*
       END COMMAND
                                                                              */
       END(%X28_END) = 'E/ND'
       QUIT(%X28_QUIT) = 'Q/UIT'
/*
       DISplay command. Displays network addresses
*/
       DIS(%X28_DIS) = 'D/ISPLAY' (.B .AN8 | .END )
/*
       DEFine command. Define new network addresses
*/
       DEF(%X28_DEF) = 'DEF/INE' .B .AN8 '=' .DECIMAL ( .ASYM54 | .END )
/*
       DELete command. Delete network addresses
*/
       DEL(%X28_DEL) = ('DEL/ETE' .B .AN8 | 'DELETEALL' .END )
/*
       USE command. Use :NETADR from a different account
*/
       USE(%X28_USE) = 'U/SE' ( ['.'-] .ACCOUNT8 | .END )
/*
       RNAME command. Set SN in M$OPEN
*/
       RNAME(%X28_RN) = 'RN/AME' (.B .AN6 | .END )
/*
       TRACE command. Trace incoming packets
*/
       TRACE(%X28_TR) = 'TR/ACE' (.B PREP .B .FID | .END )
