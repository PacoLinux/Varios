/*M*       SLIT - Status LIne Time  */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
SLIT:  PROC MAIN NOAUTO;
%INCLUDE CP_6;
  %F$DCB;
%INCLUDE CP_6_SUBS;
%INCLUDE B$JIT;
%LIST;
%INCLUDE SLIT_C61;
%PLIST;
%FPT_TIME(TIME=TIME,DATE=DATE,SOURCE=CLOCK,DEST=ANS,STCLASS=STATIC);
%FPT_TIME(FPTN=GET_UTS,DEST=UTS,TSTAMP=UTS);
%FPT_TIME(FPTN=UTS_TO_ANS,SOURCE=UTS,DEST=ANS,DATE=DATE,TIME=TIME,TSTAMP=UTS);
%FPT_OPEN(ASN=DEVICE,RES='UC  ',FUN=UPDATE,DCB=M$1,ORG=TERMINAL,
    STCLASS=STATIC);
%FPT_WRITE(DCB=M$1,BUF=BUFFER,STCLASS=STATIC,TRANS=YES);
%FPT_WRITE(FPTN=FPT_WRITEDO,DCB=M$DO,BUF=NOCLOCK,STCLASS=STATIC);
%FPT_GLINEATTR(LINEATTR=VLP_LINEATTR,DCB=M$1);
%VLP_LINEATTR(STCLASS=STATIC);
%INCLUDE XU_MACRO_C;
%PARSE$SYM(NAME=OUT$SYM,STCLASS=BASED);
%PARSE$OUT(NAME=OUT$BLK,STCLASS=BASED);
%P_PCB(NAME=PCB,STCLASS=STATIC,
 W=PARSE_WORK,
 WSZ="SIZEW(PARSE_WORK)",
 R=SLIT_NODES);
%EQU MAX_PRO = 7;
DCL PARSE_WORK(0:199) UBIN;
DCL SLIT_NODES SBIN SYMREF;
 
DCL M$1 DCB;
DCL M$DO DCB;
DCL M$ME DCB;
DCL M$SI DCB;
DCL B$JIT$ PTR SYMREF;
 
DCL X$PARSE ENTRY(1) ALTRET;
 
DCL TIME CHAR(8) STATIC;
DCL DATE CHAR(6) STATIC;
DCL BUFFER CHAR(40);
DCL ESC CHAR(1) INIT(BITASC('033'O)) STATIC;
DCL NOCLOCK CHAR(0) INIT('I don''t think you have a clock!') STATIC;
DCL I UBIN;
DCL PROFILE CHAR(11) STATIC INIT(' ');
DCL N UBIN;
DCL J UBIN;
DCL TIME_OFFSET SBIN STATIC INIT(0);
DCL BLK$ PTR;
DCL UTS UBIN STATIC;
DCL PRO(0:%MAX_PRO) CHAR(12) CONSTANT INIT(
    '7801FC','7814','BEEDM20','PCTV7800','PCTV7800GR',
    'PCTX364','PCTX364GR','PCTV7800INV');
 
    CALL M$OPEN(FPT_OPEN) ALTRET(ERR);
 
    IF B$JIT.CCDISP = B$JIT.CCARS THEN DO;
        IF DCBADDR(DCBNUM(M$SI)) -> F$DCB.AMR# THEN
           PROFILE=DCBADDR(DCBNUM(M$SI)) -> F$DCB.NAME#.C;
        END;
    ELSE DO;
       PCB.TEXT$= PINCRC(ADDR(B$JIT.CCBUF),B$JIT.CCDISP);
       PCB.NCHARS= B$JIT.CCARS - B$JIT.CCDISP;
       CALL X$PARSE(PCB)  ALTRET( ERR );
       DO I=0 TO PCB.OUT$ -> OUT$BLK.NSUBLKS - 1;
          BLK$= PCB.OUT$ -> OUT$BLK.SUBLK$(I) -> OUT$BLK.SUBLK$(0);
          DO CASE( PCB.OUT$ -> OUT$BLK.SUBLK$(I) -> OUT$SYM.CODE );
             CASE( %PROFILE## );
                PROFILE= BLK$ -> OUT$SYM.TEXT;
 
             CASE( %TIME_MINUS## );
                CALL CHARBIN( TIME_OFFSET,SUBSTR(BLK$->OUT$SYM.TEXT,0,
                                                 BLK$->OUT$SYM.COUNT) );
                TIME_OFFSET= 0 - TIME_OFFSET; /* Make value negative */
 
             CASE( %TIME_PLUS## );
                CALL CHARBIN( TIME_OFFSET,SUBSTR(BLK$->OUT$SYM.TEXT,0,
                                                 BLK$->OUT$SYM.COUNT) );
             CASE( ELSE );
 
             END; /* DO CASE */
          END; /* DO I */
 
       END; /* ELSE */
 
    IF PROFILE = ' ' THEN DO;
       CALL M$GLINEATTR(FPT_GLINEATTR)  ALTRET( ERR );
       PROFILE= VLP_LINEATTR.PROTXTC.TXT;
       END;
 
    DO N=0 TO %MAX_PRO;
        CALL INDEX(J,' ',PRO(N));
        CALL INDEX(I,SUBSTR(PRO(N),0,J),PROFILE)
        WHENRETURN DO;
            GOTO FOUND;
            END;
        END;
    CALL M$WRITE(FPT_WRITEDO) ALTRET(ERR);
    GOTO RET;
FOUND:  ;
    CALL M$TIME(FPT_TIME) ALTRET(ERR);
 
    IF TIME_OFFSET ~= 0 THEN DO; /* Did they specify a time offset? */
       CALL M$TIME( GET_UTS )  ALTRET( ERR );
       UTS= UTS + (TIME_OFFSET * 60 * 60 * 100);
       CALL M$TIME( UTS_TO_ANS )  ALTRET( ERR );
       END;
    DO CASE(N);
    CASE(0,3,4,5,6,7);
/*          VIP7801FC,PCTV7800,PCTV7800GR,PCTX364,PCTX364GR,ETAL */
/*          Concat only takes 6 strings at a time */
        CALL CONCAT(BUFFER,ESC,'[',SUBSTR(TIME,0,4),';',
                    SUBSTR(TIME,4,4),';');
        CALL CONCAT(SUBSTR(BUFFER,12,10),'19',SUBSTR(DATE,0,2),';',
                    SUBSTR(DATE,2,4),'t');
        FPT_WRITE.BUF_.BOUND=21;
    CASE(1);
/*          7814    */
         CALL CONCAT(BUFFER,ESC,'[',SUBSTR(TIME,0,4),'t');
        FPT_WRITE.BUF_.BOUND=6;
    CASE(2);
/*          BEEDM20 */
       CALL CONCAT(BUFFER,ESC,' ',SUBSTR(TIME,0,4));
        FPT_WRITE.BUF_.BOUND=5;
    CASE( ELSE );
       CALL CONCAT( BUFFER,'SLIT error: no CASE for profile of ',PROFILE);
       FPT_WRITEDO.BUF_= VECTOR(BUFFER);
       CALL M$WRITE(FPT_WRITEDO);
       CALL M$ERR;
       END; /* CASE */
 
    CALL M$WRITE(FPT_WRITE) ALTRET(ERR);
 
        PCB.TEXT$=PINCRC(ADDR(B$JIT.CCBUF),B$JIT.CCDISP);
        PCB.NCHARS=B$JIT.CCARS-B$JIT.CCDISP;
 
RET:   ;
   CALL M$EXIT;
ERR:    CALL M$MERC;
END SLIT;
