/*M* WOCKY - tells me what a key does */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
/*X* DMR,PLM=5,IND=3,CTI=3,SDI=3,MCL=10,CSI=0,ECI=0 */
 
 
%SET LISTSUB='1'B;
%SET LISTEXP='1'B;
 
WOCKY:PROC MAIN;
 
DCL M$TERM DCB;
DCL M$DO DCB;
DCL GET_OUT ENTRY ASYNC;
 
%INCLUDE CP_6;
 
     %F$DCB;
 
%FPT_OPEN (FPTN=OPEN_TERM,
               DCB=M$TERM,
               FUN=IN,
               ORG=TERMINAL);
 
%FPT_READ (FPTN=READ_RBIN,
               DCB=M$TERM,
               TRANS=YES,
               BUF=MYBUF);
 
%FPT_WRITE (FPTN=WRITE_IT_OUT,
               VFC=YES,
               DCB=M$DO,
               BUF=OUTBUF_);
 
%FPT_INT (FPTN=WHAT_TO_DO,
               UENTRY=GET_OUT);
 
%FPT_WRITE (FPTN=PROMPT_,
               DCB=M$DO,
               VFC=YES,
               BUF=PROMPT_STR);
 
DCL PROMPT_STR (0:1) BIT(9) STATIC CALIGNED INIT ('036'O,'176'O);
DCL 1 OUTBUF_ STATIC CALIGNED,
       2 VFC_ CHAR(1) CALIGNED INIT('@'),
       2 CHAR_ CHAR(5) CALIGNED INIT ('*****'),
       2 PAR_ CHAR(3) CALIGNED INIT ('---');
DCL MYBUF CHAR(1) STATIC CALIGNED;
DCL MYBUF_UBIN REDEF MYBUF UBIN BYTE CALIGNED;
DCL F$TERM$ PTR STATIC;
DCL PARITY_ BIT(1) ALIGNED STATIC INIT('0'B);
DCL LOOKUP (0:127) CHAR(5) STATIC INIT
    (' NUL ',' SOH ',' STX ',' ETX ',' EOT ',' ENQ ',' ACK ',' BEL ',
     ' BS  ',' HT  ',' LF  ',' VT  ',' FF  ',' CR  ',' SO  ',' SI  ',
     ' DLE ',' DC1 ',' DC2 ',' DC3 ',' DC4 ',' NAK ',' SYN ',' ETB ',
     ' CAN ',' EM  ',' SUB ',' ESC ',' FS  ',' GS  ',' RS  ',' US  ',
     ' SP  ',' !   ',' "   ',' #   ',' $   ',' %   ',' &   ',' ''   ',
     ' (   ',' )   ',' *   ',' +   ',' ,   ',' -   ',' .   ',' /   ',
     ' 0   ',' 1   ',' 2   ',' 3   ',' 4   ',' 5   ',' 6   ',' 7   ',
     ' 8   ',' 9   ',' :   ',' ;   ',' <   ',' =   ',' >   ',' ?   ',
     ' @   ',' A   ',' B   ',' C   ',' D   ',' E   ',' F   ',' G   ',
     ' H   ',' I   ',' J   ',' K   ',' L   ',' M   ',' N   ',' O   ',
     ' P   ',' Q   ',' R   ',' S   ',' T   ',' U   ',' V   ',' W   ',
     ' X   ',' Y   ',' Z   ',' [   ',' \   ',' ]   ',' ^   ',' _   ',
     ' `   ',' a   ',' b   ',' c   ',' d   ',' e   ',' f   ',' g   ',
     ' h   ',' i   ',' j   ',' k   ',' l   ',' m   ',' n   ',' o   ',
     ' p   ',' q   ',' r   ',' s   ',' t   ',' u   ',' v   ',' w   ',
     ' x   ',' y   ',' z   ',' {   ',' |   ',' }   ',' ~   ',' DEL ');
 
 
DCL SAVECHAR UBIN;
 
DCL SAMECOUNT UBIN;
 
%EJECT;
 
/*
          BEGIN WOCKY MAIN
                                                                  */
 
     CALL M$INT (WHAT_TO_DO)
     WHENALTRETURN
     DO;
        CALL M$XXX;
        END;
 
     SAMECOUNT = 0;
     SAVECHAR = 65535;
 
     CALL M$OPEN (OPEN_TERM)
     WHENALTRETURN
     DO;
        CALL M$XXX;
        END;
 
     F$TERM$ = DCBADDR(DCBNUM(M$TERM));
 
TOP_: ;
     PARITY_ = '0'B;
     OUTBUF_.PAR_ = ' ';
 
     CALL M$WRITE (PROMPT_);
 
     CALL M$READ(READ_RBIN)
     WHENALTRETURN
     DO;
        CALL M$XXX;
        END;
 
     IF F$TERM$ -> F$DCB.ARS# = 0
     THEN
     DO;                           /* The guy's got ACTONTRN set...the bozo */
        MYBUF = F$TERM$ -> F$DCB.EOMCHAR#;
        END;
 
     IF MYBUF_UBIN > 127
     THEN
     DO;
        MYBUF_UBIN = MYBUF_UBIN - 128;
        PARITY_ = '1'B;
        END;
 
     IF MYBUF_UBIN = SAVECHAR
     THEN DO;
        SAMECOUNT = SAMECOUNT + 1;
        IF SAMECOUNT >= 5
        THEN CALL M$EXIT;
        END;
     ELSE DO;
        SAMECOUNT = 0;
        SAVECHAR = MYBUF_UBIN;
        END;
 
 
     OUTBUF_.CHAR_ = LOOKUP(MYBUF_UBIN);
 
     IF PARITY_
     THEN
     DO;
        OUTBUF_.PAR_ = '[P]';
        END;
     CALL M$WRITE (WRITE_IT_OUT)
     WHENALTRETURN
     DO;
        CALL M$XXX;
        END;
 
     GOTO TOP_;
 
END WOCKY;
%EOD;
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
GET_OUT:PROC ASYNC;
 
%INCLUDE CP_6;
 
     CALL M$EXIT;
 
END GET_OUT;
