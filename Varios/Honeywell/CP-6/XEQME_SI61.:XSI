/*M* XEQME - XEQ's the EDIT file interwoven with itself. */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
/*X* */
XEQME:  PROC MAIN NOAUTO;
/**/
%INCLUDE CP_6;
%INCLUDE B_ERRORS_C;
%INCLUDE B$JIT;
%F$DCB;
/**/
DCL B$JIT$ PTR SYMREF;
DCL M$LM DCB;
DCL M$DO DCB;
/**/
%FPT_OPEN(DCB=M$DO,FPTN=OPEN,ASN=DEVICE,RES='JF',FUN=CREATE,
        WSN=ERASE,STCLASS=CONSTANT);
%FPT_OPEN(DCB=M$DO,FPTN=RESETDO,SETDCB=YES,REASSIGN=YES,SCRUB=YES,
        STCLASS=CONSTANT);
%FPT_REW(DCB=M$LM,FPTN=REW,STCLASS=CONSTANT);
%FPT_READ(DCB=M$LM,FPTN=READ,STCLASS=CONSTANT,
        BUF=B,KEY=K,KEYS=NO,KEYR=YES);
%FPT_WRITE(DCB=M$DO,FPTN=WRITE,STCLASS=STATIC,BUF=B);
%FPT_WRITE(DCB=M$DO,FPTN=WRNIL,STCLASS=STATIC,BUF=NIL);
%FPT_CLOSE(DCB=M$DO,FPTN=CLOSE,DISP=SAVE,STCLASS=CONSTANT);
%FPT_ERRMSG(FPTN=ERRMSG,STCLASS=CONSTANT,CODE=J,BUF=B,
        OUTDCB1=M$DO);
/**/
DCL J BIT(36)STATIC INIT('0'B);
DCL X SBIN STATIC;
DCL B CHAR(256) STATIC INIT ('!XMIT ME(');
DCL 1 K STATIC,
      2 L UBIN(9)UNAL,
      2 C CHAR(255);
%EJECT;
        CALL M$REW(REW)ALTRET(VERYBAD);
        CALL M$OPEN(OPEN)ALTRET(VERYBAD);
        CALL INDEX(X,' ',B$JIT.CCBUF);
        SUBSTR(B,9)=SUBSTR(B$JIT.CCBUF,X,B$JIT.CCARS-X);
        X=9+B$JIT.CCARS-X;
        IF SUBSTR(B,9)=' ' THEN SUBSTR(B,8,1)=' ';
        ELSE SUBSTR(B,X,1)=')';
        SUBSTR(B,X+1)=' TO JF';
        WRITE.BUF_.BOUND=X+6;
        CALL M$WRITE(WRITE)ALTRET(VERYBAD);
NXTREC: CALL M$READ(READ) ALTRET(ALTR);
        IF K.L=3 THEN
        DO CASE(DCBADDR(DCBNUM(M$LM))->F$DCB.ARS#);
         CASE(0);
           CALL M$WRITE(WRNIL)ALTRET(VERYBAD);
         CASE(ELSE);
           X=DCBADDR(DCBNUM(M$LM))->F$DCB.ARS#-1;
           WRITE.BUF_.BOUND=X;
           IF X~=0 AND SUBSTR(B,0,2)='!!'
             OR X>2 AND SUBSTR(B,0,X+1)='!EOD' THEN DO;
              WRITE.BUF_.BOUND=X+1;
              DO UNTIL X<0;
                 SUBSTR(B,X+1,1)=SUBSTR(B,X,1);
                 X=X-1;
                 END;
              END;
           CALL M$WRITE(WRITE)ALTRET(VERYBAD);
         END;
        GOTO NXTREC;
/**/
ALTR:   IF B$JIT.ERR.CODE=%E$LD AND K.L~=3 THEN
           GOTO NXTREC;
        IF B$JIT.ERR.CODE~=%E$EOF THEN
VERYBAD:   J=B$JIT.ERR;
        WRITE.BUF_.BOUND=3;
        B='!EOD';
        CALL M$WRITE(WRITE) WHENALTRETURN DO; END;
        CALL M$CLOSE(CLOSE)ALTRET(KEEPON);
KEEPON: CALL M$OPEN(RESETDO);
        IF J THEN
           CALL M$ERRMSG(ERRMSG);
        CALL M$EXIT;
END XEQME;
