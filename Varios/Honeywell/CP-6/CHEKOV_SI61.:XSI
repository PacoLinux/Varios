/*M* CHEKOV - removes the CHECK flag for all ENTDEFs in an OU */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X* DMR,PLM=5,IND=3,CTI=3,SDI=3,MCL=10,CSI=0,ECI=0 */
 
 
%SET LISTSUB='1'B;
%SET LISTEXP='1'B;
 
CHEKOV: PROC MAIN;
 
%INCLUDE CP_6;
%INCLUDE B$OBJECT_C;
 
DCL M$SI DCB;
 
DCL BUF_ (0:4095) UBIN WORD STATIC SYMDEF;
DCL BUF$ PTR STATIC SYMDEF INIT(ADDR(BUF_));
DCL 1 KEYR_ STATIC SYMDEF CALIGNED,
       2 SIZE_ UBIN BYTE CALIGNED,
       2 REST_ CHAR(240) CALIGNED;
DCL M$SI$ PTR STATIC SYMDEF;
DCL I SBIN WORD STATIC SYMDEF;
DCL J SBIN WORD STATIC SYMDEF;
DCL K SBIN WORD STATIC SYMDEF;
 
%F$DCB;
DCL LOOP_ SBIN WORD STATIC SYMDEF;
 
%B$HEADKEY;
 
%B$OUHEAD;
 
%B$OUKEY;
 
%B$EDEF;
 
%B$RECORDSUBS;
 
     %FPT_OPEN      (FPTN=OPEN_SI,
                         DCB=M$SI,
                         ACS=DIRECT,
                         FUN=UPDATE);
 
     %FPT_READ      (FPTN=READ_SI,
                         DCB=M$SI,
                         KEY=KEYR_,
                         BUF=BUF_,
                         KEYR=YES);
 
     %FPT_WRITE     (FPTN=REWRITE_SI,
                         DCB=M$SI,
                         KEY=B$OUKEY,
                         BUF=BUF_);
 
     %FPT_CLOSE     (FPTN=CLOSE_SI,
                         DCB=M$SI,
                         DISP=SAVE);
 
 
%EJECT;
 
     M$SI$ = DCBADDR(DCBNUM(M$SI));
 
     CALL M$OPEN (OPEN_SI)
     WHENALTRETURN
     DO;
        CALL M$MERC;
        END;
 
     DO WHILE('1'B);
        CALL M$READ (READ_SI)
        WHENALTRETURN
        DO;
           GOTO GET_OUT;
           END;
 
        B$OUKEY = KEYR_;
 
        IF B$OUKEY.TYPE = %TYPEDEF
        THEN
        DO;
           BUF$ = ADDR(BUF_);
           LOOP_ = M$SI$->F$DCB.ARS# / SIZEC(B$EDEF);
           REWRITE_SI.BUF_.BOUND = M$SI$->F$DCB.ARS# - 1;
 
           DO I = 1 TO LOOP_;
              BUF$ -> B$EDEF.CHECK = '0'B;
              BUF$ = PINCRC(BUF$,SIZEC(B$EDEF));
              END;                      /* DO I */
 
           CALL M$WRITE (REWRITE_SI)
           WHENALTRETURN
           DO;
              CALL M$MERC;
              END;
 
           END;                         /* DO IF AN ENTDEF RECORD */
 
        END;                            /* DO WHILE TRUE */
 
GET_OUT: ;
     CALL M$CLOSE (CLOSE_SI)
     WHENALTRETURN
     DO;
        CALL M$MERC;
        END;
 
     CALL M$EXIT;
 
END CHEKOV;
