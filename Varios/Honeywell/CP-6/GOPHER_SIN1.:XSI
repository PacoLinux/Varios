/*M* GOPHER_SIN - parse nodes for GOPHER.X */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
%LIST;
%INCLUDE GOPHER_C61;
%PLIST;
%EJECT;
GOPHER_R(%GOPHER##,D) = ( ['('] <',',GOPHER_CMDS> [')'] .END )
 
GOPHER_CMDS                  = ( AFTER           | ;
                                 ANY_FILE        | ;
                                 BEFORE          | ;
                                 COUNTFILES      | ;
                                 ECHO            | ;
                                 END             | ;
                                 GREETING        | ;
                                 IGNORE_ERRORS   | ;
                                 IGNORE_ORGS     | ;
                                 IGNORE_TYPES    | ;
                                 LS_DEFAULTS     | ;
                                 MAKESTD         | ;
                                 NO_DEFAULTS     | ;
                                 NO_GREETING     | ;
                                 NO_LS_DEFAULTS  | ;
                                 NO_PROMPT       | ;
                                 NO_SUMMARY      | ;
                                 NRECS           | ;
                                 ORG             | ;
                                 PROMPT          | ;
                                 QUIT            | ;
                                 RESIDUAL        | ;
                                 SKIP            | ;
                                 SRCH            | ;
                                 START           | ;
                                 STD             | ;
                                 STRINGS         | ;
                                 SUMMARY         | ;
                                 TALLYONLY       | ;
                                 TALLY           | ;
                                 TEXT_FILES_ONLY | ;
                                 TYPE            | ;
                                 UC              | ;
                                 UISRCH          | ;
                                 UI              | ;
                                 WARNINGS        | ;
                                 WILDCARD        | ;
                                 EBCDIC          | ;
                                 DEPTH           )
 
AFTER(%AFTER##)              = 'AF/TER' '=' .DEC1
ANY_FILE(%ANY_FILE##)        = 'AN/Y' ['F/ILES']
BEFORE(%BEFORE##)            = 'B/EFORE' '=' .DEC1
COUNTFILES(%COUNTFILES##)                = 'COUNTF/ILES'
DEPTH(%DEPTH##)              = 'D/EPTH' '=' .DEC
EBCDIC(%EBCDIC##)            = ('EB/CDIC' | 'EBC/DIC' | 'EBCDIC')
ECHO(%ECHO##)                = 'EC/HO' ['F/IDS']
END(%END##)                  = 'E/ND' '=' ONE_FID
GREETING(%GREETING##)        = 'G/REETING'
IGNORE_ERRORS(%IGNORE_ERRORS##) = ('I/GNORE' 'E/RRORS' | 'N/O' 'W/ARNINGS' )
IGNORE_ORGS(%IGNORE_ORGS##)  = ( 'I/GNORE' | 'N' ) 'O/RGS' ORGS
IGNORE_TYPES(%IGNORE_TYPES##) = ( 'I/GNORE' | 'N' ) 'T/YPES' TYPES
LS_DEFAULTS(%LS_DEFAULTS##)  = 'L/I' ['S/T'] 'D/EFAULTS'
MAKESTD(%MAKESTD##)          = 'M/AKE' ['S/TD']
NO_DEFAULTS(%NO_DEFAULTS##)  =('N/O' 'D/EFAULTS' | 'D/ONT' ['U/SE'] 'D/EFAULTS')
NO_GREETING(%NO_GREETING##)  = ('N/O' 'G/REETING' | 'D/ONT' 'G/REET' )
NO_LS_DEFAULTS(%NO_LS_DEFAULTS##)= ('N/O' 'L/S' 'D/EFAULTS' | ;
                                    'D/ONT' 'L/I' 'S/T' 'D/EFAULTS' )
NO_PROMPT(%NO_PROMPT##)      = ( 'N/O' 'P/ROMPT' | 'D/ONT' 'P/ROMPT' )
NO_SUMMARY(%NO_SUMMARY##)    = ( 'N/O' 'S/UMMARY' | 'D/ONT' 'S/UMMARIZE' )
NRECS(%NRECS##)               = 'N/UMBER' 'R/EC' ['O/RD'] ['S'] REL VALUE
    REL                       = ( LESS | EQUAL | GREATER )
      LESS(%LESS##)           = '<'
      EQUAL(%EQUAL##)         = '='
      GREATER(%GREATER##)     = '>'
    VALUE(%VALUE##)           = .DEC
ORG(%ORG##)                  = 'OR/GS' ORGS
PROMPT(%PROMPT##)            = 'P/ROMPT'
QUIT(%QUIT##)                = 'Q/UIT'
RESIDUAL(%RESIDUAL##)        = 'R/ESIDUAL'
SKIP(%SKIP##)                = 'SK/IP' FIDS
SRCH(%SRCH##)                = 'SR/CH' [ACCT_LIST]
START(%START##)              = 'ST/ART' '=' ONE_FID
STD(%STD##)                  = 'STD' '=' A_FID
STRINGS(%STRINGS##)          = 'STR/INGS' '=' A_FID
SUMMARY(%SUMMARY##)          = 'SU/MMARY'
TALLY(%TALLY##)              = 'TA/LLY'
TALLYONLY(%TALLYONLY##)      = 'TA/LLY' 'O/NLY'
TEXT_FILES_ONLY(%TEXT_FILES_ONLY##) = 'TE/XT' ['F/ILES'] ['O/NLY']
TYPE(%TYPE##)                = 'TY/PES' TYPES
UC(%UC##)                    = ( 'U/PPER' 'C/ASE' | 'C/ASELESS' ['C/OMPARES'] )
UI(%UI##)                    = 'UI'
UISRCH(%UISRCH##)            = 'UIS/RCH' ACCT_LIST
WARNINGS(%WARNINGS##)        = 'WA/RNINGS'
WILDCARD(%WILDCARD##)        = 'W/ILDCARD' ['C/HARACTER'] ['=' .FID1]
 
    ORGS                     = ( ['='] '(' <',',A_ORG> ')' | '=' A_ORG )
    A_ORG                    = ( CG        | ;
                                 CONSEC    | ;
                                 FIXED     | ;
                                 FPRG      | ;
                                 FREE      | ;
                                 IDS       | ;
                                 INDEXED   | ;
                                 IREL      | ;
                                 KEYED     | ;
                                 RANDOM    | ;
                                 RELATIVE  | ;
                                 RESTRICT  | ;
                                 SYMB      | ;
                                 TERMINAL  | ;
                                 UNDEF     | ;
                                 UR        | ;
                                 VARIABLE  )
      CG(%CG##)              = 'CG'
      CONSEC(%CONSEC##)      = 'C/ONSEC'
      FIXED(%FIXED##)        = 'FI/XED'
      FPRG(%FPRG##)          = 'FP/RG'
      FREE(%FREE##)          = 'FR/EE'
      IDS(%IDS##)            = 'ID/S'
      INDEXED(%INDEXED##)    = 'IN/DEXED'
      IREL(%IREL##)          = 'IR/EL'
      KEYED(%KEYED##)        = 'K/EYED'
      RANDOM(%RANDOM##)      = 'RA/NDOM'
      RELATIVE(%RELATIVE##)  = 'REL/ATIVE'
      RESTRICT(%RESTRICT##)  = 'RES/TRICT'
      SYMB(%SYMB##)          = 'S/YMB'
      TERMINAL(%TERMINAL##)  = 'T/ERMINAL'
      UNDEF(%UNDEF##)        = 'UN/DEF'
      UR(%UR##)              = 'UR'
      VARIABLE(%VARIABLE##)  = 'V/ARIABLE'
 
    TYPES                    = ( ['='] '(' <',',A_TYPE> ')' | '=' A_TYPE )
    A_TYPE(%A_TYPE##)        = ( .SYMBOL2+'*?' | .QUOTE2 )
 
    FIDS                     = ( ['='] '(' <',',WILD_NAME> ')' | '=' WILD_NAME )
    ACCT_LIST                = ( '(' <',', AN_ACCT> ')' )
    A_FID(%A_FID##)          = .FID
    AN_ACCT(%AN_ACCT##)      = .FID
    ONE_FID(%ONE_FID##)      = .ALPHA+'0123456789$_-.*:'
    WILD_NAME(%WILD_NAME##)  = .ALPHA+'0123456789$_-.*:?'
    PL6_INCLUDE              = [.SYM ':'] '%INCLUDE' .BLANK A_FID ';'
    FORT_INCLUDE             = [.DEC] 'INCLUDE' .BLANK A_FID [',' 'LIST']
    COBOL_COPY               = .BLANK 'COPY' .BLANK A_FID ['.']
