/*M*   DRAW    DRAW PICTURES FROM SOURCE OR OU */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
/*
*/
DRAWM:  PROC MAIN ALTRET;
/*
*/
%PLIST;
%INCLUDE B$OBJECT_C;
%INCLUDE CP_6;
%F$DCB;
%B$TCB;
%B$ALT;
%INCLUDE B$JIT_C;
%B$JIT0;
%A$JIT6X;
/*
*/
%B$HEADKEY(NAME=HKEY,STCLASS=SYMREF,HFLAGINIT=);
%B$OUHEAD(NAME=HEAD,STCLASS=SYMREF);
/*
*/
DCL DRAWOU ENTRY(4) ALTRET;
DCL DRAWS ENTRY ALTRET;
DCL DRAWOPT ENTRY ALTRET;
/*
*/
DCL M$SI DCB;
DCL M$LO DCB;
DCL M$DO DCB;
DCL M$OU DCB;
DCL B$JIT$ PTR SYMREF;
DCL B$TCB$ PTR SYMREF;
DCL M$SI$ PTR SYMREF;
DCL EQUOPT UBIN SYMREF;
DCL BITOPT UBIN SYMREF;
DCL MASKOPT UBIN SYMREF;
DCL SELOUOPT UBIN SYMREF;
DCL SELOUNAM CHAR(SELOUOPT) SYMREF;
/*
*/
%FPT_READ(FPTN=RDFPT,DCB=M$SI,BUF=BFR,KEY=HKEY,KEYR=YES,KEYS=NO,STCLASS=CONSTANT);
%FPT_READ(FPTN=RDFPT1,DCB=M$SI,BUF=HEAD,KEY=HKEY,KEYR=NO,KEYS=YES,STCLASS=CONSTANT);
%FPT_WRITE(FPTN=WRFPT,DCB=M$LO,BUF=OUNAM.TEXT,STCLASS=CONSTANT);
%FPT_WRITE(FPTN=WRTD,STCLASS=CONSTANT,DCB=M$LO,BUF=TD,VFC=YES);
%FPT_WRITE(FPTN=WRBL,STCLASS=CONSTANT,DCB=M$LO,BUF=BL);
%FPT_TIME(FPTN=TMDT,STCLASS=CONSTANT,DAY=TD.D,DATE=TD.DT,TIME=TD.TM);
%FPT_CLOSE(FPTN=CLLO,DCB=M$LO,DISP=SAVE,STCLASS=CONSTANT);
%FPT_CLOSE(FPTN=CLOU,DCB=M$OU,DISP=SAVE,STCLASS=CONSTANT);
%FPT_OPEN(FPTN=OPOU,DCB=M$OU,ORG=CONSEC,STCLASS=CONSTANT);
%FPT_ERRMSG (
      BUF = BFR,
      CODE = ERRCODE,
      DCB = M$SI,
      FLAGLEV = 4,
      OUTDCB1 = M$DO,
      STCLASS = CONSTANT);
%EJECT;
/*
*/
DCL 1 TD STATIC SYMDEF,
      2 * CHAR(1) INIT('0'),
      2 D CHAR(0) INIT('DAY. '),
      2 DT CHAR(0) INIT('MON DD ''YY '),
      2 TM CHAR(0) INIT('HH:MM:SS.SS'),
      2 * CHAR(0) INIT('  FILE='),
      2 NM CHAR(31);
DCL 1 STRCTLINE SYMREF,
      2 * CHAR(5),
      2 FNAM CHAR(30),
      2 NAM CHAR(20),
      2 OUHD CHAR(3),
      2 OUNAM CHAR(20);
DCL 1 OUNAM STATIC SYMDEF,
      2 CNT UBIN BYTE UNAL INIT(6),
      2 TEXT CHAR(255) INIT('DRAWOU=OBJECT UNIT NAME');
DCL SDCB UBIN HALF STATIC INIT(DCBNUM(M$SI));
DCL FIRSTFLAG BIT(1) STATIC SYMDEF INIT('0'B);
DCL BFR CHAR(140) SYMREF;
DCL BL CHAR(1) CONSTANT INIT(' ');
DCL ERRCODE BIT(36) STATIC;
/*
*/
        CALL M$TIME(TMDT);
        M$SI$=DCBADDR(DCBNUM(M$SI));
        TD.NM=M$SI$->F$DCB.NAME#.C;
        STRCTLINE.FNAM = TD.NM;
        CALL M$WRITE(WRTD);
        CALL M$WRITE(WRBL);
        CALL DRAWOPT;
        IF EQUOPT + BITOPT + MASKOPT > 0
        THEN
           CALL M$OPEN (OPOU) ALTRET (ERROR1);
DRAWM1: CALL M$READ(RDFPT) ALTRET(ERROR1);
        IF HKEY.HFLAG ='042'O
        THEN DO;
           FIRSTFLAG='1'B;
           OUNAM.CNT=HKEY.KEYSIZ-1;
           IF SELOUOPT>0 AND SUBSTR(HKEY.TEXT,0,OUNAM.CNT)~=SELOUNAM
           THEN GOTO DRAWM1;
           CALL CONCAT(OUNAM.TEXT,
             SUBSTR(HKEY.TEXT,0,OUNAM.CNT),'=OBJECT UNIT NAME');
           STRCTLINE.OUHD = 'OU=';
           STRCTLINE.OUNAM = SUBSTR(HKEY.TEXT,0,OUNAM.CNT);
           CALL M$WRITE (WRFPT);
           CALL DRAWOU(SDCB,OUNAM)       ALTRET(ERROR);
           CALL M$READ(RDFPT1) ALTRET(ERROR);
           GOTO DRAWM1;
           END;
        IF NOT FIRSTFLAG
        THEN CALL DRAWS ALTRET(ERROR2);
ERROR:  CALL M$CLOSE(CLLO);
        CALL M$CLOSE(CLOU);
        RETURN;
ERROR1: IF FIRSTFLAG
        THEN GOTO ERROR;
        ERRCODE=B$TCB$->B$TCB.ALT$->B$ALT.ERR;
        CALL M$ERRMSG(FPT_ERRMSG);
ERROR2: IF FIRSTFLAG
        THEN DO;
           CALL M$CLOSE(CLLO);
           CALL M$CLOSE(CLOU);
           END;
        CALL M$ERR;
END DRAWM;
