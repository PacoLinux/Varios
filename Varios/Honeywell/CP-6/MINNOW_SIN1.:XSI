/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
%LIST;
%INCLUDE MINNOW_C61;
%EJECT;
MINNOW_NODES(%MINNOW##,D)    = ( ['('] <';',MINNOW_CMDS> [')'] .END )
    MINNOW_CMDS              = ( ALL_HELP     | ;
                                 COPY         | ;
                                 CREATE       | ;
                                 DISPLAY      | ;
                                 END          | ;
                                 HELP         | ;
                                 INDEX        | ;
                                 INFORM       | ;
                                 LIST         | ;
                                 MODIFY       | ;
                                 NULL         | ;
                                 NUMBER       | ;
                                 RECSIZE        )
 
ALL_HELP(%ALL_HELP##)        = '??'
COPY(%COPY##)                = 'CO/PY' COPY_SRCE (COPY_ON|COPY_OVER) COPY_DEST ;
                                              [',' <',',COPY_OPTS>]
CREATE(%CREATE##)            = 'CR/EATE' CREATE_DEST 'F/ROM' CREATE_SRCE ;
                                       [',' <',',CREATE_OPTS>]
DISPLAY(%DISPLAY##)          = 'DI/SPLAY' [.B] .FID
END(%END##)                  = ( 'EX/IT' | 'E/ND' | 'X/IT' | 'Q/UIT' )
HELP(%HELP##)                = 'H/ELP' .ASYM+','
INDEX(%INDEX##)              = ['I/ND'['E']['X']] INDX_NUM ',' <',',INDX_OPTS>
INFORM(%INFORM##)            = 'INF/ORM' [INFORM_OC]['E/VERY'] .DEC ['R/ECORDS']
LIST(%LIST##)                = 'L/IST' ['I/ND'['E']['X']] ['K/EYS'] ;
                                       [(INDXES | ALL)] ;
                                       [( 'I/NDEXES' | 'K/EYS' )]
MODIFY(%MODIFY##)            = 'MOD/IFY' [.B] .FID
NULL(%NULL##)                = [.B] .END
NUMBER(%NUMBER##)            = 'N/UMBER' ['O/F'] [('K/EYS'|'I/ND'['E']['XES'])];
                                       '=' .DEC2
RECSIZE(%RECSIZE##)          = 'R/ECSIZE' '=' .DEC
 
COPY_OPTS                    = ( COPY_ALTBLD  | ;
                                 COPY_CTG     | ;
                                 COPY_IXTN    | ;
                                 COPY_LOAD    | ;
                                 COPY_RELG    | ;
                                 COPY_XTN       )
COPY_ALTBLD(%COPY_ALTBLD##)  = 'AL/TBLD' ( '=' NO | ['=' YES] )
COPY_CTG(%COPY_CTG##)        = 'CTG' ( '=' NO | ['=' YES] )
COPY_DEST(%COPY_DEST##)      = .FID
COPY_IXTN(%COPY_IXTN##)      = 'IX/TNSIZE' '=' .DEC
COPY_LOAD(%COPY_LOAD##)      = 'L/OAD' ( '=' NO | ['=' YES] )
COPY_ON(%COPY_ON##)          = ( 'ON' | 'TO' )
COPY_OVER(%COPY_OVER##)      = 'OVER'
COPY_RELG(%COPY_RELG##)      = 'RE/LGRANS' ( '=' NO | ['=' YES] )
COPY_SRCE(%COPY_SRCE##)      = .FID
COPY_XTN(%COPY_XTN##)        = 'XT/NSIZE' '=' .DEC
 
CREATE_OPTS                  = ( CREATE_ALTBLD  | ;
                                 CREATE_CTG     | ;
                                 CREATE_IXTN    | ;
                                 CREATE_LOAD    | ;
                                 CREATE_OVER    | ;
                                 CREATE_RELG    | ;
                                 CREATE_XTN       )
CREATE_ALTBLD(%CREATE_ALTBLD##) = 'AL/TBLD' ( '=' NO | ['=' YES] )
CREATE_CTG(%CREATE_CTG##)    = 'CTG' ( '=' NO | ['=' YES] )
CREATE_DEST(%CREATE_DEST##)  = .FID
CREATE_IXTN(%CREATE_IXTN##)  = 'IX/TNSIZE' '=' .DEC
CREATE_LOAD(%CREATE_LOAD##)  = 'L/OAD' ( '=' NO | ['=' YES] )
CREATE_OVER(%CREATE_OVER##)  = 'OVER'
CREATE_RELG(%CREATE_RELG##)  = 'RE/LGRANS' ( '=' NO | ['=' YES] )
CREATE_SRCE(%CREATE_SRCE##)  = .FID
CREATE_XTN(%CREATE_XTN##)    = 'XT/NSIZE' '=' .DEC
 
INFORM_OC(%INFORM_OC##)      = [','] 'O/N' 'O/PERATORS' 'C/ONSOLE' [',']
 
INDX_OPTS                    = ( INDX_DUPES     | ;
                                 INDX_END       | ;
                                 INDX_LEN       | ;
                                 INDX_START       )
INDX_NUM(%INDX_NUM##)        = .DEC2
INDX_DUPES(%INDX_DUPES##)    = 'D/UPLICATES'
INDX_END(%INDX_END##)        = 'E/ND' '=' .DEC
INDX_LEN(%INDX_LEN##)        = 'L/ENGTH' '=' .DEC
INDX_START(%INDX_START##)    = 'S/TART' '=' .DEC
 
ALL(%ALL##)                  = 'A/LL'
INDX(%INDX##)                = .DEC2
INDXES                       = <',',INDX>
NO(%NO##)                    = 'N/O'
YES(%YES##)                  = 'Y/ES'
