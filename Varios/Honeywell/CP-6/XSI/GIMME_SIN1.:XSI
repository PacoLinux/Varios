/*M* GIMME_SIP1 - This module contains parse nodes for GIMME                  */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/**/
/**/
%INCLUDE XUD_UTS_M;
  %XUD_UTS_EQU;
%INCLUDE GIMME_C61;
/**/
GIMME_COMMAND = (                  ;
                 ALIAS_COMMAND    |;
                 CREATE_COMMAND   |;
                 DESTROY_COMMAND  |;
                 DISPLAY_COMMAND  |;
                 GET_COMMAND      |;
                 LIST_COMMAND     |;
                 MOVE_COMMAND     |;
                 REL_COMMAND      |;
                 FLUSH_COMMAND    |;
                 STRETCH_COMMAND  |;
                 QUIT_COMMAND     |;
                 USE_COMMAND      ) .END
/*               BIG_COMMAND      |;       */
%EJECT;
/**/
/*  The GET command is used to allocate a resource.                           */
/**/
GET_COMMAND (%GET_COMMAND#) = ('GE/T'|'GI/MME') [RESOURCE_PART] [START_TIME_PART] ;
                          [END_TIME_PART] REASON_PART
/**/
/*  The FLUSH command deletes old/expired records from the database.          */
/**/
FLUSH_COMMAND (%FLUSH_COMMAND#) = 'FLUSH' [RESOURCE_PART] [START_TIME_PART] ;
                          [END_TIME_PART]
/**/
/*  The REL command is used to release a resource before the user             */
/*  originally wanted expiration.                                             */
/**/
REL_COMMAND (%REL_COMMAND#) = 'REL/EASE' [RESOURCE_PART] [ID_PART]
/**/
/*  The DISPLAY command tells you about the scheduling of a specified         */
/*  resource.                                                                 */
/**/
DISPLAY_COMMAND (%DISPLAY_COMMAND#) = 'DI/SPLAY' [RESOURCE_PART] ;
               [START_TIME_PART] [END_TIME_PART] [DISPLAY_OPT_PART]
%EJECT;
/**/
/*  The LIST command is used to find out what resources are available         */
/*  for control.                                                              */
/**/
LIST_COMMAND (%LIST_COMMAND#) = 'L/IST'
/**/
/*  The STRETCH command is used to increase or decrease (if possible)         */
/*  the time already allocated to a resource.                                 */
/**/
STRETCH_COMMAND (%STRETCH_COMMAND#) = 'ST/RETCH' [RESOURCE_PART] [ID_PART] [END_TIME_PART]
/**/
/*  The MOVE command is used to take an allocated chunk of time               */
/*  already allocated to a resource, and reschedule it.                       */
/**/
MOVE_COMMAND (%MOVE_COMMAND#) = 'MO/VE' [RESOURCE_PART] [ID_PART] [START_TIME_PART]
%EJECT;
/**/
/*  The CREATE command is used to define a resource.                          */
/**/
CREATE_COMMAND (%CREATE_COMMAND#) = 'CR/EATE' [RESOURCE_PART] ;
                                    REASON_PART [SITE_PART] ;
                                    [AVAIL_TIME_PART] [REASON_SPEC]
/**/
/*  The ALIAS command is used to define a resource.                           */
/**/
ALIAS_COMMAND (%ALIAS_COMMAND#) = 'AL/IAS' [RESOURCE_PART] ;
                                  [ALIAS_PART] REASON_PART
/**/
/*  The DESTROY command is used to get rid of a resource.                     */
/**/
DESTROY_COMMAND (%DESTROY_COMMAND#) = 'DES/TROY' [RESOURCE_PART]
/**/
/*  The USE command lets you select the set of signup sheets you want.        */
/**/
USE_COMMAND (%USE_COMMAND#) = 'USE' .FID
%EJECT;
/**/
/*  Various commands common to the whole world!                               */
/**/
QUIT_COMMAND(%QUIT_COMMAND#)     = ('Q/UIT' | 'END' | ['E'-] 'X/IT' )
%EJECT;
/**/
/* VARIOUS PIECES */
/**/
START_TIME_PART(%START_TIME_PART#) = ( NOW_TIME                  |;
                                       [('AT'|'ON'|'FROM')] SPECIFIC_TIME  |;
                                       'IN' RELATIVE_TIME)
END_TIME_PART(%END_TIME_PART#) = ( {'UNTIL'|'TO'|'THROUGH'|'-'} SPECIFIC_TIME   |;
                                   'FOR' RELATIVE_TIME)
RESOURCE_PART(%RESOURCE_PART#) = .SYM+'-*:?'
ALIAS_PART(%ALIAS_PART#) = ('AS' | '=' | 'ALIAS') .SYM+'-*:'
REASON_PART(%REASON_PART#) = [ ('BE/CAUSE'|'DE/SCRIPTION'|'CO/MMENT') .QUOTE ]
ID_PART(%ID_PART#) = ( NOW_TIME                  |;
                       [('AT'|'ON')] SPECIFIC_TIME      |;
                       'IN' RELATIVE_TIME)
SITE_PART(%SITE_PART#) = 'SITE' .QUOTE
AVAIL_TIME_PART(%AVAIL_TIME_PART#) = 'AVAIL/ABLE' <('-'|'TO'),TIME>
DISPLAY_OPT_PART(%DISPLAY_OPT_PART#) = '(' DISPLAY_OPTION ')'
REASON_SPEC(%REASON_SPEC#) = 'R/EASON' ( REQ | OPT | DEF )
    REQ(%REQUIRED#) = 'REQ/UIRED'
    OPT(%OPTIONAL#) = 'OPT/IONAL'
    DEF(%DEFAULT#)  = 'DEF/AULT' .QUOTE
    DISPLAY_OPTION(NO) = (BY_DAY | BY_WEEK | BY_MONTH)
      BY_DAY(%BY_DAY#) = 'D/AILY'
      BY_WEEK(%BY_WEEK#) = 'W/EEKLY'
      BY_MONTH(%BY_MONTH#) = 'MO/NTHLY'
    RELATIVE_TIME(%RELATIVE_TIME#) = .B .DEC .B (DAYS|WEEKS|MONTHS|YEAR|MINUTES|HOURS)
         DAYS(%DAYS#) = 'D/AYS'
         WEEKS(%WEEKS#) = 'W/EEKS'
         MONTHS(%MONTHS#) = 'MO/NTHS'
         YEAR(%YEARS#) = 'Y/EARS'
         MINUTES(%MINUTES#) = 'MI/NUTES'
         HOURS(%HOURS#) = 'H/OURS'
    SPECIFIC_TIME (%SPECIFIC_TIME#) = <(','|.B),{DAY_SPEC|TIME}>
         DAY_SPEC(NO) = (DATE | TOMORROW | TODAY | YESTERDAY)
             DATE(%DATE#) = .DEC2 '/' .DEC2 ['/' .DEC2]
             TOMORROW(%TOMORROW#) = 'TOM/ORROW'
             TODAY(%TODAY#) = 'TOD/AY'
             YESTERDAY(%YESTERDAY#) = 'YES/TERDAY'
         TIME(%TIME#) = .DEC2 ':' .DEC2 [AM_OR_PM]
             AM_OR_PM(NO) = (AM | PM)
                 AM(%AM#) = 'AM'
                 PM(%PM#) = 'PM'
    NOW_TIME (%NOW_TIME#) = 'NOW'
%EJECT;
/**/
/*  In case the user leaves off part of a command, he can give it later.      */
/**/
START_TIME_STUB(0,D,NO) = START_TIME_PART .END
END_TIME_STUB(0,D,NO) = END_TIME_PART .END
RESOURCE_STUB(0,D,NO) = RESOURCE_PART .END
ALIAS_STUB(0,D,NO) = ALIAS_PART .END
REASON_STUB(0,D,NO) = REASON_PART .END
ID_STUB(0,D,NO) = ID_PART .END
SITE_STUB(0,D,NO) = SITE_PART .END
AVAIL_TIME_STUB(0,D,NO) = AVAIL_TIME_PART .END
DISPLAY_OPT_STUB(0,D,NO) = DISPLAY_OPT_PART .END
REASON_SPEC_STUB(0,D,NO) = REASON_SPEC .END
