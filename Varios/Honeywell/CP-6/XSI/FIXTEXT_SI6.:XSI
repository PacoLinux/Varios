/*M* FIXTEXT a program to make TEXT output presentable to MAIL */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X* DMR,PLM=5,IND=5,CTI=5,SDI=5,MCL=10,CSI=0,ECI=0 */
 
 
%SET LISTSUB='1'B;
 
 
FIXTEXT:PROC MAIN;
/*
         INCLUDE FILES
*/
%INCLUDE CP_6;
/*
         DCBs
*/
DCL M$SI DCB;
/*
         Buffers for SI
*/
DCL INPUT CHAR(256) STATIC SYMDEF;
/*
         SI FPTs for:
              opening, reading,
              positioning and deleting records
*/
%FPT_OPEN(FPTN=OPEN_SI,DCB=M$SI,ASN=FILE,FUN=UPDATE);
%FPT_CLOSE(FPTN=CLOSE_SI,DCB=M$SI,DISP=SAVE);
%FPT_READ(FPTN=READ_SI,BUF=INPUT,DCB=M$SI);
%FPT_PFIL(DCB=M$SI,BOF=NO);             /* END-OF-FILE                        */
%FPT_PRECORD(DCB=M$SI,N=-1);            /* BACKSPACE                          */
%FPT_DELREC(DCB=M$SI);
%EJECT;
/*
         GENERAL DECLARATIONS
*/
%EJECT;
/*
                   Strip blank lines from
                   beginning and end of
                   TEXT output.
*/
     CALL FIX_TEXT;
     CALL M$EXIT;
%EJECT;
IO:  PROC(BFR) ALTRET;
/*
         PARAMETERS
*/
DCL BFR CHAR(1000);
/*
         DCB STRUCTURE
*/
%F$DCB;
/*
         READ M$SI
*/
READSI: ENTRY(BFR) ALTRET;
     READ_SI.BUF_.BUF$=ADDR(BFR);
     READ_SI.BUF_.BOUND=255;
     CALL M$READ(READ_SI) ALTRET(ROERR);
     RETURN;
ROERR:
     ALTRETURN;
END IO;
%EJECT;
ERROR: PROC(DCBNO);
/*
         DECLARATIONS
*/
DCL DCBNO UBIN;
DCL M$DO DCB;
DCL B$TCB$ PTR SYMREF;
DCL ERR_BUF CHAR(120) STATIC;
/*
         FPTs
*/
%VLP_ERRCODE(FPTN=ERR);
%B$ALT;
%B$TCB;
%FPT_ERRMSG(BUF=ERR_BUF,
            CODE=ERR,
            INCLCODE=NO,
            OUTDCB1=M$DO);
/*
         PROCEDURE
*/
     ERR=B$TCB$->B$TCB.ALT$->B$ALT.ERR;
     ERR.SEV=0;
     FPT_ERRMSG.V.DCB#=DCBNO;
     CALL M$ERRMSG(FPT_ERRMSG);
     RETURN;
END ERROR;
%EJECT;
FIX_TEXT:PROC;
DCL I UBIN;
     CALL M$OPEN(OPEN_SI) ALTRET(SI_ERR);
     DO WHILE('1'B);
          INPUT=' ';
          CALL READSI(INPUT) ALTRET(SI_ERR);
          IF INPUT=' '
          THEN
               CALL M$DELREC(FPT_DELREC) ALTRET(SI_ERR);
          ELSE GOTO CONT;
        END;
CONT:
     CALL M$PFIL(FPT_PFIL) ALTRET(SI_ERR); /* POSITION TO EOF              */
     DO WHILE('1'B);
          CALL M$PRECORD(FPT_PRECORD) ALTRET(SI_ERR); /* BACK ONE            */
          INPUT=' ';
          CALL READSI(INPUT) ALTRET(SI_ERR);
          IF INPUT=' '
          THEN
               CALL M$DELREC(FPT_DELREC) ALTRET(SI_ERR);
          ELSE
               GOTO FIN;
        END;
FIN:
     CALL M$CLOSE(CLOSE_SI) ALTRET(SI_ERR);
     RETURN;
SI_ERR:
     CALL ERROR(DCBNUM(M$SI));
     CALL M$CLOSE(CLOSE_SI);
     CALL M$ERR;
END FIX_TEXT;
%EJECT;
END FIXTEXT;
