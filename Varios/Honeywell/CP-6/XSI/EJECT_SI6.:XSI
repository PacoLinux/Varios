/*M* EJECT - Issues a Top Of Form to the specified destination */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X* DDA */
EJECT: PROC MAIN;
 
%INCLUDE CP_6;
   %B$ALT;
   %B$TCB;
   %F$DCB;
%INCLUDE CP_6_SUBS;
 
%FPT_DEVICE  (FPTN                     = NEW_PAGE,
              DCB=M$LL,
              PAGE=YES );
 
%FPT_FID     (FPTN                     = FID,
              ACCT=VLP_ACCT,
              ASN=OPEN_DCB.V.ASN#,
              NAME=VLP_NAME,
              PASS=VLP_PASS,
              RES=OPEN_DCB.V.RES#,
              RESULTS=VLR_FID,
              TEXTFID=TEXTFID,
              WSN=VLP_WSN );
 
%FPT_UNFID   (FPTN                     = UNFID,
              DCB=M$LL,
              LEN=FID_LEN,
              TEXTFID=TEXTFID );
 
%VLP_ACCT    (FPTN                     = VLP_ACCT );
%VLP_NAME    (FPTN                     = VLP_NAME );
%VLP_PASS    (FPTN                     = VLP_PASS );
%VLP_WSN     (FPTN                     = VLP_WSN );
%VLR_FID     (FPTN                     = VLR_FID );
 
%VLP_NAME    (FPTN                     = DCB_NAME,
              LEN=31,
              NAME='M$LL' );
 
%FPT_GETDCB  (FPTN                     = GETDCB,
              DCBNAME=DCB_NAME );
 
%FPT_EXIT    (FPTN                     = SET_STEPCC,
              STEPCC=OK );
 
%FPT_OPEN    (FPTN                     = OPEN_DCB,
              ACCT=VLP_ACCT,
              DCB=M$LL,
              DISP=NAMED,
              EXIST=OLDFILE,
              FUN=CREATE,
              NAME=VLP_NAME,
              ORG=UR,
              PASS=VLP_PASS,
              WSN=VLP_WSN );
 
%FPT_CLOSE   (FPTN                     = CLOSE_DCB,
              DCB=M$LL,
              DISP=SAVE );
 
%M$DCB       (DCBN                     = M$LL,
              ASN=DEVICE,
              FUN=UPDATE,
              ORG=UR,
              RES='ME');
 
%FPT_ERRMSG  (FPTN                     = ERRMSG,
              DCB=NIL,
              BUF=ERRMSG_BUF,
              CODE=ERRCODE,
              INCLCODE=NO,
              OUTDCB1=M$DO );
%VLP_ERRCODE (FPTN                     = ERRCODE,
              STCLASS=STATIC );
 
DCL M$DO                     DCB;
DCL M$LO                     DCB;
DCL M$UI                     DCB;
 
DCL B$JIT$                   PTR       SYMREF;
DCL B$TCB$                   PTR       SYMREF;
 
DCL ERRMSG_BUF               CHAR(120) STATIC;
DCL TEXTFID                  CHAR(80)  STATIC INIT('ME');
 
DCL FID_LEN                  UBIN      STATIC;
%EJECT;
START:
    IF DCBADDR(DCBNUM(M$LO))->F$DCB.AMR# THEN DO;  /* Was DCB4 specified?     */
         UNFID.V.DCB#= DCBNUM(M$LO);
         CALL M$UNFID( UNFID )  ALTRET( OOPS );     /* M$LO -> TEXTFID        */
         IF DCBADDR(DCBNUM(M$UI))->F$DCB.AMR# THEN DO;  /* Was DCB specified  */
              GETDCB.V.DCB#= 0;
              DCB_NAME= DCBADDR(DCBNUM(M$UI))->F$DCB.NAME#;
              CALL M$GETDCB( GETDCB )  ALTRET( OOPS );
              OPEN_DCB.V.DCB#= GETDCB.V.DCB#;
              END;
         END;
    ELSE DO;                                     /* No DCB4; Use DCB1 or M$LL */
         CALL M$UNFID( UNFID )  ALTRET( OOPS );     /* M$LL -> TEXTFID        */
         IF DCBADDR(DCBNUM(M$UI))->F$DCB.AMR# THEN DO;/* Was a DCB specified? */
              GETDCB.V.DCB#= 0;
              DCB_NAME= DCBADDR(DCBNUM(M$UI))->F$DCB.NAME#;
              CALL M$GETDCB( GETDCB )  ALTRET( OOPS );
              OPEN_DCB.V.DCB#= GETDCB.V.DCB#;
              END;
         ELSE IF DCBADDR(DCBNUM(M$LL))->F$DCB.AMR# THEN/* Just DCB1 specified?*/
                   ;
              ELSE TEXTFID= 'ME';             /* Nothing specified; assume ME */
         END;
 
    CALL M$FID( FID )  ALTRET( OOPS );        /* TEXTFID -> M$LL              */
    NEW_PAGE.V.DCB#= OPEN_DCB.V.DCB#;
    CLOSE_DCB.V.DCB#= OPEN_DCB.V.DCB#;
 
    CALL M$OPEN( OPEN_DCB )     ALTRET( SET_THE_STEPCC );
    CALL M$DEVICE( NEW_PAGE )   ALTRET( SET_THE_STEPCC );
    CALL M$CLOSE( CLOSE_DCB )   ALTRET( SET_THE_STEPCC );
    DO WHILE('0'B);
      DO WHILE('0'B);
OOPS:
        ERRCODE= B$TCB$ -> B$TCB.ALT$ -> B$ALT.ERR;
        ERRMSG.V.DCB#= B$TCB$ -> B$TCB.ALT$ -> B$ALT.DCB#;
        CALL M$ERRMSG( ERRMSG );
        END;
SET_THE_STEPCC:
      SET_STEPCC.V.STEPCC#= 4;                /* Set the STEPCC upon exit     */
      END;
    CALL M$EXIT( SET_STEPCC );
 
END EJECT;
