/*M* USE_TRACKER - routine to track a run unit's use */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
USE_TRACKER: PROC (NAME,LENGTH);
DCL NAME CHAR (LENGTH);
DCL LENGTH UBIN;
%INCLUDE CP_6;
%INCLUDE B$JIT;
%B$TCB;
%B$ALT;
 
%FPT_WRITE(FPTN=WRITEDOC,DCB=M$OU,BUF=DOCBUF,KEY=EDITKEY,NEWKEY=YES,
     ONEWKEY=NO);
 
DCL DOCBUF CHAR (80) CALIGNED STATIC;
 
%FPT_PRECORD (FPTN = FIND_END,
              DCB = M$OU,
              KEY = KEY_BUF,
              KEYR = YES,
              N = -1
              );
DCL 1 KEY_BUF WALIGNED STATIC,
     2 SIZE SBIN BYTE CALIGNED INIT (3),
     2 KEY_VAL UBIN (27) UNAL ;
 
%FPT_PFIL (FPTN = END_OF_FILE,
           BOF = NO,
           DCB = M$OU
           );
 
DCL 1 EDITKEY WALIGNED STATIC,
    2 KEYSIZE UBIN BYTE CALIGNED INIT (3),
    2 COUNT UBIN (27) UNAL INIT (1000);
DCL KEY_INCREMENT UBIN ALIGNED ;
 
%FPT_OPEN(FPTN=OPEN_OU,
           ACS=DIRECT,
           ORG=KEYED,
           FUN=CREATE,
           SHARE=ALL,
           ASN=FILE,
           EXIST=ERROR,
           CTG=YES,
           NAME = VLP_NAME,
           ACCT = VLP_ACCOUNT,
           PASS = VLP_PASS,
           SN = VLP_SN,
           DCB=M$OU);
DCL M$OU DCB;
 
%FPT_TIME (FPTN=GET_DATE,
           DATE=DATEVAL,
           TIME=TIMEVAL,
           DAY=DAYVAL,
           DEST=LOCAL);
DCL DATEVAL CHAR (8) STATIC;
DCL TIMEVAL CHAR (11) STATIC;
DCL DAYVAL CHAR (3) STATIC;
 
%FPT_CLOSE( FPTN=CLOSE_OU,
              DCB=M$OU);
DCL B$JIT$ PTR SYMREF ;
DCL CHAR_TEMP CHAR (5);
%FPT_FID ( FPTN=FPT_FID,
           ASN = OPEN_OU.V.ASN#,
           RES = OPEN_OU.V.RES#,
           ACCT = VLP_ACCOUNT,
           NAME = VLP_NAME,
           SN = VLP_SN,
           PASS = VLP_PASS);
%VLP_ACCT (FPTN = VLP_ACCOUNT);
%VLP_NAME (FPTN = VLP_NAME);
%VLP_SN (FPTN = VLP_SN);
%VLP_PASS (FPTN = VLP_PASS);
 
FPT_FID.TEXTFID_ = VECTOR (NAME);
CALL M$FID (FPT_FID);
CALL M$OPEN(OPEN_OU)
  WHENRETURN DO;
     EDITKEY.COUNT = 1000;
     KEY_BUF.KEY_VAL = 1000;
     DOCBUF='DAY   DATE   TIME          ACCOUNT  USER NAME     SYSID     ACCESS MODE';
     CALL M$WRITE (WRITEDOC);
     END;
  WHENALTRETURN DO;
     OPEN_OU.V.EXIST# = 2;
     OPEN_OU.V.FUN# = 1;
     CALL M$OPEN (OPEN_OU);
     CALL M$PFIL (END_OF_FILE) ;
     CALL M$PRECORD ( FIND_END) ;
     END;
  EDITKEY.COUNT = KEY_BUF.KEY_VAL + 1000;
/*DOCBUF = B$JIT$->B$JIT.FACCN;*/
CALL M$TIME (GET_DATE)
  WHENRETURN DO;
     CALL CONCAT (DOCBUF,DAYVAL,' ',DATEVAL,' ',TIMEVAL,'  ');
     END;
  WHENALTRETURN DO;
     CALL INSERT (DOCBUF,0,26,'M$TIME didn''t work right- ');
     END;
CALL INSERT (DOCBUF,27,8,B$JIT$->B$JIT.ACCN);
CALL INSERT (DOCBUF,34,2,'  ');
CALL INSERT (DOCBUF,36,12,B$JIT$->B$JIT.UNAME);
CALL INSERT (DOCBUF,48,2,'  ');
CALL BINCHAR (CHAR_TEMP, B$JIT$->B$JIT.SYSID);
CALL INSERT (DOCBUF,50,8,CHAR_TEMP);
CALL INSERT (DOCBUF,58,2,'  ');
DO CASE (B$JIT$->B$JIT.MODE);
  CASE (1);
     CALL INSERT (DOCBUF,60,11,'BATCH      ');
  CASE (2);
     CALL INSERT (DOCBUF,60,11,'GHOST      ');
  CASE (3);
     CALL INSERT (DOCBUF,60,11,'INTERACTIVE');
  CASE (4);
     CALL INSERT (DOCBUF,60,11,'TP         ');
  CASE (ELSE);
     CALL INSERT (DOCBUF,60,11,'??UNKNOWN??');
  END; /* DO CASE */
CALL M$WRITE (WRITEDOC) ;
 
CALL M$CLOSE ( CLOSE_OU);
END USE_TRACKER;
