/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
 
%LIST;
%INCLUDE BASS_C61;
%PLIST;
%EJECT;
BASS_NODES(%BASS##,D) = ( ['('] [<';',CMDS>] [')'] ) .END
    CMDS                   = ( ALLHELP    | ;
                               BANNER     | ;
                               CANCEL     | ;
                               CLOSE      | ;
                               COPY       | ;
                               DEBUG      | ;
                               DVBYTE     | ;
                               EXAMINE    | ;
                               FID        | ;
                               FIND       | ;
                               HOLD       | ;
                               HELP       | ;
                               INTERPRET  | ;
                               JOBSTATS   | ;
                               LEFT       | ;
                               LIST       | ;
                               ME         | ;
                               MOREHELP   | ;
                               NBANNER    | ;
                               NDVBYTE    | ;
                               NFID       | ;
                               NINTERPRET | ;
                               NUC        | ;
                               NULL       | ;
                               NVFC       | ;
                               OUTPUT     | ;
                               PAGEMODE   | ;
                               READ       | ;
                               RECORDMODE | ;
                               RIGHT      | ;
                               SE         | ;
                               SPACE      | ;
                               STATUS     | ;
                               SYMO       | ;
                               SYSTEM     | ;
                               TC         | ;
                               TN         | ;
                               TO         | ;
                               TP         | ;
                               TS         | ;
                               TV         | ;
                               TY         | ;
                               UC         | ;
                               UNHOLD     | ;
                               VFC        | ;
                               WIDTH      | ;
                               XIT          ;
                             )
 
    ALLHELP(%ALLHELP##)      = '??'
    BANNER(%BANNER##)        = 'B/ANNER'
    CANCEL(%CANCEL##)        = 'CA/NCEL' .B WHAT [NUM]
    CLOSE(%CLOSE##)          = 'CL/OSE' ['S/YMBIONT'] ['F/ILE']
    COPY(%COPY##)            = 'CO/PY' .B WHAT .B HOW .B WHERE
    DEBUG(%DEBUG##)          = 'DEBUG'
    DVBYTE(%DVBYTE##)        = ['DI/SPLAY'] 'DV/BYTE'
    EXAMINE(%EXAMINE##)      = 'EX/AMINE' [WHAT]
    FID(%FID##)              = ['DI/SPLAY'] 'FI/DS'
    FIND(%FIND##)            = 'F/IND' ['T/YPE'] [RANGE] [','] ;
                                       ( STRING | '.' OCTAL ) ;
                                       [COLUMNS] [HOW WHERE]
    HELP(%HELP##)            = 'H/ELP' .ASYM+';'
    HOLD(%HOLD##)            = 'HO/LD' .AN
    INTERPRET(%INTERPRET##)  = 'I/NTERPRET' ['S/PECIAL'] ['R/ECORDS']
    JOBSTATS(%JOBSTATS##)    = 'J/OBSTATS'
    LEFT(%LEFT##)            = 'LE/FT' [['='] .DEC3]
    LIST(%LIST##)            = 'L/IST' [WHAT] [<',',SELECTION>] [HOW WHERE]
    ME(%ME##)                = ( 'M/E' | 'N/O' 'FMR/EAD' | 'N/O' 'FMS/EC' | ;
                                 'N/O' 'DI/SPJOB' )
    MOREHELP(%MOREHELP##)    = '?'
    NBANNER(%NBANNER##)      = 'N/O' 'B/ANNER'
    NDVBYTE(%NDVBYTE##)      = 'N/O' 'DV/BYTE'
    NFID(%NFID##)            = 'N/O' 'FI/DS'
    NINTERPRET(%NINTERPRET##)= 'N/O' 'I/NTERPRET'
    NUC(%NUC##)              = 'NUC'
    NULL(%NULL##)            = [.B] .END
    NVFC(%NVFC##)            = 'N/O' 'V/FC'
    OUTPUT(%OUTPUT##)        = 'OU/TPUT' .B [HOW .B] WHERE
    PAGEMODE(%PAGEMODE##)    = 'P/AGE' ['M/ODE']
    READ(%READ##)            = ( 'RE/AD' | 'XE/Q' ) .B .FID
    RECORDMODE(%RECORDMODE##)= 'REC/ORD' ['M/ODE']
    RIGHT(%RIGHT##)          = 'RI/GHT' [['='] .DEC3]
    SE(%SE##)                = 'SE/LECT' [RANGE] [COLUMNS]
    SPACE(%SPACE##)          = 'SP/ACE' [DASH] [NUM_LINES] [['F/ROM'] ( BOF | HERE | EOF )]
    STATUS(%STATUS##)        = 'ST/ATUS'
    SYMO(%SYMO##)            = 'SY/MBI' ['O/NT']
    SYSTEM(%SYSTEM##)        = ( 'S/YSTEM' | 'FMR/EAD' | 'FMS/EC' | 'D/ISPJOB' )
    TC(%TC##)                = 'TC' [RANGE] [COLUMNS] [HOW WHERE]
    TN(%TN##)                = 'TN' [NUM_LINES] [COLUMNS] [HOW WHERE]
    TO(%TO##)                = 'TO' [RANGE] [COLUMNS] [HOW WHERE]
    TP(%TP##)                = ('TP' | '^') [NUM_LINES] [COLUMNS] [HOW WHERE]
    TS(%TS##)                = 'TS' [RANGE] [COLUMNS] [HOW WHERE]
    TV(%TV##)                = 'TV' [RANGE] [COLUMNS] [HOW WHERE]
    TY(%TY##)                = 'TY' [RANGE] [COLUMNS] [HOW WHERE]
    UC(%UC##)                = 'UC'
    UNHOLD(%UNHOLD##)        = 'UN/HOLD' .DEC6 -'-' -.DEC6
    VFC(%VFC##)              = 'VF/C'
    WIDTH(%WIDTH##)          = 'W/IDTH' '=' .DEC3
    XIT(%XIT##)              = ( 'E/ND' | 'X/IT' | 'Q/UIT' )
 
    WHAT = ( A_SYSID [NUM]        | ;
             A_ACCT               | ;
             WHAT_ME [NUM]        | ;
             LASTSYSID [NUM]      | ;
             JOBNAME A_ACCT [NUM] | ;
             JOBNAME [NUM]        | ;
             ANYTHING               ;
           )
      A_SYSID(%SYSID##)      = [NOT] .DEC6
      A_ACCT(%ACCT##)        = [NOT] '.' [(.SYM8+':-?' | .DEC8)]
      WHAT_ME(%WHAT_ME##)    = [NOT] 'ME'
      LASTSYSID(%LASTSYSID##)= [NOT] 'LAST' ['S/YSID']
      JOBNAME(%JOBNAME##)    = [NOT] ( .AN31+':_$?' | .DEC31 | .QUOTE )
      ANYTHING(%ANYTHING##)  = '?'
 
    HOW = ( ON | OVER | INTO )
      ON(%ON##)              = ( 'ON' | 'TO' )
      OVER(%OVER##)          = 'OVER'
      INTO(%INTO##)          = 'INTO'
 
    WHERE(%WHERE##)          = .FID
 
    SELECTION = ( OUTPUTTING | DEV WSN | FORM | DEV | WSN )
      DEV(%DEV##)            = ( [NOT] .AN2+'?' )
      WSN(%WSN##)            = ( [NOT] '@'-.AN8+'?' )
      NUM(%NUM##)            = '-'- .DEC6
      FORM(%FORM##)          = 'F/ORM' [NOT] '=' .AN6+'?'
      OUTPUTTING(%OUTPUTTING##)= [NOT] 'OUTPUTTING'
 
    RANGE(%RANGE##)          = ( REC1 DASH REC2 | ;
                                 REC1 DASH      | ;
                                      DASH REC2 | ;
                                      DASH      | ;
                                 REC1           )
      REC1(%REC1##)          = ( RECNUM | BOF | EOF )
      REC2(%REC2##)          = ( RECNUM | EOF )
      DASH(%DASH##)          = '-'
 
    NUM_LINES(%NUM_LINES##)  = .DEC6
    OCTAL(%OCTAL##)          = .OCTAL
    STRING(%STRING##)        = .SLASH
    COLUMNS(%COLUMNS##)      = ( ',' BEG_COL [',' END_COL] )
      BEG_COL(%BEG_COL##)    = .DEC6
      END_COL(%END_COL##)    = .DEC6
 
    RECNUM(%RECNUM##)        = .DEC6
    BOF(%BOF##)              = ( 'B/OF' | 'F/IRST' )
    EOF(%EOF##)              = ( 'E/OF' | 'L/AST' )
    HERE(%HERE##)            = 'H/ERE'
 
    NOT(%NOT##)              = ( '~' | 'NOT' | '<>' )
