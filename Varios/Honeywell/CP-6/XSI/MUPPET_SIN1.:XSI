/*M* Parse nodes for MUPPET */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
 
%INCLUDE MUPPET_C61;
 
MUPPET_CMD(NO)         = {OPEN_CMD|CLOSE_CMD|READ_CMD|WRITE_CMD|SETFP_CMD|;
                          LIST_CMD|WAIT_CMD|PROMPT_CMD|END_CMD} .END
 
OPEN_CMD(%OPEN_CMD#)   = 'OP/EN' .B .FID [DELIM OPTIONS]
CLOSE_CMD(%CLOSE_CMD#) = 'CL/OSE' [.B 'SL/OT'] .B .DEC2
READ_CMD(%READ_CMD#)   = 'RE/AD' [.B 'SL/OT'] .B .DEC2 [.B 'FR/OM'] .B .FID
WRITE_CMD(%WRITE_CMD#) = 'WR/ITE' [.B 'SL/OT'] .B .DEC2 .B HOW_TO .B .FID
SETFP_CMD(%SETFP_CMD#) = 'SET/FP' [.B 'SL/OT'] .B .DEC2 [.B 'FR/OM'] .B .FID
LIST_CMD(%LIST_CMD#)   = 'L/IST' [[.B 'SL/OT'] .B .DEC2]
WAIT_CMD(%WAIT_CMD#)   = 'WA/IT' [[.B 'SL/OT'] .B .DEC2]
PROMPT_CMD(%PROMPT_CMD#)= 'PRO/MPT' .B (.QUOTE31|.ALPHA31+'0123456789')
END_CMD(%END_CMD#)     = {['E'-] 'X/IT'|'E/ND'|'Q/UIT'}
 
OPTIONS(OUT)           = <DELIM,OPTION>
OPTION(NO)             = {READ_OPT|WRITE_OPT|MODE_OPT|FEP_OPT|SLOT_OPT|IGNORE_OPT|CONT_OPT}
 
READ_OPT(%READ_OPT#)   = ('RD'|'RE/AD'|'IN/FILE') EQ .FID
WRITE_OPT(%WRITE_OPT#) = ('WR/ITE'|'OUT/FILE') EQ .FID
MODE_OPT(%MODE_OPT#)   = 'MO/DE' EQ MODE
FEP_OPT(%FEP_OPT#)     = 'FE/P' EQ .DEC2
SLOT_OPT(%SLOT_OPT#)   = 'SL/OT' EQ .DEC2
IGNORE_OPT(%IGNORE_OPT#)= ('IGN/ORE'|'KYFPO/TF')
CONT_OPT(%CONT_OPT#)   = 'CONT/INUE'
 
EQ(NO)                 = ('='|'IS'|.B)
DELIM(NO)              = (','|.B)
 
HOW_TO(LIT=%ON#)       = ('ON'|'TO'|'OVER'|'INTO')
MODE(LIT=%MODE_START#) = ('G/HOST'|'U/SER'|'H/ANDLER'|'C/OMGROUP')
