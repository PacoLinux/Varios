/*M* HERMAN_SIP1  Contains the parse tree for HERMAN.
*/
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
    TOP_HERMAN_CMD=TEXT_COM_NODE COM_TYPE_N
      TEXT_COM_NODE(1)=('.*'- | '~*'-)
      COM_TYPE_N(2)=(ABSTRACT_N      |;
                     BREAK_N         |;
                     KEYWORD_N       |;
                     MODULE_N        |;
                     RESUME_N        |;
                     COPYRIGHT_N     |;
                     BAD_HERMAN_N    |;
                     OTHER_N)
          ABSTRACT_N(1)='A*' [ <.B,KEY_N> ] [.B] .END
          BREAK_N(2)='B*' [ <.B,KEY_N> ] [.B] .END
             KEY_N(NO) = ( KEYQ | KEYA | KEYN )
                KEYQ(99) = .QUOTE
                KEYA(99) = .ANUMERIC+'.*'':0123456789$_#@'
                KEYN(99) = .DEC
          KEYWORD_N(3)='K*' <.B,KEY_N> [.B] .END
          MODULE_N(4)='M*' .END
          RESUME_N(5)='R*' .END
          COPYRIGHT_N(6)='T*' .END
          BAD_HERMAN_N(8)=('A*'    |;
                           'B*'    |;
                           'K*'    |;
                           'M*'    |;
                           'R*'    |;
                           'T*')
          OTHER_N(7)=.ASYM              /* Any other TEXT comment             */
    HERMAN_INVOCATION_CMD(0)=HERMAN_LIT [SI_SPEC [',' UI_SPEC] [.B PREP_SPEC] ;
                          [DEST_SPEC] [',' LS_SPEC] ];
                          [.B] ['(' OPT_SPEC ')'] [.B] .END
          HERMAN_LIT(NO) = .FID
          SI_SPEC(1) = .FID
          UI_SPEC(2) = .FID
          PREP_SPEC(3) = (ON_SPEC | INTO_SPEC | OVER_SPEC)
            ON_SPEC(1) = ('ON' | 'TO')
            INTO_SPEC(2) = 'INTO'
            OVER_SPEC(3) = 'OVER'
          DEST_SPEC(4) = .FID
          OPT_SPEC(5) = <',',OPTION>
            OPTION = ;
              {LS_OPT | UI_OPT | OU_OPT | WA_OPT | CH_OPT | SU_OPT | ER_OPT}
              LS_OPT(1) = [NO_OPT] 'LS' /* LIST SOURCE                        */
              UI_OPT(2) = [NO_OPT] 'UI' /* USE UI INPUT                       */
              OU_OPT(3) = [NO_OPT] 'OU' /* CREATE OBJECT OUTPUT               */
              WA_OPT(4) = [NO_OPT] 'WA/RN' /* DISPLAY WARNING-ONLY MESSAGES   */
              CH_OPT(5) = [NO_OPT] 'CH/ATTER' /* CHATTER ABOUT OPERATIONS     */
              SU_OPT(6) = [NO_OPT] 'SU/MMARY' /* PROVIDE SUMMARY              */
              ER_OPT(7) = [NO_OPT] 'ER/EDUNDANT' /* ELIMINATE REDUNDANT SYNS  */
                NO_OPT(0) = 'N'-
          LS_SPEC(6) = .FID
