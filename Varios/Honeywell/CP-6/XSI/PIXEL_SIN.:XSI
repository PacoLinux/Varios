/*M*  Parse nodes for picture munger */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
 
%INCLUDE PIXEL_C;
 
PICTURE_COMMAND (D) =   (  ANALYZE        | ;
                           HISTOGRAM      | ;
                           PATTERN        | ;
                           ENHANCE        | ;
                           WRITE          | ;
                           PRINT          | ;
                           INVERT         | ;
                           END            | ;
                           DQMARK         | ;
                           QMARK          | ;
                           HELP           | ;
                           NULL              )
 
ANALYZE (%ANALYZE) = 'AN/ALYZE' [ .B < ',', ANAL_OPS > ] .END
 
ANAL_OPS = ( THRESHOLD | SAMPLE | SAMPLE_ALL )
 
THRESHOLD (%THRESHOLD) = 'TH/RESHOLD' '=' .DECIMAL
 
SAMPLE (%SAMPLE) = 'SA/MPLE' '=' .DECIMAL
 
SAMPLE_ALL (%SAMPLE_ALL) = [ 'SA/MPLE' ['='] ] 'A/LL'
 
 
 
HISTOGRAM (%HISTOGRAM) = 'HI/STOGRAM' [ .B HIST_OPT ] .END
 
HIST_OPT =  (  HIST_RAW      |  ;
               HIST_DESIRED  |  ;
               HIST_ACTUAL       )
 
HIST_RAW (%RAW) = 'R/AW'
 
HIST_DESIRED (%DESIRED) = 'D/ESIRED'
 
HIST_ACTUAL (%ACTUAL) = 'A/CTUAL'
 
 
PATTERN (%PATTERN) = 'PA/TTERN' .B PATTERN_LIST .END
 
PATTERN_LIST = < ',', (.QUOTE6 | .SLASH6) >
 
 
ENHANCE (%ENHANCE) = 'EN/HANCE' .B ENHANCE_OPT .END
 
ENHANCE_OPT =  (  ENHANCE_OFF       | ;
                  ENHANCE_SEGMENT   | ;
                  ENHANCE_ON           )
 
ENHANCE_ON (%ENHANCE_ON) = [ 'ON' ] .END
 
ENHANCE_OFF (%ENHANCE_OFF) = 'OFF' .END
 
ENHANCE_SEGMENT (%ENHANCE_SEGMENT) = PAIR ',' < ',', PAIR > .END
 
PAIR(0)  = '(' .DECIMAL ',' .DECIMAL ')'
 
 
WRITE (%WRITE) = 'WR/ITE' [CAP] .END
CAP (%CAP) = 'C/AP'
 
 
PRINT (%PRINT) = 'PR/INT' [ .B < ',', PRINT_OPS > ] .END
 
PRINT_OPS = (  LPI      | ;
               CPI      | ;
               COMPRESS | ;
               SMOOTH   | ;
               VFC      | ;
               SHIFT    | ;
               SHEETS      )
 
LPI (%LPI) = 'LPI' '=' .DECIMAL
 
CPI (%CPI) = 'CPI' '=' .DECIMAL
 
COMPRESS (%COMPRESS) = 'CO/MPRESS' '=' .DECIMAL [ '.' [ .DECIMAL ] ]
 
SMOOTH (%SMOOTH) = 'SM/OOTH'
 
VFC (%VFC) = 'VFC' '='  (   ANS   | ;
                            XDS   | ;
                            CP6      )
 
ANS (%ANS) =   'ANS'
 
XDS (%XDS) =   (  'XDS' | ;
                  'CPV' | ;
                  'CP' '5' )
 
CP6 (%CP6) =   'CP6'
 
SHEETS (%SHEETS) =   'SHE/ETS' '=' .DECIMAL
 
SHIFT (%SHIFT) = 'SHI/FT' '=' .DECIMAL
 
 
INVERT (%INVERT) =   'INV/ERT' ( INVERT_OFF | INVERT_ON ) .END
 
INVERT_OFF (%INVERT_OFF) = .B 'OFF'
 
INVERT_ON (%INVERT_ON) = [ .B 'ON' ]
 
 
END (%END) =   (  'E/ND'   | ;
                  'Q/UIT'  | ;
                  'X'         )  .END
 
DQMARK (%DQMARK) = '??'
 
QMARK (%QMARK) = '?'
 
HELP (%HELP) = 'HELP' .ASYM .END
 
NULL (%NULL) = [.B] .END
 
