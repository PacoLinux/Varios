/*M* SETUP - universal setup program */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X* */
SETUP:  PROC MAIN;
%INCLUDE CP_6;
%INCLUDE B$JIT_C;
DCL B$JIT$ PTR SYMREF;
%FPT_YC(STCLASS=STATIC,CMD=YCCMD);
DCL YCCMD STATIC CHAR(256)INIT('PRIV ASAVE');
%FPT_EOM(FPTN=TMOUT,STCLASS=CONSTANT,UTYPE=MIL10,TIMEOUT=1);
%FPT_EOM(FPTN=TMIN,STCLASS=CONSTANT,UTYPE=MIL10,TIMEOUT=0);
%FPT_READ(STCLASS=CONSTANT,BUF=PROCHAR,DCB=M$UC);
%FPT_PROFILE(STCLASS=CONSTANT,PROFILE=PROF,BUF=IMPBUF);
%VLP_TRMCTL;
%FPT_TRMCTL(STCLASS=CONSTANT,TRMCTL=VLP_TRMCTL);
%FPT_GLINEATTR(STCLASS=CONSTANT,LINEATTR=VLP_LINEATTR);
%VLP_LINEATTR;
DCL PROF CHAR(12)INIT(' ') STATIC;
DCL PROCHAR CHAR(1)STATIC INIT(' ');
%M$DCB(DCBN=M$SI,ASN=FILE,NAME='SETUP');
%M$DCB(DCBN=M$TRM,ORG=TERMINAL,FUN=CREATE);
%M$DCB(DCBN=M$JF,RES='JF',FUN=CREATE);
DCL M$UI DCB;
%FPT_CLOSE(DCB=M$JF,DISP=SAVE);
DCL ARS BASED UBIN ALIGNED;
%FPT_PRECORD(DCB=M$SI,FPTN=PREC,KEYS=YES,KEYR=YES,KEY=IMPKEY);
%FPT_PRECORD(DCB=M$SI,FPTN=NEXT,KEYR=YES,KEY=IMPKEY);
%FPT_READ(DCB=M$SI,FPTN=RIMP,BUF=IMPBUF,KEY=IMPKEY,KEYR=YES);
%FPT_WRITE(BUF=IMPBUF,DCB=M$TRM,FPTN=WIMP,VFC=YES,TRANS=YES);
%FPT_WRITE(DCB=M$JF,FPTN=WRJF);
DCL M$SI$ PTR;
DCL 1 IMPKEY STATIC,
      2 L UBIN(9)UNAL INIT(3),
      2 N UBIN(27)UNAL;
DCL I UBIN STATIC;
DCL NOPROMPT UBIN STATIC INIT(0);
DCL 1 PROS(0:99)STATIC,
      2 NAME CHAR(11)INIT(' '*5,'TI725','CDI1203','DBL1610','HZL2000','TWU1003','TWU9106','VIP7205','VIP7801'),
      2 C CHAR(1)INIT(' '),
      2 IMPKEY UBIN INIT(0*5,725,1203,1610,2000,1003,9106,7205,7801),
      2 BAUD UBIN INIT(0*5,300,0*4,1200,1);
DCL IMPBUF REDEF PROS CHAR(2000);
DCL XEQ BIT(36) STATIC INIT('0'B);
DCL J UBIN STATIC;
DCL PROCNT UBIN STATIC INIT(8);
DCL BAUDS(0:15) UBIN CONSTANT INIT(50,75,110,134,150,200,300,600,1050,1200,
      1800,2000,2400,4800,9600,19200);
        M$SI$=DCBADDR(DCBNUM(M$SI));
        IMPKEY.N=1000;
        CALL M$PRECORD(PREC)ALTRET(NOPROF);
        PROCNT=0;
        DO WHILE(IMPKEY.N<2000);
           CALL INSERT(IMPBUF,0,80,' ');
           CALL M$READ(RIMP)ALTRET(NOPROF);
           CALL INDEX1(J,' ',IMPBUF,0,'017'O);
           IF SUBSTR(IMPBUF,J,1)='-'
           THEN CALL INDEX1(J,' ',IMPBUF,J+1,'017'O);
           PROS.NAME(PROCNT+5)=SUBSTR(IMPBUF,0,J);
           J=J+1;
           CALL INDEX1(I,' ',IMPBUF,J,'017'O);
           PROS.C(PROCNT+5)=SUBSTR(IMPBUF,J,I-J);
           I=I+1;
           CALL INDEX1(J,' ',IMPBUF,I,'017'O);
           IF J>I THEN CALL CHARBIN(PROS.BAUD(PROCNT+5),SUBSTR(IMPBUF,I,J-I));
           ELSE PROS.BAUD(PROCNT+5)=0;
           J=J+1;
           CALL INDEX1(I,' ',IMPBUF,J,'017'O);
           IF I>J THEN CALL CHARBIN(PROS.IMPKEY(PROCNT+5),SUBSTR(IMPBUF,J,I-J));
           ELSE PROS.IMPKEY(PROCNT+5)=IMPKEY.N*1000;
           IF SUBSTR(IMPBUF,0,J)='NOPROMPT' THEN NOPROMPT=PROCNT+5;
           ELSE PROCNT=PROCNT+1;
           CALL M$PRECORD(NEXT)ALTRET(NOPROF);
           END;
NOPROF: ;
        CALL M$GLINEATTR(FPT_GLINEATTR);
        J=0;
        DO I=0 TO PROCNT-1;
           IF J=0 AND PROS.BAUD(I+5)=1
             OR PROS.BAUD(I+5)=BAUDS(VLP_LINEATTR.LINESPEED#) THEN J=I+5;
           END;
        CALL M$EOM(TMOUT);
        PROCHAR=DCBADDR(DCBNUM(M$UI))->M$SI.NAME#.C;
        IF PROCHAR=' ' AND J>=NOPROMPT
        THEN CALL M$READ(FPT_READ)ALTRET(TIMEOUT);
        IF PROCHAR~=' ' THEN DO I=0 TO PROCNT-1;
           IF PROCHAR=SUBSTR(PROS.NAME(I+5),0,1) AND PROS.C(5)=' '
             OR PROS.C(5)~=' ' AND PROCHAR=PROS.C(I+5) THEN DO;
              J=I+5;
              GOTO TIMEOUT;
              END;
           END;
TIMEOUT:;
        IF J=0 THEN J=5;
        CALL INSERT(PROF,1,,PROS.NAME(J));
        J=PROS.IMPKEY(J);
        CALL M$EOM(TMIN);
        CALL INDEX1(I,' ',SUBSTR(PROF,1));
        CALL INSERT(PROF,0,1,BINASC(I));
        IF SUBSTR(PROF,1)~='NONE' THEN CALL M$PROFILE(FPT_PROFILE);
        CALL M$GTRMCTL(FPT_TRMCTL);
        VLP_TRMCTL.SINPUTSZ#=10;
        CALL M$STRMCTL(FPT_TRMCTL);
        DO WHILE('1'B);
           IMPKEY='003'O;
           IMPKEY.N=J*1000;
           CALL M$PRECORD(PREC)ALTRET(NOZERO);
NOZERO:    I=0;
           DO WHILE(IMPKEY.L=3 AND IMPKEY.N/1000=J);
              IMPBUF=' ';
              I=0;
              CALL M$READ(RIMP)ALTRET(QUIT);
              WIMP.BUF_.BOUND=M$SI$->M$SI.ARS#-1;
              FPT_YC.CMD_ = VECTOR(SUBSTR(YCCMD, 0, WIMP.BUF_.BOUND + 1));
              IF SUBSTR(IMPBUF,0,1)~='!' THEN YCCMD=IMPBUF;
              ELSE YCCMD=SUBSTR(IMPBUF,1);
              IF M$SI$->M$SI.DVBYTE.TRANS# THEN CALL M$WRITE(WIMP);
              ELSE IF SUBSTR(IMPBUF,0,4)='JUMP' THEN DO;
                    I=4;DO WHILE(SUBSTR(IMPBUF,I,1)=' ');I=I+1;END;
                    J=0;DO WHILE(SUBSTR(IMPBUF,I,1)~=' ');
                       J=J*10+ASCBIN(SUBSTR(IMPBUF,I,1))-ASCBIN('0');
                       I=I+1;END;
                    END;
                 ELSE IF IMPBUF='XON' THEN XEQ='1'B;
                    ELSE IF IMPBUF='XOFF' THEN XEQ='0'B;
                       ELSE IF NOT XEQ THEN CALL M$YC(FPT_YC)ALTRET(IGNORE);
                          ELSE DO;
                             WRJF.BUF_=WIMP.BUF_;
                             CALL M$WRITE(WRJF);
                             END;
IGNORE:       CALL M$PRECORD(NEXT)ALTRET(QUIT);
              END;
QUIT:      IF J+I=0 THEN DO;
              CALL M$CLOSE(FPT_CLOSE);
              RETURN;
              END;
           IF I=0 THEN J=0;
           END;
END;
