100 !***************************************************************************
110 ! *T* Copyright (c) Bull HN Information Systems Inc., 1989   *
120 !***************************************************************************
130 ! *M* FAST2CAP.X
140 !***************************************************************************
150 ! Fastext to CAP conversion
160 !**************************************************************************
170 WHEN BREAK GOTO  2870
180 ! Initialization
190 WHEN MONERR GOTO ERROR
200 DIM TABLE(256)
210 CLOSE ALL
220 WHEN ERROR GOTO 2780
230 PRINT "File to Convert";
240 INPUT INPUT_FILE$
250 IF INPUT_FILE$="NO" THEN STOP
260 IF SCN(INPUT_FILE$,".",1,1)=0 THEN INPUT_FILE$=INPUT_FILE$+"."+INPUTACCT$
270 WHEN ERROR GOTO ERROR
280 LET NO_INDENTS=0
290 MARGIN 80
300 MARGIN#1,256
310 MARGIN#2,256
320 WHEN EOF#1 GOTO 2780
330 WHEN MONERR GOTO 370
340 OPEN INPUT_FILE$ TO 1,INPUT,CONSEC
350 OPEN "****OUT" TO 2, PRINT,OVER
360 GOTO 400
370 PRINT "*** One of those files doesn't have the right name."
380 WHEN MONERR GOTO ERROR
390 GOTO 100
400 REM Initialize the output file.
410       PRINT#2,".*T* Copyright"
420       PRINT#2,"..::FM"
430 ! Read records, print macros and control words.
440 DO WHILE (0=0)
450 LINPUT#1,REC$
460 IF SST$(REC$+" ",1,2)=".." THEN
470 REM ******************************************************************
480 ! Parsing and verification for all macros.
490     LET MACRO$=SST$(REC$,1,6)
500     LET PARAM$=""
510   IF SCN(REC$,"""",1,1)<>0 THEN
520     LET PARAM$=SST$(REC$,SCN(REC$,"""",1,1))
530     IF SCN(PARAM$,"\",1,1)<>0 THEN
540        PRINT "*** Backslash included in macro"
550        PRINT "at line ";KEY(2)+1;" in ";OUTPUT_FILE$
560     ENDIF
570   ENDIF
580  IF MACRO$="..:IDX" THEN
590     LET MACRO$="..::IX "
600     LET PARAM$=REP$(REP$(PARAM$," ","^",-1,1),";","\",-1,1)
610     LET REC$=MACRO$+PARAM$
620  ELSE
630  IF MACRO$="..:L1H" THEN
640     LET MACRO$="..::L1 "
650    LET LEVEL=1
660   GOSUB 2660
670   LET REC$=MACRO$+PARAM$
680  ELSE
690  IF MACRO$="..:L2H" THEN
700     LET MACRO$="..::L2 "
710    LET LEVEL=1
720   GOSUB 2660
730   LET REC$=MACRO$+PARAM$
740  ELSE
750  IF MACRO$="..:L3H" THEN
760     LET MACRO$="..::L3 "
770   LET LEVEL=1
780   GOSUB 2660
790   LET REC$=MACRO$+PARAM$
800  ELSE
810  IF MACRO$="..:L4H" THEN
820     LET MACRO$="..::IT "
830    LET LEVEL=2
840   GOSUB 2660
850   LET REC$=MACRO$+PARAM$
860  ELSE
870 IF MACRO$="..:MAT" THEN
880 LET NO_INDENTS=1
890 LET RUNNING_SIZE=0
900 LET MACRO$="..::TB "
910 REM Strip off the table title.
920 LET PARAM$=SST$(PARAM$,1,LEN(PARAM$)-1)+";#;"""
930 LET TABLE$=SST$(PARAM$,1,SCN(PARAM$,";",1,1)-1)
940 LET PARAM$=SST$(PARAM$,LEN(TABLE$)+2)
950 IF SCN(UPC$(TABLE$),"TABLE",1,1)=2 THEN
960    LET TABLE$=SST$(TABLE$,SCN(TABLE$,"-",1,1))
970    CHANGE TABLE$ TO TABLE
980    FOR J=1 TO LEN(TABLE$)
990        IF TABLE(J)>64 AND TABLE(J)<91 THEN
1000           LET NEWPOSITION=J \ LET J=257
1010            GOTO 1070
1020        ENDIF
1030        IF TABLE(J)>96 AND TABLE(J)<123 THEN
1040           LET NEWPOSITION=J \ LET J=257
1050            GOTO 1070
1060        ENDIF
1070    NEXT J
1080   LET TABLE$=""""+SST$(TABLE$,NEWPOSITION)
1090 ENDIF
1100 FOR I=1 TO 20
1110
1120 REM Calculate the column title.
1130 LET COLUMN_TITLE$=SST$(PARAM$,1,SCN(PARAM$,";",1,1)-1)
1140 IF COLUMN_TITLE$="#" THEN 1360
1150 LET PARAM$=SST$(PARAM$,LEN(COLUMN_TITLE$)+2)
1160 LET RUNNING_SIZE=LEN(COLUMN_TITLE$)+RUNNING_SIZE
1170 LET TABLE$=TABLE$+"\"+COLUMN_TITLE$
1180
1190 REM Now caluculate the spacing between the columns.
1200 LET COLUMN_SPACE$=SST$(PARAM$,1,SCN(PARAM$,";",1,1)-1)
1210 LET PARAM$=SST$(PARAM$,LEN(COLUMN_SPACE$)+2)
1220 IF COLUMN_SPACE$="#" THEN 1360
1230 IF SCN(COLUMN_SPACE$,"#",1,1)=0 THEN
1240    LET COLUMN_COUNT=VAL(COLUMN_SPACE$)
1250    LET COLUMN_COUNT=COLUMN_COUNT-RUNNING_SIZE
1260    LET RUNNING_SIZE=RUNNING_SIZE+COLUMN_COUNT
1270    LET COLUMN_SPACE$="#"+STR$(COLUMN_COUNT,"###")
1280    LET COLUMN_SPACE$=REP$(COLUMN_SPACE$," ","",-1,1)
1290 ENDIF
1300 REM LET RUNNING_SIZE=0
1310 LET TABLE$=TABLE$+"\"+COLUMN_SPACE$
1320 NEXT I
1330 LET RUNNING_SIZE=0
1340
1350 REM Finish off the new table.
1360 PARAM$=TABLE$+"\#"""
1370 LET REC$=MACRO$+PARAM$
1380 IF SCN(REC$,"\",2,1)=0 THEN LET REC$=SST$(REC$,1,LEN(REC$)-3)+""""
1390  ELSE
1400  IF MACRO$="..:END" THEN
1410  LET NO_INDENTS=0
1420    LET REC$="..::TB"
1430  ELSE
1440  IF MACRO$="..:L0H" THEN
1450     LET MACRO$="..::CH "
1460     IF SECTION_NUMBER$="p" THEN PRINT#2,"..::TT "
1470     PARAM$=SST$(PARAM$,1,LEN(PARAM$)-1)+"\"+SECTION_NUMBER$+"\"+RUNNING_FOOT$+"\"+SECTION_ID$+"\"""
1480    LET SECTION_ID$=""
1490     LET SECTION_NUMBER$=""
1500     LET RUNNING_FOOT$=""
1510  LET REC$=MACRO$+PARAM$
1520  ELSE
1530 IF MACRO$="..:HTP" THEN
1540   LET REC$="..::HL "+REP$(PARAM$,";","\",-1,1)
1550 ELSE
1560 IF MACRO$="..:L5H" THEN
1570     LET MACRO$="..::L5 "
1580   LET LEVEL=2
1590  GOSUB 2660
1600   LET REC$=MACRO$+PARAM$
1610 ELSE
1620 IF MACRO$="..:L6H" THEN
1630     LET MACRO$="..::L6 "
1640   LET LEVEL=2
1650   GOSUB 2660
1660   LET REC$=MACRO$+PARAM$
1670 ELSE
1680 IF MACRO$="..:FIG" THEN
1690 LET NO_INDENTS=1
1700 IF SCN(UPC$(PARAM$),"FIGURE",1,1)=2 THEN
1710    LET PARAM$=SST$(PARAM$,SCN(PARAM$,"-",1,1))
1720    CHANGE PARAM$ TO TABLE
1730    FOR J=1 TO LEN(PARAM$)
1740        IF TABLE(J)>64 AND TABLE(J)<91 THEN
1750           LET NEWPOSITION=J \ LET J=257
1760            GOTO 1820
1770        ENDIF
1780        IF TABLE(J)>96 AND TABLE(J)<123 THEN
1790           LET NEWPOSITION=J \ LET J=257
1800            GOTO 1820
1810        ENDIF
1820    NEXT J
1830   LET PARAM$=""""+SST$(PARAM$,NEWPOSITION)
1840  ENDIF
1850    LET REC$="..::FG "+REP$(PARAM$,";","\",-1,1)
1860 ELSE
1870 IF MACRO$="..:FND" THEN
1880  LET NO_INDENTS=0
1890   LET REC$="..::FG"
1900 ELSE
1910 IF MACRO$="..:UDS" THEN
1920   LET MACRO$="..VA$M_FONT "
1930   LET PARAM$=SST$(PARAM$,1,LEN(PARAM$)-1)+"\I"""
1940   LET REC$=MACRO$+PARAM$
1950 ELSE
1960 IF MACRO$="..:GCS" THEN
1970    LET REC$="..::SB "+PARAM$
1980 ELSE
1990 IF MACRO$="..:FMT" THEN
2000   LET REC$=".*"
2010 ELSE
2020 IF MACRO$="..:DBG" THEN
2030    LET REC$="..VA$M_DEBUG"
2040 ELSE
2050 IF MACRO$="..:ARF" THEN
2060    PRINT "*** Possible macro incompatibility:"
2070    PRINT "written to record ";KEY(2)+1;" in ";OUTPUT_FILE$
2080 ELSE
2090 IF MACRO$="..:MMO" THEN
2100    PRINT "*** Possible macro incompatibility:"
2110    PRINT "written to record ";KEY(2)+1;" in ";OUTPUT_FILE$
2120 ELSE
2130 IF MACRO$="..:PFS" THEN
2140    PRINT "*** Possible macro incompatibility:"
2150    PRINT "written to record ";KEY(2)+1;" in ";OUTPUT_FILE$
2160 ELSE
2170  ENDIF
2180  ENDIF
2190  ENDIF
2200  ENDIF
2210  ENDIF
2220  ENDIF
2230  ENDIF
2240  ENDIF
2250 ENDIF
2260 ENDIF
2270 ENDIF
2280 ENDIF
2290 ENDIF
2300 ENDIF
2310 ENDIF
2320 ENDIF
2330 ENDIF
2340 ENDIF
2350 ENDIF
2360 ENDIF
2370 ELSE
2380 IF SST$(REC$+" ",1,1)="." THEN
2390   IF SST$(UPC$(REC$)+"   ",1,4)=".INL" AND NO_INDENTS=1 THEN REC$=".* "+REC$
2400   IF SST$(UPC$(REC$)+"   ",1,4)=".UNL" AND NO_INDENTS=1 THEN REC$=".* "+REC$
2410   IF SCN(UPC$(REC$),".SRV SECTION",1,1)<>0 THEN
2420     LET SECTION_NUMBER$=REP$(SST$(REC$,14),"""","",-1,1)
2430     IF SST$(SECTION_NUMBER$,1,1)="M" THEN
2440        LET SECTION_ID$="Module"
2450        LET SECTION_NUMBER$=REP$(SECTION_NUMBER$,"M","",-1,1)
2460     ENDIF
2470     LET REC$=".*"
2480   ENDIF
2490   IF SCN(UPC$(REC$),".SRV MANUAL_NAME",1,1)<>0 THEN LET REC$=REP$(REC$,"MANUAL_NAME","TITLE",1,1)
2500   IF SCN(UPC$(REC$),".SRV MANUAL_NUMBER",1,1)<>0 THEN LET REC$=REP$(REC$,"MANUAL_NUMBER","IDENTIFIER",1,1)
2510   IF SCN(UPC$(REC$),".SRV RELEASE_DATE",1,1)<>0 THEN LET REC$=REP$(REC$,"RELEASE_DATE","DATER",1,1)
2520   IF SCN(UPC$(REC$),".SRV RELEASE",1,1)<>0 THEN LET REC$=REP$(REC$,"RELEASE","AUTHOR",1,1)
2530   IF SCN(UPC$(REC$),".SRV PROCESSOR_FOOT",1,1)<>0 THEN
2540     LET RUNNING_FOOT$=REP$(SST$(REC$,22),"""","",-1,1)
2550     LET REC$=".*"
2560   ENDIF
2570  IF SCN(UPC$(REC$),".IFI *:TOC",1,1)<>0 THEN
2580     PRINT#2,"..VA$M_HONEYWELL_COPYRIGHT ""1985"""
2590     LET REC$=".*"
2600  ENDIF
2610 ENDIF
2620 ENDIF
2630 PRINT #2,REC$
2640 END DO
2650 STOP
2660 REM  Calculate the HELP level information.
2670   IF SCN(PARAM$,";",1,1)<>0 THEN
2680    LET HELP$=REP$(SST$(PARAM$,SCN(PARAM$,";",1,1)+1),"""","",-1,1)
2690  LET PARAM$=SST$(PARAM$,1,SCN(PARAM$,";",1,1))
2700   IF HELP$="R" OR HELP$="X" OR HELP$="N" OR HELP$="H" OR HELP$="B" OR HELP$="K" OR HELP$="A" OR HELP$="" THEN
2710  LET PARAM$=SST$(PARAM$,1,SCN(PARAM$,";",1,1)-1)+"\\"+HELP$+""""
2720       LET HELP$=""
2730  ELSE
2740   LET PARAM$=SST$(PARAM$,1,LEN(PARAM$)-1)+"\"+HELP$+"\"+STR$(LEVEL,"#")+""""
2750   ENDIF
2760 ENDIF
2770 RETURN
2780 CLOSE ALL
2790 IBEX "!SET M$LL NO"\IBEX "!SET M$ME NO"
2800 LINK "PCL.:SYS","COPY ****OUT OVER "+INPUT_FILE$
2810 LINK "PCL.:SYS","DELETE ****OUT"
2820 IBEX "!RESET"
2830 PRINT "* "+INPUT_FILE$+" successfully converted."
2840 WHEN ERROR GOTO ERROR
2850 WHEN MONERR GOTO ERROR
2860 WHEN LIBERR GOTO ERROR
2870 STOP
2880 END
