/*M*    NSRELAY_SI61                                                    */
/*
        nsrelay support routines
*/
 
CGSTUFF: PROC;
%INCLUDE CP_6;
    %B$CGAURD (STCLASS="BASED (RQS$)");
    %EQU_CG;
    %F$DCB;
    %FPT_CGCTL (DCB=F$CG, CGCP=VLP_CGCP, STCLASS=CONSTANT);
    %FPT_EXTEND (DCB=F$CG, SIZ=10, STCLASS=CONSTANT);
    %FPT_OPEN (DCB=F$CG, ASN=COMGROUP, AU=YES, CTG=YES, STCLASS=CONSTANT,
              FUN=CREATE, EXIST=NEWFILE, SHARE=ALL, IXTNSIZE=20,
              ACCT=VLP_ACCT, NAME=VLP_NAME, SETSTA=VLP_SETSTA, ACCESS=ACCESS);
    %FPT_READ (DCB=F$CG, BUF=RQS, WAIT=NO, EVENT=1, STCLASS=CONSTANT);
    %FPT_WRITE (DCB=F$CG, STATION=VLP_STATION, BUF=RSP, STCLASS=CONSTANT);
    %VLP_ATTR (FPTN=ACCESS, STCLASS=CONSTANT);
    %VLP_ACCESS (READ=YES, UPD=YES, WNEW=YES, ACCT='?', LAST=";");
    %VLP_ACCT (ACCT=':SYS', STCLASS=CONSTANT);
    %VLP_CGCP (DCBCONWA=YES, DCBCONNAU=NO,
              AUDCONDCB=DISCONNECT, STCLASS=CONSTANT);
    %VLP_NAME (NAME='NAMESERCG', STCLASS=CONSTANT);
    %VLP_SETSTA (MYSTATION='SERVER', STCLASS=CONSTANT);
    %VLP_STATION;
    %VLP_STATION (FPTN=B$STATION, STCLASS="BASED (P$)");
 
    DCL F$CG DCB;
 
    DCL P$ PTR;
    DCL RQS$ PTR CONSTANT INIT (ADDR (RQS));
    DCL CLSCALLBACK EPTR STATIC SYMDEF INIT (ENTADDR (NIL));
    DCL RQSCALLBACK EPTR STATIC SYMDEF INIT (ENTADDR (NIL));
    DCL RQS CHAR(40) STATIC SYMDEF;
    DCL RSP CHAR(132) STATIC SYMDEF;
    DCL RQSSTATION CHAR(8) STATIC SYMDEF;
    DCL RSPSTATION CHAR(8) STATIC SYMDEF;
 
CGSEND: ENTRY;
    /* Sends a message through nsrelay's comgroup to a client */
    VLP_STATION.STATION# = RSPSTATION;
    CALL M$WRITE (FPT_WRITE);
    RETURN;
 
CGINIT: ENTRY;
    /* Initializes nsrelay's comgroup for client connections */
    CALL M$OPEN (FPT_OPEN);
    CALL M$CGCTL (FPT_CGCTL);
    CALL M$READ (FPT_READ);  /* issue first comgroup NoWait read */
    RETURN;
 
CGEVNT: ENTRY;
    /* Processes nsrelay's comgroup NoWait read events */
    P$ = DCBADDR (DCBNUM (F$CG))->F$DCB.LASTSTA$;
    RQSSTATION = B$STATION.STATION#;
    IF B$STATION.MSGTYP# = '*AUEV   ' THEN
        DO SELECT (B$CGAURD.EVENT);
            SELECT (%CG_DOPN#);
            SELECT (%CG_DCLS#);
                CALL CLSCALLBACK;
            SELECT (%CG_WARN#);
                DO SELECT (B$CGAURD.INFO);
                    SELECT (%CG_WARN_DISK#);
                        CALL M$EXTEND (FPT_EXTEND);
                    SELECT (ELSE);
                        CALL M$ERR;
                    END;
            SELECT (ELSE);
                CALL M$ERR;
            END;
    ELSE
        CALL RQSCALLBACK;
    CALL M$READ (FPT_READ);  /* reissue comgroup NoWait read */
    RETURN;
 
    END CGSTUFF;
