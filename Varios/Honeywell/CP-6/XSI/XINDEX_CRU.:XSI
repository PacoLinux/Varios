!DEFAULT SIACCT=:XSI,GNACCT=:XSI,LBACCT=:LIBRARY, XACCT=:XSI,JUMP=0
!JOB NAME=XINDEX_CRU,WSN=LOCAL
!RESOURCE TIME=20,MEM=256
!"******************************************************************
!"  The substitution variable JUMP is used to run parts of this job.
!"  JUMP=1    only creates XINDEX.X
!"  JUMP=2    only creates XINDEX_ID1 & XINDEX_ID2
!"  JUMP=3    only creates XINDEX_ID3
!"  JUMP=4    only creates HELP:XINDEX:
!"******************************************************************
!ROUTE Geoff Sickler, LADC
!IF 'JUMP'='1' THEN GOTO JOB1
!IF 'JUMP'='2' THEN GOTO JOB2
!IF 'JUMP'='3' THEN GOTO JOB3
!IF 'JUMP'='4' THEN GOTO JOB4
!JOB1:
!FORTRAN XINDEX_SIF1.SIACCT OVER *XINDEX_OUF.GNACCT,LP (LS,OU)
!PL6 XINDEX_SI61.SIACCT OVER *XINDEX_OU6.GNACCT,LP (NSYS,SCHEMA,SR(.LBACCT))
!LINK B_COPYRIGHT.LBACCT, ;
     *XINDEX_OU6.GNACCT,;
     *XINDEX_OUF.GNACCT ;
     OVER XINDEX.GNACCT(IP=50,DP=10)
!MOD XINDEX.GNACCT TO (ACC((?,ZZZ?,ZZZCUST),EXEC))
!IF STEPCC~=0 THEN END
!IF 'JUMP'~='0' THEN END
!JOB2:
!"********************************************************************
!"  Creates list of tools by categories and keywords as defined in
!"  by the XINDEX topic in the _SIH1.:XSI help source files.
!"  Files are stored as XINDEX_ID1 (categories) and XINDEX_ID2
!"  (keywords).
!"********************************************************************
!L (C=0) ?_CRU.SIACCT OVER *HELP_LIST(LN)
!E
E *HELP_LIST
0/_CRU /S/_SIH1 /
END
!E *HELP_LIST
SE;IF/XINDEX/;DE
END
!BASIC
110 LOCAL=1\ERRORS=0
120 LET FILES_READ=0\NOLIST=0
130 WHEN ERROR GOTO 480
140 CLOSE ALL
150 OPEN "*HELP_LIST" TO 2,INPUT,CONSEC\MARGIN#3,256
160 OPEN "*XINDEX_ID0" TO 3,PRINT,OVER\MARGIN#3,256
170 WHEN EOF#2 GOTO 530
180 LINPUT#2,FILE$
190 IF SST$(FILE$+"  ",1,2)=".." THEN 530
200 OPEN FILE$+".SIACCT" TO 1,INPUT,CONSEC\MARGIN#1,256
210 LET FILES_READ=FILES_READ+1
220 LOCAL=1\ERROR_COND=1
230 WHEN EOF#1 GOTO 480
240 LINPUT#1,REC$
250 IF SST$(REC$+"    ",1,4)=".*R*" THEN 240
260 IF SST$(REC$+"    ",1,4)=".*X*" THEN LOCAL=1
270 IF SST$(REC$+"    ",1,4)=".*K*" THEN LOCAL=1
280 IF SST$(REC$+"                 ",1,12)=".*K* XINDEX " THEN
290    LOCAL=0
300    LINPUT#1,KEY2$
310    IF SST$(KEY2$+"    ",1,4)=".*K*" THEN
320       ERROR_COND=1\LOCAL=1
330         GOTO 480
340    ENDIF
350   LET KEY3$=SST$(KEY2$,SCN(KEY2$,".",1,1)+1)
360   LET KEY2$=SST$(KEY2$,1,SCN(KEY2$,".",1,1)-1)
370    GOTO 240
380 ENDIF
390 IF LOCAL=0 THEN
400   IF REC$="AID=NOLIST" THEN
410       LOCAL=1\ERROR_COND=2\GOTO 480
420   ENDIF
430 PRINT #3,""""+REP$(REC$,"AID=","!!!",-1,1)+"""";TAB(35);""""+KEY2$+"""";TAB(70);""""+KEY3$+"""";TAB(200);"^"
440 ! PRINT REP$(REC$,"AID=","",-1,1);TAB(35);KEY2$;TAB(70);KEY3$
450    ERROR_COND=0
460 ENDIF
470 GOTO 240
480 CLOSE 1
490 IF ERROR_COND=1 THEN PRINT "*** File "+FILE$+" has an erroneous XINDEX topic."\ERRORS=ERRORS+1
500 IF ERROR_COND=2 THEN PRINT "* File "+FILE$+" rejected as 'AID=NOLIST'."\NOLIST=NOLIST+1
510 GOTO 180
520 PRINT KEY(1)
530 CLOSE ALL
540 PRINT\PRINT "Help source files read = ";FILES_READ
550 PRINT "Errors Encountered = ";ERRORS
551 PRINT "Files Excluded as NOLIST = ";NOLIST
660 END
RUN
SYS
!SORT *XINDEX_ID0 OVER *XINDEX_ID1
REC INLEN=200
KEY START=1,LEN=69
END
!COPY *XINDEX_ID1 OVER *XINDEX_ID1(ORG=K)
!COPY *XINDEX_ID1 OVER *XINDEX_ID2(ORG=K)
!E *XINDEX_ID1
FD0-99999,NOT/!!!/
SE;/!!!/D
END
!E *XINDEX_ID2
FD0-99999,/!!!/
END
!BASIC
750 OPEN "*XINDEX_ID1" TO 1,INPUT,CONSEC\MARGIN#1,256\DELIMIT#1," "\WHEN EOF#1 GOTO 840
760 OPEN "XINDEX_ID1.SIACCT" TO 2,PRINT,OVER,CONSEC\MARGIN#2,79
770 INPUT#1,KEY1$,KEY2$,KEY3$,TRASH$
780 IF KEY1$<>KEYSTORE$ THEN
790   PRINT#2," "," "," "," "," "," "
791   PRINT#2,".*K* "+KEY1$
792   PRINT#2,"Category = "+KEY1$
800   LET KEYSTORE$=KEY1$
810 ENDIF
820 PRINT#2,KEY2$+"."+KEY3$,
830 GOTO 770
840 CLOSE 1
860 END
RUN
SYS
!BASIC
750 OPEN "*XINDEX_ID2" TO 1,INPUT,CONSEC\MARGIN#1,256\DELIMIT#1," "\WHEN EOF#1 GOTO 840
760 OPEN "XINDEX_ID2.SIACCT" TO 2,PRINT,OVER,CONSEC\MARGIN#2,79
761 PRINT#2,".*K* KEYWORDS"
762 PRINT#2,"This help topic contains list of tools filed under keywords."
763 PRINT#2,"To obtain a list of keywords, type:"
764 PRINT#2,"    HELP (XINDEX.X) KEYWORDS TOPICS"\PRINT#2," "
765 PRINT#2,"To obtain the list of tools under each keyword, type:"
766 PRINT#2,"    HELP (XINDEX.X) KEYWORDS keyword"
770 INPUT#1,KEY1$,KEY2$,KEY3$,TRASH$
780 IF KEY1$<>KEYSTORE$ THEN
790   PRINT#2," "," "," "," "," "," "
791   PRINT#2,".*B* "+KEY1$
792   PRINT#2,"Keyword = "+KEY1$
800   LET KEYSTORE$=KEY1$
810 ENDIF
820 PRINT#2,KEY2$+"."+KEY3$,
830 GOTO 770
840 CLOSE 1
860 END
RUN
SYS
!IF STEPCC~=0 THEN END
!DELETE *XINDEX_ID0,*XINDEX_ID1,*XINDEX_ID2,*HELP_LIST,*SORT
!IF 'JUMP'~='0' THEN END
!JOB3:
!"*******************************************************************
!"  Creates a topic that contains one-line descriptions of all the
!"  tools as generated from the XINDEX_ABSTRACT topic.
!"  File is stored as XINDEX_ID3.SIACCT
!"*******************************************************************
!L (C=0) ?_CRU.SIACCT OVER *HELP_LIST(LN)
!E
E *HELP_LIST
0/_CRU /S/_SIH1 /
END
!E *HELP_LIST
SE;IF/XINDEX/;DE
END
!BASIC
100 LOCAL=1\ERRORS=0
110 LET FILES_READ=0\NOLIST=0
120 WHEN ERROR GOTO 620
130 CLOSE ALL
140 OPEN "*HELP_LIST" TO 2,INPUT,CONSEC\MARGIN#3,256
150 OPEN "*XINDEX_ID3" TO 3,PRINT,OVER\MARGIN#3,256
160 WHEN EOF#2 GOTO 450
170 LINPUT#2,FILE$
180 IF SST$(FILE$+"  ",1,2)=".." THEN 450
190 CLOSE 1\OPEN FILE$+".SIACCT" TO 1,INPUT,CONSEC\MARGIN#1,256
200 CLOSE 4\OPEN "*TRASH" TO 4,PRINT,OVER,CONSEC\MARGIN#4,256
220 LET FILES_READ=FILES_READ+1
230 LOCAL=1\ERROR_COND=1
240 WHEN EOF#1 GOTO 380
250 LINPUT#1,REC$
260 IF SCN(REC$+"           ","AID=NOLIST",1,1)<>0 THEN
270   PRINT "* "+FILE$+" rejected as AID=NOLIST."\NOLIST=NOLIST+1\GOTO 170
280 ENDIF
290 IF SST$(REC$+"    ",1,4)=".*R*" THEN 250
300 IF SST$(REC$+"    ",1,4)=".*X*" THEN LOCAL=1
310 IF SST$(REC$+"    ",1,4)=".*K*" THEN LOCAL=1
320 IF SST$(REC$+"                           ",1,20)=".*K* XINDEX_ABSTRACT" THEN
330     LOCAL=0
340     GOTO 250
350 ENDIF
360 IF LOCAL=0 THEN PRINT#4,REC$
370 GOTO 250
380 CLOSE 1
390 CLOSE 4
400 OPEN "*TRASH" TO 4,INPUT,CONSEC\MARGIN#4,256\WHEN EOF#4 GOTO 170
410 LINPUT#4,REC$
420 PRINT#3,REC$
430 GOTO 410
440 GOTO 170
450 PRINT\PRINT "Files read = ";FILES_READ
460 PRINT "Errors encountered = ";ERRORS
470 PRINT "Files rejected as AID=NOLIST = ";NOLIST
480 PRINT
490 CLOSE ALL
500 OPEN "*XINDEX_ID3" TO 1,INPUT,UPDATE,CONSEC\MARGIN#1,256\WHEN EOF#1 GOTO 610
510 OPEN "XINDEX_ID3.SIACCT" TO 2,PRINT,OVER,CONSEC\MARGIN#2,256
520 PRINT#2,".*K* ABSTRACTS"
521 PRINT#2,"This help topic contains short descriptions of the tools"
522 PRINT#2,"in the X account.  To obtain a complete summary of the tools in"
523 PRINT#2,"alphabetical order, type two question marks (??) followed by"
524 PRINT#2,"a carriage return.  To obtain a short description on a particular"
525 PRINT#2,"tool, type:"\PRINT#2," "
526 PRINT#2,"   !HELP (XINDEX.X) ABSTRACTS tool"
530 LINPUT#1,REC$
540 IF SST$(REC$+"      ",1,5)="TOOL=" THEN
550    LET TOPIC$=SST$(REC$,6)
551    PRINT#2," "
560    PRINT#2,".*B* "+TOPIC$
570    PRINT#2,TOPIC$
580 ELSE
590    PRINT#2,REC$
600 ENDIF
601 GOTO 530
610 STOP
620 PRINT "* Error encountered processing "+FILE$\ERRORS=ERRORS+1
630 GOTO 170
640 END
RUN
SYS
!IF STEPCC~=0 THEN END
!DELETE *HELP_LIST,*XINDEX_ID3,*TRASH
!IF 'JUMP'~='0' THEN END
!JOB4:
!"***************************************************************
!"  Collates all the XINDEX_SIH? files into a star file, which
!"  is then HERMANed into a valid help file.
!"***************************************************************
!COPY XINDEX_SIH1.SIACCT(1-37),XINDEX_ID1.SIACCT,XINDEX_ID2.SIACCT,XINDEX_ID3.SIACCT OVER *XINDEX_SIH1(NLN)
!"TUNA.X *XINDEX_SIH1(LEN=79)
!HERMAN.X *XINDEX_SIH1 OVER HELP:XINDEX:.GNACCT(NLS)
!C *XINDEX_SIH1 OVER XXINDEX_SIH1_SV1.:XSI  "DEBUG!
!IF STEPCC~=0 THEN END
!OUTPUT 'DONE'
!C   HELP:XINDEX:.GNACCT OVER HELP:XINDEX:.GNACCT (ACC((?,ZZZ?,ZZZCUST),READ))
