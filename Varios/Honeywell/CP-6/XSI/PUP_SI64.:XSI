/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
PMGRUNT: PROC (PROCNAME,OUNAME,VERS,STAR,PATCHFID,UTSTD,TEMPFID) ALTRET;
 
/*M*     PMGRUNT - write notes in PMERGE then PMLORDS  */
/*X*       DMR,PLM=5,IND=5,CTI=5,SDI=5,CSI=0,ECI=0           */
 
DCL PROCNAME CHAR(31);
DCL VERS CHAR(6);
DCL STAR CHAR(10);
DCL OUNAME CHAR(31);
DCL PATCHFID CHAR(60);
DCL TEMPFID CHAR(31);
DCL UTSTD CHAR(60);
/*
    Include files.
*/
%INCLUDE CP_6;
%INCLUDE CP_6_SUBS;
 
%FPT_OPEN (FPTN = SC_OPEN,
    DCB = M$SC,
    ACS = SEQUEN,
    ASN = FILE,
    CTG = YES,
    NAME = P1);
 
%FPT_WRITE (FPTN = SC_WRITE,
    DCB = M$SC);
 
%FPT_CLOSE (FPTN = SC_CLOSE,
    DCB = M$SC,
    DISP = SAVE);
 
%FPT_OPEN (FPTN = SI_OPEN,
    DCB = M$SI,
    ACS = SEQUEN,
    ORG = CONSEC,
    ASN = FILE,
    CTG = YES,
    NAME = PMCMD);
 
%FPT_WRITE (FPTN = SI_WRITE,
    DCB = M$SI);
 
%FPT_CLOSE (FPTN = SI_CLOSE,
    DCB = M$SI,
    DISP = SAVE);
 
DCL M$SI DCB;
%VLP_NAME (FPTN = PMCMD,
    NAME = '*PMCMD');
 
DCL M$SC DCB;
%VLP_NAME (FPTN = P1,
    NAME = '*P1');
%EJECT;
%FPT_LINK (FPTN = TO_PMERGE,
  CP_CMD = PMERGE_LINK);
 
%FPT_YC (FPTN = SETCMD,
  CMD = SETMSG);
 
 
%FPT_YC (FPTN = RSETCMD,
  CMD = RSETMSG);
 
%FPT_LINK (FPTN = TO_PCL,
  CMD=PCL_CMD,
  ACCT=SYS,
  NAME=PCL);
 
%VLP_ACCT (FPTN = SYS,ACCT=':SYS');
%VLP_NAME (FPTN = PCL,NAME = 'PCL');
 
%EJECT;
DCL SETMSG CHAR(15) CONSTANT INIT('SET M$SI *PMCMD');
DCL RSETMSG CHAR(10) CONSTANT INIT('RESET M$SI');
DCL 1 RNAMEMSG CHAR(80) STATIC;
DCL 1 PMERGE_CMD STATIC,
         2 L UBIN (9) INIT(80) UNAL,
         2 C CHAR (80) UNAL;
DCL 1 PCL_CMD STATIC,
         2 L UBIN (9) INIT(80) UNAL,
         2 C CHAR (80) UNAL;
DCL PMEND CHAR (3) STATIC INIT ('END');
 
    %VLP_NAME (FPTN=PMERGE_LINK,NAME='PMERGE.X');
 
    %VLP_NAME (FPTN=PCL_LINK,NAME='PCL');
 
DCL 1 PM_CMD CHAR(80) STATIC;
 
DCL 1 XDEL1 STATIC,
         2 XDEL CHAR (7) INIT ('!XDEL "') UNAL,
         2 VERS CHAR (5) UNAL,
         2 * CHAR (52) INIT (' ') UNAL,
         2 VERS2 CHAR (10) UNAL,
         2 * CHAR (1) INIT ('#') UNAL,
         2 STAR CHAR (5) UNAL;
DCL XDEL1X REDEF XDEL1 CHAR(80);
 
DCL 1 COMMENT STATIC,
         2 COMMENT CHAR (10) INIT ('" * NULL *') UNAL,
         2 * CHAR (54) INIT(' ') UNAL,
         2 VERS CHAR (10) UNAL,
         2 * CHAR (1) INIT ('#') UNAL,
         2 STAR CHAR (5) UNAL;
DCL COMMENTX REDEF COMMENT CHAR(80);
 
 
DCL 1 RUM1 STATIC,
         2 RUM CHAR (6) INIT ('!RUM "') UNAL,
         2 VERS CHAR (6) UNAL,
         2 * CHAR (68) INIT(' ') UNAL;
DCL RUM1X REDEF RUM1 CHAR(80);
 
DCL 1 RUM2 STATIC,
         2 RUM CHAR (64) INIT(' ') UNAL,
         2 VERS CHAR (10) UNAL,
         2 * CHAR (1) INIT ('#') UNAL,
         2 STAR CHAR (5) UNAL;
DCL RUM2X REDEF RUM2 CHAR(80);
 
DCL 1 PLO1 STATIC,
         2 PLO CHAR (9) INIT ('!PLOVER "') UNAL,
         2 VERS CHAR (6) UNAL,
         2 * CHAR (65) INIT(' ') UNAL;
DCL PLO1X REDEF PLO1 CHAR(80);
 
DCL 1 PLO2 STATIC,
         2 PLO CHAR (64) INIT(' ') UNAL,
         2 VERS CHAR (10) UNAL,
         2 * CHAR (1) INIT ('#') UNAL,
         2 STAR CHAR (5) UNAL;
DCL PLO2X REDEF PLO2 CHAR(80);
 
DCL I UBIN;
DCL J UBIN;
DCL SG$FIRST BIT (1) STATIC INIT ('1'B);
 
%EJECT;
 
     CALL INDEX1(I,' ',UTSTD);
     CALL CONCAT(PM_CMD,'STAND = ',SUBSTR(UTSTD,0,I));
     PMERGE_CMD.L=8+I;
     PMERGE_CMD.C=PM_CMD;
     SI_OPEN.V.FUN# = %CREATE#;
     SI_OPEN.V.EXIST# = %NEWFILE#;
 
     CALL M$OPEN (SI_OPEN) ALTRET (BARFOUT);
     SI_WRITE.BUF_ = VECTOR (PM_CMD);
     CALL M$WRITE (SI_WRITE) ALTRET (BARFOUT);
     PM_CMD='INSERT PLO';
     PMERGE_CMD.C='INSERT PLO';
     PMERGE_CMD.L=10;
     CALL M$WRITE (SI_WRITE) ALTRET (BARFOUT);
     CALL INDEX(I,'INCR',PATCHFID);
     IF I < 30 THEN DO;
          PM_CMD='SHORTEN';
          PMERGE_CMD.C='SHORTEN';
          PMERGE_CMD.L=7;
          CALL M$WRITE (SI_WRITE) ALTRET (BARFOUT);
        END;
     CALL INDEX1(J,' ',TEMPFID);
     CALL INDEX1(I,' ',PATCHFID);
     CALL CONCAT(PM_CMD,'PM *P1,',SUBSTR(PATCHFID,0,I),' INTO ',SUBSTR(TEMPFID,0,J),',*P3');
     PMERGE_CMD.L = 7+J+6+I+4;
     PMERGE_CMD.C=PM_CMD;
     CALL M$WRITE (SI_WRITE) ALTRET (BARFOUT);
     CALL M$CLOSE (SI_CLOSE) ALTRET (BARFOUT);
     SC_OPEN.V.FUN# = %CREATE#;
     SC_OPEN.V.EXIST# = %NEWFILE#;
 
     CALL M$OPEN (SC_OPEN) ALTRET (BARFOUT);
 
     IF PROCNAME='M:MON' THEN DO;
          XDEL1.VERS=VERS; XDEL1.VERS2=VERS; XDEL1.STAR=STAR;
          SC_WRITE.BUF_ = VECTOR (XDEL1);
          CALL M$WRITE (SC_WRITE) ALTRET (BARFOUT);
 
          COMMENT.STAR=STAR;
          COMMENT.VERS=VERS;
          SC_WRITE.BUF_ = VECTOR (COMMENT);
          CALL M$WRITE (SC_WRITE) ALTRET (BARFOUT);
        END;
 
     ELSE IF OUNAME = ' ' THEN DO;
               RUM1.VERS=VERS;
               SC_WRITE.BUF_ = VECTOR (RUM1);
               CALL M$WRITE (SC_WRITE) ALTRET (BARFOUT);
 
               CALL INDEX1(I,' ',PROCNAME);
               CALL CONCAT(RUM2.RUM,'RUM ',SUBSTR(PROCNAME,0,I),',UTS=',VERS,' "');
               RUM2.VERS=VERS;
               RUM2.STAR=STAR;
               SC_WRITE.BUF_ = VECTOR (RUM2);
               CALL M$WRITE (SC_WRITE) ALTRET (BARFOUT);
 
               COMMENT.STAR=STAR;
               COMMENT.VERS=VERS;
               SC_WRITE.BUF_ = VECTOR (COMMENT);
               CALL M$WRITE (SC_WRITE) ALTRET (BARFOUT);
             END;
 
          ELSE DO;
               PLO1.VERS=VERS;
               SC_WRITE.BUF_ = VECTOR (PLO1);
               CALL M$WRITE (SC_WRITE) ALTRET (BARFOUT);
 
               CALL INDEX1(I,' ',PROCNAME);
               CALL INDEX1(J,' ',OUNAME);
               CALL CONCAT(PLO2.PLO,'PLOVER ',SUBSTR(PROCNAME,0,I),' (OU=',SUBSTR(OUNAME,0,J),',UTS=');
               CALL INDEX1(I,'UTS=',PLO2.PLO);
               CALL INDEX1(J,' ',VERS);
               CALL CONCAT(PLO2.PLO,SUBSTR(PLO2.PLO,0,I+4),SUBSTR(VERS,0,J),') "');
               PLO2.VERS=VERS;
               PLO2.STAR=STAR;
               SC_WRITE.BUF_ = VECTOR (PLO2);
               CALL M$WRITE (SC_WRITE) ALTRET (BARFOUT);
 
               COMMENT.STAR=STAR;
               COMMENT.VERS=VERS;
               SC_WRITE.BUF_ = VECTOR (COMMENT);
               CALL M$WRITE (SC_WRITE) ALTRET (BARFOUT);
             END;
 
     CALL M$CLOSE (SC_CLOSE) ALTRET (BARFOUT);
     CALL M$YC(SETCMD);
     CALL M$LINK (TO_PMERGE) ALTRET (BARFOUT);
     CALL M$YC(RSETCMD);
     CALL CONCAT(PCL_CMD.C,'C *P3 OVER ',PATCHFID);
     CALL M$LINK (TO_PCL) ALTRET (BARFOUT);
     PCL_CMD.C='DEL *P3';
     CALL M$LINK (TO_PCL) ALTRET (BARFOUT);
     RETURN;
 
BARFOUT:
 
     ALTRETURN;
 
END PMGRUNT;
