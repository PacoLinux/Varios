/*M* JEDI_PARSE - Parse JEDI command input */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
 
JPARSE: PROC (NAME, DB, OK);
 
/* PL6 parse routine for the JEDI processor */
 
/* Synopsis - JPARSE will return to the caller the packset name, the file
   name, the file account, and the file password. If the parse routine works,
   OK will be true, if not OK will be false. */
 
/* =========================== */
/* ===== INCLUDE SECTION ===== */
/* =========================== */
 
%INCLUDE CP_6;
%INCLUDE B$JIT;
%INCLUDE B_STRINGS_C;
%INCLUDE XU_MACRO_C;
%INCLUDE XUG_ENTRY;
 
/* ========================= */
/* ===== MACRO SECTION ===== */
/* ========================= */
 
%XUG_INIT         (NAME=PARSER_INIT,
                   ALWAYS_SET_PROMPT=NO,
                   COMMANDS=NO,
                   GOODCMD=YES,
                   SYNTAX=YES,
                   ECHO=HONOR_FLAG,
                   PREPROCESS=YES,
                   WSEG=0);
 
%XUG_GETCMD       (NAME=GET_JIT,
                   CONTINUE_OK=NO,
                   NODES=JEDI$,
                   ALWAYS_SET_PROMPT=YES,
                   PROMPT=COLON,
                   TEXT=CCBUF);
 
%PARSE$OUT        (NAME=POUT,
                   STCLASS="BASED (GET_JIT.OUT$)");
 
%PARSE$SYM        (NAME=PSYM,
                   STCLASS="BASED (GET_JIT.OUT$)");
 
/* =============================== */
/* ===== DECLARATION SECTION ===== */
/* =============================== */
 
DCL B$JIT$                             PTR SYMREF;
DCL JEDI$                              PTR SYMREF;
 
DCL COLON                              CHAR (2) CONSTANT INIT (': ');
DCL NAME                               CHAR(59);
DCL CCBUF                              CHAR(300) STATIC;
DCL DB                                 SBIN WORD;
DCL OK                                 BIT(1);
 
/* ======================== */
/* ===== MAIN PROGRAM ===== */
/* ======================== */
 
START: OK = %FALSE;
       CCBUF = SUBSTR(B$JIT.CCBUF,0,B$JIT.CCARS);
 
       CALL XUG$INIT (GET_JIT, PARSER_INIT)
         WHENALTRETURN DO;
           RETURN;
         END;
 
       CALL XUG$GETCMD (GET_JIT)
         WHENALTRETURN DO;
           RETURN;
         END;
 
       NAME = SUBSTR(POUT.SUBLK$(1)->PSYM.TEXT,0,POUT.SUBLK$(1)->PSYM.COUNT);
       DB = POUT.SUBLK$(2) -> POUT.CODE;
       IF (DB = 1 OR DB = 2) THEN
       DO;
         OK = %TRUE;
       END;
 
       RETURN;
 
END JPARSE;
