/*M* CAT_SIN1 CAT'S PARSE NODES */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
%INCLUDE CP_6_SUBS;
%INCLUDE CAT_C62;
%INCLUDE CAT_C61;
CAT_NODES                       = (LIST | COPYALL | COPY | END) .END
END(%(%P#VAL#+%CAT_FUN#))       = VEND
VEND(%CAT_END#)                 = ('E/ND' | 'X' | 'Q')
LIST(%(%P#VAL#+%CAT_FUN#))      = VLIST .b RES '#'- SN ;
                                      ['/' [FNAME]] ['.' [ACCT]] ;
                                      ['(' <',',{[NOT] A | FSN | NREW | ;
                                      SFSN | SYSTEM | DUMP | DEN}> ')']
VLIST(%CAT_LIST#)               = 'L/IST'
COPY(%(%P#VAL#+%CAT_FUN#))      = VCOPY .b RES '#'- SN ;
                                      ['/' [FNAME]] ['.' [ACCT]] ;
                                      ['(' <',',{FSN | SFSN | NREW | ;
                                      SYSTEM | DUMP | DEN}> ')'] ;
                                      .b EXIST .b ;
                                      FNAMEO ['.' [ACCTO]] ;
                                      ['(' TYPEO ')']
VCOPY(%CAT_COPY#)               = 'C/OPY'
COPYALL(%(%P#VAL#+%CAT_FUN#))   = VCOPYALL .b RES '#'- SN ;
                                      ['/' [FNAME]] ['.' [ACCT]] ;
                                      ['(' <',',{FSN | SFSN | NREW | ;
                                      SYSTEM | DUMP | DEN}> ')'] ;
                                      [.b EXIST [.b ;
                                      [FNAMEQO] ['.' [ACCTO]]] ;
                                      ['(' TYPEO ')']]
VCOPYALL(%CAT_COPYALL#)         = ('CA' | 'COPY' 'A/LL')
EXIST(%(%P#VAL#+%CAT_EXIST#))   = (ON | OVER | INTO)
ON(%ERROR#)                     = ('ON' | 'TO')
OVER(%NEWFILE#)                 = 'OVER'
INTO(%OLDFILE#)                 = 'INTO'
RES(%(%P#VAL#+%CAT_RES#))       = (FT | XT | GT)
FT(%CAT__FT#)                   = 'FT'-
XT(%CAT__XT#)                   = 'XT'-
GT(%CAT__GT#)                   = 'GT'-
SYSTEM(%(%P#VAL#+%CAT_RES#))    = ['SYS/TEM' '='] (CPV | GCOS)
CPV(%CAT__XT#)                  = ('CPV' | 'CP5' | 'CP-V' | 'CP-5')
GCOS(%CAT__GT#)                 = 'GCOS'
SN(%(%P#TXC$+%CAT_SN#))         = .al6+'0123456789$:'
FNAME(%(%P#TXC$+%CAT_FNAME#))   = .al31+'0123456789$:_-?'
ACCT(%(%P#TXC$+%CAT_ACCT#))     = .al8+'0123456789$:_-'
FNAMEO(%(%P#TXC$+%CAT_FNAMEO#)) = .al31+'0123456789$:_-*'
FNAMEQO(%(%P#TXC$+%CAT_FNAMEO#))= .al31+'0123456789$:_-*?'
ACCTO(%(%P#TXC$+%CAT_ACCTO#))   = .al8+'0123456789$:_-'
DUMP(%(%P#BIT1#+%CAT__DUMP#))   = 'DUMP'
DEN(%(%P#VAL#+%CAT__DEN#))      = 'D/ENSITY' ('=' DENS | ;
                                              ['='] '(' DENS ')')
DENS                            = (D556 | D800 | D1600 | D6250)
D556(1)                         = '556'
D800(2)                         = '800'
D1600(3)                        = '1600'
D6250(4)                        = '6250'
A(%(%P#BIT1#+%CAT__A#))         = ('A' | 'EA') ['=' NO_YES]
NREW(%(%P#VAL#+%CAT__NREW#))    = 'NR/EWIND'
SFSN(%(%P#VAL#+%CAT__SFSN#))    = 'SF/SN' '=' .d
FSN(out)                        = 'F/SN' ('=' FSNR | ;
                                          ['='] '(' <',',FSNR> ')')
FSNR(%(%P#BIAS+2))              = FSN1 [FSN2]
FSN1(%(%P#VAL#+%CAT__FSN#-2))   = .d
FSN2(%(%P#VAL#+%CAT__FSN#+1-2)) = '-' .d
TYPEO(%(%P#VAL#+%CAT__TYPEO#))  = 'TY/PE' ('=' TYPE | ;
                                      ['='] '(' TYPE ')')
TYPE                            = (.al2+'0123456789$:_-*?' | .q2)
NOT(%P#NOT)                     = 'N/OT'
NO_YES                          = (NO | YES)
NO(0)                           = ('N/O' | 'OF/F' | 'F/ALSE')
YES(1)                          = ('Y/ES' | 'ON' | 'T/RUE')
