/*M* DABBLE_SIN1  Parse nodes for DABBLE.X */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
 
 
/*
         Contributed by
            The University of California
 
         Although this program has been tested by its contributor,
         no warranty, express or implied, is made by the
         contributor or the University of California, as to the
         accuracy and functioning of the program and related
         program materials, nor shall the fact of distribution
         constitute any such warranty, and no responsibility is
         assumed by the contributor or the University of California.
*/
 
/*X* */
 
%INCLUDE DABBLE_C61;
 
DABBLE_CMDS (DEF) = ;
       (   CMD_BLANK | ;
           CMD_CHECK | ;
         /*CMD_END*/ ;
           CMD_FORGET | ;
         /*CMD_HELP*/ ;
         /*CMD_QUES1*/ ;
         /*CMD_QUES2*/ ;
           CMD_READ | ;
           CMD_SELECT | ;
           CMD_USE | ;
           CMD_SEND | ;
           CMD_STATUS | ;
           CMD_ANSWER_MESSAGE | ;
           CMD_DI_MESSAGE | ;
           CMD_LIST_MESSAGE | ;
           CMD_SKIP_MESSAGE | ;
           CMD_PREVIOUS | ;
           CMD_NEXT | ;
           CMD_GO | ;
           CMD_CATALOG | ;
           CMD_REMEMBER | ;
         /*CMD_OUTPUT*/ ;
         /*CMD_X*/ ;
           CMD_ATTACH )
 
ATTACH_CMDS (DEF) = ;
         ( CMD_BLANK | ;
           CMD_END | ;
           CMD_DI_MESSAGE | ;
           CMD_NEXT | ;
           CMD_PREVIOUS | ;
           CMD_LIST_MESSAGE | ;
           CMD_GO | ;
           CMD_STATUS | ;
           CMD_CATALOG | ;
           CMD_X )
 
CMD_BLANK (%CMD_BLANK) = [.B] .END
 
CMD_DI_MESSAGE (%CMD_DI_MESSAGE) = [.B] 'DI/SPLAY' [.B 'MESSAGE'] [SELECT];
                                   .END
 
CMD_SKIP_MESSAGE (%CMD_SKIP_MESSAGE) = [.B] 'SK/IP' [.B 'MESSAGE'] [SELECT] .END
 
CMD_NEXT (%CMD_NEXT) = [.B] 'N/EXT' .END
 
CMD_PREVIOUS (%CMD_PREVIOUS) = [.B] ( 'P/REVIOUS' | '^') .END
 
CMD_LIST_MESSAGE (%CMD_LIST_MESSAGE) = [.B] 'L/IST' [.B 'MESSAGE'];
                                       [SELECT] .END
 
CMD_ANSWER_MESSAGE (%CMD_ANSWER_MESSAGE) = [.B] 'AN/SWER' [.B 'MESSAGE'];
                                           [SELECT] .END
 
CMD_GO (%CMD_GO) = [.B] 'G/O' .END
 
CMD_END (%CMD_END) = [.B] 'E/ND' .END
 
CMD_X (%CMD_X) = [.B] ('X/IT' | 'Q/UIT') .END
 
CMD_CHECK (%CMD_CHECK) = [.B] 'CH/ECK' OPT_BB .END
 
CMD_REMEMBER (%CMD_REMEMBER) = [.B] 'REM/EMBER' OPT_BB .END
 
CMD_FORGET (%CMD_FORGET) = [.B] 'FOR/GET' OPT_BB .END
 
CMD_STATUS (%CMD_STATUS) = [.B] 'STA/TUS' .END
 
CMD_READ (%CMD_READ) = [.B] 'R/EAD' OPT_BB;
                               ['(' READ_OPTS ')'] .END
 
CMD_SELECT (%CMD_SELECT) = [.B] 'SEL/ECT' OPT_BB;
                               ['(' READ_OPTS ')'] .END
 
CMD_USE (%CMD_USE) = [.B] 'U/SE' OPT_BB;
                               ['(' READ_OPTS ')'] .END
 
CMD_ATTACH (%CMD_ATTACH) = [.B] (ATTACHONCE | ATTACHLIST | ATTACHSAVE | ATTACHDEL)
 
CMD_CATALOG (%CMD_CATALOG) = [.B] 'CAT/ALOG' [CATALOG_FID] .END
 
CMD_SEND (%CMD_SEND) = [.B] 'SEN/D' [.FID] .END
 
CMD_HELP (%CMD_HELP) = [.B] 'H/ELP' .ASYM
 
CMD_QUES1 (%CMD_QUES1) = [.B] '?' .END
 
CMD_QUES2 (%CMD_QUES2) = [.B] '??' .END
 
CMD_OUTPUT (%CMD_OUTPUT) = [.B] 'OUT/PUT' [(OUTPUT_ON | ;
                                            OUTPUT_OVER | ;
                                            OUTPUT_INTO) OUTPUT_FID] .END
 
OUTPUT_ON (%OUTPUT_ON) = .B ('ON'|'TO') .B
OUTPUT_OVER (%OUTPUT_OVER) = .B 'OVER' .B
OUTPUT_INTO (%OUTPUT_INTO) = .B 'INTO' .B
OUTPUT_FID (%OUTPUT_FID) = .FID
 
OPT_BB (%OPT_BB) = .B ( BB_ALL | BB_ID )
BB_ID (%BB_ID) = <',',.ANUMERIC+'-_$:#./'>
BB_ALL (%BB_ALL) = 'ALL'
 
SELECT (%OPT_SELECT) = ( SEL_NEW | SEL_ALL | SEL_RANGE | SEL_FIRST | SEL_LAST | SEL_NEXT )
SEL_NEW (%SEL_NEW) = 'NEW'
SEL_ALL (%SEL_ALL) = 'ALL'
SEL_RANGE (%SEL_RANGE) = SEL_RNUM ['-' SEL_RNUM]
SEL_RNUM (%SEL_RNUM) = .DECIMAL
SEL_FIRST (%SEL_FIRST) = 'FIRST' [SEL_RNUM]
SEL_LAST (%SEL_LAST) = 'LAST' [SEL_RNUM]
SEL_NEXT (%SEL_NEXT) = 'NEXT' [SEL_RNUM]
 
READ_OPTS (%OPT_READ) = ( <',',READ_ALTS> )
READ_ALTS = { READ_READ | ( READ_LIST | READ_NOLIST | READ_DISPLAY ) }
READ_READ (%READ_READ) = ( 'R/EAD' | 'F/ORCE' )
READ_LIST (%READ_LIST) = 'L/IST'
READ_NOLIST (%READ_NOLIST) = 'NOL/IST'
READ_DISPLAY (%READ_DISPLAY) = 'D/ISPLAY'
 
CATALOG_FID (%OPT_FID) = .FID
 
ATTACHONCE (%OPT_ATTONCE) = 'AT/TACH' .END
ATTACHLIST (%OPT_ATTLIST) = 'AT/TACH' [.B] 'L/IST' .END
ATTACHSAVE (%OPT_ATTSAVE) = 'AT/TACH' [.B] 'S/AVE' .END
ATTACHDEL (%OPT_ATTDEL) = 'AT/TACH' [.B] 'D/ELETE' .END
