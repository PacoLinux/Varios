10 REM **************************************************************
20 REM *                                                            *
30 REM *  Copyright, (c) Bull HN Information Systems Inc., 1989   *
40 REM *                                                            *
50 REM **************************************************************
60 OPTION BASE 0
70 PRINT "This is a cheesy linker for ASMZ80/6502 OUs."
80 REM  DENNIS GRIESSER, LADC
90 REM  03/03/83
100 MARGIN#1,128
110 REM DEF STORAGE (INDEXED BY N)
120 DIM V(127),M$(127)
130 REM REF STORAGE (COUNTED BY R, RANDOM INDEX)
140 DIM E(127)
150 REM RELOC STORAGE (INDEXED BY E)
160 DIM L(300),O(400)
170 REM MICRO CORE IMAGE (RANDOM ADDRESS, ZERO BASE)
180 DIM I(65536)
190 REM SYMBOL TABLE STORAGE (INDEXED BY S)
200 DIM S$(200),S(200)
210 N=0
220 PRINT "What file contains input schema (DEFs)";
230 LINPUT D$
240 IF D$="" THEN 340
250 OPEN D$ TO 1,INPUT,CONSEC
260 WHEN EOF#1 GOTO 340
270 REM *** GET DEF LOOP
280 INPUT #1,I$
290 IF SST$(I$,8,2)<>"81" THEN 330
300 N=N+1
310 M$(N)=SST$(I$,10,16)
320 V(N)=FND(SST$(I$,4,4))
330 GOTO 270
340 CLOSE ALL
350 PRINT N;"DEFs were found"
360 PRINT "Object-unit file";
370 INPUT O$
380 OPEN O$ TO 1,INPUT,CONSEC
390 WHEN EOF#1 GOTO 830
400 R=0
410 E=0
420 T=0
430 S=0
440 B=65536
450 REM *** REF/RELOC LOOP
460 INPUT#1,I$
470 IF SST$(I$,8,2)="82" THEN 730
480 IF SST$(I$,8,2)="80" THEN 580
490 IF SST$(I$,8,2)="00" THEN 630
500 IF SST$(I$,8,2)<>"03" THEN 820
510 REM *** RELOCATE LONG RECORD
520 FOR L1=0 TO FND(SST$(I$,2,2))-3 STEP 3
530 E=E+1
540 L(E)=FND(SST$(I$,10+L1*2,4))
550 O(E)=FND(SST$(I$,14+L1*2,2))
560 NEXT L1
570 GOTO 820
580 REM *** SYMBOL TABLE ENTRY
590 S=S+1
600 S$(S)=FNT$(SST$(I$,10,16))
610 S(S)=FND(SST$(I$,4,4))
620 GOTO 820
630 REM DATA LOAD
640 F=FND(SST$(I$,4,4))
650 C=FND(SST$(I$,2,2))
660 L=F+C-1
670 IF F<B THEN B=F
680 IF L>T THEN T=L
690 FOR L1=0 TO C-1
700 I(F+L1)=FND(SST$(I$,10+L1*2,2))
710 NEXT L1
720 GOTO 820
730 REM *** REF RECORD
740 FOR L1=1 TO N
750 IF M$(L1)=SST$(I$,10,16) THEN 790
760 NEXT L1
770 PRINT "*** Error:  REF without DEF  '"+FNT$(SST$(I$,10,16))+"'"
780 GOTO 820
790 R=R+1
800 L2=FND(SST$(I$,4,4))
810 E(L2)=L1
820 GOTO 450
830 REM *** ALL REF/RELOC RECORDS FOUND
840 CLOSE ALL
850 PRINT R;"unique symbols were satisfied"
860 PRINT E;"REFs were used"
870 PRINT "Memory used:  "+FNH$(B,4)+"-"+FNH$(T,4)+" H"
880 PRINT "Do you want to add symbols (schema)";
890 INPUT I$
900 IF UPC$(SST$(I$,1,1))="N" THEN 1080
910 PRINT "Symbol NAME (CR to quit)";
920 LINPUT I$
930 IF I$="" THEN 1080
940 I$=SST$(I$+"       ",1,8)
950 PRINT "HEX Value for this symbol";
960 INPUT I2$
970 L1=FND(I2$)
980 IF S=0 THEN 1020
990 FOR L2=1 TO S
1000 IF S(L2)=L1 OR S$(L2)=I$ THEN 1060
1010 NEXT L2
1020 S=S+1
1030 S$(S)=I$
1040 S(S)=L1
1050 GOTO 1070
1060 PRINT "*** Error:  duplicate symbol detected  "+S$(L2)+" = "+FNH$(S(L2),4)
1070 GOTO 910
1080 REM *** THE MODIFICATION LOOP
1090 IF E=1 THEN 1250
1100 FOR L1=1 TO E
1110 L2=I(L(L1))+I(L(L1)+1)*256
1120 IF O(L1)>127 THEN 1170
1130 REM * ADD BASE
1140 L2=L2+V(E(O(L1)))
1150 IF L2>=65536 THEN L2=L2-65536
1160 GOTO 1200
1170 REM * SUBTRACT BASE
1180 L2=L2-V(E(O(L1)-128))
1190 IF L2<0 THEN L2=L2+65536
1200 REM PUT CHANGED ADDRESS BACK
1210 T$=FNH$(L2,4)
1220 I(L(L1))=FND(SST$(T$,3,2))
1230 I(L(L1)+1)=FND(SST$(T$,1,2))
1240 NEXT L1
1250 REM *** WRITE OUT THE RU
1260 PRINT "Run-unit output file";
1270 INPUT R$
1280 OPEN R$ TO 1,PRINT,OVER
1290 FOR L1=B TO T STEP 32
1300 C=32
1310 IF T-L1+1<C THEN C=T-L1+1
1320 I$=":"+FNH$(C,2)+FNH$(L1,4)+"00"
1330 U=C+FND(SST$(I$,4,2))+FND(SST$(I$,6,2))+0
1340 FOR L2=L1 TO L1+C-1
1350 U=U+I(L2)
1360 I$=I$+FNH$(I(L2),2)
1370 NEXT L2
1380 U=0-U
1390 IF U>0 THEN 1420
1400 U=U+256
1410 GOTO 1390
1420 REM * APPEND CHECKSUM (U)
1430 I$=I$+FNH$(U,2)
1440 PRINT#1,I$
1450 NEXT L1
1460 PRINT "Do you want schema included in the RU";
1470 INPUT I$
1480 IF UPC$(SST$(I$,1,1))="N" THEN 1650
1490 IF S=0 THEN 1650
1500 FOR L1=1 TO S
1510 I$=":09"+FNH$(S(L1),4)+"80"
1520 U=138+FND(SST$(I$,4,2))+FND(SST$(I$,6,2))
1530 FOR L2=1 TO 8
1540 I$=I$+FNH$(ASC(SST$(S$(L1),L2,1)),2)
1550 U=U+ASC(SST$(S$(L1),L2,1))
1560 NEXT L2
1570 U=0-U
1580 IF U>0 THEN 1610
1590 U=U+256
1600 GOTO 1580
1610 REM * APPEND CHECKSUM
1620 I$=I$+"01"+FNH$(U,2)
1630 PRINT#1,I$
1640 NEXT L1
1650 CLOSE ALL
1660 STOP
1670 REM *** FUNCTIONS!!!
1680 DEF FND(H$)
1690 REM FND(H$) RETURNS THE DECIMAL VALUE OF THE PASSED HEX STRING
1700 XFND=0
1710 FOR Z1=1 TO LEN(H$)
1720 Z2=ASC(SST$(H$,Z1,1))
1730 IF Z2>57 THEN Z2=Z2-7
1740 XFND=(XFND*16) + Z2-48
1750 NEXT Z1
1760 FND=XFND
1770 FNEND
1780 DEF FNH$(D,N)
1790 REM FNH$(D,N) RETURNS 'N' HEX NIBS FOR THE DECIMAL NUMBER
1800 XFNH$=""
1810 FOR Z1=N TO 1 STEP -1
1820 Z2=INT(D/(2**(4*Z1-4)))
1830 Z2=Z2-INT(Z2/16)*16
1840 XFNH$=XFNH$+SST$("0123456789ABCDEF",Z2+1,1)
1850 NEXT Z1
1860 FNH$=XFNH$
1870 FNEND
1880 DEF FNT$(H$)
1890 REM FNT$(H$) RETURNS A TEXT CHARACTER STRING FOR THE PASSED HEX STRING
1900 XFNT$=""
1910 FOR Z5=1 TO LEN(H$)-1 STEP 2
1920 XFNT$=XFNT$+CHR$(FND(SST$(H$,Z5,2)))
1930 NEXT Z5
1940 FNT$=XFNT$
1950 FNEND
1960 END
