/*
      Parse Nodes for PULSE screen editor.
      February 28, 1990.
*/
 
      %Include PULSE_C61;
 
      COMMANDWINDOW (Def) = (Close | Restore | NextBuf | PrevBuf | Save |;
                             Switch | Tabs | Delete | Transform | Line |;
                             Do | Edit | List | End | Type | Nil) .end
 
      CONFIRM ( Def ) = 'YES/$' .end
      Line (%Line) = ( .decimal5 ['.' [Thousandths]] | '.' Thousandths )
      Thousandths ( %Thousandths ) = .decimal3
      Nil (%Nil)   = [.blank]
      End (%End)   = ('EX/IT' | 'E/ND' | 'Q/UIT' | 'X/IT')
      Close = (CloseFile | CloseOffset)
        CloseFile(%CloseFile) = 'CL/OSE' .blank .fid
        CloseOffset(%CloseOffset) = 'CL/OSE'- [offset] [.blank]
      Delete = (DeleteFile | DeleteOffset)
        DeleteFile(%DeleteFile) = 'DEL/ETE' .blank .fid
        DeleteOffset(%DeleteOffset) = 'DEL/ETE'- [offset] [.blank]
      Switch(%Switch) = ('PU/LSE' | 'ED/IT' | 'GET'- ) .blank .fid [Options]
      Edit(%Edit) = ('PU/LSE'- | 'ED/IT'- | 'GET'- ) [offset] [.blank] [Options]
 
        offset(%offset) = ( NextBuf | PrevBuf )
        NextBuf(%NextBuf) = ('+' | 'NEXT') [.dec]
        PrevBuf(%PrevBuf) = ('-' | 'PREV/IOUS') [.dec]
 
      Restore(%Restore) = 'RES/TORE' .fid
      Save(%Save) = 'SAV/E' how .fid
        how(%how) = ( On | Over )
        On(%On) = ( 'ON' | 'TO' )
        Over(%Over) = 'OVER'
 
      List(%List) = 'L/IST'
      Transform(%Transform) = 'TRA/NSFORM'
      Type(%Type) = 'TYPE' ( .quote2 | .anumeric2 )
      Tabs(%Tabs) = 'TA/BS' [( Numbers | Assembler | Fortran | Cobol | None )]
        None(%None) = 'N/ONE'
        Assembler(%Assembler) = 'A/SSEMBLER'
        Fortran(%Fortran) = 'F/ORTRAN'
        Cobol(%Cobol) = 'C/OBOL'
        Numbers(%Numbers) = < ',', .dec >
 
      Do(%Do) = ('DO'|'!') .asym
 
      Options(%Options) = '(' < ',', Entry > [')']
 
      LMOPTIONS (Def) = ['(' < ',', Entry > [')']] .end
      Entry(%Entry) = ( Insert | NWork | Crpt | WordWrap | RcrWrap |;
                        Work | Overstrike | InputOnly | HScroll | WorkAlways |;
                        KeepNewMT | Strip | TextEdit | Update | Rekey |;
                        EasyAppend )
 
      Update(%Update) = 'U/PDATE'
      InputOnly (%InputOnly) = ('EX/AMINE' | 'I/NPUT' | 'R/EADONLY')
      Work(%Work) = 'W/ORK'
      NWork(%NWork) = 'NW/ORK'
      Crpt(%Crpt) = 'CR/PT' ['='] .octal12
      Rekey(%Rekey) = 'K/EYFILE'
 
      CRPTNODE (Def) = .octal12 .end
 
      FIND (Def) = * ( Quit | UpperCase | Global | Backwards | BlockOnly | Nil ) * .end
      REPLACE (Def) = * ( Quit | UpperCase | Global | All | Next | Verify |;
                          Backwards | BlockOnly | Nil ) * .end
 
      Quit(%Quit)           = 'Q'
      UpperCase(%UpperCase) = 'U'
      Global(%Global)       = 'G'
      All(%All)             = 'A'
      Next(%Next)           = 'N' .decimal
      Verify(%Verify)       = 'V'
      Backwards(%Backwards) = 'B'
      BlockOnly(%BlockOnly) = 'K'
 
      CONTEXT(Def) = [(WordWrap | Overstrike | Insert | HScroll | WorkAlways |;
                      KeepNewMT | Strip | TextEdit | RcrWrap | EasyAppend |Tabs;
                      | Type | StartUpSeq | ExitSeq | CmdOpen | CmdClose)] .end
 
      WordWrap (%WordWrap) = 'WORD/WRAPCLM' '=' .dec
      Overstrike(%Overstrike) = 'OVER/STRIKEMODE'
      Insert(%Insert) = 'INS/ERTMODE'
      HScroll(%HScroll) = 'HSC/ROLLING' [['='] ( Yes | No )]
      Strip(%Strip) = 'STR/IPBLANKS' [['='] ( Yes | No )]
      TextEdit(%TextEdit) = 'TEX/TEDIT' [['='] ( Yes | No )]
      RcrWrap(%RcrWrap) = 'RCR/WRAP' [['='] ( Yes | No )]
      KeepNewMT(%KeepNewMT) = 'KEEP/NEWEMPTY' [['='] ( Yes | No )]
      WorkAlways(%WorkAlways) = 'USEWORK/FILES' [['='] ( Yes | No )]
      EasyAppend(%EasyAppend) = 'EASY/APPEND' [['='] ( Yes | No )]
      StartUpSeq(%StartUpSeq) = 'START' '=' download
      ExitSeq(%ExitSeq) = 'FINISH' '=' download
      CmdOpen(%CmdOpen) = 'CMDOPEN' '=' download
      CmdClose(%CmdClose) = 'CMDCLOSE' '=' download
      download =  < ',', downloaditem > .end
      downloaditem = (NUL | SOH | STX | ETX | EOT | ENQ | ACK | BEL |;
                      BS | HT | LF | VT | FF | CR | SO | SI | DLE | DC1 |;
                      DC2 | DC3 | DC4 | NAK | SYN | ETB | CAN | EM |;
                      SUB | ESC | FS | GS | RS | US | .quote)
 
        NUL(%NUL) = 'NUL'
        SOH(%SOH) = 'SOH'
        STX(%STX) = 'STX'
        ETX(%ETX) = 'ETX'
        EOT(%EOT) = 'EOT'
        ENQ(%ENQ) = 'ENQ'
        ACK(%ACK) = 'ACK'
        BEL(%BEL) = 'BEL'
        BS (%BS)  = 'BS'
        HT (%HT)  = 'HT'
        LF (%LF)  = 'LF'
        VT (%VT)  = 'VT'
        FF (%FF)  = 'FF'
        CR (%CR)  = 'CR'
        SO (%SO)  = 'SO'
        SI (%SI)  = 'SI'
        DLE(%DLE) = 'DLE'
        DC1(%DC1) = 'DC1'
        DC2(%DC2) = 'DC2'
        DC3(%DC3) = 'DC3'
        DC4(%DC4) = 'DC4'
        NAK(%NAK) = 'NAK'
        SYN(%SYN) = 'SYN'
        ETB(%ETB) = 'ETB'
        CAN(%CAN) = 'CAN'
        EM (%EM)  = 'EM'
        SUB(%SUB) = 'SUB'
        ESC(%ESC) = 'ESC'
        FS (%FS)  = 'FS'
        GS (%GS)  = 'GS'
        RS (%RS)  = 'RS'
        US (%US)  = 'US'
 
      Yes (%Yes) = ( 'Y/ES' | 'ON' )
      No (%No) = ( 'N/O' | 'OFF' )
 
      ACCOUNTNODE( Def ) = .account8
      FIDNODE( Def ) = .fid24
      PREFIXNODE( Def ) = .fid20
