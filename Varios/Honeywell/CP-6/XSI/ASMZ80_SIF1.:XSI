C*T***********************************************************
C*T*                                                         *
C*T* Copyright (c) Bull HN Information Systems Inc., 1989    *
C*T*                                                         *
C*T***********************************************************
C
C     MAIN PROGRAM FOR ASM
C
C     SEE ASMZ80_HELP FOR DETAILS
C
      IMPLICIT INTEGER(A-Z)
      INCLUDE ASMZ80_C1
      DIMENSION SYMHED(13),UNDHED(5),XRHEAD(15)
      DATA SYMHED/1HS,1HY,1HM,1HB,1HO,1HL,1H ,1HV,1HA,1HL,1HU,1HE,1HS/
      DATA UNDHED/'UNDE','FINE','D SY','MBOL','(S):'/
      DATA XRHEAD/
     1  1HC,1HR,1HO,1HS,1HS,1H-,1HR,1HE,1HF,1HE,1HR,1HE,1HN,1HC,1HE/
      DATA XLTAR/'  <-'/,XRTAR/'  ->'/,XMINS/'  - '/,XPLUS/'  + '/,
     1  XASTR/'  * '/,XEXT/'EXT '/,XENT/'ENT '/,XUND/'UND '/,
     2  XREL/'(R) '/,XEXTS/'EXTS'/,XEXTL/'EXTL'/
C
C     GET USER OPTIONS INTO OPTION
C
      NBITS2=2*NBITS
      NBITS3=3*NBITS
      MBITS=-NBITS
      MBITS2=-2*NBITS
      MBITS3=-3*NBITS
      OPEN(LOUNIT)
      PASS=-1
      CALL GETOPT
      HOLDOP=OPTION
      HOLDCL=CLOCK
      DO 1010 I=1,2
        DO 1010 J=1,8
 1010 HOLDSR(I,J)=SRACCT(I,J)
      WRITE(108,900)
C
C     SET UP FOR PASS 0
C
      PASS=0
      OPEN(TMUNIT,NAME='*TM',FORM='FORMATTED',RECL=256,USAGE='CREATE')
      OPEN(MCUNIT,NAME='*MC',FORM='FORMATTED',RECL=256,USAGE='CREATE',
     1  ACCESS='DIRECT')
      CLOSE(MCUNIT,STATUS='KEEP')
      OPEN(MCUNIT,NAME='*MC',FORM='FORMATTED',RECL=256,USAGE='UPDATE',
     1  ACCESS='DIRECT')
      IUNIT=SIUNIT
      CALL DOPASS
      CLOSE(TMUNIT,STATUS='KEEP')
      CLOSE(SIUNIT,STATUS='KEEP')
      CLOSE(TMUNIT,STATUS='KEEP')
C
C     SET UP FOR PASS 1
C
      PASS=1
      OPEN(TMUNIT,NAME='*TM',FORM='FORMATTED',RECL=256,USAGE='INPUT',
     1  STATUS='OLD')
      IUNIT=TMUNIT
      CALL DOPASS
      CLOSE(TMUNIT,STATUS='KEEP')
C
C     SET UP FOR PASS 2
C
      PASS=2
      OPEN(TMUNIT,NAME='*TM',FORM='FORMATTED',RECL=256,USAGE='INPUT',
     1  STATUS='OLD')
      IF(IAND(OPTION,OPTXR).NE.0)
     1  OPEN(XRUNIT,NAME='*XR',FORM='UNFORMATTED',RECL=5,USAGE='CREATE',
     2    ACCESS='DIRECT')
      IF(IAND(OPTION,OPTOU).NE.0)
     1  OPEN(OUUNIT,FORM='FORMATTED',RECL=80)
      CALL DOPASS
      CLOSE(TMUNIT,STATUS='DELETE')
      CLOSE(MCUNIT,STATUS='DELETE')
      CALL OBJOUT(-1,0,0,0)
C
C     SORT SYMBOL TABLE INTO ORDER
C
      PASS=3
      DO 4300 I=1,HISYM-1
        DO 4200 J=I+1,HISYM
          IF(SYMBOL(1,I).LT.SYMBOL(1,J))GOTO 4200
          IF(SYMBOL(1,I).EQ.SYMBOL(1,J).AND.SYMBOL(2,I).LE.SYMBOL(2,J))
     1      GOTO 4200
          DO 4100 K=1,5
            HOLD=SYMBOL(K,I)
            SYMBOL(K,I)=SYMBOL(K,J)
            SYMBOL(K,J)=HOLD
 4100     CONTINUE
 4200   CONTINUE
 4300 CONTINUE
C
C     PAD SYMBOLS OUT WITH BLANKS
C
      DO 4500 I=1,HISYM
        DO 4400 J=1,7
 4400   CALL PACK(SYMBOL(1,I),BLANK)
 4500 CONTINUE
      IF(IAND(OPTION,OPTOU).EQ.0)GOTO 4550
      DO 4510 I=1,HISYM
        IF(IAND(SYMBOL(3,I),12).EQ.0.AND.IAND(OPTION,OPTSC).EQ.0)
     1    GOTO 4510
        IF(IAND(SYMBOL(3,I),12).EQ.0)RELOC=128
        IF(IAND(SYMBOL(3,I),8).NE.0)RELOC=130
        IF(IAND(SYMBOL(3,I),4).NE.0)RELOC=129
        RELBAS=SYMBOL(4,I)
        IF(RELOC.EQ.130)RELBAS=IAND(ISL(SYMBOL(3,I),MBITS2),127)
        CALL OBJOUT(RELOC,RELBAS,8,SYMBOL(1,I))
 4510 CONTINUE
      IF(IAND(OPTION,OPTOU).NE.0)
     1  CLOSE(OUUNIT,STATUS='KEEP')
C
C     PRINT SYMBOL TABLE IF LS SPECIFIED
C
 4550 IF(IAND(OPTION,OPTLS).EQ.0.OR.HISYM.EQ.0)GOTO 4700
      LOLINE=9999
      DO 4580 I=1,80
 4580 TITLE(I)=BLANK
      DO 4590 I=1,13
 4590 TITLE(I)=SYMHED(I)
      IF(IAND(OPTION,OPTOC).NE.0)GOTO 4650
      N=4*(LOWID/16)
      IF(N.GT.24)N=24
      I=1
 4600 DO 4610 J=1,33
 4610 LOBUF(J)=BLANK
      J=1
 4620 IF(IAND(SYMBOL(3,I),1).EQ.0)GOTO 4640
      IF(J.LT.N)GOTO 4630
      CALL LIST
      GOTO 4600
 4630 CALL CVH(LOBUF(J),SYMBOL(4,I))
      IF(IAND(SYMBOL(3,I),8).EQ.0)GOTO 4635
      LOBUF(J+1)=XLTAR
      IF(ISL(SYMBOL(3,I),MBITS2).GT.127)LOBUF(J)=XEXTL
      IF(ISL(SYMBOL(3,I),MBITS2).LE.127)LOBUF(J)=XEXTS
 4635 IF(IAND(SYMBOL(3,I),4).NE.0)LOBUF(J+1)=XRTAR
      LOBUF(J+1)=ISL(LOBUF(J+1),NBITS2)+ISL(SYMBOL(1,I),MBITS2)
      LOBUF(J+2)=ISL(SYMBOL(1,I),NBITS2)+ISL(SYMBOL(2,I),MBITS2)
      LOBUF(J+3)=ISL(SYMBOL(2,I),NBITS2)+ISL(BLANK,MBITS2)
      J=J+4
 4640 I=I+1
      IF(I.LE.HISYM)GOTO 4620
      CALL LIST
      GOTO 4700
 4650 N=5*(LOWID/20)
      IF(N.GT.25)N=25
      I=1
 4660 DO 4665 J=1,33
 4665 LOBUF(J)=BLANK
      J=1
 4670 IF(IAND(SYMBOL(3,I),1).EQ.0)GOTO 4690
      IF(J.LT.N)GOTO 4680
      CALL LIST
      GOTO 4660
 4680 IF(IAND(OPTION,OPTAO).NE.0)SYMBOL(4,I)=IAND(SYMBOL(4,I),255)+
     1  512*(SYMBOL(4,I)/256)
      CALL CVO(I1,I2,SYMBOL(4,I))
      LOBUF(J)=ISL(I1,NBITS2)+ISL(I2,MBITS2)
      IF(IAND(SYMBOL(3,I),8).EQ.0)GOTO 4685
      LOBUF(J+1)=XLTAR
      LOBUF(J)=ISL(BLANK,NBITS2)+ISL(XEXTS,MBITS2)
      IF(ISL(SYMBOL(3,I),MBITS2).GT.127)I2=XEXTL
      IF(ISL(SYMBOL(3,I),MBITS2).LE.127)I2=XEXTS
 4685 IF(IAND(SYMBOL(3,I),4).NE.0)LOBUF(J+1)=XRTAR
      LOBUF(J+1)=ISL(I2,NBITS2)+ISL(ISL(LOBUF(J+1),NBITS2),MBITS2)
      LOBUF(J+2)=SYMBOL(1,I)
      LOBUF(J+3)=SYMBOL(2,I)
      J=J+5
 4690 I=I+1
      IF(I.LE.HISYM)GOTO 4670
      CALL LIST
C
C     CHECK FOR UNDEFINED SYMBOLS
C
 4700 IF(HISYM.EQ.0)GOTO 4900
      DO 4710 I=1,HISYM
      IF(IAND(SYMBOL(3,I),1).EQ.0)GOTO 4720
 4710 CONTINUE
      GOTO 4900
 4720 N=3*(LOWID/12)
      IF(N.GT.27)N=27
      DO 4730 J=1,33
 4730 LOBUF(J)=BLANK
      CALL LIST
      CALL LIST
      DO 4740 J=1,5
 4740 LOBUF(J)=UNDHED(J)
      CALL LIST
 4800 DO 4810 J=1,33
 4810 LOBUF(J)=BLANK
      CALL LIST
      J=1
 4820 IF(IAND(SYMBOL(3,I),1).NE.0.OR.SYMBOL(1,I).EQ.1HA)GOTO 4840
      IF(J.LT.N)GOTO 4830
      CALL LIST
      GOTO 4800
 4830 LOBUF(J)=SYMBOL(1,I)
      LOBUF(J+1)=SYMBOL(2,I)
      J=J+3
 4840 I=I+1
      IF(I.LE.HISYM)GOTO 4820
      CALL LIST
C
C     DO CROSS REFERENCE, IF NECESSARY
C
 4900 CALL ACPU(CPU2)
      CPU2=CPU2-CPU1
      IF(LOLIM-LOLINE.GE.5)GOTO 4901
      LOPAGE=LOPAGE+1
      N=80
      IF(LOWID.LT.107)N=LOWID-27
      WRITE(LOUNIT,901)TIME,DATE,LOPAGE,(TITLE(I),I=1,N)
      WRITE(LOUNIT,902)
      LOLINE=2
 4901 WRITE(LOUNIT,902)
      WRITE(LOUNIT,902)
      WRITE(LOUNIT,903)CPU2
      IF(ERRCNT.EQ.0)WRITE(LOUNIT,904)
      IF(ERRCNT.EQ.1)WRITE(LOUNIT,905)
      IF(ERRCNT.GT.1)WRITE(LOUNIT,906)ERRCNT
      IF(IAND(OPTION,OPTXR).EQ.0.OR.HISYM.EQ.0)GOTO 5000
      CLOSE(XRUNIT,STATUS='KEEP')
      OPEN(XRUNIT,NAME='*XR',FORM='FORMATTED',RECL=256,USAGE='INPUT',
     1  STATUS='OLD',ACCESS='DIRECT')
      LOLINE=9999
      DO 4910 I=1,80
 4910 TITLE(I)=BLANK
      DO 4920 I=1,15
 4920 TITLE(I)=XRHEAD(I)
      N=4*((LOWID-16)/16)
      IF(N.GT.24)N=24
      I=1
 4930 DO 4940 J=1,33
 4940 LOBUF(J)=BLANK
      J=5
      LOBUF(2)=SYMBOL(1,I)
      LOBUF(3)=SYMBOL(2,I)
      IF(IAND(SYMBOL(3,I),16).NE.0)LOBUF(1)=XREL
      IF(IAND(SYMBOL(3,I),8).NE.0)LOBUF(1)=XEXT
      IF(IAND(SYMBOL(3,I),4).NE.0)LOBUF(1)=XENT
      IF(IAND(SYMBOL(3,I),3).EQ.0)LOBUF(1)=XUND
      XRREC=SYMBOL(5,I)
 4950 IF(XRREC.EQ.0)GOTO 4980
      IF(J.LE.N)GOTO 4970
      CALL LIST
      DO 4960 J=1,33
 4960 LOBUF(J)=BLANK
      J=5
 4970 READ(XRUNIT,REC=XRREC)XRBUF
      IF(XRBUF(2).EQ.INUNIT)LOBUF(J+1)=XMINS
      IF(XRBUF(2).EQ.MCUNIT)LOBUF(J+1)=XPLUS
      IF(XRBUF(1).LT.0)LOBUF(J+1)=XASTR
      CALL CVD(LOBUF(J),IABS(XRBUF(1)))
      DO 4975 K=1,7
 4975 CALL PACK(XRBUF(4),BLANK)
      LOBUF(J+1)=ISL(LOBUF(J+1),NBITS2)+ISL(XRBUF(4),MBITS2)
      LOBUF(J+2)=ISL(XRBUF(4),NBITS2)+ISL(XRBUF(5),MBITS2)
      LOBUF(J+3)=ISL(XRBUF(5),NBITS2)+ISL(BLANK,MBITS2)
      J=J+4
      XRREC=XRBUF(3)
      GOTO 4950
 4980 CALL LIST
      I=I+1
      IF(I.LE.HISYM)GOTO 4930
      CLOSE(XRUNIT,STATUS='DELETE')
C
C     END-OF-JOB
C
 5000 CLOSE(LOUNIT,STATUS='KEEP')
      STOP
  900 FORMAT(' ASM Z80 02/09/81'/)
  901 FORMAT('1',2A4,1X,2A4,1X,'PAGE',I4,1X,80A1)
  902 FORMAT(' ')
  903 FORMAT(1X,F8.2,' SECONDS OF CPU TIME USED FOR THIS ASSEMBLY.')
  904 FORMAT('       NO STATEMENTS FLAGGED.')
  905 FORMAT('        1 STATEMENT FLAGGED.')
  906 FORMAT(1X,I8,' STATEMENTS FLAGGED.')
      END
      SUBROUTINE DOPASS
C
C     PERFORM AN ASSEMBLY PASS, DEPENDING ON THE VALUE OF PASS.
C
      IMPLICIT INTEGER(A-Z)
      INCLUDE ASMZ80_C1
      CHARACTER*8 FILENM,FILEAC
      DIMENSION ENDCMD(20),LOCBUF(256)
      DATA ENDCMD/' ','E','N','D',';','S','U','P','P','L','I','E','D',
     1  ' ','B','Y',' ','A','S','M'/
      DATA XMINS/'-   '/,XPLUS/'+   '/,XASTR/'*   '/,XPERD/'.   '/,
     1  XSLSH/'/   '/,XRCON/'R   '/,XXCON/'X   '/
C
C     INITIALIZE VARIABLES FOR A PASS.
C
      OPTION=HOLDOP
      CLOCK=HOLDCL
      DO 1000 I=1,10
      LNSTK(I)=0
 1000 IUSTK(I)=0
      DO 1005 I=1,2
        DO 1005 J=1,8
 1005 SRACCT(I,J)=HOLDSR(I,J)
      REFCNT=1
      LOCCTR=256
      FLAGIF=0
      FLAGMC=0
      FLAGLS=1
      GO2LBL(1)=0
      GO2LBL(2)=0
      LNCNT=0
      LOPAGE=0
      LOLINE=99999999
      MCREC=1
      DIDMAC=0
      ERRCNT=0
      DO 1010 I=1,120
 1010 TITLE(I)=BLANK
      IF(PASS.EQ.2)GOTO 2000
      DO 1020 I=1,HISYM
 1020 SYMBOL(3,I)=0
C
C     SET UP VALUES TO READ A COMMAND IMAGE
C
 2000 ARGCNT=0
      ARG1TY=0
      ARG2TY=0
      FCOMMA=0
      ERRORS=0
      RELOC=0
      RELBAS=0
      ITIME=0
      JTIME=0
      LABEL(1)=0
      LABEL(2)=0
      OPCODE(1)=0
      OPCODE(2)=0
      OBJCNT=0
      OPIDX=OPLIM+1
      IF(BREAK.EQ.0)GOTO 2010
      IF(IUNIT.EQ.SIUNIT.OR.IUNIT.EQ.TMUNIT)WRITE(108,912)ERRCNT,PASS,
     1  LNCNT
      IF(IUNIT.EQ.MCUNIT)WRITE(108,913)ERRCNT,PASS,LNCNT
      IF(IUNIT.EQ.INUNIT)WRITE(108,914)ERRCNT,PASS,LNCNT,FILENM,FILEAC
      BREAK=0
C
C     READ AN IMAGE FROM SOMEWHERE
C
 2010 CALL READSI
      IF(ARS)5100,5000,2020
C
C     CHECK FOR COMMENT
C
 2020 CURPOS=1
      CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H*.OR.CH.EQ.1H;.OR.CH.EQ.1H!.OR.CH.EQ.KEOC)GOTO 4200
C
C     CHECK FOR AND PACK LABEL INTO LABEL
C
      IF(TY.NE.1)GOTO 2040
      SAVCHR=CH
      LEN=0
 2030 CALL NXTCHR(CH,TY)
      IF(TY.NE.1.AND.TY.NE.2)GOTO 2035
      CALL PACK(LABEL,SAVCHR)
      SAVCHR=CH
      LEN=LEN+1
      GOTO 2030
 2035 IF(LEN.GT.0.AND.SAVCHR.EQ.1H:)GOTO 2040
      CALL PACK(LABEL,SAVCHR)
C
C     SKIP BLANKS UP TO OPCODE
C
 2040 IF(CH.EQ.1H )GOTO 2050
      IF((CH.EQ.1H;.OR.CH.EQ.1H!.OR.CH.EQ.KEOC).AND.LABEL(1).EQ.0)
     1  GOTO 4200
      IF(CH.EQ.1H=)GOTO 2055
      ERRORS=IOR(ERRORS,ERRL)
      CALL NXTCHR(CH,TY)
      IF(CH.NE.KEOC)GOTO 2040
      GOTO 4200
C
C     SKIP MORE BLANKS UP TO OPCODE
C
 2050 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H )GOTO 2050
      IF(TY.EQ.1)GOTO 2060
      IF((CH.EQ.1H;.OR.CH.EQ.1H!.OR.CH.EQ.KEOC).AND.LABEL(1).EQ.0)
     1  GOTO 4200
      IF(CH.EQ.1H=)GOTO 2055
      ERRORS=IOR(ERRORS,ERRO)
      GOTO 4200
C
C     HANDLE = OPCODE
C
 2055 CALL PACK(OPCODE,1HE)
      CALL PACK(OPCODE,1HQ)
      CALL PACK(OPCODE,1HU)
      GOTO 2070
C
C     PACK OPCODE INTO OPCODE
C
 2060 CALL PACK(OPCODE,CH)
      CALL NXTCHR(CH,TY)
      IF(TY.EQ.1.OR.TY.EQ.2)GOTO 2060
      IF(CH.EQ.1H;.OR.CH.EQ.1H!.OR.CH.EQ.KEOC)GOTO 2500
      IF(CH.EQ.1H )GOTO 2070
      ERRORS=IOR(ERRORS,ERRA)
      GOTO 4200
C
C     SKIP BLANKS UP TO ARGUMENT, COMMENT, OR EOC
C
 2070 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H )GOTO 2070
      IF(CH.EQ.1H;.OR.CH.EQ.1H!.OR.CH.EQ.KEOC)GOTO 2500
      ARGBGN=CURPOS-1
C
C     SCAN ARGUMENT FIELD, COUNTING ARGUMENTS (COMMAS) AND FIND ENDS
C
 2080 IF(CH.EQ.1H')GOTO 2100
      IF(CH.EQ.1H")GOTO 2110
 2090 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H,)ARGCNT=ARGCNT+1
      IF(CH.EQ.1H,.AND.FCOMMA.EQ.0)FCOMMA=CURPOS-1
      IF(CH.EQ.1H;.OR.CH.EQ.1H!.OR.CH.EQ.1H .OR.
     1  CH.EQ.KEOC)GOTO 2120
      GOTO 2080
C
C     SCAN FOR CLOSING QUOTE
C
 2100 IF(INPBUF(CURPOS-3).EQ.1HA.AND.INPBUF(CURPOS-2).EQ.1HF)GOTO 2090
 2105 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H')GOTO 2090
      IF(CH.NE.KEOC)GOTO 2105
      ERRORS=IOR(ERRORS,ERRA)
      GOTO 4200
C
C     SCAN FOR CLOSING DOUBLE QUOTE
C
 2110 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H")GOTO 2090
      IF(CH.NE.KEOC)GOTO 2110
      ERRORS=IOR(ERRORS,ERRA)
      GOTO 4200
C
C     POINT TO ARGUMENT END AND COUNT LAST ARGUMENT
C
 2120 ARGEND=CURPOS-2
      ARGCNT=ARGCNT+1
C
C     PAD OUT OPCODE WITH BLANKS THEN LOOK IT UP
C
 2500 DO 2510 I=1,7
 2510 CALL PACK(OPCODE,BLANK)
      OPIDX=0
      RELOC=0
      RELBAS=0
 2520 OPIDX=OPIDX+1
      IF(OPIDX.LE.OPLIM)GOTO 2530
      ERRORS=IEOR(ERRORS,ERRO)
      RELOC=0
      IF(IAND(OPTION,OPTREL).NE.0)RELOC=1
      RELBAS=0
      IF(LOCCTR.GE.256)RELBAS=1
      PTR=DULBL(LOCCTR)
      RELOC=0
      RELBAS=0
      GOTO 3640
 2530 IF(OPCODE(1).NE.OPTBL(1,OPIDX).OR.
     1  OPCODE(2).NE.OPTBL(2,OPIDX))GOTO 2520
C
C     CHECK IF WE'RE IN A GOTO
C
      IF(GO2LBL(1).EQ.0)GOTO 2540
C
C     WE ARE; SEE IF WE FOUND THE LABEL
C
      IF(LABEL(1).NE.GO2LBL(1).OR.LABEL(2).NE.GO2LBL(2))GOTO 4100
C
C     WE FOUND OUR TARGET GOTO LABEL; CLEAR IT AND DO THIS INSTRUCTION
C
      GO2LBL(1)=0
      GO2LBL(2)=0
C
C     SEE IF WE'RE IN AN IF/ELSE
C
 2540 IF(FLAGIF.LE.0)GOTO 2570
C
C     WE ARE; SEE IF WE FOUND AN IF, ELSE, OR FI
C
      IF(OPTYPE(OPIDX).NE.36)GOTO 2550
C
C     IF IN AN IF; BUMP IF COUNTER
C
      FLAGIF=FLAGIF+1
      GOTO 4100
C
 2550 IF(OPTYPE(OPIDX).NE.37)GOTO 2560
C
C     ELSE IN AN IF; ARE WE AT LEVEL ONE?
C
      IF(FLAGIF.GT.1)GOTO 4100
C
C     CLEAR THE IF FLAG & DO THIS ELSE
C
      FLAGIF=0
      GOTO 4200
 2560 IF(OPTYPE(OPIDX).NE.38)GOTO 4100
C
C     FI IN AN IF; DECREMENT THE IF COUNTER
C
      FLAGIF=FLAGIF-1
      GOTO 4100
C
C     CHECK IF WE'RE DEFINING A MACRO
C
 2570 IF(FLAGMC.NE.0.AND.OPTYPE(OPIDX).NE.43)GOTO 4200
C
C     SEE WHAT TYPE OF OPCODE THIS IS
C
      IF(OPTYPE(OPIDX).GT.20)GOTO 3000
C
C     NOW FIGURE OUT WHAT TYPE OF ARGUMENT FIELD
C
      IF(ARGCNT.GT.2)ARG1TY=-1
      IF(ARGCNT.LT.1.OR.ARGCNT.GT.2)GOTO 2575
      IF(ARGCNT.EQ.2)GOTO 2572
      CALL SCANOP(ARG1TY,ARG1VL,ARGBGN,ARGEND)
      ARG1RL=RELOC
      ARG1BS=RELBAS
      GOTO 2575
 2572 CALL SCANOP(ARG1TY,ARG1VL,ARGBGN,FCOMMA-1)
      ARG1RL=RELOC
      ARG1BS=RELBAS
      CALL SCANOP(ARG2TY,ARG2VL,FCOMMA+1,ARGEND)
      ARG2RL=RELOC
      ARG2BS=RELBAS
C
C     THIS IS A MACHINE INSTRUCTION; PROCESS THE LABEL
C
 2575 RELOC=0
      IF(IAND(OPTION,OPTREL).NE.0)RELOC=1
      RELBAS=0
      IF(LOCCTR.GE.256)RELBAS=1
      PTR=DULBL(LOCCTR)
      RELOC=0
      RELBAS=0
C
C     GO TO PROPER ROUTINE BASED ON OPTYPE
C
      GOTO(2580,2620,2640,2650,2660,2670,2680,2690,2700,2710,
     1  2720,2730,2740,2750,2760,2770,2780,2790,2800,2810),OPTYPE(OPIDX)
C
C     OPTYPE=1 (LD)
C
 2580 IF(ARGCNT.NE.2.OR.ARG1TY.LE.0.OR.ARG2TY.LE.0)GOTO 2990
      IF(ARG1TY.GT.8)GOTO 2592
      IF(ARG2TY.GT.8)GOTO 2582
C
C     LD R1,R2
C     LD (HL),R
C     LD R,(HL)
C
      IF(ARG1TY.EQ.7.AND.ARG2TY.EQ.7)GOTO 2990
      OBJCNT=1
      OBJECT(1)=8*ARG1TY+ARG2TY+55
      ITIME=4
      IF(ARG1TY.EQ.7.OR.ARG2TY.EQ.7)ITIME=7
      GOTO 4200
 2582 IF(ARG2TY.NE.33.AND.ARG2TY.NE.34)GOTO 2584
C
C     LD R,N
C     LD (HL),N
C
      IF((ARG2VL.GE.256.AND.ARG2VL.LE.65407).OR.ARG2BS.GT.127)
     1  ERRORS=IOR(ERRORS,ERRT)
      OBJCNT=2
      OBJECT(1)=8*ARG1TY-2
      OBJECT(2)=IAND(ARG2VL,255)
      ITIME=7
      IF(ARG1TY.EQ.7)ITIME=10
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(4,LOCCTR+1,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2584 IF(ARG2TY.EQ.28.OR.ARG2TY.EQ.29)ARG2TY=ARG2TY+3
      IF(ARG2TY.NE.31.AND.ARG2TY.NE.32)GOTO 2586
C
C     LD R,(II+D)
C
      IF(ARG1TY.EQ.7)GOTO 2990
      OBJCNT=3
      OBJECT(1)=32*ARG2TY-771
      OBJECT(2)=8*ARG1TY+62
      IF(ARG2VL.GE.128.AND.ARG2VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(3)=IAND(ARG2VL,255)
      ITIME=19
      IF(ARG2BS.NE.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
 2586 IF(ARG1TY.NE.8)GOTO 2990
      IF(ARG2TY.GT.10)GOTO 2588
C
C     LD A,I
C     LD A,R
C
      OBJCNT=2
      OBJECT(1)=237
      OBJECT(2)=8*ARG2TY+15
      ITIME=9
      GOTO 4200
 2588 IF(ARG2TY.NE.26.AND.ARG2TY.NE.27)GOTO 2590
C
C     LD A,(BC)
C     LD A,(DE)
C
      OBJCNT=1
      OBJECT(1)=16*ARG2TY-406
      ITIME=7
      GOTO 4200
 2590 IF(ARG2TY.NE.35.AND.ARG2TY.NE.36)GOTO 2990
C
C     LD A,(NN)
C
      OBJCNT=3
      OBJECT(1)=58
      OBJECT(2)=IAND(ARG2VL,255)
      OBJECT(3)=ARG2VL/256
      ITIME=13
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+1,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2592 IF(ARG2TY.NE.8)GOTO 2598
      IF(ARG1TY.NE.9.AND.ARG1TY.NE.10)GOTO 2594
C
C     LD I,A
C     LD R,A
C
      OBJCNT=2
      OBJECT(1)=237
      OBJECT(2)=8*ARG1TY-1
      ITIME=9
      GOTO 4200
 2594 IF(ARG1TY.NE.26.AND.ARG1TY.NE.27)GOTO 2596
C
C     LD (BC),A
C     LD (DE),A
C
      OBJCNT=1
      OBJECT(1)=16*ARG1TY-414
      ITIME=7
      GOTO 4200
 2596 IF(ARG1TY.NE.35.AND.ARG1TY.NE.36)GOTO 2598
C
C     LD (NN),A
C
      OBJCNT=3
      OBJECT(1)=50
      OBJECT(2)=IAND(ARG1VL,255)
      OBJECT(3)=ARG1VL/256
      ITIME=13
      IF(PASS.NE.2.OR.ARG1RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+1,1,IAND(ARG1RL,128)+IAND(ARG1BS,127))
      RELOC=ARG1RL
      RELBAS=ARG1BS
      GOTO 4200
 2598 IF(ARG2TY.GT.8)GOTO 2600
      IF(ARG1TY.EQ.28.OR.ARG1TY.EQ.29)ARG1TY=ARG1TY+3
      IF(ARG1TY.NE.31.AND.ARG1TY.NE.32)GOTO 2990
C
C     LD (II+D),R
C
      IF(ARG2TY.EQ.7)GOTO 2990
      OBJCNT=3
      OBJECT(1)=32*ARG1TY-771
      OBJECT(2)=ARG2TY+111
      IF(ARG1VL.GE.128.AND.ARG1VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(3)=IAND(ARG1VL,255)
      ITIME=19
      IF(ARG1BS.NE.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
 2600 IF(ARG1TY.NE.35.AND.ARG1TY.NE.36)GOTO 2606
      IF(ARG2TY.NE.21)GOTO 2602
C
C     LD (NN),HL
C
      OBJCNT=3
      OBJECT(1)=34
      OBJECT(2)=IAND(ARG1VL,255)
      OBJECT(3)=ARG1VL/256
      ITIME=16
      IF(PASS.NE.2.OR.ARG1RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+1,1,IAND(ARG1RL,128)+IAND(ARG1BS,127))
      RELOC=ARG1RL
      RELBAS=ARG1BS
      GOTO 4200
 2602 IF(ARG2TY.NE.19.AND.ARG2TY.NE.20.AND.ARG2TY.NE.22)GOTO 2604
C
C     LD (NN),BC
C     LD (NN),DE
C     LD (NN),SP
C
      OBJCNT=4
      OBJECT(1)=237
      OBJECT(2)=16*ARG2TY-237
      OBJECT(3)=IAND(ARG1VL,255)
      OBJECT(4)=ARG1VL/256
      ITIME=20
      IF(PASS.NE.2.OR.ARG1RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+2,1,IAND(ARG1RL,128)+IAND(ARG1BS,127))
      RELOC=ARG1RL
      RELBAS=ARG1BS
      GOTO 4200
 2604 IF(ARG2TY.NE.23.AND.ARG2TY.NE.24)GOTO 2990
C
C     LD (NN),II
C
      OBJCNT=4
      OBJECT(1)=32*ARG2TY-515
      OBJECT(2)=34
      OBJECT(3)=IAND(ARG1VL,255)
      OBJECT(4)=ARG1VL/256
      ITIME=20
      IF(PASS.NE.2.OR.ARG1RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+2,1,IAND(ARG1RL,128)+IAND(ARG1BS,127))
      RELOC=ARG1RL
      RELBAS=ARG1BS
      GOTO 4200
 2606 IF(ARG2TY.NE.35.AND.ARG2TY.NE.36)GOTO 2612
      IF(ARG1TY.NE.21)GOTO 2608
C
C     LD HL,(NN)
C
      OBJCNT=3
      OBJECT(1)=42
      OBJECT(2)=IAND(ARG2VL,255)
      OBJECT(3)=ARG2VL/256
      ITIME=16
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+1,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2608 IF(ARG1TY.NE.19.AND.ARG1TY.NE.20.AND.ARG1TY.NE.22)GOTO 2610
C
C     LD BC,(NN)
C     LD DE,(NN)
C     LD SP,(NN)
C
      OBJCNT=4
      OBJECT(1)=237
      OBJECT(2)=16*ARG1TY-229
      OBJECT(3)=IAND(ARG2VL,255)
      OBJECT(4)=ARG2VL/256
      ITIME=20
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+2,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2610 IF(ARG1TY.NE.23.AND.ARG1TY.NE.24)GOTO 2990
C
C     LD II,(NN)
C
      OBJCNT=4
      OBJECT(1)=32*ARG1TY-515
      OBJECT(2)=42
      OBJECT(3)=IAND(ARG2VL,255)
      OBJECT(4)=ARG2VL/256
      ITIME=20
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+2,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2612 IF(ARG2TY.NE.33.AND.ARG2TY.NE.34)GOTO 2618
      IF(ARG1TY.LT.19.OR.ARG1TY.GT.22)GOTO 2614
C
C     LD BC,NN
C     LD DE,NN
C     LD HL,NN
C     LD SP,NN
C
      OBJCNT=3
      OBJECT(1)=16*ARG1TY-303
      OBJECT(2)=IAND(ARG2VL,255)
      OBJECT(3)=ARG2VL/256
      ITIME=10
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+1,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2614 IF(ARG1TY.NE.23.AND.ARG1TY.NE.24)GOTO 2616
C
C     LD II,NN
C
      OBJCNT=4
      OBJECT(1)=32*ARG1TY-515
      OBJECT(2)=33
      OBJECT(3)=IAND(ARG2VL,255)
      OBJECT(4)=ARG2VL/256
      ITIME=14
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+2,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2616 IF(ARG1TY.EQ.28.OR.ARG1TY.EQ.29)ARG1TY=ARG1TY+3
      IF(ARG1TY.NE.31.AND.ARG1TY.NE.32)GOTO 2990
C
C     LD (II+D),N
C
      OBJCNT=4
      OBJECT(1)=32*ARG1TY-771
      OBJECT(2)=54
      IF(ARG1VL.GE.128.AND.ARG1VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(3)=IAND(ARG1VL,255)
      IF(ARG2VL.GE.256.AND.ARG2VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(4)=IAND(ARG2VL,255)
      ITIME=19
      IF(ARG1BS.NE.0)ERRORS=IOR(ERRORS,ERRR)
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(4,LOCCTR+3,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2618 IF(ARG1TY.NE.22)GOTO 2990
      IF(ARG2TY.NE.21)GOTO 2619
C
C     LD SP,HL
C
      OBJCNT=1
      OBJECT(1)=249
      ITIME=6
      GOTO 4200
 2619 IF(ARG2TY.NE.23.AND.ARG2TY.NE.24)GOTO 2990
C
C     LD SP,II
C
      OBJCNT=2
      OBJECT(1)=32*ARG2TY-515
      OBJECT(2)=249
      ITIME=10
      GOTO 4200
C
C     OPTYPE=2 (ADD)
C
 2620 IF(ARGCNT.LT.1.OR.ARGCNT.GT.2)GOTO 2990
      IF(ARG1TY.LE.0)GOTO 2990
      IF(ARGCNT.EQ.2.AND.ARG2TY.LE.0)GOTO 2990
      IF(ARGCNT.EQ.2)GOTO 2622
      ARG2TY=ARG1TY
      ARG2VL=ARG1VL
      ARG2RL=ARG1RL
      ARG2BS=ARG1BS
      ARG1TY=8
 2622 IF(ARG1TY.NE.8)GOTO 2628
      IF(ARG2TY.GT.8)GOTO 2624
C
C     ADD A,R
C     ADD A,(HL)
C
      OBJCNT=1
      OBJECT(1)=ARG2TY+127
      ITIME=4
      IF(ARG2TY.EQ.7)ITIME=7
      GOTO 4200
 2624 IF(ARG2TY.EQ.28.OR.ARG2TY.EQ.29)ARG2TY=ARG2TY+3
      IF(ARG2TY.NE.31.AND.ARG2TY.NE.32)GOTO 2626
C
C     ADD A,(II+D)
C
      OBJCNT=3
      OBJECT(1)=32*ARG2TY-771
      OBJECT(2)=134
      IF(ARG2VL.GE.128.AND.ARG2VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(3)=IAND(ARG2VL,255)
      ITIME=19
      IF(ARG2BS.NE.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
 2626 IF(ARG2TY.NE.33.AND.ARG2TY.NE.34)GOTO 2990
C
C     ADD A,N
C
      OBJCNT=2
      OBJECT(1)=198
      IF(ARG2VL.GE.256.AND.ARG2VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(2)=IAND(ARG2VL,255)
      ITIME=7
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(4,LOCCTR+1,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2628 IF(ARG1TY.NE.21)GOTO 2630
      IF(ARG2TY.LT.19.OR.ARG2TY.GT.22)GOTO 2990
C
C     ADD HL,BC
C     ADD HL,DE
C     ADD HL,HL
C     ADD HL,SP
C
      OBJCNT=1
      OBJECT(1)=16*ARG2TY-295
      ITIME=11
      GOTO 4200
 2630 IF(ARG1TY.NE.23.AND.ARG1TY.NE.24)GOTO 2632
      IF(ARG2TY.NE.19.AND.ARG2TY.NE.20.AND.ARG2TY.NE.22)GOTO 2632
C
C     ADD II,BC
C     ADD II,DE
C     ADD II,SP
C
      OBJCNT=2
      OBJECT(1)=32*ARG1TY-515
      OBJECT(2)=16*ARG2TY-295
      ITIME=15
      GOTO 4200
 2632 IF(ARG1TY.NE.23)GOTO 2634
      IF(ARG2TY.NE.23)GOTO 2990
C
C     ADD IX,IX
C
      OBJCNT=2
      OBJECT(1)=221
      OBJECT(2)=41
      ITIME=15
      GOTO 4200
 2634 IF(ARG1TY.NE.24)GOTO 2990
      IF(ARG2TY.NE.24)GOTO 2990
C
C     ADD IY,IY
C
      OBJCNT=2
      OBJECT(1)=253
      OBJECT(2)=41
      ITIME=15
      GOTO 4200
C
C     OPTYPE=3 (ADC/SBC)
C
 2640 IF(ARGCNT.LT.1.OR.ARGCNT.GT.2)GOTO 2990
      IF(ARG1TY.LE.0)GOTO 2990
      IF(ARGCNT.EQ.2.AND.ARG2TY.LE.0)GOTO 2990
      IF(ARGCNT.EQ.2)GOTO 2642
      ARG2TY=ARG1TY
      ARG2VL=ARG1VL
      ARG2RL=ARG1RL
      ARG2BS=ARG1BS
      ARG1TY=8
 2642 IF(ARG1TY.NE.8)GOTO 2648
      IF(ARG2TY.GT.8)GOTO 2644
C
C     ADC/SBC A,R
C     ADC/SBC A,(HL)
C
      OBJCNT=1
      OBJECT(1)=135+16*OPBASE(OPIDX)+ARG2TY
      ITIME=4
      IF(ARG2TY.EQ.7)ITIME=7
      GOTO 4200
 2644 IF(ARG2TY.EQ.28.OR.ARG2TY.EQ.29)ARG2TY=ARG2TY+3
      IF(ARG2TY.NE.31.AND.ARG2TY.NE.32)GOTO 2646
C
C     ADC/SBC A,(II+D)
C
      OBJCNT=3
      OBJECT(1)=32*ARG2TY-771
      OBJECT(2)=142+16*OPBASE(OPIDX)
      IF(ARG2VL.GE.128.AND.ARG2VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(3)=IAND(ARG2VL,255)
      ITIME=19
      IF(ARG2BS.NE.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
 2646 IF(ARG2TY.NE.33.AND.ARG2TY.NE.34)GOTO 2990
C
C     ADC/SBC A,N
C
      OBJCNT=2
      OBJECT(1)=206+16*OPBASE(OPIDX)
      IF(ARG2VL.GE.256.AND.ARG2VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(2)=IAND(ARG2VL,255)
      ITIME=7
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(4,LOCCTR+1,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2648 IF(ARG1TY.NE.21)GOTO 2990
      IF(ARG2TY.LT.19.OR.ARG2TY.GT.22)GOTO 2990
C
C     ADC/SBC HL,BC
C     ADC/SBC HL,DE
C     ADC/SBC HL,HL
C     ADC/SBC HL,SP
C
      OBJCNT=2
      OBJECT(1)=237
      OBJECT(2)=16*ARG2TY-8*OPBASE(OPIDX)-230
      ITIME=15
      GOTO 4200
C
C     OPTYPE=4 (INC/DEC)
C
 2650 IF(ARGCNT.NE.1.OR.ARG1TY.LE.0)GOTO 2990
      IF(ARG1TY.GT.8)GOTO 2652
C
C     INC/DEC R
C     INC/DEC (HL)
C
      OBJCNT=1
      OBJECT(1)=OPBASE(OPIDX)+8*ARG1TY-4
      ITIME=4
      IF(ARG1TY.EQ.7)ITIME=11
      GOTO 4200
 2652 IF(ARG1TY.EQ.28.OR.ARG1TY.EQ.29)ARG1TY=ARG1TY+3
      IF(ARG1TY.NE.31.AND.ARG1TY.NE.32)GOTO 2654
C
C     INC/DEC (II+D)
C
      OBJCNT=3
      OBJECT(1)=32*ARG1TY-771
      OBJECT(2)=OPBASE(OPIDX)+52
      IF(ARG1VL.GE.128.AND.ARG1VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(3)=IAND(ARG1VL,255)
      ITIME=19
      IF(ARG1BS.NE.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
 2654 IF(ARG1TY.LT.19.OR.ARG1TY.GT.22)GOTO 2656
C
C     INC/DEC BC
C     INC/DEC DE
C     INC/DEC HL
C     INC/DEC SP
C
      OBJCNT=1
      OBJECT(1)=16*ARG1TY+8*OPBASE(OPIDX)-301
      ITIME=6
      GOTO 4200
 2656 IF(ARG1TY.NE.23.AND.ARG1TY.NE.24)GOTO 2990
C
C     INC/DEC II
C
      OBJCNT=2
      OBJECT(1)=32*ARG1TY-515
      OBJECT(2)=8*OPBASE(OPIDX)+35
      ITIME=10
      GOTO 4200
C
C     OPTYPE=5 (SUB/AND/XOR/OR/CP)
C
 2660 IF(ARGCNT.NE.1.OR.ARG1TY.LE.0)GOTO 2990
      IF(ARG1TY.GT.8)GOTO 2662
C
C     OP R
C     OP (HL)
C
      OBJCNT=1
      OBJECT(1)=8*OPBASE(OPIDX)+ARG1TY+127
      ITIME=4
      IF(ARG1TY.EQ.7)ITIME=7
      GOTO 4200
 2662 IF(ARG1TY.EQ.28.OR.ARG1TY.EQ.29)ARG1TY=ARG1TY+3
      IF(ARG1TY.NE.31.AND.ARG1TY.NE.32)GOTO 2664
C
C     OP (II+D)
C
      OBJCNT=3
      OBJECT(1)=32*ARG1TY-771
      OBJECT(2)=8*OPBASE(OPIDX)+134
      IF(ARG1VL.GE.128.AND.ARG1VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(3)=IAND(ARG1VL,255)
      ITIME=19
      IF(ARG1BS.NE.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
 2664 IF(ARG1TY.NE.33.AND.ARG1TY.NE.34)GOTO 2990
C
C     OP N
C
      OBJCNT=2
      OBJECT(1)=8*OPBASE(OPIDX)+198
      IF(ARG1VL.GE.256.AND.ARG1VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(2)=IAND(ARG1VL,255)
      ITIME=7
      IF(PASS.NE.2.OR.ARG1RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(4,LOCCTR+1,1,IAND(ARG1RL,128)+IAND(ARG1BS,127))
      RELOC=ARG1RL
      RELBAS=ARG1BS
      GOTO 4200
C
C     OPTYPE=6 (RLC/RRC/RL/RR/SLA/SRA/SRL)
C
 2670 IF(ARGCNT.NE.1.OR.ARG1TY.LE.0)GOTO 2990
      IF(ARG1TY.GT.8)GOTO 2672
C
C     OP R
C     OP (HL)
C
      OBJCNT=2
      OBJECT(1)=203
      OBJECT(2)=8*OPBASE(OPIDX)+ARG1TY-1
      ITIME=8
      IF(ARG1TY.EQ.7)ITIME=15
      GOTO 4200
 2672 IF(ARG1TY.EQ.28.OR.ARG1TY.EQ.29)ARG1TY=ARG1TY+3
      IF(ARG1TY.NE.31.AND.ARG1TY.NE.32)GOTO 2990
C
C     OP (II+D)
C
      OBJCNT=4
      OBJECT(1)=32*ARG1TY-771
      OBJECT(2)=203
      IF(ARG1VL.GE.128.AND.ARG1VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(3)=IAND(ARG1VL,255)
      OBJECT(4)=8*OPBASE(OPIDX)+6
      ITIME=23
      IF(ARG1BS.NE.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
C
C     OPTYPE=7 (BIT/RES/SET)
C
 2680 IF(ARGCNT.NE.2.OR.(ARG1TY.NE.33.AND.ARG1TY.NE.34).OR.ARG1BS.NE.
     1  0.OR.(ARG1VL.LT.0.OR.ARG1VL.GT.7).OR.ARG2TY.LE.0)GOTO 2990
      IF(ARG2TY.GT.8)GOTO 2682
C
C     OP B,R
C     OP B,(HL)
C
      OBJCNT=2
      OBJECT(1)=203
      OBJECT(2)=64*OPBASE(OPIDX)+8*ARG1VL+ARG2TY-1
      ITIME=8
      IF(ARG2TY.EQ.7.AND.OPBASE(OPIDX).EQ.1)ITIME=12
      IF(ARG2TY.EQ.7.AND.OPBASE(OPIDX).NE.1)ITIME=15
      GOTO 4200
 2682 IF(ARG2TY.EQ.28.OR.ARG2TY.EQ.29)ARG2TY=ARG2TY+3
      IF(ARG2TY.NE.31.AND.ARG2TY.NE.32)GOTO 2990
C
C     OP B,(II+D)
C
      OBJCNT=4
      OBJECT(1)=32*ARG2TY-771
      OBJECT(2)=203
      IF(ARG2VL.GE.128.AND.ARG2VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(3)=IAND(ARG2VL,255)
      OBJECT(4)=64*OPBASE(OPIDX)+8*ARG1VL+6
      ITIME=20
      IF(OPBASE(OPIDX).NE.1)ITIME=23
      IF(ARG2BS.NE.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
C
C     OPTYPE=8 (JP)
C
 2690 IF(ARGCNT.NE.1.AND.ARGCNT.NE.2)GOTO 2990
      IF(ARG1TY.LE.0)GOTO 2990
      IF(ARGCNT.EQ.2.AND.ARG2TY.LE.0)GOTO 2990
      IF(ARGCNT.NE.2)GOTO 2692
      IF(ARG1TY.EQ.2)ARG1TY=14
      IF(ARG1TY.LT.11.OR.ARG1TY.GT.18)GOTO 2990
      IF(ARG2TY.NE.33.AND.ARG2TY.NE.34)GOTO 2990
C
C     JP CC,NN
C
      OBJCNT=3
      OBJECT(1)=8*ARG1TY+106
      OBJECT(2)=IAND(ARG2VL,255)
      OBJECT(3)=ARG2VL/256
      ITIME=10
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+1,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2692 IF(ARG1TY.NE.7)GOTO 2694
C
C     JP (HL)
C
      OBJCNT=1
      OBJECT(1)=233
      ITIME=4
      GOTO 4200
 2694 IF(ARG1TY.NE.28.AND.ARG1TY.NE.29)GOTO 2696
C
C     JP (II)
C
      OBJCNT=2
      OBJECT(1)=32*ARG1TY-675
      OBJECT(2)=233
      ITIME=8
      GOTO 4200
 2696 IF(ARG1TY.NE.33.AND.ARG1TY.NE.34)GOTO 2990
C
C     JP NN
C
      OBJCNT=3
      OBJECT(1)=195
      OBJECT(2)=IAND(ARG1VL,255)
      OBJECT(3)=ARG1VL/256
      ITIME=10
      IF(PASS.NE.2.OR.ARG1RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+1,1,IAND(ARG1RL,128)+IAND(ARG1BS,127))
      RELOC=ARG1RL
      RELBAS=ARG1BS
      GOTO 4200
C
C     OPTYPE=9 (CALL)
C
 2700 IF(ARGCNT.LT.1.OR.ARGCNT.GT.2)GOTO 2990
      IF(ARG1TY.LE.0)GOTO 2990
      IF(ARGCNT.EQ.2.AND.ARG2TY.LE.0)GOTO 2990
      IF(ARGCNT.NE.2)GOTO 2702
      IF(ARG1TY.EQ.2)ARG1TY=14
      IF(ARG1TY.EQ.2)ARG1TY=14
      IF(ARG1TY.LT.11.OR.ARG1TY.GT.18)GOTO 2990
      IF(ARG2TY.NE.33.AND.ARG2TY.NE.34)GOTO 2990
C
C     CALL CC,NN
C
      OBJCNT=3
      OBJECT(1)=8*ARG1TY+108
      OBJECT(2)=IAND(ARG2VL,255)
      OBJECT(3)=ARG2VL/256
      ITIME=10
      JTIME=17
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+1,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2702 IF(ARG1TY.NE.33.AND.ARG1TY.NE.34)GOTO 2990
C
C     CALL NN
C
      OBJCNT=3
      OBJECT(1)=205
      OBJECT(2)=IAND(ARG1VL,255)
      OBJECT(3)=ARG1VL/256
      ITIME=17
      IF(PASS.NE.2.OR.ARG1RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(3,LOCCTR+1,1,IAND(ARG1RL,128)+IAND(ARG1BS,127))
      RELOC=ARG1RL
      RELBAS=ARG1BS
      GOTO 4200
C
C     OPTYPE=10 (RET)
C
 2710 IF(ARGCNT.NE.0.AND.ARGCNT.NE.1)GOTO 2990
      IF(ARGCNT.EQ.1.AND.ARG1TY.LE.0)GOTO 2990
      IF(ARGCNT.NE.1)GOTO 2712
      IF(ARG1TY.EQ.2)ARG1TY=14
      IF(ARG1TY.LT.11.OR.ARG1TY.GT.18)GOTO 2990
C
C     RET CC
C
      OBJCNT=1
      OBJECT(1)=8*ARG1TY+104
      ITIME=5
      JTIME=11
      GOTO 4200
C
C     RET
C
 2712 OBJCNT=1
      OBJECT(1)=201
      ITIME=10
      GOTO 4200
C
C     OPTYPE=11 (JR)
C
 2720 IF(ARGCNT.NE.1.AND.ARGCNT.NE.2)GOTO 2990
      IF(ARG1TY.LE.0)GOTO 2990
      IF(ARGCNT.EQ.2.AND.ARG2TY.LE.0)GOTO 2990
      IF(ARGCNT.NE.2)GOTO 2722
      IF(ARG1TY.EQ.2)ARG1TY=14
      IF(ARG1TY.LT.11.OR.ARG1TY.GT.14)GOTO 2990
      IF(ARG2TY.NE.33.AND.ARG2TY.NE.34)GOTO 2990
C
C     JR CC,E
C
      OBJCNT=2
      OBJECT(1)=8*ARG1TY-56
      ARG2VL=IAND(ARG2VL-LOCCTR-2,6O177777)
      IF(ARG2VL.GE.128.AND.ARG2VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(2)=IAND(ARG2VL,255)
      ITIME=7
      JTIME=12
      IF(ARG2BS.GT.1.OR.ARG2RL.LT.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
 2722 IF(ARG1TY.NE.33.AND.ARG1TY.NE.34)GOTO 2990
C
C     JR E
C
      OBJCNT=2
      OBJECT(1)=24
      ARG1VL=IAND(ARG1VL-LOCCTR-2,6O177777)
      IF(ARG1VL.GE.128.AND.ARG1VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(2)=IAND(ARG1VL,255)
      ITIME=12
      IF(ARG1BS.GT.1.OR.ARG1RL.LT.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
C
C     OPTYPE=12 (IN)
C
 2730 IF(ARGCNT.NE.2.OR.ARG1TY.LE.0.OR.ARG2TY.LE.0)GOTO 2990
      IF(ARG1TY.NE.8)GOTO 2732
      IF(ARG2TY.NE.35.AND.ARG2TY.NE.36)GOTO 2732
C
C     IN A,(N)
C
      OBJCNT=2
      OBJECT(1)=219
      IF(ARG2VL.GE.256.AND.ARG2VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(2)=IAND(ARG2VL,255)
      ITIME=10
      IF(PASS.NE.2.OR.ARG2RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(4,LOCCTR+1,1,IAND(ARG2RL,128)+IAND(ARG2BS,127))
      RELOC=ARG2RL
      RELBAS=ARG2BS
      GOTO 4200
 2732 IF(ARG1TY.GT.8)GOTO 2990
      IF(ARG2TY.NE.25)GOTO 2990
C
C     IN R,(C)
C
      OBJCNT=2
      OBJECT(1)=237
      OBJECT(2)=8*ARG1TY+56
      ITIME=11
      GOTO 4200
C
C     OPTYPE=13 (OUT)
C
 2740 IF(ARGCNT.NE.2.OR.ARG1TY.LE.0.OR.ARG2TY.LE.0)GOTO 2990
      IF(ARG2TY.NE.8)GOTO 2742
      IF(ARG1TY.NE.35.AND.ARG1TY.NE.36)GOTO 2742
C
C     OUT (N),A
C
      OBJCNT=2
      OBJECT(1)=211
      IF(ARG1VL.GE.256.AND.ARG1VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(2)=IAND(ARG1VL,255)
      ITIME=11
      IF(PASS.NE.2.OR.ARG1RL.EQ.0.OR.IAND(OPTION,OPTOU).EQ.0)GOTO 4200
      CALL OBJOUT(4,LOCCTR+1,1,IAND(ARG1RL,128)+IAND(ARG1BS,127))
      RELOC=ARG1RL
      RELBAS=ARG1BS
      GOTO 4200
 2742 IF(ARG2TY.GT.8.OR.ARG2TY.EQ.7)GOTO 2990
      IF(ARG1TY.NE.25)GOTO 2990
C
C     OUT (C),R
C
      OBJCNT=2
      OBJECT(1)=237
      OBJECT(2)=8*ARG2TY+57
      ITIME=12
      GOTO 4200
C
C     OPTYPE=14 (PUSH/POP)
C
 2750 IF(ARGCNT.NE.1.OR.ARG1TY.LE.0)GOTO 2990
      IF(ARG1TY.EQ.14)ARG1TY=22
      IF(ARG1TY.LT.19.OR.ARG1TY.GT.22)GOTO 2752
C
C     PUSH/POP BC
C     PUSH/POP DE
C     PUSH/POP HL
C     PUSH/POP AF
C
      OBJCNT=1
      OBJECT(1)=16*ARG1TY+4*OPBASE(OPIDX)-111
      ITIME=OPBASE(OPIDX)+10
      GOTO 4200
 2752 IF(ARG1TY.NE.23.AND.ARG1TY.NE.24)GOTO 2990
C
C     PUSH/POP II
C
      OBJCNT=2
      OBJECT(1)=32*ARG1TY-515
      OBJECT(2)=4*OPBASE(OPIDX)+225
      ITIME=OPBASE(OPIDX)+14
      GOTO 4200
C
C     OPTYPE=15 (EX)
C
 2760 IF(ARGCNT.NE.2.OR.ARG1TY.LE.0.OR.ARG2TY.LE.0)GOTO 2990
      IF(ARG1TY.NE.30)GOTO 2764
      IF(ARG2TY.NE.21)GOTO 2762
C
C     EX (SP),HL
C
      OBJCNT=1
      OBJECT(1)=227
      ITIME=19
      GOTO 4200
 2762 IF(ARG2TY.NE.23.AND.ARG2TY.NE.24)GOTO 2990
C
C     EX (SP),II
C
      OBJCNT=2
      OBJECT(1)=32*ARG2TY-515
      OBJECT(2)=227
      ITIME=23
      GOTO 4200
 2764 IF(ARG1TY.NE.20)GOTO 2766
      IF(ARG2TY.NE.21)GOTO 2990
C
C     EX DE,HL
C
      OBJCNT=1
      OBJECT(1)=235
      ITIME=4
      GOTO 4200
 2766 IF(ARG1TY.NE.14)GOTO 2990
      IF(ARG2TY.NE.37)GOTO 2990
C
C     EX AF,AF'
C
      OBJCNT=1
      OBJECT(1)=8
      ITIME=4
      GOTO 4200
C
C     OPTYPE=16 (DJNZ)
C
 2770 IF(ARGCNT.NE.1.OR.ARG1TY.LE.0)GOTO 2990
      IF(ARG1TY.NE.33.AND.ARG1TY.NE.34)GOTO 2990
C
C     DJNZ E
C
      OBJCNT=2
      OBJECT(1)=16
      ARG1VL=ARG1VL-LOCCTR-2
      IF(ARG1VL.GE.128.AND.ARG1VL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      OBJECT(2)=IAND(ARG1VL,255)
      ITIME=12
      IF(ARG1BS.GT.1.OR.ARG1RL.LT.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 4200
C
C     OPTYPE=17 (IM)
C
 2780 IF(ARGCNT.NE.1.OR.ARG1TY.NE.33.OR.ARG1VL.LT.0.OR.ARG1VL.GT.2
     1  .OR.ARG1RL.NE.0)GOTO 2990
C
C     IM 0
C     IM 1
C     IM 2
C
      OBJCNT=2
      OBJECT(1)=237
      IF(ARG1VL.EQ.0)OBJECT(2)=70
      IF(ARG1VL.EQ.1)OBJECT(2)=86
      IF(ARG1VL.EQ.2)OBJECT(2)=94
      ITIME=8
      GOTO 4200
C
C     OPTYPE=18 (RST)
C
 2790 IF(ARGCNT.NE.1.OR.ARG1TY.NE.33.OR.ARG1VL.LT.0.OR.ARG1VL.GT.56
     1  .OR.ARG1RL.NE.0)GOTO 2990
      IF(ARG1VL.GE.1.AND.ARG1VL.LE.7)ARG1VL=8*ARG1VL
C
C     RST T
C
      IF(IAND(ARG1VL,65479).NE.0)GOTO 2990
      OBJCNT=1
      OBJECT(1)=199+ARG1VL
      ITIME=11
      GOTO 4200
C
C     OPTYPE=19 (EXX/DAA/CPL/CCF/SCF/NOP/HALT/DI/EI/RLCA/RLA/RRCA/RRA)
C
 2800 IF(ARGCNT.NE.0)GOTO 2990
C
C     EXX   DAA   CPL   CCF   SCF   NOP
C     HALT  DI    EI    RLCA  RRCA  RLA   RRA
C
      OBJCNT=1
      OBJECT(1)=OPBASE(OPIDX)
      ITIME=OPTIME(OPIDX)
      GOTO 4200
C
C     OPTYPE=20 (LDI/LDIR/LDD/LDDR/CPI/CPIR/CPD/CPDR/NEG/RLD/RRD/RETI/
C                RETN/INI/INIR/IND/INDR/OUTI/OTIR/OUTD/OTDR)
C
 2810 IF(ARGCNT.NE.0)GOTO 2990
C
C     LDI   LDIR  LDD   LDDR  CPI   CPIR  CPD   CPDR  NEG   RLD   RRD
C     RETI  RETN  INI   INIR  IND   INDR  OUTI  OTIR  OUTD  OTDR
C
      OBJCNT=2
      OBJECT(1)=237
      OBJECT(2)=OPBASE(OPIDX)
      ITIME=IAND(OPTIME(OPIDX),31)
      JTIME=OPTIME(OPIDX)/32
      GOTO 4200
C
C     ILLEGAL COMBINATION OF ARGUMENTS
C
 2990 ERRORS=IOR(ERRORS,ERRA)
      GOTO 3640
C
C     OPTYPE(OPIDX) EXCEEDS 20, INDICATING A SPECIAL OPCODE OR MACRO
C
 3000 IF(OPTYPE(OPIDX).GT.100)GOTO 3650
      IF(OPBASE(OPIDX).EQ.0)GOTO 3010
C
C     ASSIGN LABEL THE VALUE OF THE LOCATION COUNTER
C
      RELOC=0
      IF(IAND(OPTION,OPTREL).NE.0)RELOC=1
      RELBAS=0
      IF(LOCCTR.GE.256)RELBAS=1
      PTR=DULBL(LOCCTR)
      RELOC=0
      RELBAS=0
C
C     GO TO PROPER ROUTINE
C
 3010 GOTO(3020,3020,3020,3140,3020,3170,3200,3220,3240,3240,
     1  3320,3340,3390,3400,3420,3480,3490,3500,3510,3540,
     2  3560,3570,3620,3690,3240,3210),OPTYPE(OPIDX)-20
C
C     PROCESS DATA/WORD/BYTE/TEXT/ADDR INSTRUCTIONS
C
 3020 IF(ARGCNT.EQ.0)GOTO 3630
      CURPOS=ARGBGN
      PTR=ARGBGN
 3030 IF(PTR.GT.ARGEND.OR.INPBUF(PTR).EQ.1H,)GOTO 3070
      IF(INPBUF(PTR).EQ.1H')GOTO 3050
      IF(INPBUF(PTR).EQ.1H")GOTO 3060
 3040 PTR=PTR+1
      GOTO 3030
 3050 PTR=PTR+1
      IF(INPBUF(PTR).NE.1H')GOTO 3050
      GOTO 3040
 3060 PTR=PTR+1
      IF(INPBUF(PTR).NE.1H")GOTO 3060
      GOTO 3040
 3070 HOLD=INPBUF(PTR)
      INPBUF(PTR)=KEOC
      CALL EVE(VAL)
      INPBUF(PTR)=HOLD
      IF(VAL.EQ.99999)GOTO 3630
      GOTO(3080,3100,3110,3140,3120),OPTYPE(OPIDX)-20
C
C     PROCESS DATA/TEXT OPCODE
C
 3080 IF(VAL.GE.0)GOTO 3090
      VAL=-VAL
      DO 3085 I=1,VAL
        OBJCNT=OBJCNT+1
        OBJECT(OBJCNT)=ISL(TEXT(I),MBITS3)
 3085 CONTINUE
      GOTO 3130
 3090 IF((VAL.LT.256.OR.VAL.GT.65407).AND.RELBAS.LE.127)GOTO 3095
      OBJCNT=OBJCNT+1
      OBJECT(OBJCNT)=IAND(VAL/256,255)
      IF(PASS.EQ.2.AND.RELOC.NE.0.AND.IAND(OPTION,OPTOU).NE.0)
     1  CALL OBJOUT(3,LOCCTR+OBJCNT-1,1,IAND(RELOC,128)+IAND(RELBAS,
     2  127))
 3095 OBJCNT=OBJCNT+1
      OBJECT(OBJCNT)=IAND(VAL,255)
      IF(PASS.EQ.2.AND.RELOC.NE.0.AND.IAND(OPTION,OPTOU).NE.0.AND.VAL
     1  .GE.256.AND.VAL.LE.65407)CALL OBJOUT(4,LOCCTR+OBJCNT-1,1,
     2  IAND(RELOC,128)+IAND(RELBAS,127))
      GOTO 3130
C
C     PROCESS WORD OPCODE
C
 3100 IF(VAL.LT.0)GOTO 3630
      OBJCNT=OBJCNT+1
      OBJECT(OBJCNT)=IAND(VAL/256,255)
      OBJCNT=OBJCNT+1
      OBJECT(OBJCNT)=IAND(VAL,255)
      IF(RELOC.NE.0)ERRORS=IOR(ERRORS,ERRR)
      GOTO 3130
C
C     PROCESS BYTE OPCODE
C
 3110 IF(VAL.LT.0)GOTO 3630
      IF((VAL.LT.256.OR.VAL.GT.65407).AND.RELBAS.LE.127)GOTO 3095
      ERRORS=IOR(ERRORS,ERRT)
 3115 OBJCNT=OBJCNT+1
      OBJECT(OBJCNT)=IAND(VAL,255)
      IF(PASS.EQ.2.AND.RELOC.NE.0.AND.IAND(OPTION,OPTOU).NE.0)
     1  CALL OBJOUT(4,LOCCTR+OBJCNT-1,1,IAND(RELOC,128)+IAND(RELBAS,
     2  127))
      GOTO 3130
C
C     PROCESS ADDR OPCODE
C
 3120 IF(VAL.LT.0)GOTO 3630
      OBJCNT=OBJCNT+1
      OBJECT(OBJCNT)=IAND(VAL,255)
      OBJCNT=OBJCNT+1
      OBJECT(OBJCNT)=IAND(VAL/256,255)
      IF(PASS.EQ.2.AND.RELOC.NE.0.AND.IAND(OPTION,OPTOU).NE.0)
     1  CALL OBJOUT(3,LOCCTR+OBJCNT-2,1,IAND(RELOC,128)+IAND(RELBAS,
     2  127))
C
C     BUMP TO NEXT ARGUMENT
C
 3130 PTR=PTR+1
      CURPOS=PTR
      IF(PTR.LE.ARGEND)GOTO 3030
      GOTO 4200
C
C     PROCESS SET COMMAND
C
 3140 IF(LABEL(1).NE.0)GOTO 3150
      ERRORS=IOR(ERRORS,ERRL)
      GOTO 4200
 3150 IF(ARGCNT.NE.1)GOTO 3630
      CURPOS=ARGBGN
      HOLD=INPBUF(ARGEND+1)
      INPBUF(ARGEND+1)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGEND+1)=HOLD
      IF(VAL.EQ.99999.OR.VAL.LT.0.OR.RELBAS.GT.1)GOTO 3630
      PTR=DULBL(VAL)
      IF(PTR.LE.0)GOTO 4200
      SYMBOL(3,PTR)=IOR(SYMBOL(3,PTR),2)
      GOTO 4200
C
C     PROCESS EQU COMMAND
C
 3170 IF(LABEL(1).NE.0)GOTO 3180
      ERRORS=IOR(ERRORS,ERRL)
      GOTO 4200
 3180 IF(ARGCNT.NE.1)GOTO 3630
      CURPOS=ARGBGN
      HOLD=INPBUF(ARGEND+1)
      INPBUF(ARGEND+1)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGEND+1)=HOLD
      IF(VAL.EQ.99999.OR.VAL.LT.0.OR.RELBAS.GT.1)GOTO 3630
      PTR=DULBL(VAL)
      GOTO 4200
C
C     PROCESS ORG COMMAND
C
 3200 IF(ARGCNT.NE.1)GOTO 3630
      CURPOS=ARGBGN
      HOLD=INPBUF(ARGEND+1)
      INPBUF(ARGEND+1)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGEND+1)=HOLD
      IF(VAL.EQ.99999.OR.VAL.LT.0.OR.RELBAS.GT.1)GOTO 3630
      LOCCTR=VAL
      RELOC=0
      IF(IAND(OPTION,OPTREL).NE.0)RELOC=1
      RELBAS=0
      IF(LOCCTR.GE.256)RELBAS=1
      PTR=DULBL(LOCCTR)
      GOTO 4200
C
C     PROCESS BSS COMMAND
C
 3220 IF(ARGCNT.NE.1)GOTO 3630
      CURPOS=ARGBGN
      HOLD=INPBUF(ARGEND+1)
      INPBUF(ARGEND+1)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGEND+1)=HOLD
      IF(VAL.EQ.99999.OR.VAL.LT.0.OR.RELBAS.NE.0)GOTO 3630
      IF(PASS.EQ.2.AND.IAND(OPTION,OPTOU).NE.0)
     1  CALL OBJOUT(IAND(OPTION,OPTREL)+5,LOCCTR,2,VAL)
      LOCCTR=LOCCTR+VAL
      IF(LOCCTR.GT.65535.OR.LOCCTR.LT.0)ERRORS=IOR(ERRORS,ERRT)
      LOCCTR=IAND(LOCCTR,65535)
      GOTO 4200
C
C     PROCESS BOUND COMMAND
C
 3210 IF(ARGCNT.NE.1)GOTO 3630
      CURPOS=ARGBGN
      HOLD=INPBUF(ARGEND+1)
      INPBUF(ARGEND+1)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGEND+1)=HOLD
      IF(VAL.EQ.99999.OR.VAL.LE.0.OR.RELBAS.GT.1)GOTO 3630
      IF(VAL.EQ.1)GOTO 3215
      IF(PASS.EQ.2.AND.IAND(OPTION,OPTOU).NE.0)CALL OBJOUT(-1,0,0,0)
      IF(PASS.EQ.2.AND.IAND(OPTION,OPTOU).NE.0)
     1  CALL OBJOUT(IAND(OPTION,OPTREL)+2,LOCCTR,2,VAL)
 3215 LOCCTR=IAND(((LOCCTR+VAL-1)/VAL)*VAL,6O177777)
      RELOC=0
      IF(IAND(OPTION,OPTREL).NE.0)RELOC=1
      RELBAS=0
      IF(LOCCTR.GE.256)RELBAS=1
      PTR=DULBL(LOCCTR)
      GOTO 4200
C
C     PROCESS ENT/DEF AND EXT[L|S]/REF[L|S] COMMANDS
C
 3240 IF(LABEL(1).NE.0)ERRORS=IAND(ERRORS,ERRL)
      IF(ARGCNT.LE.0)GOTO 3630
      CURPOS=ARGBGN
 3250 LABEL(1)=0
      LABEL(2)=0
      CALL NXTCHR(CH,TY)
      IF(TY.NE.1)GOTO 3630
      CALL PACK(LABEL(1),CH)
 3260 CALL NXTCHR(CH,TY)
      IF(TY.NE.1.AND.TY.NE.2)GOTO 3270
      CALL PACK(LABEL(1),CH)
      GOTO 3260
 3270 PTR=DULBL(-1)
      IF(OPTYPE(OPIDX).NE.29)GOTO 3290
      IF(IAND(SYMBOL(3,PTR),8).EQ.0)GOTO 3280
      ERRORS=IOR(ERRORS,ERRD)
      GOTO 3310
 3280 SYMBOL(3,PTR)=IOR(SYMBOL(3,PTR),4)
      IF(PASS.GT.0.AND.IAND(SYMBOL(3,PTR),3).EQ.0)ERRORS=IOR(ERRORS,
     *  ERRU)
      GOTO 3310
 3290 IF(SYMBOL(3,PTR).EQ.0)GOTO 3300
      IF(ISL(SYMBOL(3,PTR),MBITS2).NE.0)GOTO 3310
      ERRORS=IOR(ERRORS,ERRD)
      GOTO 3310
 3300 REFCNT=REFCNT+1
      IF(REFCNT.LE.127)GOTO 3305
      ERRORS=IOR(ERRORS,ERRX)
      GOTO 3310
 3305 IF(OPTYPE(OPIDX).EQ.30)SYMBOL(3,PTR)=ISL(REFCNT+128,NBITS2)+25
      IF(OPTYPE(OPIDX).EQ.45)SYMBOL(3,PTR)=ISL(REFCNT,NBITS2)+25
 3310 IF(CH.EQ.1H .OR.CH.EQ.1H;.OR.CH.EQ.1H!.OR.CH.EQ.KEOC)GOTO 4200
      IF(CH.NE.1H,)GOTO 3630
      GOTO 3250
C
C     PROCESS END COMMAND
C
 3320 IF(IUNIT.EQ.SIUNIT.OR.IUNIT.EQ.TMUNIT)GOTO 3330
      ERRORS=IOR(ERRORS,ERRO)
      GOTO 4200
 3330 IF(ARGCNT.EQ.0)GOTO 4200
      IF(ARGCNT.NE.1)GOTO 3630
      CURPOS=ARGBGN
      HOLD=INPBUF(ARGEND+1)
      INPBUF(ARGEND+1)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGEND+1)=HOLD
      IF(VAL.EQ.99999.OR.VAL.LT.0.OR.RELBAS.GT.1)GOTO 3630
      OBJCNT=2
      OBJECT(1)=IAND(VAL/256,255)
      OBJECT(2)=IAND(VAL,255)
      IF(PASS.EQ.2.AND.IAND(OPTION,OPTOU).NE.0)CALL OBJOUT(1,VAL,0,0)
      GOTO 4200
C
C     PROCESS NAME/TITLE COMMAND
C
 3340 IF(ARGCNT.NE.1)GOTO 3630
      IF(LABEL(1).EQ.0)GOTO 3345
      ERRORS=IOR(ERRORS,ERRL)
 3345 CURPOS=ARGBGN
      CALL NXTCHR(CH,TY)
      PTR=1
      IF(CH.EQ.1H')GOTO 3350
      IF(CH.EQ.1H")GOTO 3360
      GOTO 3630
 3350 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H')GOTO 3352
 3351 IF(PTR.GT.80)GOTO 3350
      TEXT(PTR)=CH
      PTR=PTR+1
      GOTO 3350
 3352 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H')GOTO 3351
      IF(CH.NE.1H .AND.CH.NE.1H;.AND.CH.NE.1H!.AND.CH.NE.KEOC)GOTO 3630
      GOTO 3370
 3360 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H")GOTO 3362
 3361 IF(PTR.GT.80)GOTO 3360
      TEXT(PTR)=CH
      PTR=PTR+1
      GOTO 3360
 3362 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H")GOTO 3361
      IF(CH.NE.1H .AND.CH.NE.1H;.AND.CH.NE.1H!.AND.CH.NE.KEOC)GOTO 3630
 3370 DO 3375 I=1,80
 3375 TITLE(I)=BLANK
      DO 3380 I=1,PTR-1
 3380 TITLE(I)=TEXT(I)
      LOLINE=LOLIM+1
      GOTO 4000
C
C     PROCESS PAGE COMMAND
C
 3390 IF(ARGCNT.NE.0)GOTO 3630
      IF(LABEL(1).EQ.0)GOTO 3395
      ERRORS=IOR(ERRORS,ERRL)
 3395 LOLINE=LOLIM+1
      GOTO 4000
C
C     PROCESS SKIP COMMAND
C
 3400 IF(ARGCNT.GT.1)GOTO 3630
      IF(LABEL(1).EQ.0)GOTO 3405
      ERRORS=IOR(ERRORS,ERRL)
 3405 VAL=1
      IF(ARGCNT.EQ.0)GOTO 3410
      CURPOS=ARGBGN
      HOLD=INPBUF(ARGEND+1)
      INPBUF(ARGEND+1)=KEOC
      CALL EVE(VAL)
 3410 IF(VAL.EQ.99999.OR.VAL.LT.0.OR.RELBAS.NE.0)GOTO 3630
      VAL=IAND(VAL,63)
      GOTO 4000
C
C     PROCESS INCL COMMAND
C
 3420 IF(ARGCNT.NE.1)GOTO 3630
      IF(LABEL(1).EQ.0)GOTO 3425
      ERRORS=IOR(ERRORS,ERRL)
 3425 CURPOS=ARGBGN
      LABEL(1)=0
      LABEL(2)=0
      CALL NXTCHR(CH,TY)
      IF(TY.NE.1)GOTO 3630
      CALL PACK(LABEL(1),CH)
 3430 CALL NXTCHR(CH,TY)
      IF(TY.NE.1.AND.TY.NE.2)GOTO 3440
      CALL PACK(LABEL(1),CH)
      GOTO 3430
 3440 IF(CH.NE.1H .AND.CH.NE.1H;.AND.CH.NE.1H!.AND.CH.NE.KEOC)GOTO 3630
      IF(IUNIT.NE.SIUNIT.AND.IUNIT.NE.TMUNIT)GOTO 3630
      DO 3445 I=1,7
 3445 CALL PACK(LABEL(1),BLANK)
      WRITE(FILENM,900)LABEL
      FILEAC='        '
      I=0
 3450 OPEN(INUNIT,NAME=FILENM,STATUS='OLD',ACCESS='SEQUENTIAL',FORM=
     1  'FORMATTED',ACCOUNT=FILEAC,ERR=3460,USAGE='INPUT')
      IUSTK(1)=IUNIT
      LNSTK(1)=LNCNT
      GOTO 4200
 3460 I=I+1
      IF(I.GT.8.OR.SRACCT(1,I).EQ.0)GOTO 3470
      WRITE(FILEAC,900)SRACCT(1,I),SRACCT(2,I)
      GOTO 3450
 3470 IF(PASS.NE.0)GOTO 4200
      WRITE(LOUNIT,906)
      WRITE(LOUNIT,905)
      STOP
C
C     PROCESS IF STATEMENT
C
 3480 IF(ARGCNT.NE.1)GOTO 3630
      CURPOS=ARGBGN
      HOLD=INPBUF(ARGEND+1)
      INPBUF(ARGEND+1)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGEND+1)=HOLD
      IF(VAL.EQ.99999.OR.VAL.LT.0.OR.RELBAS.NE.0)GOTO 3630
      IF(VAL.NE.0)GOTO 4200
      FLAGIF=1
      GOTO 4200
C
C     PROCESS ELSE STATEMENT
C
 3490 IF(ARGCNT.NE.0)GOTO 3630
      FLAGIF=1
      GOTO 4200
C
C     PROCESS FI STATEMENT
C
 3500 IF(ARGCNT.NE.0)GOTO 3630
      FLAGIF=0
      GOTO 4200
C
C     PROCESS GOTO STATEMENT
C
 3510 IF(ARGCNT.NE.1)GOTO 3630
      GO2LBL(1)=0
      GO2LBL(2)=0
      CURPOS=ARGBGN
      CALL NXTCHR(CH,TY)
      IF(TY.NE.1)GOTO 3630
      CALL PACK(GO2LBL(1),CH)
 3520 CALL NXTCHR(CH,TY)
      IF(TY.NE.1.AND.TY.NE.2)GOTO 3530
      CALL PACK(GO2LBL(1),CH)
      GOTO 3520
 3530 IF(CH.NE.1H .AND.CH.NE.1H;.AND.CH.NE.1H!.AND.CH.NE.KEOC)GOTO 3630
      GOTO 4200
C
C     PROCESS LIST STATEMENT
C
 3540 IF(ARGCNT.NE.0)GOTO 3550
      FLAGLS=1
      GOTO 4000
 3550 IF(ARGCNT.NE.1)GOTO 3630
      CURPOS=ARGBGN
      HOLD=INPBUF(ARGEND+1)
      INPBUF(ARGEND+1)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGEND+1)=HOLD
      IF(VAL.EQ.99999.OR.VAL.LT.0.OR.RELBAS.NE.0)GOTO 3630
      FLAGLS=IAND(VAL,15)
      GOTO 4000
C
C     PROCESS NLIST STATEMENT
C
 3560 IF(ARGCNT.NE.0)GOTO 3630
      FLAGLS=0
      GOTO 4200
C
C     PROCESS MAC STATEMENT
C
 3570 IF(IUNIT.EQ.MCUNIT.OR.DIDMAC.NE.0)GOTO 3630
      FLAGMC=1
      IF(LABEL(1).NE.0)GOTO 3580
      ERRORS=IOR(ERRORS,ERRL)
      GOTO 4200
 3580 DO 3585 I=1,7
 3585 CALL PACK(LABEL(1),BLANK)
      DO 3590 I=1,OPLIM
      IF(LABEL(1).EQ.OPTBL(1,I).AND.LABEL(2).EQ.OPTBL(2,I))GOTO 3610
 3590 CONTINUE
      IF(PASS.EQ.0)GOTO 3595
      WRITE(LOUNIT,907)
      WRITE(LOUNIT,905)
      STOP
 3595 OPLIM=OPLIM+1
      IF(OPLIM.LE.200)GOTO 3600
      WRITE(LOUNIT,908)
      WRITE(LOUNIT,905)
      STOP
 3600 OPTBL(1,OPLIM)=LABEL(1)
      OPTBL(2,OPLIM)=LABEL(2)
      OPTYPE(OPLIM)=101
      OPBASE(OPLIM)=MCREC
      GOTO 3612
 3610 IF(PASS.GT.0)GOTO 4200
      ERRORS=IOR(ERRORS,ERRD)
      GOTO 4200
 3612 DO 3613 I=1,256
 3613 LOCBUF(I)=BLANK
      I=0
      IF(ARGCNT.LT.1)GOTO 3619
      CURPOS=ARGBGN
 3614 I=I+1
      J=I+7
      IF(I.GT.256)GOTO 3618
      CALL NXTCHR(CH,TY)
      IF(TY.NE.1)GOTO 3618
      LOCBUF(I)=CH
      I=I+1
 3615 CALL NXTCHR(CH,TY)
      IF(TY.NE.1.AND.TY.NE.2)GOTO 3616
      IF(I.GT.J)GOTO 3615
      LOCBUF(I)=CH
      I=I+1
      GOTO 3615
 3616 I=J
      IF(CH.EQ.1H,)GOTO 3614
      IF(CH.EQ.1H;.OR.CH.EQ.1H .OR.CH.EQ.1H!.OR.CH.EQ.KEOC)GOTO 3619
 3618 FLAGMC=0
      GOTO 3630
 3619 IF(PASS.NE.0)GOTO 4200
      WRITE(MCUNIT,910,REC=MCREC)LOCBUF
      MCREC=MCREC+1
      GOTO 4200
C
C     PROCESS MEND STATEMENT
C
 3620 IF(LABEL(1).NE.0.OR.ARGCNT.NE.0)GOTO 3630
      IF(IUNIT.NE.MCUNIT)GOTO 4200
      I=11
 3625 I=I-1
      IF(IUSTK(I).EQ.0)GOTO 3625
      IUNIT=IUSTK(I)
      LNCNT=LNSTK(I)
      IF(IUNIT.EQ.MCUNIT)MCREC=LNSTK(I)
      IUSTK(I)=0
      LNSTK(I)=0
      GOTO 2000
C
C     SYNTACTICAL ERROR
C
 3630 ERRORS=IOR(ERRORS,ERRS)
      GOTO 4200
C
C     RESERVE FOUR BYTES OF ZEROS FOR BAD OPCODES
C
 3640 OBJECT(1)=0
      OBJECT(2)=0
      OBJECT(3)=0
      OBJECT(4)=0
      OBJCNT=4
      GOTO 4200
C
C     PROCESS A MACRO INVOCATION
C
 3650 RELOC=0
      IF(IAND(OPTION,OPTREL).NE.0)RELOC=1
      RELBAS=0
      IF(LOCCTR.GE.256)RELBAS=1
      PTR=DULBL(LOCCTR)
      RELOC=0
      RELBAS=0
 3660 I=0
 3665 I=I+1
      IF(I.LE.10)GOTO 3670
      ERRORS=IOR(ERRORS,ERRM)
      GOTO 4200
 3670 IF(IUSTK(I).NE.0)GOTO 3665
      DIDMAC=1
      IUSTK(I)=IUNIT
      LNSTK(I)=LNCNT
      MCREC=OPBASE(OPIDX)
      DO 3671 PTR=1,256
 3671 MCHEAD(PTR,I)=INPBUF(PTR)
      READ(MCUNIT,910,REC=MCREC,ERR=3680)INPBUF
      MCREC=MCREC+1
      CURPOS=1
      ARS=256
      DO 3673 PTR=1,32
      MCDUM(1,PTR,I)=0
      MCDUM(2,PTR,I)=0
      DO 3672 J=1,8
      CALL NXTCHR(CH,TY)
 3672 CALL PACK(MCDUM(1,PTR,I),CH)
 3673 CONTINUE
      DO 3674 PTR=1,256
 3674 INPBUF(PTR)=MCHEAD(PTR,I)
      DO 3675 PTR=1,32
      MCPTR(PTR,I)=0
 3675 MCLEN(PTR,I)=0
      IF(ARGCNT.EQ.0)GOTO 4200
      CURPOS=ARGBGN
      PTR=0
 3676 PTR=PTR+1
      IF(PTR.GT.32)GOTO 3630
      MCPTR(PTR,I)=CURPOS
 3677 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H')GOTO 3678
      IF(CH.EQ.1H")GOTO 3679
      IF(CH.NE.1H,.AND.CH.NE.1H .AND.CH.NE.1H!.AND.CH.NE.1H;.AND.
     1  CH.NE.KEOC)GOTO 3677
      MCLEN(PTR,I)=CURPOS-MCPTR(PTR,I)-1
      IF(CH.EQ.1H,)GOTO 3676
      GOTO 4200
 3678 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H')GOTO 3677
      IF(CH.NE.KEOC)GOTO 3678
      GOTO 3630
 3679 CALL NXTCHR(CH,TY)
      IF(CH.EQ.1H")GOTO 3677
      IF(CH.NE.KEOC)GOTO 3679
      GOTO 3630
 3680 WRITE(LOUNIT,909)
      WRITE(LOUNIT,905)
      STOP
C
C     PROCESS OPTS STATEMENT
C
 3690 IF(LABEL(1).NE.0.OR.ARGCNT.LE.0)GOTO 3630
      CALL STOWOP(INPBUF(ARGBGN),ARGEND-ARGBGN+1)
      GOTO 4200
C
C     <LIST> EXIT
C
 4000 IF(PASS.NE.0.OR.FLAGMC.EQ.0)GOTO 4010
      WRITE(MCUNIT,910,REC=MCREC)INPBUF
      MCREC=MCREC+1
 4010 IF(OPTYPE(OPIDX).EQ.43)FLAGMC=0
      IF(PASS.NE.0)GOTO 4020
      IF(IUNIT.EQ.SIUNIT)WRITE(TMUNIT,910)INPBUF
      LOCCTR=LOCCTR+OBJCNT
      IF(OPTYPE(OPIDX).NE.31)GOTO 4950
      RETURN
 4020 IF(PASS.NE.2)GOTO 4900
      FLAG=BLANK
      IF(IUNIT.EQ.INUNIT)FLAG=XMINS
      IF(IUNIT.EQ.MCUNIT)FLAG=XPLUS
      IF(ERRORS.NE.0)GOTO 4300
      IF(OPTYPE(OPIDX).NE.34)GOTO 4030
      LOLINE=LOLINE+VAL
      IF(LOLINE.GE.LOLIM.OR.VAL.EQ.0)GOTO 4030
      DO 4025 I=1,VAL
 4025 WRITE(LOUNIT,911)
 4030 IF(IAND(FLAGLS,8).NE.0)GOTO 4300
      GOTO 4500
C
C     <SKIPPED> EXIT
C
 4100 IF(PASS.NE.0.OR.FLAGMC.EQ.0)GOTO 4110
      WRITE(MCUNIT,910,REC=MCREC)INPBUF
      MCREC=MCREC+1
 4110 IF(OPTYPE(OPIDX).EQ.43)FLAGMC=0
      IF(PASS.NE.0)GOTO 4120
      IF(IUNIT.EQ.SIUNIT)WRITE(TMUNIT,910)INPBUF
      LOCCTR=LOCCTR+OBJCNT
      IF(OPTYPE(OPIDX).NE.31)GOTO 4950
      RETURN
 4120 IF(PASS.NE.2)GOTO 4900
      FLAG=XASTR
      IF(ERRORS.NE.0.OR.IAND(FLAGLS,8).NE.0)GOTO 4300
      GOTO 4500
C
C     <DONE> EXIT
C
 4200 IF(PASS.NE.0.OR.FLAGMC.EQ.0)GOTO 4210
      WRITE(MCUNIT,910,REC=MCREC)INPBUF
      MCREC=MCREC+1
 4210 IF(OPTYPE(OPIDX).EQ.43)FLAGMC=0
      IF(PASS.NE.0)GOTO 4220
      IF(IUNIT.EQ.SIUNIT)WRITE(TMUNIT,910)INPBUF
      LOCCTR=LOCCTR+OBJCNT
      IF(OPTYPE(OPIDX).NE.31)GOTO 4950
      RETURN
 4220 IF(PASS.NE.2)GOTO 4900
      FLAG=BLANK
      IF(IUNIT.EQ.INUNIT)FLAG=XMINS
      IF(IUNIT.EQ.MCUNIT)FLAG=XPLUS
      IF(ERRORS.NE.0)GOTO 4300
      IF(IUNIT.EQ.INUNIT.AND.IAND(FLAGLS,2).EQ.0)GOTO 4500
      IF(IUNIT.EQ.MCUNIT.AND.IAND(FLAGLS,4).EQ.0)GOTO 4500
      IF(IUNIT.EQ.TMUNIT.AND.IAND(FLAGLS,1).EQ.0)GOTO 4500
C
C     GENERATE LISTING
C
 4300 IF(PASS.NE.2.OR.(IAND(OPTION,OPTLS).EQ.0.AND.ERRORS.EQ.0))
     1  GOTO 4500
      DO 4305 I=1,33
 4305 LOBUF(I)=BLANK
      CALL CVA(LOBUF(1),ERRORS)
      CURPOS=1
      DO 4310 I=9,28
 4310 LOBUF(I)=0
      I=9
      CALL PACK(LOBUF(9),FLAG)
      J=3
 4320 IF(J.GT.0)GOTO 4325
      J=4
      I=I+1
 4325 IF(CURPOS.GT.80)GOTO 4330
      CALL PACK(LOBUF(I),INPBUF(CURPOS))
      CURPOS=CURPOS+1
      J=J-1
      GOTO 4320
 4330 CALL CVD(LOBUF(8),LNCNT)
      IF(ITIME.EQ.0.OR.CLOCK.EQ.0)GOTO 4400
C
C     TIMING CALCULATIONS
C
      CALL CVD(J,ITIME*10000/CLOCK)
      LOBUF(29)=0
      CALL PACK(LOBUF(29),J)
      CALL PACK(LOBUF(29),ISL(J,NBITS))
      CALL PACK(LOBUF(29),XPERD)
      CALL PACK(LOBUF(29),ISL(J,NBITS2))
      LOBUF(30)=ISL(J,NBITS3)+ISL(BLANK,MBITS)
      IF(JTIME.EQ.0)GOTO 4400
      ITIME=JTIME
      CALL CVD(J,ITIME*10000/CLOCK)
      LOBUF(30)=ISL(ISL(LOBUF(30),MBITS3),NBITS3)
      CALL PACK(LOBUF(30),XSLSH)
      CALL PACK(LOBUF(30),J)
      CALL PACK(LOBUF(30),ISL(J,NBITS))
      LOBUF(31)=0
      CALL PACK(LOBUF(31),XPERD)
      CALL PACK(LOBUF(31),ISL(J,NBITS2))
      CALL PACK(LOBUF(31),ISL(J,NBITS3))
      CALL PACK(LOBUF(31),BLANK)
 4400 OBJPTR=1
      IF(OBJCNT.EQ.0)GOTO 4410
      J=LOCCTR
      IF(OPTYPE(OPIDX).NE.31)GOTO 4430
      OBJCNT=0
      J=256*OBJECT(1)+OBJECT(2)
      GOTO 4430
 4410 IF(FLAG.EQ.XASTR.OR.FLAGMC.NE.0)GOTO 4460
      IF(OPTYPE(OPIDX).NE.24.AND.OPTYPE(OPIDX).NE.26)GOTO 4420
      J=VAL
      GOTO 4430
 4420 IF(OPTYPE(OPIDX).NE.27)GOTO 4425
      J=LOCCTR
      GOTO 4430
 4425 IF(OPTYPE(OPIDX).NE.28)GOTO 4460
      J=LOCCTR-VAL
 4430 IF(IAND(OPTION,OPTOC).NE.0)GOTO 4470
C
C     OBJECT CODE IS IN HEX
C
 4435 CALL CVH(LOBUF(2),J)
      PTR=3
      LEN=3
      I=1
 4440 IF(OBJPTR.GT.OBJCNT)GOTO 4460
      IF(I.GT.8)GOTO 4460
      CALL CVH(K,OBJECT(OBJPTR))
      GOTO(4441,4442,4443,4444),LEN
 4441 LOBUF(PTR)=ISL(ISL(LOBUF(PTR),MBITS),NBITS)+ISL(ISL(K,NBITS2),
     1  MBITS3)
      PTR=PTR+1
      LOBUF(PTR)=ISL(K,NBITS3)+ISL(BLANK,MBITS)
      LEN=3
      GOTO 4450
 4442 LOBUF(PTR)=ISL(ISL(LOBUF(PTR),MBITS2),NBITS2)+ISL(ISL(K,
     1  NBITS2),MBITS2)
      PTR=PTR+1
      LEN=4
      GOTO 4450
 4443 LOBUF(PTR)=ISL(ISL(LOBUF(PTR),MBITS3),NBITS3)+ISL(ISL(K,
     1  NBITS2),MBITS)+ISL(BLANK,MBITS3)
      LEN=1
      GOTO 4450
 4444 LOBUF(PTR)=ISL(K,NBITS2)+ISL(BLANK,MBITS2)
      LEN=2
 4450 OBJPTR=OBJPTR+1
      I=I+1
      IF(I.EQ.5)LEN=2
      GOTO 4440
 4460 IF(RELOC.EQ.0.OR.LOBUF(8).EQ.BLANK)GOTO 4462
      IF(RELBAS.EQ.1)LOBUF(7)=ISL(ISL(LOBUF(7),MBITS2),NBITS2)+
     1  ISL(XRCON,MBITS2)
      IF(RELBAS.NE.1)LOBUF(7)=ISL(ISL(LOBUF(7),MBITS2),NBITS2)+
     1  ISL(XXCON,MBITS2)
 4462 CALL LIST
      DO 4465 I=1,33
 4465 LOBUF(I)=BLANK
      J=J+8
      IF(OBJPTR.LE.OBJCNT)GOTO 4435
      GOTO 4500
C
C     OBJECT CODE IS IN OCTAL
C
 4470 J2=J
      IF(IAND(OPTION,OPTAO).NE.0)J2=IAND(J,255)+512*(J/256)
      CALL CVO(I1,I2,J2)
      LOBUF(2)=ISL(I1,NBITS2)+ISL(I2,MBITS2)
      LOBUF(3)=ISL(I2,NBITS2)+ISL(BLANK,MBITS2)
      I=1
 4475 IF(OBJPTR.GT.OBJCNT)GOTO 4490
      IF(I.GT.4)GOTO 4490
      CALL CVO(I1,I2,OBJECT(OBJPTR))
      PTR=I+2
      LOBUF(PTR)=ISL(ISL(LOBUF(PTR),MBITS2),NBITS2)+ISL(ISL(BLANK,
     1 MBITS3),NBITS)+ISL(ISL(I2,NBITS),MBITS3)
      LOBUF(PTR+1)=ISL(I2,NBITS2)+ISL(BLANK,MBITS2)
      OBJPTR=OBJPTR+1
      I=I+1
      GOTO 4475
 4490 IF(RELOC.EQ.0.OR.LOBUF(8).EQ.BLANK)GOTO 4492
      IF(IAND(RELBAS,127).LE.1)LOBUF(7)=ISL(ISL(LOBUF(7),MBITS2),
     1  NBITS2)+ISL(XRCON,MBITS2)
      IF(IAND(RELBAS,127).GT.1)LOBUF(7)=ISL(ISL(LOBUF(7),MBITS2),
     1  NBITS2)+ISL(XXCON,MBITS2)
 4492 CALL LIST
      DO 4495 I=1,33
 4495 LOBUF(I)=BLANK
      J=J+4
      IF(OBJPTR.LE.OBJCNT)GOTO 4470
C
C     WRITE OUT OBJECT CODE HERE
C
 4500 IF(PASS.EQ.2.AND.OBJCNT.GT.0.AND.IAND(OPTION,OPTOU).NE.0.AND.
     1  OPTYPE(OPIDX).NE.31)
     2  CALL OBJOUT(IAND(OPTION,OPTREL),LOCCTR,OBJCNT,OBJECT(1))
 4900 LOCCTR=LOCCTR+OBJCNT
      IF(OPTYPE(OPIDX).NE.31)GOTO 4950
      RETURN
 4950 IF(ERRORS.NE.0.OR.FLAGMC.NE.0)GOTO 2000
      IF(OPTYPE(OPIDX).NE.35)GOTO 4960
      LNCNT=0
      IUNIT=INUNIT
      GOTO 2000
 4960 IF(OPTYPE(OPIDX).NE.101)GOTO 2000
      LNCNT=MCREC
      IUNIT=MCUNIT
      GOTO 2000
C
C     UNEXPECTED EOF ON INPUT DCB
C
 5000 IF(IUNIT.NE.SIUNIT)GOTO 5100
      DO 5010 I=1,256
 5010 INPBUF(I)=BLANK
      DO 5020 I=1,20
 5020 INPBUF(I)=ENDCMD(I)
      ARS=20
      GOTO 2020
C
C     I/O ERROR ON SOME DCB
C
 5100 IF(IUNIT.EQ.SIUNIT)WRITE(LOUNIT,901)
      IF(IUNIT.EQ.TMUNIT)WRITE(LOUNIT,902)
      IF(IUNIT.EQ.INUNIT)WRITE(LOUNIT,903)
      IF(IUNIT.EQ.XRUNIT)WRITE(LOUNIT,904)
      IF(IUNIT.EQ.MCUNIT)WRITE(LOUNIT,909)
      WRITE(LOUNIT,905)
      STOP
  900 FORMAT(2A4)
  901 FORMAT(' ***I/O ERROR ON SOURCE INPUT FILE***')
  902 FORMAT(' ***I/O ERROR ON INTERMEDIATE FILE***')
  903 FORMAT(' ***I/O ERROR ON INCLUDE FILE***')
  904 FORMAT(' ***I/O ERROR ON CROSS-REFERENCE FILE***')
  905 FORMAT(' ***ASSEMBLY TERMINATED***')
  906 FORMAT(' ***INCL FILE DOES NOT EXIST***')
  907 FORMAT(' ***INTERNAL ERROR--MACRO MISSING FROM OPCODE TABLE***')
  908 FORMAT(' ***MACRO TABLE OVERFLOW***')
  909 FORMAT(' ***INTERNAL ERROR--I/O ERROR ON MC FILE***')
  910 FORMAT(256A1)
  911 FORMAT(' ')
  912 FORMAT(I5,' ERROR(S), PASS',I2,', LINE',I5/)
  913 FORMAT(I5,' ERROR(S), PASS',I2,', LINE',I5,'+ (EXPANDING MACRO)'/)
  914 FORMAT(I5,' ERROR(S), PASS',I2,', LINE',I5,' OF INCL FILE ',A8,
     1  '.',A8/)
      END
      SUBROUTINE SCANOP(ARGTY,ARGVAL,ARGB,ARGE)
C
C     SCAN INPUT BUFFER (INPBUF) FROM ARGB TO ARGE AND DETERMINE THE
C     TYPE OF ARGUMENT PRESENT; RETURN THE ARGUMENT TYPE INDEX IN
C     ARGTY OR -1 IF AN ERROR OCCURS.  IF THERE IS A VALUE IN THE
C     ARGUMENT, RETURN IT IN ARGVAL.
C
      IMPLICIT INTEGER(A-Z)
      INCLUDE ASMZ80_C1
      ARGVAL=0
      IF(ARGE-ARGB)900,100,200
C
C     ONE-CHARACTER ARGUMENT FIELD
C
  100 CH=INPBUF(ARGB)
      ARGTY=1
      IF(CH.EQ.1HB)RETURN
      ARGTY=2
      IF(CH.EQ.1HC)RETURN
      ARGTY=3
      IF(CH.EQ.1HD)RETURN
      ARGTY=4
      IF(CH.EQ.1HE)RETURN
      ARGTY=5
      IF(CH.EQ.1HH)RETURN
      ARGTY=6
      IF(CH.EQ.1HL)RETURN
      ARGTY=8
      IF(CH.EQ.1HA)RETURN
      ARGTY=9
      IF(CH.EQ.1HI)RETURN
      ARGTY=10
      IF(CH.EQ.1HR)RETURN
      ARGTY=12
      IF(CH.EQ.1HZ)RETURN
      ARGTY=17
      IF(CH.EQ.1HP)RETURN
      ARGTY=18
      IF(CH.EQ.1HM)RETURN
      GOTO 800
C
C     MORE THAN ONE-CHARACTER IN AF
C
  200 IF(ARGE-ARGB.GT.1)GOTO 300
      CH1=INPBUF(ARGB)
      CH2=INPBUF(ARGE)
      ARGTY=11
      IF(CH1.EQ.1HN.AND.CH2.EQ.1HZ)RETURN
      ARGTY=13
      IF(CH1.EQ.1HN.AND.CH2.EQ.1HC)RETURN
      ARGTY=14
      IF(CH1.EQ.1HA.AND.CH2.EQ.1HF)RETURN
      ARGTY=15
      IF(CH1.EQ.1HP.AND.CH2.EQ.1HO)RETURN
      ARGTY=16
      IF(CH1.EQ.1HP.AND.CH2.EQ.1HE)RETURN
      ARGTY=19
      IF(CH1.EQ.1HB.AND.CH2.EQ.1HC)RETURN
      ARGTY=20
      IF(CH1.EQ.1HD.AND.CH2.EQ.1HE)RETURN
      ARGTY=21
      IF(CH1.EQ.1HH.AND.CH2.EQ.1HL)RETURN
      ARGTY=22
      IF(CH1.EQ.1HS.AND.CH2.EQ.1HP)RETURN
      ARGTY=23
      IF(CH1.EQ.1HI.AND.CH2.EQ.1HX)RETURN
      ARGTY=24
      IF(CH1.EQ.1HI.AND.CH2.EQ.1HY)RETURN
      GOTO 800
C
C     MORE THAN TWO-CHARACTERS IN AF
C
  300 ARGTY=37
      IF(INPBUF(ARGB).EQ.1HA.AND.INPBUF(ARGB+1).EQ.1HF.AND.
     1  INPBUF(ARGB+2).EQ.1H'.AND.ARGE-ARGB.EQ.2)RETURN
      IF(INPBUF(ARGB).NE.1H(.OR.INPBUF(ARGE).NE.1H))GOTO 800
      IF(ARGE-ARGB.GT.2)GOTO 400
      ARGTY=25
      IF(INPBUF(ARGB+1).EQ.1HC)RETURN
      GOTO 850
C
C     MORE THAN THREE-CHARACTERS IN AF, FIRST IS (, LAST IS )
C
  400 IF(ARGE-ARGB.GT.3)GOTO 500
      CH1=INPBUF(ARGB+1)
      CH2=INPBUF(ARGE-1)
      ARGTY=7
      IF(CH1.EQ.1HH.AND.CH2.EQ.1HL)RETURN
      ARGTY=26
      IF(CH1.EQ.1HB.AND.CH2.EQ.1HC)RETURN
      ARGTY=27
      IF(CH1.EQ.1HD.AND.CH2.EQ.1HE)RETURN
      ARGTY=28
      IF(CH1.EQ.1HI.AND.CH2.EQ.1HX)RETURN
      ARGTY=29
      IF(CH1.EQ.1HI.AND.CH2.EQ.1HY)RETURN
      ARGTY=30
      IF(CH1.EQ.1HS.AND.CH2.EQ.1HP)RETURN
      GOTO 850
C
C     MORE THAN FOUR-CHARACTERS IN AF, ENCLOSED IN ()
C
  500 IF(ARGE-ARGB.LT.5)GOTO 850
      IF(INPBUF(ARGB+1).NE.1HI)GOTO 850
      IF(INPBUF(ARGB+3).NE.1H+.AND.INPBUF(ARGB+3).NE.1H-)GOTO 850
      ARGTY=0
      IF(INPBUF(ARGB+2).EQ.1HX)ARGTY=31
      IF(INPBUF(ARGB+2).EQ.1HY)ARGTY=32
      IF(ARGTY.EQ.0)GOTO 850
      HOLDCH=INPBUF(ARGE)
      CURPOS=ARGB+3
      INPBUF(ARGE)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGE)=HOLDCH
      ARGVAL=VAL
      IF(VAL.EQ.99999)GOTO 900
      IF(VAL.GE.128.AND.VAL.LE.65407)ERRORS=IOR(ERRORS,ERRT)
      IF(RELBAS.GT.1)ERRORS=IOR(ERRORS,ERRR)
      RETURN
C
C     EVALUATE EXPRESSION, NOT IN ()
C
  800 ARGTY=33
      HOLDCH=INPBUF(ARGE+1)
      CURPOS=ARGB
      INPBUF(ARGE+1)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGE+1)=HOLDCH
      ARGVAL=VAL
      IF(VAL.EQ.99999)GOTO 900
      IF((VAL.GE.256.AND.VAL.LE.65407).OR.RELBAS.GT.127)
     1  ARGTY=34
      RETURN
C
C     EVALUATE EXPRESSION IN ()
C
  850 ARGTY=35
      HOLDCH=INPBUF(ARGE)
      CURPOS=ARGB+1
      INPBUF(ARGE)=KEOC
      CALL EVE(VAL)
      INPBUF(ARGE)=HOLDCH
      ARGVAL=VAL
      IF(VAL.EQ.99999)GOTO 900
      IF((VAL.GE.256.AND.VAL.LE.65407).OR.RELBAS.GT.127)
     1  ARGTY=36
      RETURN
C
C     RETURN ERROR
C
  900 ARGTY=-1
      RETURN
      END
