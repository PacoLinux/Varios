/*M* CALENDAR_SIN1 - PARSE NODE DEFINITIONS FOR CALENDAR
***/
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
 
/**/
/* PARSING NODES FOR CALENDAR  */
/**/
%INCLUDE XUD_UTS_M;
  %XUD_UTS_EQU;
%INCLUDE CALENDAR_C1;
/**/
/**/
/*  PARSING NODES FOR CALENDAR COMMANDS  */
/**/
ROOT_NODE(D) = (DISPLAY_CMD       |;
                SCHEDULE_CMD      |;
                CANCEL_CMD        |;
                CHANGE_CMD        |;
                CREATE_CMD        |;
                ATTACH_CMD        |;
                DETACH_CMD        |;
                GROUP_CMD         |;
                MEMBERS_CMD       |;
                QUIT_CMD            ) .END
/**/
/*  PARSING NODES FOR CALENDAR INPUTS */
/**/
INPUT_DATE_TIME(D) = (DATE_TIME   |;
                      DATE_X      |;
                      TIME_DATE   |;
                      QUIT_CMD       ) .END
INPUT_CANCEL(D) = (DATE_TIME      |;
                   DATE_TOPIC     |;
                   QUIT_CMD         ) .END
INPUT_CHANGE(D) = (DATE_TI_SUB    |;
                   DATE_TO_SUB    |;
                   QUIT_CMD         ) .END
INPUT_DATE(D) = (QUIT_CMD | DATE_X) .END
INPUT_TIME(D) = [(QUIT_CMD | TIME_X)] .END
INPUT_RP(D) = [(QUIT_CMD | NUM_X)] .END
INPUT_PLACE(D) = (QUIT_CMD .END | PLACE_X .END)
INPUT_NOTES(D) = (QUIT_CMD .END | .ASYM .END)
/**/
/* DISPLAY COMMAND */
/**/
DISPLAY_CMD(%DISPLAY_CMD) = (DISPLAY_DATE |;
                             DISPLAY_NUM    |;
                             DISPLAY_TOPIC    )
/**/
    DISPLAY_DATE(%DISPLAY_DATE)     = 'DI/SPLAY' [.B] DATE
    DISPLAY_NUM(%DISPLAY_NUM)       = 'DI/SPLAY' [.B] .D [.B]  ;
                                      [(DAYS|WEEKS)]
    DISPLAY_TOPIC(%DISPLAY_TOPIC)   = 'DI/SPLAY' [.B] TOPIC
/**/
/* SCHEDULE, CANCEL, AND CHANGE COMMANDS */
/**/
SCHEDULE_CMD(%SCHEDULE_CMD) = 'S/CHEDULE'
CANCEL_CMD(%CANCEL_CMD)     = 'CA/NCEL' [.B DATE (','|.B) ;
                              (TIME|TOPIC)]
CHANGE_CMD(%CHANGE_CMD)     = 'CH/ANGE' [.B DATE (','|.B) ;
                              (TIME|TOPIC1) ',' ;
                              (TI|PL|TO|RP|NO|DA) ]
/**/
/*  GROUP CALENDAR COMMANDS  */
/**/
    CREATE_CMD(%CREATE_CMD) = 'CR/EATE' .FID ',' .FID ['FOR'] ALIAS
    ATTACH_CMD(%ATTACH_CMD) = 'AT/TACH' ['IN'] .FID ['FOR'] ALIAS
    DETACH_CMD(%DETACH_CMD) = 'DE/TACH' ['IN'] [.FID]
    MEMBERS_CMD(%MEMBERS_CMD) = 'ME/MBERS' ['IN'] [.FID]
    GROUP_CMD(%GROUP_CMD) = 'G/ROUP' [RANGE_CLAUSE] [GROUP_CLAUSE]
/**/
/* HELP, QUIT, AND NULL COMMANDS */
/**/
QUIT_CMD(%QUIT_CMD) = ('Q/UIT'|'EX/IT'|'E/ND'|'X/IT')
 
/**/
/*  INPUTS FOR CHANGE COMMAND */
/**/
DATE_TI_SUB(%DATE_TI_SUB) = DATE (','|.B) TIME (','|.B) ;
                            (TI|PL|TO|RP|NO|DA)
DATE_TO_SUB(%DATE_TO_SUB) = DATE (','|.B) TOPIC1 ',' ;
                            (TI|PL|TO|RP|NO|DA)
/**/
/*  INPUTS FOR CANCEL COMMAND  */
/**/
DATE_TIME(%DATE_TIME) = DATE (','|.B) TIME
DATE_TOPIC(%DATE_TOPIC) = DATE (','|.B) TOPIC
/**/
/*  INPUTS FOR SCHEDULE COMMAND */
/**/
DATE_X(%DATE_X) = DATE
TIME_DATE(%TIME_DATE) = TIME (','|.B) DATE
NUM_X(%NUM_X) = .DEC [.B] ['D/AYS']
PLACE_X(%PLACE_X) = ( .QUOTE | .ASYM )
TIME_X(%TIME_X) = .DEC ':' .DEC
/**/
/*  GROUP IDENTIFIER CLAUSES  */
/**/
    RANGE_CLAUSE(%RANGE_CLAUSE) = (DATE_ZERO      |;
                                   DATE_ONE       |;
                                   DATE_TWO       |;
                                   RANGE_NUM      |;
                                   RANGE_TOMO     )
    RANGE_NUM(%RANGE_NUM)   = .D [(DAYS|WEEKS)]
    RANGE_TOMO(%RANGE_TOMO) = 'TOMO/RROW'
/**/
    GROUP_CLAUSE(%GROUP_CLAUSE) = (NAME_FIRST|ALIAS_FIRST)
    NAME_FIRST = NAME_CLAUSE [ALIAS_CLAUSE]
    ALIAS_FIRST = ALIAS_CLAUSE [NAME_CLAUSE]
    ALIAS_CLAUSE(%ALIAS_CLAUSE) = 'FOR' <',',ALIAS>
    NAME_CLAUSE(%NAME_CLAUSE) = 'IN' .FID
    ALIAS = .AN+'.'
/**/
/*  COMMON ELEMENTS  */
/**/
    DATE(%DATE#) = (DATE_ZERO|DATE_ONE|DATE_TWO|DATE_THREE|DATE_FOUR)
         DATE_ZERO(%DATE_ZERO) = DATE_ONE [.B] ('TO'|'-') ;
                                 [.B] DATE_ONE
         DATE_ONE(%DATE_ONE) = .DEC '/' .DEC ['/' .DEC]
         DATE_TWO(%DATE_TWO) = ['NEXT' .B] (SAT|MON|TUE|WED|THU|FRI|SUN)
         DATE_THREE(%DATE_THREE) = 'EVERY' [.B] (SAT|MON|TUE|WED|THU| ;
                                   FRI|SUN) [.B] ['FOR'] [.B]  ;
                                   .DEC [(WEEKS|MONTHS)]
         DATE_FOUR(%DATE_FOUR) = 'TOMO/RROW'
TOPIC(%TOPIC) = ( .QUOTE | .ASYM )
TOPIC1(%TOPIC) = ( .QUOTE | .ASYM+',' )
TIME(%TIME#) = .DEC ':' .DEC
/**/
DAYS(%DAYS) = 'D/AYS'
WEEKS(%WEEKS) = 'W/EEKS'
MONTHS(%MONTHS) = 'M/ONTHS'
/**/
SUN(%SUN) = 'SUN/DAY'
MON(%MON) = 'MON/DAY'
TUE(%TUE) = 'TUE/SDAY'
WED(%WED) = 'WED/NESDAY'
THU(%THU) = 'THU/RSDAY'
FRI(%FRI) = 'FRI/DAY'
SAT(%SAT) = 'SAT/URDAY'
/**/
TI(%TI) = 'T/IME'
PL(%PL) = 'P/LACE'
TO(%TO) = 'E/VENT'
RP(%RP) = 'R/EMINDER' 'P/ERIOD'
NO(%NO) = 'N/OTES'
DA(%DA) = 'D/ATE'
