/*M* A_SIP1 - Nodes for "A" program. */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X* DMC,DFC */
 
%INCLUDE A_C1;
 
A@NODES(%A@ROOT@,D) = ( < ';' , A@CMDS > .END | NULL@CMD .END )
 
A@CMDS = [.B] ( CANCEL@CMD | ;
                BATCH@CMD | ;
                DELETE@CMD | ;
                EVERY@CMD | ;
                END@CMD | ;
                IBEX@CMD | ;
                LDTRC@CMD | ;
                LINK@CMD | ;
                ALL@CMD | ;
                ACCOUNT@CMD | ;
                MINE@CMD | ;
                WAIT@CMD | ;
                REPORT@CMD | ;
                GO@CMD | ;
                NDELETE@CMD | ;
                SYSID@CMD )
 
END@CMD(%END@) = ( 'EN/D' | ;
                   'E/XIT' | ;
                   'Q/UIT' | ;
                   'X' )
 
WAIT@CMD(%WAIT@) = ( 'W/AIT' | 'S/LEEP' )  WAIT@TYPE
 
     WAIT@TYPE = [ ( WAIT@DONE | WAIT@PRINT ) ] [ WAIT@DEC ]
 
          WAIT@DONE(%WAIT_DONE@) = 'D/ONE'
 
          WAIT@PRINT(%WAIT_PRINT@) = 'P/RINT'
 
          WAIT@DEC(%WAIT_DEC@) = .DEC
 
REPORT@CMD(%REPORT@) = 'R/EPORT'
 
CANCEL@CMD(%CANCEL@) = 'C/ANCEL' .B SYSID@TYPE [ .B .ASYM+';' ]
 
NDELETE@CMD(%NDELETE@) = 'ND/ELETE'
 
DELETE@CMD(%DELETE@) = 'DEL/ETE'
 
SYSID@CMD(%SYSID@) = SYSID@TYPE [ .B .ASYM+';' ]
 
     SYSID@TYPE = ( SYSID@NAME | ;
                    SYSID@LAST | ;
                    SYSID@ME | ;
                    SYSID@DECIMAL )
 
          SYSID@NAME(%SYSID_NAME@) = 'N/AME' '=' JASYM [ '.'- JASYM ]
 
               JASYM = ( .SYM+':-' | .DEC )
 
          SYSID@LAST(%SYSID_LAST@) = 'L/AST'
 
          SYSID@ME(%SYSID_ME@) = 'M/E'
 
          SYSID@DECIMAL(%SYSID_DECIMAL@) = .DEC
 
GO@CMD(%GO@) = 'G/O'
 
IBEX@CMD(%IBEX@) = ( '!' | '$' ) .ASYM+';'
 
LDTRC@CMD(%LDTRC@) = '@@' .FID [ .B .ASYM+';' ]
 
LINK@CMD(%LINK@) = '@' .FID [ .B .ASYM+';' ]
 
BATCH@CMD(%BATCH@,SUCC) = 'BATCH' .FID [ ( '(' BOPTS ')' | BOPTS ) ]
 
     BOPTS = < ',' , ( XX@REP | ;
                       REP@REP | ;
                       TEST@SPEC | ;
                       ANY@SPEC ) >
 
          XX@REP(%BATCHX@) = ( '''X/X'- '''' | 'X/X' ) '=' ( .QUOTE | .FID )
 
          REP@REP(%BATCHR@) = ( .QUOTE | .FID ) '=' ( .QUOTE | .FID )
 
          TEST@SPEC(%BATCHT@) = 'T/EST'
 
          ANY@SPEC(%BATCHA@) = .FID
 
ALL@CMD(%ALL@) = 'AL/L'
 
MINE@CMD(%MINE@) = ( 'MI/NE' | 'MY' )
 
EVERY@CMD(%EVERY@) = 'EV/ERY' WAIT@TYPE
 
ACCOUNT@CMD(%ACCOUNT@) = ['A/CCOUNT' '='] '.' [JASYM]
 
NULL@CMD(%GO@) = ( .B | [.B] )
