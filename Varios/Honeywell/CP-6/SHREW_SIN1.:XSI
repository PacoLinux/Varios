/*M* SHREW_SIN1 - Parse Nodes for SHREW */
/*T***********************************************************/
/*T*                                                         */
/*T* COPYRIGHT, (C) HONEYWELL INFORMATION SYSTEMS INC., 1985 */
/*T*                                                         */
/*T***********************************************************/
/*  Contributed by
        Grand Valley State College  -  J.Lance Wilkinson
         Although this program has been tested by its contributor,
         no warranty, express or implied, is made by the
         contributor or Grand Valley State College, as to the
         accuracy and functioning of the program and related
         program materials, nor shall the fact of distribution
         constitute any such warranty, and no responsibility is
         assumed by the contributor or Grand Valley State College.
 
         The SHREW tool as submitted herein is based upon the WEASEL.X
         tool from Bull HN Information Systems.  The original copyright
         notices are therefore incorporated.
*/
 
/*X* */
/**/
%INCLUDE SHREW_C600;
%INCLUDE EL_SUBS_C;
/**/
ELF_CMD(D) = [.B] (USE_CMD         |;
                  SPAN_CMD         |;
                  TYPE_CMD         |;
                  REPORT_CMD       |;
                  RESET_CMD        |;
                  END_CMD          |;
                  NULL_CMD         );
                  .END
/**/
USE_CMD(%USE_CMD) = 'USE' .B '.'- [(.SYMBOL+':*-'|.DECIMAL|.QUOTE)]
/**/
SPAN_CMD(%SPAN_CMD)   = ['SP/AN' .B] (TIME_N | TIME_X | TODAY)
  TIME_N(%TIME_N)    = (START_TIME STOP_TIME|START_TIME|STOP_TIME)
    START_TIME(%START) = ['FROM'] TIME_SPEC
    STOP_TIME (%STOP)  = [.B] ('-'|'TO') [.B] TIME_SPEC
      TIME_SPEC = <(','|.B),{DATE|TIME}>
      DATE(%DATE_P) = .DEC '/' .DEC ['/' .DEC]
      TIME(%TIME_P) = .DEC [':' .DEC]
  TIME_X(%TIME_X) = 'LAST' .B .DEC [.B] (DAYS|WEEKS|MONTHS|YEAR| ;
                                         MINUTES|HOURS)
    DAYS(%DAYS) = 'D/AYS'
    WEEKS(%WEEKS) = 'W/EEKS'
    MONTHS(%MONTHS) = 'MO/NTHS'
    YEAR(%YEAR) = 'Y/EARS'
    MINUTES(%MINUTES) = 'MI/NUTES'
    HOURS(%HOURS) = 'H/OURS'
  TODAY(%TODAY) = 'TODAY'
/**/
TYPE_CMD(%TYPE_CMD,D) = 'TYPE' .B (TYPE_LIST | ALL)
  ALL(%ALL) = 'A/LL'
  TYPE_LIST = <(','|.B),NUMERIC_TYPE>
     NUMERIC_TYPE(600) = .DEC3 [.QUOTE20]
RESET_CMD(%RESET_CMD) = 'RES/ET' [.B (ALL|RESET_LIST)]
  RESET_LIST = <(','|.B),RESET_TYPE>
    RESET_TYPE = (USE|SPAN|TYPE)
      USE(%USE) = 'U/SE'
      SPAN(%SPAN) = 'S/PAN'
      TYPE(%TYPE) = 'T/YPE'
/**/
REPORT_CMD(%REPORT_CMD) = 'RE/PORT' .B REP_CLAUSE1
  REP_CLAUSE1 = REP_OPT [.B FOR_CLAUSE] [.B ON_CLAUSE] [.B SUM_CLAUSE]
    REP_OPT(L=%RAW) = ('RAW'|'DET/AIL'|'SUM'|'DUMP'| ;
                  'DIST/RIBUTION' '(' DIST_CLAUSE ')' )
    DIST_CLAUSE(%DIST_CLAUSE) = ;
                  [('SUBSPAN/S'|'SPAN/S') ['=']] .DEC2 ',';
                  ['UNIT/S' ['=']] .DEC
    FOR_CLAUSE(%FOR_CLAUSE) = 'FOR' .ASYM+' '
    ON_CLAUSE(%ON_CLAUSE) = (ON_N|OVER_N|INTO_N) .B .FID
      ON_N(%ON) = ('ON'|'TO'|'CREATING')
      OVER_N(%OVER) = ('OVER'|'REPLACING')
      INTO_N(%INTO) = ('INTO'|'APPENDING')
    SUM_CLAUSE(%SUM_CLAUSE) = ['WITH' .B] 'SUM/MARY'
/**/
END_CMD(%END_CMD) = ('E/ND'|'X/IT'|'Q/UIT')
/**/
NULL_CMD(%NULL_CMD) = [.B]
/**/
ELF_LOGNUM(D,1) = .ASYM+'(' '(' LOGNUM [')']
LOGNUM(2)       = ['L/OG'] ['='] .DEC2 ['.'- .ALPHA8+'0123456789$:']
