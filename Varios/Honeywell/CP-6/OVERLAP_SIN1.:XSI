/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
%LIST;
%INCLUDE OVERLAP_C1;
%EJECT;
OVERLAP_NODES(OVERLAP##,D) = ( ['('] [<';',CMDS>] [')'] ) .END
 
    CMDS = ( { ADJACENT_OK   | ;
               ALL_HELP      | ;
               CLEAR         | ;
               CHECK         | ;
               END           | ;
               HELP          | ;
               LIST          | ;
               MAX_LENGTH    | ;
               MAX_WIDTH     | ;
               MORE_HELP     | ;
               NO_ADJACENT   | ;
               NULL          | ;
               OUTPUT        | ;
               PRINT_CHECK   | ;
               PRINT_IF      | ;
               PRINT         | ;
                                        /* QUIT                               */
               READ          | ;
               SELECT        | ;
               USE        ;
            /* XIT */        } )
 
ADJACENT_OK(ADJACENT_OK##)   = 'A/DJACENT' ['F/IELDS'] ['ARE'] ['O/K']
ALL_HELP(ALL_HELP##)         = '??'
CLEAR(CLEAR##)               = 'CL/EAR'
CHECK(CHECK##)               = 'C/HECK' [ .B <',',FRAMES> ;
                                  ['L/INES' <',',LINES> ] ]
END(END##)                   = ( 'E/ND' | 'Q/UIT' | 'X/IT' )
HELP(HELP##)                 = 'H/ELP' .ASYM+';'
LIST(LIST##)                 = 'L/IST'
MAX_LENGTH(MAX_LENGTH##)     = 'M/AX' 'L/ENGTH' '=' .DEC3
MAX_WIDTH (MAX_WIDTH##)      = 'M/AX' 'W/IDTH'  '=' .DEC3
MORE_HELP(MORE_HELP##)       = '?'
NO_ADJACENT(NO_ADJACENT##)   = 'N/O' 'A/DJACENT' ['F/IELDS'] ['ARE'] ['O/K']
NULL(NULL##)                 = [.B] .END
OUTPUT(OUTPUT##)             = 'O/UTPUT' [HOW] .B AFID
    HOW = ( ON | TO | OVER | INTO )
      ON(ON##)     = 'ON'
      TO(TO##)     = 'TO'
      OVER(OVER##) = 'OVER'
      INTO(INTO##) = 'INTO'
PRINT(PRINT##)               = 'P/RINT' [.B <',',FRAMES> ;
                                  ['L/INES' <',',LINES> ] ]
PRINT_CHECK(PRINT_CHECK##)   = 'P/RINT' 'CH/ECK' [ .B <',',FRAMES> ;
                                  ['L/INES' <',',LINES>] ]
PRINT_IF(PRINT_IF##)         = 'P/RINT' 'IF' [ .B <',',FRAMES> ;
                                  ['L/INES' <',',LINES> ] ]
READ(READ##)                 = 'R/EAD' .B AFID
SELECT(SELECT##)             = 'S/ELECT' .B <',',FRAMES> ;
                                  [.B 'L/INES' <',',LINES> ]
USE(USE##)                   = 'U/SE' .B AFID
WIDTH(WIDTH##)               = 'W/IDTH' '=' .DEC3
 
    FRAMES(FRAMES##)     = .SYM+'-'
    LINES(LINES##)       = ( .DEC3 '-' .DEC3 | .DEC3 )
 
    AFID(AFID##)   = .FID
