!DEFAULT DEFER='23:59',SUBMIT=YES
!JOB D(DEFER), NAME=TELLEM_IM_OUT,RERUN
!RESOURCE TIME=1
!DATE.X (+1, DATE=DEF)
!B *ANSWER_LETTERS
!!JOB NAME=ANSWER_LETTERS,RERUN
!!RESOURCE TIME=2,MEM=216
!!MAIL
MARK UNEX AND ~FC AND ~ACK AND FROM ~= Netmail
OUTPUT OVER **9
DI UNEX and ~FC and ~ACK and from ~= Netmail
END
!!ELBBIRD.X **9
!!"Now we build the letter that will be sent to everyone.
!!"It is IMPORTANT that the TO: and SUBJECT: keywords remain exactly
!!"as they are, the rest of the text can be altered.
!!" If you copy this file to your account, you must change the VACATION.X
!!" reference below.
!!B *LETTER
To:
SUBJECT:I am on vacation...
This letter is being answered automatically.  I don't know when I'll
return, but I'll let you know when I do.
!!E **9
SE;IF NOT /To:/?/From:/ AND NOT //?/Subject/?// AND NOT //?/Sent:/?//;DE
SE;////S//////
SE;//?/Sent:/?/:/?/    /S/!E *LETTER/
SE0-99999.999,/To:/;//?/To:/?/From:/S/SE;//To://?//      //S//To:/;/             /S////
SE;//?/Subject:/?/                /S/!SEND *LETTER/
END
!!XEQ **9
!!IF $MODE='BATCH' THEN SET M$LL NO
!!IF $MODE='BATCH' THEN LDEV LP01,ERASE,REMOVE
!!IF $MODE='BATCH' THEN OFF NONE
!!SKIPIT:
!BATCH *ANSWER_LETTERS
!BATCH VACATION.X %('DEFE'||'R=''00:01,'||DEF||'''')
!IF $MODE='BATCH' THEN SET M$LL NO
!IF $MODE='BATCH' THEN LDEV LP01,ERASE,REMOVE
!IF $MODE='BATCH' THEN OFF NONE
