/*M* FLI_SIP1:  Frame list integrator nodes.  */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X* DMC,PLM=4,IND=0,DCI=4,CSI=2,IAD=2,DTI=2,AND,ECI=0,DIN=2,STI=2 */
 
  %LIST;
  %INCLUDE FLI_C61;
 
  A_FRAME ( %FRAME, DEF ) = 'FR' ;
                            FRAME_NAME ;
                            * FRAME_CLAUSES [ ',' ] [ ';' ] * ;
                            * FIELD * ;
                            .END
 
  FRAME_NAME ( %FRAME_NAME ) = .ALPHA30+'0123456789-_'
 
  FRAME_CLAUSES ( %FRAME_CLAUSES ) = { ORIGIN_CLAUSE | ;
                                       END_OF_CLAUSES }
 
  ORIGIN_CLAUSE ( %ORIGIN_CLAUSE ) = 'ORIGIN' [ 'IS' ] ;
                                     [ 'LINE' ] .DEC3 [ ',' ] ;
                                     [ 'COLUMN' ] .DEC3
 
  FIELD ( %FIELD ) = FIELD_LEVEL ;
                     FIELD_NAME ;
                     * FIELD_CLAUSES [ ',' ] [ ';' ] *
 
  FIELD_LEVEL ( %FIELD_LEVEL ) = .DEC2
 
  FIELD_NAME ( %FIELD_NAME ) = .ALPHA30+'0123456789-_'
 
  FIELD_CLAUSES ( %FIELD_CLAUSES ) = { PICTURE_CLAUSE | ;
                                       POSITION_CLAUSE | ;
                                       REPEATS_CLAUSE | ;
                                       BWZ_CLAUSE | ;
                                       SIGN_CLAUSE | ;
                                       IO_OPTION | ;
                                       INPUT_OPTION | ;
                                       OUTPUT_OPTION | ;
                                       ENTRY_REQUIRED_CLAUSE | ;
                                       ENTRY_OPTIONAL_CLAUSE | ;
                                       CLASS_CLAUSE | ;
                                       ATTRIBUTE_CLAUSE | ;
                                       MINIMUM_SIZE_CLAUSE | ;
                                       FILL_CHARACTER_CLAUSE | ;
                                       JUSTIFIED_RIGHT_CLAUSE | ;
                                       CONSTANT_VALUE_CLAUSE | ;
                                       DEFAULT_VALUE_CLAUSE | ;
                                       PROMPT_CLAUSE | ;
                                       A_O_I_M_CLAUSE | ;
                                       END_OF_CLAUSES }
 
  PICTURE_CLAUSE ( %PICTURE ) = 'PIC/TURE' [ 'IS' ] ;
                                .ALPHA30+'0123456789,$+-*/().'
 
  POSITION_CLAUSE ( %POSITION ) = 'POS/ITION' [ 'IS' ] ;
                                  [ 'LINE' ] .DEC3 [ ',' ] ;
                                  [ 'COLUMN' ] .DEC3
 
  REPEATS_CLAUSE ( %REPEATS ) = 'REPEATS' .DEC3 [ 'TIMES' ] ;
                                [ 'SEPARATED' [ 'BY' ] .DEC3 ]
 
  BWZ_CLAUSE ( %BWZ ) = ( 'BWZ' | ;
                          'BLANK' 'WHEN' 'ZERO' )
 
  SIGN_CLAUSE ( %SIGN ) = ( 'SIGN' [ 'IS' ] ;
                            ( LEADING_SEPARATE_OPTION | ;
                              TRAILING_SEPARATE_OPTION | ;
                              TRAILING_OPTION ) ;
                                                )
  LEADING_SEPARATE_OPTION ( %LEADING_SEPARATE_OPTION ) = ;
                          'LEADING' 'SEPARATE' [ 'CHARACTER' ]
  TRAILING_SEPARATE_OPTION ( %TRAILING_SEPARATE_OPTION ) = ;
                           'TRAILING' 'SEPARATE' [ 'CHARACTER' ]
  TRAILING_OPTION ( %TRAILING_OPTION ) = 'TRAILING'
 
  IO_OPTION ( %IO_OPTION ) = 'I-O'
  INPUT_OPTION ( %INPUT_OPTION ) = 'INPUT'
  OUTPUT_OPTION ( %OUTPUT_OPTION ) = 'OUTPUT'
 
  ENTRY_REQUIRED_CLAUSE ( %ENTRY_REQUIRED ) = 'ENTRY-REQUIRED'
  ENTRY_OPTIONAL_CLAUSE ( %ENTRY_OPTIONAL ) = 'ENTRY-OPTIONAL'
 
  CLASS_CLAUSE ( %CLASS ) = 'CLASS' [ 'IS' ] ;
                            ( ALPHABETIC_OPTION | ;
                              NUMERIC_OPTION | ;
                              .ALPHA30+'0123456789-_' )
  ALPHABETIC_OPTION ( %ALPHABETIC_OPTION ) = 'ALPHABETIC'
  NUMERIC_OPTION ( %NUMERIC_OPTION ) = 'NUMERIC'
 
  ATTRIBUTE_CLAUSE ( %ATTRIBUTE ) = 'ATTRIBUTE' [ 'IS' ] ;
                                    ( EMPHASIS_ATTRIBUTE_OPTION | ;
                                      ERROR_ATTRIBUTE_OPTION | ;
                                      HIDDEN_ATTRIBUTE_OPTION | ;
                                      INPUT_ATTRIBUTE_OPTION | ;
                                      NULL_ATTRIBUTE_OPTION | ;
                                      OUTPUT_ATTRIBUTE_OPTION | ;
                                      WARNING_ATTRIBUTE_OPTION | ;
                                      .ALPHA30+'0123456789-_' )
  EMPHASIS_ATTRIBUTE_OPTION ( %EMPHASIS_ATTRIBUTE_OPTION ) = ;
                            'EMPHASIS-ATTRIBUTE'
  ERROR_ATTRIBUTE_OPTION ( %ERROR_ATTRIBUTE_OPTION ) = ;
                         'ERROR-ATTRIBUTE'
  HIDDEN_ATTRIBUTE_OPTION ( %HIDDEN_ATTRIBUTE_OPTION ) = ;
                          'HIDDEN-ATTRIBUTE'
  INPUT_ATTRIBUTE_OPTION ( %INPUT_ATTRIBUTE_OPTION ) = ;
                         'INPUT-ATTRIBUTE'
  NULL_ATTRIBUTE_OPTION ( %NULL_ATTRIBUTE_OPTION ) = ;
                        'NULL-ATTRIBUTE'
  OUTPUT_ATTRIBUTE_OPTION ( %OUTPUT_ATTRIBUTE_OPTION ) = ;
                          'OUTPUT-ATTRIBUTE'
  WARNING_ATTRIBUTE_OPTION ( %WARNING_ATTRIBUTE_OPTION ) = ;
                           'WARNING-ATTRIBUTE'
 
  MINIMUM_SIZE_CLAUSE ( %MINIMUM_SIZE ) = 'MINIMUM' [ 'SIZE' ] ;
                                          [ 'IS' ] ;
                                          .DEC3
 
  FILL_CHARACTER_CLAUSE ( %FILL_CHARACTER ) = 'FILL' [ 'CHARACTER' ] ;
                                              [ 'IS' ] ;
                                              ( SPACE_OPTION | ;
                                                ZEROES_OPTION | ;
                                                ZERO_OPTION | ;
                                                HIGH_VALUE_OPTION | ;
                                                LOW_VALUE_OPTION | ;
                                                .DSTR1+'"' )
 
  JUSTIFIED_RIGHT_CLAUSE ( %JUSTIFIED_RIGHT ) = 'JUST/IFIED' ;
                                                 [ 'RIGHT' ]
 
  DEFAULT_VALUE_CLAUSE ( %DEFAULT_VALUE ) = 'DEFAULT' ;
                                            [ 'VALUE' ] [ 'IS' ] ;
                                            ( SPACE_OPTION | ;
                                              ZEROES_OPTION | ;
                                              ZERO_OPTION | ;
                                              HIGH_VALUE_OPTION | ;
                                              LOW_VALUE_OPTION | ;
                                              ALL_VALUE_OPTION | ;
                                              NUMERIC_VALUE_OPTION | ;
                                              ALPHANUMERIC_VALUE_OPTION )
 
  CONSTANT_VALUE_CLAUSE ( %CONSTANT_VALUE ) = 'CONSTANT' ;
                                              [ 'VALUE' ] [ 'IS' ] ;
                                              ( SPACE_OPTION | ;
                                                ZEROES_OPTION | ;
                                                ZERO_OPTION | ;
                                                HIGH_VALUE_OPTION | ;
                                                LOW_VALUE_OPTION | ;
                                                ALL_VALUE_OPTION | ;
                                                NUMERIC_VALUE_OPTION | ;
                                                ALPHANUMERIC_VALUE_OPTION )
 
  PROMPT_CLAUSE ( %PROMPT ) = 'PROMPT' [ 'IS' ] ;
                              ( SPACE_OPTION | ;
                                ZEROES_OPTION | ;
                                ZERO_OPTION | ;
                                HIGH_VALUE_OPTION | ;
                                LOW_VALUE_OPTION | ;
                                ALL_VALUE_OPTION | ;
                                NUMERIC_VALUE_OPTION | ;
                                DATANAME_OPTION | ;
                                ALPHANUMERIC_VALUE_OPTION )
 
  A_O_I_M_CLAUSE ( %A_O_I_M ) = ( 'A-O-I-M' | ;
                                  'AVAILABLE-ONLY-IF-MODIFIED' )
 
  SPACE_OPTION ( %SPACE_OPTION ) = 'SPACE/S'
  ZEROES_OPTION ( %ZEROES_OPTION ) = 'ZEROES'
  ZERO_OPTION ( %ZERO_OPTION ) = 'ZERO/S'
  HIGH_VALUE_OPTION ( %HIGH_VALUE_OPTION ) = 'HIGH-VALUE/S'
  LOW_VALUE_OPTION ( %LOW_VALUE_OPTION ) = 'LOW-VALUE/S'
  ALL_VALUE_OPTION ( %ALL_VALUE_OPTION ) = 'ALL' .DSTR+'"'
  NUMERIC_VALUE_OPTION ( %NUMERIC_VALUE_OPTION ) = .DEC15+'.'
  ALPHANUMERIC_VALUE_OPTION ( %ALPHANUMERIC_VALUE_OPTION ) = .DSTR+'"'
  DATANAME_OPTION ( %DATANAME_OPTION ) = .ALPHA30+'0123456789-_'
 
  END_OF_CLAUSES ( %END_OF_CLAUSES ) = '.'
 
 
  A_PICTURE_STRING ( %A_PICTURE_STRING, DEF ) = *PICTURE_CHARACTERS*
 
  PICTURE_CHARACTERS ( %PICTURE_CHARACTERS ) = ( COMMA | ;
                                                 X | ;
                                                 NINE | ;
                                                 P | ;
                                                 DOLLAR_SIGN | ;
                                                 PLUS_SIGN | ;
                                                 MINUS_SIGN | ;
                                                 Z | ;
                                                 ASTERISK | ;
                                                 SLASH | ;
                                                 ZERO | ;
                                                 B | ;
                                                 S | ;
                                                 V | ;
                                                 CR | ;
                                                 DB | ;
                                                 PERIOD )
 
  COMMA ( %COMMA ) = ',' [ '(' .DEC5 ')' ]
  X ( %X ) = 'X' [ '(' .DEC5 ')' ]
  NINE ( %NINE ) = '9' [ '(' .DEC5 ')' ]
  P ( %P ) = 'P' [ '(' .DEC5 ')' ]
  DOLLAR_SIGN ( %DOLLAR_SIGN ) = '$' [ '(' .DEC5 ')' ]
  PLUS_SIGN ( %PLUS_SIGN ) = '+' [ '(' .DEC5 ')' ]
  MINUS_SIGN ( %MINUS_SIGN ) = '-' [ '(' .DEC5 ')' ]
  Z ( %Z ) = 'Z' [ '(' .DEC5 ')' ]
  ASTERISK ( %ASTERISK ) = '*' [ '(' .DEC5 ')' ]
  SLASH ( %SLASH ) = '/' [ '(' .DEC5 ')' ]
  ZERO ( %ZERO ) = '0' [ '(' .DEC5 ')' ]
  B ( %B ) = 'B' [ '(' .DEC5 ')' ]
  S ( %S ) = 'S'
  V ( %V ) = 'V'
  CR ( %CR ) = 'CR'
  DB ( %DB ) = 'DB'
  PERIOD ( %PERIOD ) = '.'
