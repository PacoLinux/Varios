      PROGRAM PNTGRAF
C*T***********************************************************  
C*T*                                                         *  
C*T* Copyright (c) Bull HN Information Systems Inc., 1989    *  
C*T*                                                         *  
C*T***********************************************************  
      SAVE
C
C*X* JR,IND=3,FMT=1000,STMT=100
C
C     PROGRAM TO INPUT DATA PAIRS AND PLOT A GRAPH ON A STANDARD
C     8.5" HIGH AND 11" WIDE SHEET
C
C     PROGRAM PROMPTS ON F$108 FOR ALL INPUT, READING FROM F$10
C
C
      INTEGER OPTS(16)
      DIMENSION XARRAY(4,2004),YARRAY(4,2004),KOUNTS(4),ISCAT(4)
      DIMENSION TEMPARX(2004),TEMPARY(2004),IREGR(4),REGR(4,5)
      DIMENSION SCALEARR(10,4),REGRX(30),REGRY(30)
      INTEGER LINESTYLES(4)/2525,5252,4848,9292/
      DIMENSION PHONEYX(4),PHONEYY(4)
      CHARACTER TITLE*80, XTITLE*40, YTITLE*40, ANOTE*40
      CHARACTER LINNAME(4)*12, SCRATCH*80
      INTEGER KHAR(4),VIEWSURF/0/
      DATA KHAR/2,3,4,5/
      DIMENSION XTITL(4),YTITL(4)
      DATA XTITL / 2.,4.,6.,8./
      DATA YTITL /6.45,6.45,6.45,6.45/
C
C         N.B. ADJUST THESE VALUES FOR YOUR MACHINE
C
      DATA XMAX,YMAX,XMIN,YMIN /-8.E152,-8.E152,8.E152,8.E152/
      CALL CCBUF(OPTS)
      IF (OPTS(1).EQ.9999) THEN
         OUTPUT 'Bad command line option -- aborting'
         STOP 
      ENDIF 
C
C     CLEAR THE ARRAYS
C
      DO 110 J = 1, 2004
         DO 100 I = 1, 4
            XARRAY(I,J) = 0.
            YARRAY(I,J) = 0.
  100    CONTINUE 
         TEMPARX(J) = 0.
         TEMPARY(J) = 0.
  110 CONTINUE 
      DO 120 I = 1,4
         LINNAME(I) = ' '
         ISCAT(I)=0
         IREGR(I)=0
  120 CONTINUE 
      XTITLE = ' '
      YTITLE = ' '
      TITLE = ' '
      IXGRID=0
      IYGRID=0
      ANOTE = ' '
C
C     PROMPT HERE FOR TITLES
C
      WRITE (108,1000)
      READ (10,1010) TITLE
      WRITE (108,1020)
      READ (10,1030) SCRATCH
      DO 130 I=1,(80-5)
         IF (SCRATCH(I:I+4) .EQ. ',GRID') THEN
            SCRATCH(I:I+5)= '     '
            IXGRID=1
            WRITE (108,1050) 'X'
            GOTO 140 
         ENDIF 
  130 CONTINUE 
  140 CONTINUE 
      XTITLE = SCRATCH(1:40)
      WRITE (108,1040)
      READ (10,1030) SCRATCH
      DO 150 I=1,(80-5)
         IF (SCRATCH(I:I+4) .EQ. ',GRID') THEN
            SCRATCH(I:I+5)= '     '
            IYGRID=1
            WRITE (108,1050) 'Y'
            GOTO 160 
         ENDIF 
  150 CONTINUE 
  160 CONTINUE 
      YTITLE = SCRATCH(1:40)
      WRITE (108,1060)
      READ (10,1070) ANOTE
C
C
C          OBTAIN DATA TO DETERMINE HOW MUCH WE'RE GOING
C          TO PLOT
C
  170 CONTINUE 
      WRITE (108,1080)
      READ (10,1090) NLINES
      IF (NLINES .GT. 4)GOTO 170 
      IF (NLINES .LT. 1)GOTO 170 
      DO 210 I = 1,NLINES
         WRITE (108,1100) I
         READ (10,1110) SCRATCH
         DO 180 J=1,(80-7)
            IF (SCRATCH(J:J+6) .EQ. ',NOLINE') THEN
               ISCAT(I)=1
               WRITE (108,1160)
               SCRATCH(J:J+6)='       '
            ENDIF 
            IF (SCRATCH(J:J+6) .EQ. ',LINREG') THEN
               IREGR(I)=1
               WRITE (108,1170)
               SCRATCH(J:J+6)='       '
            ENDIF 
  180    CONTINUE 
         LINNAME(I)=SCRATCH(1:12)
         WRITE (108,1120) I
         KOUNTS (I) = 0
C
C         REGRESSION TEMPS ARE:
C                        3) SUM X SQUARED
C         1) SUM X       4) SUM Y SQUARED
C         2) SUM Y       5) SUM X*Y
C
         REGR(I,1)=0.
         REGR(I,2)=0.
         REGR(I,3)=0.
         REGR(I,4)=0.
         REGR(I,5)=0.
         DO 190 J = 1,2001
            READ (10,1130,END=200) X,Y
            XARRAY(I,J)=X
            YARRAY(I,J)=Y
            IF (IREGR(I) .NE. 0) THEN
               REGR(I,1) = REGR(I,1)+X
               REGR(I,2) = REGR(I,2)+Y
               REGR(I,3) = REGR(I,3)+(X*X)
               REGR(I,4) = REGR(I,4)+(Y*Y)
               REGR(I,5) = REGR(I,5)+(X*Y)
            ENDIF 
            IF (X .LT. XMIN) XMIN=X
            IF (X .GT. XMAX) XMAX=X
            IF (Y .GT. YMAX) YMAX=Y
            IF (Y .LT. YMIN) YMIN=Y
            KOUNTS(I) = KOUNTS(I) + 1
            IF (KOUNTS(I) .GT. 2000) THEN
               WRITE (108,1150)
               CALL EXIT(7)
            ENDIF 
  190    CONTINUE 
  200    CONTINUE 
         IF (KOUNTS(I) .GT. 1)GOTO 210 
         WRITE (108,1140) I
         CALL EXIT(7)
  210 CONTINUE 
C
C          DATA SHOULD BE GATHERED NOW, GO AHEAD AND
C          SCALE AND PLOT IT
C
      CALL XG$INITIALIZE_CORE ('BUFFERED','NONE','3D','NONE')
      CALL XG$SET_COORD_SYSTEM_TYPE ('RIGHT')
      CALL XG$SET_NDC_SPACE_2 (1.0,.7)
      CALL XG$INITIALIZE_VIEW_SURFACE  (VIEWSURF,'INTENSITY')
      CALL XG$SELECT_VIEW_SURFACE  (VIEWSURF)
      CALL XG$SET_IMMEDIATE_VISIBILITY ('OFF')
      CALL XG$SET_WINDOW_CLIPPING  ('ON')
      CALL XG$SET_WINDOW  (0.,10.,0.,7.)
      CALL XG$SET_CHARPRECISION ('stroke')
      IF (OPTS(1).EQ.1) THEN
         CALL XG$SET_VIEWPORT_2 (.05,.95,.035,.665)
      ENDIF 
      CALL XG$SET_LINEWIDTH(.15)
      CALL XG$CREATE_TEMPORARY_SEG
      IF (OPTS(2).EQ.1) THEN
         CALL XG$MOVE_ABS_2 (.001,.001)
         CALL XG$LINE_ABS_2 (.001,6.999)
         CALL XG$LINE_ABS_2 (9.999,6.999)
         CALL XG$LINE_ABS_2 (9.999,.001)
         CALL XG$LINE_ABS_2 (.001,.001)
      ENDIF 
      CALL XG$MOVE_ABS_2 (0.,0.)
C
C          FIGURE OUT SCALE FACTORS FOR EACH ARRAY
C
C
C              setup phoney scaling array
C
      PHONEYX(1)=XMIN
      PHONEYX(2)=XMAX
      PHONEYY(1)=YMIN
      PHONEYY(2)=YMAX
C
C              compute most reasonable interval values for Y axis
C
      J = 10
      DO 220 I=1,6
         SCALEARR(I,1) = 5./FLOAT(J)
         J = J - 1
  220 CONTINUE 
      DO 230 I=1,6
         CALL SCALE (PHONEYY,5.0,2,1,SCALEARR(I,1))
         SCALEARR(I,2) = PHONEYY(3)
         SCALEARR(I,3) = PHONEYY(4)
  230 CONTINUE 
      STARTDY = SCALEARR(1,2)
      SCALEDY = SCALEARR(1,3)
      STEPDY = SCALEARR(1,1)
      ZYMAX = (5.0 * SCALEDY) * (1./STEPDY) + STARTDY
      RATIO = YMAX/ZYMAX
      DO 240 I=1,6
         ZYTEMP = (5.0*SCALEARR(I,3))*(1./SCALEARR(I,1))+SCALEARR(I,2)
         IF ((YMAX/ZYTEMP) .GT. RATIO) THEN
            RATIO = YMAX/ZYTEMP
            STARTDY = SCALEARR(I,2)
            SCALEDY = SCALEARR(I,3)
            STEPDY = SCALEARR(I,1)
         ENDIF 
  240 CONTINUE 
C
C              now discovered, punch into arrays
C
      DO 250 I=1,NLINES
         YARRAY(I,KOUNTS(I)+1)=STARTDY
         YARRAY(I,KOUNTS(I)+2)=SCALEDY
  250 CONTINUE 
C
C              compute most reasonable interval values for X axis
C
      J = 10
      DO 260 I=1,5
         SCALEARR(I,1) = 8./FLOAT(J)
         J = J - 1
  260 CONTINUE 
      DO 270 I=1,5
         CALL SCALE (PHONEYX,8.0,2,1,SCALEARR(I,1))
         SCALEARR(I,2) = PHONEYX(3)
         SCALEARR(I,3) = PHONEYX(4)
  270 CONTINUE 
      STARTDX = SCALEARR(1,2)
      SCALEDX = SCALEARR(1,3)
      STEPDX = SCALEARR(1,1)
      ZXMAX = (8.0 * SCALEDX) * (1./STEPDX) + STARTDX
      RATIO = XMAX/ZXMAX
      DO 280 I=1,5
         ZXTEMP = (8.0*SCALEARR(I,3))*(1./SCALEARR(I,1))+SCALEARR(I,2)
         IF ((XMAX/ZXTEMP) .GT. RATIO) THEN
            RATIO = XMAX/ZXTEMP
            STARTDX = SCALEARR(I,2)
            SCALEDX = SCALEARR(I,3)
            STEPDX = SCALEARR(I,1)
         ENDIF 
  280 CONTINUE 
C
C              now discovered, punch into arrays
C
      DO 290 I=1,NLINES
         XARRAY(I,KOUNTS(I)+1)=STARTDX
         XARRAY(I,KOUNTS(I)+2)=SCALEDX
  290 CONTINUE 
C
C          FIGURE OUT HOW MANY CHARACTERS TO PASS TO AXIS
C
      CALL CHARSIZE (XTITLE,40,KOUNTX)
      CALL CHARSIZE (YTITLE,40,KOUNTY)
      CALL CHARSIZE (TITLE ,80,KOUNTT)
C
C     MAKE AXES
C
      CALL AXIS (1.5,1.25,XTITLE,KOUNTX,8.,0.0,1,STARTDX,SCALEDX,STEPDX,
     113,1,1000.)
      CALL AXIS (1.5,1.25,YTITLE,KOUNTY,5.,90.,-1,STARTDY,SCALEDY,
     1STEPDY, 13, 1, 1000.)
C
C              check for and make grid pattern
C
      IF (IYGRID.EQ.0 .AND. IXGRID.EQ.0)GOTO 330 
      CALL XG$MOVE_ABS_2 (1.5,6.25)
      CALL XG$LINE_ABS_2 (9.5,6.25)
      CALL XG$LINE_ABS_2 (9.5,1.25)
      CALL XG$SET_LINESTYLE(16)
      IF (IXGRID.EQ.0)GOTO 310 
      NOW=(8./STEPDX)-1
      HERE=1.5+STEPDX
      DO 300 L1=1,NOW
         CALL XG$MOVE_ABS_2(HERE,1.25)
         CALL XG$LINE_ABS_2(HERE,6.25)
         HERE=HERE+STEPDX
  300 CONTINUE 
  310 CONTINUE 
      IF (IYGRID.EQ.0)GOTO 330 
      NOW=(5./STEPDY)-1
      HERE=1.25+STEPDY
      DO 320 L1=1,NOW
         CALL XG$MOVE_ABS_2(1.5,HERE)
         CALL XG$LINE_ABS_2(9.5,HERE)
         HERE=HERE+STEPDY
  320 CONTINUE 
  330 CONTINUE 
      CALL XG$SET_LINESTYLE (1)
C
C          DRAW THE PLOT TITLE
C
      CALL XG$MOVE_ABS_2 (5.,6.825)
      ICKSIZE=.40*260.
      CALL XG$ESCAPE(2,1,ICKSIZE)
      CALL XG$SET_CHARSIZE (.40,.40)
      CALL XG$SET_FONT(13)
      CALL XG$SET_CHARJUST ('CENTER','CENTER')
      CALL XG$SET_CHARUP_2 (0.,1.)
      CALL XG$TEXT_COUNT (TITLE, KOUNTT)
C
C          DO THE LEGENDS
C
      ICKSIZE=.15*260.
      CALL XG$ESCAPE(2,1,ICKSIZE)
      CALL XG$SET_CHARSIZE(.15,.15)
      CALL XG$SET_CHARUP_2 (0.,1.)
      CALL XG$SET_FONT(13)
      CALL XG$SET_CHARJUST('LEFT','CENTER')
      DO 350 I = 1,NLINES
         IF (LINNAME(I) .EQ. 'DUMMY LABEL')GOTO 340 
         IF (LINNAME(I) .EQ. ' ')GOTO 340 
         CALL XG$SET_MARKER_SYMBOL(KHAR(I))
         CALL XG$MARKER_ABS_2 (XTITL(I)-.2,YTITL(I))
         CALL XG$MOVE_ABS_2 (XTITL(I),YTITL(I))
         CALL XG$TEXT_COUNT (LINNAME(I),12)
  340    CONTINUE 
  350 CONTINUE 
C
C         draw plot notation
C
      CALL CHARSIZE (ANOTE,40,KOUNTT)
      CALL XG$SET_CHARJUST('RIGHT','BOTTOM')
      CALL XG$SET_CHARSIZE (.15,.15)
      ICKSIZE=.15*260.
      CALL XG$ESCAPE(2,1,ICKSIZE)
      CALL XG$MOVE_ABS_2 (9.9,0.01)
      CALL XG$TEXT_COUNT(ANOTE,KOUNTT)
      CALL XG$CLOSE_TEMPORARY_SEG
      IF (OPTS(1) .NE.1) THEN
         CALL XG$SET_VIEWPORT_2 (.15,.95,.125,.6250)
      ELSE 
         CALL XG$SET_VIEWPORT_2(.185,.905,.1475,.5975)
      ENDIF 
      XMAX=(8.0 * SCALEDX)*(1./STEPDX)+ STARTDX
      YMAX=(5.0 * SCALEDY)*(1./STEPDY)+ STARTDY
      CALL XG$SET_WINDOW (STARTDX,XMAX,STARTDY,YMAX)
      CALL XG$CREATE_TEMPORARY_SEG
      DO 390 I=1,NLINES
         IF (LINNAME(I) .EQ. 'DUMMY LABEL')GOTO 390 
         CALL XG$SET_MARKER_SYMBOL (KHAR(I))
         CALL PERM2TEMP (XARRAY,TEMPARX,I,KOUNTS(I))
         CALL PERM2TEMP (YARRAY,TEMPARY,I,KOUNTS(I))
         CALL XG$POLYMARKER_ABS_2 (TEMPARX,TEMPARY,KOUNTS(I))
         IF (ISCAT(I) .NE. 0)GOTO 360 
         CALL XG$MOVE_ABS_2 (TEMPARX(1),TEMPARY(1))
         CALL XG$POLYLINE_ABS_2 (TEMPARX,TEMPARY,KOUNTS(I))
  360    CONTINUE 
         IF (IREGR(I) .EQ. 0)GOTO 380 
         B=(FLOAT(KOUNTS(I))*REGR(I,5)-REGR(I,1)*REGR(I,2)) /(FLOAT(
     1      KOUNTS(I))*REGR(I,3)-(REGR(I,1)*REGR(I,1)))
         A = (REGR(I,2)-(B*REGR(I,1)))/FLOAT(KOUNTS(I))
         WRITE (108,1180) I,B,A
         NINTERV = (8./STEPDX)*2+1
         XCURR=STARTDX
         DO 370 L = 1,NINTERV
            REGRX(L)=XCURR
            REGRY(L)=(B*XCURR)+A
            XCURR=XCURR+(SCALEDX/2.)
  370    CONTINUE 
         CALL XG$SET_LINESTYLE(LINESTYLES(I))
         CALL XG$MOVE_ABS_2(REGRX(1),REGRY(1))
         CALL XG$POLYMARKER_ABS_2 (REGRX,REGRY,NINTERV)
         CALL XG$MOVE_ABS_2 (REGRX(1),REGRY(1))
         CALL XG$POLYLINE_ABS_2(REGRX,REGRY,NINTERV)
         CALL XG$SET_LINESTYLE(1)
  380    CONTINUE 
  390 CONTINUE 
      CALL XG$CLOSE_TEMPORARY_SEG
      CALL XG$DESELECT_VIEW_SURFACE (VIEWSURF)
      CALL XG$TERMINATE_VIEW_SURFACE(VIEWSURF)
      CALL XG$TERMINATE_CORE
      STOP 
 1000 FORMAT (' Input title of plot (80 char max)')
 1010 FORMAT (A80)
 1020 FORMAT (' Title along X-axis (40 char max, [,GRID])')
 1030 FORMAT (A80)
 1040 FORMAT (' Title along Y-axis (40 char max, [,GRID])')
 1050     FORMAT (' ',A1,'-axis grid set on')
 1060 FORMAT (' Input notation test (40 char max)')
 1070 FORMAT (A40)
 1080 FORMAT (' Number of lines to be plotted (1-4)')
 1090 FORMAT (I)
 1100 FORMAT (' "Title" of line #',I1,'(12 char max[,NOLINE][,LINREG])')
 1110 FORMAT (A80)
 1120 FORMAT (' Now input X,Y pairs for line #',I1,/,
     1        ' input <escape-F> or !EOD to stop inputting')
 1130 FORMAT (2F)
 1140 FORMAT (1X,'*** Not enuf points for line ',I,'...aborting')
 1150 FORMAT (1X,'*** More than 2000 data points...aborting')
 1160 FORMAT (1X,'...Scattergraph only for this line')
 1170 FORMAT (1X,'...Linear regression line will be displayed')
 1180 FORMAT (1X,'Line #',I,'equation: Y = ',1PE10.3,'X +',1PE10.3)
      END 
