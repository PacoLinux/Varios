10 REM THORN
20 REM By Dennis Griesser
30 REM Revision history:
40 REM        09/04/84 - initial version
50 REM        10/25/84 - multiple trigger strings, selectable spacing
60 REM *** GREETING
70 PRINT "This programme copies a non-UR file to a UR file."
80 PRINT "Lines that contain a given string are made to"
90 PRINT "stick out by making them darker with multiple strikes."
100 PRINT
110 REM *** SET UP TRIGGER STRINGS
120 OPTION BASE 1
130 DIM TRIGGER$(10)
140 NTRIG=0
150 FOR I=1 TO 10
160   PRINT "Trigger string number";I;
170   LINPUT TRIGGER$(I)
180   IF TRIGGER$(I)="" THEN 210
190   NTRIG=NTRIG+1
200   NEXT I
210 IF NTRIG=0 THEN STOP
220 REM *** SET SPACING                  
230 PRINT "How many spaces between lines (1-3)";
240 INPUT LSPACE
250 IF LSPACE < 1 OR LSPACE > 3 THEN 230
260 NVFC$=CHR$(ASC("@")+LSPACE)
270 REM *** SET UP INPUT FILE
280 PRINT "Input FID";
290 INPUT I$
300 OPEN I$ TO 1,INPUT,CONSEC
310 MARGIN#1,255
320 REM *** SET UP OUTPUT FILE
330 PRINT "Output FID";
340 INPUT O$
350 OPEN O$ TO 2,PRINT,OVER
360 MARGIN#2,255
370 REM *** MAIN RECORD PROCESSING LOOP
380 WHEN EOF#1 GOTO 500
390 LINPUT#1,X$
400 IF LEN(X$)=0 THEN 480
410 FOR I=1 TO NTRIG
420   IF SCN(X$,TRIGGER$(I),1,1)<>0 THEN
430      PRINT#2,"`"+X$
440      PRINT#2,"`"+X$
450      GOTO 480
460      ENDIF
470   NEXT I
480 PRINT#2,NVFC$+X$
490 GOTO 390
500 REM *** EOF 
510 CLOSE ALL
520 REM *** MAKE THE OUTPUT UR, FLAGGING OUR NEW VFCS
530 LINK "PCL.:SYS","COPY "+O$+" OVER "+O$+" (ORG=UR,VFC)"
540 END
