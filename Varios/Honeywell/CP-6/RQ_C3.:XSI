/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
%MACRO WILD_COMPARE(COMP_COUNT=NO,NO_MATCH=ALTRETURN,WILD_CHARS=WILD_CHARS,COMPARE_CHARS=COMPARE_CHARS,WILD_X=WILD_X,
WILD_XP=WILD_XP,COMPARE_X=COMPARE_X,COMPARE_XP=COMPARE_XP,WILD_MAX=0,COMPARE_MAX=0);
%LSET LISTDIR='0'B;
%LSET LISTEXP='1'B;
%LSET LISTSUB='1'B;
%LSET COUNT=TEXTCHAR(COMP_COUNT);
%LSET CMAX=TEXTCHAR(COMPARE_MAX);
%IF CMAX='0';
%LSET CMAX=CHARTEXT(CONCAT('SIZEC(',TEXTCHAR(COMPARE_CHARS),')'));
%ELSE;
%LSET CMAX=CHARTEXT(%CMAX);
%ENDIF;
%LSET WMAX=TEXTCHAR(WILD_MAX);
%IF WMAX='0';
%LSET WMAX=CHARTEXT(CONCAT('SIZEC(',TEXTCHAR(WILD_CHARS),')'));
%ELSE;
%LSET WMAX=CHARTEXT(%WMAX);
%ENDIF;
        IF WILD_CHARS='ALL' THEN DO;
           WILD_X=3;
           WILD_XP=2;
           END;
        ELSE DO;
           WILD_X=0;
           WILD_XP=%WMAX;
           END;
        COMPARE_X=0;
        COMPARE_XP=0;
        %IF COUNT~='NO';
        COMP_COUNT=0;
        %ENDIF;
        DO WHILE(WILD_X<%WMAX AND SUBSTR(WILD_CHARS,WILD_X,1)~=' '
          OR COMPARE_X<%CMAX AND SUBSTR(COMPARE_CHARS,COMPARE_X,1)~=' ');
           IF WILD_X<%WMAX AND SUBSTR(WILD_CHARS,WILD_X,1)='?' THEN DO;
              WILD_XP=WILD_X;
              END;
           ELSE DO;
              IF WILD_X=WILD_XP+1 THEN COMPARE_XP=COMPARE_X;
              IF WILD_X>=%WMAX
                OR COMPARE_X>=%CMAX
                OR SUBSTR(WILD_CHARS,WILD_X,1) ~=
                SUBSTR(COMPARE_CHARS,COMPARE_X,1) THEN DO;
                 IF WILD_XP=%WMAX OR COMPARE_XP=%CMAX
                 THEN NO_MATCH;
                 WILD_X=WILD_XP;
                 %IF COUNT~='NO';
                 COMP_COUNT=COMP_COUNT-COMPARE_X+COMPARE_XP;
                 %ENDIF;
                 COMPARE_X=COMPARE_XP;
                 END;
              %IF COUNT~='NO';
              ELSE COMP_COUNT=COMP_COUNT+1;
              %ENDIF;
              COMPARE_X=COMPARE_X+1;
              END;
           IF WILD_X<%WMAX THEN WILD_X=WILD_X+1;
           END;
        %IF COUNT~='NO';
        IF WILD_XP=%WMAX THEN COMP_COUNT=%WMAX;
        %ENDIF;
%MEND;
