/*M* WEASEL_SIN1 - Parse Nodes for WEASEL */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
/**/
%INCLUDE WEASEL_C61;
%INCLUDE EL_SUBS_C;
/**/
SL_CMD(D) = [.B] (USE_CMD          |;
                  SPAN_CMD         |;
                  TYPE_CMD         |;
                  REPORT_CMD       |;
                  RESET_CMD        |;
                  END_CMD          |;
                  NULL_CMD         );
                  .END
/**/
USE_CMD(%USE_CMD) = 'USE' .B '.'- [.ACC]
/**/
SPAN_CMD(%SPAN_CMD)   = [('SP/AN'|'T/IME') .B] (TIME_N | TIME_X | TODAY)
  TIME_N(%TIME_N)    = (START_TIME STOP_TIME|START_TIME|STOP_TIME)
    START_TIME(%START) = ['FROM'] TIME_SPEC
    STOP_TIME (%STOP)  = [.B] ('-'|'TO') [.B] TIME_SPEC
      TIME_SPEC = <(','|.B),{DATE|TIME}>
      DATE(%DATE_P) = .DEC '/' .DEC ['/' .DEC]
      TIME(%TIME_P) = .DEC [':' .DEC]
  TIME_X(%TIME_X) = 'LAST' .B .DEC [.B] (DAYS|WEEKS|MONTHS|YEAR| ;
                                         MINUTES|HOURS)
    DAYS(%DAYS) = 'D/AYS'
    WEEKS(%WEEKS) = 'W/EEKS'
    MONTHS(%MONTHS) = 'MO/NTHS'
    YEAR(%YEAR) = 'Y/EARS'
    MINUTES(%MINUTES) = 'MI/NUTES'
    HOURS(%HOURS) = 'H/OURS'
  TODAY(%TODAY) = 'TODAY'
/**/
TYPE_CMD(%TYPE_CMD) = 'TYPE' .B (TYPE_LIST | ALL)
  ALL(%ALL) = 'A/LL'
  TYPE_LIST = <(','|.B),TYPE_P>
    TYPE_P = (FILE_ACCESS | SYSTEM_ACCESS | SYSTEM_EXIT | MONSER | ;
              PRIVCHNG | ARES_BYPASS)
      FILE_ACCESS(%EL_FILE_ACCESS) = 'F/ILE' [.B] ['A/CCESS']
      SYSTEM_ACCESS(%EL_SYS_ACCESS) = ('S/YSTEM' [.B] 'A/CCESS'|;
                                       'LOGON')
      SYSTEM_EXIT(%EL_SYS_EXIT) = ('S/YSTEM' [.B] 'E/XIT'|'LOGOFF')
      MONSER(%EL_MON_SRV) = 'M/ONITOR' [.B] ['S/ERVICE']
      PRIVCHNG(%EL_PRIV_CHANGE) = 'P/RIVILEGE' [.B] ['C/HANGE']
      ARES_BYPASS(%EL_ARES_BYPASS) = 'AR/ES' [.B] ['C/HECKS'] [.B] ;
                                     ['B/YPASSED']
RESET_CMD(%RESET_CMD) = 'RES/ET' [.B (ALL|RESET_LIST)]
  RESET_LIST = <(','|.B),RESET_TYPE>
    RESET_TYPE = (USE|SPAN|TYPE)
      USE(%USE) = 'U/SE'
      SPAN(%SPAN) = 'S/PAN'
      TYPE(%TYPE) = 'T/YPE'
/**/
REPORT_CMD(%REPORT_CMD) = 'RE/PORT' REP_CLAUSE1
  REP_CLAUSE1 = REP_OPT [.B ON_CLAUSE] [.B SUM_CLAUSE]
    REP_OPT(L=%RAW) = (.B 'RAW'|.B 'DET/AIL'|.B 'SUM'| REP_DFL)
    REP_DFL(%RAW) = (.B | .END)
  REP_CLAUSE2 = DATA_OPT .B ON_CLAUSE
    DATA_OPT(%DATA) = 'DATA'
    ON_CLAUSE(%ON_CLAUSE) = (ON_N|OVER_N|INTO_N) .B .FID
      ON_N(%ON) = ('ON'|'TO'|'CREATING')
      OVER_N(%OVER) = ('OVER'|'REPLACING')
      INTO_N(%INTO) = ('INTO'|'APPENDING')
    SUM_CLAUSE(%SUM_CLAUSE) = ['WITH' .B] 'SUM/MARY'
/**/
END_CMD(%END_CMD) = ('E/ND'|'X/IT'|'Q/UIT')
/**/
NULL_CMD(%NULL_CMD) = [.B]
