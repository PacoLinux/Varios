/*M*  PARTRGE parse nodes for LINKNODES commands              */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
 
/*  CMDLINE -- This is the parse nodes for parsing the $QSP system
               variable that is setup by the GETLOADED.X command
               line processor.  The syntax of the expected value of
               $QSP is:
 
               $QSP_'SI=fidlist,OU=fid,verb,(options)'
 
              WHERE:
                 fidlist    is either a single file name or a list
                 of filenames separated by ',' all within parenthesis.
 
                 fid      is a single file name.
 
                 verb     is one of the keywords ON, OVER or INTO.
 
                 options  are the options accepted by the LINKNODES.X
                 link command.
*/
CMDLINE (D) = <',',CMDLINEOPTS>
CMDLINEOPTS = {OPTIONS | VERBS | OUSPEC | SISPEC}
 
/*   COMMAND  -- This is the parse nodes for the commands accepted
                 by LINKNODES.X.  For a full explanation of the
                 meaning and use of these commands see the HELP file.
*/
 
COMMAND (D) = ( LINKCMD | MAPCMD | SEGIDCMD | SHARECMD | APLCMD  | ;
                MAILCMD | ENDCMD | DEBUGCMD | SEVCMD   | KEYCMD  | ;
                WSCMD   | STATUS                                  ) .e
 
LINKCMD (1) = 'LINK' .b SILIST [VERBS OUNAME] [OPTIONS] .e
MAPCMD  (2) = ('MAP' | 'LIST' ) OUNAME .e
 
SEGIDCMD    = 'SEG/ID'  ('=' | .b ) SEGIDS
SHARECMD    = 'SHA/RE'  ('=' | .b ) (SHAREYES | SHARENO)
APLCMD  (3) = 'APL'
MAILCMD (4) = 'MAIL'
ENDCMD  (5) = ( 'E/ND' | 'Q/UIT' | ['E'-] 'X/IT' ) .e
DEBUGCMD    = 'DEB/UG' ('=' | .b) ( DEB_ON | DEB_OFF | DEB_APL )
DEB_ON  (6) = 'YES'
DEB_OFF (7) = 'NO'
DEB_APL (8) = 'APL'
SEVCMD      = 'SEV/ERITY' ('='|.b) SEVNUM
SEVNUM  (9) = .dec1
KEYCMD      = 'KEY' ('=' | .b) (KEYQT | KEYAN)
KEYQT  (10) = .quote
KEYAN  (10) = .anum
 
WSCMD       = 'WS/ID' ('='|.b) (CLEAR_WS | WSID)
WSID    (11)= .fid
CLEAR_WS(11)= 'CLEAR WS'
SHAREYES(12)= 'Y/ES'
SHARENO (13)= 'N/O'
 
SEGIDS  (14,R) = ('DS1' | 'DS2' | 'DS3' | 'DS4' | 'DS5' | 'DS6' | ;
                  'DS7' | 'DS8' | 'AS0' | 'AS1' | 'AS2' | 'AS3' | ;
                  .oct4                                             )
 
SILIST      = <',',SINAME>
SINAME  (15)= .fid
STATUS  (16)= 'STATUS' .e
OUNAME  (17)= .fid
 
VERBS       = (ON | OVER | INTO)
ON      (18)= ('ON' | 'TO')
OVER    (19)= 'OVER'
INTO    (20)= 'INTO'
 
OPTIONS     = '(' <',',OPTLIST> ')'
OPTLIST     = {APLCMD   | MAILCMD  | DEBUGCMD | SEVCMD | KEYCMD | ;
               SHARECMD | SEGIDCMD | WSCMD                          }
 
OUSPEC      = 'OU' '=' OUNAME
SISPEC      = 'SI' '=' ( '(' SILIST ')'  |  SINAME )
 
