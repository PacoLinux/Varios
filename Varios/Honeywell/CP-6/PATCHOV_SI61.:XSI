PATCHOV: PROC NOAUTO;
%INCLUDE CP_6;
%INCLUDE B$JIT;
DCL B$JIT$ PTR SYMREF;
DCL M$SI DCB;
%FPT_OPEN (STCLASS=CONSTANT, DCB=M$SI, ASN=FILE, TEST=YES, FPARAM=FIRST);
%F$DCB;
%FPT_ALIB (LIBNAME=DNAME, CMD=CMD, STCLASS=CONSTANT, RETRN=NO);
%VLP_NAME (FPTN=DNAME, NAME='DELTA', STCLASS=CONSTANT);
DCL CMD CHAR(256) STATIC INIT('READ *NDEFS');
%FPT_SAD (FPTN=SADSEGS, MONSID='6174'O, USERSID='6030'O);
%FPT_SAD (FPTN=SADMPT, MONSID='6037'O, USERSID='6030'O);
%FPT_CVM;
DCL SD1$S SBIN CONSTANT INIT(%BITBIN('6030'O));
DCL SD1$ REDEF SD1$S PTR;
DCL 1 SEGS(0:0) BASED(SD1$) ALIGNED,
  2 OFFSET SBIN(18) UNAL,
  2 SIZE SBIN(18) UNAL;
DCL FIRST SBIN STATIC;
DCL O SBIN STATIC INIT(1);
%EJECT;
   IF SUBSTR(DCBADDR(DCBNUM(M$SI))->F$DCB.NAME#.C,0,6)~='*NDEFS'
   THEN CALL MAKEXEQ;
   CALL M$OPEN (FPT_OPEN);
   O=ASCBIN(SUBSTR(DCBADDR(DCBNUM(M$SI))->F$DCB.TYPE#,0,1))-ASCBIN('0');
   CALL M$SAD (SADSEGS);
   FIRST=SEGS.SIZE(0)/4-64;
   O=SEGS.OFFSET(O)/4;
   DO WHILE '1'B;
      FPT_CVM.V.VPNO#=FIRST;
      IF SEGS.OFFSET(O)<0 OR FIRST=256 THEN EXIT;
      FPT_CVM.V.PPNO#=SEGS.OFFSET(O);
      CALL M$CVM (FPT_CVM);
      O=O+1;
      FIRST=FIRST+1;
   END;
   FIRST=SEGS.SIZE(0)/4;
   CALL M$SAD (SADMPT);
   DO WHILE '1'B;
      FIRST = FIRST-1;
      FPT_CVM.V.PPNO#=SEGS.OFFSET(FIRST);
      FPT_CVM.V.VPNO#=FIRST-64;
      CALL M$CVM (FPT_CVM);
      IF FIRST=66 THEN EXIT;
   END;
   CALL M$ALIB (FPT_ALIB);
   CALL M$EXIT;
%EJECT;
MAKEXEQ: PROC;
%EQU T=CHARTEXT('2 * BIT(9) INIT(''0''B),2 * CHAR(0) INIT');
%FPT_OPEN(FPTN=OPENJF,RES='JF',ASN=DEVICE,FUN=CREATE,DCB=M$SI,STCLASS=CONSTANT);
%FPT_WRITE(FPTN=WRITEJF,DCB=M$SI,BUF=CMD);
%FPT_CLOSE(FPTN=CLOSEJF,DCB=M$SI,DISP=SAVE);
DCL TEXT BASED CHAR(256);
DCL 1 T CONSTANT,
%T('!DEFAULT RUACCT=X,OVERLAY=''.'',SHOWDEFS=0'),
%T('!LET NODENAME=''OVERLAY'''),
%T('!IF ''OVERLAY''=''.'' THEN LET NODENAME=$INPUT(''Overlay name? '')'),
%T('!XMIT ME(NN=''%NODENAME'',KILL=''%$SUBSTR(''KILL'',0,4-4* SHOWDEFS)'') OVER JF'),
%T('!SET M$ME NO'),
%T('!SET M$DO NO'),
%T('!C M:MON.:SYS(O''036000000000000'') OVER *NDEFS(LN)'),
%T('!E *NDEFS'),
%T('SE1;/B$/A1;CP2'),
%T('SE2-99;2/B$/A1;IF/B$/,3,20;CP'),
%T('SE;/-/E/ /'),
%T('Q'),
%T('!IF ''NN''=''?'' THEN C *NDEFS TO ME'),
%T('!C *NDEFS OVER *NDEFS(K=S,LN)'),
%T('!E *NDEFS'),
%T('IF NOT/NN/;DE'),
%T('6L5;//?/ - /?/ /E/!C NO OVER *NDEFS(TY=/?1/)/'),
%T('Q'),
%T('!IF $FID_RECS(''*NDEFS'')=0 THEN GOTO BADNAME'),
%T('!XEQ *NDEFS'),
%T('!RUM M:MON.:SYS,I'),
%T('OUTPUT INTO *NDEFS'),
%T('UN NN'),
%T('NAME E'),
%T('END'),
%T('!CA *NDEFS OVER .(LN,SVFC)'),
%T('!E *NDEFS'),
%T('CL1,80'),
%T('0/ /S/#/;0/No/O/Yos/;2/Y/O/###/;IF NOT/Yes/;DE'),
%T('0/##/S/#/;0/##/S/#/;0/##/S/#/;0/##/S/#/;0/##/S/#/;0/#/S/ /'),
%T('4/ /A/ /;//?/ /?/ /E/DEF /?2/ /?1//'),
%T('IN.1,80'),
%T('ECH; READ ME'),
%T('CP100;1E/KILL ECH; SCH M:MON.:SYS/'),
%T('Q'),
%T('!BADNAME:'),
%T('!R M$ME'),
%T('!R M$DO'),
%T('!IF $FID_RECS(''*NDEFS'')=0 THEN OUTPUT ''That overlay does not exist.'''),
%T('!IF $FID_RECS(''*NDEFS'')=0 THEN QUIT'),
%T('!!EOD'),
%T('!PATCHOV.RUACCT *NDEFS'),
%T('!EOD'),
2 * BIT(18) INIT('0'B);
   CALL M$OPEN(OPENJF);
   CALL INDEX(O,' ',B$JIT.CCBUF);
   CALL CONCAT(CMD,'!XMIT ME(',SUBSTR(B$JIT.CCBUF,O,B$JIT.CCARS-O));
   O=9+B$JIT.CCARS-O;
   IF SUBSTR(CMD,9)=' ' THEN SUBSTR(CMD,8,1)=' ';
   ELSE SUBSTR(CMD,O,1)=')';
   SUBSTR(CMD,O+1)=' TO JF';
   WRITEJF.BUF_.BOUND=O+6;
   CALL M$WRITE(WRITEJF);
   WRITEJF.BUF_.BUF$=PINCRC(ADDR(T),1);
   DO WHILE '1'B;
      CALL INDEX(O,BINASC(0),WRITEJF.BUF_.BUF$->TEXT);
      IF O=0 THEN EXIT;
      WRITEJF.BUF_.BOUND=O-1;
      CALL M$WRITE(WRITEJF);
      WRITEJF.BUF_.BUF$=PINCRC(WRITEJF.BUF_.BUF$,O+1);
      END;
   CALL M$CLOSE(CLOSEJF);
   CALL M$EXIT;
END MAKEXEQ;
END PATCHOV;
