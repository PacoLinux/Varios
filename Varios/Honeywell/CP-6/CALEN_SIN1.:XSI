/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
   CALENDARCMD (0) = (['('] <',',OPTIONLIST> [')'] | .B) .END
 
       OPTIONLIST = (YEAR | SEMESTER | MONTH |  SCHEDULE ;
                    | QUARTER | END | FORMAT | SCREEN)
 
           YEAR (0) = ['DI/SPLAY'] ['Y/EAR' ('=' | .B)] .DEC
 
           SEMESTER (1) = ['DI/SPLAY'] (S1 | S2)
 
               S1 = 'S/EMESTER' ('=' |.B) (SNUM | SNAME)
 
               S2 = SNAME .B 'S/EMESTER'
 
               SNUM (0) = .DEC
 
               SNAME (1,LIT=0) = ('SP/RING' | 'SU/MMER' | 'FA/LL')
 
           MONTH (2) = ['DI/SPLAY']  (MON | MONNAME | EXPMON)
 
               MON (0) = 'M/ONTH' ('=' | .B)  {SEMON | SMON | CMON}
 
                   SEMON (0) =  .DEC '-' .DEC
 
                   SMON (1) = .DEC
 
                   CMON (2) = '(' <',',.DEC> ')'
 
               MONNAME (1) = ['M/ONTH' ('=' | .B)] {SEMONN | SMONN | CMONN}
 
                   SEMONN (0) = MNAME '-' MNAME
 
                   SMONN (1)  = MNAME
 
                   CMONN (2)   = '(' <',',MNAME> ')'
 
                       MNAME (LIT = 1) = ('JAN/UARY'  ;
                                         |'FEB/RUARY' ;
                                         |'MAR/CH'    ;
                                         |'AP/RIL'    ;
                                         |'MAY'       ;
                                         |'JUN/E'     ;
                                         |'JUL/Y'     ;
                                         |'AU/GUST'   ;
                                         |'SEP/TEMBER';
                                         |'OC/TOBER'  ;
                                         |'NO/VEMBER' ;
                                         |'DE/CEMBER')
 
               EXPMON (2) = ['M/ONTH' ('=' | .B)] (THIS | NEXT | ALL)
 
                   THIS (1) = 'TH/IS'
 
                   NEXT (2) = 'NE/XT' [.DEC]
 
                   ALL  (3) = 'ALL'
 
           SCHEDULE (3) = 'SCH/EDULE' ['='] ON_OFF_ALL
 
               ON_OFF_ALL (LIT = 1) = ('ON' | 'OFF' | 'ALL')
 
           QUARTER (4) = ['DI/SPLAY'] (Q1 | Q2)
 
               Q1 = 'Q/UARTER' ('=' | .B) (QNUM | QNAME)
 
               Q2 = QNAME .B 'Q/UARTER'
 
               QNUM (0) = .DEC
 
               QNAME (1,LIT=0) = ('WI/NTER' | 'SP/RING' | 'SU/MMER' | 'FA/LL')
 
           END (5) = 'E/ND'
 
           FORMAT (6) = 'FO/RMAT' ['='] ON_OFF
 
           SCREEN (7) = 'SC/REEN' ['='] ON_OFF
 
               ON_OFF (LIT=1) = ('ON' | 'OFF')
