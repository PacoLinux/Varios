/*M* CP6-LNCOUNT_PAR1 - Partrge Nodes                              */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X* AND  */
/***
***/
 
%INCLUDE XU_SUBS_C;
 
%EQU DA = 1;                            /* DATA                               */
%EQU SO = 2;                            /* SOURCE                             */
%EQU UP = 3;                            /* UPDATE                             */
%EQU SR = 4;                            /* SREPORT                            */
%EQU UR = 5;                            /* UREPORT                            */
%EQU LI = 6;                            /* LIST                               */
%EQU TI = 7;                            /* TITLE                              */
%EQU DE = 8;                            /* DELETE                             */
%EQU DF = 9;                            /* DFILE                              */
%EQU XE = 10;                           /* XEQ                                */
%EQU QU = 11;                           /* QUIT, END, or X                    */
%EQU ON = 1;                            /* ON                                 */
%EQU IN = 2;                            /* INTO                               */
%EQU OV = 3;                            /* OVER                               */
%EQU ACCT = 100;                        /* ACCOUNT SUB                        */
%EQU FID  = 101;                        /* FID SUB                            */
%EQU TAG  = 102;                        /* TAG SUB                            */
%EQU ASTR = 103;                        /* A STRING SUB                       */
 
COMMANDS = (DATA    |;
            SOURCE  |;
            UPDATE  |;
            SREPORT |;
            UREPORT |;
            LIST    |;
            TITLE   |;
            DELETE  |;
            DFILE   |;
            XEQ     |;
            QUIT ) .END
 
DATA   (%DA) = 'DA/TA' FID
SOURCE (%SO) = 'SO/URCE' .B ACCT ',' TAG
UPDATE (%UP) = 'UP/DATE' .B ACCT ',' ACCT ',' TAG
SREPORT(%SR) = 'SR/EPORT' .B [ ( ON| INTO| OVER) FID ] ',' TAG ',' TAG
UREPORT(%UR) = 'UR/EPORT' .B [ ( ON| INTO| OVER) FID ] ',' TAG ',' TAG
LIST   (%LI) = 'LI/ST' FID
TITLE  (%TI) = 'TI/TLE' ASTR
DELETE (%DE) = 'DE/LETE' .B TAG
DFILE  (%DF) = 'DF/ILE'
XEQ    (%XE) = 'XE/Q' FID
QUIT   (%QU) = ( 'Q/UIT'| 'E/ND'| 'X' )
 
ACCT (%ACCT) = ['.'] .AN8+':'
FID  (%FID)  =  .B .FID
TAG  (%TAG)  = [.B] .AN4
ASTR (%ASTR) = '=' .AS
ON   (%ON)   = ('ON'|'TO')
INTO (%IN)   = 'IN/TO'
OVER (%OV)   = 'OV/ER'
 
/* END */
