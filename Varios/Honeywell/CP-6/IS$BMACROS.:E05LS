

14:38 JUL 28 '97 IS$BMACROS.:E05SI                                         1    
    1      *T***********************************************************
    2      *T*                                                         *
    3      *T* Copyright (c) Bull HN Information Systems Inc., 1997    *
    4      *T*                                                         *
    5      *T***********************************************************
    6      *  AARDVARK MACROS FOLLOW
    7      MINIT  MACRO
    8      PLST1  PTR     S.AIS,ALST1     POINTER LIST FOR MINIQC
    9             PTR     S.AIS,ALST1+1
   10             PTR     S.AIS,ALST1+2
   11             PTR     S.AIS,ALST1+3
   12             PTR     S.AIS,ALST1+4
   13      DLST1  OCT     0               DESC LIST
   14      ALST1  BSS     5               VALUES PASSED TO MINIQ
   15      TBUF.N ZERO    0,80            LENGTH OF TBUF.C
   16      TBUF.C BSS     20              TEMPORARY BUFFER FOR INTERFACE TO PL6
   17             ENDM
   18      WAIT   MACRO
   19             INHIB   ON
   20             DIS     #1,DU
   21             TRA     *-1
   22             INHIB   OFF
   23             ENDM
   24      PUSH   MACRO
   25             IFE     AARDVARK,0,1
   26             SYMREF  SPW1
   27             SREG    SPW1,AD         SAVE ENVIRONMENT
   28             STD     DR.USE,TEMPD    NEVER LET DR5 GET CLOBBERED!!
   29             ENDM
   30      PULL   MACRO
   31             IFE     AARDVARK,0,1
   32             SYMREF  SPW1
   33             LREG    SPW1,SD         RESTORE ENVIRONMENT
   34             LDP     DR.USE,I$TEMP$
   35             LDP     DR.SSR,DSSR,DL
   36             LDP     DR.SSR,DTYP,DL
   37             ENDM
14:38 JUL 28 '97 IS$BMACROS.:E05SI                                         2    
   38      PSHAQ  MACRO                   SAVE AQ IN STACK3
   39             IFE     AARDVARK,0,1
   40             SYMREF  SPW3
   41             STAQ    SPW3,AD
   42             ENDM
   43      PLLAQ  MACRO                   PULL AQ FROM STACK3
   44             IFE     AARDVARK,0,1
   45             SYMREF  SPW3
   46             LDAQ    SPW3,SD
   47             ENDM
   48      SIMR   MACRO                   SAVE INTERRUPT MASK REGISTER
   49             SZN     S1000_FLG       ARE WE ON AN S1000?
   50             TNZ     *+8             YES
   51             RMCM    0               NO  -  LOAD MASK
   52             STAQ    SPWI,AD         SAVE IT ON ITS OWN STACK
   53             ANA     =O017,DL        KEEP PORT INFO
   54             ANQ     =O017,DL
   55             STAQ    PMASK           SAVE THIS FOR LATER
   56             SMCM    0               DISABLE INTERRUPTS
   57             TRA     *+5             EXIT
   58             RIMR                    LOAD MASK
   59             STAQ    SPWI,AD         SAVE ON STACK
   60             EAA     0               DISABLE
   61             LIMR                    INTERRUPTS
   62             ENDM
   63      RIMR1  MACRO                   RESTORE INTERRUPT MASK REGISTER
   64             LDAQ    SPWI,SD         PULL FROM SPECIAL STACK
   65             SZN     S1000_FLG       ARE WE ON AN S1000
   66             TNZ     *+4             YES
   67             ORAQ    PMASK           MAKE SURE PORTS ARE ENABLED
   68             SMCM    0               ENABLE OR WHATEVER
   69             TRA     *+2             EXIT
   70             LIMR                    RESTORE OLD INTERRUPT MASK
   71             ENDM
   72      SFAULT MACRO                   SAVE AND SWITCH FAULT ENTRY DESCRIPTORS
   73             IFE     AARDVARK,0,4
   74             SYMREF  FLTLOC
14:38 JUL 28 '97 IS$BMACROS.:E05SI                                         3    
   75             SYMREF  AFLTLC
   76             SYMREF  AAFLTD
   77             SYMREF  SPWF
   78             LDAQ    FLTLOC          OLD VALUE
   79             STAQ    SPWF,AD         PUSH IT ON ITS OWN STACK
   80             LDAQ    AAFLTD          GET AARDVARK'S FAULT DESCRIPTOR
   81             STAQ    FLTLOC          PLAANT IT
   82             STAQ    AFLTLC
   83             ENDM
   84      RFAULT MACRO                   RESTORE OLD FAULT ENTRY DESCRIPTOR
   85             IFE     AARDVARK,0,3
   86             SYMREF  FLTLOC
   87             SYMREF  AFLTLC
   88             SYMREF  SPWF
   89             LDAQ    SPWF,SD         PULL IT FROM ITS STACK
   90             STAQ    FLTLOC          AND RE-PLANT IT
   91             STAQ    AFLTLC
   92             ENDM
   93      DELTA  MACRO                   BUILT IN BREAKPOINT - GOES TO DELTA
   94             DRL
   95             ENDM
   96      DDCTX  MACRO                   X5 = X4 + X4
   97             EAA     0,4             CONVERT DCTX
   98             ALS     1               TO DOUBLEWORD
   99             EAX5    0,AU            INDEX
  100             ENDM
  101      TYPE   MACRO                   TYPE MESSAGE ON OC
  102             IFE     AARDVARK,0,1
  103             ENTREF  TYPEC
  104             SXL4    SPW2,I          PUSH X4
  105             STX3    SPW2,ID         AND X3 INTO 1 WORD
  106             LDA     #1-1            PICK UP TALLYB WORD BEFORE MSG
  107             EPPR1   0,AU            MLR'S WILL USE AR1
  108             ANA     =O777777,DL     MASK OFF ADDRESS
  109             ARS     4               JUSTIFY THE LENGTH PORTION IN BYTES
  110             EAX5    0,AL            LENGTH IN X5
  111             MLR     (1,1),(,1),=O040
14:38 JUL 28 '97 IS$BMACROS.:E05SI                                         4    
  112             ADSC9   0,,13,1
  113             ADSC9   TBUF.C,0,13
  114             SXL5    TBUF.N          TBUF IS THE FORMAT THAT TYPE WANTS
  115             EPPR0   TBUF.N          PASS TBUF TO PL6
  116             STP0    PLST1
  117             LDA     1,DU
  118             STA     DLST1
  119             EPPR0   PLST1
  120             EPPR1   DLST1
  121             TSX1    TYPEC           OUTPUT TO CONSOLE
  122             NOP                     ERROR RETURN
  123             EPPR0   ALST1           RESTORE PLST1
  124             STP0    PLST1
  125             LDX3    SPW2,DI         PULL X3
  126             LXL4    SPW2,I          AND X4
  127             ENDM
  128      TYPEST MACRO                   TYPE MESSAGE ON OC - FOLLOWED BY STATUS
  129             SXL4    SPW2,I          PUSH X4
  130             STX3    SPW2,ID         AND X3 INTO 1 WORD
  131             LDA     #1-1            TALLYB WORD PRECEEDS MESSAGE
  132             LDX3    1,DU            FCN CODE FOR WRITE CONSOLE ASCII
  133      *      EAX4    -DCT1           RELOCATION
  134             EAX4    OCINFO,X4       DEVICE INDEX FOR CONSOLE
  135             TSX7    MINIQ_PL6
  136             NOP                     ERROR RETURN
  137      *      LDQ     DCT13,X5        GET STATUS WORD(X5 IS OKAY)
  138             LDA     STATUSTW        OUTPUT TALLY WORD
  139             TSX7    BINCHR          FILL IN OUTPUT
  140             EAX3    2               FCN CODE WRITE CONSOLE ASCII(NO CR/LF)
  141             LDA     STATUSM-1       TALLY WORD PRECEEDS MESSAGE
  142             TSX7    MINIQ_PL6
  143             NOP                     ERROR RETURN
  144             LDX3    SPW2,DI         PULL X3
  145             LXL4    SPW2,I          AND X4
  146             ENDM
  147      KEYIN  MACRO                   M:KEYIN
  148             SXL4    SPW2,I          PUSH X4
14:38 JUL 28 '97 IS$BMACROS.:E05SI                                         5    
  149             STX3    SPW2,ID         AND X3 INTO 1 WORD
  150             LDA     #1-1            TALLYB WORD PRECEEDS MESSAGE
  151             EPPR1   0,AU            MLR'S WILL USE AR1
  152             ANA     =O777777,DL     MASK OFF ADDRESS
  153             ARS     6               JUSTIFY THE LENGTH PORTION
  154             EAX5    0,AL            LENGTH IN X5
  155             MLR     (1,1),(,1),=O040
  156             ADSC9   0,,13,1
  157             ADSC9   TBUF.C,0,13
  158             SXL5    TBUF.N          TBUF IS THE FORMAT THAT TYPE WANTS
  159             EPPR0   TBUF.N          PASS TBUF TO PL6
  160             STP0    ALST1
  161             STP0    ALST1+1
  162             LDA     2,DU
  163             STA     DLST1
  164             EPPR0   PLST1
  165             EPPR1   DLST1
  166             TSX1    KEYIN           DO THE I/O OPERATIONS
  167             DELTA                   CALL DELTA IF SOMETHING GOES WRONG
  168             LDA     #2-1            TALLYB FOR RESPONSE BUFFER
  169             EPPR1   0,AU            MAKE POINTER FOR MLR
  170             ANA     =O777777,DL     MASK OFF ADDRESS PORTION
  171             ARS     6               SCRAP TALLY JUNK
  172             EAX5    0,AL            LENGTH IN X5
  173             MLR     (,1),(1,1),=O040 MOVE IT TO RESPONSE BUFFER
  174             ADSC9   TBUF.C,0,13
  175             ADSC9   0,,13,1
  176             LDX3    SPW2,DI         RESTORE X3
  177             LXL4    SPW2,I          AND X4
  178             ENDM
  179      KEYINX  MACRO                   M:KEYINX - M:KEYIN WITH DEFAULT ON TIMEOUTS
  180             IFE     AARDVARK,0,4
  181             SYMREF  SPW2
  182             SYMREF  OCINFO
  183      *      SYMREF  DCT1
  184             ENTREF  MINIQ_PL6
  185             SXL4    SPW2,I          PUSH X4
14:38 JUL 28 '97 IS$BMACROS.:E05SI                                         6    
  186             STX3    SPW2,ID         AND X3 INTO 1 WORD
  187             LDA     #1-1            TALLYB WORD PRECEEDS MESSAGE
  188             LDX3    1,DU            FCN CODE FOR WRITE CONSOLE ASCII
  189      *      EAX4    -DCT1           RELOCATION
  190             EAX4    OCINFO,X4       DEVICE INDEX FOR CONSOLE
  191             TSX7    MINIQ_PL6
  192             NOP                     ERROR RETURN
  193             LDA     #2-1            TALLYB WORD PRECEEDS BUFFER
  194             LDX3    3,DU            READ CONSOLE ASCII RETURN ON TIMEOUT
  195             TSX7    MINIQ_PL6
  196             TRA     *+2             ERROR RETURN
  197             TRA     *+5             NORMAL RETURN
  198             SZN     RCVFLG          DON'T TIME OUT UNLESS RECOVERY
  199             TZE     *-6
  200             LDX7    =2U#3 ,DU       DEFAULT ANSWER
  201             STX7    #2              TO ANSWER BUFFER
  202             LDQ     #2-1
  203             QLS     12
  204             ANQ     =O7777,DU
  205             EAX3    0,QU
  206             EAQ     #2
  207             QLS     2
  208             TSX7    EDIT
  209             LDX3    SPW2,DI         PULL X3
  210             LXL4    SPW2,I          AND X4
  211             ENDM
  212      KEYINXX MACRO                   KEYIN WITH REPROMPT AFTER TIMEOUT
  213             IFE     AARDVARK,0,4
  214             SYMREF  SPW2
  215             SYMREF  OCINFO
  216      *      SYMREF  DCT1
  217             ENTREF  MINIQ_PL6
  218             SXL4    SPW2,I          PUSH X4
  219             STX3    SPW2,ID         AND X3 INTO 1 WORD
  220             LDA     #1-1            TALLYB WORD PRECEEDS MESSAGE
  221             LDX3    1,DU            FCN CODE FOR WRITE CONSOLE ASCII
  222      *      EAX4    -DCT1           RELOCATION
14:38 JUL 28 '97 IS$BMACROS.:E05SI                                         7    
  223             EAX4    OCINFO,X4       DEVICE INDEX FOR CONSOLE
  224             TSX7    MINIQ_PL6
  225             NOP                     ERROR RETURN
  226             LDA     #2-1            TALLYB WORD PRECEEDS BUFFER
  227             LDX3    3,DU            READ CONSOLE ASCII RETURN ON TIMEOUT
  228             TSX7    MINIQ_PL6
  229             TRA     *-9             TIMED OUT, REWRITE PROMPT
  230             LDQ     #2-1
  231             QLS     12
  232             ANQ     =O7777,DU
  233             EAX3    0,QU
  234             EAQ     #2
  235             QLS     2
  236             TSX7    EDIT
  237             LDX3    SPW2,DI         PULL X3
  238             LXL4    SPW2,I          AND X4
  239             ENDM
  240      PRINT  MACRO    PRINT ON LP#3 WITH # OF LINES TO SLEW (3 -> TOP OF PAGE)
  241             IFE     AARDVARK,0,3
  242             SYMREF  LPINFO
  243      *      SYMREF  DCT1
  244             ENTREF  MINIQ_PL6
  245             LDA     #1-1            TALLYB WORD PRECEEDS MESSAGE
  246             LDX3    #2,DU           FCN CODE FOR DOING IT
  247      *      EAX4    -DCT1           RELOCATION
  248             EAX4    LPINFO-1,X4      (DEVICE INDEX FOR LP1-1)
  249             ADLX4   #3,DU           ADD THE  PRINTER NUMBER
  250             TSX7    MINIQ_PL6
  251             DELTA                   ERROR RETURN
  252             ENDM
  253      READ   MACRO                   READ CARD    FROM CR
  254             IFE     AARDVARK,0,3
  255             SYMREF  CRINFO
  256      *      SYMREF  DCT1
  257             ENTREF  MINIQ_PL6
  258             LDA     #1-1            TALLYB WORD PRECEEDS BUFFER
  259             LDX3    0,DU            FCN CODE FOR READ  CARD    ASCII
14:38 JUL 28 '97 IS$BMACROS.:E05SI                                         8    
  260      *      EAX4    -DCT1           RELOCATION
  261             EAX4    CRINFO,X4       DEVICE INDEX FOR CARD READER
  262             TSX7    MINIQ_PL6
  263             DELTA                   ERROR RETURN
  264             ENDM
  265      REWIND MACRO                   REWIND TAPE
  266             IFE     AARDVARK,0,2
  267             SYMREF  DCTSIZ
  268             ENTREF  MINIQ_PL6
  269             LDX3    8,DU            FCN CODE FOR REWIND
  270             LDX4    #1,DU           DCT INDEX
  271             CMPX4   DCTSIZ+1,DU     IS DCTX REASONABLE
  272             TMI     *+2             YES -> USE IT
  273             LDX4    #1              NO -> ARG IS ADDRESS OF DCTX
  274             TSX7    MINIQ_PL6
  275             TRA     #2              ERROR RETURN
  276             ENDM
  277      BPI8   MACRO                   SET 800 BPI
  278             REWIND  #1,#2           BETTER PLAY IT SAFE
  279             LDX3    6,DU            FCN CODE FOR SET 800 BPI
  280             TSX7    MINIQ_PL6
  281             TRA     #2              ERROR RETURN
  282             ENDM
  283      BPI16  MACRO                   SET 1600 BPI
  284             REWIND  #1,#2           BETTER PLAY IT SAFE
  285             LDX3    7,DU            FCN CODE FOR SET 1600 BPI
  286             TSX7    MINIQ_PL6
  287             TRA     #2              ERROR RETURN
  288             ENDM
  289      QRET   MACRO                   RETURN FROM MINIQ
  290             SZNC    SPWQ,DI         DR7 NEEDED?
  291             TZE     #1,7            NO
  292             LDSS    SPWS,SD         YES -> ALSO RELOAD SSR
  293             TRA     #1,7,7          YES
  294             ENDM
  295      MBAB   MACRO                   MOVE BASE AND BOUND
  296             LDQ     DESC#1+1        BASE -> Q
14:38 JUL 28 '97 IS$BMACROS.:E05SI                                         9    
  297             STQ     2*#2+1,X2       BASE -> L.S.
  298             LDQ     DESC#1          BOUND -> Q(0->19)
  299             ANQ     =O777777600000  MASK OFF ALL BUT BOUND
  300             LDA     =O177777,DL     MASK
  301             ANSA    2*#2,X2         OFF OLD BOUND
  302             ORSQ    2*#2,X2         BOUND -> L.S.
  303             ENDM
  304      WRES   OPD     O12/2320,6/,O2/2,6/,O6/40,O4/3
  305      DIAG   OPD     O12/6120,6/,O2/2,6/,O6/2,O4/3
  306      RICHR  OPD     O12/1564,6/,O2/2,6/,O6/00,O4/3
  307      *  END OF AARDVARK MACROS

