/*M* CMLOAD_SIN1 - PARTRGE source file for the CMLOAD program      */
/*M* TCC#COMMANDS - PARTRGE SYMDEF of command nodes.               */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1990    */
/*T*                                                         */
/*T***********************************************************/
/*X* DFC=YES, DMC=YES, EDMC=YES                                    */
/*F*
NAME:           COMMANDS
 
The commands are entered following the "CMLOAD?" prompt.
The commands are used to READ the CM firmware tape, LOAD the
CM firmware into the controller, perform a power-on RESET to a
specific CM controller, and REPORT on the configuraton of the
IPC-IPI channel and the CM controller.
 
The commands accepted by the CMLOAD program are::
 
.       Command         Description
.       -------         ------------
.       LOAD            Load CM firmware into CM controller.
.       READ            Read CM firmware tape into memory.
.       REPORT          Report IPC or CM configuration.
.       RESET           Perform a power-on reset to a CM controller.
 
Further information can be obtained by typing:
 
HELP command
 
where "command" is the Command shown above.
                                                                   */
/********************** INCLUDE FILES ******************************/
%INCLUDE        CMLOAD_C64 ;
/********************** COMMANDS ***********************************/
/*B*
TCC#COMMANDS    is a PARTRGE SYMDEF that defines the nodes of the
      commands accepted by the CMLOAD program.
                                                                   */
TCC#COMMANDS(DEF)       = ( END | LOAD | READ | REPORT | RESET )
/*F*
NAME:           END
 
Syntax:         { E/ND | Q/UIT | X/IT }
 
.*B*            PARAMETERS
Parameters:     None.
 
.*B*            DESCRIPTION
Description:
 
The END command is used to terminate the CMLOAD program.
                                                                   */
END(TCC#END)            = ( 'E/ND' | 'Q/UIT' | 'X/IT' ) .END
/*F*
NAME:           LOAD
 
Syntax:         LOAD DCnn ( CM = m )
 
.*B*            PARAMETERS      nn
Parameters:
 
nn              specifies the disk controller name given to the
        IPC-IPI channel to which the CM is attached.  "nn" must be
        between 00 and 99, inclusive.
 
.*B*            m
m               specifies a single CM number.  "m" must be between
        0 and 7, inclusive.
 
.*B*            DESCRIPTION
Description:
 
The LOAD command loads the CM firmware into the selected CM(s)
attached to the specified disk controller (DCnn).
 
The CM firmware must have previously been read into memory using
the READ command.
 
The disk controller (DCnn) must have been previously partitioned
using SYSCON.  However, the IOM and the logical channels connected to
DCnn should be RETURNed (under SYSCON).
 
The LOAD command must be repeated for each CM connected to the
disk controller (DCnn).  Each CM must be LOADed with the same
firmware.
 
.*B*            EXAMPLES SAMPLES
Examples:
 
LOAD DC03(CM=1)         Loads CM firmware into CM 1 attached to DC03.
 
                                                                   */
/*
The following syntax was changed because of Star #40592 and the fact
that LADC did not have available more than one CM cabinet to test
the LOADing of more than one CM.  Since this couldn't be tested and
since this failed to work at a customer site, this syntax was
changed to only allow (and require) a single CM to be loaded at
a time.  The LOAD command now must be repeated for each CM connected
to the disk controller.
 
LOAD(TCC#LOAD)          = 'L/OAD' .B DCNN ;
                          [ '(' 'CM' '=' LOADOPTS ')' ]  .END
                                                                   */
LOAD(TCC#LOAD)          = 'L/OAD' .B DCNN '(' 'CM' '=' CMONE ')' .END
 
DCNN                    = 'DC'- .DEC2
 
 
LOADOPTS                = ( CMALL | CMLIST | CMONE )
 
CMALL(TCO#ALL)          = 'A/LL'
CMLIST(TCO#LIST)        = '(' < ',', .OCT1 > ')'
CMONE(TCO#ONE)          = .OCT1
 
/*F*
NAME:           READ
 
Syntax:         READ FT#sn [ ( DENSITY = { 800 | 1600 | 6250 } ) ]
 
.*B*            PARAMETERS sn
Parameters:
 
sn              specifies the serial number of the tape to be read.
.       "sn" must be the 1 to 6 character reel number used to mount
.       the firmware tape.
 
.*B*            DESCRIPTION
Description:
 
The READ command is used to read the CM firmware tape into memory.
 
If not specified, DENSITY=1600 is assumed.
 
.*B*            EXAMPLES  SAMPLES
Examples:
 
READ FT#CMIFAD          Reads the free-tape CMIFAD at 1600 bpi.
 
                                                                   */
READ(TCC#READ)          = 'REA/D' .B .FID ;
                          [ '(' 'D/ENSITY' '=' DENSOPTS ')' ] .END
 
DENSOPTS                = ( D800 | D1600 | D6250 )
 
D800(TCO#D800)          = '8/00'
D1600(TCO#D1600)        = '16/00'
D6250(TCO#D6250)        = '62/50'
 
/*F*
NAME:           REPORT
 
Syntax:         REPORT DCnn [ ( IPC | SUBSYSTEM ) ]
 
.*B*            PARAMETERS  nn
Parameters:
 
nn              specifies the disk controller name given to the
.       IPC-IPI channel to which the CM is attached.  "nn" must be
.       between 00 and 99, inclusive.
 
.*B*            IPC
IPC             specifies that the IPC-IPI configuration report
        is to be done.
 
.*B*            SUBSYSTEM SS
SUBSYSTEM       specifies that the MDS subsystem configuration report
        is to be done.  SUBSYSTEM may be abbreviated SS.
 
.*B*            DESCRIPTION
Description:
 
The REPORT command is used to report either the IPC or SUBSYSTEM
configuration for the selected disk controller (DCnn).  If not specified,
SUBSYSTEM is the default.
 
.*B*            EXAMPLES  SAMPLES
Examples:
 
REPORT DC01 (IPC)       Reports the configuration from the IPC-IPI.
 
REPORT DC02 (SUB)       Reports the subsystem configuration.
 
                                                                   */
REPORT(TCC#REPORT)      = 'REP/ORT' .B DCNN ;
                          [ '(' REPORTOPT ')' ] .END
 
REPORTOPT               = ( REPORTIPC | REPORTSS )
 
REPORTIPC(TCO#IPC)      = 'I/PC'
REPORTSS(TCO#SS)        = ( 'SU/BSYSTEM' | 'SS' )
/*F*
NAME:           RESET
 
Syntax:         RESET DCnn ( CM= {m|ALL|(m1,m2,...)} )
 
.*B*            PARAMETERS      nn
Parameters:
 
nn              specifies the disk controller name given to the
        IPC-IPI channel to which the CM is attached.  "nn" must be
        between 00 and 99, inclusive.
 
.*B*            m
m               specifies a single CM number.  "m" must be between
        0 and 7, inclusive.
 
.*B*            ALL
ALL             specifies ALL CMs attached to DCnn.
 
.*B*            m1 m2
m1,m2,...       specifies a list of CM numbers.  Each number must
        be between 0 and 7, inclusive.
 
.*B*            DESCRIPTION
Description:
 
The RESET issues a power-on RESET command to the selected CM(s)
attached to the specified disk controller (DCnn).
 
This command should only be attempted after attempts to LOAD the
CM have failed.  If the RESET is successful, the LOAD of the firmware
can be re-attempted.  If the CM fails to load again, manual interven-
tion will be necessary.
 
The disk controller (DCnn) must have been previously partitioned
using SYSCON.  However, the IOM and the logical channels connected to
DCnn should be RETURNed (under SYSCON).
 
.*B*            EXAMPLES SAMPLES
Examples:
 
RESET DC03(CM=1)         Issues a power-on reset to CM 1 attached to DC03.
 
RESET DC05(CM=(0,2,4))   Resets CMs 0, 2, and 4 attached to DC05.
                                                                   */
RESET(TCC#RESET)         = 'RES/ET' .B DCNN ;
                           '(' 'CM' '=' RESETOPTS ')'   .END
 
RESETOPTS                = ( CMALL | CMLIST | CMONE )
