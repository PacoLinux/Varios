/*M* CONGO - mini-command processor for CONSOLE GHOST screening */
/*T***********************************************************/
/*T*                                                         */
/*T* Copyright (c) Bull HN Information Systems Inc., 1989    */
/*T*                                                         */
/*T***********************************************************/
/*X* DMC,PLM=6,DIN=6,SDI=2,IND=3,CTI=3,CSI=3,ECI=3,DTI=3,DCI=6 */
%SET LISTSUB='1'B;
 
CONGO: PROC MAIN;
 
      DCL B$JIT$                PTR       SYMREF;
 
      DCL NOT_CONGO_BUFFER      CHAR(0)   CONSTANT INIT
          ('This logon is only allowed to be a console ghost');
      DCL NOT_FCPROC_BUFFER     CHAR(0)   CONSTANT INIT
          ('This processor may only be used as a first CPROC');
 
      %INCLUDE CP_6;
      %INCLUDE CP_6_SUBS;
      %INCLUDE B$JIT;
 
      %F$DCB;
/*
**
**    Remember CPs cannot have *ANY* static data, so all
**    fpts and such must be declared as constant and moved
**    into auto fpts to change any of the default data.
*/
      %VLP_NAME (FPTN=IBEX_NAME,
                 NAME='IBEX',
                 STCLASS=CONSTANT);
 
      %VLP_ACCT (FPTN=SYS_ACCT,
                 ACCT=':SYS',
                 STCLASS=CONSTANT);
 
      %FPT_CPEXIT (STCLASS=CONSTANT);
 
      %FPT_CPEXIT (FPTN=FPT$CPEXIT,
                   STCLASS=AUTO);
 
      %FPT_WRITE (FPTN=UC_WRITE,
                  DCB=M$UC,
                  STCLASS=CONSTANT);
 
      %FPT_WRITE (FPTN=UC$WRITE,
                  STCLASS=AUTO);
 
/*
**
**    First let's check if we are comming from LOGON.
*/
      IF NOT (B$JIT.CPFLAGS1 & %CP_FIRSTCP#) THEN
         DO;
            UC$WRITE = UC_WRITE;
            UC$WRITE.V_ = VECTOR(UC$WRITE.V);
            UC$WRITE.BUF_ = VECTOR(NOT_FCPROC_BUFFER);
            CALL M$WRITE (UC$WRITE);
            GOTO IBEX;
            END;
/*
**
**    Now, if we are not a console ghost, log 'em off.  The
**    CONSTANT fpt can be used for this.  If we are a console
**    ghost, we need to build an AUTO fpt to CPEXIT off to IBEX.
*/
      IF B$JIT.MODE ~= %M_INT# OR DCBADDR(DCBNUM(M$UC))->F$DCB.ASN# = %DEVICE# THEN
         DO;
            UC$WRITE = UC_WRITE;
            UC$WRITE.V_ = VECTOR(UC$WRITE.V);
            UC$WRITE.BUF_ = VECTOR(NOT_CONGO_BUFFER);
            CALL M$WRITE (UC$WRITE);
            CALL M$CPEXIT (FPT_CPEXIT);
            END;
      ELSE
IBEX:    DO;
            FPT$CPEXIT = FPT_CPEXIT;
            FPT$CPEXIT.V_ = VECTOR(FPT$CPEXIT.V);
            FPT$CPEXIT.NAME_ = VECTOR(IBEX_NAME);
            FPT$CPEXIT.ACCT_ = VECTOR(SYS_ACCT);
            CALL M$CPEXIT (FPT$CPEXIT);
            END;
 
END CONGO;
