/*
\section{ILCG grammar}
This is a definition of the grammer of ILCG using the Sable grammar specification
lanaguage. It is input to Sable to generate a parser for machine descriptions in
ilcg


\begin{verbatim}
*/




Package ilcg;
/*\end{verbatim}
\subsection{Helpers}
Helpers are regular expressions macros used in the definition of terminal symbols of the grammar.
\begin{verbatim}*/
Helpers
  letter = [['A'..'Z']+['a'..'z']];
  digit = ['0'..'9'];
  alphanum = [letter+['0'..'9']];
  cr = 13;
  lf = 10;
  tab = 9;
    digit_sequence = digit+;
    fractional_constant = digit_sequence? '.' digit_sequence | digit_sequence '.';
    sign = '+' | '-';
    exponent_part = ('e' | 'E') sign? digit_sequence;
    floating_suffix = 'f' | 'F' | 'l' | 'L';
  eol = cr lf | cr | lf;        // This takes care of different platforms
  not_cr_lf = [[32..127] - [cr + lf]];
  exponent = ('e'|'E');
  quote = ''';
  all =[0..127];
  schar = [all-'''];
  not_star = [all - '*'];
  not_star_slash = [not_star - '/'];
/*\end{verbatim}
\subsection{Tokens}
The tokens section defines the terminal symbols of the grammar.
\begin{verbatim}
*/
Tokens
  floating_constant = fractional_constant exponent_part? floating_suffix? |
        digit_sequence exponent_part floating_suffix?;
 /* \end{verbatim}
terminals specifying data formats \begin{verbatim}
*/
 void ='void';
 octet = 'octet'; int8 = 'int8'; uint8 = 'uint8';
 halfword = 'halfword'; int16 = 'int16' ; uint16 = 'uint16' ;
 word = 'word'; int32 = 'int32' ; 
 uint32 = 'uint32' ; ieee32 = 'ieee32';
 doubleword = 'doubleword'; int64 = 'int64' ; 
 uint64 = 'uint64'; ieee64 = 'ieee64';
 quadword = 'quadword';
/*\end{verbatim}
 terminals describing reserved words
\begin{verbatim}*/
 function= 'function';
 flag = 'flag';
 location = 'loc';
 procedure='instruction';
 returns ='returns';
 label = 'label';
 goto='goto';
 for ='for';
 to='to';
 step='step';
 do ='do';
 ref='ref';
 const='const';
 reg= 'register';
 operation = 'operation';
 alias = 'alias';
 instruction = 'instruction';
 address = 'address';
 vector ='vector';
 stack = 'stack';
 sideeffect='sideeffect';
 if ='if';
 reserved='reserved';
 precondition ='precondition';

 instructionset='instructionset';
/* \end{verbatim}terminals for describing new patterns\begin{verbatim} */
 pattern = 'pattern';
 means = 'means';
 assembles = 'assembles'; 

/*\end{verbatim} terminals specifying operators \begin{verbatim}*/
 colon = ':';
 semicolon= ';';
 comma = ',';
 dot = '.' ;
 bra ='(';
 
 ket =')';
 plus = '+';
 satplus = '+:';
 satminus = '-:';
 map='->';
 equals = '=';
 le = '<=';
 ge='>=';
 ne='<>';
 lt='<';
 gt='>';
 minus = '-';
 times = '*';
 exponentiate = '**';
 divide = 'div';
 and = 'AND';
 or = 'OR' ;
 xor = 'XOR';
 not = 'NOT';
 sin='SIN';
 cos='COS';
 abs='ABS';
 tan='TAN';
 remainder = 'MOD'; 
 store = ':=';
 deref = '^';
 push ='PUSH';
 pop ='POP';
 call='APPLY';
 full='FULL';
 empty='EMPTY';
 subscript='SUBSCRIPT'; 
 intlit = digit+;
 
 vbar = '|';
 sket=']';
 sbra='[';
 end='end';
 typetoken='type';
 mem='mem';
 string = quote schar+ quote;
/*\end{verbatim}

 identifiers come after reserved words in the grammar 
\begin{verbatim}

*/
 identifier = letter alphanum*;  
 blank = (' '|cr|lf|tab)+;
 comment = '/*' not_star* '*'+ (not_star_slash not_star* '*'+)* '/';

Ignored Tokens 
	blank,comment;
/*\end{verbatim}\subsection{Non terminal symbols}\begin{verbatim}
*/
Productions
 program = statementlist instructionlist;
 instructionlist =instructionset sbra alternatives sket;
/*\end{verbatim} non terminals specifying data formats \begin{verbatim}*/
 format = {octet} octet| {halfword} halfword |
          {word} word | {doubleword} doubleword |
          {quadword} quadword|{tformat}tformat;
 
 
/*\end{verbatim} non terminals corresponding to type descriptions \begin{verbatim}*/
 reference =   ref type ;
 array = vector bra number ket;
 aggregate={stack} stack bra number ket |{vector}array |{non};
 
 type={format} format |  {typeid}  typeid|{array}type array|{cartesian}sbra type cartesian* sket|
		{map}bra [arg]:type map [result]:type ket;
 cartesian =  comma type;
 
 tformat = {signed} signed|{unsigned}unsigned|{ieee32}ieee32|{ieee63}ieee64;
 signed = int32 | {int8} int8 | {int16} int16 | {int64} int64;
 unsigned = uint32 | {uint8} uint8 | {uint16} uint16 | 
            {uint64} uint64;
   

/*\end{verbatim} non terminals corresponding to typed values \begin{verbatim}*/
 value = {refval}refval |{rhs}rhs|{void}void|{cartval}cartval|
	{dyadic} dyadic bra [left]:value comma [right]:value ket|
	{monadic}monadic bra value ket;
/*\end{verbatim} value corresponding to a cartesian product type e.g. record initialisers \begin{verbatim}*/
 cartval=sbra value carttail* sket;
 carttail = comma value;
/*\end{verbatim} conditions used in defining control structures \begin{verbatim}*/
condition={dyadic} dyadic bra [left]:condition comma [right]:condition ket|
	{monadic}monadic bra condition ket |
	{id}identifier|
	{number}number;
 rhs=	{number}number| {cast}bra type ket value|{const}const identifier |
         {deref}deref  bra  refval ket
        |{castpop} bra type ket pop bra value ket;
 
 refval = loc| {refcast} bra reference ket loc;
 loc = {id}identifier|{memory}mem bra value ket ;
 

/*predeclaredregister = {fp}fp|{gp}gp;*/
 number =  {reallit} optionalsign reallit|
           {integer} optionalsign intlit;
 optionalsign = |{plus}plus|{minus}minus;
 reallit= floating_constant;
/*\end{verbatim} operators \begin{verbatim}*/ 
dyadic  = {plus} plus| {minus} minus | {identifier} identifier|{exp}exponentiate|
             	{times} times | {divide}  divide|{lt}lt|{gt}gt|{call}call|
		{le}le|{ge}ge|{eq}equals|{ne}ne|{push}push|{subscript}subscript|
		{satplus}satplus|{satminus}satminus|
             	{remainder} remainder|{or}or|{and}and|{xor}xor;
monadic ={not}not|{full}full|{empty}empty|/*{pop}pop|*/{sin}sin|{cos}cos|{tan}tan|{abs}abs ;
/*\end{verbatim} register declaration \begin{verbatim}*/
registerdecl =  reservation reg aggregate format identifier assembles sbra string sket  ;
reservation = {reserved}reserved|{unreserved};
aliasdecl =  
          alias reg aggregate format
          [child]:identifier equals [parent]:identifier bra [lowbit]:intlit colon [highbit]:intlit ket
          assembles sbra string sket;
	 
opdecl = operation identifier means operator assembles sbra string sket;
operator = {plus}plus|{minus}minus|{times}times|{lt}lt|{gt}gt|
		{le}le|{ge}ge|{eq}equals|{ne}ne|{divide} divide|
           {remainder}remainder|{or}or|{and}and|{xor}xor;

/*\end{verbatim} pattern declarations \begin{verbatim}*/
assign =  refval store value ;
meaning = {value}value|{assign}assign|{goto}goto value|{if}if bra value ket meaning|
	 {for} for refval store [start]:value to [stop]:value step [increment]:value do meaning| 
          {loc}location value;
patterndecl = pattern identifier paramlist means sbra meaning sket assemblesto sideeffects precond
               |
              {alternatives} pattern identifier means sbra alternatives sket;

paramlist = bra param paramtail* ket|{nullparam}bra ket;
param = typeid identifier|{typeparam} typetoken identifier|{label}label identifier;
typeid = identifier;
paramtail = comma param;
alternatives = type alts*; 
alts = vbar type;
precond = precondition sbra condition sket|{unconditional};
asideeffect=   sideeffect  returnval;
sideeffects = asideeffect*;
assemblesto=assembles sbra assemblypattern sket;
assemblypattern = assemblertoken*;
assemblertoken = {string} string | {identifier} identifier;
returnval = returns identifier;
/*\end{verbatim} statements \begin{verbatim}*/
statement = {aliasdecl} aliasdecl|
            {registerdecl} registerdecl |	    
		{addressmode} address patterndecl|
		{instructionformat}procedure patterndecl|
		{opdecl}opdecl|
		{flag} flag identifier equals intlit| 
		{typerename}typetoken format equals identifier|
		{patterndecl} patterndecl;
statementlist = statement semicolon statements*;
statements =  statement  semicolon;


// \end{verbatim}


