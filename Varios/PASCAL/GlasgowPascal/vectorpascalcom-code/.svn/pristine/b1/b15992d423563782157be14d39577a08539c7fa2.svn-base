<sect> ;
<sect>     -> <prelude>,<section>,<postlude>;
<section>  -> |nl>;
<section>  -> <seq> ;
<section>  -> <decls> ;
<section>  -> <program> ;
<section>  -> <unit> ;
<unit>     -> "unit",|"{\keyfn UNIT }">,<title>,
              <sep>,"interface",|"{\keyfn Interface }">,
              <uses>,<decls>,<unitend> ;
<program>  ->  "program",|" {\keyfn PROGRAM } "> ,<title>,
               <sep>,|nl>,|tabin>,<decls>,<dot>,|tabout> ;
<prelude>   -> <fl>,<fl>,|"\normfn ">,<tabon>,
               <fl>,|tabin>,|tabin> ;
<postlude>  -> |nl>,|tabout>,|tabout>,<fl>,<fl>,<taboff>,|"\rm "> ;
<taboff>    -> |" \end{tabbing} "> ;
<tabon>     -> |"\begin{tabbing}">,|nl>,<margin>,<tabs>,<tabs>,<tabs>,|nl> ;
<tabs>      -> |"  \ \ \ \= \ \ \ \=  \ \ \ \= \ \ \ \= \ \ \= "> ;
<tabin>     -> |" \+ "> ;
<tabout>    -> |" \- "> ;
<spaces>    -> |" \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ \ ">;
<margin>    ->  <spaces>,<spaces>,|" \= " >,<fl> ;
<title>     -> |"{\procfn ">,"identifier_",|emit>,|"\index{">,|emit>,|"}}">;
<id>        -> "identifier_",|emit>  ;
<unitend>   -> <dot> ;
<unitend>   -> <fin>,<dot> ;
<decl>      -> <category1>, <cdecls> ;
<decl>      -> <category2>,<vardecl> ;
<decl>      -> <category3>,<tdecls> ;
<decl>      -> <lables>,<explist> ;
<decl>      -> <procedure>;
<decl>      -> <function>;
<decl>      -> <imple>,|tabin>,<decls>,|tabout> ;
<decl>      -> <tag>;
<declsep>   -> <decl> ;
<declsep>   -> <decl>,<sep>;
<decls>     -> <block> ;
<decls>     -> <declsep> ;
<decls>     -> <declsep>,<decls> ;
<uses>      -> "uses",|"{\keyfn Uses} ">,<explist> ,<sep>;
<imple>     -> "implementation",|nl>,|"{\keyfn Implementation}">,|nl>;
<procedure> -> <proch> ;
<procedure> -> <proch>,"forward",|"{\keyfn FORWARD }"> ;
<procedure> -> <proch>,<decls> ;
<proch>     -> "procedure",|tabout>,<line>,|"{\keyfn PROCEDURE }">,
               <title>,<params>,<sep>,|nl>,|tabin>;
<function>  -> <funh> ;
<function>  -> <funh>,"forward",|"{\keyfn FORWARD }">  ;
<function>  -> <funh>,<decls>;
<funh>      -> "function",|tabout>,<line>,|"{\keyfn FUNCTION }">,
               <title>,<params>,<optype>,|nl>,|tabin> ;
<optype>   -> <colon>,<type>,<sep>;
<optype>
-> <sep>;
<params>    -> <null> ;
<params>    -> <var1> ;
<cdecl>     -> |"$">,<id>,|"$">,<cvalue> ;
<cvalue>    -> <null> ;
<cvalue>    -> "=",|" = ">,|"$">,<exp>,|"$"> ;
<cvalue>    -> <colon>,<type>,<cvalue> ;
<cdecls>    -> <cdecl> ;
<cdsep>     -> <cdecl>,<sep> ;
<cdecls>    -> <cdsep>;
<cdecls>    -> <cdsep>,<cdecls> ;
<cdecls>    -> <decls> ;
<address>   -> <exp> ;
<address>   -> <exp>,<colon>,<exp> ;
<avarhead>  -> |tabin>,|"$">,<exp>,|"$">;
<avar>      -> <avarhead> ;
<avar>      -> <avarhead>,<colon>,<type> ;
<varpref>   -> <avar> ;
<varpref>   -> <avar>,"absolute",|"{\keyfn absolute }">, <address> ;
<vartidy>   -> <sep>,|tabout> ;
<varnext>   -> <vartidy>,<vardecl> ;
<vardecl>   -> <null> ;
<vardecl>   -> <varpref> ;
<vardecl>   -> <varpref>,<vartidy>,<vardecl> ;
<vardecl>   -> "case",|"{\keyfn Case }">, <variant> ;
<vardecl>   -> <fin> ;
<vardecl>   -> <decls> ;
<atype>     -> <id>,"=",|"=">,<type> ;
<tdecls>    -> <null> ;
<tdecls>    -> <atype>,|tabout> ;
<tdecls>    -> |tabin>,<atype>,<vartidy>,<tdecls>;
<tdecls>    -> <decls> ;
<type>      -> <exp> ;
<type>      -> "array",|"{\keyfn Array }">,<brac>,<type>,<ket>,<of>,<type> ;
<of>        -> "of",|"{\keyfn of }"> ;
<type>      -> "set","of",|"{\keyfn Set of }">,<type> ;
<type>      -> "file", |"{\keyfn File }"> ;
<type>      -> "file","of",|"{\keyfn File of }">,<type> ;
<type>      -> "record",|"{\keyfn Record }">,|nl>,|tabin>,<recdecl> ;
<type>      -> "packed",|"{\keyfn Packed }">,<type> ;
<type>      -> "char",|"{\keyfn char }"> ;
<type>      -> "integer",|"{\keyfn integer }"> ;
<type>      -> "boolean",|"{\keyfn boolean }"> ;
<type>      -> "real",|"{\keyfn real }"> ;
<type>      -> "^",|"$\uparrow $">,<type> ;
<type>      -> <tag> ;
<recdecl>   -> <vardecl>;
<variant>   -> <sel>,<of>,<vars> ;
<sel>       -> <id> ;
<sel>       -> <id>,<colon>,<id> ;
<colon>     -> ":",|" : "> ;
<vars>      -> |" "> ;
<vars>      -> <variants> ;
<vars>      -> <variants> , <sep>, <vars> ;
<vars>      -> <fin> ;
<variants>  -> <exp> ,<colon>, <var1> ;
<var1>      -> "(",|"(">,    |tabin>,
               <vardecl>,")",|tabout>,|tabout>,|")"> ;
<category2> -> "var",<outdent>,|"{\keyfn  Var }">,|nl>;
<category1> -> "const",<outdent>,|"{\keyfn  Const }">,|nl>;
<category3> -> "type",<outdent>,|"{\keyfn Type }"> ,|nl>;
<lables>    -> "label",|"{\keyfn Label }"> ;
<outdent>   -> |tabout>,|nl>,|tabin> ;
<pseq>      -> <stat> ;
<pseq>      -> <stat> ,<sep>  ;
<seq>       -> <pseq> ;
<seq>       -> <pseq>,<seq>;
<seq>       -> <fin>  ;
<fin>       -> <reptail> ;
<fin>       ->  "end" ,|tabout>,|nl>,|"{\keyfn End}">  ;
<sep>       -> ";", |" ;"> ;
<label>
-> <int> ;
<label>         -> <id> ;
<nstat>     -> <null> ;
<nstat>     -> <stat> ;
<stat>      -> <lid> ;
<stat>      -> <id>,<colon> ,<stat>;
<stat>
    -> <int>,<colon>,<stat>;
<stat>      -> <tag> ;
<stat>      -> <assign>  ;
<stat>      -> <repeat> ;
<stat>      -> "goto",|"{\keyfn Goto }">,<label>;
<stat>      -> <with> ;
<stat>      -> <for>  ;
<stat>      -> <while> ;
<stat>      -> <block>  ;
<stat>      -> <ifstat> ;
<stat>      -> <case>,<casetop>, <casebot> ;
<ifstat>    -> "if",|"{\keyfn If }">,|"$">, <exp>, <then>, <choice> ;
<then>      -> "then",|"$">,|" {\keyfn then }">  ;
<choice>    -> <nstat> ;
<choice>    -> <stat>, "else",|nl>,|"{\keyfn else }" >, <stat> ;
<repeat>    -> "repeat", |"{\keyfn Repeat }">,|nl>,|tabin>, <seq>;
<reptail>   -> "until",|tabout>, |" {\keyfn Until }">,|"$">, <exp>,|"$"> ;
<while>     -> "while",|"{\keyfn While }">,|"$" >,<exp>,|"$">,<do> ;
<casetop>   -> <exp>,"of",|"$">,|" { \keyfn of} "> ;
<case>      -> "case",|"{\keyfn Case }">,|"$" >;
<casebot>   -> |nl>,|tabin>,<caselist> ;
<acase>     -> |"$">,<exp>,<colon>,|"$">,|nl>,|tabin>,<nstat> ;
<caselist>  -> <acase> ;
<caselist>  -> <acase>, ";", |" ;"> ,|nl> ,|tabout>,<caselist>;
<caselist>  -> <casetail> ;
<casetail>  -> "else", |"{\keyfn Else } ">, <stat>,<sepfin> ;
<casetail>  -> <fin> ;
<sepfin>    -> <fin> ;
<sepfin>    -> <sep>,<fin> ;
<for>       -> "for",|"{\keyfn For }">,<assign>,<dir>,<fortail>;
<fortail>   -> |"$">,<exp>,|"$">,<do> ;
<dir>       -> "to",|" {\keyfn to }">  ;
<dir>       -> "downto",|" {\keyfn downto }">  ;
<do>        -> "do" ,|" {\keyfn do }">,|nl>,|tabin>,<nstat>,|tabout>  ;
<assign>    -> <lid>, ":=", |"$">,|"\leftarrow ">,<exp>,|"$"> ;
<exp>       -> <term>  ;
<exp>       -> <term>,<op>,<exp>  ;
<exp>       -> <op>, <exp> ;
<exp>       -> "abs",|" \left| ">,<exp>,|" \right| "> ;
<term>      -> <atom> ;
<term>      -> <braketed>  ;
<term>      -> <tag> ;
<explist>   -> <exp>  ;
<explist>   -> <exp>,<colon>,<exp>,<sep>,<explist> ;
<atom>      -> <id>  ;
<atom>      -> <int> ;
<atom>      -> "string_",|"$">,|"`{\tt ">,|emit>,|"}'' ">,|"$"> ;
<atom>      -> <real> ;
<atom>      -> <id>, <subscript>  ;
<atom>      -> <op> ,<atom> ;
<int>       -> "int_",|emit> ;
<int>       -> "-",|"-">,<int> ;
<int>       -> "+",<int> ;
<real>      -> <int>, <exponent> ;
<real>      -> <int>,".",|".">,<realtail> ;
<realtail>  -> <int> ;
<realtail>  -> <int>, <exponent> ;
<exponent>  -> "e",|"\times 10^{">,<int>,|"}"> ;
<indir>     -> "^", |"\uparrow ">  ;
<field>     -> ".", |".">,<atom>  ;
<field>     -> "^.",|"\uparrow .">,<atom>;
<brac>      -> "[",|"["> ;
<ket>       -> "]",|"]" > ;
<braketed>  -> "[",|"\lbrack ">,<explist>,"]",|"\rbrack ">  ;
<braketed>  -> "(", |"(">,|tabin>, <explist>,|tabout>, ")", |")">  ;
<subscript> -> <field>  ;
<subscript> -> <indir>  ;
<subscript> -> <indir>, <subscript>  ;
<subscript> -> <braketed>  ;
<subscript> -> <braketed>, <subscript>  ;
<op>        -> "+", |"+">  ;
<op>        -> "-", |"-">  ;
<op>        -> "/", |"/">  ;
<op>        -> "not",|"\lnot "> ;
<op>        -> "shl", |"$ shl $">  ;
<op>        -> "shr", |"$ shr $">  ;
<op>        -> "@", |"@">  ;
<op>        -> "*", |"\times ">  ;
<op>        -> "div",|"\div ">  ;
<op>        -> "mod",|"$">,|"{\keyfn mod }">,|"$">  ;
<op>        -> "=", |"=">  ;
<op>        -> "<=",|"\leq ">  ;
<op>        -> "<", |"<">  ;
<op>        -> ">", |">">  ;
<op>        -> "/>", |"/>">  ;
<op>        -> "/<", |"/<">  ;
<op>        -> ">=",|"\geq ">  ;
<op>        -> "<>",|"\not= ">  ;
<op>        -> "in",|"\in ">  ;
<op>        -> "..", |"..">  ;
<op>        -> "or",|"$">,|"{\keyfn or }">,|"$" >  ;
<op>        -> "xor",|"$">,|"{\keyfn xor }">,|"$" >  ;
<op>        -> "and",|"$">,|"{\keyfn and }">,|"$" >  ;
<op>        -> "," , |", "> ;
<block>     -> "begin",|"{\keyfn Begin}">,|nl>,|tabin> , <seq>   ;
<with>      -> "with",|"{\keyfn With }">,|"$" > , <exp>,|"$">, <do> ;
<lid>       -> "identifier_" ,|"$" >  ;
<lid>       -> "identifier_" , |"$">,|emit > ,<subscript>,| "$" >  ;
<fl>        -> |flush> ;
<null>      -> |" "> ;
<tag>       -> "tag_",
               <ll>,|" \bf{">,|emit>,|" : see \ref{">,|emit>,|" prog}} " >,
   <rr> ;
<ll>        -> |"$\langle$ "> ;
<rr>        -> |"$\rangle$ "> ;
<line>   -> |nl>,|"\medbreak ">;
<dot>       -> ".", |".">,|nl>

