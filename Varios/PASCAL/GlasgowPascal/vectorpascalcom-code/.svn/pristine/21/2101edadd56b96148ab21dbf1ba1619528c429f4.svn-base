From:   Ryan Lanctot
Subj:   LZW Compression 1/6
-------------------------------

UNIT LZWUnit;
 
{ LZWUnit - Defines the LZWObj object to compress and uncompress      }
{           files using the LZW compression algorithm                 }
{ If you would like to create a file with a copy of the string        }
{ table LZWUnit used to compress the input file just define the       }
{ Debugging value in the next line                                    }
 
{Define Debugging}
 
{ ========================= } INTERFACE { =========================== }
 
USES Dos,Crt;
 
CONST
  MaxTableEntries = 3000;               {Set size of string code table}
TYPE
  TableBytes = (Prefix,Suffix,Link);
  LZWObj = OBJECT
    InF,OutF       : FILE;
    CodeTbl        : ARRAY[0..MaxTableEntries,Prefix..Link] OF Integer;
    HitTbl         : ARRAY[0..MaxTableEntries] OF Integer;
    PrefixCandidate: Integer;
    SuffixCandidate: Integer;
    TableFull      : Boolean;
    TableTop       : Integer;
    CONSTRUCTOR Init;
    FUNCTION ManageTbl: Integer; VIRTUAL;
    PROCEDURE InsertEntry; VIRTUAL;
    FUNCTION InputUnCompValue: Byte; VIRTUAL;
    FUNCTION InputCompValue: Integer; VIRTUAL;
    PROCEDURE OutputUnCompValue(B: Byte); VIRTUAL;
    PROCEDURE OutputCompValue(I: Integer); VIRTUAL;
    PROCEDURE CompressFile(InFile,OutFile : PathStr);
    PROCEDURE OutputTable(F:PathStr); VIRTUAL;
    FUNCTION ExpandValue(InputValue : Integer;
                         Output     : Boolean): Integer; VIRTUAL;
    PROCEDURE UnCompressFile(InFile,OutFile : PathStr);
    DESTRUCTOR CompressDone;
  END;

{ ====================== } IMPLEMENTATION { ========================= }
 
 
CONSTRUCTOR LZWObj.Init; {--------------------------------------------}
{  Initializes the string code table with the atomic values           }
{    and the table management values TableFull, TableTop, HitTbl[]    }
{  HitTbl is an array that contains the head pointer for linked       }
{    lists (yes multiple lists) of compression code to facilitate     }
{    faster lookup of PrefixCandidate-SuffixCandidate pairs.          }
{  If HitTbl[Prefix value] = 0 then no P-S entries with the Prefix    }
{    value have been added to the string table. If                    }
{    HitTbl[Prefix value] <> 0, it contains the entry number of the   }
{    first element in the string table with that Prefix value         }
{  The CodeTbl[X,Link] element will contain a 0 if the string table   }
{    does not have any other entries that start with the prefix in    }
{    CodeTbl[X,Prefix], otherwise CodeTbl[X,Link] points to the next  }
{    entry with a matching PrefixCandidate value                      }
{---------------------------------------------------------------------}
 
VAR I : Integer;
BEGIN
  TableFull := FALSE;
  TableTop := 255;
  FOR I := 0 TO MaxTableEntries DO BEGIN
    HitTbl[I] := 0;
    CodeTbl[I, Link] := 0;
    IF I > 255 THEN BEGIN
      CodeTbl[I, Prefix] := 0;
      CodeTbl[I, Suffix] := 0
      END
    ELSE BEGIN
      CodeTbl[I, Prefix] := -1;
      CodeTbl[I, Suffix] := I
      END
    END
  END;
 
 
FUNCTION LZWObj.ManageTbl: Integer; {=================================}
{  ManageTbl searches the Table for PrefixCandidate-SuffixCandidate   }
{    pairs of characters/codes. If the pair is not in the string      }
{    table, it adds them and updates the linked list (See INIT)       }
{    If the pair is found, it returns the entry number for the pair.  }
{=====================================================================}
 
VAR
  Found,                {Character pair Found}
  EndOfLinks : Boolean; {End of linked list found while searching list}
  CurPtr : Integer;     {Current element number in string table       }
BEGIN
 
  Found := FALSE;       {Initialize values}
  EndOfLinks := FALSE;
 
  IF HitTbl[PrefixCandidate] <> 0 THEN BEGIN {Entries exist for Prefix}
    CurPtr := HitTbl[PrefixCandidate];  {Trace list starting at head  }
    REPEAT
      IF (CodeTbl[CurPtr,Prefix] = PrefixCandidate) AND
         (CodeTbl[CurPtr,Suffix] = SuffixCandidate) THEN
         Found := TRUE
      ELSE                                    {Not found              }
        IF CodeTbl[CurPtr,Link] <> 0 THEN     {Check if at end of list}
          CurPtr := CodeTbl[CurPtr,Link]      {Get next element to chk}
        ELSE
          EndOfLinks := TRUE                  {End of list            }
      UNTIL Found OR EndOfLinks
    END;
 
  IF Found THEN                               {If pair found          }
    ManageTbl := CurPtr                       {  return element #     }
  ELSE BEGIN                                  {otherwise, add to table}
    IF NOT TableFull THEN BEGIN
      Inc(TableTop);
      InsertEntry;
      IF HitTbl[PrefixCandidate] = 0 THEN     {Adjust links           }
        HitTbl[PrefixCandidate] := TableTop
      ELSE
        CodeTbl[CurPtr,Link] := TableTop
      END;
    ManageTbl := -1;                          {Not found signal       }
    END;
  END;

PROCEDURE LZWObj.InsertEntry; {---------------------------------------}
{ Insert PrefixCandidate-SuffixCandidate into the next available      }
{   entry in the table                                                }
{---------------------------------------------------------------------}
 
BEGIN
  CodeTbl[TableTop, Prefix] := PrefixCandidate;
  CodeTbl[TableTop, Suffix] := SuffixCandidate;
  IF TableTop = MaxTableEntries THEN TableFull := TRUE;
  END;
 
{---------------------------------------------------------------------}
{ The next four methods provide input and output for file i/o         }
{---------------------------------------------------------------------}
 
FUNCTION LZWObj.InputUnCompValue: Byte;
VAR
  B : Byte;
BEGIN
  BlockRead(InF, B, 1);
  InputUnCompValue := B
  END;
 
FUNCTION LZWObj.InputCompValue: Integer;
VAR
  I : Integer;
BEGIN
  BlockRead(InF, I, 1);
  InputCompValue := I
  END;
 
PROCEDURE LZWObj.OutputUnCompValue(B: Byte);
BEGIN
  BlockWrite(OutF, B, 1)
  END;
 
PROCEDURE LZWObj.OutputCompValue(I: Integer);
BEGIN
  BlockWrite(OutF, I, 1)
  END;
 
 
PROCEDURE LZWObj.OutputTable(F:PathStr); {----------------------------}
{ OutputTable dumps a formatted list of the string table into the FILE}
{   specified in F.                                                   }
{---------------------------------------------------------------------}
 
VAR
  T : Text;
  I : Integer;
  J : TableBytes;
  S : String;
BEGIN
  Assign(T,F);
  Rewrite(T);
  FOR I:= 256 TO TableTop DO BEGIN
    Write(T,I:4,'  ',
          CodeTbl[I,Prefix]:4,'  ',
          CodeTbl[I,Suffix]:4,'  ',
          CodeTbl[I,Link]:4,'  ');
    FOR J := Prefix TO Suffix DO
      IF (CodeTbl[I,J] < 255) AND (CodeTbl[I,J] >= 32) THEN BEGIN
        S := Chr(Byte(CodeTbl[I,J]));
        Write(T,S,' ')
        END
      ELSE
        Write(T,'  ');
    WriteLn(T,' ')
    END;
  Close(T)
  END;

PROCEDURE LZWObj.CompressFile(InFile,OutFile : PathStr); {------------}
{  CompressFile manages all the compression tasks                     }
{---------------------------------------------------------------------}
 
VAR
  Ctr : Longint;           {Counter for onscreen display              }
  FoundCode : Integer;     {Used to manage results from ManageTbl code}
BEGIN
 
  Assign(InF,InFile);      {Open input file as 1 byte/record file     }
  Reset(InF,1);
  Assign(OutF,OutFile);    {Open output file as a 2 byte/record file  }
  Rewrite(OutF,2);         {  because we write out integers           }
 
  Ctr := 0;
 
  PrefixCandidate := InputUnCompValue;
 
  REPEAT
 
    Inc(Ctr);                          {Manage counter display}
    IF (Ctr AND 127) = 127 THEN BEGIN
      GotoXY(10,10);
      Write(Ctr);
      END;
 
    SuffixCandidate := InputUnCompValue;
 
    FoundCode := ManageTbl;                 {Search table for P-S pair}
 
    IF FoundCode >= 0 THEN                  {If pair found            }
      PrefixCandidate := FoundCode          {  go look for next pair  }
    ELSE BEGIN
      OutputCompValue(PrefixCandidate);     {otherwise, output Prefix }
      PrefixCandidate := SuffixCandidate    {  and reset for next pair}
      END
    UNTIL Eof(InF);
  OutputCompValue(PrefixCandidate);         {Write last character out }
 
  {$IFDEF Debugging}
  OutputTable('S:\COmpTbl.pas');
  {$ENDIF}
 
  END;

 
FUNCTION LZWObj.ExpandValue(InputValue : Integer; {-------------------}
                            Output:Boolean) : Integer;
{  ExpandValue expands compression codes. Note, if the prefix value   }
{    retrieved in KPrefix is another compression code, ExpandValue    }
{    will recursively call itself until KPrefix is an extended ASCII  }
{    character.                                                       }
{                                                                     }
{  Input:                                                             }
{    InputValue is the value to expand                                }
{    Output turns on/off writing of expanded characters to            }
{      file so you can retrieve (without writing) the first ASCIi     }
{      character at the head of the compressed character chain. This  }
{      becomes necessary when you must fill in the Suffix value in    }
{      string table for adjacent Prefix pointers.                     }
{  Output:                                                            }
{    Returns the character at the head of compressed byte chain when  }
{      you pass it a compressed byte. If you pass it an ASCII         }
{      character, it returns that character. This made coding simpler }
{---------------------------------------------------------------------}
 
VAR
  KPrefix, KSuffix, KReturned : Integer;
 
 BEGIN
  IF InputValue > 255 THEN BEGIN                 {Is compressed value?}
    KPrefix := CodeTbl[InputValue,Prefix];       {Yes, get table entry}
    KSuffix := CodeTbl[InputValue,Suffix];
    IF KPrefix > 255 THEN                        {If prefix is a      }
      KReturned := ExpandValue(KPrefix,Output)   { compressed char    }
    ELSE BEGIN                                   { recursively call   }
      KReturned := KPrefix;                      { ExpandValue        }
      IF Output THEN OutputUnCompValue(KPrefix)  {otherwise, set head }
                                                 { value and output   }
                                                 { uncompressed bytes }
      END;                                       { to file if Output  }
                                                 { set TRUE           }
    IF Output THEN OutputUnCompValue(KSuffix)
    END
  ELSE
    KReturned := InputValue; {Return ASCII value if passed ASCII value}
 
  ExpandValue := KReturned
  END;
 
 
PROCEDURE LZWObj.UnCompressFile(InFile,OutFile : PathStr); {----------}
{ UnCompresFile manages all aspects of uncompressing files            }
{---------------------------------------------------------------------}
 
VAR
  Ctr : Longint;                   {Onscreen info                     }
  Found : Integer;                 {Returned from ManageTbl routine   }
  Dummy, SuffixCopy, I :Integer;
 
BEGIN
  Assign(InF,InFile);            {Open input file to read integers    }
  Reset(InF,2);
  Assign(OutF,OutFile);          {Open output file to write characters}
  Rewrite(OutF,1);
 
  Ctr := 0;

  PrefixCandidate := InputCompValue;
 
  REPEAT
 
    Inc(Ctr);                               {Manage onscreen display  }
    IF (Ctr AND 127) = 127 THEN BEGIN
      GotoXY(10,10);
      Write(Ctr)
      END;
 
    IF PrefixCandidate < 256 THEN           {Output an ASCII character}
      OutputUnCompValue(PrefixCandidate);
 
    SuffixCandidate := InputCompValue;
 
    IF SuffixCandidate > 255 THEN BEGIN     {Compressed character?    }
 
      SuffixCopy := SuffixCandidate;   {Save just in case we expand it}
 
      {Handle special case when you need to expand an entry that you  }
      { have not yet added to table                                   }
 
      IF TableTop + 1 = SuffixCandidate THEN BEGIN
        SuffixCandidate := ExpandValue(PrefixCandidate,FALSE);
        Found := ManageTbl;
        SuffixCandidate := SuffixCopy;
        Dummy := ExpandValue(SuffixCandidate,TRUE);
        END
      ELSE BEGIN
        SuffixCandidate := ExpandValue(SuffixCandidate,TRUE); {Normal }
        Found := ManageTbl;                                   {expand }
        SuffixCandidate := SuffixCopy
        END
      END
    ELSE
      Found := ManageTbl;
    PrefixCandidate := SuffixCandidate
    UNTIL Eof(InF);
 
  IF PrefixCandidate < 256 THEN            {Output last character if  }
    OutputUnCompValue(PrefixCandidate)     { not a compressed code    }
 
  {$IFDEF Debugging}
  OutputTable('S:\Ucomptbl.pas');
  {$ENDIF}
  END;
 
DESTRUCTOR LZWObj.CompressDone; {-------------------------------------}
{ CompressDone closes the files.                                      }
{---------------------------------------------------------------------}
 
BEGIN
  Close(InF);
  Close(OutF)
  END;
 
 
END.

{ ===================== Test Program ================================ }

PROGRAM LZWTest;
 
{ Author: Gary Conway and Blake Ragsdell          }
{ This program compresses and uncompresses a file }
{ using LZW compression.                          }
 
USES Crt,Dos,LZWUnit;
 
VAR
  LZW       : LZWObj;
  InputFile : PathStr;
BEGIN
  ClrScr;
  InputFile := ParamStr(1);
  IF LENGTH(InputFile) = 0 THEN BEGIN
    WriteLn('Please include a filename on the command line.');
    Halt
    END;
  WriteLn('Compressing ',InputFile,' into OUTPUT.PAS');
  LZW.Init;
  LZW.CompressFile(InputFile,'OUTPUT.PAS');
  LZW.CompressDone;
  ClrScr;
  WriteLn('UnCompressing OUTPUT.PAS into RESTORED.PAS');
  LZW.Init;
  LZW.UnCompressFile('Output.PAS','Restored.PAS');
  LZW.CompressDone
END.

 * Origin: The Polish Connection [Westbank BC Canada] (1:353/240@FIDONET.ORG)

