/* KWL$READ_DISK_BUFFER.SPL, PrintMaster, Jeff Hostetler, November 23, 1983   */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

kwl$read_disk_buffer: proc (unit, p_buf);

%nolist;
%include '*>insert>gbl.ins.spl';
%include '*>insert>kwl.ins.spl';
%include '*>entry>err$.ins.spl';
%include '*>entry>gp$.ins.spl';
%include '*>entry>primos.ins.spl';
%list;

   dcl
      unit        bin,                      /* (in) */
      p_buf       ptr,                      /* (out) */

      fs          bin,                      /* kwl file status */
      (rnw, code) bin,

      data_size               bin,
      1  buf                  based,
         2  data_len          bin,
         2  data (data_size)  bin;

   p_buf = null;
   call prwf$$ (k$read+k$prea, unit, addr(fs), 1, 0, rnw, code);
   if code ^= 0  |  fs = kwl$kwl_incomplete then
      goto kwl_err;

   call prwf$$ (k$read, unit, addr(data_size), 1, 0, rnw, code);
   if code ^= 0 then
      goto kwl_err;

   allocate buf set (p_buf);
   p_buf->buf.data_len = data_size;

   call prwf$$ (k$read, unit, addrel(p_buf, 1), data_size, 0, rnw, code);

   if code = 0 then
      return;

   free p_buf->buf;
   p_buf = null;

kwl_err:
   call err$ (err$user_et, 'KWL file incomplete', err$kwl_incomplete_ue, 0, 0);
   return;
   end kwl$read_disk_buffer;
