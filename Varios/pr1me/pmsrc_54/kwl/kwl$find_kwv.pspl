/* KWL$FIND_KWV.SPL, PrintMaster, Jeff Hostetler, September 12, 1983          */
/* Procedure to find a kw (character varying) in a kwl.                       */
/*    Returns: TRUE if kw is found (p_kw will point to entry)                 */
/*             FALSE otherwise (p_kw will be null)                            */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

kwl$find_kwv: proc (kw, p_kwl, p_kw) returns (bit);

%nolist;
%include '*>insert>gbl.ins.spl';
%include '*>insert>kwl.ins.spl';
%list;

   dcl
      kw       char(*) var,                 /* kw to be found (in) */
      p_kwl    ptr,                         /* (in) */
      p_kw     ptr;                         /* ptr to kw_entry in kwl (out) */

   do p_kw = p_kwl repeat p_kw->kw_entry.next while (p_kw ^= null);
      if kw = p_kw->kw_entry.kw then        /* We found it... */
         return (true);
      end;

   return (false);
   end kwl$find_kwv;
