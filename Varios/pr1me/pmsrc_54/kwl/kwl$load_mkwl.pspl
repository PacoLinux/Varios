/* KWL$LOAD_MKWL.SPL, PrintMaster, Jeff Hostetler, September 12, 1983         */
/* Procedure to read the "master_kwl".                                        */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

kwl$load_mkwl: proc;

%nolist;
%include '*>insert>gbl_ext.ins.spl';
%include '*>insert>kwl_ext.ins.spl';
%include '*>entry>gp$.ins.spl';
%include '*>entry>kwl$expand_value.ins.spl';
%include '*>entry>kwl$destroy_kwl.ins.spl';
%include '*>entry>err$.ins.spl';
%Include '*>entry>primos.ins.spl';
%list;

   dcl
      expand_info    like kwl$expand_info,
      code           bin,
      new_master_kwl ptr,
      local_old_master_kwl ptr;

   new_master_kwl = null;
   local_old_master_kwl = null;
   string(expand_info) = kwl$default_expand_info; /* set to default values */
   expand_info.special_kwl = true;

   on condition (err$condition_before_msg)
      begin;
      call kwl$destroy_kwl (new_master_kwl);
      if (err$es.type = err$primos_et) & (err$es.status1 = e$fntf) then
         goto master_list_not_found;
      call cnsig$ (code);
      end;

   call kwl$expand_value (kwl$master_kwl, new_master_kwl, expand_info);
   revert condition (err$condition_before_msg);

   local_old_master_kwl = psv.old_master_kwl;
   psv.old_master_kwl = psv.master_kwl;
   psv.master_kwl = new_master_kwl;
   call kwl$destroy_kwl (local_old_master_kwl);
   return;



master_list_not_found:

   if kwl.master_kwl_not_found_printed then
      return;

   kwl.master_kwl_not_found_printed = true;
   call err$ (err$warning_et+err$user_et,
              'WARNING:  The system MASTER keyword file was not found.',
              err$master_list_not_found_ue, 0, 0);
   return;
   end kwl$load_mkwl;
