/* KWL$DESTROY_KWL.SPL, PrintMaster, Jim Wilcoxson, January 26, 1984          */
/* Frees a keyword list and all associated data structures.                   */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

kwl$destroy_kwl : procedure (kwlp);

%nolist;
%include '*>insert>gbl.ins.spl';
%include '*>insert>kwl.ins.spl';
%include '*>entry>gp$.ins.spl';
%include '*>entry>primos.ins.spl';
%list;

dcl
   kwlp     ptr,                            /* pointer to kwl (in/out) */
   (p,q,r)  ptr,
   saved_swi   like swi_switches;


   do while (kwlp ^= null);
      p = kwlp;
      kwlp = kwlp->kw_entry.next;

      /* free all values */

      do r = p->kw_entry.pf_value repeat q while (r ^= null);
         q = r->value_entry.next;
         if segno(r) < '4000'b3 then do;    /* shared keyword list */
            call gp$lock_shared_area (saved_swi);
            call ar$fre (psv.shared_area, r);
            call gp$unlock_shared_area (saved_swi);
            end;
         else
            free r->value_entry;
         end;

      /* free the keyword */

      if segno(p) < '4000'b3 then do;
         call gp$lock_shared_area (saved_swi);
         call ar$fre (psv.shared_area, p);
         call gp$unlock_shared_area (saved_swi);
         end;
      else
         free p->kw_entry;
   end;

end kwl$destroy_kwl;
