10 dim a0$(100),n0$(100),p9(200)
100 def fnp1(x$)                            ! find start of last item on line
110    local x
120    for x = len(x$) to 1 step -1
130       if sub(x$,x) = ' ' then 180
140       next x
150    print 'No page number found on line: ' + x$
160    fnp1 = 1
170    goto 190
180    fnp1 = x+1
190 fnend
300 def fnp(x$,a$,b$,d$)                       ! x$(in), a$,b$,d$,p(out)
310    local i0,p0
315    p0 = fnp1(x$)
319    d$ = '`'
320    i0 = index(x$,d$)
321    if i0 = 0 then do
322       d$ = '='
323       i0 = index(x$,d$)
324       doend
325    if i0 = 0 then do
326       d$ = ''
327       a$ = left(x$,p0-2)
328       b$ = ''
329       doend
330    else do
340       a$ = left(x$,i0-1)
350       b$ = sub(x$,i0+1,p0-2)
360       doend
370    fnp = val(sub(x$,p0,len(x$)))
380 fnend
400 def fnl(x$,d)
410    local i
420    for i = 1 to d
430       if a0$(i) = x$ then goto 443
440       next i
441    fnl = 0
442    goto 444
443    fnl = i
444 fnend
445 def fnf(x$,y$,d)
446    local i
447    i = fnl(x$,d)
448    if i = 0 then do
450       d = d+1
460       a0$(d) = x$
470       n0$(d) = y$
480       fnf = d
482       doend
484    else do
485       fnf = i
486       doend
510 fnend
1000 input 'Input Pathname  ',f$
1005 input 'Output Pathname ',g$
1010 define read file #1 = f$
1015 define file #2 = g$
1018 gosub 8000
1020 on end #1 goto 9000
1025 mat p9 = zer
1030 a1$ = 'zyxwvu'
1031 b1$ = ''
1032 a$,b$,d$ = ''
1035 p1 = -1
1036 p,d,c,c1 = 0
1039 for z = 0 while (1)                    ! do forever
1040    read line #1, x$
1050    ! x$ => (t, a$, b$, p)    !! Do not change this line number !!
1060    gosub 2000                             ! parse and process the thing
1070    next z
1080 stop
1090 !
2000 ! subroutine to parse x$ and process the line
2005 c = 0
2010 p = fnp(x$,a$,b$,d$)
2020 if d$ = '=' then do
2030    x = fnf(a$,b$,d)
2045    goto 2699
2050    doend
2052 if d$ = '`' then do
2054    if b$ = '' then c = 1 else c = 2
2056    doend
2060 !
2100 i = fnl(a$,d)
2110 if i > 0 then a$ = n0$(i)         ! set value from abbrev table
2120 if c = 2 then do
2130    if c1 = 2 then do
2140       if a$ = a1$ then do         !
2150          if b$ = b1$ then do
2160             if p <> p1 then gosub 3000 ! append new page number if different
2165             p1 = p
2170             doend
2180          else do
2190             gosub 3100            ! write out previous level 2 entry
2200             gosub 3200            ! set up for new level 2
2210             doend
2220          doend
2230       else do
2240          gosub 3100               ! write out previous level 2 entry
2245          gosub 3400               ! set up for new level 1
2250          gosub 3500               ! write out level 1 entry; no page numbers
2260          gosub 3200               ! set up for new level 2
2270          doend
2280       doend
2290    else do
2300       if a$ = a1$ then do
2310          gosub 3500               ! write out level 1 entry
2320          gosub 3200               ! set up for new level 2
2330          doend
2340       else do
2350          gosub 3300               ! write out old level 1 entry
2360          gosub 3400               ! set up for new level 1 entry
2365          gosub 3500               ! write out level 1 entry; no page numbers
2370          gosub 3200               ! set up for new level 2
2380          doend
2390       doend
2400    doend
2410 else do
2420    if c1 < 2 then do
2430       if a$ = a1$ then do
2440          if p <> p1 then gosub 3000 ! append new page number if different
2450          p1 = p
2470          doend
2480       else do
2490          gosub 3300               ! write out old level 1 entry
2500          gosub 3400               ! set up for new level 1
2510          doend
2520       doend
2530    else do
2540       if a$ = a1$ then do
2550          gosub 3100               ! write out old level 2 entry
2560          doend
2570       else do
2580          gosub 3100               ! write out old level 2 entry
2590          gosub 3400               ! set up for new level 1
2600          doend
2610       doend
2620    doend
2650 c1 = c
2699 return
2700 stop
2701 !
2702 !
3000 ! append new page number
3005 p9(p)=1
3010 ! p$ = p$ + ', ' + str$(p)
3020 return
3100 !
3110 ! write out level 2 entry
3120 gosub 3600   ! build page number string p$
3130 ! write #2, '.dns '
3131 write #2, '.Break'
3132 write #2, b1$ + ' ' + p$
3140 return
3200 !
3210 ! set up for new level 2 entry
3215 ! write #2, '.dl 2'
3216 write #2, '.Undent'
3217 write #2, '.Indent 7'
3218 write #2, '.Paragraph -3 0'
3220 b1$ = b$
3223 mat p9 = zer
3225 p9(p) = 1
3230 ! p$ = ' ' + str$(p)
3240 p1 = p
3250 return
3300 !
3310 ! write out level 1 entry
3315 if a1$ <> 'zyxwvu' then do
3320    gosub 3600   ! build page number string p$
3340    ! write #2, '.dns '
3341    write #2, '.Break'
3342    write #2, a1$ + ' ' + p$
3350    doend
3360 return
3400 !
3410 ! set up for level 1
3420 ! write #2, '.dl 1'
3421 write #2, '.Undent'
3422 write #2, '.Indent 1'
3423 write #2, '.Paragraph 0 1'
3430 a1$ = a$
3433 mat p9 = zer
3435 p9(p) = 1
3440 ! p$ = ' ' + str$(p)
3450 p1 = p
3460 return
3500 !
3510 ! write out level 1 entry without page numbers
3515 if a1$ <> 'zyxwvu' then do
3540    ! write #2, '.dns '
3541    write #2, '.Break'
3542    write #2, a1$
3550    doend
3560 return
3600 !
3610 ! build page number string from table
3620 !
3630 p$ = ''
3640 for p8 = 1 to 200
3650    if p9(p8) = 1 then do
3660       if len(p$) = 0 then p$ = str$(p8) else p$ = p$ + ', ' + str$(p8)
3680       doend
3690    next p8
3695 return
3699 stop
8000 !
8010 ! write runoff commands to set up decimalization indentation and columns
8020 !
8022 write #2, '.Fill'
8025 write #2, '.Header //INDEX//'
8030 write #2, '.Columns 2'
8040 ! write #2, '.DSkip 1 1 0'
8050 ! write #2, '.DSkip 2 0 0'
8060 ! write #2, '.DIndent 1 0 0'
8070 ! write #2, '.DIndent 2 3 6'
8080 write #2, '.Footer //X - #//'
8099 return
9000 !
9005 ! on-unit for eof
9006 !
9010 x$='~ -1'
9020 on end #1 goto 9050
9030 goto 1050                              ! handle last line
9050 close #1,2
9060 end
