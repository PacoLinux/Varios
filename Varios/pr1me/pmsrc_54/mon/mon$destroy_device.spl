/* MON$DESTROY_DEVICE.SPL, PrintMaster, Jim Wilcoxson, March 11, 1984         */
/* Destroys a device info block.                                              */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

mon$destroy_device : procedure (devp);

%nolist;
%include '*>insert>gbl_ext.ins.spl';
%include '*>insert>mon.ins.spl';
%include '*>entry>kwl$.ins.spl';
%include '*>entry>primos.ins.spl';
%list;

dcl
   devp     ptr;                            /* to device info block (in,out) */

dcl
   status   bin,
   (p,q)    ptr;

   q=null;
   do p = mon.device_bol repeat p->device_info.next while (p ^= null);
      if p = devp then
         leave;
      q = p;
      end;

   if p ^= null then                        /* device found */
      if q = null then
         mon.device_bol = p->device_info.next;
      else
         q->device_info.next = p->device_info.next;

   call kwl$destroy_kwl (devp->device_info.kwlp);
   if devp->device_info.semaphore ^= 0 then
      call sem$cl (devp->device_info.semaphore, status);
   free devp->device_info;
   devp = null;

end mon$destroy_device;
