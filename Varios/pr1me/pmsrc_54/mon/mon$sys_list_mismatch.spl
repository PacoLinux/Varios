/* MON$SYS_LIST_MISMATCH.SPL, PrintMaster, Jim Wilcoxson, January 19, 1984    */
/* Called when my system list does not match another monitor's system list.   */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

mon$sys_list_mismatch : procedure (system_id, system_list, system_list_size);

%nolist;
%include '*>insert>gbl_ext.ins.spl';
%include '*>insert>mon.ins.spl';
%include '*>entry>primos.ins.spl';
%include '*>entry>err$.ins.spl';
%include '*>entry>gp$.ins.spl';
%list;

dcl
   system_id         char(*) var,           /* remote's id (in) */
   system_list(*)    char(*) var,           /* remote's system list (in) */
   system_list_size  bin;                   /* size of list (in) */

dcl
   i                 bin,
   sl_unit           bin,                   /* system list unit */
   line              char(32),              /* line to write to disk */
   new_name          char(32) var,          /* name of new system list */
   version           bin,                   /* version number for sys list */
   status            bin;                   /* primos return status */


   on condition (cleanup$)
      call cleanup;

   on condition (err$condition_after_msg)
      begin;
      if err$es.type = err$user_et & err$es.status1 = err$system_list_ue then
         call cnsig$ (status);              /* I signalled this */
       else
         goto proc_exit;                    /* bomb if anything else */
      end;

   /* the object here is to write out a copy of the list received from
      the remote system, to allow the administrator to just copy into
      SYSTEM_LIST.  The new copy will be called SYSTEM_LIST.v where
      v is the version (starting with 1, increasing to 999)  */

   if ^gp$attach_system_dir(status) then
      goto proc_exit;

   do version = 1 to maxint;
      new_name = 'SYSTEM_LIST.' || trim (char (version), '10'b);
      if ^gp$file_exists (new_name) then
         leave;
      end;

   call gp$open_file (k$writ+k$getu, new_name, sl_unit);
   call gp$write_line (sl_unit, 'The following system list was received from "'
                       || system_id || '":');

   do i = 1 to system_list_size;
      call gp$write_line (sl_unit, system_list(i));
      end;

proc_exit:
   call cleanup;
   call err$ (err$user_et, 'System list mismatch; see file "' || new_name
              || '" in system directory.', err$system_list_ue, 0, 0);


cleanup: procedure;

   call gp$close (sl_unit);

end cleanup;
end mon$sys_list_mismatch;
