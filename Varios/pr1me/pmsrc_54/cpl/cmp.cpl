/* cmp, jlh
/* compile all source files in the sub-directory indicated.
&severity
&args dir:char; rest:uncl; prod_sw:-prod; mod_sw:-mod
como *>como>cmp.como -c
rwlock *>como>cmp.como updt
&debug &echo com
&if [null %prod_sw%] &then &do  /* build test version of system
   &s prod := -noopt -range
   &s debug := -debug -noopt -range
   &s bin := BIN
   &end
&else &do                       /* build release version of system
   &s debug := -opt -quick
   &s prod := -opt -quick    /* should be same as debug options */
   &s bin := BIN.NODEBUG
   &end

&on ACCESS_VIOLATION$ &routine acc_hlr

&do i &list [wild *>%dir%>@.@ -file]
   &s j := [after %i% '.']
   &s file := [before %i% '.']
   &if ^ [null %mod_sw%] &then ~
      &if [exists *>%bin%>%file%.bin] &then ~
         &if [attrib *>%dir%>%i% -dtm] <= [attrib *>%bin%>%file%.bin -dtm] &then ~
             &goto eol
   &if %j% = 'SPL' &then ~
      spl *>%dir%>%i% -b *>%bin%>=.bin %debug% -no_copy %rest%
   &else &if %j% = 'PSPL' &then ~
      spl *>%dir%>%i% -b *>%bin%>=.bin %prod% -no_copy %rest%
   &else &if %j% = 'PMA' &then ~
      pma *>%dir%>%i% -b *>%bin%>=.bin -l no

&label eol
&end
&debug &no_echo com
como -end
&return

&routine acc_hlr
type ACCESS_VIOLATION$ raised while compiling %i%
&goto eol
