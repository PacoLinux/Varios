&args dir1:tree; dir2:tree; rpt_sw: -report, -rpt report:tree=CMPF.COMO

&if [null %dir1%] | [null %dir2%] &then &stop 1 &message Directory not specified.
COMO %report%
type Comparing %dir1% and %dir2%
type
COMO -PAUSE
&s bigtemp := ALL.[date -ftag]
OPEN %bigtemp% 2 2002
CLOSE 2
&do d &list PRINTMASTER PRINTMASTER>CMDNC0 BIO COMM CPL DS ENTRY EPF_COMMAND ERR GP HOOK INSERT KWL L2 L3 MISC MON SPL SQ
&s need_header := true         /* need directory heading
&s unit := 0
&do f &items [wild %dir1%>%d%>@@PL -single unit]
    &s temp := CMPF.[date -ftag]
    &if [exists %dir2%>%d%>%f%] &then ~
        &s file2 := %dir2%>%d%>%f%
    &else &do
        &s base := [before %f% .]
        &s file2 := [wild %dir2%>%d%>%base%.@@ -file]
        &if [null %file2%] &then ~
            type %dir1%>%d%>%f% does not exist in %dir2%>%d%!!
        &else ~
            &s file2 := [unquote %dir2%>%d%>(%file2%)]
        &end
    &if ^ [null %file2%] &then CMPF %dir1%>%d%>%f% %file2% -REPORT %temp%
    &if %severity$% = 0 & ^ [null %file2%] &then DELETE %temp%
    &else &do
       COMO -CONTINUE
       &if %need_header% &then &do
          TYPE
          TYPE Comparing %d% directory:
          TYPE
          &s need_header := false
          &end
       type '     '%f% different.
       COMO -PAUSE
       APPEND %bigtemp% %temp%
       DELETE %temp%
       &end
    &end
&end
COMO -CONTINUE
TYPE
TYPE End of comparison.
TYPE
TYPE
COMO -END
APPEND %report% %bigtemp%
DELETE %bigtemp%
