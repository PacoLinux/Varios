/* Update to rev 4.4
/*
/* 1.  Add keyword -CHECK_CARRIER for AMLC devices
/*
/* 2.  Change device driver keyword -LEVEL_3_DRIVER:
/*
/*         L3$AMLC          ->  L3$STANDARD  (same -ASSIGN_DEVICE)
/*         L3$VG            ->  L3$STANDARD  (same -ASSIGN_DEVICE)
/*         L3$SPX           ->  L3$PX        (same -ASSIGN_DEVICE)
/*
/*        (L3$TTY           ->  L3$STANDARD  with -ASSIGN_DEVICE TERMINAL
/*
/*           these last two must be changed manually)
/*
/*

/* 1.  Add -CHECK_CARRIER

EKF -MASTER -EDIT -CHECK_CARRIER:i

/* 2.  Change -LEVEL_3_DRIVER in all device driver keyword files

&s curdir := [dir [pathname X]]

A PRINTMASTER*>DEVICES
DELETE *>TIMEOUT>@@ -NVFY -NQ

&do d &list [wild @@.CPL]
&s n := [before %d% .]
&s existed := [exists %n%.TF]
EKF %n%.KF -LIST %n%.TF
&data ED %n%.TF
C/L3$AMLC/L3$STANDARD/G999
T
C/L3$VG/L3$STANDARD/G999
T
C/L3$SPX/L3$PX/G999
FILE
&end
EKF %n%.KF -CREATE %n%.TF
&if ^ %existed% &then DELETE %n%.TF
&end

TYPE
&if [query 'Are any devices using the TTY level 3 driver L3$TTY (no)', FALSE] &then &do
   TYPE
   TYPE Execute the command:
   TYPE
   TYPE '    EKF device -ASSIGN_DEVICE TERMINAL'
   TYPE
   TYPE where "device" is the device name, for all TTY devices in PRINTMASTER*>DEVICES.
   TYPE
   &end
&else ~
   TYPE

ATTACH %curdir%
