/* REMOVE_PRINTMASTER.CPL, PrintMaster, Jeff Hostetler, June 9, 1984          */
/* Command file to remove part or all of PrintMaster.                         */
/*                                                                            */
/* Usage:  REMOVE_PRINTMASTER  [-RPT]                                         */
/*    or:  REMOVE_PRINTMASTER -HELP                                           */
/*                                                                            */
/* This should be run from the system console.                                */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

&args rpt_sw:     -rpt, -report; ~
      help_sw:    -help

&s rev := 5.4
type '[Remove PrintMaster '%rev%']'
type 'Copyright (C) 1984-1988 Merit Software, Inc.'
type

&if ^ [null %help_sw%] &then &do
   type
   type 'Usage:  REMOVE_PRINTMASTER [-RPT]'
   type '   or:  REMOVE_PRINTMASTER -HELP'
   type
   type 'This procedure removes all or part of PrintMaster.'
   type 'It requires ALL access to CMDNC0 and HELP*, and'
   type 'should be run from the system console if Prime SPOOL'
   type 'and PROP commands are being restored.'
   type
   type 'If -RPT is given, all steps will be reported.'
   type
   &return
   &end

&s need_ice := FALSE

/* determine where the entry$ search list file is located

&if [exists SYSTEM>ENTRY$.SR] &then ~
   &if [exists SEARCH_RULES*>ENTRY$.SR] &then ~
      &if [query 'Are you running rev 20.2 or above on this system (yes)' TRUE] &then ~
         &s search_rule_dir := SEARCH_RULES*
      &else ~
         &s search_rule_dir := SYSTEM
   &else ~
      &s search_rule_dir := SYSTEM
&else ~
   &s search_rule_dir := SEARCH_RULES*

&if ^ [exists %search_rule_dir%>ENTRY$.SR] &then ~
   &stop 1 &message Can't find default entry$ search list.

&if ^ [exists PRINTMASTER*>PHASE-II] &then &goto phase_I

&if ^ [null [wild CMDNC0>SPOOL.PRIME_VERSION.@]] ~
  |  [exists CMDNC0>SPOOL.CPL]  ~
  |  ^ [null [wild CMDNC0>PROP.PRIME_VERSION.@]]  ~
  |  [exists CMDNC0>PROP.CPL] &then &do

   &if [query 'Restore Prime SPOOL and PROP commands (no)' FALSE -tty] &then &do
      &if [exists CMDNC0>SPOOL.CPL] &then DELETE CMDNC0>SPOOL.CPL %rpt_sw% -NQ
      &if [exists CMDNC0>PROP.CPL] &then DELETE CMDNC0>PROP.CPL  %rpt_sw% -NQ

      &if [exists CMDNC0>PROP.PRIME_VERSION.RUN] &then &do
         &if [exists CMDNC0>PROP.RUN] &then &do
            &severity &error &ignore
            DELETE CMDNC0>PROP.PRIME_VERSION.RUN -NQ %rpt_sw%
            &severity &error &fail
            &end
         &else &do
            COPY CMDNC0>PROP.PRIME_VERSION.RUN CMDNC0>PROP.RUN -NQ %rpt_sw%
            &severity &error &ignore
            DELETE CMDNC0>PROP.PRIME_VERSION.RUN -NQ %rpt_sw%
            &severity &error &fail
            &end
         type '    Prime PROP command restored.'
         &end
      &else &if [exists CMDNC0>PROP.PRIME_VERSION.SAVE] &then &do
         &if [exists CMDNC0>PROP.SAVE] &then &do
            &severity &error &ignore
            DELETE CMDNC0>PROP.PRIME_VERSION.SAVE -NQ %rpt_sw%
            &severity &error &fail
            &end
         &else &do
            COPY CMDNC0>PROP.PRIME_VERSION.SAVE CMDNC0>PROP.SAVE
            &severity &error &ignore
            DELETE CMDNC0>PROP.PRIME_VERSION.SAVE -NQ %rpt_sw%
            &severity &error &fail
            &end
         type '    Prime PROP command restored.'
         &end
      &else &if [exists CMDNC0>PROP.RUN] | [exists CMDNC0>PROP.SAVE] &then ~
         type '    Prime PROP command already restored.'
      &else ~
         type '    Prime PROP command not found.'

      &if [exists CMDNC0>SPOOL.PRIME_VERSION.RUN] &then &do
         &if [exists CMDNC0>SPOOL.RUN] &then &do
            &severity &error &ignore
            DELETE CMDNC0>SPOOL.PRIME_VERSION.RUN -NQ %rpt_sw%
            &severity &error &fail
            &end
         &else &do
            COPY CMDNC0>SPOOL.PRIME_VERSION.RUN CMDNC0>SPOOL.RUN -NQ %rpt_sw%
            &severity &error &ignore
            DELETE CMDNC0>SPOOL.PRIME_VERSION.RUN -NQ %rpt_sw%
            &severity &error &fail
            &end
         type '    Prime SPOOL command restored.'
         &end
      &else &if [exists CMDNC0>SPOOL.PRIME_VERSION.SAVE] &then &do
         &if [exists CMDNC0>SPOOL.SAVE] &then &do
            &severity &error &ignore
            DELETE CMDNC0>SPOOL.PRIME_VERSION.SAVE -NQ %rpt_sw%
            &severity &error &fail
            &end
         &else &do
            COPY CMDNC0>SPOOL.PRIME_VERSION.SAVE CMDNC0>SPOOL.SAVE
            &severity &error &ignore
            DELETE CMDNC0>SPOOL.PRIME_VERSION.SAVE -NQ %rpt_sw%
            &severity &error &fail
            &end
         type '    Prime SPOOL command restored.'
         &end
      &else &if [exists CMDNC0>SPOOL.RUN] | [exists CMDNC0>SPOOL.SAVE] &then ~
         type '    Prime SPOOL command already restored.'
      &else ~
         type '    Prime SPOOL command not found.'

      type '    Removing SPOOL$ from default entry$ search list.'
      &s need_ice := TRUE
      COMO -NTTY
      &data NSED %search_rule_dir%>ENTRY$.SR
      L SPOOL$;D;*
      TOP
      L spool$;D;*
      TOP
      L Spool$;D;*
      FILE
      &end
      COMO -TTY
      &end
   &else &do
      type
      &return
      &end

   DELETE PRINTMASTER*>PHASE-II

   &end

&label phase_I

&if ^ [query 'Remove PrintMaster commands and help files (no)' FALSE -tty] &then &do
   type
   &return
   &end
/*
/* remove Printmaster libraries from system search list
/*

&s need_ice := TRUE
type '    Removing PrintMaster library from default entry search rules.'
COMO -NTTY
&data NSED %search_rule_dir%>ENTRY$.SR
   L PRINTMASTER;D;*
   TOP
   L printmaster;D;*
   TOP
   L Printmaster;D;*
   TOP
   L PrintMaster;D;*
   FILE
   &end
COMO -TTY

type '    Deleting PrintMaster commands from CMDNC0 and help files from HELP*.'
&severity &error &ignore
DELETE (CMDNC0 HELP*)>(APJ DC DPJ EKF LPJ LPQ CPJ INIT_PRINTMASTER CDD CFD CPQ).@ -NQ -NVFY %rpt_sw%
&severity &error &fail

type
type 'PRINTMASTER* system directory and PRINTMASTER_QUEUE* print queue'
type 'directories may be deleted from the MFD with the DELETE command'
type 'to completely remove PrintMaster.'
&if %need_ice% &then &do
   type
   type 'ICE or SSR -DFLT ENTRY$ command must be issued for each logged-in user'
   type 'to activate new search rules.'
   &end
type
&return
