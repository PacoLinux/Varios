/* COPY_SYSTEM_CONFIGURATION.CPL, PrintMaster, Jeff Hostetler, July 2, 1984   */
/* PrintMaster procedure to Copy the System Configuration to another system.  */
/*                                                                            */
/* Usage:  r COPY_SYSTEM_CONFIGURATION <from_partition> <to_partition> [-LIB] */
/*                                        [-USER] [-PROJ] [-RPT]              */
/*    or:  r COPY_SYSTEM_CONFIGURATION -HELP                                  */
/*                                                                            */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

&args from_partition: entry; ~
      to_partition:   entry; ~
      lib_sw:         -lib, -library; ~
      usr_sw:         -user, -user_defaults; ~
      proj_sw:        -proj, -project_defaults; ~
      rpt_sw:         -rpt, -report; ~
      help_sw:        -help


&s rev := 5.4
type '[Copy System Configuration '%rev%']'
type


&if ^ [null %help_sw%] &then &do
   type 'Usage:  r COPY_SYSTEM_CONFIGURATION'
   type '   or:  r COPY_SYSTEM_CONFIGURATION -HELP'
   type
   type 'This command copies the PrintMaster system directory from one'
   type 'partition to another, including forms, user and project'
   type 'defaults, and system defaults.'
   type
   type 'It is intended to be used with networked systems to allow'
   type 'all remote systems to be configured once a single node has'
   type 'been configured.'
   type
   &return
   &end


&s pmsd := PRINTMASTER*
&s prog := PRINTMASTER*>PROGRAMS


&s from_partition := [r %prog%>GET_PARTITION_NAME %from_partition% ~
                                                  [quote From partition (containing configured %pmsd%)] ~
                                                  -QUERY]
&if [null %from_partition%] &then &return
&s to_partition   := [r %prog%>GET_PARTITION_NAME %to_partition% ~
                                                  [quote To partition (containing unconfigured %pmsd%)]]
&if [null %to_partition%] &then &return
&if %from_partition% = %to_partition% &then &do
    TYPE From partition and to partition are the same.  No copy will be done.
    &return
    &end
&else ~

    /* determine which partition is remote; this is so we can attach to the
    /* remote partition before the copy commands, to prevent eating up a
    /* bunch of slaves

    &if [query 'Is partition '%from_partition%' remote (yes)' TRUE] &then ~
        &s remote_partition := %from_partition%
    &else ~
        &s remote_partition := %to_partition%

&severity &error &ignore

/* Copy the config from the other system.
type Copying PrintMaster system configuration from %from_partition% to %to_partition%.

&s old_attach_point := [dir [pathname X]]

&if %remote_partition% = %from_partition% &then &do
   ATTACH <%from_partition%>%pmsd%
   &s f := *
   &s t := <%to_partition%>%pmsd%
   &end
&else &do
   ATTACH <%to_partition%>%pmsd%
   &s f := <%from_partition%>%pmsd%
   &s t := *
   &end

COPY %f%>SYSTEM_LIST                       %t%>== -DTM -NQ %rpt_sw%
COPY %f%>MASTER.KF                         %t%>== -DTM -NQ %rpt_sw%
COPY %f%>SYSTEM_DEFAULTS.KF                %t%>== -DTM -NQ %rpt_sw%
COPY %f%>DEVICES>DEVICE_DEFAULTS.KF        %t%>DEVICES>== -DTM -NQ %rpt_sw%
COPY %f%>DEVICES>L2.KF                     %t%>DEVICES>== -DTM -NQ %rpt_sw%

&if [null %lib_sw%] &then ~
   &if [query 'Copy keyword library (yes)' TRUE] ~
      &then &s lib_sw := -LIB
      &else &s lib_sw :=
&if ^ [null %lib_sw%] &then ~
   COPY %f%>LIBRARY %t%>== -DTM -NQ %rpt_sw%


&if [null %usr_sw%] &then ~
   &if [query 'Copy user defaults (yes)' TRUE] ~
      &then &s usr_sw := -USER
      &else &s usr_sw :=
&if ^ [null %usr_sw%] &then ~
   COPY %f%>USER_DEFAULTS %t%>== -DTM -NQ %rpt_sw%

&if [null %proj_sw%] &then ~
   &if [query 'Copy project defaults (yes)' TRUE] ~
      &then &s proj_sw := -PROJ
      &else &s proj_sw :=
&if ^ [null %proj_sw%] &then ~
   COPY %f%>PROJECT_DEFAULTS %t%>== -DTM -NQ %rpt_sw%

ATTACH %old_attach_point%
type
&return
