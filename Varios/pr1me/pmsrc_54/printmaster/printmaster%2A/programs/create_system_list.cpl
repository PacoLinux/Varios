/* CREATE_SYSTEM_LIST.CPL, PrintMaster, Jeff Hostetler, June 24, 1984         */
/* PrintMaster procedure to help the user Create the System List.             */
/*                                                                            */
/* Usage:  r CREATE_SYSTEM_LIST <system_partition>                            */
/*    or:  r CREATE_SYSTEM_LIST -HELP                                         */
/*                                                                            */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

&args partition: entry; ~
      help_sw:   -help


&s rev := 5.4
type '[Create System List '%rev%']'
type


&if ^ [null %help_sw%] &then &do
   type 'Usage:  r CREATE_SYSTEM_LIST  system_partition'
   type '   or:  r CREATE_SYSTEM_LIST -HELP'
   type
   type 'SYSTEM_PARTITION is the partition name of the system directory PRINTMASTER*.'
   type
   &return
   &end


/* Initialize variables used throughout.

&s pgm := CREATE_SYSTEM_LIST
&s pmsd := PRINTMASTER*
&s prog := PRINTMASTER*>PROGRAMS
&s sl := SYSTEM_LIST


&s partition := [r %prog%>GET_PARTITION_NAME %partition%]
&if [null %partition%] &then &return


/* Let's see if they have enough rights to the system directory.

&severity &error &ignore
r %prog%>CPL$CALAC$ <%partition%>%pmsd% DALURW
&if %severity$% > 0 &then &do
   &s sev := %severity$%
   type Insufficient access rights.  This program requires DALURW access
   type rights to the PrintMaster system directory.
   type
   &return %sev%
   &end

type 'Please enter the Primenet system name of each system that will share'
type 'print queues or devices.  This list will be used to determine which'
type 'system runs the job scheduler; the first active system in the list'
type 'will do job scheduling for all jobs on the network.  Enter one per'
type 'line, terminated by a blank line.'
type
&s nr_sys := 0
&s x := [response 'System name' '']
&do &while [length %x%] > 0
   &s nr_sys := [calc %nr_sys% + 1]
   &s system_%nr_sys% := %x%
   &s x := [response 'System name' '']
   &end

&if %nr_sys% = 0 &then &do
   type 'No systems given; system list will not be created.'
   &if [exists <%partition%>%pmsd%>%sl%] &then ~
      DELETE <%partition%>%pmsd%>%sl% -NQ -NVFY
   &return
   &end

COMO <%partition%>%pmsd%>%sl% -NTTY
&do i := 1 &to %nr_sys%
   type [quote [unquote [get_var system_%i%]]]
   &end
COMO -END -TTY
type
type PrintMaster system list <%partition%>%pmsd%>%sl% created.
type
&return
