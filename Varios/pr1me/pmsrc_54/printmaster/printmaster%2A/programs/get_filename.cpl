/* GET_FILENAME.CPL, PrintMaster, Jeff Hostetler, July 13, 1984               */
/* FUNCTION to return a valid filename.                                       */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

&args initial_sw: -initial fn: char; default_sw: -default def_fn: char; ~
      prompt_sw: -prompt msg: charl; ~
      max_sw: -max maxlen=32; min_sw: -min minlen=1

&s prog := PRINTMASTER*>PROGRAMS

&severity &error &ignore

R %prog%>CPL$FNCHK$ [quote [unquote %fn%]]
&if %severity$% = 0 & %minlen% <= [length %fn%] & [length %fn%] <= %maxlen% &then &do
   &result %fn%
   &return
   &end

&s okay := FALSE
&do &until %okay%
   &s fn := [trim [response %msg% %def_fn%]]
   &if ^ [null %fn%] &then ~
       R %prog%>CPL$FNCHK$ [quote [unquote %fn%]]
   &else ~
      &s severity$ := 0
   &if %severity$% ^= 0 &then ~
      type 'This must be a valid filename.'
   &else &if [length %fn%] < %minlen% &then ~
            &if %minlen% = 1 &then ~
               type 'The name has to be entered.'
            &else ~
               type 'The name has to be at least '%minlen%' characters long.'
   &else &if [length %fn%] > %maxlen% &then ~
            type 'The name can only be '%maxlen%' characters long, not '[length %fn%]'.'
   &else &s okay := true

   &end

type
&result [translate %fn%]
&return
