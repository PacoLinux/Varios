/* SET_DEVICE_DEFAULTS.CPL, PrintMaster, Jeff Hostetler, June 20, 1984        */
/* PrintMaster command to help the user set up some system device defaults.   */
/*                                                                            */
/* Usage:  SET_DEVICE_DEFAULTS [system_partition]                             */
/*    or:  SET_DEVICE_DEFAULTS -HELP                                          */
/*                                                                            */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

&args partition: entry; ~
      help_sw:   -help

&s rev := 5.4
type '[Set Device Defaults '%rev%']'
type

&if ^ [null %help_sw%] &then &do
   type 'Usage:  SET_DEVICE_DEFAULTS system_partition'
   type '   or:  SET_DEVICE_DEFAULTS -HELP'
   type
   type 'SYSTEM_PARTITION is the partition name of the system directory PRINTMASTER*.
   type
   &return
   &end

/* Get the following information from the user:
/*    1)  Enable auto-restart (after halts, network errors, etc.)
/*
/*
/* Initialize all variables used by main procedure.
/*
&s pgm := SET_DEVICE_DEFAULTS
&s pmsd := PRINTMASTER*
&s prog := PRINTMASTER*>PROGRAMS
&s sdd := %pmsd%>DEVICES>DEVICE_DEFAULTS
&s banner := %pmsd%>BANNER_LETTERS


&s partition := [r %prog%>GET_PARTITION_NAME %partition%]
&if [null %partition%] &then &return


/* Let's see if they have enough rights to the system directory.

&severity &error &ignore
r %prog%>CPL$CALAC$ <%partition%>%pmsd% DALURW
&if %severity$% > 0 &then &do
   &s sev := %severity$%
   type Insufficient access rights.  This program requires DALURW access
   type rights to the PrintMaster system directory.
   type
   &return %sev%
   &end

type 'Please answer the following questions concerning defaults'
type 'for all devices on this system.'
type
&s enable_ar := [query 'Should devices automatically restart after system halts (yes)' TRUE]

/* Create text form of keyword list with COMO trick.

COMO <%partition%>%sdd%.TF -NTTY
type -ACCOUNTING L2$ACCOUNTING
&if %enable_ar% &then  type [quote -AUTO_RESTART]
COMO -END -TTY


&if [exists <%partition%>%sdd%.KF] &then ~
   DELETE <%partition%>%sdd%.KF -NQ -NVFY

EKF <%partition%>%sdd% -CREATE <%partition%>%sdd%.TF
&if %severity$% ^= 0 &then &do
   &if [exists <%partition%>%sdd%.KF] &then ~
      DELETE <%partition%>%sdd%.KF -NQ -NVFY
   type [quote PrintMaster device defaults not established.]
   &end
&else type [quote PrintMaster device defaults established.]

DELETE <%partition%>%sdd%.TF -NQ -NVFY

type
&return
