/* GET_PARTITION_NAME.CPL, PrintMaster, Jeff Hostetler, July 10, 1984         */
/* FUNCTION to fetch partition name of PRINTMASTER*                           */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

&args partition: entry; msg: charl; query_sw: -query

&s pmsd := PRINTMASTER*
&s prog := PRINTMASTER*>PROGRAMS

&s msg := [unquote %msg%]
&if [length %msg%] = 0 &then ~
   &s msg := PrintMaster system directory partition name

&s partition := [translate %partition%]
&s default_partition := ''

&s curdir := [dir [pathname X]]
&if [length %partition%] > 0 &then &do
   &severity &error &ignore
   R %prog%>CPL$ATTACH <%partition%>MFD
   &severity &error &fail
   &if %severity$% = 0 &then ~
      &if [null %query_sw%] &then &do
         &result %partition%
         ATTACH %curdir%
         &return
         &end
      &else &s default_partition := %partition%
   &end

ATTACH %curdir%
&if [length %default_partition%] = 0 &then &do
   &severity &error &ignore
   R %prog%>CPL$ATTACH %pmsd%
   &severity &error &fail
   &if %severity$% = 0 &then &do
      &s default_partition := [pathname X]
      &s default_partition := [substr %default_partition% 2 ~
                                      [calc [index %default_partition% >] - 2]]
      ATTACH %curdir%
      &end
   &else &do
      type 'PRINTMASTER* directory not found, run INSTALL_PRINTMASTER.'
      &result
      &return 1
      &end
   &end

&s question := [quote %msg% (%default_partition%)]

&s partition := [translate [response %question% %default_partition%]]
&s partition := [trim [trim %partition% -left <] -right >]
&do &while ^ [null %partition%]
   &severity &error &ignore
   R %prog%>CPL$ATTACH [quote <%partition%>MFD>%pmsd%]
   &severity &error &fail
   &if %severity$% = 0 &then &do
      ATTACH %curdir%
      type
      &result %partition%
      &return
      &end
   type 'PrintMaster system directory not found on partition '%partition%'.'
   &s partition := [translate [response %question% %default_partition%]]
   &s partition := [trim [trim %partition% -left <] -right >]
   &end
ATTACH %curdir%
type
&result
&return
