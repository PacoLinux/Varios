/* PROP.CPL, PrintMaster, Jeff Hostetler, June 2, 1984                        */
/* PrintMaster PROP procedure to simulate the Prime PROP command.             */
/*                                                                            */
/* Usage:  PROP <device_name> <action>                                        */
/*    or:  PROP <device_name> <action> -PRIME                                 */
/*                                                                            */
/* If -PRIME is given, command will be passed as is to the Prime PROP command.*/
/*                                                                            */
/* Unsupported options (when option -PRIME is omitted):                       */
/*    PROP -STAtus                                                            */
/*    PROP <device_name> -CREate                                              */
/*    PROP <device_name> -DELete                                              */
/*    PROP <device_name> -MODify                                              */
/*                                                                            */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

&args device_name: entry; uncl_cl: uncl; prime_sw: -prime

&if  ^ [null %prime_sw%] &then &goto use_primes_prop_command

&s pmsd := PRINTMASTER*
&s sev := 0

/* Uppercase and Strip blanks
&s uncl_cl := [trim [translate [quote %uncl_cl%]]]

&if [index %uncl_cl% ' '] = 0 &then &do
   &s option := %uncl_cl%
   &s arg := ''
   &end
&else &do
   &s option := [before %uncl_cl% ' ']
   &s arg := [after %uncl_cl% ' ']
   &if [index %arg% ' '] ^= 0 &then &s arg := [before %arg% ' ']
   &end

/* Device name must always be given, unless the "PROP -STAtus" form is used.
/* This form has not been implemented.
&if [length %device_name%] = 0 &then ~
   &if [substr %option% 1 4] = -STA &then &goto undefined_action
                                    &else &goto undefined_device

&if [length %option%] = 0 &then &goto undefined_action

&select [substr %option% 1 4]
   &when -DIS
      &s dc_option := -LIST
   &when -STA
      &s dc_option := -START
   &when -CON
      &s dc_option := -CONTINUE
   &when -RES
      &s dc_option := -RESTART
   &when -BAC
      &s dc_option := -BACKUP 3
   &when -ABO
      &s dc_option := -ABORT
   &when -DRO
      &s dc_option := -DROP_JOB
   &when -LIN
      &if [length %arg%] > 0 &then &s dc_option := -RESTART -ALIGN %arg%
                             &else &s dc_option := -RESTART -ALIGN 1
   &when -STO
      &if [length %arg%] = 0 &then &s dc_option := -STOP_IDLE
                             &else &select [substr %arg% 1 2]
                                      &when NO
                                         &s dc_option := -STOP
                                      &when FI
                                         &s dc_option := -STOP_EOJ
                                      &when ID
                                         &s dc_option := -STOP_IDLE
                                      &otherwise
                                         &goto undefined_modifier
                                      &end /* of select
   &when -HAN
      &if [length %arg%] = 0 &then &s dc_option := -PAUSE_IDLE
                             &else &select [substr %arg% 1 2]
                                      &when NO
                                         &s dc_option := -PAUSE
                                      &when FI
                                         &s dc_option := -PAUSE_EOJ
                                      &when ID
                                         &s dc_option := -PAUSE_IDLE
                                      &otherwise
                                         &goto undefined_modifier
                                      &end /* of select
   &otherwise
      &goto undefined_action
   &end /* of select

/* Parse was successful, invoke Device_Command
&if [exists %pmsd%>TEACH_PRINTMASTER] &then &do
   type PrintMaster command to perform similiar action:
   &debug &echo com
   &end
&severity &error &ignore
DC %device_name% %dc_option%

&return %severity$%
/* Logical end of PROP.CPL

&label undefined_modifier
   &s sev := 68                             /* e$cmnd
   type Illegal modifier, item must be either "NOw", "FInish", "IDle", or blank.
   &return %sev%

&label undefined_action
   &s sev := 71                             /* e$barg
   type Action undefined or unsupported by PrintMaster PROP command.
   &return %sev%

&label undefined_device
   &s sev := 68                             /* e$cmnd
   type Device name must be given.
   &return %sev%


&label use_primes_prop_command
   &severity &error &ignore
   PROP.PRIME_VERSION %device_name% %uncl_cl%
   &return %severity$%
