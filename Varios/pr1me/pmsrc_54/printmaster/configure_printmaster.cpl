/* CONFIGURE_PRINTMASTER.CPL, PrintMaster, Jeff Hostetler, July 3, 1984       */
/* PrintMaster procedure the configure the system.                            */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/


&args partition: entry; ~
      help_sw:   -help

&s rev := 5.4
type '[Configure PrintMaster '%rev%']'
type 'Copyright (C) 1984-1988 Merit Software, Inc.'
type


&if ^ [null %help_sw%] &then &do
   type 'Usage:  CONFIGURE_PRINTMASTER system_partition'
   type '   or:  CONFIGURE_PRINTMASTER -HELP'
   type
   type 'SYSTEM_PARTITION is the partition name of the system directory PRINTMASTER*.'
   type
   &return
   &end

/* see if system has been initialized

&severity &error &ignore
COMO -NTTY
&data EKF -MASTER -LIST                   /* required to prevent More? question
&end
&s sev := %severity$%
COMO -TTY
&if %sev% ^= 0 &then &do
   type 'Run INIT_PRINTMASTER from the system console.'
   type
   &return %sev%
   &end
&severity &error &fail


&s curdir := [dir [pathname X]]
&s pmsd := PRINTMASTER*
&s prog := PRINTMASTER*>PROGRAMS
&s sl   := SYSTEM_LIST
&s pmpq := PRINTMASTER_QUEUE*
&s partition := [r %prog%>GET_PARTITION_NAME %partition%]
&if [null %partition%] &then &return


&if [exists <%partition%>%pmsd%>%sl%] &then &do
   &if [query 'Do you want to go to a single system configuration (no)' FALSE -tty] &then &do
      DELETE <%partition%>%pmsd%>%sl% -NQ -NVFY %rpt_sw%
      &end
   &end

&if [query 'Copy system configuration from another system (no)' FALSE -TTY] &then &do
   type
   &s null_arg :=
   R %prog%>COPY_SYSTEM_CONFIGURATION  %null_arg%, %partition%
   &goto create_queues
   &end

type 'Configuring PrintMaster'
type
r %prog%>SET_SYSTEM_DEFAULTS %partition%
r %prog%>SET_DEVICE_DEFAULTS %partition%

&label create_queues

R %prog%>CHECK_FOR_QUEUES
ATTACH %curdir%

type
type '* * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * * *'
type
type 'PrintMaster configured.'
type 'Use CDD command to create device definitions, APJ to queue jobs, and'
type 'LPQ to list queues.'
type
&return
