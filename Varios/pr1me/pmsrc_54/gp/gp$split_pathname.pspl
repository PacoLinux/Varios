/* GP$SPLIT_PATHNAME.SPL, PrintMaster, Jeff Hostetler, December 26, 1983      */
/* Procedure to separate a pathname into its components.                      */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

gp$split_pathname: procedure (pathname, partition, dir, entry);

%nolist;
%Include '*>entry>err$.ins.spl';
%Include '*>entry>primos.ins.spl';
%list;

   dcl
      (pathname, partition, dir, entry) char(*) var, /* (in, out, out, out) */

      path                    char(128) var,
      (first_gt, last_gt)     bin;

   partition = '';
   dir = '';

   path = trim (pathname, '11'b, ' ');
   if ^tnchk$ (k$uprc, path) then           /* upper case first */
      call err$ (err$primos_et, 'Invalid pathname', e$itre, 0, 0);

   last_gt = length (path) - index (reverse (path), '>') + 1;
   if last_gt = length (path) + 1 then do;  /* we have a simple entry name */
      entry = path;
      return;
      end;

   entry = substr (path, (last_gt + 1));
   path = substr (path, 1, last_gt);

   first_gt = index (path, '>');
   if substr (path, 1, 1) = '<' then do;
      if first_gt > 2 then
         partition = substr (path, 2, first_gt - 2); /* "<partition>..." */
      if first_gt < last_gt then
         dir = substr (path, (first_gt + 1), (last_gt - first_gt - 1));
      return;
      end;

   dir = substr (path, 1, (last_gt - 1));
   return;
   end gp$split_pathname;

