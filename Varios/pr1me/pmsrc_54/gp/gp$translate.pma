* GP$TRANSLATE.PMA, PrintMaster, Jim Wilcoxson, April 11, 1984
* Performs in-place translation of a varying character string.  A complete
* 256 character translation table must be passed in.
* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.
*
* This procedure is short-called
*
*
*   call gp$translate (in_string, out_string, string_len, trans_table);
*
*   dcl
*      (in_string, out_string) char(string_len),
*      string_len              bin,
*      trans_table             char(256);
*

       SEG
       SYML
       RLIT

       ENT     GP$TRANSLATE

RTN$   EQU     SB%+'12         SHORTCALL TEMP FOR RETURN ADDRESS
ARG$   EQU     SB%+'14         SHORTCALL TEMP FOR ARGLIST POINTER

GP$TRANSLATE EQU *

       STL     ARG$            POINTER TO ARGUMENT LIST
       EAL     XB%             RETURN ADDRESS
       STL     RTN$
       EAXB    ARG$,*          XB POINTS TO ARGLIST
       EAFA    0,XB%,*         FAR0 POINTS TO INPUT STRING
       EAFA    1,XB%+3,*       FAR1 POINTS TO OUTPUT STRING
       LDA     XB%+6,*         GET STRING LENGTH
       XCA                     CONVERT TO LONG
       TLFL    0
       TLFL    1
       EAXB    XB%+9,*         XB NOW POINTS TO TRANSLATION TABLE
       ZTRN                    TRANSLATE
       JMP     RTN$,*          RETURN
       END
