/* GP$CHECK_PACL.SPL, PrintMaster, Jim Wilcoxson, September 29, 1984          */
/* Checks for a PrintMaster priority ACL on a partition.                      */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

gp$check_pacl : procedure (partition, user_aclp, status) returns (bit);

%nolist;
%include '*>insert>gbl.ins.spl';
%include '*>entry>err$.ins.spl';
%include '*>entry>primos.ins.spl';
%list;

dcl
   partition      char(*) var,
   user_aclp      ptr,
   status         bin,

   aclp           ptr,
   1 acl like primos_acl based (aclp),
   spool_acl_pair char(36) var,             /* userid:ALL */
   (i,j)          bin;


   spool_acl_pair = gbl$spool_acl_group || ':ALL';
   if user_aclp = null then
      allocate acl set(aclp);
   else
      aclp = user_aclp;
   acl.version = acl_version;

   call pa$lst ('<'||partition||'>MFD', aclp, max_acl_ents, status);
   select (status);

      when (e$panf)                         /* priority ACL not found */
         acl.count = 0;

      when (0) do;
         status = -1;                       /* .PRINTMASTER_SYSTEM not found */
         do j = 1 to acl.count;             /* see if spooler already in acl */
            if acl.ent(j) = spool_acl_pair then
               status = 0;
            end;
         end;

      otherwise
         ;

      end; /* select */

   if user_aclp = null then
      free aclp->acl;

   return (status = 0);

end gp$check_pacl;
