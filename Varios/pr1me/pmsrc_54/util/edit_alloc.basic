10 def fns$(v)
20   local l$,l
30   l$ = str$(v)
40   l = len(l$)
50   if l = 5 then fns$=l$ else fns$ = mid('     ',1,5-l) + l$
60   fnend
1000 input 'Pathname ',f$
1010 define read file #1 = f$
1015 define file #2 = f$ + '.out'
1020 on end #1 goto 9000
1030 dim a$(1000), c$(1000), s(1000), o(1000)
1031 ! addr of block, caller, size alloc, old size
1040 t = 0
1050 gosub 2000 ! read a line, return (a1$, c1$, s1, o1, p1$)
1055 x = 0
1056 h = 0
1060 for i = 1 to t
1070   if a$(i) = a1$ then 1500
1075   if x = 0 then do
1076      if s(i)=0 then do
1077         x = 1             ! save the location of the
1078         h = i             !   first hole
1079         doend
1080      doend
1085   next i
1090 if p1$ = '---' then do
1091   m$ = 'Unknown item of size '+fns$(s1)+' at '+a1$+' freed by '+c1$
1095   if o1 = s1 then do
1096      m$ = m$ + ' and isolated'
1097      doend
1098   else do
1099      m$ = m$ + ' creating block of size '+fns$(o1)
1100      doend
1105   write #2, m$
1110   doend
1120 else do
1125   if x = 0 then do  ! no holes found
1130     t = t + 1
1135     i = t
1140     doend
1150   else do
1153     i = h        ! use location of first hole
1155     doend
1160   a$(i) = a1$
1170   c$(i) = c1$
1180   s(i) = s1
1185   o(i) = o1
1190   doend
1200 goto 1050
1500 ! item found
1510 a$(i) = ''
1520 c$(i) = ''
1530 s(i) = 0
1535 o(i) = 0
1540 goto 1050
2000 ! read a line.
2010 read line #1,l$
2020 if left(l$,1) = '-' or left(l$,1) = '+' then do
2030   p1$ = mid(l$,1,3)
2040   s1 = val(mid(l$,5,5))
2050   a1$ = mid(l$,20,14)
2055   o1 = val(mid(l$,36,5))
2060   c1$ = sub(l$,65,len(l$))
2070   doend
2080 else do
2081   goto 2010
2082   doend
2090 return
9000 ! eof
9010 for i = 1 to t
9020   if s(i) > 0 then do
9030     m$ = 'Unfreed item of size '+fns$(s(i))+' (created from '+fns$(o(i))
9031     m$ = m$+' wd block) at '+a$(i)+' allocated by '+c$(i)
9034     write #2, m$
9035     doend
9040   next i
9050 close #1
9060 close #2
9070 end
