/* X$FCON.PLP,  PRINETSRC>VNETLB,  COMMS SW GROUP, 03/01/84
   Do an X.25 Fast Select call over Primenet (short form call)
   Copyright (c) 1983, Prime Computer, Inc., Natick, Ma 01760 */

/* TITLE : X$FCON - Do a X.25 Fast select call over primenet */

/* Modifications:
/* 03/01/84 Lindsay        Extend XLCONN argument lists
/* 07/04/83 Lindblad       Template constructed

/* START-DESCRIPTION :
   This routine is an interlude to XLCONN for a user program which wants
   to make a fast select call without having to bother about detailed formats
   for facility fields etc.

   Some legality checks are done here, but in principle we rely on the testing
   inside XLCONN.
   END-DESCRIPTION */

/* START-DESIGN :
   X$FCON:
   BEGIN
     Check on legal key values
     Set up needed further arguments to xlconn
     Call XLCONN
   END
   END-DESIGN */

/* START-CODE : */

X$FCON:
   procedure (key, anskey, vcid, port, adr, adrn, udata, udatan, vcstat,
             rusdat, rusmax, rusbc);

/* caller's arguments */

   dcl key bit(16);                    /* as xlconn: key */
   dcl anskey bin;                     /* selects restricted response or not */
   dcl vcid bin;                       /* VC identifier, returned, see xlconn
                                          */
   dcl port bin;                       /* target process identifier mechanism,
                                          see xlconn */
   dcl adr char(2);                    /* Array holding target node's name */
   dcl adrn bin;                       /* # chars in adr, see xlconn */
   dcl udata char(2);                  /* Array holding user's userdata field
                                          */
   dcl udatan bin;                     /* # chars in udata, see xlconn */
   dcl vcstat(2) bin;                  /* Virtual circuit status array, see
                                          xlconn */
   dcl rusdat char(2);                 /* User supplied array to catch returned
                                          user data field */
   dcl rusmax bin;                     /* Max # bytes accepted into rusdat */
   dcl rusbc bin;                      /* Actually returned # bytes userdata */

/* Called procedures */

   dcl xlconn entry(bit(16), bin, bin, char(*), bin, bit(16), bin, char(*), bin,
                    char(*), bin, (2) bin, char(*), bin, bin, bin, char(*), bin,
                    char(*), bin);

/* Local declarations */

   dcl fct_field bit(16);              /* For fast select req to xlconn */

$INSERT syscom>x$keys.ins.pl1

/* Make sure the caller did NOT specify XK$FCT */

      if ((key & xk$fct) ^= 0)
      then
         do;
            vcstat(1) = xs$bpm;
            return;
         end;

/* Check anskey for legality, and at same time set appropriate facility
   field */

      select(anskey);

      when(xk$acc)
         fct_field = '0180'b4;

      when(xk$clr)
         fct_field = '01C0'b4;

      otherwise
         do;
            vcstat(1) = xs$bpm;
            return;
         end;
      end;

/* Ready to go, leave the rest for XLCONN... */

      call xlconn(key, vcid, port, adr, adrn, fct_field, 2, 0, 0,
                                       /* No prid field supplied */
                  udata, udatan, vcstat, rusdat, rusmax, rusbc,
                  '0000'b4, '', 0, '', 0); /* morkey, sadr, sadrn, pnet, pnetn */

      return;

   end;

/* END-CODE */

