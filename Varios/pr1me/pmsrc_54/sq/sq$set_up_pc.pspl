/* SQ$SET_UP_PC.SPL, PrintMaster, Jeff Hostetler, November 17, 1983           */
/* Procedure to set up the page control for a job.                            */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

sq$set_up_pc: proc (pc_sd_sd_unit, job_id, nr_copies);

%nolist;
%Include '*>insert>gbl.ins.spl';
%Include '*>insert>sq.ins.spl';
%Include '*>entry>gp$.ins.spl';
%Include '*>entry>err$.ins.spl';
%Include '*>entry>sq$open_entry.ins.spl';
%Include '*>entry>primos.ins.spl';
%list;

   dcl
      pc_sd_sd_unit  bin,                   /* (in) */
      job_id         bin,                   /* (in) */
      nr_copies      bin,                   /* (in) */

      entryb         bin,
      e0_unit        bin,
      code           bin,
      rnw            bin,
      i              bin,
      buf (sq$pc_hdr_len + nr_copies) bin;

   e0_unit = 0;

   on condition (CLEANUP$)
      begin;
      call gp$close (e0_unit);
      end;

%page;

   buf(sq$copies_left_wd) = nr_copies;
   buf(sq$tsn_wd) = 1;

   do i = 1 to nr_copies;
      buf(sq$pc_hdr_len + i) = sq$copy_not_started;
      end;

   /* Now set the size of the pc_sd_sd to nr_copies requested plus 1 for  */
   /* the control file. */

   call sgdr$$ (k$msiz, pc_sd_sd_unit, 1 + nr_copies, entryb, code);
   if code ^= 0 then
      call err$ (err$primos_et, 'Setting size of PC segdir',
                 code, 0, 0);

   /* Now, open pc_sd>pc_sd_sd>0.  It contains the status for each copy and */
   /* the header info */

   call sq$open_entry (k$writ + k$getu + k$iseg + k$nsam,
                       pc_sd_sd_unit, 0, e0_unit);
   call prwf$$ (k$writ, e0_unit, addr(buf), sq$pc_hdr_len + nr_copies,
                0, rnw, code);
   if code ^= 0 then
      call err$ (err$primos_et, 'Writing PC file', code, 0, 0);
   call gp$close (e0_unit);

   return;
   end sq$set_up_pc;
