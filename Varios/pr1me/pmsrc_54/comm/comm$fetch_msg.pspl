/* COMM$FETCH_MSG.SPL, PrintMaster, Jim Wilcoxson, August 4, 1983             */
/* Fetch next incoming message, return pointer to the message and a pointer   */
/* to the user data part of the message.  If no message has been received,    */
/* the function returns false.  Messages are fetched from the receive list.   */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

comm$fetch_msg : procedure (msgp, user_datap) returns (bit);

dcl
   (msgp, user_datap)   ptr;

%nolist;
%include '*>insert>gbl.ins.spl';
%include '*>insert>comm.ins.spl';
%include '*>entry>comm$service_ipc.ins.spl';
%include '*>entry>comm$dequeue_message.ins.spl';
%list;

   if psv.user_msg_bol (gbl.my_user_nr) ^= null then
      call comm$service_ipc;

   msgp = null;
   user_datap = null;
   if comm.receive_list_bol = null then
      return (false);

   if comm$dequeue_message (comm.receive_list_bol,
                            comm.receive_list_eol, msgp) then do;
      user_datap = addr(msgp->msg.user_data);
      return (true);
      end;
   else
      return (false);

end comm$fetch_msg;
