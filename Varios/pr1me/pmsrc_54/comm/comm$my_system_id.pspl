/* COMM$MY_SYSTEM_ID.SPL, PrintMaster, Jim Wilcoxson, March 2, 1986           */
/* Returns the Primenet name of the local system or '' if networks are not    */
/* currently configured.                                                      */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

comm$my_system_id : procedure (my_name) returns (bit);

%nolist;
%include '*>insert>gbl.ins.spl';
%include '*>entry>err$.ins.spl';
%include '*>entry>primos.ins.spl';
%list;

dcl
   my_name     char(*) var,

   x$stat      entry (bin,                  /* key (in) */
                      bin,                  /* unused */
                      char(*),              /* Primenet name (out) */
                      bin,                  /* length of above (out) */
                      char(*),              /* X.25 address (out) */
                      bin,                  /* length of above (out) */
                      bin,                  /* status (out) */
                      bin),                 /* local time (out) */

   num               bin,                   /* not used in this call */
   primenet_name     char(32),
   primenet_name_len bin,
   x25_name          char(32),
   x25_name_len      bin,
   status            bin,
   mins_since_12     bin;                   /* minutes since midnight */

   my_name = '';
   call x$stat (xi$myn, num, primenet_name, primenet_name_len,
                x25_name, x25_name_len, status, mins_since_12);
   select (status);
      when (xs$cmp) do;
         my_name = trim (substr (primenet_name, 1, primenet_name_len), '01'b);
         return (true);
         end;
      when (xs$net)
         return (false);
      otherwise
         call err$ (err$network_et, '', status, 0, 0);
      end;

end comm$my_system_id;
