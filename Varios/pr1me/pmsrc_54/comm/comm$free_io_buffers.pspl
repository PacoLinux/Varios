/* COMM$FREE_IO_BUFFERS.SPL, PrintMaster, Jim Wilcoxson, February 8, 1984     */
/* Frees old xmit buffers.                                                    */
/* Copyright (C) 1984 J.E. Wilcoxson, J.L. Hostetler.  All rights reserved.   */
/******************************************************************************/

comm$free_io_buffers : procedure;

%nolist;
%include '*>insert>gbl.ins.spl';
%include '*>insert>comm.ins.spl';
%include '*>entry>primos.ins.spl';
%include '*>entry>comm$destroy_msg.ins.spl';
%include '*>entry>comm$find_cb.ins.spl';
%list;

dcl
   cbp         ptr,                         /* connection block ptr */
   (p,q,r)     ptr;                         /* current, previous, next */

   q = null;
   do p = comm.buffer_bol repeat r while (p ^= null);
      r = p->msg.next;
      select (p->msg.buffer_status);
         when (xs$ip, xs$mem, xs$max, xs$rst)
            q = p;                          /* leave this buffer */
         otherwise do;
            if q = null then                /* remove from xmit list */
               comm.buffer_bol = r;
            else
               q->msg.next = r;
            if comm$find_cb (p->msg.target_system, cbp) then
               cbp->cb.pending_xmits = cbp->cb.pending_xmits - 1 ;
            call comm$destroy_msg (p);
            end;  /* otherwise */
         end;  /* select */
      end;  /* do */

   comm.buffer_eol = q;                     /* update end of list */

end comm$free_io_buffers;
