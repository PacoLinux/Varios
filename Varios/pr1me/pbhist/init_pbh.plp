/* INIT_PBH.PL1, PRI400>PBHIST, OS GROUP, 08/07/80
/* Initialize Program Base Histogram.
/* Copyright (C) 1979 by Prime Computer Inc., Wellesley MA  02181
/*
/*    Usage: init_pbh -user n -segments s1 ... s8
/*
/* MODIFICATIONS:
/*
   07/23/79 Hampson Initial coding.  */

init_pbh:
        proc (com_args, com_status, com_name) options (nocopy);

dcl com_args char(160) var,
    com_status fixed bin,
    com_name char(32) var;

dcl pbprof entry (fixed bin, fixed bin, (8) fixed bin),
    ioa$er entry options (variable),
    cl$pix entry (bit(16) aligned, char(*) var, ptr, fixed bin, char(*) var, ptr, fixed
            bin, fixed bin, fixed bin);

dcl 1 ci,                                        /* command args info */
        2 user_sw bit(1) aligned,
        2 user_num fixed bin(31),
        2 segs_sw bit(1) aligned,
        2 seg_nums(8) fixed bin(31);

dcl com_pix char(43) var static init ('-user dec; -segments, -segs 8*oct=7777; end');

dcl num_segs fixed bin,
    (pixidx, badidx, code) fixed bin,
    seg_numbers(8) fixed bin,
    meteree fixed bin;

/* Process command args. */

        call cl$pix ('0002'b4, com_name, addr (com_pix), 64, com_args, addr (ci), pixidx,
                    badidx, code);
        if code ^= 0
            then do;
            com_status = 10000;
            return;
            end;

        if ^user_sw
            then meteree = 0;
        else if user_num <= 0 | user_num > 128
            then do;
            call ioa$er ('Invalid user number to meter. %:2d (%v)%.', 100, user_num,
                        com_name);
            com_status = 10001;
            return;
            end;
        else meteree = bin (user_num, 15);
        if ^segs_sw
            then do;
            num_segs = 1;
            seg_numbers(1) = '4000'b3;           /* default metered seg */
            end;
        else do;
            num_segs = 0;
            do pixidx = 1 to 8;
                if seg_nums(pixidx) = 4095
                    then;
                else if seg_nums(pixidx) >= 0 & seg_nums(pixidx) <= 31 | seg_nums(pixidx)
                            >= 1024 & seg_nums(pixidx) <= 1151 | seg_nums(pixidx) >= 2048
                            & seg_nums(pixidx) <= 2303
                    then do;
                    num_segs = num_segs + 1;
                    seg_numbers(num_segs) = bin (seg_nums(pixidx), 15);
                    end;
                else do;
                    call ioa$er ('Illegal segment number to meter. %:2o (%v)%.', 100,
                                seg_nums(pixidx), com_name);
                    com_status = 10002;
                    return;
                    end;
                end;
            end;

        call pbprof (meteree, num_segs, seg_numbers);
        return;

        end;                                     /* init_pbh */
