* ALOC$S, HAMPSO>PL1, BEH, 10/06/78.
* ALLOCATE STORAGE ON THE STACK (FREE ONLY BY PRTN).
* COPYRIGHT (C) 1978 BY PRIME COMPUTER INC., WELLESLEY, MA.
*
* 10/06/78 BEH RECODED AS A SHOPTCALL ENTRY.
* 03/22/78 BEH INITIAL CODING.
*
* ALOC$S: PROC (TEMP$SIZE, RET$PTR, CONTIG) OPTIONS (SHORTCALL (4));
*
* DCL TEMP$SIZE FIXED BIN; NEEDED SIZE OF STORAGE IN WORDS
* DCL RET$PTR PTR; PTR TO ALLOC'D STORAGE (OUTPUT)
* DCL CONTIG BIT(1) ALIGNED; '1'B IF NEW STORAGE CONTIG W/ PRESENT FRAME
*
* TABS-- 10 20 38
*
         SEG
         RLIT
$INSERT STACK_FR_HDR.INS.PMA
*
         ENT       ALOC$S
* ARGUMENT LIST (POINTED TO AT ENTRY BY <L>)
*
TEMP$SIZE EQU      0
RET$PTR  EQU       3
CONTIG   EQU       6
*
ALOC$S   EQU       *
         STL       SB%+STEMP2        SAVE ARG LIST PTR
         EAL       XB%+0             SAVE RETURN PTR
         STL       SB%+STEMP0        ...
         EAXB      SB%+STEMP2,*      ADDRESS ARG LIST
         LDA       XB%+TEMP$SIZE,*   CHECK DESIRED SIZE
         BGT       ALOC$1
         LDL       NULL_PTR          REQ SIZE <=0, DO NOT ALLOC
         STL       XB%+RET$PTR,*     RETURN (NULL)
         JMP       SB%+STEMP0,*      RETURN
*
ALOC$1   XCA                         NUMBER OF WORDS TO EXTEND
         STEX                        DO THE EXTEND
         STL       XB%+RET$PTR,*     SAVE PTR TO EXTENSION
         EAL       SB%+0             SEE IF EXTEN. CONTIG
         ERL       XB%+RET$PTR,*     WITH OUR FRAME
         ANA       SEGNO$MASK        IGNORE DIFFERENT RINGS
         LEQ
         ARR       1
         STA       XB%+CONTIG,*      CONTIG= (SEGNO(RET$PTR) = SEGNO(RETURN$SB))
*
* DONE.
*
         JMP       SB%+STEMP0,*      RETURN
NULL_PTR DATA      '7777,0
SEGNO$MASK EQU     NULL_PTR
*
         FIN
*
         END
