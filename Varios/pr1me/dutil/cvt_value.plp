/* Cvt_value.plp, Dutil, JEW, 10-Mar-80
   Converts a string to a value according to a format structure.
*/

cvt_value: proc (string, my_format, status) returns (bin(31));

$Insert *>insert>global.plp.ins

dcl
   string  char(32) var,
   1  my_format like print_format,
   status  bin;

dcl
   ch$fx2  entry (char(*) var, bin(31), bin),
   ch$hx2  entry (char(*) var, bin(31), bin),
   ch$oc2  entry (char(*) var, bin(31), bin);

dcl
   temp   char(4),
   value  bin(31);


   select (my_format.mode);

      when (1)
         call ch$hx2 (string, value, status);

      when (2)
         do;

         /* ascii conversion - for single word format add leading
            blanks so conversion to short integer will work.
            For two word format, add trailing blanks */

      temp = '';
         select (my_format.words);
            when (1)
               substr (temp, 3, 2) = string;
            when (2)
               temp = string;
            end;
         value = addr (temp) -> fb31;
         status = 0;
         end;

      when (3)
         call ch$oc2 (string, value, status);

      when (4)
         call ch$fx2 (string, value, status);

      end;

   return (value);

end;      /* Cvt_value */

