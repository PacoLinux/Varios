/* To_hts.plp, Dutil, JEW, 9-May-81
   Converts a record address to a head and track address.

   dcl to_hts entry (bin(31), bin, bin, bin, bin, label)

   call to_hts (ra, dvno, head, track, sector, abort);

*/

To_hts: proc (ra, dvno, head, track, sector, abort);

%replace
   sectors_per_track by 9;

dcl
   ra       bin(31),
   dvno     bit(16),
   head     bin,
   track    bin,
   sector   bin,
   abort    label;

dcl
   ioa$er   entry options (variable);

dcl
   head_offset    bin,
   max_nr_recs    bin(31),
   nr_heads       bin,
   sectors_per_cylinder bin;

   nr_heads = substr (dvno, 5, 4) * 2 + substr (dvno, 16, 1);
   sectors_per_cylinder = nr_heads * sectors_per_track;

   max_nr_recs = sectors_per_cylinder * 823;
   if ra > max_nr_recs-1 then
      do;
      call ioa$er ('Partition %:0o has no record address %:3o.%.', -1, dvno, ra);
      goto abort;
      end;

   track = divide (ra, sectors_per_cylinder, 15);
   head_offset = substr (dvno, 1, 4) * 2;
   head = divide ( mod(ra, sectors_per_cylinder), sectors_per_track, 15) + head_offset;
   sector = mod (ra, sectors_per_cylinder);

end;     /* To_hts */
