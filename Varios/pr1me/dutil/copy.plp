/* Copy.plp, Dutil, JEW, 13-Mar-81
   Copy command handler.  Moves records around on the same or
   on different partitions.
*/

Copy: proc (cl, info, clp);

$Insert *>insert>cl.plp.ins
$Insert *>insert>global.plp.ins
$Insert *>insert>diskio.plp.ins

dcl
   help entry (char(*) var, bit);

dcl
   c2080  char(2080) based,
   from_dvno      bin,
   from_ra        bin(31),
   1 my_rec like disk_record,
   to_dvno        bin,
   to_ra          bin(31);


   call cl$par ('0'b, cl, token, token_sz, info, clp, status);
   if token ^= 'TO' then
      do;       /* a from address must have been given */
      call get_address (from_dvno, from_ra);
      if token ^= 'TO' then
         goto help_me;
      call diskio (read_key, from_ra, from_dvno, my_rec, status);
      if status ^= 0 then
         return;
      end;
   else

      /* no from address given, use the current record */

      addr(my_rec) -> c2080 = addr(rec) -> c2080;

   /* parse token after the TO */

   call cl$par ('0'b, cl, token, token_sz, info, clp, status);
   call get_address (to_dvno, to_ra);
   my_rec.cra = to_ra;       /* change cra to match new address */
   call diskio (write_key, to_ra, to_dvno, my_rec, status);
   return;

help_me:
   call help ('COPY', false);
   return;


/* Internal procedure to get a device,record address pair or just
   a record address */

get_address: proc (dvno, ra) options (shortcall);

dcl
   dvno     bin,
   ra       bin(31);


   if info.oct_number then
      do;
      ra = info.oct_val;
      call cl$par ('0'b, cl, token, token_sz, info, clp, status);
      if info.oct_number then
         do;
         dvno = ra;
         ra = info.oct_val;
         call cl$par ('0'b, cl, token, token_sz, info, clp, status);
         end;
      else
         dvno = cpartition;
      end;
   else
      goto help_me;

end;    /* get_address */

end;    /* Copy */
