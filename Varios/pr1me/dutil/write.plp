/* Write.plp, Dutil, JEW, 10-Feb-81
   Write the current disk record.

   WRITE [[<dvno>] <record>]

   If <dvno> is not specified, the current disk is used.  If <record>
   is not specified, it defaults to the current record address (from
   rec.cra).  If <record> is specified, the cra field in the current
   record header is updated to reflect the new record address.

*/

Write: proc (cl, info, clp);

$Insert *>insert>cl.plp.ins
$Insert *>insert>global.plp.ins
$Insert *>insert>diskio.plp.ins

dcl
   help entry (char(*) var, bit);

dcl
   dvno     bin,
   err      bit,
   ra       bin(31);


   ra = rec.cra;
   dvno = cpartition;
   call cl$par ('0'b, cl, token, token_sz, info, clp, status);
   if info.flags.oct_number then
      do;
      ra = info.oct_val;
      call cl$par ('0'b, cl, token, token_sz, info, clp, status);
      if info.flags.oct_number then
         do;
         dvno = ra;
         ra = info.oct_val;
         call cl$par ('0'b, cl, token, token_sz, info, clp, status);
         end;
      end;

   if info.flags.eol then        /* everything ok, do write */
      do;
      rec.cra = ra;
      call diskio (write_key, ra, dvno, rec, status);
      end;
   else
      call help ('WRITE', false);

   return;

end;     /* Write */
