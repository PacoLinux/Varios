/* Findpath.plp, Dutil, JEW, 13-Mar-81
   Find the pathname a record belongs to.
*/

findpath: proc (my_rec, ra) returns (char(160) var);

$Insert *>insert>global.plp.ins
$Insert *>insert>diskio.plp.ins

dcl
   1 my_rec like disk_record,
   ra    bin(31);

dcl
   bra_search entry (1, 2, 3 bin(31), 3 bin(31), 3 bin, 3 bin, 3 bin(31),
                     3 bin(31), 3 bin, 3 (5) bin, 2 (0:1023) bin,
                     bin(31), bin(31), char(*) var, bit) returns (bit);

dcl
   entrybra    bin(31),
   entryname   char(32) var,
   parent      bin(31),
   status      bin;


   call diskio (read_key, ra, cpartition, my_rec, status);
   if status ^= 0 then
      goto freturn;

   /* position to file beginning */

   if my_rec.back ^= 0 then
      do;
      call diskio (read_key, my_rec.bra, cpartition, my_rec, status);
      if status ^= 0 | my_rec.back ^= 0 then
         goto freturn;
      end;

   /* file is at bra.  If this is the MFD, return; else look for
      the file in the parent ufd */

   if my_rec.cra = 1 then
      return ('MFD');
   else
      do;
      entrybra = my_rec.cra;      /* bra of this file */
      parent = my_rec.bra;        /* address of parent directory */

      if bra_search (my_rec, parent, entrybra, entryname, '1'b) then  /* ignore deleted entries */
         return (findpath (my_rec, parent) || '>' || entryname);   /* found me! */
      else
         if bra_search (my_rec, parent, entrybra, entryname, '0'b) then   /* read deleted entries */
            return (findpath (my_rec, parent) || '>' || entryname || '<deleted>');
         else
            return (findpath (my_rec, parent) || '>' || '?');
      end;

freturn:
   return ('?');

end;    /* Findpath */
