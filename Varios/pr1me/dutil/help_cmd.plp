/* Help_cmd.plp, Dutil, JEW, 9-May-81
   HELP command handler.

   HELP [<name> [-S | -L]]

   If <name> is omitted, a directory of available help will be listed.

   If <name> is given, -S or -L define whether long or short help should
   be printed.  -L is the default.

*/

Help_cmd: proc (cl, info, clp);

$Insert *>insert>global.plp.ins
$Insert *>insert>cl.plp.ins

dcl
   help     entry (char(*) var, bit),
   ioa$     entry options (variable);

dcl
   command     char(32) var,
   do_long     bit;


   /* get command name if given */

   call cl$par ('0'b, cl, token, token_sz, info, clp, status);

   select;

      when (info.eol)
         call help ('', true);     /* print directory */

      when (info.keyword)
         call ioa$ ('-S and -L may only be used if a name is given.%.', -1);

      otherwise
         do;

         /* have <name>, check for -S or -L */

         command = token;
         do_long = true;     /* default */
         call cl$par ('0'b, cl, token, token_sz, info, clp, status);
         if ^info.eol then
            do;
            if token = '-S' then
               do_long = false;
            else if token = '-L' then ;
            else
               goto help_me;

            call cl$par ('0'b, cl, token, token_sz, info, clp, status);
            if ^info.eol then
               call ioa$ ('Extraneous tokens ignored.%.', -1);
            end;

         call help (command, do_long);
         end;
      end;     /* select */

   return;

help_me:
   call ioa$ ('Unrecognized option "%v".%.', -1, token);

end;     /* Help_cmd */
