/* Disk.plp, Dutil, JEW, 7-Feb-81
   Disk command handler.

   DISK {<dvno>}

   If <dvno> is omitted, prints information about the current partition.

*/

disk: proc (cl, info, clp);

$Insert *>insert>cl.plp.ins
$Insert *>insert>global.plp.ins
$Insert *>insert>diskio.plp.ins

dcl
   cp$       entry (char(*) var, bin, bin),
   disk_info entry (bin, 1, 2 bin, 2 bin, 2 bin, 2 bin, 2 bin, 2 bin(31) ),
   disp_info entry (bin, 1, 2 bin, 2 bin, 2 bin, 2 bin, 2 bin, 2 bin(31) ),
   help entry (char(*) var, bit),
   ioa$ entry options (variable),
   print_header entry (1, 2, 3 bin(31), 3 bin(31), 3 bin, 3 bin, 3 bin(31),
                       3 bin(31), 3 bin, 3 (5) bin, 2 (0:1023) bin),
   srwrec entry (bin, bin, bin, bit(16), bin(31), bin, label);

dcl
   partition_nr   bin(31);


/* setup cpartition and seek to cylinder zero */

   call cl$par ('0'b, cl, token, token_sz, info, clp, status);
   if info.flags.oct_number then
      do;

      /* new partition setup */

      cpartition = info.oct_val;
      call disk_info (cpartition, cpartition_info);

      /* assign the device */

      partition_nr = cpartition;
      if cpartition < 0 then
         partition_nr = partition_nr + 65536;   /* so to_octal will work */
      call cp$ ('as disk [to_octal ' || char(partition_nr) || ']', status, status);

      /* seek to track zero and read MFD record */

      call srwrec (2, 0, 0, 0, 0, cpartition, io_error);
      call diskio (read_key, 1, cpartition, rec, status);

      /* display partition info *

      call disp_info (cpartition, cpartition_info);

      if status =  0 then       /* MFD is current record */
         do;
         call ioa$ ('%.', -1);
         call print_header (rec);
         end;
      end;
   else
      if info.flags.eol then      /* display info about current partition */
         if cpartition ^= -1 then
            call disp_info (cpartition, cpartition_info);
         else
            call ioa$ ('No partition currently defined.%.', -1);
      else
         call help ('DISK', false);

   return;

io_error:
   call ioa$ ('Couldn''t seek to cylinder zero, next command must be DISK%/%.', -1);
   cpartition = -1;

end;   /* disk */
