/* Cpl command file to compile and optionally load DUTIL.
/*
/* R cmp [filename] -load -debug
/*
/* If <filename> is given, only 1 module will be compiled.
/*

&args filename:char; options:uncl; load:-load,-l; debug:-debug,-d; list:-list; binary: -b

&severity &warning &ignore
como cmp.como
&severity &warning &routine oops

&if ^ [null %filename%] &then &do
   &do file &list [unquote %filename%]
      &if [null [after %file% .]] &then ~
          &if [exists %file%.plp] &then ~
              &s file := %file%.PLP
          &else &if [exists %file%.pma] &then ~
              &s file := %file%.PMA
          &else &if [exists %file%.ftn] &then ~
              &s file := %file%.FTN
          &else &do
              type %file% not found.
              &call oops
              &end
      &call compile_file
      &end   /* &do &list */
   &end    /* &if */

&else &if [query 'Verify compiling all files' FALSE] &then &do

   /* Compiling all of Dutil, get rid of all old object and listing filenames

   &data futil
      f *>bin
      ufddel
      f *>list
      ufddel
      quit
      &end

   &s unit := 0
   &do file &items [wild @@ -single unit]
      &call compile_file
      &end     /* &do */

   &end    /* &else &do */
&else &stop

&if %load% = -LOAD &then r load [before %filename% .]
como -e
&return


&routine compile_file

&severity &warning &routine oops

/* routine to compile one routine.  %file% is the file to compile */

&s base := [before %file% .]
&s compiler := [after %file% .]
&select %compiler%
   &when PLP, FTN, F77, PMA
      &do
      type Compiling %file%
      &if %compiler% = PMA &then ~
         %compiler% %file% -b *>bin>%base%.bin -l *>list>%base%.list %options%
      &else ~
         %compiler% %file% -b *>bin>%base%.bin -l *>list>%base%.list %debug% %options%
      &end
   &end    /* &select */
&return


&routine oops

/* routine called on error */

como -e
&stop &message Compilation aborted.
