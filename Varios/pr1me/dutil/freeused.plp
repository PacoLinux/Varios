/* Freeused.plp, Dutil, JEW, 14-Feb-81
   Free/Used command.  Marks a record free/used in the disk r.a.t.  */

Free: proc (cl, info, clp);

$Insert *>insert>global.plp.ins
$Insert *>insert>cl.plp.ins

dcl
   help entry (char(*) var, bit),
   ioa$er entry options (variable),
   set_rat entry (bin(31), bit) returns (bit);

dcl
   ra       bin(31),
   rat_value bit;       /* new value for rat bit, '1'b = free, '0'b = used */


   rat_value = '1'b;
   goto do_command;


Used: entry (cl, info, clp);

   rat_value = '0'b;

do_command:

   call cl$par ('0'b, cl, token, token_sz, info, clp, status);
   if info.flags.oct_number then
      do;
      ra = info.oct_val;
      if ^set_rat (ra, rat_value) then
         call ioa$er ('Unable to change rat.%.', -1);
      end;
   else
      if rat_value = '1'b then
         call help ('FREE', false);
      else
         call help ('USED', false);

end;    /* Free/Used */
