/* Claim_record.plp, Dutil, JEW, 28-Mar-81
   Claim a disk record.  Sets data count to zero and optionally
   updates rat. */

Claim_record: proc (ra, status);

$Insert *>insert>global.plp.ins
$Insert *>insert>diskio.plp.ins

dcl
   ra       bin(31),
   status   bin;

dcl
   set_rat  entry (bin(31), bit) returns (bit);

dcl
   1  my_rec like disk_record;


   call diskio (read_key, ra, cpartition, my_rec, status);
   if status ^= 0 then
      return;

   if my_rec.count ^= 0 then
      do;

      my_rec.count = 0;

      call diskio (write_key, ra, cpartition, my_rec, status);
      if status ^= 0 then
         return;

      end;

   if update_rat then
      status = set_rat (ra, '0'b);     /* mark record unavailable */
   else
      status = 0;

end;
