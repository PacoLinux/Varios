/* Print_header.plp, Dutil, JEW, 7-Feb-81
   Print record header info.  */

print_header: proc (my_rec);

$Insert *>insert>global.plp.ins

dcl
   ioa$ entry options (variable);

dcl
   1 my_rec like disk_record,
   type  char(25) var;


   call ioa$ ('Record  %7:3o%5x%$', -1, my_rec.cra);
   select (my_rec.type);
      when (0) type = 'Sam file';
      when (1) type = 'Dam index';
      when (2) type = 'Sam segment directory';
      when (3) type = 'Dam segment directory';
      when (4) type = 'Ufd';
      otherwise
         if my_rec.type = -32768 then
            type = 'Special';
         else
            type = '';
      end;

   if type = '' then
      call ioa$ ('Record type (%6w)%.', -1, my_rec.type);
   else
      call ioa$ ('%v record%.', -1, type);

   call ioa$ ('Bra%5x%7:3o%5xData count  %d%.', -1, my_rec.bra, my_rec.count);
   call ioa$ ('Back%4x%7:3o%5xNext%5x%7:3o%.', -1, my_rec.back, my_rec.next);

   if my_rec.level ^= 0 then
      call ioa$ ('Level   %d%.', -1, my_rec.level);

end;   /* print_header */
