/* Set_rat.plp, Dutil, JEW, 10-May-81
   Changes the rat bit for a record.

   dcl set_rat entry (bin(31), bit) returns (bit)

   if set_rat (ra, new_rat_value) then ...

*/

Set_rat: proc (ra, new_value) returns (bit);

$Insert *>insert>global.plp.ins
$Insert *>insert>diskio.plp.ins

dcl
   ra          bin(31),
   new_value   bit;

dcl
   rat_address entry (bin(31)) returns (bin(31)),
   rat_offsets entry (bin(31), bin, bin);

dcl
   bit16       bit(16) based,
   bitx        bin,                 /* rat bit for ra */
   1  my_rec like disk_record,
   rat_ra      bin(31),             /* addr of rat record for ra */
   status      bin,
   wordx       bin;                 /* rat word for ra */


   rat_ra = rat_address (ra);      /* get rat record address */
   call diskio (read_key, rat_ra, cpartition, my_rec, status);
   if status = 0 then
      do;
      call rat_offsets (ra, wordx, bitx);     /* compute word and bit number */
      substr (addr (my_rec.data(wordx)) -> bit16, bitx, 1) = new_value;
      call diskio (write_key, rat_ra, cpartition, my_rec, status);
      end;
   return (status = 0);

end;     /* Set_rat */
