/* Delete.plp, Dutil, JEW, 11-Mar-81
   Deletes a file in the file system by changing to vacant
   entry.
*/

delete: proc (cl, info, clp);

$Insert *>insert>cl.plp.ins
$Insert *>insert>global.plp.ins
$Insert *>insert>diskio.plp.ins

dcl
   findbra  entry (1, 2, 3 bin(31), 3 bin(31), 3 bin, 3 bin, 3 bin(31),
                   3 bin(31), 3 bin, 3 (5) bin, 2 (0:1023) bin,
                   char(*) var, bin(31), bit, bin),
   findecw  entry (1, 2, 3 bin(31), 3 bin(31), 3 bin, 3 bin, 3 bin(31),
                   3 bin(31), 3 bin, 3 (5) bin, 2 (0:1023) bin,
                   bin(31), bin(31), bit, 1, 2 bin(31), 2 bin) returns (bit),
   ioa$ entry options (variable);

dcl
   1  ecw  based,
      2  type     bit(8),
      2  length   bit(8),
   1  ecwpos,
      2  ra    bin(31),
      2  wn    bin,
   filebra     bin(31),
   1  my_rec like disk_record,
   parent      bin(31);


   call cl$par ('0'b, cl, token, token_sz, info, clp, status);
   if info.oct_number then
      filebra = info.oct_val;
   else
      do;
      call findbra (my_rec, token, filebra, '1'b, status);   /* ignore deleted entries */
      if status ^= 0 then
         do;
         call ioa$ ('%v not found.%.', -1, token);
         return;
         end;
      end;

   /* have file bra.  Verify this and find parent */

   call diskio (read_key, filebra, cpartition, my_rec, status);
   if status ^= 0 then return;

   if my_rec.back ^= 0 then
      call ioa$ ('This is not the beginning of a file.  Bra is %:3o.%.', -1,
                  my_rec.bra);
   else
      do;
      parent = my_rec.bra;
      if findecw (my_rec, filebra, parent, '1'b, ecwpos) then
         do;
         addr (my_rec.data (ecwpos.wn)) -> ecw.type = '02'b4;   /* make deleted */
         call diskio (write_key, (my_rec.cra), cpartition, my_rec, status);
         if status = 0 then
            call ioa$ ('File deleted.%.', -1);
         else
            call ioa$ ('Could not rewrite parent directory.%.', -1);
         end;
      else
         call ioa$ ('Cannot find file in parent directory.%.', -1);
      end;

end;
