/* Link_record.plp, Dutil, JEW, 28-Mar-81
   Links a record into a file chain.

   dcl link_record entry (bin(31), 1, 2, 3, ..., bin)

   call link_record (new_ra, my_rec, status)

   Links record at new_ra to file chain after my_rec.

*/

Link_record: proc (ra, his_rec, status);

$Insert *>insert>global.plp.ins
$Insert *>insert>diskio.plp.ins

dcl
   ra       bin(31),
   1  his_rec like disk_record,
   status   bin;

dcl
   1  my_rec like disk_record,
   new_ra   bin(31);


   /* read new record */

   call diskio (read_key, ra, cpartition, my_rec, status);
   if status ^= 0 then
      return;

   /* link new record to old and write both */

   my_rec.back = his_rec.cra;
   my_rec.next = his_rec.next;
   his_rec.next = ra;

   /* write both records */

   call diskio (write_key, (my_rec.cra), cpartition, my_rec, status);
   if status ^= 0 then
      return;
   call diskio (write_key, (his_rec.cra), cpartition, his_rec, status);
   if status ^= 0 then
      return;

   if my_rec.next ^= 0 then
      do;

      /* read forward record and link my_rec to it */

      new_ra = my_rec.cra;
      call diskio (read_key, (my_rec.next), cpartition, my_rec, status);
      if status ^= 0 then
         return;

      my_rec.back = new_ra;
      call  diskio (write_key, (my_rec.cra), cpartition, my_rec, status);
      end;

end;     /* Link_record */

