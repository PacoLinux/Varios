$ SET NOVERIFY
$ !
$ WRITE SYS$OUTPUT " 	***  Welcome to UETP V1.0 built 21-AUG-78  *** "
$ !
$ ON ERROR THEN CONTINUE
$ MCR PIP UETPLOG.LOG=TI:

$ MCR PIP LOAD.LOG=TI:

$ MCR PIP LOGP.LOG=TI:

$ !
$ !
$ ASKUSR:
$ INQUIRE USERS ENTER NUMBER OF LOAD TEST USERS [D]
$ IF USERS .LT. 1 .OR. USERS .GT. 48 THEN GOTO ASKUSR
$ DEF/GR UETP$USERS 'USERS
$ !
$ LABEL1:
$ INQUIRE COUNT ENTER NUMBER OF COMPLETE UETP RUNS [D]
$ IF COUNT .LT. 1 THEN GOTO LABEL1
$ !
$ !Get the device number of a scratch magtape for the FLX test.(i.e.MT0)
$ INQUIRE X ENTER SCRATCH MAGTAPE (e.g. MT0:) or a <CR>
$ MAGTAP:=='X
$ !
$ !
$ !GET THE CURRENT CONFIGURATION FROM THE SYSTEM TABLES.
$ RUN UETINIT00
$ !
$ ! NOW QUICK CHECK ALL DEVICES AND MARK OFFLINE IF UNAVAILABLE
$ RUN UETINIT01
$ !
$ END_OF_PASS:= 0
$ !
$ LOOP:
$ !
$ !DELETE DETACHED PROCESS' LOGS FROM PREVIOUS PASS
$ !
$ DEL LOAD.LOG;*
$ DEL LOGP.LOG;*
$ !
$ SET VERIFY
$ ! **********  PERIPHERAL DEVICE TEST ************
$ RUN UETPDEV01
$ !
$ !
$ ! ********** NATIVE MODE TESTS *******
$ !
$ ! VAX-11 RMS TESTS
$ @UETNRMS00

$ ! VMS SYSTEM SERVICES
$ RUN UETNATV01
$
$ ! NATIVE MODE UTILITIES
$ @UETNATV02/OUTPUT=UNATIVE.LOG
$
$ ! NATIVE MODE FORTRAN IV TEST
$ @UETFORT00
$
$ SET VERIFY
$ ! ***************  LOAD TEST  *******************
$ RUN UETLOAD01
$ !
$ ! ********* COMPATIBILITY MODE TESTS ************
$ !
$ @UETCOMP00/OUTPUT=UCOMP.LOG
$ SET NOVERIFY
$ !
$ ! PERFORM SOME CLEANUP. MERGE LOG FILES INTO ONE.
$ MCR PIP UETPLOG.LOG=LOGP.LOG;*/AP
$ MCR PIP UETPLOG.LOG=UNATIVE.LOG;*/AP
$ MCR PIP UETPLOG.LOG=LOAD.LOG;*/AP
$ MCR PIP UETPLOG.LOG=UCOMP.LOG;*/AP
$ PURGE *.*
$ !
$ SHOW DAYTIME
$ END_OF_PASS = END_OF_PASS + 1
$ !
$ SET VERIFY
$ !************************************************************
$ !
$ 	SHOW 	SYMB 		END_OF_PASS
$ !
$ !************************************************************
$ SET NOVERIFY
$ IF END_OF_PASS .LT. COUNT THEN GOTO LOOP
$ !
$ SET NOON
$ DEAS/GR UETP$CTRLNAME
$ DEAS/GR UETP$USERS
$ DEAS/GR UETP$DETACH
$ DEAS/GR SYSTST$ERRS
$ DEAS/GR TAPE0
$ DEAS/GR TAPE1
$ DEAS/GR TAPE2
$ DEAS/GR TAPE3
$ DEAS/GR TAPE4
$ DEAS/GR TAPE5
$ DEAS/GR TAPE6
$ DEAS/GR TAPE7
$ RUN UETTERM01
