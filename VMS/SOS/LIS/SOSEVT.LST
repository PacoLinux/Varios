SOSEVT - EVENT RECORD AND REPOR	MACRO M1110  22-AUG-78 05:32  PAGE 2


    100						.TITLE	SOSEVT - EVENT RECORD AND REPORT FOR DEBUG
    200						.IDENT	/6B0201/
    300					;
    400					; COPYRIGHT 1977, DIGITAL EQUIPMENT CORP., MAYNARD, MASS. 01754
    500					;
    600					; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF ITS
    700					; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
    800					;
    900					; THIS SOFTWARE IS FURNISHED TO PURCHASER UNDER A LICENSE FOR USE
   1000					; ON A SINGLE COMPUTER SYSTEM AND CAN BE COPIED (WITH INCLUSION
   1100					; OF DEC'S COPYRIGHT NOTICE) ONLY FOR USE IN SUCH SYSTEM, EXCEPT
   1200					; AS MAY OTHERWISE BE PROVIDED IN WRITING BY DEC.
   1300					;
   1400					; THE INFORMATION IN THIS DOCUMENT IS SUBJECT TO CHANGE WITHOUT
   1500					; NOTICE AND SHOULD NOT BE CONSTRUED AS A COMMITMENT BY DIGITAL
   1600					; EQUIPMENT CORPORATION.
   1700					;
   1800					; PETER H. LIPMAN	9-JUN-77
   1900					;
   2000						.IF	DF,R$$DBG		;ENTIRE MODULE IS FOR DEBUG ONLY
   2100
   2200						CODE$	EVT
SOSEVT - EVENT RECORD AND REPOR	MACRO M1110  22-AUG-78 05:32  PAGE 3


     20						.SBTTL	DELETE & RENAME BUG PRINTOUT
     22					;
     24					; ERROR RENAMING FILE
     26					;
     30					CATBUG::				;RENAME FAILURE
     40						JSR	PC,BUG1			;EXTRA LEVEL OF SUBR
     50						JMP	EXIT			;MUST EXIT, CAN'T CONTINUE
     60
     80					BUG1:	NEG	R0			;MAKE FCS ERR CODE POSITIVE
    100						JSR	PC,BUGINI		;INIT FOR BUG REPORT
    130						JSR	R1,(R2)
    140						.WORD	CAT1			;FAILED TO RENAME FILE
    160						INC	(SP)			;MIN FIELD WIDTH = 1
    170						JSR	PC,PRINTN		;PRINT DECIMAL NUMBER
    180						JSR	PC,TYPECR		;CR, LF
    190						CLR	(SP)			;ZERO AT 0(SP) FOR PRTFIL
    200						BR	BUG2			;WILL RETURN TO "JMP EXIT" ABOVE
    210
    300					;
    320					; ATTEMPTED A DELETE ON FDB WITH 0 VERSION NUMBER
    340					;
    400					DELBUG::
    700						JSR	R1,$SAV3		;SAVE R1,R2,R3
    800						JSR	PC,BUGINI		;INIT FOR BUG PRINTOUT
    900						CLR	(SP)			;ZERO FOR PRTFIL
   1300						JSR	R1,(R2)
   1400						.WORD	DEL1			;ATTEMPTED TO DELETE FILE
   1500						JSR	PC,(R3)			;PRINT IT'S NAME
   1600					BUG2:	JSR	R1,(R2)
   1700						.WORD	DEL2			;FILUSE VALUE
   1800						MOV	FILUSE,-(SP)
   1900						MOV	#1,-(SP)
   2000						JSR	PC,PRINTN		;PRINT ITS VALUE
   2100						CMP	(SP)+,(SP)+
   2200						JSR	PC,TYPECR
   2300						JSR	R1,(R2)
   2400						.WORD	DEL3			;FILEIN FDB
   2500						MOV	#FILEIN,(R1)		;STORE ADDRESS OF FILEIN FDB
   2600						JSR	PC,(R3)			;PRINT FILE NAME
   2700						JSR	R1,(R2)
   2800						.WORD	DEL4			;FILEOUT FDB
   2900						MOV	#FILEOUT,(R1)		;STORE ADDRESS OF FILEOUT FDB
   3000						JSR	PC,(R3)			;PRINT FILE NAME
   3100						JSR	R1,(R2)
   3200						.WORD	DEL5			;FILETMP FDB
   3300						MOV	#FILETMP,(R1)		;STORE ADDRESS OF FILETMP FDB
   3400						JSR	PC,(R3)			;PRINT FILE NAME
   3500						CMP	(SP)+,(SP)+
   3600						JSR	PC,GIVEVT		;TYPE THE EVENT REPORT
   3700						RTS	PC
   3720					;
   3740					; INIT FOR BUG REPORTING
   3760					;	RETURNS WITH 2 ADDITIONAL WORDS ON CALLER'S STACK:
   3770					;		0(SP) = 0
   3780					;		2(SP) = SAVED R0
   3800					;	AND WITH REGISTERS SET AS FOLLOWS:
   3820					;		R0 = GARBAGE
SOSEVT - EVENT RECORD AND REPOR	MACRO M1110  22-AUG-78 05:32  PAGE 3-1


   3840					;		R1 = POINTER TO 2(SP)
   3860					;		R2 = ADR OF PRTINL SUBR
   3880					;		R3 = ADR OF PRTFIL SUBR
   3900					;
   3920					BUGINI:	MOV	(SP),R2			;SAVE RETURN ADDRESS
   4000						MOV	R0,(SP)			;SAVE R0
   4100						MOV	SP,R1			;R1 = ADR TO STORE FDB ADR'S FOR PRTFIL
   4200						CLR	-(SP)			;RESERVE ANOTHER WORD
   4250						MOV	R2,-(SP)		;PUT RETURN ADDRESS BACK ON STACK
   4300						MOV	#PRTINL,R2		;PRINT "IN LINE" MSG SUBR ADR
   4400						MOV	#PRTFIL,R3		;PRINT FILE NAME SUBROUTINE ADR
   4500						JSR	R1,(R2)
   4600						.WORD	BUGM1			;PLEASE REPORT
   4700						RTS	PC			;INIT COMPLETE
SOSEVT - EVENT RECORD AND REPOR	MACRO M1110  22-AUG-78 05:32  PAGE 4


    100						.SBTTL	EVENT RECORDING AND PRINTING ROUTINES
    200					;
    300					; ALL OF THIS CODE IS PRESENT FOR KEEPING A RECORD OF OPENS, RENAMES,
    400					; AND DELETES SO THAT A BUG CAN BE FOUND
    500					;
    600					;
    700					; RECORD AN EVENT
    800					;	R0 = FDB ADDRESS
    900					;	2(SP) = EVENT CODE
   1000					;		1 - OPEN FOR READ
   1100					;		2 - OPEN FOR WRITE
   1200					;		3 - REOPEN FOR READ
   1300					;		4 - RENAME FROM FDB GIVEN (NOT TO -1 FDB)
   1400					;		5 - RENAME FROM FDB GIVEN TO -1 FDB
   1500					;		6 - DELETE (FDB CODE OF 0 IF -1)
   1600					; RECORDS 1 WORD IN THE EVENT BUFFER (A RING BUFFER)
   1700					;	3 DECIMAL DIGITS FROM HIGH ORDER TO LOW ORDER (LEFT TO RIGHT)
   1800					;		1 - EVENT CODE
   1900					;		2 - FDB CODE
   2000					;			0 - THE -1 FDB WAS GIVEN
   2100					;			1 - FILEIN
   2200					;			2 - FILEOUT
   2300					;			3 - FILETMP
   2400					;			4 - FILELST
   2500					;			5 - FILECOP = FILENEW
   2600					;		3 - FILUSE PARAMETER
   2610					;			1 - CREATING AN OUTPUT FILE VIA INPUT
   2620					;			2 - EDITING SINGLE FILE, BAK NOT YET DONE
   2630					;			3 - EDITING SINGLE FILE, BAK DONE, RO, OR NOBAK
   2640					;			4 - TWO FILES GIVEN IN INITIAL COMMAND
   2650					;			5 - USING TEMP INPUT, FILEOUT IS REAL DESTINATION
   2660					;			6 - USING TEMP INPUT, FILEIN IS REAL DESTINATION
   2700					;
   2800					RECEVT::MOV	R0,-(SP)		;SAVE SOME REGISTER, ALL ARE PRESERVED
   2900						MOV	R1,-(SP)
   3000						MOV	R0,R1			;FDB ADDRESS OR -1
   3100						MOV	6(SP),R0		;EVENT CODE
   3200						JSR	PC,MUL10		;MULTIPLY R0 BY 10 DECIMAL
   3300						CMP	#-1,R1			;-1 FDB?
   3400						BEQ	25$			;LEAVE 0 FDB CODE
   3500						INC	R0			;OTHERWISE FDB CODES START AT 1
   3600						SUB	#FILEIN,R1		;SUBTRACT OUT BASE OF FDB ARRAY
   3700						BR	20$
   3800					10$:	INC	R0			;COUNT ANOTHER FDB
   3900						SUB	#S.FDB,R1		;TAKE ANOTHER FDB'S WORTH AWAY
   4000					20$:	BGT	10$			;BRANCH IF NOT DONE
   4100					25$:	JSR	PC,MUL10		;MULTIPLY BY 10 AGAIN
   4200						ADD	FILUSE,R0		;PUT IN FILUSE PARAMETER
   4300						MOV	EVTADR,R1		;POINTER TO NEXT WORD IN RING BUFFER
   4400						MOV	R0,(R1)+		;STORE THE EVENT
   4500						CMP	R1,#EVTEND		;AND WRAP
   4600						BLO	30$			;THE POINTER
   4700						MOV	#EVTBEG,R1		;IF NECESSARY
   4800					30$:	MOV	R1,EVTADR		;UPDATE THE COPY IN MEMORY
   4900						MOV	(SP)+,R1		;RESTORE REGISTERS
   5000						MOV	(SP)+,R0
   5100						RTS	PC			;AND RETURN
SOSEVT - EVENT RECORD AND REPOR	MACRO M1110  22-AUG-78 05:32  PAGE 4-1


   5200					;
   5300					; MULTIPLY R0 BY 10
   5400					;
   5500					MUL10:	MOV	R0,-(SP)
   5600						ASL	R0
   5700						ASL	R0
   5800						ADD	(SP)+,R0
   5900						ASL	R0
   6000						RTS	PC
   6100					;
   6200					; GIVEVT - GIVE (PRINT) EVENT BUFFER FROM OLDEST TO NEWEST, LEFT TO RIGHT
   6300					;	SEE ABOVE FOR FORMAT
   6400					;
   6500					GIVEVT::JSR	R1,$SAV2		;SAVE SOME REGISTERS, ONLY R0 ALTERED
   6600						JSR	R1,PRTINL		;PRINT IN LINE MSG
   6700						.WORD	EVT1			;EVENTS =
   6800						MOV	#<EVTEND-EVTBEG>/2,R2	;NO. OF WORDS IN EVENT BUFFER
   6900						MOV	EVTADR,R1		;NEXT WORD TO STORE IS FIRST TO PRINT
   7000						CMP	-(SP),-(SP)		;MAKE ROOM FOR 2 PARAMETERS
   7100					10$:	MOV	(R1)+,2(SP)		;WORD TO BE CONVERTED TO DECIMAL
   7200						MOV	#3,(SP)			;NO. OF DIGITS
   7300						JSR	PC,PRINTN		;PRINT DECIMAL DIGITS
   7400						MOV	#' ,(SP)		;SEPARATE WITH SPACES
   7500						JSR	PC,TYPE
   7600						CMP	R1,#EVTEND		;WRAP THE POINTER
   7700						BLO	20$			;IF NECESSARY
   7800						MOV	#EVTBEG,R1
   7900					20$:	SOB	R2,10$			;PRINT EACH WORD IN RING BUFFER
   8000						JSR	PC,TYPECR		;CR, LF
   8100						CMP	(SP)+,(SP)+		;CLEAN THE STACK
   8200						RTS	PC			;AND RETURN
   8300
   8400						PLIT	EVT1,<^Events = ^>
   8500					;
   8600					; ***** DEBUGING MESSAGES
   8700					;
   9200						PLIT	BUGM1,<<15><12>^Please report this output to Software Support^>
   9400						PLIT	DEL1,<<15><12>^Attempted to DELETE: ^>
   9700						PLIT	DEL2,<^FILUSE  = ^>
   9800						PLIT	DEL3,<^FILEIN  = ^>
   9900						PLIT	DEL4,<^FILEOUT = ^>
  10000						PLIT	DEL5,<^FILETMP = ^>
  10100						PLIT	CAT1,<<15><12>^Fatal RENAME Error Code = -^>
  10200
  10300
  10350						.ENDC
  10400		000001 				.END
SOSEVT - EVENT RECORD AND REPOR	MACRO M1110  22-AUG-78 05:32  PAGE 4-2
SYMBOL TABLE

RONLY$= 000001   	R$$EIS= 000001   	R$$SHR= 000001

. ABS.	000000	   000
      	000000	   001
ERRORS DETECTED:  0

VIRTUAL MEMORY USED:  582 WORDS  ( 3 PAGES)
DYNAMIC MEMORY:  2772 WORDS  ( 10 PAGES)
ELAPSED TIME:  00:00:04
OBJ$:SOSEVT,LIS$:SOSEVT/-SP/NL:TOC=SRC$:PREFIX,SOSEVT
