SYSUNWIND       - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK     21-AUG-1978 23:48:54   VAX-11 MACRO X0.3-11               Page   0
TABLE OF CONTENTS   

   (1)      56  UNWIND PROCEDURE CALL STACK
   (1)     198  UNWIND CONDITION HANDLER
   (1)     213  CALCULATE VALUE OF SP BEFORE CALL
SYSUNWIND       - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK     21-AUG-1978 23:48:54   VAX-11 MACRO X0.3-11               Page   1
01                                                                                                                               (1)

                                     0000     1 	.TITLE	SYSUNWIND - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK
                                     0000     2 	.IDENT	/01/
                                     0000     3  
                                     0000     4 ;
                                     0000     5 ; COPYRIGHT (C) 1977
                                     0000     6 ; DIGITAL EQUIPMENT CORPORATION, MAYNARD, MASS.
                                     0000     7 ;
                                     0000     8 ; THIS SOFTWARE IS FURNISHED UNDER  A LICENSE FOR USE ONLY  ON  A
                                     0000     9 ; SINGLE COMPUTER SYSTEM AND MAY BE  COPIED ONLY WITH  THE INCLU-
                                     0000    10 ; SION OF  THE  ABOVE  COPYRIGHT NOTICE.  THIS SOFTWARE,  OR  ANY
                                     0000    11 ; OTHER COPIES THEREOF, MAY NOT BE  PROVIDED  OR  OTHERWISE  MADE
                                     0000    12 ; AVAILABLE TO ANY OTHER PERSON EXCEPT  FOR  USE  ON  SUCH SYSTEM
                                     0000    13 ; AND TO  ONE WHO AGREES  TO  THESE LICENSE  TERMS.  TITLE TO AND
                                     0000    14 ; OWNERSHIP OF THE SOFTWARE SHALL AT ALL TIMES REMAIN IN DEC.
                                     0000    15 ;
                                     0000    16 ; THE INFORMATION IN THIS SOFTWARE  IS  SUBJECT TO CHANGE WITHOUT
                                     0000    17 ; NOTICE AND SHOULD NOT BE CONSTRUED  AS  A COMMITMENT BY DIGITAL
                                     0000    18 ; EQUIPMENT CORPORATION.
                                     0000    19 ;
                                     0000    20 ; DEC ASSUMES NO RESPONSIBILITY FOR THE USE OR RELIABILITY OF ITS
                                     0000    21 ; SOFTWARE ON EQUIPMENT WHICH IS NOT SUPPLIED BY DEC.
                                     0000    22 ;
                                     0000    23 ; D. N. CUTLER 16-DEC-76
                                     0000    24 ;
                                     0000    25 ; SYSTEM SERVICE UNWIND PROCEDURE CALL STACK
                                     0000    26 ;
                                     0000    27 ; MACRO LIBRARY CALLS
                                     0000    28 ;
                                     0000    29  
                                     0000    30 	$CHFDEF				;DEFINE CONDITION HANDLING ARGLIST OFFSETS
                                     0000    31 	$SSDEF				;DEFINE SYSTEM STATUS VALUES
                                     0000    32  
                                     0000    33 ;
                                     0000    34 ; LOCAL SYMBOLS
                                     0000    35 ;
                                     0000    36 ; ARGUMENT LIST OFFSET DEFINITIONS
                                     0000    37 ;
                                     0000    38  
                           00000004  0000    39 DEPADR=4				;ADDRESS OF NUMBER OF FRAMES TO UNWIND
                           00000008  0000    40 NEWPC=8					;CHANGE OF FLOW FINAL RETURN ADDRESS
                                     0000    41  
                                     0000    42 ;
                                     0000    43 ; CALL FRAME OFFSET DEFINITIONS
                                     0000    44 ;
                                     0000    45  
                           00000000  0000    46 HANDLER=0				;CONDITION HANDLER ADDRESS
                           00000004  0000    47 SAVPSW=4				;SAVED PSW FROM CALL
                           00000006  0000    48 SAVMSK=6				;REGISTER SAVE MASK
                           00000008  0000    49 SAVAP=8					;SAVED AP REGISTER IMAGE
                           0000000C  0000    50 SAVFP=12				;SAVED FP REGISTER IMAGE
                           00000010  0000    51 SAVPC=16				;SAVED PC REGISTER IMAGE
                           00000014  0000    52 SAVRG=20				;OTHER SAVED REGISTER IMAGES
                                     0000    53 
                                 00000000    54 	.PSECT	YEXEPAGED,BYTE		;PAGED PSECT
SYSUNWIND       - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK     21-AUG-1978 23:48:54   VAX-11 MACRO X0.3-11               Page   2
01              UNWIND PROCEDURE CALL STACK                                                                                      (1)

                                     0000    56 	.SBTTL	UNWIND PROCEDURE CALL STACK
                                     0000    57 ;+
                                     0000    58 ; EXE$UNWIND - UNWIND PROCEDURE CALL STACK
                                     0000    59 ;
                                     0000    60 ; THIS SERVICE PROVIDES THE CAPABILITY TO UNWIND THE PROCEDURE CALL STACK
                                     0000    61 ; TO A SPECIFIED DEPTH AFTER A HARDWARE- OR SOFTWARE-DETECTED EXCEPTION
                                     0000    62 ; CONDITION HAS BEEN SIGNALLED. OPTIONALLY A CHANGE OF FLOW RETURN ADDRESS
                                     0000    63 ; MAY ALSO BE SPECIFIED. THE ACTUAL UNWIND IS NOT PERFORMED IMMEDIATELY BY
                                     0000    64 ; THE SERVICE, BUT RATHER THE RETURN ADDRESSES IN THE CALL STACK ARE MODIFIED
                                     0000    65 ; SUCH THAT WHEN THE CONDITION HANDLER RETURNS THE UNWIND OCCURS.
                                     0000    66 ;
                                     0000    67 ; INPUTS:
                                     0000    68 ;
                                     0000    69 ;	DEPADR(AP) = ADDRESS OF NUMBER OF FRAMES TO UNWIND.
                                     0000    70 ;	NEWPC(AP) = CHANGE OF FLOW FINAL RETURN ADDRESS.
                                     0000    71 ;
                                     0000    72 ;	R4 = CURRENT PROCESS PCB ADDRESS.
                                     0000    73 ;
                                     0000    74 ; OUTPUTS:
                                     0000    75 ;
                                     0000    76 ;	R0 LOW BIT CLEAR INDICATES FAILURE TO FULLY UNWIND CALL STACK.
                                     0000    77 ;
                                     0000    78 ;		R0 = SS$_ACCVIO - CALL STACK NOT ACCESSIBLE TO CALLING ACCESS
                                     0000    79 ;			MODE.
                                     0000    80 ;
                                     0000    81 ;		R0 = SS$_INSFRAME - INSUFFICIENT CALL FRAMES TO UNWIND TO
                                     0000    82 ;			SPECIFIED DEPTH.
                                     0000    83 ;
                                     0000    84 ;		R0 = SS$_NOSIGNAL - NO SIGNAL IS CURRENTLY ACTIVE TO UNWIND.
                                     0000    85 ;
                                     0000    86 ;		R0 = SS$_UNWINDING - UNWIND ALREADY IN PROGRESS.
                                     0000    87 ;
                                     0000    88 ;	R0 LOW BIT SET INDICATES SUCCESSFUL COMPLETION.
                                     0000    89 ;
                                     0000    90 ;		R0 = SS$_NORMAL - NORMAL COMPLETION.
                                     0000    91 ;-
                                     0000    92  
                               003C' 0000    93 	.ENTRY	EXE$UNWIND,^M<R2,R3,R4,R5>
            6D       00DF'CF     DE  0002    94 	MOVAL	W^CHANDL,(FP)		;ESTABLISH CONDITION HANDLER
            54            5D     D0  0007    95 	MOVL	FP,R4			;SET ADDRESS OF FIRST FRAME TO EXAMINE
                                     000A    96  
                                     000A    97 ;
                                     000A    98 ; SEARCH CALL STACK FOR A FRAME THAT WAS CREATED BY A CALL FROM THE SIGNAL
                                     000A    99 ; DISPATCH VECTOR OR BY A CALL FROM THE UNWIND SIGNAL DISPATCHER.
                                     000A   100 ;
                                     000A   101  
            50       0900 8F     3C  000A   102 	MOVZWL	#SS$_NOSIGNAL,R0	;ASSUME NO SIGNAL ACTIVE
            54         0C A4     D0  000F   103 10$:	MOVL	SAVFP(R4),R4		;GET ADDRESS OF PREVIOUS FRAME
                          19     13  0013   104 	BEQL	20$			;IF EQL END OF CALL STACK
         10 A4   00000004'8F     D1  0015   105 	CMPL	#SYS$CALL_HANDL+4,SAVPC(R4) ;CALL FROM CONDITION HANDLER DISPATCHER?
                          10     13  001D   106 	BEQL	30$			;IF EQL YES
         10 A4   000000C7'8F     D1  001F   107 	CMPL	#CALLUNWIND+4,SAVPC(R4)	;CALL FROM UNWIND SIGNAL DISPATCHER?
                          E6     12  0027   108 	BNEQ	10$			;IF NEQ NO
            50       0928 8F     3C  0029   109 	MOVZWL	#SS$_UNWINDING,R0	;SET ALREADY UNWINDING
                                 04  002E   110 20$:	RET				;
                                     002F   111  
                                     002F   112 ;
SYSUNWIND       - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK     21-AUG-1978 23:48:54   VAX-11 MACRO X0.3-11               Page   3
01              UNWIND PROCEDURE CALL STACK                                                                                      (1)

                                     002F   113 ; SET TO UNWIND PROCEDURE CALL STACK TO SPECIFIED DEPTH
                                     002F   114 ;
                                     002F   115  
            53         04 AC     D0  002F   116 30$:	MOVL	DEPADR(AP),R3		;GET ADDRESS OF NUMBER OF FRAMES TO UNWIND
                          05     13  0033   117 	BEQL	40$			;IF EQL NONE SPECIFIED
            53            63     D0  0035   118 	MOVL	(R3),R3			;GET NUMBER OF FRAMES TO UNWIND
                          0F     11  0038   119 	BRB	50$			;
            52            54     D0  003A   120 40$:	MOVL	R4,R2			;COPY CURRENT FRAME ADDRESS
                          00BE   30  003D   121 	BSBW	OLDSP			;CALCULATE VALUE OF SP BEFORE CALL
            52         0C A2     D0  0040   122 	MOVL	CHF$L_MCHARGLST+4(R2),R2 ;GET ADDRESS OF MECHANISM ARGUMENT LIST
         08 A2            01     C1  0044   123 	ADDL3	#1,CHF$L_MCH_DEPTH(R2),R3 ;CALCULATE DEPTH OF ESTABLISHER'S CALLER
                          53         0048       
                          3E     15  0049   124 50$:	BLEQ	90$			;IF LEQ NO FRAMES TO REMOVE
         10 A4         A3'AF     DE  004B   125 	MOVAL	B^STARTUNWIND,SAVPC(R4)	;SET CONDITION HANDLER UNWIND ADDRESS
                                     0050   126  
                                     0050   127 ;
                                     0050   128 ; SCAN THROUGH SPECIFIED NUMBER OF FRAMES SETTING EACH FRAME TO UNWIND ON RETURN
                                     0050   129 ;
                                     0050   130  
            55         0C A4     D0  0050   131 60$:	MOVL	SAVFP(R4),R5		;GET ADDRESS OF PREVIOUS FRAME
                          47     13  0054   132 	BEQL	100$			;IF EQL INSUFICIENT FRAMES
                          53     D7  0056   133 	DECL	R3			;ANY MORE FRAMES TO CONSIDER?
                          2F     15  0058   134 	BLEQ	90$			;IF LEQ NO
         10 A5   00000004'8F     D1  005A   135 	CMPL	#SYS$CALL_HANDL+4,SAVPC(R5) ;CALL FROM CONDITION DISPATCHER?
                          1B     12  0062   136 	BNEQ	80$			;IF NEQ NO
            52            55     D0  0064   137 	MOVL	R5,R2			;COPY ADDRESS OF CURRENT FRAME
                          0094   30  0067   138 	BSBW	OLDSP			;CALCULATE VALUE OF SP BEFORE CALL
            52         0C A2     D0  006A   139 	MOVL	CHF$L_MCHARGLST+4(R2),R2 ;GET ADDRESS OF MECHANISM ARGUMENT LIST
         04 A2            55     D1  006E   140 70$:	CMPL	R5,CHF$L_MCH_FRAME(R2)	;UNWOUND TO ESTABLISHER FRAME?
                          0B     13  0072   141 	BEQL	80$			;IF EQL YES
         10 A5         AB'AF     DE  0074   142 	MOVAL	B^LOOPUNWIND,SAVPC(R5)	;SET FRAME UNWIND ADDRESS
            55         0C A5     D0  0079   143 	MOVL	SAVFP(R5),R5		;GET ADDRESS OF PREVIOUS FRAME
                          EF     11  007D   144 	BRB	70$			;
         10 A5         AB'AF     DE  007F   145 80$:	MOVAL	B^LOOPUNWIND,SAVPC(R5)	;SET FRAME UNWIND ADDRESS
            54            55     D0  0084   146 	MOVL	R5,R4			;SET ADDRESS OF CURRENT FRAME
                          C7     11  0087   147 	BRB	60$			;
                                     0089   148  
                                     0089   149 ;
                                     0089   150 ; MODIFY CHANGE OF FLOW RETURN IF NEW ADDRESS SPECIFIED
                                     0089   151 ;
                                     0089   152  
            50            01     3C  0089   153 90$:	MOVZWL	#SS$_NORMAL,R0		;SET NORMAL COMPLETION
            51         08 AC     D0  008C   154 	MOVL	NEWPC(AP),R1		;GET CHANGE OF FLOW RETURN ADDRESS
                          10     13  0090   155 	BEQL	110$			;IF EQL NONE SPECIFIED
            55         0C A4     D0  0092   156 	MOVL	SAVFP(R4),R5		;GET ADDRESS OF PREVIOUS FRAME
                          05     13  0096   157 	BEQL	100$			;IF EQL INSUFFICIENT FRAMES
         10 A5            51     D0  0098   158 	MOVL	R1,SAVPC(R5)		;SET NEW FINAL RETURN ADDRESS
                                 04  009C   159 	RET				;
            50       012C 8F     3C  009D   160 100$:	MOVZWL	#SS$_INSFRAME,R0	;SET INSUFFICIENT FRAMES
                                 04  00A2   161 110$:	RET				;
                                     00A3   162  
                                     00A3   163 ;
                                     00A3   164 ; UNWIND HANDLER FRAME
                                     00A3   165 ;
                                     00A3   166  
                                     00A3   167 STARTUNWIND:				;START OF ACTUAL UNWIND
            50         0C AE     D0  00A3   168 	MOVL	CHF$L_MCHARGLST+4(SP),R0 ;GET ADDRESS OF MECHANISM ARGUMENT LIST
SYSUNWIND       - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK     21-AUG-1978 23:48:54   VAX-11 MACRO X0.3-11               Page   4
01              UNWIND PROCEDURE CALL STACK                                                                                      (1)

            50         0C A0     7D  00A7   169 	MOVQ	CHF$L_MCH_SAVR0(R0),R0	;RESTORE REGISTERS R0 AND R1
                                     00AB   170  
                                     00AB   171 ;
                                     00AB   172 ; UNWIND CALL FRAME SIGNALING CONDITION HANDLER IF ONE IS SPECIFIED
                                     00AB   173 ;
                                     00AB   174  
                                     00AB   175 	.ENABL	LSB
                                     00AB   176 LOOPUNWIND:				;UNWIND CALL FRAME
                          6D     D5  00AB   177 	TSTL	(FP)			;CONDITION HANDLER SPECIFIED?
                          1C     13  00AD   178 	BEQL	10$			;IF EQL NO
            7E       0920 8F     3C  00AF   179 	MOVZWL	#SS$_UNWIND,-(SP)	;PUSH UNWIND SIGNAL CONDITION
                          01     DD  00B4   180 	PUSHL	#1			;PUSH NUMBER OF SIGNAL ARGUMENTS
                          03     BB  00B6   181 	PUSHR	#^M<R0,R1>		;PUSH REGISTERS R0 AND R1
                          00     DD  00B8   182 	PUSHL	#0			;PUSH FRAME DEPTH
                          5D     DD  00BA   183 	PUSHL	FP			;PUSH FRAME ADDRESS
                          04     DD  00BC   184 	PUSHL	#4			;PUSH NUMBER OF MECHANISM ARGUMENTS
                          6E     DF  00BE   185 	PUSHAL	(SP)			;PUSH ADDRESS OF MECHANISM ARGUMENTS
                       18 AE     DF  00C0   186 	PUSHAL	24(SP)			;PUSH ADDRESS OF SIGNAL ARGUMENTS
                                     00C3   187 CALLUNWIND:				;SIGNAL UNWIND
         00 BD            02     FB  00C3   188 	CALLS	#2,@(FP)		;CALL CONDITION HANDLER
            50         0C AE     7D  00C7   189 	MOVQ	CHF$L_MCH_SAVR0(SP),R0	;RETRIEVE NEW VALUES FOR R0 AND R1
            5C         14 AD     DE  00CB   190 10$:	MOVAL	SAVRG(FP),AP		;GET ADDRESS OF REGISTER SAVE AREA
            03         06 AD     E9  00CF   191 	BLBC	SAVMSK(FP),20$		;IF LBC R0 NOT SAVED
            8C            50     D0  00D3   192 	MOVL	R0,(AP)+		;SAVE R0 FOR SUBSEQUENT RESTORATION
         06 AD            01     E1  00D6   193 20$:	BBC	#1,SAVMSK(FP),30$	;IF CLR, R1 NOT SAVED
                          03         00DA       
            6C            51     D0  00DB   194 	MOVL	R1,(AP)			;SAVE R1 FOR SUBSEQUENT RESTORATION
                                 04  00DE   195 30$:	RET				;
                                     00DF   196 	.DSABL	LSB
SYSUNWIND       - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK     21-AUG-1978 23:48:54   VAX-11 MACRO X0.3-11               Page   5
01              UNWIND CONDITION HANDLER                                                                                         (1)

                                     00DF   198 	.SBTTL	UNWIND CONDITION HANDLER
                                     00DF   199 ;
                                     00DF   200 ; UNWIND CONDITION HANDLER
                                     00DF   201 ;
                                     00DF   202  
                               0000  00DF   203 CHANDL:	.WORD	0			;ENTRY MASK
            50         04 AC     D0  00E1   204 	MOVL	CHF$L_SIGARGLST(AP),R0	;GET ADDRESS OF SIGNAL ARGUMENT LIST
         04 A0       0920 8F     B1  00E5   205 	CMPW	#SS$_UNWIND,CHF$L_SIG_NAME(R0) ;UNWINDING?
                          10     13  00EB   206 	BEQL	10$			;IF EQL YES
            51         08 AC     D0  00ED   207 	MOVL	CHF$L_MCHARGLST(AP),R1	;GET ADDRESS OF MECHANISM ARGUMENT LIST
         0C A1         04 A0     D0  00F1   208 	MOVL	CHF$L_SIG_NAME(R0),CHF$L_MCH_SAVR0(R1) ;SET FINAL RETURN STATUS
                          7E     7C  00F6   209 	CLRQ	-(SP)			;CLEAR DEPTH AND NEW PC ARGUMENTS
       FF03 CF            02     FB  00F8   210 	CALLS	#2,W^EXE$UNWIND		;UNWIND TO ESTABLISHER'S CALLER
                                 04  00FD   211 10$:	RET				;
SYSUNWIND       - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK     21-AUG-1978 23:48:54   VAX-11 MACRO X0.3-11               Page   6
01              CALCULATE VALUE OF SP BEFORE CALL                                                                                (1)

                                     00FE   213 	.SBTTL	CALCULATE VALUE OF SP BEFORE CALL
                                     00FE   214 ;
                                     00FE   215 ; SUBROUTINE TO CALCULATE VALUE OF SP BEFORE CALL
                                     00FE   216 ;
                                     00FE   217  
            02            0E     EF  00FE   218 OLDSP:	EXTZV	#14,#2,SAVMSK(R2),-(SP)	;GET STACK ALIGNMENT BIAS
            7E         06 A2         0101       
            0C            00     EF  0104   219 	EXTZV	#0,#12,SAVMSK(R2),R1	;GET REGISTER SAVE MASK
            51         06 A2         0107       
            52            14     C0  010A   220 	ADDL	#SAVRG,R2		;ADD OFFSET TO REGISTER SAVE AREA
            52            8E     C0  010D   221 	ADDL	(SP)+,R2		;ADD STACK ALIGNMENT BIAS
            03            51     E9  0110   222 10$:	BLBC	R1,20$			;IF LBC CORRESPONDING REGISTER NOT SAVED
            52            04     C0  0113   223 	ADDL	#4,R2			;ADJUST FOR SAVED REGISTER
            51         FF 8F     78  0116   224 20$:	ASHL	#-1,R1,R1		;ANY MORE REGISTERS SAVED?
                          51         011A       
                          F3     12  011B   225 	BNEQ	10$			;IF NEQ YES
                                 05  011D   226 	RSB				;
                                     011E   227  
                                     011E   228 	.END
SYSUNWIND       - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK     21-AUG-1978 23:48:54   VAX-11 MACRO X0.3-11               Page   7
SYMBOL TABLE                                                                                                                     (1)

BIT...         = 00000000            SS$_CTRLERR    = 00000054            SS$_INSFMEM    = 00000124            
CALLUNWIND       000000C3 R     03   SS$_DATACHECK  = 0000005C            SS$_INSFRAME   = 0000012C            
CHANDL           000000DF R     03   SS$_DATAOVERUN = 00000838            SS$_INSFWSL    = 0000011C            
CHF$L_MCHARGLST  00000008            SS$_DEBUG      = 0000046C            SS$_IVADDR     = 00000134            
CHF$L_MCH_ARGS   00000000            SS$_DEVACTIVE  = 000002C4            SS$_IVCHAN     = 0000013C            
CHF$L_MCH_DEPTH  00000008            SS$_DEVALLOC   = 00000840            SS$_IVCHNLSEC  = 0000026C            
CHF$L_MCH_FRAME  00000004            SS$_DEVALRALLOC= 00000641            SS$_IVDEVNAM   = 00000144            
CHF$L_MCH_SAVR0  0000000C            SS$_DEVASSIGN  = 00000848            SS$_IVGSDNAM   = 0000014C            
CHF$L_MCH_SAVR1  00000010            SS$_DEVFOREIGN = 00000064            SS$_IVLOGNAM   = 00000154            
CHF$L_SIGARGLST  00000004            SS$_DEVICEFULL = 00000850            SS$_IVLOGTAB   = 0000015C            
CHF$L_SIG_ARG1   00000008            SS$_DEVMOUNT   = 0000006C            SS$_IVPROTECT  = 000002F4            
CHF$L_SIG_ARGS   00000000            SS$_DEVNOTALLOC= 00000858            SS$_IVQUOTAL   = 00000164            
CHF$L_SIG_NAME   00000004            SS$_DEVNOTMBX  = 00000074            SS$_IVSECFLG   = 0000016C            
DEPADR         = 00000004            SS$_DEVNOTMOUNT= 0000007C            SS$_IVSECIDCTL = 000002E4            
EXE$UNWIND       00000000 RG    03   SS$_DEVOFFLINE = 00000084            SS$_IVSSRQ     = 00000174            
GBL...         = 00000000            SS$_DIRFULL    = 00000860            SS$_IVSTSFLG   = 0000017C            
HANDLER        = 00000000            SS$_DRVERR     = 0000008C            SS$_IVTIME     = 00000184            
LOOPUNWIND       000000AB R     03   SS$_DUPFILENAME= 00000868            SS$_LENVIO     = 0000018C            
NEWPC          = 00000008            SS$_DUPLNAM    = 00000094            SS$_LKWSETFUL  = 00000194            
OLDSP            000000FE R     03   SS$_ENDOFFILE  = 00000870            SS$_MBFULL     = 000008D8            
SAVAP          = 00000008            SS$_ENDOFTAPE  = 00000878            SS$_MBTOOSML   = 0000019C            
SAVFP          = 0000000C            SS$_ENDOFUSRLBL= 00000970            SS$_MCHECK     = 000002BC            
SAVMSK         = 00000006            SS$_EXQUOTA    = 0000001C            SS$_MEDOFL     = 000001A4            
SAVPC          = 00000010            SS$_EXTIDXFILE = 00000880            SS$_MSGNOTFND  = 00000621            
SAVPSW         = 00000004            SS$_FCPREADERR = 00000888            SS$_MUSTCLOSEFL= 00000948            
SAVRG          = 00000014            SS$_FCPREPSTN  = 00000988            SS$_NODATA     = 000001AC            
SS$_ABORT      = 0000002C            SS$_FCPREWNDERR= 00000890            SS$_NOHANDLER  = 000008F8            
SS$_ACCONFLICT = 00000800            SS$_FCPSPACERR = 00000898            SS$_NOHOMEBLK  = 000008E0            
SS$_ACCVIO     = 0000000C            SS$_FCPWRITERR = 000008A0            SS$_NOIOCHAN   = 000001B4            
SS$_ARITH      = 00000404            SS$_FILACCERR  = 0000009C            SS$_NOLINKS    = 0000027C            
SS$_ASTFLT     = 0000040C            SS$_FILALRACC  = 000000A4            SS$_NOLOGNAM   = 000001BC            
SS$_BADATTRIB  = 00000034            SS$_FILELOCKED = 000008A8            SS$_NOMBX      = 00000274            
SS$_BADCHKSUM  = 00000808            SS$_FILENUMCHK = 000008B0            SS$_NOMOREFILES= 00000930            
SS$_BADESCAPE  = 0000003C            SS$_FILESEQCHK = 000008B8            SS$_NONEXDRV   = 000001C4            
SS$_BADFILEHDR = 00000810            SS$_FILESTRUCT = 000008C0            SS$_NONEXPR    = 000008E8            
SS$_BADFILENAME= 00000818            SS$_FILNOTACC  = 000000AC            SS$_NONLOCAL   = 000008F0            
SS$_BADFILEVER = 00000820            SS$_FILNOTCNTG = 000002AC            SS$_NOPRIV     = 00000024            
SS$_BADIMGHDR  = 00000044            SS$_FILNOTEXP  = 000000B4            SS$_NORMAL     = 00000001            
SS$_BADIRECTORY= 00000828            SS$_FORMAT     = 000000BC            SS$_NOSIGNAL   = 00000900            
SS$_BADPARAM   = 00000014            SS$_GPTFULL    = 000000C4            SS$_NOSOLICIT  = 00000284            
SS$_BADSTACK   = 000002B4            SS$_GSDFULL    = 000000CC            SS$_NOSUCHDEV  = 00000908            
SS$_BEGOFFILE  = 00000938            SS$_GSDNOTXXX  = 000000D4            SS$_NOSUCHFILE = 00000910            
SS$_BLOCKCNTERR= 00000940            SS$_HANGUP     = 000002CC            SS$_NOSUCHNODE = 0000028C            
SS$_BREAK      = 00000414            SS$_HEADERFULL = 000008C8            SS$_NOSUCHSEC  = 00000978            
SS$_BUFFEROVF  = 00000601            SS$_IDXFILEFULL= 000008D0            SS$_NOTAPEOP   = 00000264            
SS$_BUGCHECK   = 000002A4            SS$_ILLBLKNUM  = 000000DC            SS$_NOTFILEDEV = 000001CC            
SS$_CANCEL     = 00000830            SS$_ILLCNTRFUNC= 000000E4            SS$_NOTINTBLSZ = 000001D4            
SS$_CHANINTLK  = 0000004C            SS$_ILLEFC     = 000000EC            SS$_NOTLABELMT = 000001DC            
SS$_CLIFRCEXT  = 00000980            SS$_ILLIOFUNC  = 000000F4            SS$_NOTNETDEV  = 000002EC            
SS$_CMODSUPR   = 0000041C            SS$_ILLLBLAST  = 00000968            SS$_NOTRAN     = 00000629            
SS$_CMODUSER   = 00000424            SS$_ILLPAGCNT  = 000000FC            SS$_NOTSQDEV   = 000001E4            
SS$_COMPAT     = 0000042C            SS$_ILLSEQOP   = 000002DC            SS$_OPCCUS     = 00000434            
SS$_CONTINUE   = 00000001            SS$_ILLSER     = 00000104            SS$_OPCDEC     = 0000043C            
SS$_CONTROLC   = 00000651            SS$_ILLUSRLBLRD= 00000958            SS$_OPINCOMPL  = 000002D4            
SS$_CONTROLO   = 00000609            SS$_ILLUSRLBLWT= 00000960            SS$_PAGOWNVIO  = 000001EC            
SS$_CONTROLY   = 00000611            SS$_INCVOLLABEL= 0000010C            SS$_PAGRDERR   = 00000444            
SS$_CREATED    = 00000619            SS$_INSFARG    = 00000114            SS$_PARITY     = 000001F4            
SYSUNWIND       - SYSTEM SERVICE UNWIND PROCEDURE CALL STACK     21-AUG-1978 23:48:54   VAX-11 MACRO X0.3-11               Page   8
SYMBOL TABLE                                                                                                                     (1)

SS$_PARTESCAPE = 000001FC            
SS$_PFMBSY     = 00000204            
SS$_PSTFULL    = 0000020C            
SS$_RADRMOD    = 0000044C            
SS$_REJECT     = 00000294            
SS$_REMOTE     = 00000649            
SS$_RESIGNAL   = 00000918            
SS$_RESULTOVF  = 00000214            
SS$_ROPRAND    = 00000454            
SS$_SECTBLFUL  = 0000021C            
SS$_SSFAIL     = 0000045C            
SS$_SUPERSEDE  = 00000631            
SS$_TAPEPOSLOST= 00000224            
SS$_TBIT       = 00000464            
SS$_TIMEOUT    = 0000022C            
SS$_TOOMANYVER = 00000990            
SS$_TOOMUCHDATA= 0000029C            
SS$_UNASEFC    = 00000234            
SS$_UNSAFE     = 0000023C            
SS$_UNWIND     = 00000920            
SS$_UNWINDING  = 00000928            
SS$_VASFULL    = 00000244            
SS$_VECINUSE   = 0000024C            
SS$_VOLINV     = 00000254            
SS$_WAITUSRLBL = 00000950            
SS$_WASCLR     = 00000001            
SS$_WASECC     = 00000639            
SS$_WASSET     = 00000009            
SS$_WRITLCK    = 0000025C            
STARTUNWIND      000000A3 R     03   
SYS$CALL_HANDL   ********   X   03   


PROGRAM SECTION SYNOPSIS

.  ABS  .        00000000      00     NOPIC   USR   CON   ABS   LCL NOSHR NOEXE NORD  NOWRT BYTE  
. BLANK .        00000000      01     NOPIC   USR   CON   REL   LCL NOSHR   EXE   RD    WRT BYTE  
$ABS$            00000014      02     NOPIC   USR   CON   ABS   LCL NOSHR   EXE   RD    WRT BYTE  
YEXEPAGED        0000011E      03     NOPIC   USR   CON   REL   LCL NOSHR   EXE   RD    WRT BYTE  


THERE WERE NO ERRORS OR WARNINGS.
19632. BYTES LEFT IN FREE MEMORY POOL.
3032. BYTES OF RECLAIMED MEMORY.
OBJ$:SYSUNWIND,LIS$:SYSUNWIND/-SP=LIB$:LIB/ML,SRC$:SYSUNWIND
2 MLB DIR RDS - 301 GETS TO DEFINE 9 MACROS. 24 INTER. FILE WRITES. 
